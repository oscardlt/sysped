      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE10R MODULE(*LIBL/TJGE10R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF12 kall Med UNIK/REFF = kall fra booking/Z-filer
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFREETXT1  UF A E           K DISK    USROPN
     F                                     PREFIX(X)
N01  FZREETXT2  UF A E           K DISK    USROPN
N01  F                                     RENAME(FREETXTR:ZREETXTR)
N01  F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D FRTAVD          S              4S 0
     D FRTOPD          S              7S 0
N01  D FRUNIK          S              9S 0
N01  D FRREFF          S             17
     D FRTDT           S              8S 0
     D FRTLI           S              2S 0
     D FRTTXT          S             70
     D FRTKOD          S              1
     D FRTFAK          S              7S 0
     D FRTLES          S              1

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬frtavd¬ : ¬'+%trim(%editc(frtavd:'Z'))+'¬, '
          +'¬frtopd¬ : ¬'+%trim(%editc(frtopd:'Z'))+'¬, '
N01       +'¬frreff¬ : ¬'+%trim(frreff)+'¬, '
N01       +'¬frunik¬ : ¬'+%trim(%editc(frunik:'Z'))+'¬, '
          +'¬frtdt¬ : ¬'+%trim(%editc(frtdt:'Z'))+'¬, '
          +'¬frtli¬ : ¬'+%trim(%editc(frtli:'Z'))+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"freetxtUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     FREETXT1
N01  C  N50              CLOSE     ZREETXT2

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('FRTAVD')
     C                   EVAL      FRTAVD     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('FRTOPD')
     C                   EVAL      FRTOPD     = c2n2(WA)
N01  C                   eval      FRREFF    = zhbgetvar('FRREFF')
N01  C                   eval      WA      = zhbgetvar('FRUNIK')
N01  C                   eval      FRUNIK  = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('FRTDT')
     C                   EVAL      FRTDT      = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('FRTLI')
     C                   EVAL      FRTLI      = c2n2(WA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      FRTTXT     = zhbgetvar('FRTTXT')
     C                   EVAL      FRTKOD     = zhbgetvar('FRTKOD')
N01  C                   IF        FRTKOD = 'b'
N01  C                   EVAL      FRTKOD = *blanks
N01  C                   ENDIF
     C                   EVAL      WA      = zhbgetvar('FRTFAK')
     C                   EVAL      FRTFAK  = c2n2(WA)
     C                   EVAL      FRTLES  = zhbgetvar('FRTLES')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     FREEKEY       KLIST
     C                   KFLD                    FRTAVD
     C                   KFLD                    FRTOPD
     C                   KFLD                    FRTDT
     C                   KFLD                    FRTLI
N01  C     ZREEKEY       KLIST
N01  C                   KFLD                    FRUNIK
N01  C                   KFLD                    FRTDT
N01  C                   KFLD                    FRTLI
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      FREETXT1
N01  C  N50              OPEN      ZREETXT2

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
N01  C                   IF        frtopd <> *zeros
     C     FREEKEY       CHAIN     FREETXT1                           55
N01  C                   ELSE
N01  C     ZREEKEY       CHAIN     ZREETXT2                           55
N01  C                   ENDIF
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
N01  C                   exsr      FixAinput
N01  C                   IF        frtopd <> *zeros
N01  C     FREEKEY       CHAIN(N)  FREETXT1                           55
N01  C                   ELSE
N01  C     ZREEKEY       CHAIN(N)  ZREETXT2                           55
N01  C                   ENDIF
N01  C                   IF        *IN55=*OFF
N01  C                   eval      Error = 'Freetxt line already exists'
N01  C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRA
N01  C                   ENDIF
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Freetxt not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Freetxt not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
N01   ***************************************
N01  C     FixAinput     BEGSR
N01   ***************************************
N01   * Add
N01   *   dato 0 = sett dagens
N01  C                   IF        FRTDT = *zeros
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
N01  c                   MOVE      WDT           FRTDT
N01  C                   ENDIF
N01   *   linje 0 = finn siste + 1
N01  C                   IF        FRTLI = *zeros
N01   /free
N01       if frtopd <> *zeros;
N01          setgt ( FRTAVD : FRTOPD : FRTDT : 99) FREETXT1;
N01          readpe ( FRTAVD : FRTOPD : FRTDT ) FREETXT1;
N01       else;
N01          setgt ( FRUNIK : FRTDT : 99 ) ZREETXT2;
N01          readpe ( FRUNIK : FRTDT ) ZREETXT2;
N01       endif;
N01       frtli=xfrtli+1;
N01   /end-free
N01  C                   ENDIF
N01  C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
      *
     C                   CLEAR                   FREETXTR
N01  C                   CLEAR                   ZREETXTR
     C                   EVAL      XFRTAVD=FRTAVD
     C                   EVAL      XFRTOPD=FRTOPD
     C                   EVAL      XFRTDT=FRTDT
     C                   EVAL      XFRTLI=FRTLI

     C                   EVAL      XFRTTXT=FRTTXT
     C                   EVAL      XFRTKOD=FRTKOD
     C                   EVAL      XFRTFAK=FRTFAK
     C                   EVAL      XFRTLES=FRTLES

N01  C                   IF        frtopd <> *zeros
     C                   WRITE     FREETXTR
N01  C                   ELSE
N01  C                   EVAL      XFRUNIK=FRUNIK
N01  C                   EVAL      XFRREFF=FRREFF
N01  C                   WRITE     ZREETXTR
N01  C                   ENDIF
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
N01  C                   IF        frtopd <> *zeros
     C                   DELETE    FREETXTR
N01  C                   ELSE
N01  C                   DELETE    ZREETXTR
N01  C                   ENDIF
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XFRTTXT=FRTTXT
     C                   EVAL      XFRTKOD=FRTKOD
     C                   EVAL      XFRTFAK=FRTFAK
     C                   EVAL      XFRTLES=FRTLES
N01  C                   IF        frtopd <> *zeros
     C                   UPDATE    FREETXTR
N01  C                   ELSE
N01  C                   UPDATE    ZREETXTR
N01  C                   ENDIF
     C                   ENDSR
