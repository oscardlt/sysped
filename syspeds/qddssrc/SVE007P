********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11082 *01CB  PTF10 REPETITION AV R44     70/80/10/63
********************************************************************
     A                                      REF(SVEH)
      *________
      * Heading
      *""""""""
     A          R SVE007PA
     A                                  2 30'VARUPOSTER'
     A                                      UNDERLINE
     A                                  2 50DATE EDTCDE(Y)
     A                                  2 59'Sidan:'
     A                                  2 66PAGNBR EDTCDE(Z)
     A                                  4  2'Tullid:'
     A            SVEH_TUID R           4 10
     A                                  4 43'Referans:'
     A            SVEH_SYAV R           4 53EDTCDE(Z)
     A                                  4 57'/'
     A            SVEH_SYOP R           4 58EDTCDE(Z)
     A                                  4 65'/'
     A            SVEH_SYSG R           4 66
      *
     A                                  6  4'NR'
     A                                  6  7'VARUKOD'
     A                                  6 24'LK'
     A                                  6 27'PROCEDUR'
     A                                  6 36'FÖRM'
     A                                  6 51'BRUTTO'
     A                                  6 65'NETTO'
      *
     A                                  7  7'VARUSLAG'
     A                                  7 50'A.KVANT'
     A                                  7 62'ST.VÄRDE'
      *
     A                                  8  7'SÄRSK.UPPL.TEXT'
      *
     A                                  9  7'AVG.SLAG'
     A                                  9 16'AVG.GRUND'
     A                                  9 49'AVG.SATS'
     A                                  9 60'AVG.BELOPP'
      *
      *
     A          R SVE007PB                  SPACEB(1)
     A                                     4'----------------------------------'
     A                                    38'--------------------------------'
     A            WRUBR32        5  0      1EDTCDE(Z)
     A                                      SPACEB(1)
     A            WRUBR33A       8         7
     A            WRUBR33B       2        16
     A            WRUBR33C       4        19
     A            WRUBR34        2        24
     A            WRUBR37A       4        27
     A            WRUBR37B       3        32
     A            WRUBR36        3        36
     A            WRUBR35       11  3     45EDTCDE(4)
     A            WRUBR38       11  3     58EDTCDE(4)
     A            WRUBR31       35         7SPACEB(1)
     A            WRUBR31B      35         7SPACEB(1)
     A            WRUBR41       10  0     47EDTCDE(Z)
     A            WRUBR46       11  0     59EDTCDE(Z)
     A            WRUBR31C      35         7SPACEB(1)
S01  A*           WRUBR44       17         7SPACEB(1)
     A          R SVE007PC                  SPACEB(1)
     A            WRUBR47A       3         7
     A            WRUBR47B      11  3     13EDTCDE(4)
     A            WRUBR47C       9  4     47EDTCDE(4)
     A            WRUBR47D       7  0     63EDTCDE(Z)
N02  A          R SVE007PD                  SPACEB(1)
N02  A            WRUBR44       70         7SPACEB(1)
     A          R SVE007PZ                  SPACEB(1)
     A                                     4'---------------------  S I S T A  '
     A                                    38'S I D A N  ---------------------'
