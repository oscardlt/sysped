********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10006 * 01 sh  PTF10 *cr  går til *CE for eksport
********************************************************************
     FFRISOK    IF   E           K DISK
     FTVI10D    CF   E             WORKSTN
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSADH      UF   E           K DISK
     FSAEH      UF   E           K DISK
     FTRIH      UF   E           K DISK
     FTRUH      UF   E           K DISK
     FTRUGH     UF   E           K DISK
     FEDIM      UF A E           K DISK
     FTVINF     O  A E             DISK
     FEDIC      UF   E             DISK
     FTVINME    O    E             PRINTER
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    MMN           WMN
     C     *LIKE         DEFINE    MAVD          WAVD
     C     *LIKE         DEFINE    MTDN          WTDN
     C     *LIKE         DEFINE    M0004         W0004
     C     *LIKE         DEFINE    M0010         W0010
     C     *LIKE         DEFINE    M0068A        W0068A
     C     *LIKE         DEFINE    M0068B        W0068B
     C     *LIKE         DEFINE    M0068C        W0068C
     C     *LIKE         DEFINE    M1001         W1001
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WMN           PARM                    WMN
     C     WAVD          PARM                    WAVD
     C     WTDN          PARM                    WTDN
     C                   PARM                    U0068A            8
     C     W0068B        PARM                    W0068B
     C     W0068C        PARM                    W0068C
     C     W1001         PARM                    W1001
     C                   PARM      *IN03         WIN03
     C                   MOVE      U0068A        W0068A
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     FRIKEY        KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C                   KFLD                    WSKODE
     C                   move      '*CR'         WSKODE            3
     C     KEYA          KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C     KEYB          KLIST
     C                   KFLD                    WMN
     C     KODTPK        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    WAVD
     C                   KFLD                    KOPLNR
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C* Init felt for EDIM
     C     KEYB          CHAIN     EDIM                               20
     C                   MOVE      M0004         M0010
     C                   MOVE      'CUSRES'      M0065
     C                   Z-ADD     0             M2005B
     C                   Z-ADD     0             M5004D
     C     W0068C        IFEQ      0
     C                   MOVE      '999'         M1N07
     C                   ELSE
     C                   MOVE      'DII'         M1N07
     C                   END
     C                   MOVE      *BLANKS       M1N08
     C                   MOVE      W1001         M1001
      * IND. 50 PÅ ---> SAD-IMPORT
      * IND. 51 PÅ ---> SAD-EKSPORT
      * IND. 52 PÅ ---> TET-IMPORT
      * IND. 53 PÅ ---> TET-EKSPORT
      * IND. 54 PÅ ---> TET-GARAMTI
     C     M1001         COMP      '929'                                  50
     C     M1001         COMP      '830'                                  51
     C     M1001         COMP      'TEI'                                  52
     C     M1001         COMP      'TET'                                  53
     C   53M1N07         COMP      '026'                              535354
     C                   Z-ADD     0             M9N01
     C                   Z-ADD     0             MSN
     C                   MOVE      'R'           MSR
     C                   MOVE      'C'           MST
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      *BLANKS       MVEN
     C* Init felt for TVINF
     C                   Z-ADD     0             FMN
     C                   Z-ADD     1             FLN
     C                   IF        *IN54
     C                   MOVE      'SAN'         F0077
     C                   ELSE
     C                   MOVE      'CUS'         F0077
     C                   END
     C                   MOVE      *BLANKS       F4815
     C                   MOVE      *BLANKS       F0078A
     C                   MOVE      *BLANKS       F0078B
     C                   MOVE      *BLANKS       F0078C
     C                   MOVE      *BLANKS       F0078D
     C                   MOVE      *BLANKS       F0078E
      *
     C                   MOVE      'P'           KOPUNI
     C     *IN50         IFEQ      *ON
     C                   Z-ADD     87            KOPLNR
     C                   ELSE
     C                   Z-ADD     97            KOPLNR
     C                   END
     C     KODTPK        CHAIN     KOPR                               20
     C     KOPFM         IFNE      *BLANKS
     C     KOPFM         SETLL     KODTFM
     C     KOPFM         READE     KODTFM                                 20
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Valgbilde                                            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Behandle bilde
     C                   EXFMT     TVI10DA
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      * Oppdatere tolldeklarasjon med ny status
     C                   SELECT
     C     *IN50         WHENEQ    '1'
     C                   MOVE      *BLANKS       FSSOK
N01  C                   move      '*CR'         WSKODE            3
     C     FRIKEY        CHAIN     FRISOK                             20
     C     KEYA          CHAIN     SADH                               20
     C                   MOVE      'P'           SIST
     C                   UPDATE    SADHR
     C     *IN51         WHENEQ    '1'
     C                   MOVE      *BLANKS       FSSOK
N01  C                   move      '*CE'         WSKODE            3
     C     FRIKEY        CHAIN     FRISOK                             20
     C     KEYA          CHAIN     SAEH                               20
     C                   MOVE      'P'           SEST
     C                   UPDATE    SAEHR
     C     *IN52         WHENEQ    '1'
     C     KEYA          CHAIN     TRIH                               20
     C     TIDK          IFEQ      *BLANKS
     C                   MOVE      'M'           TIST
     C                   ELSE
     C                   MOVE      'N'           TIST
     C                   END
     C                   UPDATE    TRIHR
     C     *IN53         WHENEQ    '1'
     C     KEYA          CHAIN     TRUH                               20
     C                   MOVE      'M'           THST
     C                   UPDATE    TRUHR
     C     *IN54         WHENEQ    '1'
     C                   MOVEL     M0068         TGGNR
     C     TGGNR         CHAIN     TRUGH                              20
     C                   MOVE      ' '           TGST
     C                   UPDATE    TRUGHR
     C                   ENDSL
      * Hente neste meldingsnummer.
     C     1             CHAIN     EDIC                               20
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      * Legg ut ny meldingspost.
     C                   Z-ADD     CMN           MMN
     C                   WRITE     EDIMR
      * Legg ut ny feiltekst.
     C                   Z-ADD     MMN           FMN
     C                   WRITE     TVINFF
      * Skriv ut melding.
     C                   WRITE     TVINMEA
     C     F0078B        COMP      *BLANKS                            41
     C     F0078C        COMP      *BLANKS                            42
     C     F0078D        COMP      *BLANKS                            43
     C     F0078E        COMP      *BLANKS                            44
     C                   WRITE     TVINMEB
     C                   WRITE     TVINMEØ
     C                   CLOSE     TVINME
      *
     C                   END
      *
     C                   ENDSR
