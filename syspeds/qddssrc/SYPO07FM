*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYPO07PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV INTERVALLS-
     A                                      TYRING POSTNR.')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'søking i registre')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPO07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0028 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(025 SHU0058 *LIBL/SYMSGF)
     A  41                                  DSPATR(ND)
     A            STICUS    R        B  5 28REFFLD(STEDIR/STICUS *LIBL/STEDI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  41                                  DSPATR(ND)
     A  41                                  DSPATR(PR)
     A            KNAVN         30A  O  5 38COLOR(WHT)
     A  41                                  DSPATR(ND)
     A                                  7  2MSGCON(025 SHU0029 *LIBL/SYMSGF)
     A            STINLK    R        B  7 28REFFLD(STEDIR/STINLK *LIBL/STEDI)
     A  50                             10  2'Når landkode er NO må du også velg-
     A                                      e om du vil vedlikeholde           -
     A                                        '
     A  50                             11  2'standardtabell (S) eller avvikende-
     A                                       steds/sone-definisjoner.          -
     A                                        '
     A  50                             15  2'Vedlikeholdskode.........'
     A  50                             15 32'(S=Standardtabell)                -
     A                                              '
     A  50                             16 32'(D=Domestic, ved avvik fra standar-
     A                                      d)      '
     A  50        WSSU           1A  B 15 28
     A N50                                  DSPATR(PR)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A  50                             17 32'(P=Postens soneinndelinger (geb-ty-
     A                                      pe POS))'
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  7  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            STINPF    R        O  7  8REFFLD(STEDIR/STINPF *LIBL/STEDI)
     A                                  7 14'->'
     A            STINPT    R        O  7 17REFFLD(STEDIR/STINPT *LIBL/STEDI)
     A            STINS1    R        O  7 36REFFLD(STEDIR/STINS1 *LIBL/STEDI)
     A            STINS2    R        O  7 43REFFLD(STEDIR/STINS2 *LIBL/STEDI)
     A            STINBA    R        O  7 25REFFLD(STEDIR/STINBA *LIBL/STEDI)
     A            STINSL    R        O  7 55REFFLD(STEDIR/STINSL *LIBL/STEDI)
     A                                      EDTCDE(Z)
     A            STINS3    R        O  7 63REFFLD(STEDIR/STINS3 *LIBL/STEDI)
     A            STINS4    R        O  7 70REFFLD(STEDIR/STINS4 *LIBL/STEDI)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYPO07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0028 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 SHU0034 *LIBL/SYMSGF)
     A            STINLK    R        O  2 16REFFLD(STEDIR/STINLK *LIBL/STEDI)
     A                                      COLOR(WHT)
     A            KLKNVN        30A  O  3  2
N01  A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 36MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 36MSGCON(044 JOV1521 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*                                 6  2MSGCON(078 SHU0036 *LIBL/SYMSGF)
     A                                  6  2MSGCON(078 JOV1520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            SPETAB        40A  O  2 24
     A                                  5 36MSGCON(040 JOV1521 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
S02  A*         H                           HLPDOC('SYPO07070' 'SYPO07' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYPO07070 SYPO07PNL)
     A                                      HLPARA(01 001 04 080)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG206        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYPO07020 SYPO07PNL)
     A                                      HLPARA(02 001 02 015)
     A          H                           HLPPNLGRP(SYPO07030 SYPO07PNL)
     A                                      HLPARA(02 016 02 028)
     A          H                           HLPPNLGRP(SYPO07040 SYPO07PNL)
     A                                      HLPARA(02 029 02 044)
     A          H                           HLPPNLGRP(SYPO07050 SYPO07PNL)
     A                                      HLPARA(02 045 02 059)
     A          H                           HLPPNLGRP(SYPO07060 SYPO07PNL)
     A                                      HLPARA(02 060 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
S01  A*                                 1  2MSGCON(078 SHU0036 *LIBL/SYMSGF)
     A                                  1  2MSGCON(078 JOV1520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSINPF         5A  B  2  8TEXT('GEBYRKODE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A  90                                  DSPATR(RI)
     A            WSINPT    R        B  2 17REFFLD(STEDIR/STINPT *LIBL/STEDI)
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSINS1    R        B  2 36REFFLD(STEDIR/STINS1 *LIBL/STEDI)
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSINS2    R        B  2 43REFFLD(STEDIR/STINS2 *LIBL/STEDI)
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A            WSINBA    R        B  2 25REFFLD(STEDIR/STINBA *LIBL/STEDI)
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
N01  A            WSINSL    R        B  2 55REFFLD(STEDIR/STINSL *LIBL/STEDI)
N01  A                                      DSPATR(UL)
N01  A                                      EDTCDE(Z)
N01  A  83                                  DSPATR(RI)
N01  A            WSINS3    R        B  2 62REFFLD(STEDIR/STINS2 *LIBL/STEDI)
N01  A                                      DSPATR(UL)
N01  A  84                                  DSPATR(RI)
N01  A            WSINS4    R        B  2 69REFFLD(STEDIR/STINS2 *LIBL/STEDI)
N01  A                                      DSPATR(UL)
N01  A  85                                  DSPATR(RI)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2INPF    R        I  2 15REFFLD(STEDIR/STINPF *LIBL/STEDI)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            W2INBA    R        I  2 25REFFLD(STEDIR/STINBA *LIBL/STEDI)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
S01  A*                                 1  2MSGCON(078 SHU0036 *LIBL/SYMSGF)
     A                                  1  2MSGCON(078 JOV1520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            STINPF    R        O  2  8REFFLD(STEDIR/STINPF *LIBL/STEDI)
     A                                  2 14'->'
     A            STINPT    R        O  2 17REFFLD(STEDIR/STINPT *LIBL/STEDI)
     A            STINS1    R        O  2 36REFFLD(STEDIR/STINS1 *LIBL/STEDI)
     A            STINS2    R        O  2 43REFFLD(STEDIR/STINS2 *LIBL/STEDI)
     A            STINBA    R        O  2 25REFFLD(STEDIR/STINBA *LIBL/STEDI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            STINSL    R        O  2 55REFFLD(STEDIR/STINSL *LIBL/STEDI)
     A            STINS3    R        O  2 62REFFLD(STEDIR/STINS3 *LIBL/STEDI)
     A            STINS4    R        O  2 69REFFLD(STEDIR/STINS4 *LIBL/STEDI)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYPO07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0028 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 SHU0034 *LIBL/SYMSGF)
     A            STINLK    R        O  2 16REFFLD(STEDIR/STINLK *LIBL/STEDI)
     A                                      COLOR(WHT)
     A            KLKNVN        30A  O  3  2
N01  A                                      COLOR(WHT)
S01  A*                                 4  2MSGCON(008 DIV0585 *LIBL/SYMSGF)
S01  A* 41                                  DSPATR(ND)
S01  A*           STICUS         8S 0O  4 16COLOR(WHT)
S01  A* 41                                  DSPATR(ND)
S01  A*           KNAVN         30A  O  4 25
S01  A* 41                                  DSPATR(ND)
E01  A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E01  A                                  4 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  5 36MSGCON(040 JOV1521 *LIBL/SYMSGF)
N01  A                                      COLOR(WHT)
S01  A*                                 6  2MSGCON(078 SHU0036 *LIBL/SYMSGF)
N01  A                                  6  2MSGCON(078 JOV1520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG511        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            SPETAB        40   O  2 24
