     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYSJ50FPNL)
     A                                      HLPTITLE('B/L MODELL 2 (VARELI-
     A                                      NJER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            BVLI           3Y 0H
     A            BVSORT         3Y 0H
     A            WSNR           1A  I  5  3VALUES(' ' '2' '3' '4')
     A  95                                  DSPATR(PR)
     A  95                                  DSPATR(ND)
     A            BVMN          17A  O  5  6
     A            BVKD           1   O  5 25
     A            BVFT          51A  O  5 29
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYSJ50F010 SYSJ50FPNL)
     A                                      HLPARA(01 001 23 080)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ50F'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  1 20MSGCON(40 DIV3176 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  5MSGCON(4 DIV3163 SYMSGF)
     A            UAVD           4Y 0O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 15MSGCON(6 DIV3164 SYMSGF)
     A            UOPD           7Y 0O  2 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 30MSGCON(5 DIV3155 SYMSGF)
     A            UDTR           6S 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(5 DIV3165 SYMSGF)
     A            USG            3A  O  2 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 55MSGCON(3 DIV3166 SYMSGF)
     A            UUKE           2Y 0O  2 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 63MSGCON(4 DIV3167 SYMSGF)
     A            UAAR           2S 0O  2 68DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(50 YBC0704 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3177 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF10(10 'B/L VARELINJE TYPE 1')
     A                                      CF12(12 'FORRIGE')
     A                                      CF13(13 'TIL SIDE 2')
     A  27                                  CF15(15 'TIL OPPDRAG')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYSJ50F999 SYSJ50FPNL)
     A                                      HLPARA(01 001 03 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  29                              1 62MSGCON(18 DIV3175 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(18 DIV3169 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              1 42MSGCON(18 DIV3136 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV3179 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYSJ50F010 SYSJ50FPNL)
     A                                      HLPARA(01 001 08 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(78 DIV3178 SYMSGF)
     A            WSMN1         17A  B  3  2DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSKDF          2A  B  3 22DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSFT1         51A  B  3 27DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSMN2         17A  B  4  2DSPATR(UL)
     A                                  4 21'DOC'
     A            WSFT2         51A  B  4 27DSPATR(UL)
     A            WSMN3         17A  B  5  2DSPATR(UL)
     A            WSKD           1A  B  5 22DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSFT3         51A  B  5 27DSPATR(UL)
     A            WSMN4         17A  B  6  2DSPATR(UL)
     A            WSFT4         51A  B  6 27DSPATR(UL)
     A                                  7  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 71MSGCON(5 YBC0703 SYMSGF)
     A            WSSORT         3Y 0B  7 77DSPATR(UL)
     A                                      EDTCDE(4)
     A  30        MSGTXT        78A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      OVERLAY
     A                                  2  2MSGCON(78 DIV3178 SYMSGF)
     A                                      COLOR(WHT)
     A            WSMN1         17A  O  3  2DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            WSFT1         51A  O  3 27DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSMN2         17A  O  4  2DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4 21'DOC'
     A            WSFT2         51A  O  4 27DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSMN3         17A  O  5  2DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSKD           1A  O  5 22DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSFT3         51A  O  5 27DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSMN4         17A  O  6  2DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSFT4         51A  O  6 27DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 71MSGCON(5 YBC0703 SYMSGF)
     A            WSSORT         3Y 0O  7 77DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF10(10 'B/L VARELINJE TYPE 1')
     A                                      CF12(12 'AVSLUTT')
     A                                      CF13(13 'TIL SIDE 2')
     A  27                                  CF15(15 'TIL OPPDRAG')
     A          H                           HLPPNLGRP(SYSJ50F999 SYSJ50FPNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ50F'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3176 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  5MSGCON(4 DIV3163 SYMSGF)
     A            UAVD           4Y 0O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 15MSGCON(6 DIV3164 SYMSGF)
     A            UOPD           7Y 0O  2 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 30MSGCON(5 DIV3155 SYMSGF)
     A            UDTR           6S 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(5 DIV3165 SYMSGF)
     A            USG            3A  O  2 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 55MSGCON(3 DIV3166 SYMSGF)
     A            UUKE           2Y 0O  2 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 63MSGCON(4 DIV3167 SYMSGF)
     A            UAAR           2S 0O  2 68DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(78 DIV3177 SYMSGF)
     A                                      DSPATR(HI)
     A  29                             22 62MSGCON(18 DIV3175 SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(18 DIV3169 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                             22 42MSGCON(18 DIV3136 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV3179 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
