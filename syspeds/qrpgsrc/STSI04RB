      *  OBS - KUN MODUL - INNGÅR I STSI04R
      *****************************************************
      *        FJERNE ETT OPPDRAG FRA GITT TUR           **
      *****************************************************
     FSTSTUR    UF   E           K DISK    usropn
     FSTSOPD    UF   E           K DISK    usropn
     FSTSFBR    UF   E           K DISK    usropn
     FSTSCLI    UF   E           K DISK    usropn
      *copy qrpgsrccpy,prototypeb
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      ****************************************************************************
      * Mainline
      ****************************************************************************
      * Initialization
     C                   exsr      Init
      *
      * Main programme
     C     TURKEY        CHAIN(N)  STSTUR                             33
     C  N33STSTAT        IFEQ      ' '
     C                   exsr      MAIN
     C                   END
      *
      * End program
     C                   close     STSTUR
     C                   close     STSOPD
     C                   close     STSFBR
     C                   close     STSCLI
     C                   return
      *
      ****************************************************************************
     C     MAIN          begsr
      ****************************************************************************
      * CLI
     C     OPDKEY        SETLL     STSCLI
     C     OPDKEY        READE     STSCLI                                 33
     C     *IN33         DOWEQ     *OFF
     C                   DELETE    STSCLIR
     C     OPDKEY        READE     STSCLI                                 33
     C                   END
      * FBR
     C     OPDKEY        SETLL     STSFBR
     C     OPDKEY        READE     STSFBR                                 33
     C     *IN33         DOWEQ     *OFF
     C                   DELETE    STSFBRR
     C     OPDKEY        READE     STSFBR                                 33
     C                   END
      * FBR
     C     OPDKEY        CHAIN     STSOPD                             33
      * TUR
     C     *IN33         IFEQ      *OFF
     C                   DELETE    STSOPDR
     C     TURKEY        CHAIN     STSTUR                             33
     C     *IN33         IFEQ      *OFF
     C                   SUB       1             STANOP
     C     SOSTAT        IFEQ      'X'
     C                   SUB       1             STANOK
     C                   END
     C                   UPDATE    STSTURR
     C                   END
     C                   END
     C                   endsr
      ****************************************************************************
     C     Init          begsr
      ****************************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    YTYPE             5
     C                   PARM                    wspro             8 0
     C                   PARM                    wsavd             4 0
     C                   PARM                    wsopd             7 0
      *
     C     TURKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C     OPDKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
      *
     C                   open      STSTUR
     C                   open      STSOPD
     C                   open      STSFBR
     C                   open      STSCLI
     C                   endsr
