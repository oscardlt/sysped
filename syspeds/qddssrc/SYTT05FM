     A*%%TS  SD  20141023  111704  JOVO        REL-V7R1M0  5770-WDS
N04  A* KAN OVERSTYRE KODE FOR MAU (S=SKAL SENDES SIM STATUS TIL AGENT)
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/TRACKF TRACKFR)
     A                                      PRINT
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 16 74 *NOMSGLIN)
     A                                      OVERLAY
     A* *DS3                                WINDOW(*DFT 16 74 *NOMSGLIN)
     A* *DS4                                WINDOW(*DFT 16 74 *NOMSGLIN)
     A          R TTWINDOW
     A*%%TS  SD  20141023  111704  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(*DFT 16 74 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 16 74 *NOMSGLIN)
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1 10'Legge til Track & Tracs / Hendelse-
     A                                      slogg via turnr.'
     A                                      COLOR(WHT)
     A                                  2 14'Tur.:'
     A            WSTUNR         8Y 0O  2 20TEXT('TURNR')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 14'Kode:'
     A            TTACTI    R        B  3 20COLOR(WHT)
     A  89                                  DSPATR(RI)
     A            KODTXT        30A  O  3 24COLOR(WHT)
     A                                  3 55'Grp:'
     A            KODGRP         3A  O  3 60COLOR(WHT)
     A                                  3 64'Vis WEB:'
     A            KODWEB         1A  O  3 74COLOR(WHT)
     A                                  4  2'Hendelsestidspkt:'
     A            TTDATE    R        B  4 20EDTWRD('    .  .  ')
     A                                      COLOR(WHT)
     A  87                                  DSPATR(RI)
     A                                  4 31'Kl:'
     A            TTTIM4         4Y 0B  4 35EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A  88                                  DSPATR(RI)
     A                                  4 41'(hhmm) Sts:S=Send,F=Ferdig,J=Man'
     A            TTMANU    R        B  4 74COLOR(WHT)
S04  A**                                    DSPATR(PR)
     A                                  6  2'EN'
     A            TTTEXT    R        B  6  5COLOR(WHT)
     A                                      CHECK(LC)
     A                                  5  2'NO'
     A            TTTEXL    R        B  5  5COLOR(WHT)
     A                                      CHECK(LC)
     A                                  7  2'Event code:'
     A            TTEDEV    R        B  7 14COLOR(WHT)
     A                                  7 20'Reason code:'
     A            TTEDRE    R        B  7 33COLOR(WHT)
     A                                  7 37'Depot/term:'
     A            TTDEPO    R        B  7 49COLOR(WHT)
     A                                  7 60'Name'
     A            TTNAME    R        B  7 65COLOR(WHT)
     A                                  8  2'Loggføringstid..:'
     A            TTDATL    R        B  8 20EDTWRD('    .  .  ')
     A                                      COLOR(BLU)
     A                                      DSPATR(PR)
     A                                  8 31'Kl:'
     A            TTTIML    R        B  8 35EDTWRD('  :  :  ')
     A                                      COLOR(BLU)
     A                                      DSPATR(PR)
     A                                  8 44'Av bruker ID:'
     A            TTUSER    R        B  8 58COLOR(BLU)
     A                                      DSPATR(PR)
     A                                  9  2'Filter:'
     A                                      COLOR(WHT)
     A                                 10  2'Kun oppdr for transp.nr...'
     A            WSKTRA         8Y 0B 10 30
     A  85                                  DSPATR(RI)
     A                                      TEXT('Kun transpnr.')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11  2'Kun oppdr UTEN T&T-kode...'
     A            WSUTT1         3A  B 11 30COLOR(WHT)
     A            WSUTT2         3A  B 11 34COLOR(WHT)
     A            WSUTT3         3A  B 11 38COLOR(WHT)
     A            WSUTT4         3A  B 11 42COLOR(WHT)
     A            WSUTT5         3A  B 11 46COLOR(WHT)
     A                                 11 50'medtas om INGEN finnes'
     A                                 12  2'Kun oppdr MED  T&T-kode...'
     A            WSKTT1         3A  B 12 30COLOR(WHT)
     A            WSKTT2         3A  B 12 34COLOR(WHT)
     A            WSKTT3         3A  B 12 38COLOR(WHT)
     A            WSKTT4         3A  B 12 42COLOR(WHT)
     A            WSKTT5         3A  B 12 46COLOR(WHT)
     A                                 12 50'medtas om ALLE  finnes'
     A  30        MSGTXT        70A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             16 45MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
