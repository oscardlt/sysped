     A*%%TS  SD  20191114  213021  JOVO        REL-V7R1M0  5770-WDS
     A*
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12001A* 01 YBC PTF12 MVA-REGISTRERTE OG SPESIFIKASJON
     A* 02 YBC PTF12 EHF J/BLANK
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR VKUNDPNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KUNDER SPE-
     A                                      DISJON')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CF04(04 'Søk kunder')
     A                                      CF06(06 'LAG NY')
     A                                      CF10(10 'LAG NY L1')
     A                                      CF03(03 'Avslutt')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/VKUND'
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 DIV1590 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  2 62TIME
     A                                 10  2MSGCON(016 DIV1591 *LIBL/SYMSGF)
     A            WSKNR          8Y 0B 10 21TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                 12  2MSGCON(016 SHU1131 *LIBL/SYMSGF)
     A            WSFTNR        11A  B 12 21
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  28                             23 41MSGCON(038 JOV0995 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 22 41MSGCON(025 SHU1432 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R BILDE02
     A*%%TS  SD  20191114  213021  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Avslutt')
     A N27                                  CF04(04 'Søk kunder')
     A N27                                  CF06(06 'Registrere AVVIK-ADR')
     A N27                                  CF07(07 'FRITEKST')
     A N27                                  CF08(08 'Samlefakt.profil')
     A N27                                  CF09(09 'Samlefakt avd.liste')
     A  27                                  CF10(10 'Sletting')
     A N27                                  CF11(11 'KONTAKTPERSONER')
     A                                      CF12(12 'Til forrige bilde')
     A N27                                  CF13(13 'OPPDRAGSTILLEGG')
     A                                      CF14(14 'DIV PARAMETRE')
     A                                      CF15(15 'VEDL. MILJØAVG.')
     A                                      CF16(16 'Til ARKIVET')
     A                                      CF19(19 'GEOLOCATION')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(VEDLK01 VKUNDPNL)
     A  *DS3                                HLPARA(05 032 05 061)
     A  *DS4                                HLPARA(05 032 05 061)
     A          H                           HLPPNLGRP(VEDLK02 VKUNDPNL)
     A  *DS3                                HLPARA(06 032 06 061)
     A  *DS4                                HLPARA(06 032 06 061)
     A          H                           HLPPNLGRP(VEDLK03 VKUNDPNL)
     A  *DS3                                HLPARA(07 032 08 061)
     A  *DS4                                HLPARA(07 032 08 061)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/VKUND'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1590 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A  29                              2 31MSGCON(040 SHU1433 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A N29                              2 31MSGCON(040 SHU1434 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  3  3MSGCON(025 DIV1592 *LIBL/SYMSGF)
     A            WSKNR          8Y 0O  3 31TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  31                              3 54MSGCON(015 DIV1611 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A N31        INACT         15A  O  3 54COLOR(RED)
     A                                  4  3MSGCON(025 SHU1200 *LIBL/SYMSGF)
     A            WSEORI        17A  B  4 31COLOR(YLW)
     A                                  5  3MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            WSNAK         30A  B  5 31TEXT('Kundenavn')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                  5 63'TDS Godslokalkod'
     A                                  6  3MSGCON(025 DIV1594 *LIBL/SYMSGF)
     A            WSKPNR        30A  B  6 31TEXT('Kontaktperson')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            WSGOLK         4A  B  6 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  7  3MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            WSAD1         30A  B  7 31TEXT('Adresse 1')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A  52                              7 63'Bre:'
     A N52                              7 63'F19=Len/Bre-grad'
     A                                      COLOR(BLU)
     A  52        WSBRE          8Y 6B  7 69EDTCDE(J)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            WSAD2         30A  B  8 31TEXT('Adresse 2')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A  52                              8 63'Len:'
     A  52        WSLEN          9Y 6B  8 68EDTCDE(J)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            WSPNR          4Y 0B  9 31TEXT('Postnummer')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A* 90                                  DSPATR(PC)
     A            WSPOGE         9A  B  9 36DSPATR(UL)
     A                                      COLOR(YLW)
     A  90                                  DSPATR(RI)
     A            WSPB          10A  B  9 46COLOR(YLW)
     A            WSAD3         24A  B  9 57TEXT('Sted')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 10  3MSGCON(025 DIV2005 *LIBL/SYMSGF)
     A            WSLAND         2A  B 10 31COLOR(YLW)
     A  86                                  DSPATR(RI)
     A            WSVK           3A  B 10 36TEXT('Valutakode')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A            WSSPRA         1A  B 10 42COLOR(YLW)
     A                                 10 51MSGCON(015 SHU0526 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10 67MSGCON(013 YBC0659 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 11  3MSGCON(025 DIV1599 *LIBL/SYMSGF)
     A            WSSØNA        10A  B 11 31TEXT('Søkenavn')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                 11 48MSGCON(016 SHU1105 *LIBL/SYMSGF)
     A            WSM3           5Y 0B 11 65EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSLM           5Y 0B 11 71EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 12  3MSGCON(025 SHU0561 *LIBL/SYMSGF)
     A            WSAKNR         8Y 0B 12 31TEXT('Fakturamottager v/kjede')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            AVNAVN        30A  O 12 42
     A                                 12 73'Samtak'
     A            WSSAMT         1A  B 12 80VALUES('J' 'N' ' ')
     A                                      COLOR(YLW)
     A                                 13  3MSGCON(025 DIV1601 *LIBL/SYMSGF)
     A            WSFMNR         8Y 0B 13 31TEXT('Fakturamottager v/kjede')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A            FMNAVN        30A  O 13 42
     A                                 14  3MSGCON(025 DIV1598 *LIBL/SYMSGF)
     A            WSTLF         15A  B 14 31TEXT('Telefon')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A            WSFAXN        15A  B 14 49COLOR(YLW)
     A                                 15  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            WSEPOS        70A  B 15 11CHECK(LC)
     A                                      COLOR(YLW)
     A                                 16  3MSGCON(025 DIV1602 *LIBL/SYMSGF)
     A            WSMINU         4Y 2B 16 31TEXT('MIN.UTLPRO')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 16 42MSGCON(015 SHU0619 *LIBL/SYMSGF)
     A            WSSELG         3A  B 16 58COLOR(YLW)
S32  A*                                16 63MSGCON(012 SHU0770 *LIBL/SYMSGF)
N32  A                                 16 63'SEC'
S32  A*           WSSECU         1A  B 16 76VALUES('Y' 'N' ' ')
N32  A            WSSECU         1A  B 16 67VALUES('Y' 'N' ' ')
     A                                      COLOR(YLW)
N32  A                                 16 69'Fullmakt:'
N32  A            WSFULL         1A  B 16 79VALUES('J' 'N' ' ')
N32  A                                      COLOR(YLW)
     A                                 17  3MSGCON(025 DIV1603 *LIBL/SYMSGF)
     A            WSUTLP         4Y 2B 17 31TEXT('UTL.PROV')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 17 42MSGCON(040 DIV1612 *LIBL/SYMSGF)
     A                                 18  3MSGCON(025 DIV1604 *LIBL/SYMSGF)
     A            WSREGN         8Y 0B 18 31TEXT('REGNR')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 18 42MSGCON(025 DIV1605 *LIBL/SYMSGF)
     A            WSFR02         1A  B 18 69DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKN1          5Y 0B 18 71DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKN2          2Y 0B 18 78DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 19  3MSGCON(025 DIV1606 *LIBL/SYMSGF)
     A            WSOPDT         2A  B 19 31TEXT('OPD.TYPE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A            WSFR03         2A  B 19 34COLOR(YLW)
S02  A*                                19 42MSGCON(025 DIV1607 *LIBL/SYMSGF)
S02  A*           WSLIKV         1A  B 19 69TEXT('LIKV.KODE')
N02  A                                 19 42MSGCON(014 JOV1787 *LIBL/SYMSGF)
N02  A            WSFR06         1A  B 19 57COLOR(YLW)
N02  A                                      VALUES(' ' 'J')
N02  A                                 19 59MSGCON(007 JOV1788 *LIBL/SYMSGF)
N02  A                                 19 69MSGCON(009 JOV1786 *LIBL/SYMSGF)
N02  A            WSLIKV         1A  B 19 79TEXT('LIKV.KODE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                 20  3MSGCON(025 SHU0024 *LIBL/SYMSGF)
     A            WSSAMG         1A  B 20 31DSPATR(UL)
     A                                      COLOR(YLW)
     A  91                                  DSPATR(RI)
     A            WSKUGR         2A  B 20 34COLOR(YLW)
     A            WSBET          2A  B 20 37COLOR(YLW)
     A  87                                  DSPATR(RI)
     A                                 20 42MSGCON(025 SHU0172 *LIBL/SYMSGF)
     A            WSFORE        11A  B 20 69DSPATR(UL)
     A                                      COLOR(YLW)
     A  88                                  DSPATR(RI)
     A                                 21  3MSGCON(025 SHU0139 *LIBL/SYMSGF)
     A            WSDKUN         1A  B 21 31DSPATR(UL)
     A                                      COLOR(YLW)
     A  91                                  DSPATR(RI)
     A                                      VALUES(' ' 'D')
S01  A*                                21 42MSGCON(011 SHU1199 *LIBL/SYMSGF)
S01  A*           WSMVA         20A  B 21 55COLOR(YLW)
     A                                 21 34MSGCON(011 SHU1199 *LIBL/SYMSGF)
     A            WSMVA         20A  B 21 46COLOR(YLW)
     A                                 21 67'Reg'
     A            WSMVJN         1A  B 21 71COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 21 73'Spes'
     A            WSMVSP         1A  B 21 78COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 22  3MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             22 23MSGCON(018 JOV1497 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             22 42MSGCON(015 CBA0270 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             22 63MSGCON(018 JOV0440 *LIBL/SYMSGF)
     A  35                                  COLOR(BLU)
     A N35                                  COLOR(WHT)
     A N27                             23  3MSGCON(018 JOV0441 *LIBL/SYMSGF)
     A  36                                  COLOR(BLU)
     A N36                                  COLOR(WHT)
     A  27                             23 23MSGCON(018 DIV1555 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 42MSGCON(019 CBA0280 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 63MSGCON(018 JOV0870 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24  3'F14=Div parametere'
     A                                      COLOR(BLU)
     A N30                             24 23'F16=Arkiv'
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R BILDE03
     A                                      CF12(12 'Gå til forrige bilde')
     A                                      CF03(12)
     A                                      CF06(06 'ALLE VADR')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/VKUND'
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 DIV1590 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  3  2MSGCON(025 DIV1592 *LIBL/SYMSGF)
     A            WSKNR          8Y 0O  3 29TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            WSNAK         30A  O  4 29TEXT('Kundenavn')
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            WSAD1         30A  O  5 29TEXT('Adresse 1')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            WSAD2         30A  O  6 29TEXT('Adresse 2')
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            WSPNR          4Y 0O  7 29TEXT('Postnummer')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSAD3         24A  O  7 35TEXT('Sted')
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(025 DIV1610 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            W2NAV         30A  B 10 29TEXT('Navn')
     A  99                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 11  2MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            W2AD1V        30A  B 11 29TEXT('Adresse 1')
     A                                      CHECK(LC)
     A                                 12  2MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            W2AD2V        30A  B 12 29TEXT('Adresse 2')
     A                                      CHECK(LC)
     A                                 13  2MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            W2AD3V        30A  B 13 29TEXT('Sted vareadresse')
     A  97                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 14  2MSGCON(025 SHU1286 *LIBL/SYMSGF)
     A            W2LK           2   B 14 29
     A                                 15  2MSGCON(025 JOV1496 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 16  2MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            W3NAV         30A  B 16 29TEXT('Navn')
     A  99                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 17  2MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            W3AD1V        30A  B 17 29TEXT('Adresse 1')
     A                                      CHECK(LC)
     A                                 18  2MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            W3AD2V        30A  B 18 29TEXT('Adresse 2')
     A                                      CHECK(LC)
     A                                 19  2MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            W3AD3V        30A  B 19 29TEXT('Sted vareadresse')
     A  97                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 20  2MSGCON(025 SHU1286 *LIBL/SYMSGF)
     A            W3LK           2   B 20 29
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 29MSGCON(018 JOV1498 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
