     A          R LL013P1
     A            KFMT1         75      1  1
     A  88                              1 76'¤&l'
     A  88        COPY           2  0   1 79
     A  88                              1 81'X¤(s12H¤&l1O¤&a-3R'
     A            KFMT2         75      2  1
     A  88                              2 76'¤(12U¤(s0p12h0s3b4099T'
     A N47N46                           3  2MSGCON(20 YBC1075 SYMSGF)
     A N47 46                           3  2MSGCON(20 YBC1076 SYMSGF)
     A  47                              3  2MSGCON(20 YBC1077 SYMSGF)
     A                                  3 85MSGCON(05 YBC1078 SYMSGF)
     A            XIDAG          8  0   3 91EDTWRD('    .  .  ')
     A                                  3103MSGCON(04 YBC1079 SYMSGF)
     A            XTID           4  0   3108EDTWRD('  :  ')
     A N47                              4 85MSGCON(08 YBC1080 SYMSGF)
     A N47        XALN           7  0   4 94EDTCDE(Z)
     A N47                              4103MSGCON(05 YBC1081 SYMSGF)
     A                                  4110PAGNBR
     A                                  5  2MSGCON(11 YBC1082 SYMSGF)
     A            LHPNR          8  0   5 14EDTCDE(Z)
     A            KNAVN         30A     5 23
     A N47 45                           5 85MSGCON(08 YBC1083 SYMSGF)
     A                                      HIGHLIGHT
     A N47 45     XALNG          7  0   5 94EDTCDE(Z)
     A                                      HIGHLIGHT
     A N47                              6  2MSGCON(11 YBC1084 SYMSGF)
     A N47        LHPAK         17      6 14
     A N47                              6 33MSGCON(11 YBC1085 SYMSGF)
     A N47        LHORDR        17      6 45
     A N47                              6 85MSGCON(14 YBC1086 SYMSGF)
     A N47        LHTDT          8  0   6100EDTWRD('    .  .  ')
     A N47                              7  2MSGCON(11 YBC1087 SYMSGF)
     A N47        LHBNR         17      7 14
     A N47        LHBNA         30A     7 33
     A N47        LHBA1         30A     7 64
     A N47        LHBA2         30A     8 33
     A N47        XHBA3         34A     8 64
     A                                  9  2'-------------------------'
     A                                  9 27'-------------------------'
     A                                  9 52'-------------------------'
     A                                  9 77'-------------------------'
     A                                  9102'------------------'
     A                                 10  2MSGCON(15 YBC1088 SYMSGF)
     A                                 10 18MSGCON(34 YBC1089 SYMSGF)
     A                                 10 54MSGCON(07 YBC1090 SYMSGF)
     A                                 10 62MSGCON(05 YBC1091 SYMSGF)
     A                                 10 68MSGCON(05 YBC1092 SYMSGF)
     A                                 10 74MSGCON(05 YBC1093 SYMSGF)
     A                                 10 80MSGCON(07 YBC1094 SYMSGF)
     A                                 10 88MSGCON(25 YBC1095 SYMSGF)
     A                                 10114MSGCON(06 YBC1096 SYMSGF)
     A                                 11  2'-------------------------'
     A                                 11 27'-------------------------'
     A                                 11 52'-------------------------'
     A                                 11 77'-------------------------'
     A                                 11102'------------------'
     A                                 12  2' '
      *
     A          R LL013P2                   SPACEB(1)
     A N41        LLANR         15A        2
     A N41        LLVB          35A       18
     A  42                                18MSGCON(08 YBC1097 SYMSGF)
     A  42        VASENR        25A       27
     A            VDLANT         7  0     54EDTCDE(3)
     A            VDSAE3         5  0     62EDTCDE(Z)
     A            VDSAE2         5  0     68EDTCDE(Z)
     A            VDSAE1         5  0     74EDTCDE(Z)
     A            VDSAE0         7  0     80EDTCDE(Z)
     A N43        VALLOK        25A       88
     A  43                                88MSGCON(25 YBC1098 SYMSGF)
     A                                   114'______'
     A                                      SPACEA(1)
      *
     A          R LL013P21
     A N44                                18'SERIENR:'
     A            SNSN          35A       27UNDERLINE
     A                                      SPACEA(1)
      *
     A          R LL013P3                   SPACEB(2)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'------------------'
     A                                      SPACEA(1)
     A                                     2MSGCON(10 YBC1106 SYMSGF)
     A            TLWNT0         7  0     54EDTCDE(3)
     A            TLWNT3         5  0     62EDTCDE(Z)
     A            TLWNT2         5  0     68EDTCDE(Z)
     A            TLWNT1         5  0     74EDTCDE(Z)
     A            TLWNTL         7  0     80EDTCDE(Z)
     A                                      SPACEA(2)
     A N47                                 2MSGCON(24 YBC1099 SYMSGF)
     A N47                                27'________/________'
     A N47                                52MSGCON(25 YBC1100 SYMSGF)
     A N47                                78'________/________'
     A                                      SPACEA(1)
     A                                     2'Plassert: ________________________-
     A                                      ______'
     A  47                                  SPACEA(1)
     A N47                                52MSGCON(19 YBC1101 SYMSGF)
     A N47                                72MSGCON(16 YBC1102 SYMSGF)
     A N47                                92MSGCON(16 YBC1103 SYMSGF)
     A                                      SPACEA(1)
     A                                     2'Påbegynt kl. ___:___'
     A                                    24'Ferdig kl. ___:___'
     A  47                                  SPACEA(1)
     A N47                                72MSGCON(16 YBC1102 SYMSGF)
     A N47                                92MSGCON(16 YBC1103 SYMSGF)
     A                                      SPACEA(1)
     A                                     2'Signatur: ________________________-
     A                                      ______'
     A N47                                72MSGCON(16 YBC1102 SYMSGF)
     A N47                                92MSGCON(16 YBC1103 SYMSGF)
      *
     A          R LL013P4
     A                                    18MSGCON(08 YBC1097 SYMSGF)
     A            LLSER         25A       27SPACEA(1)
      *
     A          R LL013P5                   SPACEB(1)
     A            XLANR         15A        2
     A            XLVB          35A       18
     A            XDLANT         7  0     54EDTCDE(3)
     A            XDSAE3         5  0     62EDTCDE(Z)
     A            XDSAE2         5  0     68EDTCDE(Z)
     A            XDSAE1         5  0     74EDTCDE(Z)
     A            XDSAE0         7  0     80EDTCDE(Z)
     A            XALLOK        25A       88
     A                                   114'______'
     A                                      SPACEA(1)
      *
     A          R LL013P6
     A                                    18MSGCON(08 YBC1097 SYMSGF)
     A            XASENR        25A       27SPACEA(1)
      *
     A          R LL013P7                   SPACEB(2)
     A  41                                 2MSGCON(28 YBC1104 SYMSGF)
     A            LHALN          7  0     31EDTCDE(Z)
     A  42                                39MSGCON(08 YBC1105 SYMSGF)
     A            LHBNA         30A       48
      *
     A          R LL013P8                   SPACEB(1)
     A            LTTXT         70A        2
