********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 Avrunda vikter på varuposter
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE062R MODULE(*LIBL/TSVE062R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVEFZL01  UF A E           K DISK    USROPN
     F                                     RENAME(SVEFR:SVEFZR)
     FSVEVZL01  UF A E           K DISK    USROPN
     F                                     RENAME(SVEVR:SVEVZR)
     FSVEF      O  A E             DISK    USROPN
     FSVEV      IF A E           K DISK    USROPN
     FSVTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D WMODE           S              1
     D UNIKA           S              9
     D REFF            S             17
     D JSONstring      S          32000
     D Error           S            150
N01  D W110            S             11  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬unik¬ : ¬'+%trim(UNIKA)+'¬, '
        +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬avd¬ : ¬'+%trim(SYAVA)+'¬, '
        +'¬opd¬ : ¬'+%trim(SYOPA)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errmsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"updexternfakt" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVEFZL01
     C  N50              CLOSE     SVEVZL01
     C  N50              CLOSE     SVEF
     C  N50              CLOSE     SVEV
     C  N50              CLOSE     SVTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA = zhbgetvar('OPD')
     C                   EVAL      SYOP  = c2n2(SYOPA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      REFF  = zhbgetvar('REFF')
     C                   EVAL      UNIKA   = zhbgetvar('UNIK')
     C                   EVAL      UNIK  = c2n2(UNIKA)
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C     *LIKE         DEFINE    SVEF_UNIK     UNIK
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS
     C     *LIKE         DEFINE    SVEV_SYLI     ZVEV_SYLI
     C                   EVAL      ZVVK_DTS=99999999
     C     SVEFKEY       KLIST
     C                   KFLD                    UNIK
     C                   KFLD                    REFF
     C     SVTVKKEY      KLIST
     C                   KFLD                    SVEF_VAKD
     C                   KFLD                    ZVVK_DTS
     C     SVEVKEY       KLIST
     C                   KFLD                    SVEF_SYAV
     C                   KFLD                    SVEF_SYOP

      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVEFZL01
     C  N50              OPEN      SVEVZL01
     C  N50              OPEN      SVEF
     C  N50              OPEN      SVEV
     C  N50              OPEN      SVTVK

     c                   endsr
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta extern faktura
     C     SVEFKEY       CHAIN     SVEFZL01                           55
      * MODE D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SVEFZR

     C     SVEFKEY       SETLL     SVEVZL01
     C     SVEFKEY       READE     SVEVZL01                               56
     C     *IN56         DOWEQ     *OFF
     C                   DELETE    SVEVZR
     C     SVEFKEY       READE     SVEVZL01                               56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      SVEF_SYAV=SYAV
     C                   EVAL      SVEF_SYOP=SYOP
     C                   IF        SVEF_FATY=*BLANKS
     C                   EVAL      SVEF_FATY='N380'
     C                   ENDIF
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  57
     C     *IN57         IFEQ      *OFF
     C                   EVAL      SVEF_VAKU=SVVK_KRS
     C                   ENDIF
     C                   UPDATE    SVEFZR
     C                   WRITE     SVEFR

      * Hitta høgsta varupostnummret
     C                   EVAL      *IN57=*OFF
     C     SVEVKEY       SETLL     SVEV
     C     SVEVKEY       READE     SVEV                                   57
     C     *IN57         DOWEQ     *OFF
     C                   EVAL      ZVEV_SYLI=SVEV_SYLI
     C     SVEVKEY       READE     SVEV                                   57
     C                   ENDDO
      * Skriv til produktionsfil
     C     SVEFKEY       SETLL     SVEVZL01
     C     SVEFKEY       READE     SVEVZL01                               56
     C     *IN56         DOWEQ     *OFF
     C                   IF        SVEV_SYAV=*ZEROS
     C                   EVAL      SVEV_SYAV=SYAV
     C                   EVAL      SVEV_SYOP=SYOP
     C                   ADD       1             ZVEV_SYLI
     C                   EVAL      SVEV_SYLI=ZVEV_SYLI
     C                   EVAL      SVEV_VANO=ZVEV_SYLI
     C                   EVAL      SVEV_CALL='00'
     C                   UPDATE    SVEVZR
     C                   EXSR      SRUP
     C                   ENDIF
     C     SVEFKEY       READE     SVEVZL01                               56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update, skriv till produktionsfiler              SRUP
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUP          BEGSR
N01  C                   IF        SVEV_BRUT > 1
N01  C                   EVAL(H)   W110=SVEV_BRUT
N01  C                   EVAL      SVEV_BRUT=W110
N01  C                   ENDIF
N01  C                   IF        SVEV_NETO > 1
N01  C                   EVAL(H)   W110=SVEV_NETO
N01  C                   EVAL      SVEV_NETO=W110
N01  C                   ENDIF
     C                   WRITE     SVEVR
     C                   ENDSR
