********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10244 * 01 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
10262 * 02 YBC PTF10 TOLLSATS ER UTVIDERT TIL 7.2
10286 * 03 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
10329 * 04 YBC PTF10 MENGDE I ANNEN ENHET ER ENDRET FRA 7.0 TIL 9.0
      *              INGEN MERKING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11009 * 05 YBC PTF11 VIS FEILMELDING PÅ EN ANNEN MÅTE
11016 * 06 YBC PTF11 OMBEREGNING
11174 * 07 YBC PTF11 TILLEGGSLINJER MED SAMME POST
11185 * 08 YBC PTF11 VIS TOTAL VEKT OG SUM AV LINJEVEKT
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12053 * 09 YBC PTF12 FLERE BELØP-FELTER ER ENDRET FOR Å TA STØRRE VERDI
12109 * 10 YBC PTF12 BYTT SADL MED SADVARE, MYE ENDRING OG INGEN MERKING
12281 * 11 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SAD010' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           3Y 0O  7  2EDTCDE(4)
     A            SV_LEVENR      8S 0H
     A            SV_VARENR     28A  O  7  6TEXT('ALFASØKING')
     A            SV_VAREBE1    20A  O  7 35
     A            SV_VAREBE     50A  H
     A            SV_W2VF        1A  O  7 56
     A            SV_W2LK        2A  O  7 59TEXT('OPPRINGELSE LAND')
     A            SV_W2VNT       8Y 0O  7 62TEXT('TARIFFNR.')
     A                                      EDTCDE(4)
     A            SV_W2VNTI      8A  H
     A            SV_W2TN        1A  O  7 71TEXT('TOLLNEDSETTELSE')
     A            SV_W2PRE       1A  O  7 74
S03  A*           SLVEKT         5Y 0O  7 76TEXT('VEKR')
N03  A            SV_W2VKTB     12Y 3O  7 76TEXT('VEKR')
     A                                      EDTCDE(4)
     A            SV_W2PVA       1A  O  7 90
S02  A*           SLSATS         5Y 2O  7 85TEXT('PVA SATS')
N02  A            SV_W2AS        7Y 2O  7 93TEXT('PVA SATS')
     A                                      EDTCDE(4)
S02  A*           SLMFR          1A  O  7 92
N02  A            SV_W2MFR       1A  O  7102
     A            SV_W2AKD1      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD2      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD3      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD4      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD5      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD6      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD7      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2AKD8      2A  H      TEXT('AVGIFTSKODE')
     A            SV_W2ASV1      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV2      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV3      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV4      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV5      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV6      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV7      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_W2ASV8      3A  H      TEXT('AVGIFTS SEKVENS')
     A            SV_SVTOP1     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP2     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP3     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP4     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP5     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP6     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP7     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP8     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP9     17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVTOP10    17A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SV_SVCRE1      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE2      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE3      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE4      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE5      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE6      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE7      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE8      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE9      3A  H      TEXT('KOMMUNIKASJONREF.')
     A            SV_SVCRE10     3A  H      TEXT('KOMMUNIKASJONREF.')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  9'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 30'Tdeknr.'
     A            UAVD           4Y 0O  1 38COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 43'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 45COLOR(BLU)
S06  A*                                     EDTCDE(4)
N06  A                                      EDTCDE(L)
E08  A                                  2  2'Fsum->'
E08  A            SIBEL3        13Y 2O  2 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E08  A                                  2 25'Vsum->'
E08  A            UVSUM         13Y 2O  2 32TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E08  A                                  2 47'Diff->'
E08  A            UDIFF         13Y 2O  2 54TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
N08  A                                  2 70'Tvkt->'
N08  A            SIVKB          9Y 0O  2 77TEXT('Beløp Faktsum')
N08  A                                      COLOR(WHT)
N08  A                                      EDTCDE(4)
N08  A                                  2 87'Lvkt->'
N08  A            UVKB          13Y 3O  2 94TEXT('Beløp Faktsum')
N08  A                                      COLOR(WHT)
N08  A                                      EDTCDE(4)
N08  A                                  2109'Diff->'
N08  A            UVDIF         13Y 3O  2116TEXT('Beløp Faktsum')
N08  A                                      COLOR(WHT)
N08  A                                      EDTCDE(L)
S09  A*                                 3  1'Siste li Op Varenr.. TN P T.verdi.-
S09  A*                                     .... Nettovekt        Mengde PV Tol-
S09  A*                                     lsats M Avgift......... Varebeskriv-
S09  A*                                     else S'
N09  A                                  3  1'Siste li Op Varenr.. TN P T.verdi.-
N09  A                                      ...... Nettovekt        Mengde PV T-
N09  A                                      ollsats M Avgift........... Varebes-
N09  A*                                     krivelse S'
N09  A                                      krivelse                 S Pr'
     A                                      COLOR(WHT)
     A            SVLK           2A  O  4 10COLOR(BLU)
     A            SVVNT          8Y 0O  4 13COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVTN           1A  O  4 22COLOR(BLU)
     A            SVPRE          1A  O  4 25COLOR(BLU)
E09  A            SVBELT        13Y 2O  4 27EDTCDE(4)
     A                                      COLOR(BLU)
S02  A*           WXVKTN         5Y 0O  4 40EDTCDE(4)
S03  A*           SVVKTN         9Y 0O  4 40EDTCDE(4)
E09  A            SVVKTN        12Y 3O  4 42EDTCDE(4)
     A                                      COLOR(BLU)
S02  A*           WXNTM          3Y 0O  4 46EDTCDE(4)
E09  A            SVNTM          9Y 0O  4 54EDTCDE(4)
     A                                      COLOR(BLU)
S02  A*           SVPVA          1A  O  4 52COLOR(BLU)
E09  A            SVPVA          1A  O  4 66COLOR(BLU)
S02  A*           SVAS           5Y 2O  4 54COLOR(BLU)
E09  A            SVAS           7Y 2O  4 69COLOR(BLU)
     A                                      EDTCDE(4)
S02  A*           SVMFR          1A  O  4 61COLOR(BLU)
E09  A            SVMFR          1A  O  4 78COLOR(BLU)
S02  A*           SVKDAÆ         2A  O  4 63COLOR(BLU)
E09  A            SVAKD1         2A  O  4 80COLOR(BLU)
S02  A*           WXBLÆ          5Y 0O  4 66EDTCDE(4)
E09  A            SVABL1        13Y 2O  4 83EDTCDE(4)
     A                                      COLOR(BLU)
S02  A*           WXVT           8A  O  4 73COLOR(BLU)
S03  A*           SVVT          15A  O  4 94COLOR(BLU)
S02  A*           SV01           1A  O  4 81COLOR(RED)
S03  A*           SV01           1A  O  4110COLOR(RED)
E09  A            SVVT01        30A  O  4 98COLOR(BLU)
E09  A            SV01           1A  O  4129COLOR(RED)
     A            SVDP           2Y 0O  4131COLOR(BLU)
     A                                      EDTCDE(Z)
     A  39                              5 50'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              5 65'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Lin Alfasøkingtekst............. T-
     A                                      ekst.............. R31 Op Tariffnr -
     A                                      TN P VEKT......... PVA Tol.sats MF'
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF04(04 'Divers søking')
     A                                      CF08(08 'Søk NY VARE')
     A                                      CF09(09 'Kalkulator av/på')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF03(03 'Be om utskrift')
     A                                      CF16(16 'JUSTERING')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CF11(11 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A                                      CF12(12 'Gå til bilde02')
     A  60                                  CF21(21 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A  60                                  CF22(22 'MERK B PÅ STATUS')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPDOC('SAD010' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 002 02 015)
     A          H                           HLPDOC('SAD011' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 018 02 018)
     A          H                           HLPDOC('SADPRE' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 022 02 022)
     A          H                           HLPDOC('SAD012' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 026 02 066)
     A          H                           HLPDOC('SAD013' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 068 02 078)
     A          H                           HLPDOC('SAD014' 'HELPNU' 'HLP400')
     A                                      HLPARA(03 009 03 040)
     A          H                           HLPDOC('SAD014A' 'HELPNU' 'HLP400')
     A                                      HLPARA(04 006 04 050)
     A          H                           HLPDOC('SAD015' 'HELPNU' 'HLP400')
     A                                      HLPARA(05 006 05 054)
     A          H                           HLPDOC('SAD016' 'HELPNU' 'HLP400')
     A                                      HLPARA(06 001 06 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
S09  A*                                 1  2'VF Linjenr. Opp TN Pre. Tollverdi.-
S09  A*                                     .. Bruttovekt... Nettovekt.... Meng-
S09  A*                                     de... PVA Tol.sats Mf'
N09  A                                  1  2'VF Linjenr. Opp TN Pre. Tollverdi.-
N09  A                                      .... Bruttovekt... Nettovekt.... Me-
N09  A                                      ngde... PVA Tol.sats Mf Pros'
     A                                      COLOR(WHT)
     A            W2VF           1A  B  2  2TEXT('Verdi fasts.kode')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  89                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      DUP
     A            W2NR           3Y 0B  2  7TEXT('LINJENR.')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  87                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2LK           2A  B  2 14TEXT('Landk. oppr.land')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  88                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      DUP
     A            W2TN           1A  B  2 18TEXT('Tollnedsettelse')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  86                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      DUP
     A            W2PRE          1A  B  2 22TEXT('Preferanse')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  85                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      DUP
E09  A            W2BELT        13Y 2B  2 26TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0,00)
S03  A*           W2VKTB         9Y 0B  2 39TEXT('Vekt Brutto')
E09  A            W2VKTB        12Y 3B  2 41TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S03  A*           W2VKTN         9Y 0B  2 49TEXT('Vekt Netto')
E09  A            W2VKTN        12Y 3B  2 55TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
E09  A            W2NTM          9Y 0B  2 69TEXT('Mengde')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
E09  A            W2PVA          1A  B  2 80TEXT('Enhet PVAF')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  82                                  DSPATR(RI)
     A  46
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      DUP
S02  A*           W2AS           5Y 2B  2 73TEXT('Avgiftssats')
E09  A            W2AS           7Y 2B  2 83TEXT('Avgiftssats')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     AO 81                                  DSPATR(RI)
     A  46
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
S02  A*           W2MFR          1A  B  2 80TEXT('MVA fri kod')
E09  A            W2MFR          1A  B  2 92TEXT('MVA fri kod')
     A                                      DSPATR(UL)
     A  39
     AO 80                                  DSPATR(HI)
     A  46
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      DUP
     A            W2DP           2Y 0B  2 95TEXT('Prosedyre')
     A  39
     AO 73                                  DSPATR(HI)
     A  46
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
N11  A            W2DP2          2Y 0B  2 98TEXT('Prosedyre')
N11  A  39
N11  AO 73                                  DSPATR(HI)
N11  A  46
N11  AO 39                                  DSPATR(PR)
N11  A N39                                  COLOR(YLW)
N11  A                                      EDTCDE(4)
     A                                  3  2'Avg'
     A            W2AKD1         2A  B  3  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2ASV1         3A  B  3  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA1         7Y 2B  3 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
E09  A            W2AGR1        13Y 2B  3 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
E09  A            W2ABL1        13Y 2B  3 37DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 57'F3=Avsl'
     A N35                                  COLOR(BLU)
     A  35                                  COLOR(RED)
     A                                  3 65'F4/F8=Søk F9=Kal'
     A                                      COLOR(BLU)
     A                                  4  2'V.B'
S03  A*           W2FT          17A  B  4  6TEXT('Fritekst')
N03  A            W2FT01        28A  B  4  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2NT01         6Y 0B  4 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH01         4A  B  4 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
S03  A*           W2VT          15A  B  4 47TEXT('Varetekst')
N03  A            W2VT01        30A  B  4 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
S03  A*                                 4 52'Rf'
S03  A*                                     COLOR(BLU)
S03  A*                                 4 56'F10=Ekst.l F11=Auto vekt'
S03  A*                                     COLOR(BLU)
     A                                  5  2'T.O'
     A            W2TOP1        45A  B  5  6DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
S03  A*           W2CREF         3A  B  5 52TEXT('KOMMUNIKASJONREF.')
N03  A                                  5 62'Ref.'
N03  A                                      COLOR(BLU)
N03  A            W2CRE1         3A  B  5 67TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A N75                                  COLOR(YLW)
     A  75                                  COLOR(RED)
     A                                      DUP
N03  A                                  5 76'F15=Auto T.O F17=Neste/K.'
N03  A                                      COLOR(BLU)
     A N30        MSGTX2        78A  O  6  2COLOR(RED)
S05  A* 30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
S05  A*                                     COLOR(WHT)
N05  A  30        MSGTX3        78A  O  6  2COLOR(WHT)
N03  A                                  6 82'F11=Auto vekt'
N03  A                                      COLOR(BLU)
N03  A                                  6107'F15=Auto T.O F17=Neste/K.'
N03  A                                      COLOR(BLU)
S05  A*           MSGNR          7A  P
      *
     A          R JUSTL
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      WINDOW(*DFT 6 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Justering på linjenivå'
     A                                      COLOR(WHT)
     A                                  2  2'Justering.....:'
     A            W3BEL         11  2B  2 18DSPATR(UL)
     A  89                                  DSPATR(HI)
     A N30
     AO 89                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                  3  2'% av vareverdi:'
     A            W3PROS         5  2B  3 18DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                  4  2'Valutakode....:'
     A            W3VAL          3A  B  4 18DSPATR(UL)
     A  88                                  DSPATR(HI)
     A  88                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  5  2'Beløp i NOK...:'
     A            W3BELN        13  2O  5 18DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(L)
     A N30                              6  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        36A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                  1  9'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 30'Tdeknr.'
     A            UAVD           4Y 0O  1 38COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 43'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 45COLOR(BLU)
S06  A*                                     EDTCDE(4)
N06  A                                      EDTCDE(L)
     A                                  2  3'Fakt.bel:'
S09  A*           UFAKNO         9Y 0O  2 15COLOR(BLU)
N09  A            UFAKNO        11Y 0O  2 15COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 30'Frakt:'
     A            SIBEL8        11Y 2O  2 38TEXT('Frakt omberegnet')
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A                                  2 54'Rabatt:'
     A            SIBELC        11Y 2O  2 62COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  3'Andre kost:'
     A                                  3 30'Forsik:'
     A            SIBEL9        11Y 2O  3 15TEXT('Andre kost.omber')
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
S09  A*           SIBELA         7Y 0O  3 38TEXT('Beløp Forsikring')
N09  A            SIBELA         9Y 0O  3 38TEXT('Beløp Forsikring')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3 54'CIFSUM:'
S09  A*           WSBEL4         9Y 0O  3 62TEXT('Beløp CIFsum')
N09  A            WSBEL4        11Y 0O  3 62TEXT('Beløp CIFsum')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
E08  A                                  4  2'Fsum->'
E08  A            SIBEL3        13Y 2O  4 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E08  A                                  4 25'Vsum->'
E08  A            UVSUM         13Y 2O  4 32TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E08  A                                  4 47'Diff->'
E08  A            UDIFF         13Y 2O  4 54TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
N08  A                                  4 70'Tvkt->'
N08  A            SIVKB          9Y 0O  4 77TEXT('Beløp Faktsum')
N08  A                                      COLOR(WHT)
N08  A                                      EDTCDE(4)
N08  A                                  4 87'Lvkt->'
N08  A            UVKB          13Y 3O  4 94TEXT('Beløp Faktsum')
N08  A                                      COLOR(WHT)
N08  A                                      EDTCDE(4)
N08  A                                  4109'Diff->'
N08  A            UVDIF         13Y 3O  4116TEXT('Beløp Faktsum')
N08  A                                      COLOR(WHT)
N08  A                                      EDTCDE(L)
     A  39                              5 50'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              5 65'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Lin Alfasøkingtekst............. T-
     A                                      ekst.............. R31 Op Tariffnr -
     A                                      TN P VEKT......... PVA Tol.sats MF'
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  3  2'Søk fra kundenr.  --->'
     A            W2KNR          8Y 0B  3 25TEXT('KUNDENR.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4  2'  fra vare (alfa) --->'
     A            W2ALFA        28A  I  4 25TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
     A                                  4 43'* i pos 1 = frisøk'
