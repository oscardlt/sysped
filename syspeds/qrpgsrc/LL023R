********************************************************************
      * Vedlikehold MOTTAKSRAPPORT header
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Fordel EU-frakt
11XXX * 02 YBC PTF11 OPSJON S --> SEND DANSK FORTOLLING IMPORT
      * 03 JOV PTF11 USPØR='M',Inn via opsjon M i Logi/10 =Arb m m.rapp (eller se)
      * 04 JOV PTF11 vise Lref(inv.no) i subfil - utvidet fra 6 til 10 linjer
11XXX * 06 YBC PTF11 VIS TOTAL BRUTTO VEKT
11XXX * 07 JOV PTF11 Tillatt å slettemerke M-rapp når den har ingen linjer
11XXX * 08 JOV PTF11 Åpne for å benytte F10 > Endre selv om en kommer inn via spørr/opsjon M
********************************************************************
     FLLMRFL01  IF   E           K DISK
     F                                     RENAME(LLMRFR:LLMRFR1)
     FLLMRFL02  IF   E           K DISK
     F                                     RENAME(LLMRFR:LLMRFR2)
     FLLMRFL05  IF   E           K DISK
     F                                     RENAME(LLMRFR:LLMRFR5)
     FLLMRF     UF A E           K DISK
     FLLMRT     UF A E           K DISK
     FFIRM      IF   E           K DISK
     FLLVADR3   IF   E           K DISK
     FLLAGJL07  IF   E           K DISK
N01  FLLAGJFEU  UF A E           K DISK
N01  FKODTVA    IF   E           K DISK
     FLL023D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
N01  F                                     INFDS(FBAREA)
N01  D FBAREA          DS
N01  D  WSCLOC               370    371B 0
N01  D  WSSUBN               378    379B 0
N01   * CURSOR LOKASJON AV AKTIV VINDU
N01  D  WSCLOW               382    383B 0
     D                 DS
     D  WTIME                  1     14  0
     D  WTM                    1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  XIDAG                  1      8  0
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D                 DS
     D  MRLNA                  1     30
     D  XLNA                   1     17
N04  D                 DS
N04  D  MRLREF                 1     25
B04  D  XREF                   1     15
     D                 DS
     D  VADRN3                 1     30
     D  VXPNR                  1      4
     D  VXBLAN                 5      5
     D  VXSTED                 6     30
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27LMR1K         SETLL     LLMRFL01
     C   28LMR2K         SETLL     LLMRFL02
     C   29LMR5K         SETLL     LLMRFL05
     C   27LMR1K1        READE     LLMRFL01                               94
     C   28LMR2K1        READE     LLMRFL02                               94
     C   29LMR5K1        READE     LLMRFL05                               94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C     MRMRNR        COMP      UMRNR                                  50
     C                   WRITE     SUBFIL
     C   27LMR1K1        READE     LLMRFL01                               94
     C   28LMR2K1        READE     LLMRFL02                               94
     C   29LMR5K1        READE     LLMRFL05                               94
     C  N94              ENDDO
      *
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *
     C   05              GOTO      START
      *
     C   06              DO
     C                   EXSR      CRTMRN
     C                   GOTO      START3
     C                   END
      *
     C   08              DO
     C                   MOVE      'J'           W2ÅPEN
     C                   GOTO      START6
     C                   END
      *
     C   12              GOTO      SLUTT
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     START6        TAG
     C                   EXFMT     VAREL2
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKKD2
     C                   GOTO      START6
     C                   END
     C     *IN12         IFEQ      '1'
     C                   GOTO      START3
     C                   END
     C                   SELECT
     C     W2LNR         WHENNE    *BLANKS
     C                   MOVEA     '001'         *IN(27)
     C     W2MRNR        WHENNE    0
     C     W2ÅPEN        ANDEQ     'J'
     C                   MOVEA     '100'         *IN(27)
     C     W2MRNR        WHENNE    0
     C     W2ÅPEN        ANDEQ     'N'
     C                   MOVEA     '010'         *IN(27)
     C                   OTHER
     C                   GOTO      START6
     C                   ENDSL
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    '1'
     C     MRST          IFEQ      ' '
     C     USPØR         OREQ      'J'
     C                   EXSR      PLKMRN
     C                   END
     C     WSNR          WHENEQ    '2'
     C     MRST          ANDEQ     ' '
     C                   MOVE      '1'           *IN99
     C                   EXSR      CHGMRN
N07  C     WSNR          WHENEQ    '4'
N07  C     MRST          ANDEQ     ' '
N07  C                   MOVE      '1'           *IN99
N07  C                   EXSR      DELMRN
N02  C     WSNR          WHENEQ    'S'
N02  C     MRST          ANDEQ     ' '
N02  C                   MOVE      '1'           *IN99
N02  C                   EXSR      SNDDKI
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKKD         BEGSR
     C***********************************************
N01  C     WSCLOW        DIV       256           LINNBR
N01  C                   MVR                     POSNBR
      *

N01  C     *IN55         IFEQ      *ON
N01  C     LINNBR        IFEQ      6
N01  C     POSNBR        ANDGE     59
N01  C     POSNBR        ANDLE     61
N01  C                   CALL      'SAD061F'
N01  C                   PARM                    WSFVUE
N01  C                   PARM                    USIDT             8 0
N01  C                   Z-ADD     7             LINNBR
N01  C                   Z-ADD     48            POSNBR
N01  C                   GOTO      SLSØK
N01  C                   END
N01   *
N01  C     LINNBR        IFEQ      7
N01  C     POSNBR        ANDGE     59
N01  C     POSNBR        ANDLE     61
N01  C                   CALL      'SAD061F'
N01  C                   PARM                    WSFVIE
N01  C                   PARM                    USIDT             8 0
N01  C                   GOTO      SLSØK
N01  C                   END
N01  C                   ENDIF
      *
     C     WSLNA         IFEQ      *BLANKS
     C                   CALL      'LL033R'
     C                   PARM                    UKJED
     C                   PARM                    WSLNR
     C     WSLNR         IFNE      *BLANKS
     C                   MOVE      *BLANKS       WSLNA
     C                   END
     C                   ELSE
     C                   MOVE      'P'           UPLUKK            1
     C                   MOVE      *BLANKS       WSGN
     C                   MOVE      *BLANKS       WSMOTT
     C                   MOVE      *BLANKS       WSTRNR
     C                   CALL      'SYTL01CL'
     C                   PARM                    UPLUKK
     C                   PARM                    WSGN
     C                   PARM                    WSMOTT
     C                   PARM                    WSTRNR
     C                   END
      *
      *
N01  C     SLSØK         TAG
     C                   ENDSR
      *
     C***********************************************
     C     SØKKD2        BEGSR
     C***********************************************
     C                   CALL      'LL033R'
     C                   PARM                    UKJED
     C                   PARM                    W2LNR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTMRN        BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN40
     C                   MOVE      *ZEROS        WSMRNR
     C                   MOVE      *BLANKS       WSLNR
     C                   MOVE      *BLANKS       WSLNA
     C                   MOVE      *BLANKS       WSLAD1
     C                   MOVE      *BLANKS       WSLAD2
     C                   MOVE      *BLANKS       WSLPN
     C                   MOVE      *BLANKS       WSLAD3
     C                   MOVE      XIDAG         WSDT
     C                   MOVE      *ZEROS        WSTM
     C                   MOVE      *BLANKS       WSLREF
     C                   MOVE      *BLANKS       WSKREF
     C                   MOVE      *BLANKS       WSFT1
     C                   MOVE      *BLANKS       WSFT2
     C                   MOVE      *BLANKS       WSGN
     C                   MOVE      *BLANKS       WSMOTT
     C                   MOVE      *BLANKS       WSTRNR
     C                   MOVE      *ZEROS        WSPTDF
     C                   MOVE      *ZEROS        WSPTDT
     C                   MOVE      *BLANKS       WSPSG
N06  C                   MOVE      *ZEROS        WSVKTB
N06  C     LMRFK         SETLL     LLAGJL07
N06  C     LMRFK         READE(N)  LLAGJL07                               33
N06  C                   DOW       *IN33 = *OFF
N06  C                   IF        VAVKF0 = 0
N06  C                   EVAL      WSVKTB = WSVKTB + VAVKT0 * VAOANT * 1.1
N06  C                   ELSE
N06  C                   EVAL      WSVKTB = WSVKTB + VAVKF0 * VAOANT
N06  C                   END
N06  C     LMRFK         READE(N)  LLAGJL07                               33
N06  C                   ENDDO
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKKD
     C                   MOVE      '0'           *IN30
     C                   GOTO      STCRT
     C                   END
     C     *IN12         CABEQ     '1'           SLCRT
      *
     C                   EXSR      TESTB
     C     *IN30         CABEQ     '1'           STCRT
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGMRN        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN40
     C                   MOVE      ' '           WSNR
     C                   MOVE      MRMRNR        WSMRNR
     C     LMRFK         CHAIN(N)  LLMRF                              33
N03  C                   if        USPØR='M' and MRST <> ' '
N03  C                   MOVE      *ON           *IN56                          Protect mm
N03  C                   endif
     C                   MOVE      MRLNR         WSLNR
     C                   MOVE      MRLNA         WSLNA
     C                   MOVE      MRLAD1        WSLAD1
     C                   MOVE      MRLAD2        WSLAD2
     C                   MOVE      MRLPN         WSLPN
     C                   MOVE      MRLAD3        WSLAD3
     C                   MOVE      MRDT          WSDT
     C                   MOVE      MRTM          WSTM
     C                   MOVE      MRLREF        WSLREF
     C                   MOVE      MRKREF        WSKREF
     C                   MOVE      MRFT1         WSFT1
     C                   MOVE      MRFT2         WSFT2
     C                   MOVE      MRGN          WSGN
     C                   MOVE      MRMOTT        WSMOTT
     C                   MOVE      MRTRNR        WSTRNR
     C                   MOVE      MRPTDF        WSPTDF
     C                   MOVE      MRPTDT        WSPTDT
     C                   MOVE      MRPSG         WSPSG
N06  C                   MOVE      *ZEROS        WSVKTB
N06  C     LMRFK         SETLL     LLAGJL07
N06  C     LMRFK         READE(N)  LLAGJL07                               33
N06  C                   DOW       *IN33 = *OFF
N06  C                   IF        VAVKF0 = 0
N06  C                   EVAL      WSVKTB = WSVKTB + VAVKT0 * VAOANT * 1.1
N06  C                   ELSE
N06  C                   EVAL      WSVKTB = WSVKTB + VAVKF0 * VAOANT
N06  C                   END
N06  C     LMRFK         READE(N)  LLAGJL07                               33
N06  C                   ENDDO
      *
N01  c                   exsr      GetEUF
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
N08  C                   if        *IN10=*ON and *IN56=*ON and USPØR='M'
N08  C                   MOVE      *OFF          *IN56
N08  C                   goto      STCHG
N08  C                   endif
      *
     C     *IN04         IFEQ      '1'
N03  c     *IN56         andeq     *off
     C                   EXSR      SØKKD
     C                   MOVE      '0'           *IN30
     C                   GOTO      STCHG
     C                   END
N01
N01  C     *IN11         IFEQ      '1'
N03  c     *IN56         andeq     *off
N01  C     *IN55         IFEQ      '1'
N01   * *in55 VAR *on = Åpne + F11 en gang til = Fordel
     c                   exsr      TestFord
     c     *in30         ifeq      *off
N01  c                   exsr      FordEUF
N01  c                   move      '0'           *in55
     c                   endif
N01  c                   else
N01  c                   move      '1'           *in55
N01  c                   endif
N01  c                   goto      STCHG
N01  c                   endif
N01   * Med 55 ON = Åpne felt for EUfrakt-fordeling
N01   *             er det kun tillatt m/F11=Fordel(over) eller F12=Angre fordel Eufrakt
N01  C     *IN55         IFeq      '1'
N01  C     *IN12         IFEQ      '1'
N01  c                   move      '0'           *in55
N01  C                   SETOFF                                       308283
N01  c                   else
N01  C                   MOVE      'JOV1635'     MSGNR
N01  C                   MOVE      '1'           *IN30
N01  c                   endif
N01  c                   goto      STCHG
N01  c                   endif
      *
N03   *  spørring = UT herfra unasett om enter/F12 or whatever
N03  c     *IN56         ifeq      *ON
N03  C                   GOTO      SLCHG
N03  c                   endif
N03   *  Inn via Ops M fra logi/10 - avbrudd = hopp rett ut
N03  C     *IN12         IFEQ      '1'
N03  C     USPØR         ANDEQ     'M'
N03  C                   GOTO      SLCHG
N03  C                   END
      *
     C     *IN12         IFEQ      '1'
     C     USPØR         ANDNE     'E'
     C                   UPDATE    SUBFIL
     C                   GOTO      SLCHG
     C                   END
      *
     C                   EXSR      TESTB
     C     *IN30         CABEQ     '1'           STCHG
      *
     C     SLCHG         ENDSR
      *
N07  C***********************************************
N07  C     DELMRN        BEGSR
N07  C***********************************************
N07  C                   MOVE      '1'           *IN40
N07  C                   MOVE      ' '           WSNR
N07  C                   MOVE      MRMRNR        WSMRNR
N07  C     LMRFK         CHAIN(N)  LLMRF                              33
N07  C                   MOVE      *ON           *IN56                          Protect mm
N07  C                   MOVE      MRLNR         WSLNR
N07  C                   MOVE      MRLNA         WSLNA
N07  C                   MOVE      MRLAD1        WSLAD1
N07  C                   MOVE      MRLAD2        WSLAD2
N07  C                   MOVE      MRLPN         WSLPN
N07  C                   MOVE      MRLAD3        WSLAD3
N07  C                   MOVE      MRDT          WSDT
N07  C                   MOVE      MRTM          WSTM
N07  C                   MOVE      MRLREF        WSLREF
N07  C                   MOVE      MRKREF        WSKREF
N07  C                   MOVE      MRFT1         WSFT1
N07  C                   MOVE      MRFT2         WSFT2
N07  C                   MOVE      MRGN          WSGN
N07  C                   MOVE      MRMOTT        WSMOTT
N07  C                   MOVE      MRTRNR        WSTRNR
N07  C                   MOVE      MRPTDF        WSPTDF
N07  C                   MOVE      MRPTDT        WSPTDT
N07  C                   MOVE      MRPSG         WSPSG
N07  C                   MOVE      *ZEROS        ANTLIN           11 0
N07  C                   MOVE      *ZEROS        WSVKTB
N07  C     LMRFK         SETLL     LLAGJL07
N07  C     LMRFK         READE(N)  LLAGJL07                               33
N07  C                   DOW       *IN33 = *OFF
N07  C                   add       1             Antlin
N07  C                   IF        VAVKF0 = 0
N07  C                   EVAL      WSVKTB = WSVKTB + VAVKT0 * VAOANT * 1.1
N07  C                   ELSE
N07  C                   EVAL      WSVKTB = WSVKTB + VAVKF0 * VAOANT
N07  C                   END
N07  C     LMRFK         READE(N)  LLAGJL07                               33
N07  C                   ENDDO
N07   *
N07   *
N07  C     STDEL         TAG
N07  c                   move      *on           *in30
N07  c                   if        Antlin > 0
N07  C                   MOVE      'JOV1643'     MSGNR                          kan ikke slettes
N07  c                   else
N07  C                   MOVE      'JOV1644'     MSGNR                          Kan slettes
N07  c                   endif
N07  C                   EXFMT     VARELI
N07  C                   MOVEA     '0000000'     *IN(84)
N07  C                   MOVE      '0'           *IN30
N07   *
N07  C     *IN04         IFEQ      '1'
N07  C     *IN11         oreq      '1'
N07  C                   GOTO      STDEL
N07  C                   END
N07
N07   *
N07  C     *IN12         IFEQ      '1'
N07  C                   UPDATE    SUBFIL
N07  C                   GOTO      SLDEL
N07  C                   END
N07   * Enter tastet - tvinge de til F12 - slik at avbryt sletting er BEVISST
N07  c                   if        Antlin > 0
N07  C                   GOTO      STDEL
N07  c                   endif
N07   * Enter tastet som bekrefting av slett
N07  C     LMRFK         CHAIN     LLMRF                              33
N07  c                   move      'D'           MRST
N07  c  N33              UPDATE    LLMRFR
N07  c  N33              eval      XLNA = '*DELETED'
N07  C                   UPDATE    SUBFIL
N07   *
N07  C     SLDEL         ENDSR
N07   *
N02   ***********************************************
N02  C     SNDDKI        BEGSR
N02   ***********************************************
N02  C                   MOVE      '1'           *IN40
N02  C                   MOVE      ' '           WSNR
N02  C                   MOVE      UGRUP         UDKGRP            8
N02  C                   MOVE      UKJED         UDKPNR            8
N02  C                   MOVE      MRMRNR        UDKMRNR           5
N02  C                   MOVE      ' '           UDKFLAG           1
N02  C                   CALL      'DKI501C'
N02  C                   PARM                    UDKGRP
N02  C                   PARM                    UDKPNR
N02  C                   PARM                    UDKMRNR
N02  C                   PARM                    UDKFLAG
N02   *
N07  C                   MOVE      MRMRNR        WSMRNR
N02  C     LMRFK         CHAIN(N)  LLMRF                              33
N07  c                   eval      XLNA = '*OVERFØRT'
N02  C                   UPDATE    SUBFIL
N02   *
N02  C                   ENDSR
N02   *
N01  C***********************************************
N01  C     GetEUF        BEGSR
N01  C***********************************************
N01  c                   move      '0'           *in55                          DFT = PROTECTED
N01  C                   MOVE      *ZEROS        TOTVKT            9 3
N01  C                   MOVE      *blanks       WSFVUE
N01  C                   Move      *zeros        WSFRUE
N01  C                   MOVE      *blanks       WSFVIE
N01  C                   Move      *zeros        WSFRIE
N01   *
N01  C     LMRFK         setll     LLAGJL07
N01  C     LMRFK         reade(N)  LLAGJL07                               33
N01  C     *in33         doweq     '0'
N01  c     LAG01Key      chain(N)  LLAGJFEU                           33
N01   *
N01  C     VAVKT0        MULT      VAOANT        TMPVKT            9 3
N01  C                   ADD       TMPVKT        TOTVKT
N01  c     *in33         ifeq      '0'
N01   * frakt/val UTENFOR EU
N01  c     VAFRUE        ifgt      0
N01  c*                  add       VAFRUE        WSFRUE
N01  C                   EVAL      WSFRUE = WSFRUE + VAFRUE * VASALD
N01  c     WSFVUE        ifeq      *blanks
N01  c                   move      VAFVUE        WSFVUE
N01  c                   else
N01  c     VAFVUE        ifne      WSFVUE
N01  c                   move      'MIX'         WSFVUE
N01  c                   endif
N01  c                   endif
N01  c                   endif
N01   * frakt/val Innefor EU
N01  c     VAFRIE        ifgt      0
N01  c*                  add       VAFRIE        WSFRIE
N01  C                   EVAL      WSFRIE = WSFRIE + VAFRIE * VASALD
N01  c     WSFVIE        ifeq      *blanks
N01  c                   move      VAFVIE        WSFVIE
N01  c                   else
N01  c     VAFVIE        ifne      WSFVIE
N01  c                   move      'MIX'         WSFVIE
N01  c                   endif
N01  c                   endif
N01  c                   endif
N01  c                   endif
N01   *
N01  C     LMRFK         reade(N)  LLAGJL07                               33
N01  c                   enddo
N01   *
N01  c                   endsr
N01  C***********************************************
N01  C     FordEUF       BEGSR
N01  C***********************************************
N01  c     TotVkt        cabeq     *zeros        UtFord
N01  c     WSFVUE        cabeq     'MIX'         UtFord
N01  c     WSFVIE        cabeq     'MIX'         UtFord
N01   * Fordel EUfrakt ettar andel av vekt
N01  C     LMRFK         setll     LLAGJL07
N01  C     LMRFK         reade(N)  LLAGJL07                               33
N01  C     *in33         doweq     '0'
N01  C*    VAVKT0        MULT      VAOANT        TMPVKT            9 3
N01  C                   Z-ADD     VAVKT0        TMPVKT            9 3
N01  c     TMPVKT        Div       TOTVKT        FAKTOR           1911
N01  c     LAG01Key      chain     LLAGJFEU                           33
N01  C                   MOVE      WSFVUE        VAFVUE
N01  C     WSFRUE        MULT(H)   FAKTOR        VAFRUE
N01  C                   MOVE      WSFVIE        VAFVIE
N01  C     WSFRIE        MULT(H)   FAKTOR        VAFRIE
N01  C   33              WRITE     LLAGJEUR
N01  C  N33              UPDATE    LLAGJEUR
N01   *
N01  c     *in33         ifeq      '0'
N01  C     VAVKT0        MULT      VAOANT        TMPVKT            9 3
N01  c                   endif
N01   *
N01  C     LMRFK         reade(N)  LLAGJL07                               33
N01  c                   enddo
N01   *
N01  c     Utford        endsr
      *
     C***********************************************
     C     PLKMRN        BEGSR
     C***********************************************
     C                   MOVE      MRMRNR        UMRNR
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSLNR         IFNE      *BLANKS                                      1<-B
     C     VADRK         CHAIN     LLVADR3                            90
     C     *IN90         IFEQ      '1'                                           2<-B
     C                   MOVE      'SPL0110'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   ELSE                                                    2
     C     WSLNA         IFEQ      *BLANKS                                        3<-B
     C                   MOVE      VADRNA        WSLNA
     C                   MOVE      VADRN1        WSLAD1
     C                   MOVE      VADRN2        WSLAD2
      * NORSK POSTNR??
     C                   TESTN                   VXPNR                33
     C     *IN33         IFEQ      '1'                                               6<-B
     C     VXBLAN        ANDEQ     ' '
     C                   MOVE      VXPNR         WSLPN
     C                   MOVEL     VXSTED        WSLAD3
     C                   ELSE                                                        6
     C                   MOVEL     VADRN3        WSLAD3
     C                   END                                                         6<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSLNA         IFEQ      *BLANKS
     C                   MOVE      'YBC0660'     MSGNR
     C                   MOVE      '1'           *IN89
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSDT          IFEQ      0
     C                   MOVE      'DIV0001'     MSGNR
     C                   MOVE      '1'           *IN88
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSLREF        IFEQ      *BLANKS
     C                   MOVE      'YBC0661'     MSGNR
     C                   MOVE      '1'           *IN87
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSPTDF        IFNE      0
     C                   MOVEL     WSPTDF        XNR02             2 0
     C  N86XNR02         COMP      24                                 86  86
     C  N86              MOVE      WSPTDF        XNR02
     C  N86XNR02         COMP      60                                 86  86
     C     *IN86         IFEQ      '1'
     C                   MOVE      'YBC1881'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSPTDT        COMP      0                                    8585
     C  N85              MOVEL     WSPTDT        XNR02             2 0
     C  N85XNR02         COMP      24                                 85  85
     C  N85              MOVE      WSPTDT        XNR02
     C  N85XNR02         COMP      60                                 85  85
     C     *IN85         IFEQ      '1'
     C                   MOVE      'YBC1882'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSPSG         IFEQ      *BLANKS
     C                   MOVE      '1'           *IN84
     C                   MOVE      'YBC1868'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
      *
     C     LMRFK         CHAIN     LLMRF                              33
     C                   MOVE      WSLNR         MRLNR
     C                   MOVE      WSLNA         MRLNA
     C                   MOVE      WSLAD1        MRLAD1
     C                   MOVE      WSLAD2        MRLAD2
     C                   MOVE      WSLPN         MRLPN
     C                   MOVE      WSLAD3        MRLAD3
     C                   MOVE      WSDT          MRDT
     C                   MOVE      WSTM          MRTM
     C                   MOVE      WSLREF        MRLREF
     C                   MOVE      WSKREF        MRKREF
     C                   MOVE      WSFT1         MRFT1
     C                   MOVE      WSFT2         MRFT2
     C                   MOVE      WSGN          MRGN
     C                   MOVE      WSMOTT        MRMOTT
     C                   MOVE      WSTRNR        MRTRNR
     C                   MOVE      WSPTDF        MRPTDF
     C                   MOVE      WSPTDT        MRPTDT
     C                   MOVE      WSPSG         MRPSG
     C     *IN33         IFEQ      '0'
     C                   UPDATE    LLMRFR
     C     USPØR         CABEQ     'E'           SLTB1
N03  C     USPØR         CABEQ     'M'           SLTB1
     C                   UPDATE    SUBFIL
     C                   ELSE
     C     LMRTK         CHAIN     LLMRT                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      MTNR          MRMRNR
     C                   ADD       1             MTNR
     C                   UPDATE    LLMRTR
     C                   ELSE
     C                   Z-ADD     1             MRMRNR
     C                   Z-ADD     2             MTNR
     C                   MOVE      UGRUP         MTGRUP
     C                   MOVE      UKJED         MTKJED
     C                   WRITE     LLMRTR
     C                   END
     C                   MOVE      ' '           MRST
     C                   MOVE      UGRUP         MRGRUP
     C                   MOVE      UKJED         MRKJED
     C                   WRITE     LLMRFR
     C                   ADD       1             XRECNO
     C                   MOVE      XRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   END
      *
     C     SLTB1         ENDSR
N01   *
N01  C***********************************************
N01  C     TESTFORD      BEGSR
N01  C***********************************************
N01  C                   SETOFF                                       308283
N01   * Frakt utenfor  EU / valuta
N01  c     WSFRUE        IFNE      *zeros
N01  c     WSFVUE        orne      *blanks
N01  C                   MOVE      WSFVUE        XXVAL
N01  C     KODVAK        CHAIN     KODTVA                             83
N01  C     *IN83         IFEQ      '1'
N01  C                   MOVE      'SPV0001'     MSGNR
N01  C                   MOVE      '1'           *IN30
N01  C                   GOTO      SLTTF
N01  C                   END
N01  C                   END
N01   * Frakt innenfor EU / valuta
N01  c     WSFRIE        IFNE      *zeros
N01  c     WSFVIE        orne      *blanks
N01  c     WSFVIE        ifeq      *blanks
N01  C                   MOVE      WSFVUE        WSFVIE
N01  c                   ENDIF
N01  C                   MOVE      WSFVIE        XXVAL
N01  C     KODVAK        CHAIN     KODTVA                             82
N01  C     *IN82         IFEQ      '1'
N01  C                   MOVE      'SPV0001'     MSGNR
N01  C                   MOVE      '1'           *IN30
N01  C                   GOTO      SLTTF
N01  C                   END
N01  C                   END
N01   *
N01  C     SLTTF         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LMRTK         KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C     LMRFK         KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    WSMRNR
     C     LMR1K         KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    W2MRNR
     C     LMR1K1        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C     LMR2K         KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    W2MRNR
     C     LMR2K1        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C     LMR5K         KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    W2LNR
     C                   KFLD                    W2DT
     C     LMR5K1        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    W2LNR
     C     VADRK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    UKJED
     C                   KFLD                    WSLNR
     C     LAG01Key      KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    VAMN
N01  C     KODVAK        KLIST
N01  C                   KFLD                    KVAUNI
N01  C                   KFLD                    XXVAL             3
N01  C                   MOVE      'VA'          KVAUNI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRUP             8 0
     C                   PARM                    UKJED             8 0
     C                   PARM                    UNKJE            30
     C                   PARM                    UMRNR             5 0
     C                   PARM                    USPØR             1
      *
     C                   TIME                    WTIME
     C                   MOVE      WÅÅ           XIDAGÅ
     C                   MOVE      WMM           XIDAGM
     C                   MOVE      WDD           XIDAGD
     C                   MOVE      UMRNR         W2MRNR
     C                   MOVEA     '100'         *IN(27)
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   WRITE     WINDOWSZ
      *
     C     UMRNR         IFNE      0
     C     USPØR         ANDEQ     'E'
     C                   MOVE      UMRNR         MRMRNR
     C                   EXSR      CHGMRN
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
      *
N03   * Inn med USPØR = 5 = kall fra eksternt prog for å se)
N03  C     UMRNR         IFNE      0
N03  C     USPØR         ANDEQ     'M'
N03  C                   MOVE      UMRNR         MRMRNR
N03  C                   EXSR      CHGMRN
N03  C                   MOVE      '1'           *INLR
N03  C                   RETURN
N03  C                   END
     C                   ENDSR
      *
