      *********************************************************************
      * Oppdater overtallige og innleste kollinr
      *
CRTPG+*  CRTPGM SYSPED/STS13 MODULE(*LIBL/STS13 *LIBL/STS13CM *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     F*RIDF     UF   E           K DISK    USROPN
     FBRIDF     IF   E           K DISK    USROPN
     FHENOTW    UF A E           K DISK    USROPN
     FHENOKW3   UF   E           K DISK    USROPN
     F                                     PREFIX(W_)
     FHENOK     UF   E           K DISK    USROPN
     FHENOS     UF   E           K DISK    USROPN
     F*HENOP     UF   E           K DISK    USROPN
     FHENOP     IF   E           K DISK    USROPN
     FCUNDC03   IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *Array over adresser en vil sende mankomail til
     D MAIL            S             70    DIM(5)
      *
     D lng             s              2
     D BckGnd          s             10
     D WSUSER          s             10
     D WSPARK          s              1
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  HSDS                   1     30
     D  HSDSUS                 1     10
     D  HSDSDT                11     18
     D  HSDSTI                19     22
     D  HSDSDM                23     23
     D                 DS
     D  XXMANKO                1      3
     D  XXMANKO1               1      1
     D  XXMANKO2               2      2
     D                 DS
     D  XXTOTAL                1      3
     D  XXTOTAL1               1      1
     D  XXTOTAL2               2      2
      /copy syspeds/qrpgsrcpy,variables3
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      *
      * Parse input
     C                   exsr      Parse
     C                   EXSR      INIT
      *
     C  N60              IF        BIINN <> ' '
      *
     C     WSUSER        SETLL     HENOKW3
     C     WSUSER        READE     HENOKW3                                33
     C                   DOW       *IN33 = *OFF
     C                   IF        W_HKLNR = 0
      * Oppdater overtallige
     C                   EVAL      HTUSER = W_HKUSER
     C                   EVAL      HTID   = W_HKID
     C                   MOVE      WDT           HTDT
     C                   MOVEL     WTM           HTDTT
     C                   EVAL      HTPALL = W_HKPALL
     C                   EVAL      HTSNDN = W_HSSNDN
     C                   EVAL      HTRFA  = W_HSRFA
     C                   EVAL      HTKNK  = W_HSKNK
     C                   EVAL      HTNAK  = W_HSNAK
     C                   EVAL      HTADK1 = W_HSADK1
     C                   EVAL      HTPNRK = W_HSPNRK
     C                   EVAL      HTSTDK = W_HSSTDK
     C                   EVAL      HTAGTR = W_HSAGTR
     C                   EVAL      HTANTP = W_HSANTP
     C                   EVAL      HTANT  = W_HSANT
     C                   EVAL      HTBILN = W_HOBILN
     C                   EVAL      HTKNS  = W_HOKNS
     C                   EVAL      HTNAS  = W_HONAS
     C                   EVAL      HTADS1 = W_HOADS1
     C                   EVAL      HTPNRS = W_HOPNRS
     C                   WRITE     HENOTWR
      *
     C                   ELSE
      * Oppdater innleste
      *
      * Oppdater på HENOK
     C     HENOKKEY      CHAIN     HENOK                              34
     C                   IF        W_HKPALL = ' '
     C                   EVAL      HKSTIN = W_HKSTIN
     C                   ELSE
     C                   EVAL      HKSTIN = 'P'
     C                   END
     C                   UPDATE    HENOKR
      * HVIS kolli nå er lest, fjern evt post fra overtallig-fil
     C     HKSTIN        IFNE      ' '
     C     HKID          CHAIN     HENOTW                             34
     C     *in34         IFeq      *OFF
     c                   delete    henotwr
     c                   end
     c                   end
      * Oppdater på HENOS
     C                   IF        ((XXSNDN <> *BLANKS) AND
     C                              (XXSNDN <> W_HSSNDN))
     C     HENOSKEY      CHAIN     HENOS                              34
     C     *IN34         IFEQ      *OFF
      * Riktig antall lest - sett merke på HENOS.
     C                   IF        XXANT = XXANTIN
     C                   MOVE      'X'           HSSTIN
      * Feil antall lest (MANKO) send mankomail.
      * men ikke hvis det bare er "parkering" (oppdat det som ER skutt)
     C                   ELSE
     C     WSPARK        IFNE      'Y'
     C                   EXSR      MANKOMAIL
     C                   END
     C                   END
     C                   Z-ADD     XXANTP        HSANTP
     C                   MOVE      WDT           HSDT
     C                   MOVEL     WTM           HSDTT
      * UserID brukes som FLAGG (skyting fllført av...)
     C     WSPARK        IFNE      'Y'
     C                   MOVEL     WSUSER        HSDSUS
     C                   MOVE      WDT           HSDSDT
     C                   MOVEL     WTM           HSDSTI
     C                   MOVEL     ' '           HSDSDM
     c     XXANTIN       IFNE      XXANT
     C     XXANTIN       andne     *zeros
     C                   MOVEL     'D'           HSDSDM
     c                   end
     C                   end
     C                   UPDATE    HENOSR
     C                   end
     C                   MOVE      *ZEROS        XXANTIN           5 0
     C                   MOVE      *ZEROS        XXANTP            5 0
     C                   END
     C                   IF        W_HKSTIN = 'X'
     C                   ADD       1             XXANTIN
     C                   END
     C                   MOVE      W_HKLNR       XXHKLNR           7 0
     C                   MOVE      W_HKLNR2      XXHKLNR2          3 0
     C                   MOVE      W_HSSNDN      XXSNDN           20
     C                   MOVE      W_HSANT       XXANT             5 0
     C                   IF        W_HKPALL <> ' '
     C                   ADD       1             XXANTP
     C                   END
      *
     C                   END
      *
     C                   DELETE    HENOKWR
     C     WSUSER        READE     HENOKW3                                33
     C  N33              ENDDO
      *
      * Oppdater på HENOS (siste sending i leseloopen)
     C                   IF        XXSNDN <> *BLANKS
     C     HENOSKEY      CHAIN     HENOS                              34
     C     *IN34         IFEQ      *OFF
     C                   IF        XXANT = XXANTIN
     C                   MOVE      'X'           HSSTIN
      * Feil antall lest (MANKO) send mankomail.
     C                   ELSE
     C     WSPARK        IFNE      'Y'
     C                   EXSR      MANKOMAIL
     C                   END
     C                   END
     C                   Z-ADD     XXANTP        HSANTP
     C                   MOVE      WDT           HSDT
     C                   MOVEL     WTM           HSDTT
      * UserID brukes som FLAGG (skyting fllført av...)
     C     WSPARK        IFNE      'Y'
     C                   MOVEL     WSUSER        HSDSUS
     C                   MOVE      WDT           HSDSDT
     C                   MOVEL     WTM           HSDSTI
     C                   MOVEL     ' '           HSDSDM
     c     XXANTIN       IFNE      XXANT
     C     XXANTIN       andne     *zeros
     C                   MOVEL     'D'           HSDSDM
     c                   end
     C                   end
     C                   UPDATE    HENOSR
     C                   end
     C                   end
      *
     C                   END
      *
     C**N60              EVAL      BIINN = ' '
     C**N60              UPDATE    BRIDFR
      *
     C                   EXSR      VISSTARTB
      *
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Send mail vedr manko
      *=====================================================================******
     C     MANKOMAIL     begsr
      * Interne bruker som skal ha mail (max 2)
     c                   clear                   mail
     c                   move      *zeros        mn                2 0
     C     MAILKEYI      SETLL     CUNDC03
     C     MAILKEYI      READE     CUNDC03                                35
     C                   DOW       *IN35 = *OFF
     c                   add       1             mn
     c                   movel     CEMAIL        mail(mn)
     c     mn            ifeq      1
     c                   movel     CPHONE        XXAVID           10
     c                   movel     CMOBIL        XXAVADR          10
     C                   MOVEL     CTYPE         XXSIGN           30
     c                   end
     c     mn            cabeq     2             extmail
     C     MAILKEYI      READE     CUNDC03                                35
     C  N35              ENDDO
      *
      * Eksterne brukere (avsender)som skal ha mail (max 5) samlet
     c     extmail       tag
     C     HENOPKEY      CHAIN     HENOP                              35
      *
     C     MAILKEYE      SETLL     CUNDC03
     C     MAILKEYE      READE     CUNDC03                                35
     C                   DOW       *IN35 = *OFF
     c                   add       1             mn
     c                   movel     CEMAIL        mail(mn)
     c     mn            cabeq     5             utmail
     C     MAILKEYE      READE     CUNDC03                                35
     C  N35              ENDDO
     c     utmail        tag
      *
     C     MAIL(1)       IFNE      *BLANKS
     C     XXAVID        IFEQ      *BLANKS
     c                   movel     '*CURRENT'    XXAVID
     c                   move      *BLANKS       XXAVADR
     c                   END
      *
     C                   MOVEL     HONAS         XXAVS            30
     C                   MOVEL     HSSNDN        XXSNR            20
     C                   MOVEL     HSNAK         XXNAV            30
     C                   MOVEL     HSADK1        XXADR            30
     C                   MOVEL     HSPNRK        XXADR2           30
     C                   MOVE      HSSTDK        XXADR2
     C     XXANT         SUB       XXANTIN       XXMANK            3 0
     C                   MOVE      XXMANK        XXMANKO           3
     C     XXMANKO1      IFEQ      '0'
     C                   MOVE      ' '           XXMANKO1
     C     XXMANKO2      IFEQ      '0'
     C                   MOVE      ' '           XXMANKO2
     C                   END
     C                   END
     C                   MOVE      XXANT         XXTOTAL           3
     C     XXTOTAL1      IFEQ      '0'
     C                   MOVE      ' '           XXTOTAL1
     C     XXTOTAL2      IFEQ      '0'
     C                   MOVE      ' '           XXTOTAL2
     C                   END
     C                   END
     C                   MOVEL     FIFT          XXFIRM           30
     C                   MOVEL     MAIL(1)       XXINT1           70
     C                   MOVEL     MAIL(2)       XXINT2           70
     C                   MOVEL     MAIL(3)       XXINT3           70
     C                   MOVEL     MAIL(4)       XXINT4           70
     C                   MOVEL     MAIL(5)       XXINT5           70
     C                   CALLB     'STS13CM'
     C                   PARM                    XXAVS
     C                   PARM                    XXSNR
     C                   PARM                    XXNAV
     C                   PARM                    XXADR
     C                   PARM                    XXADR2
     C                   PARM                    XXMANKO
     C                   PARM                    XXTOTAL
     C                   PARM                    XXFIRM
     C                   PARM                    XXSIGN
     C                   PARM                    XXAVID
     C                   PARM                    XXAVADR
     C                   PARM                    XXINT1
     C                   PARM                    XXINT2
     C                   PARM                    XXINT3
     C                   PARM                    XXINT4
     C                   PARM                    XXINT5
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      WSPARK  = zhbgetvar('PARK')
      *
     C                   endsr
      *=====================================================================******
      *  VIS STARTBILDE (menu)
      *=====================================================================******
     C     VISSTARTB     begsr
     C                   exsr      SetSkl01B
     C                   exsr      SetTop
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS02'
      *
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHTMLvar('besk':BIBESK)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      * Color for text, background, links, visited links, active links
     C                   exsr      SetColor
      *
     C                   callp     updHTMLvar('FEILTXT':spaces)
      *
     C                   endsr
      *=====================================================================
      * Color for text, background, links, visited links, active links
      *=====================================================================
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     HENOKKEY      KLIST
     C                   KFLD                    W_HKLNR
     C                   KFLD                    W_HKLNR2
     C                   KFLD                    W_HKLNR3
     C     HENOSKEY      KLIST
     C                   KFLD                    XXHKLNR
     C                   KFLD                    XXHKLNR2
     C     HENOPKEY      KLIST
     C                   KFLD                    XXHKLNR
     C     MAILKEYI      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    YYCONTA
     C                   KFLD                    BIKUNR
     C     MAILKEYE      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    YYCONTA
     C                   KFLD                    HOKNS
     C                   MOVEL     '*MANKO'      YYCONTA          30
      *
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      HENOKW3                              50
     C                   open      HENOTW                               50
     C                   open      HENOK                                50
     C                   open      HENOS                                50
     C                   open      HENOP                                50
     C                   open      CUNDC03                              50
     C                   open      FIRM                                 50
     C     BIFIRM        CHAIN     FIRM
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     HENOKW3
     C                   close     HENOTW
     C                   close     HENOK
     C                   close     HENOS
     C                   close     HENOP
     C                   close     CUNDC03
     C                   close     FIRM
     C                   eval      *inlr = *on
      *
     C                   endsr
