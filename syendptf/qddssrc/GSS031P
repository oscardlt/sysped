********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FRITEKST BESTEMMES AV KODE
10172 * 02 SH  PTF10 HØYDE FOR UTENLANDSK ADRESSE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 FEIL POSISJON PÅ BUNN
11XXX * 04 JOV PTF11 RETTELSER I PTF 3  + BOXLENGDE REDUSERT
********************************************************************
     A          R GSS031CO
     A            STRICO        70      6  1
     A          R GSS031P1
     A            KFMT1         75      7  1
     A            KFMT2         75      8  1
     A N28        STRI02        70     10  1
     A N50                             11 25'MANIFEST / PROFORMA INVOICE'
     A  77                                  FNTCHRSET(C0N400B0 +
     A                                      T1V10277)
     A  50N51                          11 25'MANIFEST / PROFORMA INVOICE (COPY)'
     A  77                                  FNTCHRSET(C0N400B0 +
     A                                      T1V10277)
     A  50 51                          11 25'MANIFEST / PROFORMA INVOICE'
     A  77                                  FNTCHRSET(C0N400B0 +
     A                                      T1V10277)
     A N28        STRI01        70     12  1
     A            TRMFT1        70     13  2
     A            TRMFT2        70     14  2
     A                                 15  2'HAWBNO.:'
     A            XHAWB         15     15 16
     A            FINM          30     15 65
     A                                 16  2'ARRIVAL DATE/TIME:'
     A            XDTL           6  0  16 35EDTWRD('  .  .  ')
     A                                 16 47'/'
     A            TRTML          4  0  16 49EDTWRD('  :  ')
     A            FIAD1         30     16 65
     A                                 17  2'DEPARTURE DATE   :'
     A            XDTS           6  0  17 35EDTWRD('  .  .  ')
     A            FIAD2         30     17 65
     A                                 18  2'FINAL DESTINATION:'
     A            XSTDT         30     18 31
     A            FIAD3         30     18 65
     A N40                             19  2'MASTER OF:'
     A N40        CBNAB         30     19 19
     A  40                             19  2'LOCATION FROM/TO:'
     A  40        TRLOCF         5     19 31
     A  40                             19 36'/'
     A  40        TRLOCT         5     19 37
     A                                 20  2'IN TRANSIT'
     A                                 20 65'PAGE:'
     A            XSIDE          3  0  20 86
     A                                 21  2'C/O AGENT:'
     A            TRAD1         30     21 19
     A                                 21 65'REF.:'
     A            TRTNO         11  0  21 74EDTWRD('0    /       ')
     A                                 21 89'/'
     A            HESG           3     21 91
     A            TRAD2         30     22 19
     A                                 22 65'ORIGIN:'
     A            XSTDF         20     22 77
     A            TRAD3         30     23 19
     A            TRAD4         30     24 19
     A            TRAD5         30     25 19
     A            TRAD6         30     26 19
     A          R GSS031PÆ
     A N28        KFMT1         75      8  1
     A N28        KFMT2         75      9  1
     A N28        STRI02        70     10 31
     A                                 10 31'COMMERCIAL INVOICE NO:'
     A  77                                  FNTCHRSET(C0N400B0 +
     A                                      T1V10277)
     A            TRTNO         11  0  10 74EDTWRD('0    /       ')
     A                                      HIGHLIGHT
     A          R GSS031PØ
     A                                      CPI(10)
     A N28        STRI01        70     10  1
     A            TRMFT1        70     13  2
     A            TRMFT2        70     14  2
     A            XXNA          30     14 74
     A                                 15  2'HAWBNO.:'
     A            XHAWB         15     15 16
     A            XXAD1         30     15 74
     A                                 16  2'ARRIVAL DATE/TIME:'
     A            XDTL           6  0  16 35EDTWRD('  .  .  ')
     A                                 16 47'/'
     A            TRTML          4  0  16 49EDTWRD('  :  ')
     A            XXAD2         30     16 74
     A                                 17  2'DEPARTURE DATE   :'
     A            XDTS           6  0  17 35EDTWRD('  .  .  ')
     A            XXAD3         30     17 74
     A                                 18  2'FINAL DESTINATION:'
     A            XSTDT         30     18 31
     A                                 18 74'Phone:'
     A            XXTLF         15     18 83
     A N40                             19  2'MASTER OF:'
     A N40        CBNAB         30     19 19
     A  40                             19  2'LOCATION FROM/TO:'
     A  40        TRLOCF         5     19 31
     A  40                             19 36'/'
     A  40        TRLOCT         5     19 37
     A                                 19 74'Fax:'
     A            XXFAX         15     19 83
     A                                 20  2'IN TRANSIT'
     A                                 20 74'PAGE:'
     A            XSIDE          3  0  20 83
     A                                 21  2'C/O AGENT:'
     A            TRAD1         30     21 19
     A                                 21 74'REF.:'
     A            TRTNO         11  0  21 83EDTWRD('0    /       ')
     A                                 21 98'/'
     A            HESG           3     21100
     A            TRAD2         30     22 19
SXX  A*                                22 75'ORIGIN:'
NXX  A                                 22 74'ORIGIN:'
SXX  A*           XSTDF         20     22 87
NXX  A            XSTDF         20     22 83
     A            TRAD3         30     23 19
     A            TRAD4         30     24 19
     A            TRAD5         30     25 19
     A            TRAD6         30     26 19
     A          R GSS031PXP
      * PARAMETRE P1 P2 P3 P4 P5 P1,P2=STARTPUNKT P3,P4=MOTSATT HJØRNT
N04  A*                                     BOX(4.35 0.05 4.85 7.85 0.02)
N04  A                                      BOX(4.35 0.05 4.85 7.80 0.02)
     A          R GSS031PX
     A                                      CPI(15)
     A N28        STRI03        70     27 31
     A N77                             27  2'-------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      ------------------'
     A                                 28  2'SUPPLIER'
S17  A* 40                             28 32' ORDERNO'
S17  A* 40                             28 40' P. ORDER NO.'
S17  A*N40                             28 32' P. ORDER NO.'
S17  A*                                28 53' COL NO.'
S17  A*                                28 61' CONTENTS'
N17  A  40                             28 27' ORDERNO'
N17  A  40                             28 35' P. ORDER NO.'
N17  A N40                             28 27' P. ORDER NO.'
N17  A                                 28 48' PACK #.'
N17  A                                 28 56' CONTENTS'
     A                                 28 90' PCS'
     A                                 28 96' WEIGHT'
     A                                 28103' CUSTOMS VALUE'
     A N77                             29  2'-------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      ------------------'
     A                                 30  2' '
     A          R GSS031P2                  SPACEB(1)
     A                                      CPI(15)
S17  A*           ORSNM         30         2
S17  A* 40        ORONO          7  0     33EDTCDE(Z)
S17  A* 40        XCONO         12        41
S17  A*N40        POCONO        20        33
S17  A*           XCLNF          3  0     54EDTCDE(Z)
S17  A*N32        XCLNT          3  0     58EDTCDE(Z)
S17  A*           XDSC          25        62
N17  A            XSNM          24         2
N17  A  40        ORONO          7  0     27EDTCDE(Z)
N17  A  40        XCONO         12        36
N17  A N40        POCONO        20        28
N17  A            XCLNF          3  0     49EDTCDE(Z)
N17  A N32        XCLNT          3  0     53EDTCDE(Z)
N17  A            ORDSC         30        58
     A            ORCLI          5  0     88EDTCDE(Z)
     A            ORWT           7  1     94EDTCDE(4)
     A            ORAMT         11  2    102EDTCDE(3)
     A            ORCURC         3       115
     A          R GSS031P3                  SPACEB(1)
     A                                      CPI(15)
     A N40        POCONO        20        33
     A  40        XCONO2        20        41
     A            GDDSC         55        62
     A          R GSS031P4                  SPACEB(2)
     A                                      CPI(15)
S17  A*                                   63'TOTAL FOR THIS COLLI:'
N17  A                                    62'TOTAL FOR THIS PACK(S):'
     A            XCLTD          5  0     88EDTCDE(Z)
     A            XVKTD          7  1     94EDTCDE(4)
     A            XBLTD         11  2    102EDTCDE(4)
     A            UCUR           3       115SPACEA(1)
     A          R GSS031P5
     A                                      CPI(10)
     A N28        STRI01        70     60  2
S01  A*           TRFT1         70     60  2
N01  A            XTRFT1        70     60  2
     A N77                             61  2'-------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      -----------------'
     A                                 62  2'TOTAL WEIGHT:'
     A            XVKTOT         7  1  62 20EDTCDE(4)
S17  A*                                62 40'TOTAL COLLI:'
N17  A                                 62 40'TOTAL PACKS:'
     A            XCLTOT         5  0  62 54EDTCDE(Z)
     A                                 62 86'TOTAL VALUE:'
S04  A*           XBLTOT        11  2  62102EDTCDE(4)
S04  A*           UCUR           3     62115
N04  A            XBLTOT        11  2  62101EDTCDE(4)
N04  A            UCUR           3     62114
     A N77                             63  2'-------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      -------------------------+
     A                                      ------------------'
     A            KNAVN         30     64  2
S03  A*           ADR1          30     64 33
S04  A*           ADR1          30     64 86
N04  A            ADR1          30     64 48
S03  A*           XIDAGP        11     64 86
N03  A            XIDAGP        11     64 94
     A            ADR2          30     65  2
S02  A*           POSTNR         4  0  65 33
S02  A*           ADR3          24     65 38
S03  A*           ADR3NY        30     65 38
N03  A            ADR3NY        30     65 48
S03  A*           KOSFNV        16     65 86
N03  A            KOSFNV        16     65 94
     A          R GSS031P5P
      * PARAMETRE P1 P2 P3 P4 P5 P1,P2=STARTPUNKT P3,P4=MOTSATT HJØRNT
S04  A*                                     BOX(10.1 0.05 10.45 7.85 0.02)
S04  A*                                     BOX(10.45 0.05 10.9 7.85 0.02)
N04  A                                      BOX(10.1 0.05 10.45 7.80 0.02)
N04  A                                      BOX(10.45 0.05 10.9 7.80 0.02)
     A          R GSS031P6
     A                                      CPI(10)
     A N28        STRI01        70     64  2
     A                                 65 70'CONTINUOUS'
     A          R GSS031P7
     A                                      CPI(10)
     A                                   110'CONTINUOUS'
     A                                      SPACEA(2)
     A          R GSS031P8                  SPACEB(1)
     A                                      CPI(15)
     A                                     2'SHIP:'
     A            ORBCD          5         8
     A            CBNAB         30        14
     A          R GSS031P9                  SPACEB(1)
     A                                      CPI(15)
     A            GDDSC         55        62
     A          R GSS031PA                  SPACEB(1)
     A                                      CPI(15)
     A            XXFT          30        62
      *
     A          R GSS031PB                  SPACEB(1)
     A                                      CPI(15)
     A                                    33'CONTAINER:'
     A            OLMN04        31        44
