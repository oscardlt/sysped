      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR05PO MODULE(*LIBL/TJETUR05PO *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
     FBILLOF    UF   E           K DISK    USROPN
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D USER            S             10
     D WSAVD           S              4
     D WBAVD           S              4  0
     D WSOPD           S              7
     D WBOPD           S              7  0
     D XPOSA           S              3A
     D XPOSN           S              3  0
     D WSBLORR         S              1
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE

     C                   EXSR      INIT

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬wsopd¬ : ¬'+%trim(WSOPD)+'¬, '
        +'¬wspos¬ : ¬'+%trim(XPOSA)+'¬, '
        +'¬wsblorr¬ : ¬'+%trim(WSBLORR)+'¬, '
        +'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   EVAL      JSONstring ='"setposonorder" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Find/send lines
     C     ERROR         IFEQ      *BLANKS
     C                   EXSR      SROPDPOS
     C                   ENDIF

     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   eval      user     = zhbgetvar('user')
     C                   eval      WSAVD    = zhbgetvar('WSAVD')
     C                   EVAL      WBAVD    = c2n2(WSAVD)
     C                   eval      WSOPD    = zhbgetvar('WSOPD')
     C                   EVAL      WBOPD    = c2n2(WSOPD)
     C                   eval      XPOSA    = ZhbGetVar('WSPOS')
     C                   EVAL      XPOSN    = c2n2(XPOSA)
     C                   eval      WSBLORR  = ZhbGetVar('WSBLORR')
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     BILLOF
     C                   MOVE      *ON           *INLR
     C                   return
     C                   ENDSR
      *=====================================================================******
      *  SROPDPOS
      *=====================================================================******
     C     SROPDPOS      BEGSR
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF
N10  C                   IF        XPOSN > 0
     C                   MOVEL     XPOSN         HEPOS1
N10  C                   ELSE
N10  C                   MOVE      *BLANKS       HEPOS1
N10  C                   ENDIF
     C                   UPDATE    HEADFR
     C     HEADFK        CHAIN     BILLOF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVEL     WSBLORR       BLCH
     C                   UPDATE    BILLOFR
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      HEADF
     C  N50              OPEN      BILLOF

     C     HEADFK        KLIST
     C                   KFLD                    WBAVD
     C                   KFLD                    WBOPD
     C                   ENDSR
