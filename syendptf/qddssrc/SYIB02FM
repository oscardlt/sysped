     A*%%TS  SD  20181207  061718  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20181002  193711  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A            KONTO     R        O  5  6REFFLD(KONTR/KONTO *LIBL/KONTF)
     A            KONTNA    R        O  5 12REFFLD(KONTR/KONTNA *LIBL/KONTF)
     A            MVAO      R        O  5 45REFFLD(KONTR/MVAO *LIBL/KONTF)
     A            MVAOK     R        O  5 48REFFLD(KONTR/MVAOK *LIBL/KONTF)
     A            KONTY     R        O  5 61REFFLD(KONTR/KONTY *LIBL/KONTF)
     A            STEDK     R        O  5 66REFFLD(KONTR/STEDK *LIBL/KONTF)
     A            BARERK    R        O  5 72REFFLD(KONTR/BARERK *LIBL/KONTF)
     A            PROSJK    R        O  5 78REFFLD(KONTR/PROSJK *LIBL/KONTF)
     A            AKTKOD    R        O  5 87REFFLD(KONTR/AKTKOD *LIBL/KONTF)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20181002  190749  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYIB02'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(130 SHU1899 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 17MSGCON(040 SHU1900 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A*%%TS  SD  20181207  061718  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSNTO     R        B  2  6REFFLD(KONTR/KONTO *LIBL/KONTF)
     A  90                                  DSPATR(RI)
     A            WSNTNA    R        B  2 12REFFLD(KONTR/KONTNA *LIBL/KONTF)
     A                                      CHECK(LC)
     A            WSAO      R        B  2 45REFFLD(KONTR/MVAO *LIBL/KONTF)
     A            WSAOK     R        B  2 48REFFLD(KONTR/MVAOK *LIBL/KONTF)
     A            WSNTY     R        B  2 61REFFLD(KONTR/KONTY *LIBL/KONTF)
     A            WSEDK     R        B  2 66REFFLD(KONTR/STEDK *LIBL/KONTF)
     A            WSRERK    R        B  2 72REFFLD(KONTR/BARERK *LIBL/KONTF)
     A            WSOSJK    R        B  2 78REFFLD(KONTR/PROSJK *LIBL/KONTF)
     A            WSTKOD    R        B  2 87REFFLD(KONTR/AKTKOD *LIBL/KONTF)
     A                                      VALUES('A' 'I')
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(130 SHU1899 *LIBL/SYMSGF)
     A          R VAREL3
     A*%%TS  SD  20181002  193711  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            KONTO     R        O  2  6REFFLD(KONTR/KONTO *LIBL/KONTF)
     A            KONTNA    R        O  2 12REFFLD(KONTR/KONTNA *LIBL/KONTF)
     A            MVAO      R        O  2 45REFFLD(KONTR/MVAO *LIBL/KONTF)
     A            MVAOK     R        O  2 48REFFLD(KONTR/MVAOK *LIBL/KONTF)
     A            KONTY     R        O  2 61REFFLD(KONTR/KONTY *LIBL/KONTF)
     A            STEDK     R        O  2 66REFFLD(KONTR/STEDK *LIBL/KONTF)
     A            BARERK    R        O  2 72REFFLD(KONTR/BARERK *LIBL/KONTF)
     A            PROSJK    R        O  2 78REFFLD(KONTR/PROSJK *LIBL/KONTF)
     A            AKTKOD    R        O  2 87REFFLD(KONTR/AKTKOD *LIBL/KONTF)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(130 SHU1899 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R NYVARE
     A*%%TS  SD  20181002  190749  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYIB02'
     A                                      COLOR(BLU)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 17MSGCON(040 SHU1900 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(130 SHU1899 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
