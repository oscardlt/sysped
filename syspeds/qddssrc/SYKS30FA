********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01     PTF11 FEILMELDING DERSOM......
11XXX * 02 YBC PTF11 NY VERDI PÅ PARAMETER
********************************************************************
      *
E01   * ALLE FELT UNNTATT JOBBKØ FLYTTET 1 LINJE NED
N01   * NYE FELT I TOPPEN FOR STANDARD RAP.MAL, F13 LAGRE MAL.
      *
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      *
     A          R BILDE01
     A                                      CF03(03 'Avbryt')
     A                                      CF04(04 'SØK')
     A                                      CF10(10 'BEKREFT OVERSKR')
     A                                      CF12(03)
     A                                      CF13(13 'LAGRE MAL')
     A                                      CF14(14 'OVERSKRIV MAL')
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  4'SYSPED/SYKS30   '
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(035 SHU0544 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A                                  2  4MSGCON(020 JOV1231 *LIBL/SYMSGF)
     A            WSRNAV        10A  B  2 26
     A  96                                  DSPATR(RI)
     A            WSBESK        30A  B  2 37
     A  96                                  DSPATR(RI)
     A                                  3  4MSGCON(020 SHU0552 *LIBL/SYMSGF)
     A            WSOPTU         1A  B  3 26
     A                                  3 31MSGCON(016 SHU0553 *LIBL/SYMSGF)
     A                                  3 49MSGCON(032 DIV2586 *LIBL/SYMSGF)
     A                                  4  4MSGCON(015 SHU0545 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 31MSGCON(039 DIV2587 *LIBL/SYMSGF)
     A            WSGLKJ         1A  B  4 71
     A                                  4 73'(J/N)'
     A                                  5  4MSGCON(020 JOV1067 *LIBL/SYMSGF)
     A            WSFALI         1A  B  5 26
     A                                  5 31'(J/N)'
     A                                  5 44MSGCON(015 SHU1575 *LIBL/SYMSGF)
     A                                  6  4MSGCON(020 SHU0547 *LIBL/SYMSGF)
     A            WSUFLI         1A  B  6 26
     A                                  6 31'(J/N)'
     A                                  7  4MSGCON(020 SHU0554 *LIBL/SYMSGF)
     A            WSFKOS         1A  B  7 26
     A                                  7 31'(J/N)'
     A                                  8  4MSGCON(020 SHU0555 *LIBL/SYMSGF)
     A            WSBKOS         1A  B  8 26
     A                                  8 31'(J/N)'
     A                                  9  4MSGCON(020 SHU0558 *LIBL/SYMSGF)
     A            WSILIN         1A  B  9 26
     A                                  9 31'(J/N/F=KunFerdige/U=Kun Uferdige)'
     A                                 10  4MSGCON(020 SHU0557 *LIBL/SYMSGF)
     A            WSAVRS         1A  B 10 26
     A                                 10 31'(J/N)'
     A                                 11  4MSGCON(020 JOV1065 *LIBL/SYMSGF)
     A            WSAVSL         1A  B 11 26VALUES('A' 'B' 'N')
     A                                 11 36MSGCON(040 JOV1066 *LIBL/SYMSGF)
     A                                 12  4MSGCON(020 SHU0548 *LIBL/SYMSGF)
     A            WSREGT         7S 0B 12 26
     A                                 12 36MSGCON(020 JOV0896 *LIBL/SYMSGF)
     A            WSDBGT         5Y 2B 12 57EDTCDE(M)
     A                                 12 65'Or/And.:'
     A            WSORAN         1A  B 12 74
     A                                 12 76'(O/A)'
     A                                 13  4MSGCON(020 SHU0549 *LIBL/SYMSGF)
     A            WSRELT         7S 0B 13 26
     A                                 13 36MSGCON(020 JOV0897 *LIBL/SYMSGF)
     A            WSDBLT         5Y 2B 13 57EDTCDE(M)
     A                                 14  4MSGCON(020 JOV1349 *LIBL/SYMSGF)
     A            WSPEF          4Y 0B 14 26
     A  99                                  DSPATR(RI)
     A            WSPEFD         2Y 0B 14 31EDTCDE(4)
     A                                 14 36MSGCON(004 JOV1350 *LIBL/SYMSGF)
     A            WSPET          4Y 0B 14 41
     A  99                                  DSPATR(RI)
     A            WSPETD         2Y 0B 14 46EDTCDE(4)
     A                                 14 50MSGCON(030 JOV1351 *LIBL/SYMSGF)
     A                                 15  4MSGCON(020 SHU0551 *LIBL/SYMSGF)
     A            WSAVDF         4Y 0B 15 26EDTCDE(4)
     A            WSAVDT         4Y 0B 15 31EDTCDE(4)
     A                                 15 36MSGCON(013 SHU1154 *LIBL/SYMSGF)
     A            WSSG           3A  B 15 50
     A                                 16  4MSGCON(020 JOV0899 *LIBL/SYMSGF)
     A            WSAVDG         7A  B 16 26
     A                                 16 36MSGCON(026 DIV2588 *LIBL/SYMSGF)
     A            WSOTYP         2A  B 16 63
     A                                 16 67'(SG/PG..)'
     A                                 17  4MSGCON(020 SHU0556 *LIBL/SYMSGF)
     A            WSGEIO         1A  B 17 26
     A            WSGE01         3A  B 17 28
     A            WSGE02         3A  B 17 32
     A            WSGE03         3A  B 17 36
     A            WSGE04         3A  B 17 40
     A            WSGE05         3A  B 17 44
     A            WSGE06         3A  B 17 48
     A            WSGE07         3A  B 17 52
     A            WSGE08         3A  B 17 56
     A            WSGE09         3A  B 17 60
     A            WSGE10         3A  B 17 64
     A            WSGE11         3A  B 17 68
     A            WSGE12         3A  B 17 72
     A            WSGE13         3A  B 17 76
     A                                 18  4MSGCON(020 JOV0906 *LIBL/SYMSGF)
     A            WSSTIO         1A  B 18 26
     A            WSSTGR        10A  B 18 28
     A                                 18 39'(ABC...)'
     A                                 19  4MSGCON(020 JOV0364 *LIBL/SYMSGF)
S02  A*           WSFIL          1A  B 19 26VALUES(' ' 'J' 'Y' 'F' 'N')
S02  A*                                19 31MSGCON(043 JOV1038 *LIBL/SYMSGF)
N02  A            WSFIL          1A  B 19 26VALUES(' ' 'J' 'Y' 'F' 'N' 'E')
N02  A                                 19 31MSGCON(043 YBC2019 *LIBL/SYMSGF)
     A                                 20  4MSGCON(020 JOV1037 *LIBL/SYMSGF)
     A            WSLIBR        10A  B 20 26
     A  98                                  DSPATR(RI)
     A                                 20 37'/'
     A            WSFILE        10A  B 20 39
     A  97                                  DSPATR(RI)
     A                                 21  4MSGCON(020 JOV1062 *LIBL/SYMSGF)
     A            WSBAVD         1A  B 21 26VALUES('J' 'N' 'B' ' ')
     A                                 21 31MSGCON(020 JOV1063 *LIBL/SYMSGF)
     A                                 22  4MSGCON(020 DIV4060 *LIBL/SYMSGF)
     A            WSJOBQ         1A  B 22 26
     A                                 22 31'(J/N)'
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 JOV1232 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
