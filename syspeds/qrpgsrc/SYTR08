      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE FERJEPRISAVTALER             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97       ***
      **************************************************************
      *
     FREIS      UF A E           K DISK
     FTURER     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYTR08FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D*BRUKER FELTET RESJ TIL Å MERKE SLETTET
     D  RESJ                   1      8
     D  XDLT                   1      8
     C*
     C     STINI         TAG
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C*FLYTTER VARIABLE FRA W2 TIL YS FOR SØKING  KUN KEYFELTER
     C                   MOVE      W2SJ          YSSJ
     C                   MOVE      W2NBR         YSNBR
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSI1        SETLL     REIS
     C     START1        TAG
     C     KODRED        READE(N)  REIS                                   94
     C*FLYTTER VARIABLE FRA WS TIL YS FOR SØKING  KUN KEYFELTER
     C  N94              MOVE      WSSJ          YSSJ
     C  N94              MOVE      WSNBR         YSNBR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     KODRED        READE(N)  REIS                                   94
     C*FLYTTER VARIABLE FRA WS TIL YS FOR SØKING  KUN KEYFELTER
     C  N94              MOVE      WSSJ          YSSJ
     C  N94              MOVE      WSNBR         YSNBR
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03KALL          IFNE      'J'
     C                   MOVE      *ZEROS        REAVD
     C                   MOVE      *ZEROS        REPRO
     C                   GOTO      STINI
     C                   ELSE
     C                   GOTO      SLUTT
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSI3        SETLL     REIS
     C  N94KODRED        READE(N)  REIS                                   94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C* GENERELLE TESTER VED BÅDE ENDRING OG NY
     C*
     C     VALKEY        CHAIN     VALUL01                            70
     C   70              MOVE      'SPV0001'     MSGNR
     C   70              SETON                                        30
     C   70              Z-ADD     022           POSNBR
     C   30              GOTO      SLTB
     C*
     C     WSSJ          COMP      *BLANKS                                90
     C  N90WSNBR         COMP      *BLANKS                                90
     C  N90WSBEL         COMP      *ZEROS                                 90
     C   90              MOVE      'SPG0011'     MSGNR
     C* GENERELLE TESTER VED NY
     C  N40              DO
     C  N90KODSIK        CHAIN     REIS                               33
     C  N90
     CANN33              DO
     C                   UPDATE    REISR
     C                   SETON                                        90
     C                   MOVE      'SPR0102'     MSGNR
     C                   END
     C                   END
     C   90              SETON                                        30
     C   90
     CAN 40              Z-ADD     022           POSNBR
     C   90
     CANN40              Z-ADD     04            POSNBR
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     REIS                               33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    REISR
     C   33              WRITE     REISR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNRO         IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C     XDLT          COMP      '*DELETE*'                             98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNRO         IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNRO
     C     XDLT          COMP      '*DELETE*'                             98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSID        CHAIN     REIS                               33
     C  N33              DELETE    REISR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C* BLANKER ALLE FILVARIABLE
     C                   MOVE      *BLANKS       RESJ
     C                   MOVE      *BLANKS       RENBR
     C                   MOVE      *BLANKS       REVAL
     C                   MOVE      *ZEROS        REBEL
     C                   MOVE      *BLANKS       RETXT
     C*
     C                   MOVEL     '*DELETE*'    XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C* BLANKER ALLE VARIABLE TIL BILDE VARELI
     C                   MOVE      *BLANKS       WSSJ
     C                   MOVE      *BLANKS       WSNBR
     C                   MOVE      *BLANKS       WSVAL
     C                   MOVE      *ZEROS        WSBEL
     C                   MOVE      *BLANKS       WSTXT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C* FLYTTER FRA FIL TIL VARELI
     C                   MOVE      RESJ          WSSJ
     C                   MOVE      RENBR         WSNBR
     C                   MOVE      REVAL         WSVAL
     C                   MOVE      REBEL         WSBEL
     C                   MOVE      RETXT         WSTXT
     C*
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     22            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C* FIX KEY IF NOT FOUND
     C   33              DO
     C                   MOVE      WSSJ          RESJ
     C                   MOVE      WSNBR         RENBR
     C                   END
     C*
     C* FIXES VARIABLES
     C                   MOVE      WSVAL         REVAL
     C                   MOVE      WSBEL         REBEL
     C                   MOVE      WSTXT         RETXT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    RESJ          YSSJ
     C     *LIKE         DEFINE    RENBR         YSNBR
     C     KODSIK        KLIST
     C                   KFLD                    REAVD
     C                   KFLD                    REPRO
     C                   KFLD                    WSSJ
     C                   KFLD                    WSNBR
     C     KODSID        KLIST
     C                   KFLD                    REAVD
     C                   KFLD                    REPRO
     C                   KFLD                    RESJ
     C                   KFLD                    RENBR
     C     KODSI1        KLIST
     C                   KFLD                    REAVD
     C                   KFLD                    REPRO
     C                   KFLD                    W2SJ
     C                   KFLD                    W2NBR
     C     KODSI3        KLIST
     C                   KFLD                    REAVD
     C                   KFLD                    REPRO
     C                   KFLD                    YSSJ
     C                   KFLD                    YSNBR
     C*
     C* VED SUBFILREGISTRERING FAST NØKKELBEGREP FRA SIDE 1 ELLER KALL
     C     KODRED        KLIST
     C                   KFLD                    REAVD
     C                   KFLD                    REPRO
     C     VALKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSVAL
     C                   READ      FIRM                                   33
     C*
     C     *ENTRY        PLIST
     C                   PARM                    REAVD
     C                   PARM                    REPRO
     C* VED KALL FRA CL ER AVDELING/TUR=0    TUREN FINNES IKKE
     C     KODRED        CHAIN     TURER                              33
     C  N33              MOVE      'J'           KALL              1
     C  N33              GOTO      UTI
     C*
     C     STI           TAG
     C                   EXFMT     BILDE1
     C   03              GOTO      UTI
     C     KODRED        CHAIN     TURER                              44
     C   44              MOVE      'SPT0002'     MSGNR
     C   44              GOTO      STI
     C*
     C     UTI           ENDSR
     C*
