     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/TVINE)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
     A          R TVI99DA                   SFL
     A            ZOP            1A  I  7  3VALUES(' ' '2' '4' '6')
     A            E9705     R        O  7  6
     A            E4440     R        O  7 11
     A          R TVI99DB                   SFLCTL(TVI99DA)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Gå ut')
     A                                      CF05(05 'Frisk opp')
     A                                      CF06(06 'Lage Ny')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 33'Funksjonsfeil'
     A                                      COLOR(WHT)
     A                                  1 75'TVI99D'
     A                                  3  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  4  4'2=Endre   4=Slette'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  4 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  6  2'Alt'
     A                                      COLOR(WHT)
     A                                  6  6'Kode'
     A                                      COLOR(WHT)
     A                                  6 11'Tollvesenets tekst'
     A                                      COLOR(WHT)
     A          R TVI99DC
     A                                      OVERLAY
     A                                 22  2'F3=Avslutt   F5=Frisk opp   F6=Lag-
     A                                      e ny   F12=Forrige'
     A                                      COLOR(BLU)
     A          R TVI99DD
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A            Å9705     R        B 21  6REFFLD(E9705)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A                                      RANGE('000' '999')
     A            Å4440     R        B 21 11REFFLD(E4440)
     A  41                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 22  2'F12=Forrige'
     A                                      COLOR(BLU)
     A          R TVI99DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
