     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('ORI0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søke')
     A                                      CF05(05 'HENTE TEKSTER')
     A                                      CF12(12 'AVBRYT program')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYSJ04'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  1 20MSGCON(40 DIV3911 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  6  2MSGCON(30 DIV3926 SYMSGF)
     A            WSAVD          4Y 0B  6 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(030 DIV3912 *LIBL/SYMSGF)
     A            WSKNR          8Y 0B  8 40
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(030 DIV3913 *LIBL/SYMSGF)
     A            WSTR           4   B 10 40
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                 12  2MSGCON(030 DIV3914 *LIBL/SYMSGF)
     A            WSLK           2   B 12 40
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                 14  2MSGCON(030 DIV3915 *LIBL/SYMSGF)
     A            WSDT           6Y 0B 14 40
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(030 DIV3916 *LIBL/SYMSGF)
     A            WSKD           3   B 16 40
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV3917 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CF12(12 'Avbryt')
     A                                  1  3'SYSPED/SYSJ04'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  1 20MSGCON(40 DIV3911 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(004 DIV3918 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2  7
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 12MSGCON(006 DIV3918 *LIBL/SYMSGF)
     A            WSKNR          8Y 0O  2 19
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNAR         20   O  2 28
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 49MSGCON(008 DIV3919 *LIBL/SYMSGF)
     A            WSTR           4   O  2 58
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSNATR        18   O  2 63
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(008 DIV3920 *LIBL/SYMSGF)
     A            WSLK           2   O  3 11
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSNALK        20   O  3 20
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 45MSGCON(008 DIV3921 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  3 54
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 65MSGCON(008 DIV3922 *LIBL/SYMSGF)
     A            WSKD           3   O  3 74
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
      *
     A            WSTX1         62   B  5 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX2         62   B  6 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX3         62   B  7 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX4         62   B  8 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX5         62   B  9 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX6         62   B 10 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX7         62   B 11 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX8         62   B 12 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX9         62   B 13 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX10        62   B 14 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX11        62   B 15 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX12        62   B 16 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX13        62   B 17 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX14        62   B 18 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX15        62   B 19 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX16        62   B 20 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX17        62   B 21 10DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTX18        62   B 22 10DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
