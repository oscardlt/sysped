/*TATT BORT FRONMGN VED TYPE F         */
/*SYSTEMA ENDRING I OVRPRTF VED TYPE=D */
/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
/*  KODE D = DUPLEX TOSIDIG UTSKRIFT                      */
             PGM        PARM(&AWB &IN08 &KOWFET)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FLYF) TYPE(*DEC) LEN(3 0) VALUE(017)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&OPD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&LOP) TYPE(*CHAR) LEN(3)
             DCL        VAR(&AWB) TYPE(*LGL) LEN(1) /* MAWB-liste +
                          type. */
             DCL        VAR(&IN08) TYPE(*LGL) LEN(1) /* Antall +
                          utskrifter. */
             DCL        VAR(&KOWFET) TYPE(*CHAR) LEN(1)
    /* */
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til merkelapp-1. */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til merkelapp-1. */
             DCL        VAR(&PRT3) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til merkelapp-2. */
             DCL        VAR(&PT3) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til merkelapp-2. */
             DCL        VAR(&PRT17) TYPE(*CHAR) LEN(10) /* +
                          Printernavn til flyfraktbrev-eksport. */
             DCL        VAR(&PT17) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til flyflaktbrev-eksport. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('aw')
             DCL        VAR(&ATYPE2) TYPE(*CHAR) LEN(2) VALUE('al')
             DCL        VAR(&ATYPE3) TYPE(*CHAR) LEN(2) VALUE('ax')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
 RTVTIME:    RTVJOBA    USER(&AUSER)
    /* */
             RTVDTAARA  DTAARA(*LDA (601 10)) RTNVAR(&PRT2)
             RTVDTAARA  DTAARA(*LDA (611 10)) RTNVAR(&PT2)
             RTVDTAARA  DTAARA(*LDA (621 10)) RTNVAR(&PRT3)
             RTVDTAARA  DTAARA(*LDA (631 10)) RTNVAR(&PT3)
             RTVDTAARA  DTAARA(*LDA (661 10)) RTNVAR(&PRT17)
             RTVDTAARA  DTAARA(*LDA (671 10)) RTNVAR(&PT17)
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
/*V10        RTVDTAARA  DTAARA(*LDA (25 5)) RTNVAR(&OPD)   */
/*N10*/      RTVDTAARA  DTAARA(*LDA (82 7)) RTNVAR(&OPD)
    /* */
    /*  TESTER LASER OG LPI                   */
    /* */
             CALL       PGM(GETPRN) PARM(&AVD &FLYF &PNAVN &FORM +
                        &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
    /* */
    /* SKRIVER UT INNENLANDS FLYFRAKTBREV */
             OVRPRTF    FILE(MERKL1) OUTQ(&PRT2) PAGESIZE(18 50) +
                          CPI(10) OVRFLW(18) FORMTYPE(&PT2)
             OVRPRTF    FILE(MERKL2) OUTQ(&PRT3) PAGESIZE(24 50) +
                          CPI(10) OVRFLW(24) FORMTYPE(&PT3)
             IF         COND(&LASER *NE 'P') THEN(DO)
             IF         COND(&KOWFET = 'J') THEN(DO)
             OVRPRTF    FILE(SYEF14P) DEVTYPE(*IPDS) PAGESIZE(96 80) +
                          LPI(8) CPI(10) OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT17) +
                          FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
             ELSE       CMD(DO)
             OVRPRTF    FILE(SYEF14P) DEVTYPE(*SCS) PAGESIZE(96 80) +
                          LPI(8) CPI(10) OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT17) +
                          FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
             ENDDO
             IF         COND(&LASER *EQ 'P') THEN(DO)
             OVRPRTF    FILE(SYEF14P) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) LPI(8) CPI(10) OVRFLW(96) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          PRTQLTY(*DRAFT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT17) FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
 /*N01 - EGEN AFP-STYRING */
             IF         COND(&LASER *EQ 'F') THEN(DO)
             OVRPRTF    FILE(SYEF14P) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) LPI(8) CPI(10) +
                          OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT17) +
                          FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
 /*N01 - EGEN AFP-STYRING DUPLEX */
             IF         COND(&LASER *EQ 'D') THEN(DO)
             OVRPRTF    FILE(SYEF14P) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) LPI(8) CPI(10) FRONTMGN(0 0.10) +
                          OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          DUPLEX(*YES) FRONTOVL(&LOGO) +
                          BACKOVL(AWBBAK 0.2 0 *CONSTANT) +
                          OUTQ(&PRT17) FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
             OVRDBF     FILE(DOKOTH) FRCRATIO(1)
             CALL       PGM(SYEF14) PARM(&IN08 &PRANT &AVD &OPD &LOP &LASER)
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT +
                          'flyfraktbrev-eksport er klare til utskrift +
                          på skriver ' *BCAT &PRT17 *BCAT '.') +
                          MSGTYPE(*INFO)
    /* */
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE2 &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE3 &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             ENDPGM
