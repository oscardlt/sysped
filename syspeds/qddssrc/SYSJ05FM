     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2')
     A            INAVD          4  0H
     A            INKNR          8  0H
     A            INTR           4A  H
     A            INLK           2A  O  5  4
EV5  A            INDT           8  0H
     A            WSDT           6  0O  5  7EDTCDE(4)
     A            INKD           3A  O  5 14
     A            INTXT         62A  O  5 18COLOR(YLW)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ05'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3911 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(004 DIV3927 *LIBL/SYMSGF)
     A            W2AVD          4Y 0O  2  7
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 12MSGCON(006 DIV3918 *LIBL/SYMSGF)
     A            W2KNR          8Y 0O  2 19
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNAR         20   O  2 28
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 49MSGCON(008 DIV3919 *LIBL/SYMSGF)
     A            W2TR           4   O  2 58
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSNATR        18   O  2 61
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(79 DIV3923 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA04(04 'SØKING')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 18MSGCON(56 DIV3925 SYMSGF)
     A                                  2  2MSGCON(15 DIV3924 SYMSGF)
     A            W2AVD          4  0B  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KNR          8  0B  2 29DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TR           4A  B  2 43DSPATR(UL)
     A            W2LK           2A  B  2 52DSPATR(UL)
     A            W2DT           6  0B  2 57DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KD           3A  B  2 67DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ05'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 20MSGCON(40 DIV3911 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(004 DIV3927 *LIBL/SYMSGF)
     A            W2AVD          4Y 0O  2  7
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 12MSGCON(006 DIV3918 *LIBL/SYMSGF)
     A            W2KNR          8Y 0O  2 19
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNAR         20   O  2 28
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 49MSGCON(008 DIV3919 *LIBL/SYMSGF)
     A            W2TR           4   O  2 58
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSNATR        18   O  2 63
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(79 DIV3923 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
