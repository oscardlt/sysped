      *********************************************************************
      * ARBEID MED ORDRE
      * PROGRAM BEHANDLER INPUT FRA ESOVERS22
      *
CRTPG+*  CRTPGM SYSPED/ESOVERS22 MODULE(*LIBL/ESOVERS22 *LIBL/ESOVERS23
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FBLV2      UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D SubmPressed     s              1
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D i               s             10i 0
     D TB              S              1    DIM(7)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/ESOVERS23')
     D FEIL01EN        C                   CONST('Freetext can not be blank.')
     D FEIL01          C                   CONST('Fritekst må oppgis.')
      *
     D r               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80
     D XXNY            S              1
     D WSAVD           S              4
     D BSAVD           S              4  0
     D WSOPD           S              7
     D BSOPD           S              7  0
     D WSLI            S              3
     D BSLI            S              3  0
     D                 DS
     D WSFT1                   1     51
     D WSFT2                  52    102
     D WSFT3                 103    153
     D WSFT4                 154    204
     D FT                      1    204    DIM(4)
     D                 DS
     D XXREF                   1     24
     D XXAVD                   1      4
     D XXOPD                   5     11
     D XXLI                   12     14
     D XXUSER                 15     24
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *------------------
      * Retrieve number of boats to be shown
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
      * Show the requested boats
     C                   IF        ItemCount = 0
     C                   eval      SernoC = zhbgetvar('xxref')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   eval      BSAVD   = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   eval      BSOPD   = c2n2(WSOPD)
     C                   eval      WSLI    = zhbgetvar('WSLI')
     C                   eval      BSLI    = c2n2(WSLI)
     C                   eval      XXNY    = zhbgetvar('XXNY')
     C                   ELSE
     C                   eval      i = 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   eval      WSUSER  = zhbgetvar('USER':i)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName':i)
     C                   MOVEL     SERNOC        XXREF
     C                   eval      BSAVD   = c2n2(XXAVD)
     C                   eval      BSOPD   = c2n2(XXOPD)
     C                   eval      BSLI    = c2n2(XXLI)
     C                   MOVE      XXLI          BVLI
     C                   END
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   SELECT
     C                   WHEN      XXNY = 'J'
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
     C                   WHEN      XXNY = 'E'
     C                   MOVE      *BLANKS       FT
     C                   MOVE      *ZEROS        XN                1 0
     C     BLV2K         CHAIN(N)  BLV2                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVE      BVFT          FT(XN)
     C     BLV2K         READE     BLV2                                   33
     C                   ENDDO
     C                   OTHER
     C                   EXSR      ARBLINJE
     C                   ENDSL
      *
     C                   CALLB     PGM2
     C                   PARM                    BSAVD
     C                   PARM                    BSOPD
     C                   PARM                    BSLI
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFARG
     C                   PARM                    FT
     C                   PARM                    FMELDNR
     C                   PARM                    FEILMELDING
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   IF        UINN = 'N'
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   END
      *
     C                   close     BLV2
     C                   close     BRIDF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSFT1   = zhbgetvar('WSFT1')
     C                   eval      WSFT2   = zhbgetvar('WSFT2')
     C                   eval      WSFT3   = zhbgetvar('WSFT3')
     C                   eval      WSFT4   = zhbgetvar('WSFT4')
     C                   eval      XXNY    = zhbgetvar('XXNY')

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      BLV2                                 50
     C                   open      BRPARF                               50
     C     BLV2K         klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   kfld                    BSLI
     c     BLV2KSIST     klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   KFLD                    BVLISIST
     c     BLV2K2        klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     c     BLV2K3        klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   kfld                    BSLI
     C                   kfld                    XN
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C     *LIKE         DEFINE    BVLI          BVLISIST
     C                   MOVE      *BLANKS       FMELDNR           2
     C                   MOVE      *BLANKS       FEILMELDING
     C                   Z-ADD     999           BVLISIST
      *
     C                   endsr
      *=====================================================================
      *  ARBEID MED LINJE
      *=====================================================================
     C     ARBLINJE      BEGSR
      * TEST INNGÅENDE DATA
     C                   EXSR      TESTB
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   EXSR      OPDLINJE
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
      * TEST FRITEKST
     C                   IF        WSFT1 = *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEIL01EN      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL01        FEILMELDING
     C                   END
     C                   MOVEL     '01'          FMELDNR
     C                   MOVE      *ON           *IN33
     C                   GOTO      SLTESTB
     C                   END
      *
     C     SLTESTB       ENDSR
      *
      *=====================================================================
      *  Oppdater linje
      *=====================================================================
     C     OPDLINJE      begsr
      *
     C                   IF        BSLI = 0
     C     BLV2KSIST     SETLL     BLV2
     C     BLV2K2        READPE(N) BLV2                                   33
     C                   IF        *IN33 = *OFF
     C                   ADD       1             BVLI
     C                   ELSE
     C                   Z-ADD     1             BVLI
     C                   ENDIF
     C                   MOVE      BSAVD         BVAVD
     C                   MOVE      BSOPD         BVOPD
     C                   EVAL      BSLI = BVLI
     C                   END
      *
     C     1             DO        4             XN                1 0
     C     BLV2K3        CHAIN     BLV2                               33
     C                   SELECT
     C                   WHEN      *IN33 AND FT(XN) <> *BLANKS
     C                   EVAL      BVLI2  = XN
     C                   EVAL      BVFT   = FT(XN)
     C                   EVAL      BVSORT = BVLI * 10
     C                   WRITE     BLV2R
     C                   WHEN      *IN33 = *OFF AND FT(XN) <> *BLANKS
     C                   EVAL      BVFT   = FT(XN)
     C                   UPDATE    BLV2R
     C                   WHEN      *IN33 = *OFF AND FT(XN) = *BLANKS
     C                   DELETE    BLV2R
     C                   ENDSL
     C                   ENDDO
      *
     C                   MOVE      *ZEROS        BSLI
     C                   MOVE      *BLANKS       FT
      *
     C                   endsr
      *
