      * UTGÅENDE IFCSUM - startprogram
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 ikke kjør dersom turnr = 0
********************************************************************
     FTURER     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FECMSG     IF   E           K DISK
     FFIRM      IF   E             DISK
     FEDIC      UF   E             DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  W1004                  1     20
     D  WUAVD                  1      4  0
     D  WUPRO                  5     12  0
     D  WUKNA                 13     20  0
     D  WUMN                  21     29  0
     D  WUMNA                 21     29
     D                 DS
     D  W1004B                 1     20
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C     *IN41         CASEQ     '1'           SRB
     C                   END
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WAVD              4
     C                   PARM                    WPRO              8
     C                   PARM                    WKNA              8
     C                   PARM                    WBRUDD            1
     C                   MOVE      WAVD          WUAVD
     C                   MOVE      WPRO          WUPRO
     C                   MOVE      WKNA          WUKNA
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    ECSR          WCSR
     C     *LIKE         DEFINE    EC0065        WC0065
     C                   MOVE      'S'           WCSR
     C                   MOVEL     'IFCSUM'      WC0065
     C                   READ      FIRM                                   51
     C     *LIKE         DEFINE    UMN           WMN
      * DEF AV KEYS
     C     TURKEY        KLIST
     C                   KFLD                    WUAVD
     C                   KFLD                    WUPRO
     C     TRAKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNT2
     C     KODAKE        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WUAVD
     C                   MOVE      'A'           KOAUNI
     C     KODAKE        CHAIN     KODTA                              33
     C   33              MOVE      ' '           KOAIE
     C                   MOVE      'N'           INLAND            1
     C     KOAIE         IFEQ      '1'
     C     KOAIE         OREQ      '2'
     C                   MOVE      'J'           INLAND            1
     C                   END
      * SKAL/BØR MED TIDEN VÆRE LAGRET PÅ TUR/AGENT-NIVÅ
     C                   MOVEL     '40270123'    W1004B
     C     ECMKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WUKNA
     C                   KFLD                    WCSR
     C                   KFLD                    WC0065
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________________
      * Retreive trip information
      *""""""""""""""""""""""""""
N01  C     WUPRO         CABEQ     *zeros        SRA9
     C     TURKEY        CHAIN     TURER                              51
     C     *IN51         CABEQ     '1'           SRA9
      * HVIS BEDT OM TRANSPORTØR-EDI, HENT KNR FRA TURENS TRANSPORTØR
     C     WBRUDD        IFEQ      'T'
     C     TRAKEY        CHAIN     TRAN                               51
     C     *IN51         CABEQ     '1'           SRA9
     C                   MOVE      VMTRKU        WUKNA
     C                   END
      *_________________________________
      * Send IFCSUM ? and wich release ?
      *"""""""""""""""""""""""""""""""""
     C     ECMKEY        CHAIN     ECMSG                              51
     C     *IN51         CABEQ     '1'           SRA9
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     '1'           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   MOVE      '1'           *IN41
     C     SRA9          ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      SRBA
     C     WANTO         IFGT      0
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      TVINM0
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                   SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C     INLAND        IFEQ      'J'
     C                   MOVEL     'E303R_'      WPGM              9
     C                   ELSE
     C                   MOVEL     'E302R_'      WPGM              9
     C                   END
     C                   MOVE      EC0054        WPGM
     C                   Z-ADD     WMN           WUMN
     C                   Z-ADD     0             WANTO             5 0
     C                   CALL      WPGM
     C                   PARM                    WAVD
     C                   PARM                    WPRO
     C                   PARM                    WKNA
     C                   PARM                    WUMNA
     C                   PARM                    WANTO
     C                   PARM                    WBRUDD
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     EC0065        U0065
     C                   MOVEL     '1'           U0052
     C                   MOVEL     EC0054        U0054
     C                   MOVEL     'UN'          U0051
     C     INLAND        IFNE      'J'
     C                   MOVEL     'NODI  '      U0057
     C                   END
     C                   MOVEL     '621'         U1001
     C                   MOVEL     W1004         U1004
     C     INLAND        IFEQ      'J'
     C**** SENERE....      MOVELW1004B    U1004
     C                   END
     C                   MOVEL     U0062A        U0062B
     C     EC0054        IFEQ      '93A'
     C                   MOVEL     'S'           U0052
     C     INLAND        IFEQ      'J'
     C                   MOVEL     '700'         U1001
     C                   ELSE
     C                   MOVEL     '787'         U1001
     C                   END
     C                   ENDIF
     C                   WRITE     UNHR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TVINM0                                              TVINM0/
      *////////////////////////////////////////////////////////////
     C     TVINM0        BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVE      '1'           M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   Z-ADD     WUAVD         MAVD
     C                   WRITE     EDIMR
     C                   ENDSR
