********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10038 * 01 CB  PTF10 Extra for SEND EXTENDED PASV
10XXX * 02 CB  PTF10 Send direkte fra IFS
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 03 JOV PTF11 Mottaker NO009101 er Schenker - krav om navnestruktur
10XXX *        PTF11 OBS! bytter bruk av IIDS med S0004 - GRUMS BAKERST I PARAMETER IIDS ....
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 04 JOV PTF12 Mulighet for avvikende mappe på Meldingstype-nivå (K2 m fl krever det)
12XXX * 05 YBC PTF12 Hent fil fra Google Cloud
12XXX * 06 YBC PTF12 Test om fil ligger i /asp/tmp
12XXX * 07 YBC PTF12 Sjekk om nedlastet fil fra Google er låst.
********************************************************************
N05  FFIRM      IF   E           K DISK
N05  FFIRMGOOG  IF   E           K DISK
     FEDII      IF   E           K DISK
     FTELLGE    UF A E           K DISK
N02  FEDIS      IF   E           K DISK
N04  FEDIM6     IF   E           K DISK
N04  FEDIIAM    IF   E           K DISK
     FEDI07C3R  O  A F  256        DISK
N03  D                 DS
N03  D  XULTID                 1     14  0
N03  D  XULTM                  1      6
N03  D  XULDD                  7      8
N03  D  XULMM                  9     10
N03  D  XULÅÅ                 11     14
N02  D A               S              1    DIM(256)
N02  C     *LIKE         DEFINE    SSN           WSSN
N02  C     *LIKE         DEFINE    SIFS          WSIFS
N04  C     AvMapKey      klist
N04  C                   kfld                    IID
N04  C                   kfld                    M0065
      * INPUT PARAMETER
     C     *ENTRY        PLIST
     C                   PARM                    LIB              10
     C                   PARM                    FILE             10
     C                   PARM                    MBR              10
     C                   PARM                    IIDS             35
     C                   PARM                    IID              35
     C                   PARM                    INTCHN
     C                   PARM                    IFS               1
N02  C                   PARM                    WSSN
N05   *
N05  C     *LOVAL        SETLL     FIRM
N05  C                   READ      FIRM                                   33
N05  C     FIFIRM        CHAIN     FIRMGOOG                           33
N05  C                   IF        *IN33 = *OFF
N05  C                   MOVE      'J'           XGC               1
N05  C                   END
      * Hent ID
     C     IID           CHAIN     EDII                               20
N02  C     WSSN          CHAIN     EDIS                               20
N04   * via først og beste melding fin meldingstye (kan ikke være blandet)
N04  C     WSSN          CHAIN     EDIM6                              20
N04  c                   if        M0065 <> *blanks
N04  c     AvMapKey      CHAIN     EDIIAM
N04  C                   endif
      * Via IFS ? Dette ordnes utefor dette programet i egen cl.
     C     INTCRD        IFEQ      '*IFS'
     C                   EVAL      IFS='Y'
     C                   GOTO      SLUTT
     C                   ELSE
     C                   EVAL      IFS='N'
     C                   ENDIF
      */////////////////////////
      *  Velg kommunikasjonsmåte
      */////////////////////////
      * EDB Stormaskin
     C     INTCHN        IFEQ      'IBMFTP'
     C                   EXSR      HAMAR
     C                   GOTO      SLUTT
     C                   ENDIF
      * EBCDIC
     C     INEBCDIC      IFEQ      'Y'
     C     INEBCDIC      OREQ      'J'
     C                   EXSR      EBCDIC
     C                   GOTO      SLUTT
     C                   ENDIF
      * ASCII
     C     INASCII       IFEQ      'Y'
     C     INASCII       OREQ      'J'
     C                   EXSR      ASCII
     C                   GOTO      SLUTT
     C                   ENDIF
      * Ellers nå i en overgang uten markering så er det ASCII
     C                   EXSR      ASCII
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  EBCDIC                                            EBCDIC /
      *////////////////////////////////////////////////////////////
     C     EBCDIC        BEGSR
      * USERID AND PASSWORD RECORD
     C                   MOVEL(P)  INTCUS        DATA            256
     C                   CAT       INTCPW:1      DATA
     C                   EXCEPT    AWRK
      * SENDPASV
N01  C                   SELECT
N01  C     INSPASV       WHENEQ    'Y'
N01  C     INSPASV       OREQ      'J'
N01  C                   MOVEL(P)  'sendpasv 0'  DATA
N01  C                   EXCEPT    AWRK
N01  C     INSPASV       WHENEQ    'X'
N01  C                   MOVEL(P)  'sendepsv 0'  DATA
N01  C                   EXCEPT    AWRK
N01  C                   ENDSL
      * EBCDIC
     C                   MOVEL(P)  'ebcdic'      DATA
     C                   EXCEPT    AWRK
      * CD
     C     INTCRDT       IFNE      *BLANKS
     C                   MOVEL(P)  'cd'          DATA
     C                   CAT       INTCRDT:1     DATA
     C                   EXCEPT    AWRK
     C                   ELSE
     C     INTCRD        IFNE      *BLANKS
     C                   MOVEL(P)  'cd'          DATA
     C                   CAT       INTCRD:1      DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C                   ENDIF
      * Kommando før put
     C     INCMDF1       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDF1       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDF2       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDF2       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDF3       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDF3       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
      * PUT COMMAND RECORD
     C                   MOVEL(P)  'PUT'         DATA
     C                   CAT       'QTEMP/Q.Q':1 DATA
      *
     C     INTCRF        IFEQ      *BLANKS
     C                   CAT       MBR:1         DATA
     C                   ELSE
     C                   CAT       INTCRF:1      DATA
     C                   ENDIF
     C                   EXCEPT    AWRK
      * Eventuelt rename
     C     INTCRDT       IFNE      *BLANKS
     C                   MOVEL(P)  'ren'         DATA
     C                   CAT       MBR:1         DATA
     C                   CAT       INTCRD:1      DATA
     C                   CAT       '/':0         DATA
     C                   CAT       MBR:0         DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
      * Kommando etter put
     C     INCMDE1       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDE1       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDE2       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDE2       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDE3       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDE3       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
      * QUIT COMMAND RECORD
     C                   MOVEL(P)  'quit'        DATA
     C                   EXCEPT    AWRK
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  ASCII                                              ASCII /
      *////////////////////////////////////////////////////////////
     C     ASCII         BEGSR
      * USERID AND PASSWORD RECORD
     C                   MOVEL(P)  INTCUS        DATA            256
     C                   CAT       INTCPW:1      DATA
     C                   EXCEPT    AWRK
      * SENDPASV
N01  C                   SELECT
N01  C     INSPASV       WHENEQ    'Y'
N01  C     INSPASV       OREQ      'J'
N01  C                   MOVEL(P)  'sendpasv 0'  DATA
N01  C                   EXCEPT    AWRK
N01  C     INSPASV       WHENEQ    'X'
N01  C                   MOVEL(P)  'sendepsv 0'  DATA
N01  C                   EXCEPT    AWRK
N01  C                   ENDSL
      * BIN
     C     INBIN         IFEQ      'Y'
     C     INBIN         OREQ      'J'
     C                   MOVEL(P)  'bin'         DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
      * NAMEFMT
     C                   MOVEL(P)  'NAMEFMT 1'   DATA
     C                   EXCEPT    AWRK
      * CD
     C     INTCRDT       IFNE      *BLANKS
     C                   MOVEL(P)  'cd'          DATA
     C                   CAT       INTCRDT:1     DATA
     C                   EXCEPT    AWRK
     C                   ELSE
     C     INTCRD        IFNE      *BLANKS
     C                   MOVEL(P)  'cd'          DATA
     C                   CAT       INTCRD:1      DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C                   ENDIF
      * Kommando før put
     C     INCMDF1       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDF1       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDF2       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDF2       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDF3       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDF3       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
N02   * EBCDIC eller IFS ?
N02  C                   IF        FILE='*IFS'
N02  C                   EXSR      SRIFS
N02  C                   ELSE
      * PUT COMMAND RECORD
     C                   MOVEL(P)  'PUT'         DATA
     C                   CAT       '/':1         DATA
     C                   CAT       LIB:0         DATA
     C                   CAT       '/':0         DATA
     C                   CAT       FILE:0        DATA
     C                   CAT       '/':0         DATA
     C                   CAT       MBR:0         DATA
      *
     C     INTCRF        IFEQ      *BLANKS
     C                   CAT       MBR:1         DATA
     C                   ELSE
     C                   CAT       INTCRF:1      DATA
     C                   ENDIF
     C                   EXCEPT    AWRK
      * Eventuelt rename
     C     INTCRDT       IFNE      *BLANKS
     C                   MOVEL(P)  'ren'         DATA
     C                   CAT       MBR:1         DATA
     C                   CAT       INTCRD:1      DATA
     C                   CAT       '/':0         DATA
     C                   IF        IID = '102976949757'
S03  C*                  CAT       IIDS:0        DATA
S03  C                   CAT       S0004:0       DATA
     C                   CAT       '_':0         DATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   CAT       WTM:0         DATA
     C                   CAT       '_':0         DATA
     C     'EDIPOST '    CHAIN     TELLGE                             33
     C                   IF        *IN33
     C                   EVAL      GECO = 'EDIPOST'
     C                   EVAL      GENO = 1001
     C                   WRITE     TELLGER
     C                   MOVE      '1000'        XAN04             4
     C                   ELSE
     C                   MOVE      GENO          XAN04
     C                   IF        GENO = 9999
     C                   Z-ADD     1000          GENO
     C                   ELSE
     C                   ADD       1             GENO
     C                   END
     C                   UPDATE    TELLGER
     C                   END
     C                   CAT       XAN04:0       DATA
     C                   ELSE
N03   * Schenker skal ha fast filnavn-struktur  (og de skal  alltid via TEMP-mappe )
N03   *   struktur: ediwebmmddhhmmss.NODINUMR   eks:  ediweb0410134012.NO032001
N03  C     INID          IFEQ      'NO009101'
N03  C                   TIME                    XULTID
N03  C                   CAT       'ediweb':0    DATA
N03  C                   CAT       XULMM:0       DATA
N03  C                   CAT       XULDD:0       DATA
N03  C                   CAT       XULTM:0       DATA
N03  C                   CAT       '.':0         DATA
N03  C                   CAT       S0004:0       DATA
N03  C                   ELSE
      * helt normalt... Ren mbr -> mbr
     C                   CAT       MBR:0         DATA
N03  C                   END
     C                   END
     C                   EXCEPT    AWRK
     C                   ENDIF
N02  C                   ENDIF                                                  IFS
      * Kommando etter put
     C     INCMDE1       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDE1       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDE2       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDE2       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
     C     INCMDE3       IFNE      *BLANKS
     C                   MOVEL(P)  INCMDE3       DATA
     C                   EXCEPT    AWRK
     C                   ENDIF
      * QUIT COMMAND RECORD
     C                   MOVEL(P)  'quit'        DATA
     C                   EXCEPT    AWRK
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  HAMAR                                              HAMAR /
      *////////////////////////////////////////////////////////////
     C     HAMAR         BEGSR
      * USERID AND PASSWORD RECORD
     C                   MOVEL(P)  INTCUS        DATA
     C                   CAT       INTCPW:1      DATA
     C                   EXCEPT    AWRK
      * CD COMMAND RECORD
     C                   MOVEL(P)  'CD ..'       DATA
     C                   EXCEPT    AWRK
      * EBCDIC
     C                   MOVEL(P)  'EBCDIC'      DATA
     C                   EXCEPT    AWRK
      * PUT COMMAND RECORD
     C                   EVAL      DATA='PUT QTEMP/Q.Q NI1122P.SDDXXTT'
     C     IID           IFEQ      'NODNB101'
     C                   CAT       'K.':0        DATA
     C                   ELSE
     C                   CAT       'T.':0        DATA
     C                   ENDIF
S03  C*                  CAT       IIDS:0        DATA
N03  C                   CAT       S0004:0       DATA
     C                   CAT       '.':0         DATA
     C                   CAT       MBR:0         DATA
     C                   EXCEPT    AWRK
      * QUIT COMMAND RECORD
     C                   MOVEL(P)  'quit'        DATA
     C                   EXCEPT    AWRK
     C                   ENDSR
N02   *////////////////////////////////////////////////////////////
N02   *  IFS                                                SRIFS /
N02   *////////////////////////////////////////////////////////////
N02  C     SRIFS         BEGSR
N05   *
N05  C                   IF        XGC = 'J'
N05  C     '.pdf'        SCAN      SIFS                                   33
N05  C  N33'.PDF'        SCAN      SIFS                                   33
N05  C  N33'.msg'        SCAN      SIFS                                   33
N05  C  N33'.doc'        SCAN      SIFS                                   33
N05  C  N33'.docx'       SCAN      SIFS                                   33
N05  C  N33'.xls'        SCAN      SIFS                                   33
N05  C  N33'.xlsx'       SCAN      SIFS                                   33
N05x1C  N33'.csv'        SCAN      SIFS                                   33
N05  C                   IF        *IN33
N05  C                   Z-ADD     2             XPOS1             3 0
N05  C                   Z-ADD     0             XPOS2             3 0
N05  C                   Z-ADD     0             XPOS3             3 0
N05  C                   DOW       *IN33
N05  C     '/'           SCAN      SIFS:XPOS1    XPOS2                    33
N05  C   33              EVAL      XPOS1 = XPOS2 + 1
N05  C   33              MOVE      XPOS2         XPOS3
N05  C                   ENDDO
N06   *
N06  C                   EVAL      GOTMPF100 = %TRIM(GOTMPF)
N06  C                             + %SUBST(SIFS:XPOS3+1:60)
N06  C                   CALL      'E093C3'
N06  C                   PARM                    GOTMPF100
N06  C                   PARM                    OK                1
N06   *
N06  C                   IF        OK = 'J'
N06  C                   EVAL      SIFS = GOTMPF100
N06  C                   ELSE
N05  c                   eval      gotmpf100=gotmpf
N05  c                   eval      arlenke=%SUBST(SIFS:XPOS3+1:60)
N05  c                   eval      newfile=ARLENKE
N05  c                   call      'ARC99WSD1'
N05  c                   parm                    gobuid           10
N05  c                   parm                    gotmpf100       100
N05  c                   parm                    arlenke          60
N05  c                   parm                    NewFile          60
N05  c                   parm                    uok               1
N05  C                   IF        UOK = 'J'
N05  C                   EVAL      SIFS = %TRIM(GOTMPF) + %TRIM(ARLENKE)
N07   *
N07   * SJEKK OM FIL ER LÅST. HVIS JA, VENT INNTIL 10 GANGER 1 SEKUND
N07  C     1             DO        10
N07  C                   CALL      'SYGE140CL'
N07  C                   PARM                    SIFS
N07  C                   PARM                    UOK
N07  C                   IF        UOK = '0'
N07  C                   LEAVE
N07  C                   END
N07  C                   Z-ADD     1             USEC              3 0
N07  C                   CALL      'DLYJBSEC'
N07  C                   PARM                    USEC
N07  C                   ENDDO
N07   *
N05  C                   END
N06  C                   END
N05  C                   END
N05  C                   END
N05   *
N02   * Finn filnavnet gjennom å finne siste skråstreken, alt bak den er filnavnet
N02  C                   EVAL      *IN21=*OFF
N02  C                   MOVEA     *BLANKS       A
N02  C                   MOVEA     SIFS          A
N02  C     1             DO        255           WX                3 0
N02  C     256           SUB       WX            WY                3 0
N02  C     A(WY)         IFEQ      '/'
N02  C                   LEAVE
N02  C                   ENDIF
N02  C                   ENDDO
N02  C                   ADD       1             WY
N02  C                   MOVEA     A(WY)         WSIFS
N02   * PUT
N02  C                   MOVEL(P)  'PUT'         DATA
N02  C                   CAT       SIFS:1        DATA
N02
     C                   IF        INTCRDT=*BLANKS
N02  C                   CAT       WSIFS:1       DATA
     C                   ELSE
N02  C                   CAT       INTCRDT:1     DATA
N02  C                   CAT       '/':0         DATA
N02  C                   CAT       WSIFS:0       DATA
     C                   ENDIF
N02  C                   EXCEPT    AWRK
N02   * Eventuell rename
     C     INTCRDT       IFNE      *BLANKS
N02  C                   MOVEL(P)  'rename'      DATA
N02  C                   CAT       INTCRDT:1     DATA
N02  C                   CAT       '/':0         DATA
N02  C                   CAT       WSIFS:0       DATA
N02  C                   CAT       INTCRD:1      DATA
N02  C                   CAT       '/':0         DATA
N02  C                   CAT       WSIFS:0       DATA
N02  C                   EXCEPT    AWRK
     C                   ENDIF
N02  C                   ENDSR
     OEDI07C3R  EADD         AWRK
     O                       DATA               256
