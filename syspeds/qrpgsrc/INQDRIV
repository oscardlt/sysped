      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/inqdriv MODULE(*LIBL/inqdriv *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDRIV      IF   E           K DISK    USROPN
     FDRIVL01   IF   E           K DISK    USROPN
     F                                     RENAME(DRIVR:DRIVR01)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
     D WSSJA           s             20
     D WSSJN           s             20
     D WSFORMS         s             20
     D WSREFR          s              1
     D REFRESH         s             50
     D SOKSJN          s             30
     D SOKSJA          s              8
     D SOKSJAN         s              8  0
     D user            s             10
     D MaxV            s              5
     D MaxVis          s              5  0
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKSJN  = zhbgetvar('SOKSJN')
     C                   eval      SOKSJN  = uppify(SOKSJN)
     C                   eval      SOKSJA  = zhbgetvar('SOKSJA')
     C                   eval      SOKSJAN = c2n2(SOKSJA)
     C                   eval      WSSJA  = zhbgetvar('WSSJA')
     C                   IF        WSSJA  = *BLANKS
     C                   EVAL      WSSJA  = 'WSSJA'
     C                   END
     C                   eval      WSSJN  = zhbgetvar('WSSJN')
     C                   IF        WSSJN  = *BLANKS
     C                   EVAL      WSSJN  = 'WSSJN'
     C                   END
     C                   eval      WSFORMS = zhbgetvar('WSFORMS')
     C                   IF        WSFORMS = *BLANKS
     C                   EVAL      WSFORMS = 'forms[0]'
     C                   END
     C                   eval      WSREFR  = zhbgetvar('WSREFR')
     C                   IF        ((((WSREFR = 'J') OR
     C                                (WSREFR = 'j')) OR
     C                                (WSREFR = 'Y')) OR
     C                                (WSREFR = 'y'))
     C                   eval      REFRESH = 'self.opener.document.'
     C                             + %trim(WSFORMS)
     C                             + '.submit();'
     C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     DRIV
     C                   CLOSE     DRIVL01
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQDRIV'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('wssja':WSSJA)
     C                   callp     updHTMLvar('wssjn':WSSJN)
     C                   callp     updHTMLvar('wsforms':WSFORMS)
     C                   callp     updHTMLvar('wsrefr':WSREFR)
     C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   Move      *zeros        AntVist           5 0
     C     SOKSJN        IFEQ      *BLANKS
     C     SOKSJAN       ANDEQ     *ZEROS
     C                   GOTO      UTLES
     C                   END
      *
     C                   SELECT
     C                   WHEN      SOKSJAN <> 0
     C     SOKSJAN       SETLL     DRIV
     C                   READ      DRIV                                   33
     C                   WHEN      SOKSJN <> *BLANKS
     C     SOKSJN        SETLL     DRIVL01
     C                   READ      DRIVL01                                33
     C                   ENDSL
      *
     C                   DOW       *IN33 = *OFF
     C                   exsr      exclude
     c                   IF        EXCL = 'N'
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('DRANSA':
     C                             %trim(%editc(DRANSA:'3')))
     C                   callp     updHTMLvar('DRNAVN':DRNAVN)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
      *
     C                   SELECT
     C                   WHEN      SOKSJAN <> 0
     C                   READ      DRIV                                   33
     C                   WHEN      SOKSJN <> *BLANKS
     C                   READ      DRIVL01                                33
     C                   ENDSL
      *
     C                   ENDDO
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UtExcl        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      DRIV
     C                   OPEN      DRIVL01
      * Key for CUNDL01
      *
      *
     C                   endsr
