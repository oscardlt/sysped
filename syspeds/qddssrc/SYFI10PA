********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFI10A010' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Gå til rollbilde')
     A                                      CF23(23 'bekreft sletting')
     A                                      CF10(10 'Gå til varelinjer')
     A                                      CF17(17 'Rubrikk 36')
     A                                      CF18(18 'Rubrikk 29')
     A                                  1  2'SYSPED/SYFM21C'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(013 SHU0441 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSSNDN        17A  O  1 36COLOR(RED)
     A            DFAVD          4Y 0O  1 54EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFOPD          7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFFBNR         3Y 0O  1 67EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  3  1MSGCON(006 DIV0151 *LIBL/SYMSGF)
     A            DFST           1A  O  3  8TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 10MSGCON(010 DIV0152 *LIBL/SYMSGF)
     A            DFKDME         1A  O  3 21TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNTLA         4Y 0O  3 23EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  3 29MSGCON(012 DIV0153 *LIBL/SYMSGF)
     A            DFKDE          1A  O  3 42TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 45MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            DFSG           3A  O  3 55
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  1MSGCON(007 DIV0154 *LIBL/SYMSGF)
     A            DFKNV          8Y 0O  5  9TEXT('Kundenr Sender')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFNAV         30A  O  5 18COLOR(YLW)
     A            DFAD1S        30A  O  5 49COLOR(YLW)
     A            DFAD2S        30A  O  6 18COLOR(YLW)
     A            DFPNLS         4Y 0O  6 49
     A                                      COLOR(YLW)
     A            WSAD3S        24A  O  6 54COLOR(YLW)
     A                                  7  1MSGCON(007 DIV0155 *LIBL/SYMSGF)
     A            DFNAVM        30A  O  7 18TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD1M        30A  O  7 49TEXT('Adresse Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*                                 9  1MSGCON(013 DIV0160 *LIBL/SYMSGF)
S01  A*           DFTOLL         2Y 0O  9 15TEXT('Tollsted')
N01  A                                  9  1MSGCON(011 DIV0160 *LIBL/SYMSGF)
N01  A            DFTOLL         4Y 0O  9 13TEXT('Tollsted')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFAD2M        30A  O  9 18TEXT('P-adr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  1MSGCON(016 DIV0158 *LIBL/SYMSGF)
     A            DFAD1L        30A  O 10 18TEXT('Leveringsadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFPOUL         4Y 0O 10 49
     A                                      COLOR(YLW)
     A            DSADUL        25A  O 10 54TEXT('Utleveringssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  1MSGCON(016 DIV0163 *LIBL/SYMSGF)
     A            DFTRAN    R     Y  O 12 18REFFLD(DOKUFR/DFTRAN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            DFNAT         20A  O 12 28TEXT('Navn Transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 49'EDIfact ?'
     A            DFCMN     R        O 12 61REFFLD(DOKUFR/DFCMN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                 12 64'Y/N'
     A                                 12 68'Ventekode'
     A            DFVEN     R        O 12 78REFFLD(DOKUFR/DFVEN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                 13  1MSGCON(016 DIV0164 *LIBL/SYMSGF)
     A            DFNTEU         2Y 0O 13 18TEXT('Ant EURO-plr utveksl')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 13 21MSGCON(008 DIV0165 *LIBL/SYMSGF)
     A            DFNTAU         2Y 0O 13 30TEXT('Ant andr-plr utveksl')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 14 44MSGCON(016 DIV0167 *LIBL/SYMSGF)
     A            DFKNS         12A  O 14 61TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 44MSGCON(016 DIV0168 *LIBL/SYMSGF)
     A            DFKNM         12A  O 15 61TEXT('Kundenr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  1MSGCON(020 DIV5018 *LIBL/SYMSGF)
     A            DFFVCU    R        O 17 22REFFLD(DOKUFR/DFFVCU *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            DFFVBL    R     Y  O 17 26REFFLD(DOKUFR/DFFVBL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                 19  1MSGCON(075 TBR0256 *LIBL/SYMSGF)
     A            DFGM          12A  O 20  1TEXT('Godsmerking')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNT           7Y 0O 20 14TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFVS1         25A  O 20 23TEXT('Vareslag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVKT          9Y 0O 20 49TEXT('Vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFM3           7Y 3O 20 67TEXT('Kubikk')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            DFGM2         12A  O 21  1TEXT('Godsmerking')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVS2         25A  O 21 23TEXT('Vareslag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVKTF         9Y 0O 21 49TEXT('Vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A  88                             22  4'*** S L E T T I N G   V A L G T   -
     A                                      B E K R E F T   M E D   F23'
     A                                      COLOR(RED)
     A  88                                  DSPATR(BL)
     A                                 23  1MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 20MSGCON(018 TRO0001 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 39MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(007 BBA0145 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 73MSGCON(007 BBA0146 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  7  2VALUES('5' '4' '3' '2' ' ')
     A            DFFBNR    R        O  7  4REFFLD(DOKUFR/DFFBNR *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            WSSNDN        17A  O  6  2COLOR(YLW)
     A            DFNAVM    R        O  6 20REFFLD(DOKUFR/DFNAVM *LIBL/DOKUF)
     A            DSSTED        30A  O  6 51
     A            DFNT      R        O  7 11REFFLD(DOKUFR/DFNT *LIBL/DOKUF)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            DFVS      R        O  7 19REFFLD(DOKUFR/DFVS *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            DFVKT     R        O  7 45REFFLD(DOKUFR/DFVKT *LIBL/DOKUF)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            DFNAT     R        O  7 55REFFLD(DOKUFR/DFNAT *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFI10P'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 65TIME
     A                                  3  2MSGCON(007 TBR0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 13MSGCON(009 TBR0070 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 27MSGCON(009 TBR0018 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(009 TBR0071 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(078 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 11MSGCON(069 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 YBC0064 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYFI10P'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76DSPATR(ND)
     A                                  2 65TIME
     A                                  4  3MSGCON(078 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 12MSGCON(069 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
