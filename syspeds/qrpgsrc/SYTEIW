      *********************************************************************
      * Spørring på innenlands sendinger
      * PROGRAM BEHANDLER INPUT FRA SYMN1
CRTPG+* CRTPGM SYSPED/SYTEIW MODULE(*LIBL/SYTEIW *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 UsrSpcNam
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     Fkodtri    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
N01  D UsrSpcName      s             10
     D dager           s              3
      *
     D TERM            s              4
     D TERM1N          s              4  0
     D TERMN           s              4  0
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  BIWELC                 1     50
     D  BIAVD                  1     19
     D  BIAVD1                 1      4
     D  BIAVD2                 6      9
     D  BIAVD3                11     14
     D  BIAVD4                16     19
      *
     D  BITER1                47     50
      *
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA1_5              1      5
     D  PAVRDA7_46             7     46
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Blank passord=anonym, OK, Ellers må være "Inne"
     C     BIPO          IFNE      *BLANKS
     C*                  IF        ((BIBRID = *BLANKS) OR
     C                   IF        ((*IN50 = *ON) OR
     C                              (BIINN = *BLANKS))
     C                   exsr      SetSkl01B
     C                   GOTO      SLUTT
     C                   END
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     c     TermDefin     ifeq      'N'
      * er det oppgitt "primær" terminal ??
     C                   eval      Term1n  = c2n2(BITER1)
     c     Term1N        chain     kodtri                             33
     c     *in33         IFEQ      *ON
     c                   move      *zeros        Term1N
     c                   else
     c                   move      riterm        term
     C                   callp     updHTMLvar('TERM':term)
     C                   callp     wrtsection('row')
     c                   end
      *
     c                   z-add     1             TermN             4 0
     C     TermN         setll     kodtri
     C                   read      kodtri                                 33
     c     *in33         DOWEQ     *OFF
     c     RiTerm        Ifne      Term1N
     c                   move      riterm        term
     C                   callp     updHTMLvar('TERM':term)
     C                   callp     wrtsection('row')
     c                   end
     c     RITerm        add       1             TermN
     C     TermN         setll     kodtri
     C                   read      kodtri                                 33
     c                   end
     c                   else
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 40
     c     *in40         doweq     '0'
     C                   callp     updHTMLvar('TERM':PAVRDA1_5)
     C                   callp     wrtsection('row')
     C     BrParKey      READE     BRPARF                                 40
     C                   ENDDO
     c                   end
     C                   callp     wrtsection('bot')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
N01  C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      dager   = zhbgetvar('dager')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     updHTMLvar('FEILTXT':'.')
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updHtmlVar('USER':WSUSER)
N01  C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('TERM':TERM)
     C                   callp     updHTMLvar('dager':dager)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYTEIW'
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   OPEN      KODTRI
     C                   open      BRPARF
      *
     C     BrParKey      KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVEL     'TERMK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C   33              MOVE      'N'           TermDefin         1
      *
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     kodtri
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
