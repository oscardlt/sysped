      *
     FFRATN     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTV     IF   E           K DISK
     D                 DS
     D  HEFBV                  1      9  0
     D  HEFBVN                 5      9  0
     D                 DS
     D  HEM3                   1      7  3
     D  HEM3N                  2      6  2
     D                 DS
     D  FRVK1                  1      5  0
     D  FRVK2                  6     10  0
     D  FRVK3                 11     15  0
     D  FRVK4                 16     20  0
     D  FRVK5                 21     25  0
     D  FRVK6                 26     30  0
     D  FRVK7                 31     35  0
     D  FRVK8                 36     40  0
     D  FRVK9                 41     45  0
     D  FRVK10                46     50  0
     D  FRVK11                51     55  0
     D  FRVK12                56     60  0
     D  FV                     1     60  0
     D                                     DIM(12) ASCEND                       VEKTGR. AVTALE
     D                 DS
     D  FB01                   1      3P 0
     D  FB02                   4      6P 0
     D  FB03                   7      9P 0
     D  FB04                  10     12P 0
     D  FB05                  13     15P 0
     D  FB06                  16     18P 0
     D  FB07                  19     21P 0
     D  FB08                  22     24P 0
     D  FB09                  25     27P 0
     D  FB10                  28     30P 0
     D  FB11                  31     33P 0
     D  FB12                  34     36P 0
     D  FB                     1     36P 0
     D                                     DIM(12)
     D                 DS
     D  FM                     1     36P 0
     D                                     DIM(12)
     D  FM01                   1      3P 0
     D  FM02                   4      6P 0
     D  FM03                   7      9P 0
     D  FM04                  10     12P 0
     D  FM05                  13     15P 0
     D  FM06                  16     18P 0
     D  FM07                  19     21P 0
     D  FM08                  22     24P 0
     D  FM09                  25     27P 0
     D  FM10                  28     30P 0
     D  FM11                  31     33P 0
     D  FM12                  34     36P 0
      *
     D                 DS
     D  FTMAX1                 1      5  0
     D  FTMAX2                 6     10  0
     D  FTMAX3                11     15  0
     D  AX                     1     15  0
     D                                     DIM(3)
      *
     C                   EXSR      INIT
     C                   SETON                                        LR
     C                   RETURN
      ******************************************
     C     BERFRA        BEGSR
      ******************************************
      *
     C                   MOVE      XTKNR         XTKNRG
      *
      * BEREGNER FRAKT
      *
      *
      *
     C                   MOVE      HEFR          XTFRAN
     C                   MOVE      HEOT          XTOT
     C                   MOVE      *ZEROS        PRBELØ
     C                   MOVE      *ZEROS        FTMIN
     C                   MOVE      *ZEROS        XPROS            10 3
      *
      *  32 = IKKE FUNNET FRAKTAVTALE
      *
     C                   Z-ADD     1             XN                2 0          VGR.AVD
     C                   SETON                                        32
      *
     C                   MOVE      ' '           XKDA              1
     C     STBF00        TAG
      *
     C                   MOVE      HESDF         XTFRA
     C                   MOVE      SONEF         XSONEF
     C                   MOVE      HESDT         XTTIL
     C                   MOVE      SONET         XSONET
     C                   MOVE      HEOT          XTOT
     C                   EXSR      CHAINF
      *
      * 32 PÅ INGEN AVTALE
      * B=BRUTTO-RATE / A=AVTALEPRIS
      *
     C     XKDA          IFEQ      ' '
     C                   MOVE      'A'           XKDA
     C                   ELSE
     C                   MOVE      'B'           XKDA
     C                   END
     C     *IN32         CABEQ     '1'           STBFR
      *
      * KOMMER HIT KUN VED KUNDEAVTALE
      *
      *
      * SKJEKKER OM AVVIKKENDE FRAKTBEREGNING
      *
     C     FTKOLL        IFNE      *BLANKS
     C                   EXSR      TSTAVV
     C     HOPP          IFEQ      'J'
     C                   GOTO      SLBFR
     C                   END
     C                   ELSE
     C                   MOVE      HEFBV         LOKUPF            9 0
      * SKJEKKER OM DIVERGERENDE OMREGNING
     C     FTM3OM        IFNE      *ZEROS
     C     FTLMOM        ORNE      *ZEROS
     C                   EXSR      BERFVK
     C                   END
     C                   END
      *
      * VEKTGR.INNDELING LIGGER I FRAKTAVTALEN
      *
     C                   Z-ADD     1             XN                2 0          VGR.AVD
     C     LOKUPF        IFGT      99999
     C                   MOVE      99999         LOKUP2            5 0
     C                   ELSE
     C                   Z-ADD     LOKUPF        LOKUP2
     C                   END
     C     LOKUP2        LOOKUP    FV(XN)                             76  76
     C                   SETOFF                                       76
      *
     C     FTKODE        IFNE      'P'                                          1<-B
     C                   Z-ADD     FB(XN)        PRBELØ
     C                   Z-ADD     FTPRPR        PRANT             5 0
      * SKJEKKER OM FAST PRIS TIL OG MED
     C     XN            IFLE      FTFAST                                         3<-B
     C                   Z-ADD     0             PRANT
     C                   END                                                      3<-E
      * PRIS PR KOLLI
     C  N76FTKOLL        IFEQ      'X'                                           2<-B
     C     XN            IFGT      FTFAST                                         3<-B
     C                   MULT      HENT          PRBELØ
     C                   Z-ADD     0             PRANT
     C                   END                                                     2<-E
     C                   END                                                     2<-E
      * PRIS VED VAREVERDI
     C  N76FTKOLL        IFEQ      'V'                                           2<-B
     C     PRBELØ        DIV       10000         XWORK            17 5
     C     XWORK         MULT      TRVERB        PRBELØ
     C*                    MOVE TRVERV    UVALUT
     C                   MOVE      TRVERV        FTVAL
     C                   Z-ADD     0             PRANT
     C                   END                                                     2<-E
      * PRIS MEN GRUNNPRISLOGIKK
     C  N76FTKOLL        IFEQ      'G'                                           2<-B
     C                   MULT      LOKUPF        PRBELØ
     C                   DIV       100           PRBELØ
     C                   ADD       FTMIN         PRBELØ
     C                   Z-ADD     0             PRANT
     C                   END                                                     2<-E
      * PRIS PR KOLLI
     C  N76FTKOLL        IFEQ      'M'                                           2<-B
     C     FTKOLL        OREQ      'L'
     C     XN            IFGT      FTFAST                                         3<-B
     C                   MULT      LOKUPF        PRBELØ
     C                   DIV       100           PRBELØ
     C                   END                                                      3<-E
     C                   Z-ADD     0             PRANT
     C                   END                                                     2<-E
      *  HAR AVTALE, MEN IKKE PROSENTAVTALE
      *
     C                   EXSR      SRMIMA
      *
      * FOR BRUTTO, FLYTTER AVTALEAVHENGING  MINIMUM
     C     XTKNR         IFEQ      0
     C     XFTMIN        ANDNE     0
     C                   Z-ADD     XFTMIN        FTMIN
     C                   END
      *
     C                   GOTO      SLBFR
     C                   END                                                    1<-E
      *
      * PROSENTAVTALE START
      *
     C     XTKNR         IFNE      0
     C     XPROS         IFEQ      *ZEROS
     C                   Z-ADD     FB(XN)        XPROS
     C                   ELSE
      * PROSENT AV PROSENTAVTALE
     C     FB(XN)        MULT      XPROS         XPROS
     C                   DIV       100           XPROS
     C                   END
     C                   Z-ADD     FTMIN         XFTMIN            5 0
     C                   END
      *
     C     STBFR         TAG
      *
     C     XTKNR         IFNE      0
      *    *******
      *     BRUTTO
      *    *******
      * HENTER FRAKTBELØP FRA BRUTTOTARIFF
     C                   MOVE      *ZEROS        XTKNR
     C                   MOVE      HEOT          XTOT
     C                   MOVE      HEFR          XTFRAN
     C                   GOTO      STBF00
     C                   END
      *
     C     SLBFR         TAG
     C                   MOVE      XTKNRG        XTKNR
     C     SLSHF         TAG
     C                   SETOFF                                       32
      *
     C     PRANT         IFGT      0
     C                   EXSR      OMREGN
     C                   END
      *
     C     PRBELØ        IFLT      FTMIN                                         2<-B
     C                   Z-ADD     FTMIN         PRBELØ
     C                   END                                                     2<-E
     C     XFTMAX        IFNE      *ZEROS                                        2<-B
     C     PRBELØ        ANDGT     XFTMAX
     C                   Z-ADD     XFTMAX        PRBELØ
     C                   END                                                     2<-E
      *
     C                   ENDSR
      ***********************************************
     C     CHAINF        BEGSR
      ***********************************************
      * CHAINER I FRAKTREGISTER   - HOVEDFRAKT ORDINÆR
      * (SETT INDIKATOR DERSOM IKKE BEHOV FOR Å CHAINE EVT. SONEFRAKT)
     C     XSONEF        COMP      *BLANKS                            2828
     C     XSONET        COMP      *BLANKS                            2929
     C                   MOVE      'N'           FRA000            1
     C     FR000         TAG
      *
     C     FRATK         CHAIN     FRATN                              32        *RENE STEDSKODER
     C   32
     CAN 28FRATK2        CHAIN     FRATN                              32        *SONE FRA
     C   32
     CAN 29FRATK3        CHAIN     FRATN                              32        *SONE TIL
     C   32
     CAN 28
     CAN 29FRATK4        CHAIN     FRATN                              32        *SONE FRA/TIL
     C   32              MOVE      'GE'          XTOT
     C   32FRATK         CHAIN     FRATN                              32        *RENE STEDSKODER
     C   32
     CAN 28FRATK2        CHAIN     FRATN                              32        *SONE FRA
     C   32
     CAN 29FRATK3        CHAIN     FRATN                              32        *SONE TIL
     C   32
     CAN 28
     CAN 29FRATK4        CHAIN     FRATN                              32        *SONE FRA/TIL
      *
      * INGEN AVTALE? PRØV IGJEN MED 000 SOM GENERELL FRANKATUR
     C     *IN32         IFEQ      '1'
     C     FRA000        ANDEQ     'N'
     C                   MOVE      HEOT          XTOT
     C                   MOVE      '000'         XTFRAN
     C                   MOVE      'J'           FRA000
     C                   GOTO      FR000
     C                   END
      *
      * VALUTAKODE PÅ FRAKAVTALEN (ELLER BRUTTO)??
     C     FTVAL         IFEQ      *BLANKS
     C     *IN32         OREQ      '1'
     C                   MOVE      FICURR        FTVAL
     C                   END
      *
     C                   ENDSR
      *
      *
     C******************************************
     C     SRMIMA        BEGSR
     C******************************************
     C*
     C* KOMPLETT LASS
     C*
     C     UKOM          IFGT      *ZEROS
     C     UKOM          ANDLT     4
     C                   Z-ADD     AX(UKOM)      PRBELØ
     C                   MOVE      *ZEROS        FTMIN             5 0
     C                   MOVE      *ZEROS        XFTMAX            5 0
     C                   MOVE      *ZEROS        PRANT
     C                   GOTO      SLMIMA
     C                   END
     C*
     C     FTMIN         IFGT      *ZEROS                                       1<-B
     C     XN            ANDEQ     1
     C     FTMM          IFEQ      2                                             2<-B
     C                   Z-ADD     FM(XN)        XFTMAX
     C                   END                                                     2<-E
     C                   ELSE                                                   1
     C     FTMM          IFEQ      1                                             2<-B
     C                   Z-ADD     FM(XN)        FTMIN
     C                   ELSE                                                    2
     C     FTMM          IFEQ      2                                              3<-B
     C                   Z-ADD     FM(XN)        XFTMAX
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
      * LEGGER UT KOMPLETT LASS I MAX HVIS FOREFINNES
      *
     C     AX(3)         IFNE      *ZEROS
     C     XFTMAX        IFGT      AX(3)
     C                   Z-ADD     AX(3)         XFTMAX
     C                   END
     C     XFTMAX        IFEQ      *ZEROS
     C                   Z-ADD     AX(3)         XFTMAX
     C                   END
     C                   END
     C*
     C     SLMIMA        ENDSR
      ***********************************************
     C     TSTAVV        BEGSR
      ***********************************************
      *
      * SKJEKKER AVVIKENDE BERGNINGSMETODE
      *
      * KUBIKKPRIS
      *
     C                   MOVE      *BLANKS       HOPP              1
     C                   MOVE      HEFBV         LOKUPF
     C     FTKOLL        IFEQ      'M'                                          1<-B
     C     HEM3          IFNE      *ZEROS                                        2<-B
     C                   MOVE      *ZEROS        LOKUPF
     C                   MOVE      HEM3N         LOKUPF
     C                   MOVE      0             FTPRPR
     C                   ELSE                                                    2
     C                   MOVE      'J'           HOPP
     C                   GOTO      SLBFRX
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      * LASTEMETERPRIS
     C     FTKOLL        IFEQ      'L'                                          1<-B
     C     HELM          IFNE      *ZEROS                                        2<-B
     C                   MOVE      *ZEROS        LOKUPF
     C                   MOVE      HELM          LOKUPF
     C                   MOVE      0             FTPRPR
     C                   ELSE                                                    2
     C                   MOVE      'J'           HOPP
     C                   GOTO      SLBFRX
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      * KOLLIPRIS
     C     FTKOLL        IFEQ      'X'                                          1<-B
     C     HENT          IFNE      *ZEROS                                        2<-B
     C                   Z-ADD     HENT          LOKUPF
     C                   ELSE                                                    2
     C                   MOVE      'J'           HOPP
     C                   GOTO      SLBFRX
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      * KILOPRIS
     C     FTKOLL        IFEQ      'B'                                          1<-B
     C     HEVKT         IFNE      *ZEROS                                        2<-B
     C                   MOVE      HEVKT         LOKUPF
     C                   MOVE      HEVKT         HEFBV
     C                   ELSE                                                    2
     C                   MOVE      'J'           HOPP
     C                   GOTO      SLBFRX
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      * SKJEKKER OM DIVERGERENDE OMREGNING
     C     FTKOLL        IFEQ      'G'                                          1<-B
     C     FTM3OM        IFNE      *ZEROS                                        2<-B
     C     FTLMOM        ORNE      *ZEROS
     C                   EXSR      BERFVK
     C                   MOVE      HEFBV         LOKUPF
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   MOVE      LOKUPF        HEFBV
     C     SLBFRX        ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C* INITIALIZATION OF VARIABLES, KEYS ETC.
     C*
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UGEB              3
     C                   PARM                    UKNR              8 0
     C                   PARM                    USK               1
     C                   PARM                    UAVTN             1 0
     C                   PARM                    UKOM              1 0
     C                   PARM                    UKDA              1            BRUTTO/AVTALE-KODE
     C                   PARM                    PRBELØ            9 2
     C                   PARM                    UVALUT            3
     C                   PARM                    UVALBE           13 2
     C*
     C     HEAKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C*
     C     KODVKE        KLIST
     C                   KFLD                    UNIK
     C                   KFLD                    UAVD
     C*
     C     VALKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    UVALUT
     C*
      * KEYS FOR FRAKT - HOVEDRELASJON
     C     FRATK         KLIST
     C                   KFLD                    XTKNR
     C                   KFLD                    XTFRAN
     C                   KFLD                    XTOT
     C                   KFLD                    XTSK
     C                   KFLD                    XTFRA
     C                   KFLD                    XTTIL
     C                   KFLD                    XTAVTN
     C     FRATK2        KLIST
     C                   KFLD                    XTKNR
     C                   KFLD                    XTFRAN
     C                   KFLD                    XTOT
     C                   KFLD                    XTSK
     C                   KFLD                    XSONEF
     C                   KFLD                    XTTIL
     C                   KFLD                    XTAVTN
     C     FRATK3        KLIST
     C                   KFLD                    XTKNR
     C                   KFLD                    XTFRAN
     C                   KFLD                    XTOT
     C                   KFLD                    XTSK
     C                   KFLD                    XTFRA
     C                   KFLD                    XSONET
     C                   KFLD                    XTAVTN
     C     FRATK4        KLIST
     C                   KFLD                    XTKNR
     C                   KFLD                    XTFRAN
     C                   KFLD                    XTOT
     C                   KFLD                    XTSK
     C                   KFLD                    XSONEF
     C                   KFLD                    XSONET
     C                   KFLD                    XTAVTN
     C*
     C*
     C                   READ      FIRM                                   33
     C*
     C                   Z-ADD     99999         FRVK12
     C                   MOVE      'V'           UNIK              1
     C     HEAKEY        CHAIN     HEADF                              33
     C*
     C                   MOVE      UKNR          UUKNR             8 0
     C                   MOVE      UKNR          XTKNR             8 0
     C* BEVAR INNPARAMETER KUNDENS VALUTA (UTSTEDELSESVALUTA)
     C                   MOVE      UVALUT        KUNVAL            3
     C*
     C                   MOVE      HESDFF        UFF               5
     C                   MOVE      HESDVT        UVF               5
     C                   MOVE      *ZEROS        XTKNRG            8 0
     C                   MOVE      HEFR          XTFRAN            3
     C                   MOVE      HEOT          XTOT              2
     C                   MOVE      USK           XTSK              1
     C                   MOVE      HESDF         XTFRA             5
     C                   MOVE      HESDT         XTTIL             5
     C                   MOVE      UAVTN         XTAVTN            1 0
     C                   MOVE      *BLANKS       XSONEF            5
     C                   MOVE      *BLANKS       XSONET            5
     C*
     C*
     C     HESDF         CHAIN     STED2                              33
     C  N33ST2KO2        IFNE      *BLANKS
     C                   MOVE      ST2KO2        SONEF             5
     C                   END
     C* Destination to:
     C     HESDT         CHAIN     STED2                              33
     C  N33ST2KO2        IFNE      *BLANKS
     C                   MOVE      ST2KO2        SONET             5
     C                   END
     C*
     C     KODVKE        CHAIN     KODTV                              33
     C*
     C                   EXSR      BERFRA
     C*
     C* SJEKK OM AVTALE / FAKTURTA UTSTEDELSE I FREMMEDVALUTA
     C     PRBELØ        IFNE      *ZEROS
     C                   MOVE      FTVAL         UVALUT
     C                   Z-ADD     PRBELØ        UVALBE
     C                   EXSR      TESTVA
     C                   END
     C*
     C                   ENDSR
     C*
      ***********************************************
     C     OMREGN        BEGSR
      ***********************************************
      *
      *** AJUSTING VEIGHT- LESS THAN 1000 TO NEAREST  10 KILO
      *** ELSE TO NEAREST 100 KG, BUT IF PRICE IS GIVEN PR. 2-10
      *** TO NEAREST  10 KG. NOT IF PRICE PR 1 KILO
      *
      *** IF FIRM HAVE DEFINED NO AJUSTMENT
      *** (INGAVR = 'J') NO AJUSTMENT IS PERFORMED
      *
     C                   Z-ADD     HEFBV         XVEKT             9 0
      *
      * 1
     C     KOVAVR        IFNE      'J'
      * 2
     C     FTMM          IFNE      3
      * 2B
     C     PRANT         IFNE      1
      * 3
     C     XVEKT         IFLT      1000                                         WEIGHT <   1000 KG
     C     XVEKT         ADD       9             XVEKT2            9 0          AJUST TO NEARES
     C                   MOVE      '0'           XVEKT2                         10 KG
     C                   ELSE                                                   WEIGHT >=     1000 K
     C     PRANT         IFGT      10                                           PRICE IS GIVEN PR 10
     C     XVEKT         ADD       99            XVEKT2                         KG OR MORE  , AJUST
     C                   MOVE      '00'          XVEKT2                         TO NEAREST   100 KG
     C                   ELSE
     C     XVEKT         ADD       9             XVEKT2                         PRICE GIVEN PR
     C                   MOVE      '0'           XVEKT2                         2-9 KG, AJUST TO
     C                   END                                                    NEAREST  10 KG
     C                   END
      * 3
     C                   ELSE                                                   NO AJUSTMENT
     C                   Z-ADD     XVEKT         XVEKT2
     C                   END
      * 2B
     C                   ELSE                                                   PRICE PR 1 AND NOT P
     C                   Z-ADD     XVEKT         XVEKT2                         LINEPRICE/PIESESPRIC
     C                   END
      * 2
     C                   ELSE                                                   PRICE PR 1 AND NOT P
     C                   Z-ADD     XVEKT         XVEKT2                         LINEPRICE/PIESESPRIC
     C                   END
      * 1
     C     PRBELØ        DIV       PRANT         XWORK
     C     XWORK         MULT(H)   XVEKT2        PRBELØ
      *
     C                   ENDSR
      *
     C*
     C***********************************************
     C     TESTVA        BEGSR
     C***********************************************
      * HVIS PRISEN ER I VALUTA, REGN OM TIL "NOK" (FICURR)
     C     FTVAL         IFNE      FICURR
     C     VALKEY        CHAIN     VALUL01                            95
     C     *IN95         IFEQ      '0'
     C     PRBELØ        DIV       OMRFAK        XWORK
     C     XWORK         MULT(H)   VALKU2        PRBELØ                          = KURS SALG
     C                   END
     C                   END
      *
      * HVIS KUNDE-VALUTA (UTSTEDELSESVAL) HVERKEN ER LIK "NOK"(FICURR)
      * ELLER AVTALENS VALUTA MÅ DET REGNES OM FRA "NOK" TIL KUNDEVAL.
     C     KUNVAL        IFNE      FICURR
     C     KUNVAL        ANDNE     FTVAL
     C                   MOVE      KUNVAL        UVALUT
     C     VALKEY        CHAIN     VALUL01                            95
     C     *IN95         IFEQ      '0'
     C     PRBELØ        MULT      OMRFAK        XWORK
     C     XWORK         DIV(H)    VALKU2        UVALBE                          = KURS SALG
     C                   END
     C                   END                                                    1<-E
      *
     C                   ENDSR                                                  1<-E
     C******************************************
     C     BERFVK        BEGSR
     C******************************************
     C*
     C* FINNER FRAKTBEREGNINGSVEKT
     C*
     C     HELM          MULT      FTLMOM        XFBV1             5 0
     C     HEM3          MULT      FTM3OM        XFBV2             5 0
     C     HEVKT         IFGE      XFBV1
     C                   Z-ADD     HEVKT         HEFBV
     C                   ELSE
     C                   Z-ADD     XFBV1         HEFBV
     C                   END
     C     HEFBV         IFLT      XFBV2
     C                   Z-ADD     XFBV2         HEFBV
     C                   END
     C*
     C                   ENDSR
