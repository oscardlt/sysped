      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLLINEE LLLINEER)
     A                                      PRINT
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 66 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            LLMN      R        H
     A            LLVN      R        H
     A            LLANR     R        H
     A            LLVB      R        H
     A            ELPAK     R        H
     A            ELSN      R        H
     A            ELTYPE    R        H
     A            ELBOND    R        H
     A            ELANT0    R        O  5  4EDTCDE(Z)
     A            ELITEM    R        O  5 12
     A            ELTAR     R        O  5 48
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL083R '
     A                                      COLOR(BLU)
     A                                  1 11'Vis sammenslåings detaljer'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0B  1 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Artikkelnr:'
     A            LLANR         15A  O  2 14COLOR(WHT)
     A                                  2 32'Dato ut :'
     A            ELBDAT         8Y 0O  2 42EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  4  2'   Antall Item-nr                 -
     A                                                  tariffnr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(18)
     A                                      CLRL(01)
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F3/F12=Avbryt   F5=Frisk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'LL083R '
     A                                      COLOR(BLU)
     A                                  1 11'Vis sammenslåings detaljer'
     A                                      COLOR(WHT)
     A                                  2  2'Artikkelnr:'
     A            LLANR         15A  O  2 14COLOR(WHT)
     A                                  2 32'Dato ut :'
     A            ELBDAT         8Y 0O  2 42EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  4  2'   Antall Item-nr                 -
     A                                                  tariffnr'
     A                                      COLOR(WHT)
     A N30                             18  2'F3/F12=Avbryt   F5=Frisk'
     A  30        MSGTXT        50A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
