             PGM        PARM(&GETFIL)
             DCL        VAR(&URL) TYPE(*CHAR) LEN(256)
             DCL        VAR(&IFSFIL) TYPE(*CHAR) LEN(45)
             DCL        VAR(&GETFIL) TYPE(*CHAR) LEN(45)
             DCL        VAR(&RESFIL) TYPE(*CHAR) LEN(45)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)

/* innparameter er TYPEN som er key for oppslag i først json (TVGETFIL1)  */
/* Ulike muligheter pr oktober 2021 :                                     */
/* "https://data.toll.no/api/3/action/help_show?name=package_list"        */
/*                                                                        */
/*       "innfoerselsavgift",                                             */
/*       "innfoerselsreferanse",                                          */
/*       "innfoerselsrestriksjon",       Ikke med                         */
/*       "landgruppe",                                                    */
/*       "lettelse",                     Ikke med                         */
/*       "medlemsland",                                                   */
/*       "raavaretollsats",                                               */
/*       "tollkvote",                    Ikke med                         */
/*       "tollsats",                                                      */
/*       "tolltariffstruktur",           Ikke med                         */
/*       "utfoerselsavgift",                                              */
/*       "utfoerselsreferanse",                                           */
/*       "utfoerselsrestriksjon",        Ikke med                         */
/*       "valutakurs",                                                    */
/*       "valutakurs_historisk"          Ikke med                         */
/*                                                                        */

        /* Hent fil til /temp/..   via webservice/libhttp */
        /* låner parameter IFS-fil for INNVERDI til json-prog */
             CHGVAR     VAR(&IFSFIL) VALUE(&GETFIL)
             CALL       PGM(TVGETFIL1) PARM(&IFSFIL)

        /* UT-param IFSFIL er JSON som gir gjeldende URL(XML)for nedlasting */
        /* Les fila (parse JSON via YAJL)/ trekk ut URL.. */
             IF         COND(&IFSFIL *NE ' ') THEN(DO)
             ADDLIBLE   LIB(YAJL)
             MONMSG     MSGID(CPF0000)

             CALL       PGM(TVGETFIL2) PARM(&URL &IFSFIL)

             DEL        OBJLNK(&IFSFIL)
             RMVLIBLE   LIB(YAJL)
             MONMSG     MSGID(CPF0000)
             ENDDO
        /* Les fila (parse JSON via YAJL)/ trekk ut URL.. */
             IF         COND(&URL *NE ' ') THEN(DO)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
             CHGVAR     VAR(&RESFIL) VALUE('/' *TCAT &LIB *TCAT +
                          '/TOLL/' *TCAT &GETFIL *TCAT '.xml')
             CALL       PGM(TVGETFIL3) PARM(&URL &RESFIL)
             ENDDO

    /* behandle valutafila (kunne vært gjort direkte i foregående prog */
             IF         COND(&IFSFIL *NE ' ') THEN(DO)

             IF         COND(&GETFIL *EQ 'valutakurs') THEN(DO)
             CALL       PGM(SYTI01C)
             ENDDO
             IF         COND(&GETFIL *EQ 'utfoerselsavgift') THEN(DO)
             CALL       PGM(SYTI02C)          /*Fiskavgift*/
             ENDDO
             IF         COND(&GETFIL *EQ 'innfoerselsreferanse') +
                          THEN(DO)
             CALL       PGM(SYTI03C)
             ENDDO
             IF         COND(&GETFIL *EQ 'medlemsland') THEN(DO)
             CALL       PGM(SYTI04C)       /*Landkode*/
             ENDDO
             IF         COND(&GETFIL *EQ 'landgruppe') THEN(DO)
             CALL       PGM(SYTI05C)
             ENDDO
             IF         COND(&GETFIL *EQ 'raavaretollsats') THEN(DO)
             CALL       PGM(SYTI06C)      /*Råvaretoll*/
             ENDDO
             IF         COND(&GETFIL *EQ 'tollsats') THEN(DO)
             CALL       PGM(SYTI07C)
             ENDDO
             IF         COND(&GETFIL *EQ 'innfoerselsavgift') THEN(DO)
             CALL       PGM(SYTI08C)
             ENDDO
             IF         COND(&GETFIL *EQ 'utfoerselsreferanse') +
                          THEN(DO)
             CALL       PGM(SYTI09C)
             ENDDO

             ENDDO

             ENDPGM

