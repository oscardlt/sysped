      *
      *****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE FREIGHT MANIFEST                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01 02 05-11 13 14 16-26
      *** LEDIGE INDIKATORER 29 31 32 34-83
      *****************************************************************
      *
     FFIRM      IF   E           K DISK
     FBILLOF    IF   E           K DISK
     FBLLOK     IF   E           K DISK
     FBOOKING   UF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FFRMDOC    UF A E           K DISK
     FFRMDOC1   UF A E           K DISK
     FFRMDOC2   UF A E           K DISK
     FFRMDOC3   UF A E           K DISK
     FSYSJ25FA  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D VAL             S              3    DIM(4)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSFT1                  1     30
     D  WSFT2                 31     60
     D  WSFT3                 61     90
     D  WSFT4                 91    120
     D  WSFT5                121    150
     D  WSFT6                151    180
     D  WSFT7                181    210
     D  WSFT8                211    240
     D  WSFT9                241    270
     D  WSFT10               271    300
     D  FT1                    1    300
     D                                     DIM(10)
     D                 DS
     D  WSKU1                  1      7  4
     D  WSKU2                  8     14  4
     D  WSKU3                 15     21  4
     D  WSKU4                 22     28  4
     D  WSKU5                 29     35  4
     D  WSKU6                 36     42  4
     D  WSKU7                 43     49  4
     D  WSKU8                 50     56  4
     D  WSKU9                 57     63  4
     D  WSKU10                64     70  4
     D  KU1                    1     70  4
     D                                     DIM(10)
     D                 DS
     D  WSVA1                  1      3
     D  WSVA2                  4      6
     D  WSVA3                  7      9
     D  WSVA4                 10     12
     D  WSVA5                 13     15
     D  WSVA6                 16     18
     D  WSVA7                 19     21
     D  WSVA8                 22     24
     D  WSVA9                 25     27
     D  WSVA10                28     30
     D  VA1                    1     30
     D                                     DIM(10)
     D                 DS
     D  WSPR1                  1     13  2
     D  WSPR2                 14     26  2
     D  WSPR3                 27     39  2
     D  WSPR4                 40     52  2
     D  WSPR5                 53     65  2
     D  WSPR6                 66     78  2
     D  WSPR7                 79     91  2
     D  WSPR8                 92    104  2
     D  WSPR9                105    117  2
     D  WSPR10               118    130  2
     D  PR1                    1    130  2
     D                                     DIM(10)
     D                 DS
     D  WSCO1                  1     13  2
     D  WSCO2                 14     26  2
     D  WSCO3                 27     39  2
     D  WSCO4                 40     52  2
     D  WSCO5                 53     65  2
     D  WSCO6                 66     78  2
     D  WSCO7                 79     91  2
     D  WSCO8                 92    104  2
     D  WSCO9                105    117  2
     D  WSCO10               118    130  2
     D  CO1                    1    130  2
     D                                     DIM(10)
     D                 DS
     D  WSVA21                 1      3
     D  WSVA22                 4      6
     D  WSVA23                 7      9
     D  WSVA24                10     12
     D  WSVA25                13     15
     D  VA2                    1     15
     D                                     DIM(5)
     D                 DS
     D  WSPR21                 1     13  2
     D  WSPR22                14     26  2
     D  WSPR23                27     39  2
     D  WSPR24                40     52  2
     D  WSPR25                53     65  2
     D  PR2                    1     65  2
     D                                     DIM(5)
     D                 DS
     D  WSKU21                 1      7  4
     D  WSKU22                 8     14  4
     D  WSKU23                15     21  4
     D  WSKU24                22     28  4
     D  WSKU25                29     35  4
     D  KU2                    1     35  4
     D                                     DIM(5)
     D                 DS
     D  WSVA2A                 1      3
     D  WSVA2B                 4      6
     D  WSVA2C                 7      9
     D  WSVA2D                10     12
     D  WSVA2E                13     15
     D  V22                    1     15
     D                                     DIM(5)
     D                 DS
     D  WSP221                 1     13  2
     D  WSP222                14     26  2
     D  WSP223                27     39  2
     D  WSP224                40     52  2
     D  WSP225                53     65  2
     D  P22                    1     65  2
     D                                     DIM(5)
     D                 DS
     D  WSVA31                 1      3
     D  WSVA32                 4      6
     D  WSVA33                 7      9
     D  WSVA34                10     12
     D  WSVA35                13     15
     D  WSVA36                16     18
     D  WSVA37                19     21
     D  WSVA38                22     24
     D  WSVA39                25     27
     D  WSVA3A                28     30
     D  WSVA3B                31     33
     D  VA3                    1     33
     D                                     DIM(11)
     D                 DS
     D  WSAM31                 1     13  2
     D  WSAM32                14     26  2
     D  WSAM33                27     39  2
     D  WSAM34                40     52  2
     D  WSAM35                53     65  2
     D  WSAM36                66     78  2
     D  WSAM37                79     91  2
     D  WSAM38                92    104  2
     D  WSAM39               105    117  2
     D  WSAM3A               118    130  2
     D  WSAM3B               131    143  2
     D  AM3                    1    143  2
     D                                     DIM(11)
     D                 DS
     D  WSKU31                 1      7  4
     D  WSKU32                 8     14  4
     D  WSKU33                15     21  4
     D  WSKU34                22     28  4
     D  WSKU35                29     35  4
     D  WSKU36                36     42  4
     D  WSKU37                43     49  4
     D  WSKU38                50     56  4
     D  WSKU39                57     63  4
     D  WSKU3A                64     70  4
     D  WSKU3B                71     77  4
     D  KU3                    1     77  4
     D                                     DIM(11)
     D                 DS
     D  WSLC31                 1     13  2
     D  WSLC32                14     26  2
     D  WSLC33                27     39  2
     D  WSLC34                40     52  2
     D  WSLC35                53     65  2
     D  WSLC36                66     78  2
     D  WSLC37                79     91  2
     D  WSLC38                92    104  2
     D  WSLC39               105    117  2
     D  WSLC3A               118    130  2
     D  WSLC3B               131    143  2
     D  LC3                    1    143  2
     D                                     DIM(11)
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  FMDTÅ                  1      4  0
     D  FMDTM                  5      6  0
     D  FMDTD                  7      8  0
     D  FMDT                   1      8  0
     D                 DS
     D  BLDTVÅ                 1      4  0
     D  BLDTVM                 5      6  0
     D  BLDTVD                 7      8  0
     D  BLDTVH                 1      8  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C  N04
     CANN30              Z-ADD     5             LINNBR
     C  N04
     CANN30              Z-ADD     14            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE01
     C                   EXSR      KDTFMT
     C   12              GOTO      SLUTT
     C*
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVEA     '00000000'    *IN(84)
     C                   SETOFF                                       30
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDT1
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      START
     C                   EXSR      CALTOT
     C*
     C   03              DO
     C                   EXSR      TESTB2
     C   30              GOTO      START2
     C                   EXSR      TESTB3
     C   30              GOTO      START3
     C                   GOTO      SLUTT
     C                   END
     C   15              GOTO      SLUTT
     C*
     C     START2        TAG
     C  N04
     CANN30              Z-ADD     15            LINNBR
     C  N04
     CANN30              Z-ADD     29            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE02
     C                   EXSR      KDTFMT
     C*
     C                   MOVEA     '000000'      *IN(94)
     C                   SETOFF                                       30
     C   12              GOTO      START
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDT2
     C                   GOTO      START2
     C                   END
     C*
     C                   EXSR      TESTB2
     C   30              GOTO      START2
     C*
     C   03              DO
     C                   EXSR      TESTB3
     C  N30              GOTO      SLUTT
     C                   END
     C   15              GOTO      SLUTT
     C*
     C     START3        TAG
     C  N04
     CANN30              Z-ADD     11            LINNBR
     C  N04
     CANN30              Z-ADD     32            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE03
     C                   EXSR      KDTFMT
     C*
     C                   MOVEA     '00'          *IN(98)
     C                   SETOFF                                       30
     C   12              GOTO      START2
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDT3
     C                   GOTO      START3
     C                   END
     C*
     C                   EXSR      TESTB3
     C   30              GOTO      START3
     C*
     C  N03
     CANN15              Z-ADD     0             UFLAGG
     C*
     C     SLUTT         TAG
     C  N12              EXSR      UPDFRM
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C   15              Z-ADD     5             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     TESTB1        BEGSR
     C******************************************
     C     WSNAA         COMP      *BLANKS                                99
     C   99              MOVE      'SPN0105'     MSGNR
     C   99              SETON                                        30
     C*
     C     WSTR          COMP      '  '                                   98
     C   98
     CANN30              MOVE      'SPT0132'     MSGNR
     C   98
     CANN30              SETON                                        30
     C*
     C     WSLSH         CHAIN     STED2                              97
     C   97
     CANN30              MOVE      'SPS0102'     MSGNR
     C   97
     CANN30              SETON                                        30
     C*
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C   96
     CANN30              MOVE      'SPD0001'     MSGNR
     C   96
     CANN30              SETON                                        30
     C*
     C     WSOB          CHAIN     CDBOAT                             95
     C   95
     CANN30              MOVE      'SPB0104'     MSGNR
     C   95
     CANN30              SETON                                        30
     C*
     C     WSREIS        COMP      *BLANKS                                94
     C   94
     CANN30              MOVE      'SPR0101'     MSGNR
     C   94
     CANN30              SETON                                        30
     C*
     C     1             DO        10            XN                2 0
     C     PR1(XN)       IFNE      0
     C     VA1(XN)       ANDEQ     '   '
     C                   MOVE      FICURR        VA1(XN)
     C                   END
     C     CO1(XN)       IFNE      0
     C     VA1(XN)       ANDEQ     '   '
     C                   MOVE      FICURR        VA1(XN)
     C                   END
     C                   END
     C*
     C     1             DO        10            XN                             1<-B
     C     VA1(XN)       IFNE      '   '                                         2<-B
     C                   MOVE      VA1(XN)       YVAL
     C     VALUFK        CHAIN     VALUL01                            33
     C   33              DO                                                       3<-B
     C     94            SUB       XN            X2                2 0
     C                   MOVE      '1'           *IN(X2)
     C                   END                                                      3<-E
     C   33
     CANN30              MOVE      'SPV0001'     MSGNR
     C   33
     CANN30              SETON                                        30
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C   30              EXSR      SETPC1
     C*
     C                   ENDSR
     C*
     C******************************************
     C     SETPC1        BEGSR
     C******************************************
     C   84
     COR 85
     COR 86              Z-ADD     43            POSNBR
     C   87
     COR 88
     COR 89
     COR 90
     COR 91
     COR 92
     COR 93              Z-ADD     43            POSNBR
     C   94              Z-ADD     40            POSNBR
     C   95              Z-ADD     14            POSNBR
     C   96              Z-ADD     70            POSNBR
     C   97              Z-ADD     49            POSNBR
     C   98
     COR 99              Z-ADD     14            POSNBR
     C*
     C   84              Z-ADD     20            LINNBR
     C   85              Z-ADD     19            LINNBR
     C   86              Z-ADD     18            LINNBR
     C   87              Z-ADD     17            LINNBR
     C   88              Z-ADD     16            LINNBR
     C   89              Z-ADD     15            LINNBR
     C   90              Z-ADD     14            LINNBR
     C   91              Z-ADD     13            LINNBR
     C   92              Z-ADD     12            LINNBR
     C   93              Z-ADD     11            LINNBR
     C   94
     COR 95              Z-ADD     7             LINNBR
     C   96
     COR 97
     COR 98              Z-ADD     6             LINNBR
     C   99              Z-ADD     5             LINNBR
     C*
     C                   ENDSR
     C*
     C******************************************
     C     TESTB2        BEGSR
     C******************************************
     C     PR2(5)        IFNE      0
     C     VA2(5)        ANDEQ     '   '
     C                   MOVE      FICURR        VA2(5)
     C                   END
     C*
     C     VA2(5)        IFNE      '   '                                        1<-B
     C                   MOVE      VA2(5)        YVAL
     C     VALUFK        CHAIN     VALUL01                            99
     C   99
     CANN30              MOVE      'SPV0001'     MSGNR
     C   99
     CANN30              SETON                                        30
     C                   END                                                    1<-E
     C*
     C     WSKUTA        IFNE      0
     C     WSPRTA        ORNE      0
     C                   MOVE      WSVATA        YVAL
     C     VALUFK        CHAIN     VALUL01                            98
     C   98
     CANN30              MOVE      'SPV0001'     MSGNR
     C   98
     CANN30              SETON                                        30
     C                   END
      *
     C                   EXSR      CALT2
     C*
     C   30              EXSR      SETPC2
     C*
     C                   ENDSR
     C*
     C******************************************
     C     SETPC2        BEGSR
     C******************************************
     C   98              Z-ADD     60            POSNBR
     C   99              Z-ADD     29            POSNBR
     C*
     C   98              Z-ADD     18            LINNBR
     C   99              Z-ADD     15            LINNBR
     C*
     C                   ENDSR
     C*
     C******************************************
     C     TESTB3        BEGSR
     C******************************************
     C     1             DO        11            XN
     C     AM3(XN)       IFNE      0
     C     VA3(XN)       ANDEQ     '   '
     C                   MOVE      FICURR        VA3(XN)
     C                   END
     C                   END
     C*
     C     1             DO        11            XN                             1<-B
     C     VA3(XN)       IFNE      '   '                                         2<-B
     C                   MOVE      VA3(XN)       YVAL
     C     VALUFK        CHAIN     VALUL01                            33
     C   33              DO                                                       3<-B
     C     100           SUB       XN            X2                2 0
     C                   MOVE      '1'           *IN(X2)
     C                   END                                                      3<-E
     C   33
     CANN30              MOVE      'SPV0001'     MSGNR
     C   33
     CANN30              SETON                                        30
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C                   MOVE      *ZEROS        WSLCTO
     C     1             DO        11            XN
     C     KU3(XN)       IFEQ      0
     C                   MOVE      AM3(XN)       LC3(XN)
     C                   ELSE
     C     KU3(XN)       MULT      AM3(XN)       LC3(XN)
     C                   END
     C                   Z-ADD(H)  LC3(XN)       XWORK            11 0
     C                   Z-ADD     XWORK         LC3(XN)
     C                   ADD       LC3(XN)       WSLCTO
     C                   END
     C*
     C   30              EXSR      SETPC3
     C*
     C                   ENDSR
     C*
     C******************************************
     C     SETPC3        BEGSR
     C******************************************
     C   88
     COR 89
     COR 90
     COR 91
     COR 92
     COR 93              Z-ADD     32            POSNBR
     C   94
     COR 95
     COR 96
     COR 97
     COR 98
     COR 99              Z-ADD     32            POSNBR
     C*
     C   88              Z-ADD     22            LINNBR
     C   89              Z-ADD     21            LINNBR
     C   90              Z-ADD     20            LINNBR
     C   91              Z-ADD     19            LINNBR
     C   92              Z-ADD     18            LINNBR
     C   93              Z-ADD     17            LINNBR
     C   94              Z-ADD     16            LINNBR
     C   95              Z-ADD     15            LINNBR
     C   96              Z-ADD     14            LINNBR
     C   97              Z-ADD     13            LINNBR
     C   98              Z-ADD     12            LINNBR
     C   99              Z-ADD     11            LINNBR
     C*
     C                   ENDSR
     C*
     C******************************************
     C     CALTOT        BEGSR
     C******************************************
     C     1             DO        4             X2
     C                   MOVE      *BLANKS       VA2(X2)
     C                   MOVE      *BLANKS       KU2(X2)
     C                   MOVE      *ZEROS        PR2(X2)
     C                   MOVE      *ZEROS        P22(X2)
     C                   END
      *
     C     1             DO        10            XN                             1<-B
     C     VA1(XN)       IFNE      *BLANKS                                       2<-B
     C     1             DO        4             X2                               3<-B
     C     VA2(X2)       IFEQ      VA1(XN)                                         4<-B
     C                   ADD       PR1(XN)       PR2(X2)
     C                   GOTO      SLCALT
     C                   ELSE                                                      4
     C     VA2(X2)       IFEQ      *BLANKS                                          5<-B
     C                   MOVE      VA1(XN)       VA2(X2)
     C                   MOVE      KU1(XN)       KU2(X2)
     C                   MOVE      PR1(XN)       PR2(X2)
     C                   GOTO      SLCALT
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     SLCALT        TAG
     C                   END                                                    1<-E
      *
     C                   EXSR      CALT2
     C*
     C                   ENDSR
     C*
     C******************************************
     C     CALT2         BEGSR
     C******************************************
     C                   MOVE      *ZEROS        WSPRTL
     C     1             DO        4             XN
     C     KU2(XN)       IFEQ      0
     C                   MOVE      PR2(XN)       P22(XN)
     C                   ELSE
     C     PR2(XN)       MULT      KU2(XN)       P22(XN)
     C                   END
     C                   Z-ADD(H)  P22(XN)       XWORK            11 0
     C                   Z-ADD     XWORK         P22(XN)
     C                   ADD       P22(XN)       WSPRTL
     C                   END
      *
     C     KU2(5)        IFEQ      0
     C                   MOVE      PR2(5)        P22(5)
     C                   ELSE
     C     PR2(5)        MULT      KU2(5)        P22(5)
     C                   END
     C                   Z-ADD(H)  P22(5)        XWORK            11 0
     C                   Z-ADD     XWORK         P22(5)
     C                   SUB       P22(5)        WSPRTL
     C*
     C                   ENDSR
     C*
     C******************************************
     C     UPDFRM        BEGSR
     C******************************************
     C     FRMDK         CHAIN     FRMDOC                             33
     C                   EXSR      MOVWF
     C  N33              UPDATE    FRMDOCR
     C   33              WRITE     FRMDOCR
      *
     C     WSREFN        IFNE      *BLANKS
     C     BILLK         CHAIN     BOOKING                            33
     C  N33              MOVE      WSREFN        BOREFN
     C  N33              EXCEPT    OPDBO
     C                   END
      *
     C     1             DO        10            YN
     C     FRMD1K        CHAIN     FRMDOC1                            33
     C     FT1(YN)       IFEQ      *BLANKS
     C     KU1(YN)       ANDEQ     *ZEROS
     C     VA1(YN)       ANDEQ     *BLANKS
     C     PR1(YN)       ANDEQ     *ZEROS
     C     CO1(YN)       ANDEQ     *ZEROS
     C  N33              DELETE    FRMDOC1R
     C                   ELSE
     C                   EXSR      MOVWF1
     C  N33              UPDATE    FRMDOC1R
     C   33              WRITE     FRMDOC1R
     C                   END
     C                   END
      *
     C     1             DO        5             YN
     C     FRMD2K        CHAIN     FRMDOC2                            33
     C     KU2(YN)       IFEQ      *ZEROS
     C     VA2(YN)       ANDEQ     *BLANKS
     C     PR2(YN)       ANDEQ     *ZEROS
     C     P22(YN)       ANDEQ     *ZEROS
     C  N33              DELETE    FRMDOC2R
     C                   ELSE
     C                   EXSR      MOVWF2
     C  N33              UPDATE    FRMDOC2R
     C   33              WRITE     FRMDOC2R
     C                   END
     C                   END
      *
     C     1             DO        11            YN
     C     FRMD3K        CHAIN     FRMDOC3                            33
     C     KU3(YN)       IFEQ      *ZEROS
     C     VA3(YN)       ANDEQ     *BLANKS
     C     AM3(YN)       ANDEQ     *ZEROS
     C     LC3(YN)       ANDEQ     *ZEROS
     C  N33              DELETE    FRMDOC3R
     C                   ELSE
     C                   EXSR      MOVWF3
     C  N33              UPDATE    FRMDOC3R
     C   33              WRITE     FRMDOC3R
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVWF         BEGSR
     C******************************************
     C                   MOVE      ' '           FMST
     C                   MOVE      UAVD          FMAVD
     C                   MOVE      UOPD          FMOPD
     C                   MOVE      WSNAA         FMNAA
     C                   MOVE      WSTR          FMTR
     C                   MOVE      WSLSH         FMLSH
     C                   MOVE      WSDTD         FMDTD
     C                   MOVE      WSDTM         FMDTM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          FMDTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          FMDTÅ
     C                   END
     C                   MOVE      WSOB          FMOB
     C                   MOVE      WSREIS        FMREIS
     C                   MOVE      WSBLNR        FMBLNR
     C                   MOVE      WSPRTL        FMPRTL
     C                   MOVE      WSKUTA        FMKUTA
     C                   MOVE      WSVATA        FMVATA
     C                   MOVE      WSPRTA        FMPRTA
     C                   MOVE      WSLCTO        FMLCTO
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVWF1        BEGSR
     C******************************************
     C                   MOVE      UAVD          FMAVD
     C                   MOVE      UOPD          FMOPD
     C                   MOVE      YN            FMLN
     C                   MOVE      FT1(YN)       FMFT1
     C                   MOVE      KU1(YN)       FMKU1
     C                   MOVE      VA1(YN)       FMVA1
     C                   MOVE      PR1(YN)       FMPR1
     C                   MOVE      CO1(YN)       FMCO1
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVWF2        BEGSR
     C******************************************
     C                   MOVE      UAVD          FMAVD
     C                   MOVE      UOPD          FMOPD
     C                   MOVE      YN            FMLN
     C                   MOVE      KU2(YN)       FMKU2
     C                   MOVE      VA2(YN)       FMVA2
     C                   MOVE      PR2(YN)       FMPR2A
     C                   MOVE      P22(YN)       FMPR2B
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVWF3        BEGSR
     C******************************************
     C                   MOVE      UAVD          FMAVD
     C                   MOVE      UOPD          FMOPD
     C                   MOVE      YN            FMLN
     C                   MOVE      KU3(YN)       FMKU3
     C                   MOVE      VA3(YN)       FMVA3
     C                   MOVE      AM3(YN)       FMAM3
     C                   MOVE      LC3(YN)       FMLC3
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVFW         BEGSR
     C******************************************
     C                   MOVE      FMNAA         WSNAA
     C                   MOVE      FMTR          WSTR
     C                   MOVE      FMLSH         WSLSH
     C                   MOVE      FMDTD         WSDTD
     C                   MOVE      FMDTM         WSDTM
     C                   MOVE      FMDTÅ         WSDTÅ
     C                   MOVE      FMOB          WSOB
     C                   MOVE      FMREIS        WSREIS
     C                   MOVE      FMBLNR        WSBLNR
     C                   MOVE      FMPRTL        WSPRTL
     C                   MOVE      FMKUTA        WSKUTA
     C                   MOVE      FMVATA        WSVATA
     C                   MOVE      FMPRTA        WSPRTA
     C                   MOVE      FMLCTO        WSLCTO
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVFW1        BEGSR
     C******************************************
     C                   MOVE      FMLN          YN
     C                   MOVE      FMFT1         FT1(YN)
     C                   MOVE      FMKU1         KU1(YN)
     C                   MOVE      FMVA1         VA1(YN)
     C                   MOVE      FMPR1         PR1(YN)
     C                   MOVE      FMCO1         CO1(YN)
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVFW2        BEGSR
     C******************************************
     C                   MOVE      FMLN          YN
     C                   MOVE      FMKU2         KU2(YN)
     C                   MOVE      FMVA2         VA2(YN)
     C                   MOVE      FMPR2A        PR2(YN)
     C                   MOVE      FMPR2B        P22(YN)
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVFW3        BEGSR
     C******************************************
     C                   MOVE      FMLN          YN
     C                   MOVE      FMKU3         KU3(YN)
     C                   MOVE      FMVA3         VA3(YN)
     C                   MOVE      FMAM3         AM3(YN)
     C                   MOVE      FMLC3         LC3(YN)
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDT1        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C*
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     18
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSOB
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     49
     C     POSNBR        ANDLE     53
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    WSLSH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     70            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFGE      11
     C     LINNBR        ANDLE     20
     C     POSNBR        ANDGE     43
     C     POSNBR        ANDLE     45
     C     LINNBR        SUB       10            XN
     C                   MOVE      VA1(XN)       YVAL
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    YVAL
     C                   MOVE      YVAL          VA1(XN)
     C                   Z-ADD     49            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     SØKDT2        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C*
     C     LINNBR        IFGE      11
     C     LINNBR        ANDLE     15
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     31
     C     LINNBR        SUB       10            XN
     C                   MOVE      VA2(XN)       YVAL
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    YVAL
     C                   MOVE      YVAL          VA2(XN)
     C                   Z-ADD     65            POSNBR
     C                   GOTO      SLSØK2
     C                   END
     C*
     C     LINNBR        IFEQ      18
     C     POSNBR        ANDGE     60
     C     POSNBR        ANDLE     62
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    WSVATA
     C                   Z-ADD     65            POSNBR
     C                   GOTO      SLSØK2
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK2        ENDSR
     C*
     C***********************************************
     C     SØKDT3        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C*
     C     LINNBR        IFGE      11
     C     LINNBR        ANDLE     21
     C     POSNBR        ANDGE     34
     C     POSNBR        ANDLE     36
     C     LINNBR        SUB       10            XN
     C                   MOVE      VA3(XN)       YVAL
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    YVAL
     C                   MOVE      YVAL          VA3(XN)
     C                   Z-ADD     39            POSNBR
     C                   GOTO      SLSØK3
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK3        ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     BLLOKK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BILLK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     FRMDK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     FRMD1K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YN                2 0
     C     FRMD2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YN
     C     FRMD3K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YN
     C     VALUFK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YVAL
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    USG               3
     C                   PARM                    UFLAGG            1 0
     C*** UFLAGG = 1  -->  MULIG Å BE UTSKRIFT AV MANIFEST
     C     UFLAGG        COMP      1                                      27
     C     UFLAGG        COMP      2                                      28
     C*
     C                   MOVE      *BLANKS       YVAL              3
     C                   MOVE      'J'           USØK              1
     C                   MOVE      *BLANKS       FT1
     C                   MOVE      *ZEROS        KU1
     C                   MOVE      *BLANKS       VA1
     C                   MOVE      *ZEROS        PR1
     C                   MOVE      *ZEROS        CO1
     C                   MOVE      *BLANKS       VA2
     C                   MOVE      *ZEROS        PR2
     C                   MOVE      *ZEROS        KU2
     C                   MOVE      *ZEROS        P22
     C                   MOVE      *BLANKS       VA3
     C                   MOVE      *ZEROS        AM3
     C                   MOVE      *ZEROS        KU3
     C                   MOVE      *ZEROS        LC3
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        FMDT
     C                   MOVE      *ZEROS        BLDTVH
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     1             DO        5             XN
     C                   MOVE      FICURR        V22(XN)
     C                   END
     C                   MOVE      FICURR        WSVALC
     C                   MOVE      FICURR        WSVATL
     C*
     C     FRMDK         CHAIN(N)  FRMDOC                             33
     C  N33              DO
     C                   EXSR      MOVFW
     C     FRMD1K        SETLL     FRMDOC1
     C     STIN1         TAG
     C     UAVD          READE(N)  FRMDOC1                                33
     C  N33UOPD          COMP      FMOPD                              3333
     C  N33              EXSR      MOVFW1
     C  N33              GOTO      STIN1
     C                   MOVE      *ZEROS        YN
     C     FRMD2K        SETLL     FRMDOC2                            33
     C     STIN2         TAG
     C     UAVD          READE(N)  FRMDOC2                                33
     C  N33UOPD          COMP      FMOPD                              3333
     C  N33              EXSR      MOVFW2
     C  N33              GOTO      STIN2
     C                   MOVE      *ZEROS        YN
     C     FRMD3K        SETLL     FRMDOC3                            33
     C     STIN3         TAG
     C     UAVD          READE(N)  FRMDOC3                                33
     C  N33UOPD          COMP      FMOPD                              3333
     C  N33              EXSR      MOVFW3
     C  N33              GOTO      STIN3
     C                   SETOFF                                       33
     C                   END
     C   33              DO
     C     *IN28         IFEQ      '1'
      * VED SPØRRING RETURNERE HVIS FR.MANIFEST IKKE FINNES.
     C                   MOVE      *ZEROS        UFLAGG
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C     BILLK         CHAIN     BILLOF                             33
     C     BLLOKK        CHAIN     BLLOK                              33
     C                   MOVE      BENAM         WSNAA
     C                   MOVE      BLTR          WSTR
     C                   MOVE      BLVH          WSLSH
     C                   MOVE      BLDTVD        WSDTD
     C                   MOVE      BLDTVM        WSDTM
     C                   MOVE      BLDTVÅ        WSDTÅ
     C                   MOVE      BLOB          WSOB
     C                   MOVE      BLREIS        WSREIS
     C     BLBLNR        IFEQ      *BLANKS
     C                   MOVE      BLBBLN        WSBLNR
     C                   ELSE
     C                   MOVE      BLBLNR        WSBLNR
     C                   END
     C     BILLK         CHAIN(N)  BOOKING                            33
     C  N33              MOVE      BOREFN        WSREFN
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
     OBOOKINGR  E            OPDBO
     O                       BOREFN
