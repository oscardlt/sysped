      *
      *****************************************************************
      *    VEDLIKEHOLD AV AVVIKSRAPPORT (TILLEGGSINFO)
      *
      *****************************************************************
      *
     FISOLFL1   UF A E           K DISK
     FISOKDF    IF   E           K DISK
     FISOKDP    IF   E           K DISK
     FSYIS10DD  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
      * DS FOR DATOFORMATERING
      *
     D                 DS
     D  W2LRDA                 1      6  0
     D  W2LRDAD                1      2  0
     D  W2LRDAM                3      4  0
     D  W2LRDAÅ                5      6  0
     D                 DS
     D  WSLRDA                 1      6  0
     D  WSLRDAD                1      2  0
     D  WSLRDAM                3      4  0
     D  WSLRDAÅ                5      6  0
     D                 DS
     D  ISLRDA                 1      8  0
     D  ISLRDAÅ                1      4  0
     D  ISLRDAM                5      6  0
     D  ISLRDAD                7      8  0
     D                 DS
     D  ISLAV1                 1     75
     D  ISLAV101               1     54
     D                 DS
     D  FTEXT                  1     35
     D  FTEXT20                1     20
      *
     C     ISOLFK        KLIST
     C                   KFLD                    USAKNR
     C                   KFLD                    ISLLNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    USAKNR            7 0
     C                   PARM                    UTYPE             3
     C     UTYPE         COMP      'DSP'                                  51
      *
     C                   EXSR      FRASR
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     FRASR         BEGSR
      ***********************************************
     C     STTUR         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XSISTN            3 0
      ****************
      * Fyll Subfile *
      ****************
      * LES ALLE POSTER MED OPPDRAGSNR
     C     USAKNR        SETLL     ISOLFL1
     C     USAKNR        READE(N)  ISOLFL1                                94
     C                   DOW       *IN94 = *OFF
     C                   MOVE      ISLRDAÅ       W2LRDAÅ
     C                   MOVE      ISLRDAM       W2LRDAM
     C                   MOVE      ISLRDAD       W2LRDAD
     C                   ADD       1             ZRECNO
     C                   MOVE      ISLLNR        XSISTN
     C     ISLPFK        CHAIN     ISOKDF                             33
     C     ISLANS        CHAIN     ISOKDP                             33
     C                   WRITE     SUBFIL
     C     USAKNR        READE(N)  ISOLFL1                                94
     C                   ENDDO
      *
     C                   IF        ZRECNO > 1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   MOVE      ZRECNO        XRECNO            4 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     STTUR3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NULLPS
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C   12              GOTO      UT
     C   03              GOTO      UT
     C     *IN05         IFEQ      '1'
     C                   GOTO      STTUR
     C                   END                                                    <*IN05
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   EXSR      MOVB
     C                   MOVE      '0'           *IN40
     C                   MOVEA     '000'         *IN(87)
     C                   GOTO      STTUR5
     C                   END                                                    <*IN06
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C                   GOTO      STTUR
      *
     C     STTUR5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(87)
     C                   MOVE      '0'           *IN30
      *** F12=Avbryt
     C                   IF        *IN12
     C                   EXSR      MOVB
     C                   ELSE
     C                   IF        *IN04
     C                   EXSR      SØKDTA
     C                   GOTO      STTUR5
     C                   END
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STTUR5
     C                   END                                                    <*IN12
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      STTUR3
      *
     C     UT            ENDSR
      *
      ***********************************************
     C     HHENT         BEGSR
      ***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   SETOFF                                       9830
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     ISLAV101      ANDNE     '*SLETTET'
     C     *IN51         ANDEQ     *OFF
     C                   EXSR      CHGPOS
     C                   EXSR      POSLIN
     C     *IN03         CABEQ     '1'           UTHENT
     C     *IN12         CABEQ     '1'           UTHENT
     C                   MOVE      '0'           *IN40
     C                   END
      *
     C  N98WSNR          IFEQ      '3'
     C     ISLAV101      ANDNE     '*SLETTET'
     C     *IN51         ANDEQ     *OFF
     C                   EXSR      CPYPOS
     C     *IN03         CABEQ     '1'           UTHENT
     C     *IN12         CABEQ     '1'           UTHENT
     C                   MOVE      '0'           *IN40
     C                   END
      *
     C  N98WSNR          IFEQ      '4'
     C     ISLAV101      ANDNE     '*SLETTET'
     C     *IN51         ANDEQ     *OFF
     C                   EXSR      DLTPOS
     C     *IN03         CABEQ     '1'           UTHENT
     C     *IN12         CABEQ     '1'           UTHENT
     C                   MOVE      '0'           *IN40
     C                   END
      *
     C  N98WSNR          IFEQ      '5'
     C     ISLAV101      ANDNE     '*SLETTET'
     C                   EXSR      DSPPOS
     C     *IN03         CABEQ     '1'           UTHENT
     C     *IN12         CABEQ     '1'           UTHENT
     C                   MOVE      '0'           *IN40
     C                   END
      *
     C                   ENDDO
      *
     C     UTHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C     WSLPFK        CHAIN     ISOKDF                             89
     C                   IF        *IN89
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      'YBC1927'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSLANS        CHAIN     ISOKDP                             88
     C                   IF        *IN88
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     53            POSNBR
     C                   MOVE      'YBC1928'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSLAV1        COMP      *BLANKS                                87
     C                   IF        *IN87
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'YBC1929'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   IF        ISLLNR = 0
     C                   ADD       1             XSISTN
     C                   MOVE      XSISTN        ISLLNR
     C                   MOVE      USAKNR        ISLSAK
     C                   MOVE      WDT           ISLRDA
     C                   MOVE      WTM           ISLRTM
     C                   MOVE      *ON           *IN33
     C                   ELSE
     C     ISOLFK        CHAIN     ISOLFL1                            33
     C                   END                                                    <*IN40
      *
     C                   MOVE      WSLPFK        ISLPFK
     C                   MOVE      WSLANS        ISLANS
     C                   MOVE      WSLAV1        ISLAV1
     C                   MOVE      WSLAV2        ISLAV2
     C                   MOVE      WSLAV3        ISLAV3
     C                   MOVE      WDT           ISLRDS
     C                   MOVE      WTM           ISLRTS
      *
     C                   MOVE      ' '           WSNR
     C                   IF        *IN33
     C                   WRITE     ISOLFR
     C                   MOVE      ISLRDAÅ       W2LRDAÅ
     C                   MOVE      ISLRDAM       W2LRDAM
     C                   MOVE      ISLRDAD       W2LRDAD
     C                   ADD       1             XRECNO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   ELSE
     C                   UPDATE    ISOLFR
     C                   UPDATE    SUBFIL
     C                   END                                                    <*IN40
     C                   WRITE     SUBCTL
      *
     C     SLTB1         TAG
     C  N30              EXSR      MOVB
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       WSLPFK
     C                   MOVE      *BLANKS       WSLANS
     C                   MOVE      *BLANKS       WSLAV1
     C                   MOVE      *BLANKS       WSLAV2
     C                   MOVE      *BLANKS       WSLAV3
     C                   MOVE      *ZEROS        ISLLNR
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGPOS        BEGSR
      ***********************************************
      *
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     14            POSNBR
      *
     C                   MOVE      ISLPFK        WSLPFK
     C                   MOVE      ISLANS        WSLANS
     C                   MOVE      ISLAV1        WSLAV1
     C                   MOVE      ISLAV2        WSLAV2
     C                   MOVE      ISLAV3        WSLAV3
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '000'         *IN(87)
      *
     C                   IF        *IN12
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB
     C                   ELSE
     C                   IF        *IN04
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STCHG
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     CPYPOS        BEGSR
      ***********************************************
      *
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     14            POSNBR
      *
     C                   MOVE      ISLPFK        WSLPFK
     C                   MOVE      ISLANS        WSLANS
     C                   MOVE      ISLAV1        WSLAV1
     C                   MOVE      ISLAV2        WSLAV2
     C                   MOVE      ISLAV3        WSLAV3
     C                   MOVE      *ZEROS        ISLLNR
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '000'         *IN(87)
      *
     C                   IF        *IN12
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB
     C                   ELSE
     C                   IF        *IN04
     C                   EXSR      SØKDTA
     C                   GOTO      STCPY
     C                   END
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STCPY
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     DLTPOS        BEGSR
      ***********************************************
      *
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     14            POSNBR
      *
     C                   EXFMT     VAREL3
      *
     C                   IF        *IN12 = *OFF
     C     ISOLFK        CHAIN     ISOLFL1                            33
     C                   DELETE    ISOLFR
     C                   MOVE      *BLANKS       ISLPFK
     C                   MOVE      *BLANKS       ISLANS
     C                   EVAL      ISLAV1 = '*SLETTET'
     C                   MOVE      *BLANKS       ISLAV2
     C                   MOVE      *BLANKS       ISLAV3
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   WRITE     SUBCTL
      *
     C                   ENDSR
      *
      ***********************************************
     C     DSPPOS        BEGSR
      ***********************************************
      *
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     14            POSNBR
      *
     C                   EXFMT     VAREL3
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   WRITE     SUBCTL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SØKDTA        BEGSR
      ***********************************************
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * PARTSFORKLARING
     C     LINNBR        IFEQ      19
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     18
     C                   MOVE      *BLANKS       UFLG
     C                   CALL      'SYIS11'
     C                   PARM                    WSLPFK
     C                   PARM                    UFLG              2
     C     WSLPFK        CHAIN     ISOKDF                             33
     C   33              MOVE      *BLANKS       ATEXT
     C                   Z-ADD     53            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * ANSVARLIG PART
     C     LINNBR        IFEQ      19
     C     POSNBR        ANDGE     53
     C     POSNBR        ANDLE     57
     C                   MOVE      *BLANKS       UFLG
     C                   CALL      'SYIS13'
     C                   PARM                    WSLANS
     C                   PARM                    UFLG              2
     C     WSLANS        CHAIN     ISOKDP                             33
     C   33              MOVE      *BLANKS       PTEXT
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     SLSØK         ENDSR
      *
      ***********************************************
     C     POSLIN        BEGSR
      ***********************************************
      *
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
      *
     C                   ENDSR
      *
