      *
      ***********************************************************
      *** PROGRAM SKAL REGISTRERE EKSPR.FORT. DEFAULT MASTER  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-46 50-79          ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEXDEF  UF A E           K DISK
     FTRAN      IF   E           K DISK
     FTELL      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTTST   IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FTRKODL01  IF   E           K DISK
     FSAD169D   CF   E             WORKSTN INFDS(FBAREA)
     D                SDS
     D  UUSR                 254    263
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSETDDD                1      2  0
     D  WSETDDM                3      4  0
     D  WSETDDA                5      6  0
     D  WSETDD                 1      6  0
     D                 DS
     D  EMETDDC                1      2  0
     D  EMETDDA                3      4  0
     D  EMETDDM                5      6  0
     D  EMETDDD                7      8  0
     D  EMETDD                 1      8  0
     D                 DS
     D  WSETADD                1      2  0
     D  WSETADM                3      4  0
     D  WSETADA                5      6  0
     D  WSETAD                 1      6  0
     D                 DS
     D  EMETADC                1      2  0
     D  EMETADA                3      4  0
     D  EMETADM                5      6  0
     D  EMETADD                7      8  0
     D  EMETAD                 1      8  0
     D                 DS
     D  WSATDDD                1      2  0
     D  WSATDDM                3      4  0
     D  WSATDDA                5      6  0
     D  WSATDD                 1      6  0
     D                 DS
     D  EMATDDC                1      2  0
     D  EMATDDA                3      4  0
     D  EMATDDM                5      6  0
     D  EMATDDD                7      8  0
     D  EMATDD                 1      8  0
     D                 DS
     D  WSATADD                1      2  0
     D  WSATADM                3      4  0
     D  WSATADA                5      6  0
     D  WSATAD                 1      6  0
     D                 DS
     D  EMATADC                1      2  0
     D  EMATADA                3      4  0
     D  EMATADM                5      6  0
     D  EMATADD                7      8  0
     D  EMATAD                 1      8  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  EMDTRC                 1      2  0
     D  EMDTRA                 3      4  0
     D  EMDTRM                 5      6  0
     D  EMDTRD                 7      8  0
     D  EMDTR                  1      8  0
     D                 DS
     D  WSSJADTD               1      2  0
     D  WSSJADTM               3      4  0
     D  WSSJADTA               5      6  0
     D  WSSJADT                1      6  0
     D                 DS
     D  EMSJADTC               1      2  0
     D  EMSJADTA               3      4  0
     D  EMSJADTM               5      6  0
     D  EMSJADTD               7      8  0
     D  EMSJADT                1      8  0
     D                 DS
     D  WSSJ2DTD               1      2  0
     D  WSSJ2DTM               3      4  0
     D  WSSJ2DTA               5      6  0
     D  WSSJ2DT                1      6  0
     D                 DS
     D  EMSJ2DTC               1      2  0
     D  EMSJ2DTA               3      4  0
     D  EMSJ2DTM               5      6  0
     D  EMSJ2DTD               7      8  0
     D  EMSJ2DT                1      8  0
     D                 DS
     D  WSETDTH                1      2  0
     D  WSETDTM                3      4  0
     D  WSETDTS                5      6  0
     D  WSETDT                 1      4  0
     D  WSETDT6                1      6  0
     D                 DS
     D  EMETDTH                1      2  0
     D  EMETDTM                3      4  0
     D  EMETDTS                5      6  0
     D  EMETDT4                1      4  0
     D  EMETDT                 1      6  0
     D                 DS
     D  WSETATH                1      2  0
     D  WSETATM                3      4  0
     D  WSETATS                5      6  0
     D  WSETAT                 1      4  0
     D  WSETAT6                1      6  0
     D                 DS
     D  EMETATH                1      2  0
     D  EMETATM                3      4  0
     D  EMETATS                5      6  0
     D  EMETAT4                1      4  0
     D  EMETAT                 1      6  0
     D                 DS
     D  WSATDTH                1      2  0
     D  WSATDTM                3      4  0
     D  WSATDTS                5      6  0
     D  WSATDT                 1      4  0
     D  WSATDT6                1      6  0
     D                 DS
     D  EMATDTH                1      2  0
     D  EMATDTM                3      4  0
     D  EMATDTS                5      6  0
     D  EMATDT4                1      4  0
     D  EMATDT                 1      6  0
     D                 DS
     D  WSATATH                1      2  0
     D  WSATATM                3      4  0
     D  WSATATS                5      6  0
     D  WSATAT                 1      4  0
     D  WSATAT6                1      6  0
     D                 DS
     D  EMATATH                1      2  0
     D  EMATATM                3      4  0
     D  EMATATS                5      6  0
     D  EMATAT4                1      4  0
     D  EMATAT                 1      6  0
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
      *
      *  Prototype for 'SOEKKUN'
     d SOEKKUN         pr                  extpgm('SOEKKUN')
     d wsknd_                              like(wsknd)
     d fifirm_                             like(fifirm)
     d wsnad_                              like(wsnad)
      *  Prototype for 'SYFAKDA'
     d SYFAKDA         pr                  extpgm('SYFAKDA')
     d kftyp_                              like(kftyp)
     d uupdat_                             like(uupdat)
     d xkfkod_                             like(xkfkod)
      *  Prototype for 'SYFI10L'
     d SYFI10L         pr                  extpgm('SYFI10L')
     d wsklk_                              like(wsklk)
     d usped_                              like(usped)
      *  Prototype for 'SYFI10F'
     d SYFI10F         pr                  extpgm('SYFI10F')
     d usd_                                like(usd)
     d unvn_                               like(unvn)
     d ulk_                                like(ulk)
      *  Prototype for 'TR002R'
     d TR002R          pr                  extpgm('TR002R')
     d uunik_                              like(uunik)
     d ukode_                              like(ukode)
     d utype2_                             like(utype2)
      *  Prototype for 'TESTDATO'
     d TESTDATO        pr                  extpgm('TESTDATO')
     d xdg_                                like(XDG)
     d xmn_                                like(XMN)
     d xar_                                like(XAR)
     d uflagg_                             like(UFLAGG)
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for SAD169R
     d sad169r         pr
     d uavd_                               like(uavd)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad169r         pi
     d uavd                           4  0
     d utype                          3a
     d uflagg                         1  0
      *
     d wdt             s              8a
     d wtm             s              6a
     d usped           s              1a
     d uupdat          s              1a
     d xkfkod          s                   like(kfkod)
     d xrecno          s              4  0
     d xidag           s              8  0
     d xtid            s              4  0
     d xdg             s              2  0
     d xmn             s              2  0
     d xar             s              2  0
     d xpos            s              3  0
     d i               s              3s 0
     d uunik           s              3a
     d ukode           s             10a
     d utype2          s              3a
     d usd             s              5a
     d unvn            s             30a
     d ulk             s              2a
      *
      /free

         exsr init;

         dow *in03 = *off and *in12 = *off;
           exfmt bilde01;
           //*in(50) = '00000000000000000';
           for i = 50 to 99;
             *in(i) = *off;
           endfor;
           *in30 = *off;

           select;
           when *in04;
             exsr soekdta;
           when *in12;
           when *in03 = *off;
             if *in49;
               leave;
             else;
               exsr testb1;
               if *in30 = *off;
                 exsr opdtur;
                 leave;
               endif;
             endif;
           endsl;

         enddo;

         select;
         when *in03;
           uflagg = 3;
         when *in12;
           uflagg = 1;
         other;
           uflagg = 0;
         endsl;

         *inlr = *on;
         return;

      //*********************************************
       begsr soekdta;

       linnbr = wscloc / 256;
       posnbr = wscloc - (%int(linnbr) * 256);
       xrecno = wssubn;

         select;
           when linnbr = 4 and posnbr >= 15 and posnbr <= 22;
         // SØK DEKLARANT
             soekkun (wsknd: fifirm: wsnad);
             if wsknd <> 0;
               exsr testd;
             endif;
             linnbr = 7;
             posnbr = 15;

         when linnbr = 5 and posnbr >= 101 and posnbr <= 102;
         // SØK landkode
           syfi10l (wslkd: usped);
           linnbr = 5;
           posnbr = 108;

         when linnbr = 7 and posnbr >= 15 and posnbr <= 22;
         // SØK REPRESENTATIVE
           soekkun (wsknr: fifirm: wsnar);
           if wsknr <> 0;
             exsr testr;
           endif;
           linnbr = 10;
           posnbr = 15;

         when linnbr = 7 and posnbr >= 20 and posnbr <= 22;
         // SØK transportmåte
           // xxxxxxx (wstm);
           linnbr = 8;
           posnbr = 20;

         when linnbr = 8 and posnbr >= 101 and posnbr <= 102;
         // SØK landkode
           syfi10l (wslkr: usped);
           linnbr = 8;
           posnbr = 108;

         when linnbr = 10 and posnbr >= 52 and posnbr <= 53;
         // SØK kjøretøy kode, typeOfIdentification
           uunik = '750';
           ukode = *blanks;
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wsktyp = ukode;
           endif;
           chain (tkunik: tkkode) trkodl01;
           if %found;
             wsktypt = tktxtn;
           else;
             wsktypt = *blanks;
           endif;
           linnbr = 10;
           posnbr = 65;

         when linnbr = 10 and posnbr >= 65 and posnbr <= 68;
         // SØK typeOfMeansOfTransport
           uunik = '751';
           ukode = *blanks;
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wsktm = ukode;
           endif;
           chain (tkunik: tkkode) trkodl01;
           if %found;
             wsktmt = tktxtn;
           else;
             wsktmt = *blanks;
           endif;
           linnbr = 10;
           posnbr = 74;

         when linnbr = 10 and posnbr >= 74 and posnbr <= 75;
         // SØK landkode
           syfi10l (wsklk: usped);
           linnbr = 10;
           posnbr = 82;

         when linnbr = 10 and posnbr = 82;
         // SØK typeOfMeansOfTransport
           uunik = '018';
           ukode = *blanks;
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wsktkd= ukode;
           endif;
           chain (tkunik: tkkode) trkodl01;
           linnbr = 10;
           posnbr = 90;

         when linnbr = 10 and posnbr >= 90 and posnbr <= 91;
         // SØK landkode
           syfi10l (wslkr1: usped);
           linnbr = 10;
           posnbr = 93;

         when linnbr = 10 and posnbr >= 93 and posnbr <= 94;
         // SØK landkode
           syfi10l (wslkr2: usped);
           linnbr = 10;
           posnbr = 96;

         when linnbr = 10 and posnbr >= 96 and posnbr <= 97;
         // SØK landkode
           syfi10l (wslkr3: usped);
           linnbr = 10;
           posnbr = 99;

         when linnbr = 10 and posnbr >= 99 and posnbr <= 100;
         // SØK landkode
           syfi10l (wslkr4: usped);
           linnbr = 10;
           posnbr = 102;

         when linnbr = 10 and posnbr >= 102 and posnbr <= 103;
         // SØK landkode
           syfi10l (wslkr5: usped);
           linnbr = 10;
           posnbr = 105;

         when linnbr = 10 and posnbr >= 105 and posnbr <= 106;
         // SØK landkode
           syfi10l (wslkr6: usped);
           linnbr = 10;
           posnbr = 108;

         when linnbr = 10 and posnbr >= 108 and posnbr <= 109;
         // SØK landkode
           syfi10l (wslkr7: usped);
           linnbr = 10;
           posnbr = 111;

         when linnbr = 10 and posnbr >= 111 and posnbr <= 112;
         // SØK landkode
           syfi10l (wslkr8: usped);
           linnbr = 11;
           posnbr = 15;

         when linnbr = 11 and posnbr >= 52 and posnbr <= 53;
         // SØK kjøretøy kode, typeOfIdentification
           uunik = '750';
           ukode = *blanks;
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wsptyp = ukode;
           endif;
           linnbr = 11;
           posnbr = 65;

         when linnbr = 11 and posnbr >= 65 and posnbr <= 68;
         // SØK typeOfMeansOfTransport
           uunik = '751';
           ukode = *blanks;
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wsptm = ukode;
           endif;
           chain (tkunik: tkkode) trkodl01;
           if %found;
             wsptmt = tktxtn;
           else;
             wsptmt = *blanks;
           endif;
           linnbr = 11;
           posnbr = 74;

         when linnbr = 11 and posnbr >= 74 and posnbr <= 75;
         // SØK landkode
           syfi10l (wsplk: usped);
           linnbr = 12;
           posnbr = 15;

         when linnbr = 13 and posnbr >= 80 and posnbr <= 81;
         // SØK landkode
           syfi10l (wssjalk: usped);
           posnbr = 96;

         when linnbr = 14 and posnbr >= 80 and posnbr <= 81;
         // SØK landkode
           syfi10l (wssj2lk: usped);
           posnbr = 96;

         when linnbr = 15 and posnbr >= 15 and posnbr <= 22;
         // SØK tollsted
           uunik = '106';
           ukode = *blanks;
           utype2 = 'TRS';
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wstsd = ukode;
           endif;
           posnbr = 34;

         when linnbr = 16 and posnbr >= 104 and posnbr <= 105;
         // SØK andre aktors rolle
           uunik = '704';
           ukode = *blanks;
           tr002r (uunik: ukode: utype2);
           if ukode <> *blanks;
             wsscar = ukode;
           endif;
           linnbr = 17;
           posnbr = 15;

         when linnbr = 17 and posnbr >= 15 and posnbr <= 16;
         // SØK landkode
           syfi10l (wslkl: usped);
           posnbr = 18;

         when linnbr = 17 and posnbr >= 18 and posnbr <= 22;
         // SØK sted
           unvn = *blanks;
           syfi10f (wssdl: unvn: ulk);
           chain (ulk: wssdl) sted2d;
           if %found;
             wssdlt = st2nvn;
           endif;
           posnbr = 75;

         when linnbr = 17 and posnbr >= 75 and posnbr <= 76;
         // SØK landkode
           syfi10l (wslku: usped);
           posnbr = 78;

         when linnbr = 17 and posnbr >= 78 and posnbr <= 82;
         // SØK sted
           unvn = *blanks;
           syfi10f (wssdu: unvn: ulk);
           chain (ulk: wssdu) sted2d;
           if %found;
             wssdut = st2nvn;
           endif;
           posnbr = 75;

         when linnbr = 18 and posnbr >= 15 and posnbr <= 22;
         // SØK TRANSPORTØR
           soekkun (wsknt: fifirm: wsnat);
           if wsknd <> 0;
             exsr testt;
           endif;
           linnbr = 21;
           posnbr = 15;

         when linnbr = 18 and posnbr >= 101 and posnbr <= 102;
         // SØK landkode
           syfi10l (wslkd: usped);
           linnbr = 18;
           posnbr = 108;

         when linnbr = 21 and posnbr >= 73 and posnbr <= 76;
         // SØK landkode
           tkunik = '754';
           tkkode = wsdkmt;
           tr002r (tkunik: tkkode: utype2);
           wsdkmt = tkkode;
           linnbr = 21;
           posnbr = 78;

         other;
           msgnr = 'DIV1037';
           *in30 = *on;

         endsl;

       endsr;

      //*********************************************
       begsr testb1;

      //*** TEST AVDELING
       if *in48 = *off;
         chain wsavd tell;
         if not %found;
           *in99 = *on;
         endif;
       endif;

      //*** TEST DEKLARANT
       if wsknd <> 0;
         exsr testd;
       endif;

       if wsrgd = *blanks;
         *in97 = *on;
       endif;

       if wsnad = *blanks;
         *in96 = *on;
       endif;

       if wsad1d = *blanks;
         *in95 = *on;
       endif;

       if wspnd = *blanks;
         *in94 = *on;
       endif;

       if wspsd = *blanks;
         *in93 = *on;
       endif;

       if wslkd <> *blanks;
         chain ('S2 ': wslkd) kodts2;
         if not %found;
           *in92 = *on;
         endif;
       endif;

       //if wsemd = *blanks;
       //  *in91 = *on;
       //endif;

       xpos = %scan('@': wsemd);
       select;
       when wsemd = *blanks;
         wsemdt = '  ';
       when xpos > 0;
         wsemdt = 'EM';
       other;
         wsemdt = 'TE';
       endsl;

      //*** TEST REPRESENTATIV
       if wsknr <> 0;
         exsr testr;
       endif;

       if wsrgr = *blanks or wsrgr <> *blanks and wsstr = ' ';
       //*in89 = *on;
       endif;

       if wsnar = *blanks;
       //*in88 = *on;
       endif;

       if wsad1r = *blanks;
       //*in87 = *on;
       endif;

       if wspnr = *blanks;
       //*in86 = *on;
       endif;

       if wspsr = *blanks;
       //*in85 = *on;
       endif;

       if wslkr <> *blanks;
         chain ('S2 ': wslkr) kodts2;
         if not %found;
           *in84 = *on;
         endif;
       endif;

       if wsemr = *blanks;
       //*in83 = *on;
       endif;

       if wskmrk = *blanks;
       //*in54 = *on;
       endif;

       if wsktyp <> *blanks;
         uunik = '750';
         ukode = wsktyp;
         chain (uunik: ukode) trkodl01;
         if not %found;
           *in53 = *on;
         endif;
       endif;

       if wsktm <> *blanks;
         uunik = '751';
         ukode = wsktm;
         chain (uunik: ukode) trkodl01;
         if not %found;
           *in52 = *on;
         endif;
       endif;

       if wsptyp <> *blanks;
         uunik = '750';
         ukode = wsptyp;
         chain (tkunik: tkkode) trkodl01;
       endif;

       if wsptm <> *blanks;
         uunik = '751';
         ukode = wsptm;
         chain (tkunik: tkkode) trkodl01;
       endif;

       xpos = %scan('@': wsemr);
       select;
       when wsemr = *blanks;
         wsemrt = '  ';
       when xpos > 0;
         wsemrt = 'EM';
       other;
         wsemrt = 'TE';
       endsl;

        //*** TEST ActiveBorderTransportMeans
       if wsplk <> *blanks or wspmrk <> *blanks;
         chain ('S2 ': wsklk) kodts2;
         if not %found;
           *in82 = *on;
         endif;
       endif;

       if wsktkd <> *blanks;
         uunik = '018';
         ukode = wsktkd;
         chain (uunik: ukode) trkodl01;
         if not %found;
           *in51 = *on;
         endif;
       endif;

       if wslkr1 <> *blanks;
         chain ('S2 ': wslkr1) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 90;
         endif;
       endif;

       if wslkr2 <> *blanks;
         chain ('S2 ': wslkr2) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 93;
         endif;
       endif;

       if wslkr3 <> *blanks;
         chain ('S2 ': wslkr3) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 96;
         endif;
       endif;

       if wslkr4 <> *blanks;
         chain ('S2 ': wslkr4) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 99;
         endif;
       endif;

       if wslkr5 <> *blanks;
         chain ('S2 ': wslkr5) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 102;
         endif;
       endif;

       if wslkr6 <> *blanks;
         chain ('S2 ': wslkr6) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 105;
         endif;
       endif;

       if wslkr7 <> *blanks;
         chain ('S2 ': wslkr7) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 108;
         endif;
       endif;

       if wslkr8 <> *blanks;
         chain ('S2 ': wslkr8) kodts2;
         if not %found;
           *in81 = *on;
           posnbr = 111;
         endif;
       endif;

        //*** TEST Passiv kjøretøy
       if wsplk <> *blanks or wspmrk <> *blanks;
         chain ('S2 ': wsplk) kodts2;
         if not %found;
           *in80 = *on;
         endif;
       endif;

        //*** TEST diverse dato
       if wsetdd <> 0;
         xar = wsetdda;
         xmn = wsetddm;
         xdg = wsetddd;
         testdato (xdg: xmn: xar: uflagg);
         if uflagg = 1;
           *in79 = *on;
         endif;
       endif;

       if wsetdth >= 24 or wsetdtm >= 60;
         *in78 = *on;
       endif;

       if wsetad <> 0;
         xar = wsetada;
         xmn = wsetadm;
         xdg = wsetadd;
         testdato (xdg: xmn: xar: uflagg);
         if uflagg = 1;
           *in77 = *on;
         endif;
       endif;

       if wsetat = *zeros;
       //*in76 = *on;
       endif;
       if wsetath >= 24 or wsetatm >= 60;
         *in76 = *on;
       endif;

       if wsatdd <> 0;
         xar = wsatdda;
         xmn = wsatddm;
         xdg = wsatddd;
         testdato (xdg: xmn: xar: uflagg);
         if uflagg = 1;
           *in75 = *on;
         endif;
       endif;

       if wsatdth >= 24 or wsatdtm >= 60;
         *in74 = *on;
       endif;

       if wsatad <> 0;
         xar = wsatada;
         xmn = wsatadm;
         xdg = wsatadd;
         testdato (xdg: xmn: xar: uflagg);
         if uflagg = 1;
           *in73 = *on;
         endif;
       endif;

       if wsatath >= 24 or wsatatm >= 60;
         *in72 = *on;
       endif;

        //*** TEST Sjåfør
       if wssjaf = *blanks;
       //*in50 = *on;
       endif;

       if wssjalk <> *blanks or wssjaf <> *blanks;
         chain ('S2 ': wssjalk) kodts2;
         if not %found;
           *in71 = *on;
         endif;
       endif;

       if wssjadt <> 0 or wssjaf <> *blanks;
         xar = wssjadta;
         xmn = wssjadtm;
         xdg = wssjadtd;
         testdato (xdg: xmn: xar: uflagg);
          if uflagg = 1;
           *in70 = *on;
         endif;
       endif;

       if wssj2lk <> *blanks or wssj2f <> *blanks;
         chain ('S2 ': wssj2lk) kodts2;
         if not %found;
           *in69 = *on;
         endif;
       endif;

       if wssj2dt <> 0 or wssj2f <> *blanks;
         xar = wssj2dta;
         xmn = wssj2dtm;
         xdg = wssj2dtd;
         testdato (xdg: xmn: xar: uflagg);
         if uflagg = 1;
           *in68 = *on;
         endif;
       endif;

        //*** TEST Diverse
       if wstsd <> *blanks;
         tkkode = wstsd;
         chain ('106': tkkode) trkodl01;
         if not %found;
           *in67 = *on;
         endif;
       endif;

        //*** TEST andre aktors rolle
       if wsscar <> *blanks;
         tkkode = wsscar;
         chain ('704': tkkode) trkodl01;
         if not %found;
         //*inxx = *on;
         endif;
       endif;

        //*** TEST Loading and unloading places
       if wslkl <> *blanks;
         chain ('S2 ': wslkl) kodts2;
         if not %found;
           *in66 = *on;
         endif;
       endif;

       if wssdl <> *blanks;
         chain (wslkl: wssdl) sted2d;
         if not %found;
           *in65 = *on;
         else;
           if wssdlt = *blanks;
             wssdlt = st2nvn;
           endif;
         endif;
       endif;

       if wslku <> *blanks;
         chain ('S2 ': wslku) kodts2;
         if not %found;
           *in64 = *on;
         endif;
       endif;

       if wssdu <> *blanks;
         chain (wslku: wssdu) sted2d;
         if not %found;
           *in63 = *on;
         else;
           if wssdut = *blanks;
             wssdut = st2nvn;
           endif;
         endif;
       endif;

        //*** TEST Transportør
       if wsknt <> 0;
         exsr testt;
       endif;

       if wsrgt = *blanks;
       //*in61 = *on;
       endif;

       if wsnat = *blanks;
       //*in60 = *on;
       endif;

       if wsad1t = *blanks;
       //*in59 = *on;
       endif;

       if wspnt = *blanks;
       //*in58 = *on;
       endif;

       if wspst = *blanks;
       //*in57 = *on;
       endif;

       if wslkt <> *blanks;
         chain ('S2 ': wslkt) kodts2;
         if not %found;
           *in56 = *on;
         endif;
       endif;

       if wsemt = *blanks;
       //*in55 = *on;
       endif;

       xpos = %scan('@': wsemt);
       select;
       when wsemt = *blanks;
         wsemtt = '  ';
       when xpos > 0;
         wsemtt = 'EM';
       other;
         wsemtt = 'TE';
       endsl;

       if wsdkmt <> *blanks;
         uunik = '754';
         ukode = wsdkmt;
         chain (uunik: ukode) trkodl01;
         if *in81 = *off;
           if not %found;
             msgnr = 'YBC2199';
             *in30 = *on;
             linnbr = 21;
             posnbr = 73;
           else;
             if wsdkm = *blanks;
             //msgnr = 'YBC2200';
             //*in30 = *on;
             //linnbr = 21;
             //posnbr = 78;
             endif;
           endif;
         endif;
       endif;

       exsr hmsg;

       endsr;

      //***********************************************
       begsr hmsg;

       select;
       // Deklarant
       when *in99;
         msgnr = 'SPA0001';
         *in30 = *on;
         linnbr = 2;
         posnbr = 7;

       when *in98;
         msgnr = 'YBC2037';
         *in30 = *on;
         linnbr = 4;
         posnbr = 15;

       when *in97;
         msgnr = 'YBC2038';
         *in30 = *on;
         linnbr = 4;
         posnbr = 33;

       when *in96;
         msgnr = 'YBC2039';
         *in30 = *on;
         linnbr = 4;
         posnbr = 59;

       when *in95;
         msgnr = 'YBC2040';
         *in30 = *on;
         linnbr = 5;
         posnbr = 15;

       when *in94;
         msgnr = 'YBC2135';
         *in30 = *on;
         linnbr = 5;
         posnbr = 62;

       when *in93;
         msgnr = 'YBC2041';
         *in30 = *on;
         linnbr = 5;
         posnbr = 72;

       when *in92;
         msgnr = 'YBC2042';
         *in30 = *on;
         linnbr = 5;
         posnbr = 101;

       when *in91;
         msgnr = 'YBC2043';
         *in30 = *on;
         linnbr = 6;
         posnbr = 15;

       // Representativ
       when *in90;
         msgnr = 'YBC2136';
         *in30 = *on;
         linnbr = 7;
         posnbr = 15;

       when *in89;
         *in30 = *on;
         linnbr = 7;
         if wsrgr = *blanks;
           msgnr = 'YBC2137';
           posnbr = 33;
         else;
           posnbr = 55;
           msgnr = 'YBC2189';
         endif;

       when *in88;
         msgnr = 'YBC2138';
         *in30 = *on;
         linnbr = 7;
         posnbr = 63;

       when *in87;
         msgnr = 'YBC2139';
         *in30 = *on;
         linnbr = 8;
         posnbr = 15;

       when *in86;
         msgnr = 'YBC2140';
         *in30 = *on;
         linnbr = 8;
         posnbr = 62;

       when *in85;
         msgnr = 'YBC2141';
         *in30 = *on;
         linnbr = 8;
         posnbr = 72;

       when *in84;
         msgnr = 'YBC2142';
         *in30 = *on;
         linnbr = 8;
         posnbr = 101;

       when *in83;
         msgnr = 'YBC2143';
         *in30 = *on;
         linnbr = 9;
         posnbr = 15;

       when *in54;
         msgnr = 'YBC2193';
         *in30 = *on;
         linnbr = 10;
         posnbr = 15;

       when *in53;
         msgnr = 'YBC2194';
         *in30 = *on;
         linnbr = 10;
         posnbr = 52;

       when *in52;
         msgnr = 'YBC2195';
         *in30 = *on;
         linnbr = 10;
         posnbr = 65;

       // ActiveBorderTransportMeans
       when *in82;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 10;
         posnbr = 74;

       when *in51;
         msgnr = 'YBC2196';
         *in30 = *on;
         linnbr = 10;
         posnbr = 82;

       when *in81;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 10;
         // posnbr = 90;

       // Passiv kjøretøy
       when *in80;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 11;
         posnbr = 74;

       // Dato og tid
       when *in79;
         msgnr = 'SPD0001';
         *in30 = *on;
         linnbr = 12;
         posnbr = 20;

       when *in78;
         msgnr = 'YBC2144';
         *in30 = *on;
         linnbr = 12;
         posnbr = 32;

       when *in77;
         msgnr = 'SPD0001';
         *in30 = *on;
         linnbr = 12;
         posnbr = 42;

       when *in76;
         msgnr = 'YBC2144';
         *in30 = *on;
         linnbr = 12;
         posnbr = 54;

       when *in75;
         msgnr = 'SPD0001';
         *in30 = *on;
         linnbr = 12;
         posnbr = 64;

       when *in74;
         msgnr = 'YBC2144';
         *in30 = *on;
         linnbr = 12;
         posnbr = 76;

       when *in73;
         msgnr = 'SPD0001';
         *in30 = *on;
         linnbr = 12;
         posnbr = 86;

       when *in72;
         msgnr = 'YBC2144';
         *in30 = *on;
         linnbr = 12;
         posnbr = 98;

       // Sjåfør
       when *in50;
         msgnr = 'YBC2197';
         *in30 = *on;
         linnbr = 13;
         posnbr = 15;

       when *in71;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 13;
         posnbr = 80;

       when *in70;
         msgnr = 'SPD0001';
         *in30 = *on;
         linnbr = 13;
         posnbr = 96;

       when *in69;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 14;
         posnbr = 80;

       when *in68;
         msgnr = 'SPD0001';
         *in30 = *on;
         linnbr = 14;
         posnbr = 96;

       // Div info.
       when *in67;
         msgnr = 'YBC2145';
         *in30 = *on;
         linnbr = 15;
         posnbr = 15;

       // Andre aktors rolle
       //when *inxx;
       //msgnr = 'YBC2xxx';
       //*in30 = *on;
       //linnbr = 16;
       //posnbr = 104;

       // Loading and unloading places
       when *in66;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 17;
         posnbr = 15;

       when *in65;
         msgnr = 'SPS0106';
         *in30 = *on;
         linnbr = 17;
         posnbr = 18;

       when *in64;
         msgnr = 'SPL0001';
         *in30 = *on;
         linnbr = 17;
         posnbr = 75;

       when *in63;
         msgnr = 'SPS0106';
         *in30 = *on;
         linnbr = 17;
         posnbr = 78;

       // Transportør
       when *in62;
         msgnr = 'YBC2146';
         *in30 = *on;
         linnbr = 18;
         posnbr = 15;

       when *in61;
         msgnr = 'YBC2147';
         *in30 = *on;
         linnbr = 18;
         posnbr = 33;

       when *in60;
         msgnr = 'YBC2148';
         *in30 = *on;
         linnbr = 18;
         posnbr = 59;

       when *in59;
         msgnr = 'YBC2149';
         *in30 = *on;
         linnbr = 19;
         posnbr = 15;

       when *in58;
         msgnr = 'YBC2150';
         *in30 = *on;
         linnbr = 19;
         posnbr = 62;

       when *in57;
         msgnr = 'YBC2151';
         *in30 = *on;
         linnbr = 19;
         posnbr = 72;

       when *in56;
         msgnr = 'YBC2152';
         *in30 = *on;
         linnbr = 19;
         posnbr = 101;

       when *in55;
         msgnr = 'YBC2153';
         *in30 = *on;
         linnbr = 20;
         posnbr = 15;

       when msgnr = 'YBC2199' and linnbr = 21 and posnbr = 73;

       endsl;

       endsr;

      //***********************************************
       begsr movemw;

       if utype <> 'CPY';
         WSAVD   = EMAVD;
       endif;
       //WSAVD    = EMAVD;
       WSDTRD   = EMDTRD;
       WSDTRM   = EMDTRM;
       WSDTRA   = EMDTRA;

       // Deklarant
       WSKND    = EMKND;
       WSRGD    = EMRGD;
       WSNAD    = EMNAD;
       WSNA2D   = EMNA2D;
       WSAD1D   = EMAD1D;
       WSNRD    = EMNRD;
       WSPND    = EMPND;
       WSPSD    = EMPSD;
       WSLKD    = EMLKD;
       WSPBD    = EMPBD;
       WSEMD    = EMEMD;
       WSEMDT   = EMEMDT;

       // Representative
       WSKNR    = EMKNR;
       WSRGR    = EMRGR;
       WSSTR    = EMSTR;
       WSNAR    = EMNAR;
       WSNA2R   = EMNA2R;
       WSAD1R   = EMAD1R;
       WSNRR    = EMNRR;
       WSPNR    = EMPNR;
       WSPSR    = EMPSR;
       WSLKR    = EMLKR;
       WSPBR    = EMPBR;
       WSEMR    = EMEMR;
       WSEMRT   = EMEMRT;

       // ActiveBorderTransportMeans
       WSKMRK   = EMKMRK;
       WSKTYP   = EMKTYP;
       WSKTYPT  = EMKTYPT;
       WSKTM    = EMKTM;
       WSKTMT   = EMKTMT;
       WSKLK    = EMKLK;
       WSKTKD   = EMKTKD;
       WSLKR1   = EMLKR1;
       WSLKR2   = EMLKR2;
       WSLKR3   = EMLKR3;
       WSLKR4   = EMLKR4;
       WSLKR5   = EMLKR5;
       WSLKR6   = EMLKR6;
       WSLKR7   = EMLKR7;
       WSLKR8   = EMLKR8;
       WSKMRK   = EMKMRK;

       // Passiv kjøretøy
       WSPMRK   = EMPMRK;
       WSPTYP   = EMPTYP;
       WSPTM    = EMPTM;
       WSPTMT   = EMPTMT;
       WSPLK    = EMPLK;

       // Dato og tid
       WSETDDD  = EMETDDD;
       WSETDDM  = EMETDDM;
       WSETDDA  = EMETDDA;
       WSETDT   = EMETDT4;
       WSETADD  = EMETADD;
       WSETADM  = EMETADM;
       WSETADA  = EMETADA;
       WSETAT   = EMETAT4;
       WSATDDD  = EMATDDD;
       WSATDDM  = EMATDDM;
       WSATDDA  = EMATDDA;
       WSATDT   = EMATDT4;
       WSATADD  = EMATADD;
       WSATADM  = EMATADM;
       WSATADA  = EMATADA;
       WSATAT   = EMATAT4;

       // Sjåfør
       WSSJAF   = EMSJAF;
       WSSJALK  = EMSJALK;
       WSSJADTD = EMSJADTD;
       WSSJADTM = EMSJADTM;
       WSSJADTA = EMSJADTA;
       WSSJ2F   = EMSJ2F;
       WSSJ2LK  = EMSJ2LK;
       WSSJ2DTD = EMSJ2DTD;
       WSSJ2DTM = EMSJ2DTM;
       WSSJ2DTA = EMSJ2DTA;

       // Div info.
       WSTSD    = EMTSD;
       WSVKB    = EMVKB;
       WSCN     = EMCN;
       WSCNR    = EMCNR;
       //WSDKH    = EMDKH;
       //WSDKHT   = EMDKHT;
       WSUCR    = EMUCR;

       // AdditionalSupplyChainActor
       WSSCA    = EMSCA;
       WSSCAR   = EMSCAR;

       // Loading and unloading places
       WSLKL    = EMLKL;
       WSSDL    = EMSDL;
       WSSDLT   = EMSDLT;
       WSLKU    = EMLKU;
       WSSDU    = EMSDU;
       WSSDUT   = EMSDUT;

       // Transportør
       WSKNT    = EMKNT;
       WSRGT    = EMRGT;
       WSNAT    = EMNAT;
       WSNA2T   = EMNA2T;
       WSAD1T   = EMAD1T;
       WSNRT    = EMNRT;
       WSPNT    = EMPNT;
       WSPST    = EMPST;
       WSLKT    = EMLKT;
       WSPBT    = EMPBT;
       WSEMT    = EMEMT;
       WSEMTT   = EMEMTT;

       WSRCEM1  = EMRCEM1;
       WSRCEM2  = EMRCEM2;
       WSRCEM3  = EMRCEM3;
       WSDKM    = EMDKM;
       WSDKMT   = EMDKMT;

       endsr;

      //***********************************************
       begsr movwem;

       EMAVD    = WSAVD;
       EMDTRD   = WSDTRD;
       EMDTRM   = WSDTRM;
       EMDTRA   = WSDTRA;
       EMDTRC   = 20;

       // Deklarant
       EMKND    = WSKND;
       EMRGD    = WSRGD;
       EMNAD    = WSNAD;
       EMNA2D   = WSNA2D;
       EMAD1D   = WSAD1D;
       EMNRD    = WSNRD;
       EMPND    = WSPND;
       EMPSD    = WSPSD;
       EMLKD    = WSLKD;
       EMPBD    = WSPBD;
       EMEMD    = WSEMD;
       EMEMDT   = WSEMDT;

       // Representative
       EMKNR    = WSKNR;
       EMRGR    = WSRGR;
       EMSTR    = WSSTR;
       EMNAR    = WSNAR;
       EMNA2R   = WSNA2R;
       EMAD1R   = WSAD1R;
       EMNRR    = WSNRR;
       EMPNR    = WSPNR;
       EMPSR    = WSPSR;
       EMLKR    = WSLKR;
       EMPBR    = WSPBR;
       EMEMR    = WSEMR;
       EMEMRT   = WSEMRT;

       // ActiveBorderTransportMeans
       EMKMRK   = WSKMRK;
       EMKTYP   = WSKTYP;
       EMKTYPT  = WSKTYPT;
       EMKTM    = WSKTM;
       EMKTMT   = WSKTMT;
       EMKLK    = WSKLK;
       EMKTKD   = WSKTKD;
       EMLKR1   = WSLKR1;
       EMLKR2   = WSLKR2;
       EMLKR3   = WSLKR3;
       EMLKR4   = WSLKR4;
       EMLKR5   = WSLKR5;
       EMLKR6   = WSLKR6;
       EMLKR7   = WSLKR7;
       EMLKR8   = WSLKR8;
       EMKMRK   = WSKMRK;

       // Passiv kjøretøy
       EMPMRK   = WSPMRK;
       EMPTYP   = WSPTYP;
       EMPTM    = WSPTM;
       EMPTMT   = WSPTMT;
       EMPLK    = WSPLK;

       // Dato og tid
       IF WSETDD = 0;
         EMETDD   = 0;
         EMETDT   = 0;
       ELSE;
         EMETDDD  = WSETDDD;
         EMETDDM  = WSETDDM;
         EMETDDA  = WSETDDA;
         EMETDDC  = 20;
         EMETDTS  = 0;
         EMETDT4  = WSETDT;
       ENDIF;
       IF WSETAD = 0;
         EMETAD   = 0;
         EMETAT   = 0;
       ELSE;
         EMETADD  = WSETADD;
         EMETADM  = WSETADM;
         EMETADA  = WSETADA;
         EMETADC  = 20;
         EMETATS  = 0;
         EMETAT4  = WSETAT;
       ENDIF;
       IF WSATDD = 0;
         EMATDD   = 0;
         EMATDT   = 0;
       ELSE;
         EMATDDD  = WSATDDD;
         EMATDDM  = WSATDDM;
         EMATDDA  = WSATDDA;
         EMATDDC  = 20;
         EMATDTS  = 0;
         EMATDT4  = WSATDT;
       ENDIF;
       IF WSATAD = 0;
         EMATAD   = 0;
         EMATAT   = 0;
       ELSE;
         EMATADD  = WSATADD;
         EMATADM  = WSATADM;
         EMATADA  = WSATADA;
         EMATADC  = 20;
         EMATATS  = 0;
         EMATAT4  = WSETAT;
       ENDIF;

       // Sjåfør
       EMSJAF   = WSSJAF;
       EMSJALK  = WSSJALK;
       IF WSSJADT = 0;
         EMSJADT  = 0;
       ELSE;
         EMSJADTD = WSSJADTD;
         EMSJADTM = WSSJADTM;
         EMSJADTA = WSSJADTA;
         IF EMSJADTA >= 50;
           EMSJADTC = 19;
         ELSE;
           EMSJADTC = 20;
         ENDIF;
       ENDIF;
       EMSJ2F   = WSSJ2F;
       EMSJ2LK  = WSSJ2LK;
       IF WSSJADT = 0;
         EMSJADT  = 0;
       ELSE;
         EMSJ2DTD = WSSJ2DTD;
         EMSJ2DTM = WSSJ2DTM;
         EMSJ2DTA = WSSJ2DTA;
         IF EMSJ2DTA >= 50;
           EMSJ2DTC = 19;
         ELSE;
           EMSJ2DTC = 20;
         ENDIF;
       ENDIF;

       // Div info.
       EMTSD    = WSTSD;
       EMVKB    = WSVKB;
       EMCN     = WSCN;
       EMCNR    = WSCNR;
       //EMDKH    = WSDKH;
       //EMDKHT   = WSDKHT;
       EMUCR    = WSUCR;

       // AdditionalSupplyChainActor
       EMSCA    = WSSCA;
       EMSCAR   = WSSCAR;

       // Loading and unloading places
       EMLKL    = WSLKL;
       EMSDL    = WSSDL;
       EMSDLT   = WSSDLT;
       EMLKU    = WSLKU;
       EMSDU    = WSSDU;
       EMSDUT   = WSSDUT;

       // Transportør
       EMKNT    = WSKNT;
       EMRGT    = WSRGT;
       EMNAT    = WSNAT;
       EMNA2T   = WSNA2T;
       EMAD1T   = WSAD1T;
       EMNRT    = WSNRT;
       EMPNT    = WSPNT;
       EMPST    = WSPST;
       EMLKT    = WSLKT;
       EMPBT    = WSPBT;
       EMEMT    = WSEMT;
       EMEMTT   = WSEMTT;

       EMRCEM1  = WSRCEM1;
       EMRCEM2  = WSRCEM2;
       EMRCEM3  = WSRCEM3;
       EMDKM    = WSDKM;
       EMDKMT   = WSDKMT;

       endsr;

      //***********************************************
       begsr testd;

       if wsknd = 0;               // FEIL: Kundenr er 0.
       //*in98 = *on;
       else;
         chain (fifirm: wsknd) cundl01;
         if %found;                // OK: Hent info. fra kunderegister.
           wsnad  = knavn;
           wsad1d = adr1;
           wsnrd  = adr2;
           wspnd  = *blanks;
           if postnr = 0;
             wspnd  = sypoge;
           else;
      /end-free
     C                   MOVEL     POSTNR        WSPND
      /free
           endif;
           wspsd  = adr3;
           if syland = *blanks;
             wslkd = filand;
           else;
             wslkd = syland;
           endif;
           select;
           when syepos <> *blanks;
             wsemd  = syepos;
             wsemdt = 'EM';
           when tlf <> *blanks;
             wsemd = tlf;
             wsemdt = 'TE';
           endsl;
           if syfor9 <> *zeros;
             wsrgd = syrg;
           endif;
         else;                     // FEIL: Kundenr finnes ikke i kunderegister
           *in98 = *on;
         endif;
       endif;

       endsr;

      //***********************************************
       begsr testr;

       if wsknr = 0;               // FEIL: Kundenr er 0.
       //*in90 = *on;
       else;
         chain (fifirm: wsknr) cundl01;
         if %found;                // OK: Hent info. fra kunderegister.
           wsnar  = knavn;
           wsad1r = adr1;
           wsnrr  = adr2;
           wspnr  = *blanks;
           if postnr = 0;
             wspnr  = sypoge;
           else;
      /end-free
     C                   MOVEL     POSTNR        WSPNR
      /free
           endif;
           wspsr  = adr3;
           if syland = *blanks;
             wslkr = filand;
           else;
             wslkr = syland;
           endif;
           select;
           when syepos <> *blanks;
             wsemr  = syepos;
             wsemrt = 'EM';
           when tlf <> *blanks;
             wsemr = tlf;
             wsemrt = 'TE';
           endsl;
           if syfor9 <> *zeros;
             wsrgr = syrg;
           endif;
         else;                     // FEIL: Kundenr finnes ikke i kunderegister
           *in90 = *on;
         endif;
       endif;

       endsr;

      //***********************************************
       begsr testt;

       if wsknt = 0;               // FEIL: Kundenr er 0.
       //*in61 = *on;
       else;
         chain (fifirm: wsknt) cundl01;
         if %found;                // OK: Hent info. fra kunderegister.
           wsnat  = knavn;
           wsad1t = adr1;
           wsnrt  = adr2;
           wspnt  = *blanks;
           if postnr = 0;
             wspnt  = sypoge;
           else;
      /end-free
     C                   MOVEL     POSTNR        WSPNT
      /free
           endif;
           wspst  = adr3;
           if syland = *blanks;
             wslkt = filand;
           else;
             wslkt = syland;
           endif;
           select;
           when syepos <> *blanks;
             wsemt  = syepos;
             wsemtt = 'EM';
           when tlf <> *blanks;
             wsemt = tlf;
             wsemtt = 'TE';
           endsl;
           if syfor9 <> *zeros;
             wsrgt = syrg;
           endif;
         else;                     // FEIL: Kundenr finnes ikke i kunderegister
           *in61 = *on;
         endif;
       endif;

       endsr;

      //***********************************************
       begsr opdtur;

       chain wsavd sadexdef;

       // FLYTTER SKJERMVARIABLE TIL FILVARIABLE
       exsr movwem;

       // ikke %found -> NY AVDEDELING
       if not %found (sadexdef);
         emavd  = wsavd;
         emdtr  = %dec(wdt:8:0);
         write sadexdefr;
         wsdtrd = emdtrd;
         wsdtrm = emdtrm;
         wsdtra = emdtra;
       else;
         update sadexdefr;
       endif;

       endsr;

      //***********************************************
       begsr init;

       syge15c (wdt: wtm);

       setll *loval firm;
       read firm;

       usped = 'N';
       wsdtr = 0;
       wsetdd = 0;
       wsetdt = 0;
       wsetad = 0;
       wsetat = 0;
       wsatdd = 0;
       wsatdt = 0;
       wsatad = 0;
       wsatat = 0;
       wssjadt = 0;
       wssj2dt = 0;
       wsktypt = *blanks;
       emdtr = 0;
       emetdd = 0;
       emetdt = 0;
       emetad = 0;
       emetat = 0;
       ematdd = 0;
       ematdt = 0;
       ematad = 0;
       ematat = 0;
       emsjadt = 0;
       emsj2dt = 0;

       select;
       when utype = 'DSP';
         *in49 = *on;
       when utype = 'CHG';
         *in48 = *on;
       when utype = 'CPY';
         wsavd = 0;
       endsl;

       chain uavd tell;

       chain(n) uavd sadexdef;
       if not %found;
         emdtr = 0;
       endif;
       exsr movemw;

       endsr;

      /end-free
