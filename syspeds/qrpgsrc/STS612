      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STS612 MODULE(*LIBL/STS612 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FBRPARF    IF   E           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          s             10
     D WSTYPE          s              2
     D WSLOK           s             50
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D TXT01           C                   CONST('Scan outgoing trip')
     D TXT02           C                   CONST('Scan incoming trip')
     D TXT03           C                   CONST('Scan other trip')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
      * Er ikke riktig innlogget...
     C     *in60         Ifeq      *ON
     C     BIINN         Oreq      ' '
     C                   exsr      SetSkl01B
     C                   callp     wrtsection('all')
     c                   goto      slutt
     c                   end
      *
     C                   EXSR      SetSkl01
      *
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      *
     c     Slutt         tag
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C                   exsr      EXIT
     c
      *
      *********************************************************
     C     EXIT          begsr
      *********************************************************
     C                   close     BRIDF
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   RETURN
     C                   ENDSR
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      wsuser = zhbgetvar('USER')
     C                   eval      wstype = zhbgetvar('TYPE')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C* inngangsbilde for valg av fraktbrevsnr.
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS611B'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01      begsr
      * scannerbilde
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS613'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('TYPE':WSTYPE)
     C                   callp     updhtmlvar('LOK':WSLOK)
     C                   SELECT
     C                   WHEN      WSTYPE = 'UT'
     C                   callp     updhtmlvar('LEDETEKST':TXT01)
     C                   WHEN      WSTYPE = 'IN'
     C                   callp     updhtmlvar('LEDETEKST':TXT02)
     C                   OTHER
     C                   callp     updhtmlvar('LEDETEKST':TXT03)
     C                   ENDSL
      *
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              60
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      BRPARF
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent "location" som denne userid kan skyte på
     C                   MOVE      'SSSCA'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        WSLOK
      *
     C                   ENDSR
