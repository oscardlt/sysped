      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESKS03 MODULE(*LIBL/ESKS03 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ekstra felt ............
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FARKIVL11  IF   E           K DISK    USROPN
     FARKTXT    IF   E           K DISK    USROPN
     FARKEXT    IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D UNDER           s             10
     D OddEven         s             10
     D Lenke           s            200
     D GMLINK          s             40
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      *
     c                   move      UNDER         ARUNDE
     c     Ark11Key      setll     ARKIVL11
     c     Ark11Key      Reade     ARKIVL11                               33
     c     *IN33         DOWEQ     '0'
     c     ARlink        ifne      GMLink
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   end
     c                   eval      GMLink = ARLINK
     c     Ark11Key      Reade     ARKIVL11                               33
     c                   end
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      Under= zhbgetvar('Under')
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     ARKIVL11
     C                   CLOSE     ARKEXT
     C                   CLOSE     ARKTXT
     C                   CLOSE     FIRMARC
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESKS03':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C*                  callp     updHTMLvar('FRAMES':FRAMES)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
      * Typen dok lagret i avvikende lager... (ofte tilfelle ved scannet)
     c     ARTYPE        chain     ARKTXT                             33
     C     ARKLAG        IFNE      *blanks
     c     ArklagKey     chain     arkext
     c                   else
     c     BIFIRM        chain     firmarc                            33
     c                   end
      *  ARCANE er plukket ENTEN fra firma ELLER fra typr/ekstern lager
     c                   eval      Lenke= '<a href="/'+%trim(arcane) +
     C                                        '/'+%trim(ARLINK) + '">'+
     c                                        'Klikk her' +'</a>'
      *
     C                   callp     updHTMLvar('LENKE':LENKE)
N01  C                   callp     updHTMLvar('ARDATE':
N01  C                             %trim(%editW(ARDATE:'    .  .  ')))
N01  C                   callp     updHTMLvar('ARLINK':ARLINK)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      ARKIVL11
     C                   OPEN      ARKEXT
     C                   OPEN      ARKTXT
     C                   OPEN      FIRMARC
      *
     C     Ark11Key      klist
     C                   kfld                    ARSTAT
     C                   kfld                    ARUNDE
     C                   move      'A'           ARSTAT
     C     ArklagKey     KLIST
     C                   KFLD                    Bifirm
     C                   KFLD                    arklag
     C                   endsr
      *
