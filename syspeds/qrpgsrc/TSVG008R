********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11048 * 01 CB  PTF11 Testindikator
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVG008R MODULE(*LIBL/TSVG008R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVEA      IF   E           K DISK    USROPN
     FSVIA      IF   E           K DISK    USROPN
     FSVXSTD    IF   E           K DISK    USROPN
     FSVNSTD    IF   E           K DISK    USROPN
     FSTANDI    IF   E           K DISK    USROPN
     FSTANDE    IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WIE             S              1
N01  D WTST            S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬ie¬ : ¬'+%trim(WIE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"avdelningar" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta avdelningar
      *
     c     Error         ifeq      *blanks
     C     *LOVAL        SETLL     SVEA
     C     *LOVAL        SETLL     SVIA
     C     *LOVAL        SETLL     SVXSTD
     C     *LOVAL        SETLL     SVNSTD
     C     *LOVAL        SETLL     STANDI
     C     *LOVAL        SETLL     STANDE
     C     *LOVAL        SETLL     KODTA
     C                   SELECT
     C                   WHEN      WIE = 'E'
     C                   READ      SVEA                                   55
     C                   EVAL      WKOAAVD = SVEA_SYAV
N01  C                   EVAL      WTST    = SVEA_0035
     C                   WHEN      WIE = 'I'
     C                   READ      SVIA                                   55
     C                   EVAL      WKOAAVD = SVIA_SYAV
N01  C                   EVAL      WTST    = SVIA_0035
     C                   WHEN      WIE = 'X'
     C                   READ      SVXSTD                                 55
     C                   EVAL      WKOAAVD = THAVD
N01  C                   EVAL      WTST    = S0035
     C                   WHEN      WIE = 'N'
     C                   READ      SVNSTD                                 55
     C                   EVAL      WKOAAVD = TIAVD
N01  C                   EVAL      WTST    = S0035
     C                   WHEN      WIE = 'A'
     C                   READ      KODTA                                  55
     C                   EVAL      WKOAAVD = KOAAVD
     C                   WHEN      WIE = 'B'
     C                   READ      STANDI                                 55
     C                   EVAL      WKOAAVD = SIAVD
N01  C                   EVAL      WTST    = S0035
     C                   WHEN      WIE = 'C'
     C                   READ      STANDE                                 55
     C                   EVAL      WKOAAVD = SEAVD
N01  C                   EVAL      WTST    = S0035
     C                   ENDSL
     C     *IN55         DOWEQ     *OFF

     C     WIE           IFNE      'A'
     C     KODTAKEY      CHAIN     KODTA                              56
     C                   ENDIF
     C                   IF        *IN56 = *OFF

     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(KOAAVD:'Z'))+'¬, '
          +'¬namn¬ : ¬'+%trim(KOANVN)+'¬, '
          +'¬tst¬ : ¬'+%trim(WTST)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C                   ENDIF

     C                   SELECT
     C                   WHEN      WIE = 'E'
     C                   READ      SVEA                                   55
     C                   EVAL      WKOAAVD = SVEA_SYAV
N01  C                   EVAL      WTST    = SVEA_0035
     C                   WHEN      WIE = 'I'
     C                   READ      SVIA                                   55
     C                   EVAL      WKOAAVD = SVIA_SYAV
N01  C                   EVAL      WTST    = SVIA_0035
     C                   WHEN      WIE = 'X'
     C                   READ      SVXSTD                                 55
     C                   EVAL      WKOAAVD = THAVD
N01  C                   EVAL      WTST    = S0035
     C                   WHEN      WIE = 'N'
     C                   READ      SVNSTD                                 55
     C                   EVAL      WKOAAVD = TIAVD
N01  C                   EVAL      WTST    = S0035
     C                   WHEN      WIE = 'A'
     C                   READ      KODTA                                  55
     C                   EVAL      WKOAAVD = KOAAVD
     C                   WHEN      WIE = 'B'
     C                   READ      STANDI                                 55
     C                   EVAL      WKOAAVD = SIAVD
N01  C                   EVAL      WTST    = S0035
     C                   WHEN      WIE = 'C'
     C                   READ      STANDE                                 55
     C                   EVAL      WKOAAVD = SEAVD
N01  C                   EVAL      WTST    = S0035
     C                   ENDSL
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVEA
     C  N50              CLOSE     SVIA
     C  N50              CLOSE     SVXSTD
     C  N50              CLOSE     SVNSTD
     C  N50              CLOSE     STANDI
     C  N50              CLOSE     STANDE
     C  N50              CLOSE     KODTA

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WIE = zhbgetvar('IE')
     C
     c                   endsr
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     KODTAKEY      KLIST
     C                   KFLD                    WKOAUNI
     C                   KFLD                    WKOAAVD
     C     *LIKE         DEFINE    KOAUNI        WKOAUNI
     C     *LIKE         DEFINE    KOAAVD        WKOAAVD
     C                   EVAL      WKOAUNI = 'A'

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVEA
     C  N50              OPEN      SVIA
     C  N50              OPEN      SVXSTD
     C  N50              OPEN      SVNSTD
     C  N50              OPEN      STANDI
     C  N50              OPEN      STANDE
     C  N50              OPEN      KODTA
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           XDATO             8 0

     c                   endsr
