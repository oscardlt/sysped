     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R TVI67DA
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 34'Vise melding 1/2'
     A                                      COLOR(WHT)
     A                                  1 73'TVI67D A'
     A                                      COLOR(BLU)
     A                                  3  2'Avdeling . . . . . . :'
     A            MAVD      R        O  3 29
     A                                  3 36'Oppdragsnummer :'
     A            MTDN      R        O  3 54
     A            WSIMEX         6   O  3 70COLOR(RED)
     A                                  4  2'Deklarasjonens dato  :'
     A            M0068A    R        O  4 25EDTWRD('    .  .  ')
     A                                  4 36'Sekvensnummer  :'
     A            M0068B    R        O  4 53EDTCDE(4)
     A                                  4 62'Versjonsnummer :'
     A            M0068C    R        O  4 79EDTCDE(4)
     A                                  6  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  8  6'NODI Id Avsender . . . . . . . . :'
     A            M0004     R        O  8 43
     A                                  9  6'NODI Id Mottaker . . . . . . . . :'
     A            M0010     R        O  9 43
     A                                 10  6'Testindikator  . . . . . . . . . :'
     A            M0035     R        O 10 43
     A                                 11  6'Meldings Referanse . . . . . . . :'
     A            M0062     R        O 11 43
     A                                 12  6'Meldings Identitet . . . . . . . :'
     A            M0065     R        O 12 43
     A                                 13  6'Omberegning, Deklarasjonens dato :'
     A            M0068D    R        O 13 43
     A                                 14  8'Sekvensnummer  . . . . . . . . :'
     A            M0068E    R        O 14 43EDTCDE(4)
     A                                 15 10'Versjonsnummer . . . . . . . :'
     A            M0068F    R        O 15 43EDTCDE(4)
     A N50                             16  6'Ønsket Behandlingsdato . . . . . :'
     A  50                             16  6'Motparts mottaksdato . . . . . . :'
     A            M2005B    R        O 16 43EDTWRD('    .  .  ')
     A N50                             17  6'Deklarantens Referanse . . . . . :'
     A N50        M3039D    R        O 17 43EDTCDE(4)
     A  50                             17  6'Motparts mottakstid  . . . . . . :'
     A  50        X3039D         6  0O 17 43EDTWRD('  :  :  ')
     A                                 18  6'Ekspidisjons Enhet . . . . . . . :'
     A            M3039E    R        O 18 43EDTCDE(4)
     A                                 19  6'Depositum Beløp  . . . . . . . . :'
     A            M5004D    R        O 19 43EDTCDE(K)
     A                                 20  6'Meldingsfunksjon . . . . . . . . :'
     A            M1N07     R        O 20 43
     A                                 21  8'Korrigert  . . . . . . . . . . :'
     A            M1N08     R        O 21 43
     A                                 21 72'Mer...'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A          R TVI67DB
     A                                      OVERLAY
     A                                      ROLLDOWN(26)
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 32'Vise melding 2/2'
     A                                      COLOR(WHT)
     A                                  1 73'TVI67D B'
     A                                      COLOR(BLU)
     A                                  3  2'Avdeling . . . . . . :'
     A            MAVD      R        O  3 29
     A                                  3 36'Oppdragsnummer :'
     A            MTDN      R        O  3 54
     A                                  4  2'Deklarasjonens dato  :'
     A            M0068A    R        O  4 25EDTWRD('    .  .  ')
     A                                  4 36'Sekvensnummer  :'
     A            M0068B    R        O  4 53EDTCDE(4)
     A                                  4 62'Versjonsnummer :'
     A            M0068C    R        O  4 79EDTCDE(4)
     A                                  6  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  8  4'Ekspidisjons Prioritet  . . . . :'
     A            M9N01     R        O  8 40
     A                                  9  4'Sendningsnummer . . . . . . . . :'
     A            MSN       R        O  9 40EDTCDE(4)
     A                                 10  6'Meldingsnummer  . . . . . . . :'
     A            MMN       R        O 10 40EDTCDE(4)
     A                                 11  4'Send/Recive . . . . . . . . . . :'
     A            MSR       R        O 11 40
     A                                 12  4'Status  . . . . . . . . . . . . :'
     A            MST       R        O 12 40
     A                                 13  6'Status Dato . . . . . . . . . :'
     A            MDT       R        O 13 40
     A                                 14  8'Status Tid  . . . . . . . . :'
     A            MTM       R        O 14 40EDTWRD('  :  :  ')
     A                                 15  4'Ventekode . . . . . . . . . . . :'
     A            MVEN      R        O 15 40
     A                                 21 72'Slutt '
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A          R TVI67DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
