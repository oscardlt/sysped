     A                                      REF(*LIBL/SAEH)
     A          R TVINUAEA                  SKIPB(2)
     A            KFMT1         75         1
     A            KFMT2         75         1SPACEA(1)
     A                                    24'UTLEVERINGSATTEST (Utførsel)'
     A                                      UNDERLINE
     A                                     2'Referanse:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SEAVD     R             13EDTCDE(Z)
     A                                    17'/'
     A            SETDN     R             18EDTCDE(Z)
     A                                    25'/'
     A            SESG      R             26
N01  A            FSSOK         35        32
     A                                     2'Godkjennelsedato:'
     A                                      UNDERLINE
     A                                      SPACEB(1)
     A            SEDTG     R             20
     A                                    34'Løpenr.'
     A                                      UNDERLINE
     A            SETLL     R             42
     A                                    53'Ekspedisjonsnr.'
     A                                      UNDERLINE
     A            SETLE     R             69
     A                                     2' 2.Avsender:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SEKNK     R             15EDTCDE(Z)
     A            SERG      R             27
     A            SENAK     R             15
     A                                      SPACEB(1)
     A            SEADK1    R             47
     A            SEADK2    R             15
     A                                      SPACEB(1)
     A            SEADK3    R             47
     A                                     2'12.Mottaker:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SENAS     R             15
     A            SEADS1    R             47
     A            SEADS2    R             15
     A                                      SPACEB(1)
     A            SEADS3    R             47
     A                                     2'1.Tolldeklarasjon:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SEDTY     R             21
     A            SEDP      R             24EDTCDE(Z)
     A                                    31' 6.Antall kolli:'
     A                                      UNDERLINE
     A            SENTK     R             48EDTCDE(K)
     A                                     2'12.Frakt:'
     A                                      UNDERLINE
     A                                      SPACEB(3)
     A N04        SEBEL2    R             12EDTCDE(K)
     A                                    31'15.Avsenderland:'
     A                                      UNDERLINE
     A            SELKA     R             48
     A                                    58'19.Cont.'
     A                                      UNDERLINE
     A            SEKDC     R             67
     A                                     2'20.Lev.vilkår:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SELV      R             17
     A            SELVT     R             22
     A                                    43'21.Identitet:'
     A                                      UNDERLINE
     A            SETRID    R             57
     A                                     2'22.Faktura:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SEVAL1    R             14
     A N03        SEBEL1    R             18EDTCDE(K)
     A                                    43'23.Kurs:'
     A                                      UNDERLINE
     A N03        SEVKU     R             52EDTCDE(K)
     A                                    63'24.Trans.type:'
     A                                      UNDERLINE
     A            SETST     R             78EDTCDE(Z)
     A                                     2'25.Trans.måte:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SETRM     R             17EDTCDE(Z)
     A                                    26'28.Finans:'
     A                                      UNDERLINE
     A            SEFIF     R             37
     A            SEFID     R             65EDTWRD('    .  .  ')
     A                                     2'30.Varenes lagringssted:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SELS      R             27
     A            SEKDLS    R             44
     A                                     2'48.Konto:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A N01N02     SEKTC     R             12
     A N01N02     SEKTA     R             13EDTCDE(Z)
     A N01N02                             20'-'
     A N01N02     SEKTB     R             21EDTCDE(Z)
     A  01                                13'DAGSOPPGJØR'
     A  02                                13'KONTANT'
     A                                    25'49.Godsnr.'
     A                                      UNDERLINE
     A            SEGN      R             36
     A                                    47'/'
     A            SEPOS     R             52
     A                                     2'54.Sted og dato:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SEDST     R             19
     A            SEDT      R             45EDTWRD('    .  .  ')
     A                                    19'            '
     A                                      SPACEB(1)
     A            SETARF    R             45
     A                                     2'Deklarasjons dato:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            M0068A    R             21REFFLD(M0068A EDIM)
     A                                      EDTCDE(Z)
     A                                    41'Sekvensenr.'
     A                                      UNDERLINE
     A            M0068B    R             53REFFLD(M0068B EDIM)
     A                                      EDTCDE(Z)
     A                                    62'Versjonsnr.'
     A                                      UNDERLINE
     A            M0068C    R             73REFFLD(M0068C EDIM)
     A                                      EDTCDE(Z)
     A                                     2'Ref Funk Tekst'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A          R TVINUAEB                  SPACEB(2)
     A            F0077     R              2REFFLD(F0077 TVINF)
     A            F4815     R              6REFFLD(F4815 TVINF)
     A            F0078A    R             11REFFLD(F0078A TVINF)
     A                                      SPACEA(1)
     A            F0078B    R             11REFFLD(F0078B TVINF)
     A                                      SPACEA(1)
     A            F0078C    R             11REFFLD(F0078C TVINF)
     A                                      SPACEA(1)
     A            F0078D    R             11REFFLD(F0078D TVINF)
     A                                      SPACEA(1)
     A            F0078E    R             11REFFLD(F0078E TVINF)
     A                                      SPACEA(1)
