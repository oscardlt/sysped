********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12277 * 01 YBC PTF12 F18 FLERE EKSPORT-ID PÅ SAMME OPPDRAG
********************************************************************
     A                                      DSPSIZ(27 132 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 80 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Avbryt')
N01  A                                      CF18(18 'Flere')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD134R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            EFAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            EFPRO          8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'UUID:'
     A            EFUUID        36A  O  2 35TEXT('Turnr')
     A                                      COLOR(BLU)
      *
     A  51                              3  2'OBS - Splittet oppdrag - se valg 1-
     A                                      1 for detaljer'
     A                                      COLOR(YLW)
      *
     A                                  4  2'Avdeling:'
     A            WSAVD          4Y 0O  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 20'Oppdrag:'
     A            WSTDN          7Y 0O  4 29TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 37'Status:'
     A            CLST           1A  O  4 45TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4 47'Kolli:'
     A            WSNTK          7Y 0B  4 54TEXT('Kolli')
     A                                      DSPATR(UL)
     A N48
     AO 49                                  DSPATR(PR)
     A  48
     AAN99                                  COLOR(YLW)
     A N48
     AO 99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 62'Vekt:'
     A            WSVKB          9Y 0B  4 68TEXT('Vekt')
     A                                      DSPATR(UL)
     A N48
     AO 49                                  DSPATR(PR)
     A  48
     AAN98                                  COLOR(YLW)
     A N48
     AO 98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2'Vareslag:'
     A            WSVT          30A  B  5 15TEXT('Vareslag')
     A                                      DSPATR(UL)
     A N48
     AO 49                                  DSPATR(PR)
     A  48
     AAN97                                  COLOR(YLW)
     A N48
     AO 97                                  COLOR(WHT)
      *
     A                                  6  2'Prosedyre:'
     A            WSPR           2A  B  6 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSPRT         45A  O  6 21COLOR(WHT)
      *
     A                                  7  2'Deklarant:'
     A            WSRG          11A  B  7 15TEXT('Senders org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A                                  7 27'Dato:'
     A            WS0068A        6Y 0B  7 33TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7 42'Sekvens:'
     A            WS0068B        6Y 0B  7 51TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A                                  8  2'MRN-nr:'
     A            WSTRNR        18A  B  8 15TEXT('MRN-nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                  8 34'Avs:'
     A            WSNAS         30A  B  8 39TEXT('Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                  9 34'Mot:'
     A            WSNAK         30A  B  9 39TEXT('Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
      *
     A                                 10  2'Fra sted:'
     A            WSLKF          2A  B 10 15TEXT('Fra land')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A            WSSDF          5A  B 10 18TEXT('Fra sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A            WSSDFT        30A  B 10 24TEXT('Fra sted navn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A                                 11  2'Til sted:'
     A            WSLKT          2A  B 11 15TEXT('Til land')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A            WSSDT          5A  B 11 18TEXT('Til sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A            WSSDTT        30A  B 11 24TEXT('Til sted navn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
      *
     A                                 12  2'Eksp.type:'
     A            WSETYP         2A  B 12 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  87                                  COLOR(WHT)
     A            WSETYPT       45A  O 12 21COLOR(WHT)
     A                                 13  2'Eksport Id:'
     A            WSEID         18A  B 13 15TEXT('Eksport Id')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  86                                  COLOR(WHT)
     A                                 13 35'Sertifisert:'
     A            WSESER         1A  B 13 48TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 13 50'(J/N)'
     A  49                             14  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             14  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
N01  A                                 14 25'F18=Flere export-id'
N01  A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
