      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED KODER FOR KUNDEGRPPER      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FGSSCCF    UF A E           K DISK
     FGSSCCL01  IF   E           K DISK
     F                                     RENAME(GSSCCFR:GSSCCFR1)
     FGSS002D   CF   E             WORKSTN
     F                                     SFILE(GSS002DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  CCDSC                  1     50
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     GSS002DB
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2DSC         SETLL     GSSCCL01
     C     START1        TAG
     C                   READ      GSSCCL01                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     GSS002DA
     C                   READ      GSSCCL01                               94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS002DG
     C                   ELSE
     C                   WRITE     GSS002DB
     C                   EXFMT     GSS002DC
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     6             POSNBR
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*** AVSLUTT
     C   12              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     GSS002DD
     C                   MOVEA     '00'          *IN(89)
     C                   SETOFF                                           30
      *
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     GSS002DE
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     16            POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSCG          COMP      *BLANKS                                90
     C   90              MOVE      'GSS0010'     MSGNR
     C   90              SETON                                        30
     C  N90WSCG          CHAIN(N)  GSSCCF                             33
     C  N90
     CANN33              DO
     C                   MOVE      'GSS0011'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     22            LINNBR
     C   90              Z-ADD     6             POSNBR
     C                   END
      *
     C     WSDSC         IFEQ      *BLANKS
     C  N30              MOVE      'GSS0012'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     12            POSNBR
     C                   SETON                                        8930
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSCG          CHAIN     GSSCCF                             33
     C  N33
     CANN40              UPDATE    GSSCCFR
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWCC
     C  N33              UPDATE    GSSCCFR
     C   33              WRITE     GSSCCFR
     C   40              UPDATE    GSS002DA
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     GSS002DA
     C                   WRITE     GSS002DB
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS002DA                               98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   MOVE      CCCG          UCG
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCC
     C   12              GOTO      SLHENT
     C                   END
     C  N27WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCC
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'GSS0009'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGCC         BEGSR
     C***********************************************
     C                   EXSR      MOVCCW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     GSS002DD
     C                   MOVEA     '00'          *IN(89)
     C                   SETOFF                                         30
      *
     C   12              DO
     C                   UPDATE    GSS002DA
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTCC         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     GSS002DF
     C  N12              DO
     C     CCCG          CHAIN     GSSCCF                             33
     C  N33              DELETE    GSSCCFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    GSS002DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWCC        BEGSR
     C***********************************************
     C                   MOVE      WSCG          CCCG
     C                   MOVE      WSDSC         CCDSC
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVCCW        BEGSR
     C***********************************************
     C                   MOVE      CCCG          WSCG
     C                   MOVE      CCDSC         WSDSC
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       CCCG
     C                   MOVE      *BLANKS       CCDSC
     C                   MOVE      '*DELETE'     XDLT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSCG
     C                   MOVE      *BLANKS       WSDSC
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UCG               2
      * UFLAGG = 1 ---> CALL SØKING FRA ET ANNET PROGRAM
     C     UFLAGG        COMP      1                                      27
      *
     C                   ENDSR
      *
