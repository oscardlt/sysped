     FLLAGJL01  UF   E           K DISK
     FLLAGJS03  UF   E           K DISK
     F                                     RENAME(LLAGJR:LLAGJR3)
     FLLAGJD02  UF   E           K DISK
     FLLVARE01  UF   E           K DISK
      *
     C                   EXSR      INIT
     C                   SELECT
     C     UTYPE         WHENEQ    'MRK'
     C                   EXSR      SR01
     C     UTYPE         WHENEQ    'DMK'
     C                   EXSR      SR02
     C     UTYPE         WHENEQ    'DM2'
     C                   EXSR      SR03
     C     UTYPE         WHENEQ    'DLT'
     C                   EXSR      SR04
     C     UTYPE         WHENEQ    'DL2'
     C                   EXSR      SR05
     C                   OTHER
     C                   MOVE      'ERR'         UTYPE
     C                   ENDSL
      *
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UGRP
     C                   PARM                    UPNR
     C                   PARM                    UANR
     C                   PARM                    UMN
     C                   PARM                    UVN
     C                   PARM                    UTYPE             3
      *
     C     *LIKE         DEFINE    VDGRUP        UGRP
     C     *LIKE         DEFINE    VDKJED        UPNR
     C     *LIKE         DEFINE    VAANR         UANR
     C     *LIKE         DEFINE    VDMNL         UMN
     C     *LIKE         DEFINE    VDVNL         UVN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     LAGL1K        KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    VDMN
     C     LAGS3K        KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UANR
     C     LAGD2K        KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UMN
     C                   KFLD                    UVN
     C     LVAREK        KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    VAANR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SR01          BEGSR
      *////////////////////////////////////////////////////////////
      *____________________________________________
      * MERKE STATUS PÅ ARTIKKEL OG OPPDATERE SALDO
      *""""""""""""""""""""""""""""""""""""""""""""
     C     LAGS3K        SETLL     LLAGJS03
     C     LAGS3K        READE     LLAGJS03                               33
     C     VASTAT        IFNE      ' '
      *
      * VENT MAX 5 GANGER 5 SEKUNDER
     C                   DO        5
     C                   Z-ADD     5             USEC              3 0
     C                   CALL      'DLYJBSEC'
     C                   PARM                    USEC
     C     LAGS3K        SETLL     LLAGJS03
     C     LAGS3K        READE     LLAGJS03                               33
     C                   IF        VASTAT = ' '
     C                   LEAVE
     C                   END
     C                   ENDDO
     C     VASTAT        IFNE      ' '
     C                   MOVE      'ERR'         UTYPE
     C                   GOTO      SLSR01
     C                   END
     C                   END
     C     *IN33         DOWEQ     '0'
     C                   MOVE      'X'           VASTAT
     C                   UPDATE    LLAGJR3
     C     LAGS3K        READE     LLAGJS03                               33
     C  N33              ENDDO
      *
     C     LAGD2K        SETLL     LLAGJD02
     C     LAGD2K        READE(N)  LLAGJD02                               33
     C     *IN33         DOWEQ     '0'
     C     LAGL1K        CHAIN     LLAGJL01                           34
     C                   MOVE      'X'           VASTAT
     C                   ADD       VDLANT        VASALD
     C                   ADD       VDSAE0        VASAE0
     C                   ADD       VDSAE1        VASAE1
     C                   ADD       VDSAE2        VASAE2
     C                   ADD       VDSAE3        VASAE3
     C                   UPDATE    LLAGJR
     C     LVAREK        CHAIN     LLVARE01                           34
     C                   ADD       VDLANT        LVSALD
     C                   UPDATE    LLVARER
     C     LAGD2K        READE(N)  LLAGJD02                               33
     C  N33              END
      *
     C     SLSR01        ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SR02          BEGSR
      *////////////////////////////////////////////////////////////
      *_____________________________________________
      * BLANKE STATUS PÅ ARTIKKEL OG OPPDATERE SALDO
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     LAGS3K        SETLL     LLAGJS03
     C     LAGS3K        READE     LLAGJS03                               33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      ' '           VASTAT
     C                   UPDATE    LLAGJR3
     C     LAGS3K        READE     LLAGJS03                               33
     C  N33              ENDDO
      *
     C     LAGD2K        SETLL     LLAGJD02
     C     LAGD2K        READE(N)  LLAGJD02                               33
     C     *IN33         DOWEQ     '0'
     C     LAGL1K        CHAIN     LLAGJL01                           34
     C                   SUB       VDLANT        VASALD
     C                   SUB       VDSAE0        VASAE0
     C                   SUB       VDSAE1        VASAE1
     C                   SUB       VDSAE2        VASAE2
     C                   SUB       VDSAE3        VASAE3
     C                   UPDATE    LLAGJR
     C     LVAREK        CHAIN     LLVARE01                           34
     C                   SUB       VDLANT        LVSALD
     C                   UPDATE    LLVARER
     C     LAGD2K        READE(N)  LLAGJD02                               33
     C  N33              END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SR03          BEGSR
      *////////////////////////////////////////////////////////////
      *__________________________
      * BLANKE STATUS PÅ ARTIKKEL
      *""""""""""""""""""""""""""
     C     LAGS3K        SETLL     LLAGJS03
     C     LAGS3K        READE     LLAGJS03                               33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      ' '           VASTAT
     C                   UPDATE    LLAGJR3
     C     LAGS3K        READE     LLAGJS03                               33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SR04          BEGSR
      *////////////////////////////////////////////////////////////
      *_____________________
      * SLETTE DETALJELINJER
      *"""""""""""""""""""""
     C     LAGD2K        SETLL     LLAGJD02
     C     LAGD2K        READE     LLAGJD02                               33
     C     *IN33         DOWEQ     '0'
     C     LAGL1K        CHAIN     LLAGJL01                           34
     C     VAANR         IFNE      UANR
     C                   MOVE      ' '           VASTAT
     C                   END
     C                   UPDATE    LLAGJR
     C                   DELETE    LLAGJDR
     C     LAGD2K        READE     LLAGJD02                               33
     C  N33              END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SR05          BEGSR
      *////////////////////////////////////////////////////////////
      *____________________________________________________
      * SLETTE DETALJELINJER OG OPPDATERE SALDO PÅ ARTIKKEL
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     LAGD2K        SETLL     LLAGJD02
     C     LAGD2K        READE     LLAGJD02                               33
     C     *IN33         DOWEQ     '0'
     C     LAGL1K        CHAIN     LLAGJL01                           34
     C                   ADD       VDLANT        VASALD
     C                   ADD       VDSAE0        VASAE0
     C                   ADD       VDSAE1        VASAE1
     C                   ADD       VDSAE2        VASAE2
     C                   ADD       VDSAE3        VASAE3
     C                   UPDATE    LLAGJR
     C     LVAREK        CHAIN     LLVARE01                           34
     C                   ADD       VDLANT        LVSALD
     C                   UPDATE    LLVARER
     C                   DELETE    LLAGJDR
     C     LAGD2K        READE     LLAGJD02                               33
     C  N33              END
      *
     C                   ENDSR
      *
