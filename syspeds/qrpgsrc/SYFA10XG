********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * XX sh  PTF11
********************************************************************
     FARKTXT    IF   E           K DISK
     FCUNDC03   IF   E           K DISK
     FHEADF     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FPRIK1     IF   E           K DISK
     FPRIS      IF   E           K DISK
     FFAKT      UF A E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  LK                     1     20
     D                                     DIM(10)                              I/O. landkode
     D  PKLK01                 1      2
     D  PKLK02                 3      4
     D  PKLK03                 5      6
     D  PKLK04                 7      8
     D  PKLK05                 9     10
     D  PKLK06                11     12
     D  PKLK07                13     14
     D  PKLK08                15     16
     D  PKLK09                17     18
     D  PKLK10                19     20
     D                 DS
     D  UR                     1     10
     D                                     DIM(10)                              I/O. ursp.kode
     D  PKUR01                 1      1
     D  PKUR02                 2      2
     D  PKUR03                 3      3
     D  PKUR04                 4      4
     D  PKUR05                 5      5
     D  PKUR06                 6      6
     D  PKUR07                 7      7
     D  PKUR08                 8      8
     D  PKUR09                 9      9
     D  PKUR10                10     10
     D                 DS
     D  TR                     1     40
     D                                     DIM(10)                              I/O. ursp.kode
     D  PTRA01                 1      4
     D  PTRA02                 5      8
     D  PTRA03                 9     12
     D  PTRA04                13     16
     D  PTRA05                17     20
     D  PTRA06                21     24
     D  PTRA07                25     28
     D  PTRA08                29     32
     D  PTRA09                33     36
     D  PTRA10                37     40
     D                 DS
     D  FR                     1     30
     D                                     DIM(10)                              I/O. frankatur
     D  PKFR01                 1      3
     D  PKFR02                 4      6
     D  PKFR03                 7      9
     D  PKFR04                10     12
     D  PKFR05                13     15
     D  PKFR06                16     18
     D  PKFR07                19     21
     D  PKFR08                22     24
     D  PKFR09                25     27
     D  PKFR10                28     30
     D                 DS
     D  FS                     1     50
     D                                     DIM(10)                              I/O. frasted
     D  PKFS01                 1      5
     D  PKFS02                 6     10
     D  PKFS03                11     15
     D  PKFS04                16     20
     D  PKFS05                21     25
     D  PKFS06                26     30
     D  PKFS07                31     35
     D  PKFS08                36     40
     D  PKFS09                41     45
     D  PKFS10                46     50
     D                 DS
     D  TS                     1     50
     D                                     DIM(10)                              I/O. tilsted
     D  PKTS01                 1      5
     D  PKTS02                 6     10
     D  PKTS03                11     15
     D  PKTS04                16     20
     D  PKTS05                21     25
     D  PKTS06                26     30
     D  PKTS07                31     35
     D  PKTS08                36     40
     D  PKTS09                41     45
     D  PKTS10                46     50
      *
      * lossested
      *
     D                 DS
s41  D* LS                     1     80
s41  D*                                    DIM(3)                               I/O. last/loss
N41  D  LS                     1    100
N41  D                                     DIM(5)                               I/O. last/loss
     D  PKLS01                 1     20
     D  PKLS02                21     40
     D  PKLS03                41     60
N41  D  PKLS04                61     80
N41  D  PKLS05                81    100
      *
      * lastested
      *
N41  D                 DS
N41  D  L2                     1    100
N41  D                                     DIM(5)                               I/O. last/loss
N41  D  PKL201                 1     20
N41  D  PKL202                21     40
N41  D  PKL203                41     60
N41  D  PKL204                61     80
N41  D  PKL205                81    100
N43  D                 DS
N43  D  TP                     1      5
N43  D                                     DIM(5)                               I/O. tillprodu
N43  D  HESTN1                 1      1
N43  D  HESTN2                 2      2
N43  D  HESTN3                 3      3
N43  D  HESTN4                 4      4
N43  D  HESTN5                 5      5
N43  D  TPX                    1      5
      *
     c                   EXSR      INIT
      * Sjekk om oppdrag er fakturert - skipp i så fall
     C                   EXSR      Fakturert
     C     Slutt         tag
     C                   seton                                        LR
     C                   return
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *entry        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UINT              5 0
     c                   z-add     uavd          faavd
     c                   z-add     uopd          faopd
     C                   z-add     uint          xint              5 0
     C                   MOVE      '****'        bltr              4
      *
     C     FakKey        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
      *
     c     *loval        setll     FIRM
     c                   read      FIRM
      *
     c     fakkey        chain     headf                              33
      * TEST LK INC/OMIT PÅ "UTENLANDS-SIDEN" (=HELKA VED SPEDISJ.OPPD)
     C                   MOVE      HELKA         INOMLK            2
      * TRANSP.OPPD: HELKA=FRASIDE/HETRI=TILSIDE (HVIS NORGE, BRUK TIL)
     C     HEUR          IFEQ      'H'
     C     HELKA         IFEQ      FILAND
     C                   MOVE      HETRI         INOMLK            2
     C                   ELSE
     C     HETRI         IFNE      FILAND
     C                   MOVE      HETRI         INOMLK            2
     C                   END
     C                   END
     C                   END
     C                   ENDSR
      *
     C***********************************************
     C     Fakturert     BEGSR
     C***********************************************
      *  sjekk om gebyret allerede er på fakturabildet
      *  PKGEBY
      *  den som har frakt belastet skal ha gebyret
      *
     C     KEYGENGE      KLIST
     C                   KFLD                    ALLE              8
     C                   KFLD                    HEFRGE            3
     C                   KFLD                    HEOTGE            2
     C                   move      '    ALLE'    ALLE
     C                   move      'GEN'         HEFRGE
     C                   move      'GE'          HEOTGE
     C                   SETOFF                                       55
     c     keygenge      SETLL     prik1
     C     *IN55         DOWEQ     '0'
     c     keygenge      READE     prik1                                  55
     C     *IN55         IFEQ      '0'
     c                   move      'N'           Skipp             1
      *
     C     fakkey        setll     FAKT
     C     fakkey        reade(N)  FAKT                                   34
     C     *in34         doweq     '0'
     c                   if        FASK = PKAVTN
     C                   move      FASK          PARTSK            1
     C                   move      FAKUNR        PARTKU            8 0
      * GEBYR LIGGER DER FRA FØR  SKIPP
     c                   if        FAVK = PKGEBY
     c                   move      'J'           Skipp
     c                   leave
     c                   endif
     c                   endif
     C     fakkey        reade(N)  FAKT                                   34
     C                   enddo
     c     skipp         ifeq      'N'
N38  C     EPOKEYk       KLIST
N38  C                   KFLD                    FIFIRM
N38  C                   KFLD                    mail30
N38  C                   KFLD                    PARTKU
N38  C     'fa'          chain     arktxt                             33
N38  C                   MOVE      ARTXT         MAIL16           16
N38  C                   MOVEL     '*'           MAIL16
N38  c                   movel     MAIL16        mail30           30
      *
N38  C                   MOVE      'N'           EPOSTK            1
N38  c     epokeyk       CHAIN     cundc03                            33
N38  c  N33              MOVE      'J'           epostk
     c                   exsr      tstgio
     C   78              MOVE      'J'           SKIPP
     c                   end
      *
     C     SKIPP         IFEQ      'N'
     C                   EXSR      CrtTilLin
     C                   END
      *
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     CrtTilLin     BEGSR
     C***********************************************
      * legg ut linje i FAKT
      *
      * hent pris
      *
      *
     C     KEYPRIS       KLIST
     C                   KFLD                    PKTNR
     C                   KFLD                    PKGEBY
     C                   KFLD                    XINT
      *
      *
     C     KEYPRIS1      KLIST
     C                   KFLD                    PKTNR
     C                   KFLD                    PKGEBY
      *
     c     keypris       setll     pris
     c     keypris1      reade     pris                                   33
     c     *in33         ifeq      '0'
     c                   CLEAR                   FAKTR
     C                   MOVE      PARTSK        FASK
     C                   MOVE      PARTKU        FAKUNR
     C                   MOVE      UAVD          FAAVD
     C                   MOVE      UOPD          FAOPD
     C                   MOVE      PKGEBY        FAVK
     C                   MOVE      FICURR        FAVAL
     C                   MOVE      'B'           FAKDA
     C                   MOVE      PKMVA         FAKDM
     C                   eval      FABELV = PRBELØ
     C                   eval      FABELN = PRBELØ
     C                   move      *BLANKS       FAVT
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   exsr      valuta
     C                   WRITE     FAKTR
     C                   end
     C                   ENDSR
      ***********************************************
     C     VALUTA        BEGSR
      ***********************************************
      * Converts from local currency to whatever currency that is
      * to be used on the invoice.
      * If the invoice is to be written in local currency or the line
      * already is in the right currency, nothing is done.
      *
     C*
     C*CURRENCY INVOICED (BLANK=FICURR)
     C     VALU1K        KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    wsval
     C                   MOVE      *BLANKS       FACURI
     C     FASK          IFEQ      'I'
     C                   MOVE      FAVAL         WSVAL             3
     C                   ELSE
     C     FASK          IFEQ      'S'
     C                   MOVE      HEVALS        WSVAL
     C                   ELSE
     C     FASK          IFEQ      'K'
     C                   MOVE      HEVALK        WSVAL
     C                   ELSE
     C     FASK          IFEQ      'X'
     C                   MOVE      VALKOD        WSVAL
     C                   END
     C                   END
     C                   END
     C                   END
      * SKAL FAKTURERES I "HJEMMEVALUTA" - HOPP UT..
     C     WSVAL         IFEQ      *BLANKS
     C     WSVAL         OREQ      FICURR
     C                   GOTO      SLVAL
     C                   END
      *
      * SKAL FAKTURERES I "FREMMEDVALUTA" MEN LINJA HAR ALT DEN SAMME
      * INGEN OMREGNING - FREMMEDVALUTAEN LAGRES PÅ LINJA -HOPP SÅ UT
      *
     C     FAVAL         IFEQ      WSVAL
     C                   MOVE      WSVAL         FACURI
     C                   GOTO      SLVAL
     C                   END
     C*
     C     VALU1K        CHAIN     VALUL01                            31
      *
     C     FABELN        MULT      OMRFAK        XWORK            17 5
     C     XWORK         DIV(H)    VALKU1        FABELV
     C                   MOVE      WSVAL         FAVAL
     C*CURRENCY INVOICED (NÅR FORSKJELLIG FRA FICURR)
     C                   MOVE      WSVAL         FACURI
     C*                  DUMP
      *
     C     SLVAL         ENDSR
      *
      ***********************************************
     C     TSTGIO        BEGSR
      ***********************************************
      *
      *    INDIKATOR  78    betyr ikke skal være med
      *
      *          contains all include/omit conditions possible for one
      *          priceline (max. include or omit per condition)
      *
     C                   SETOFF                                       78
      * KODE I HOPP UT VED EPOSTKUNDE
N38  C     PKKOLL        IFEQ      'I'
N38  C     EPOSTK        ANDEQ     'J'
N38  C                   SETON                                        78
N38  C                   GOTO      UTGIO
N38  C                   END
      *
s41  C*    PRJOIK        CHAIN     PRJOIN                             80
s41  C* N80              DO                                                     I
      * ______________________________
      * Ursprungskode - Include / Omit
      * """"""""""""""""""""""""""""""
     C     PKURIO        IFNE      ' '                                          II
     C     PKURIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HEUR          LOOKUP    UR                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HEUR          LOOKUP    UR                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
N59   *--------------------------------
N59   * ved *AVD  er det avdelinger
N59   *--------------------------------
N59  C     TR(1)         IFEQ      '*AVD'
N59  C     PTRAIO        ANDNE     ' '                                          III
N59  C                   SETOFF                                       80
N59  C                   movel     heavd         test4             4
N59  C     test4         LOOKUP    TR                                     80
N59  C     PTRAIO        IFEQ      'I'                                          III
N59  C  N80              SETON                                        78
N59  C                   ELSE
N59   * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
N59  C   80              SETON                                        78
N59  C                   END                                                    III
N59  C                   ELSE                                                   II
      *--------------------------------
N40   * ved *pro  er det produktkode
      *--------------------------------
N40  C     TR(1)         IFEQ      '*PRO'
N40  C     HEKDPL        ANDNE     ' '
N40  C     PTRAIO        ANDNE     ' '                                          III
N40  C                   SETOFF                                       80
N40  C                   movel     hekdpl        test4             4
N40  C     test4         LOOKUP    TR                                     80
N40  C     PTRAIO        IFEQ      'I'                                          III
N40  C  N80              SETON                                        78
N40  C                   ELSE
N40   * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
N40  C   80              SETON                                        78
N40  C                   END                                                    III
N40  C                   ELSE                                                   II
      *--------------------------------
N43   * ved *TIL  er det tilleggsgebyrer
      *--------------------------------
N43  C     TR(1)         IFEQ      '*TIL'
N43  C     TPX           ANDNE     *BLANKS
N43  C     PTRAIO        ANDNE     ' '                                          III
      * skal alltid følge frakten
N48  C     PARTSK        COMP      TRKDFF                                 80
N48  C  N80              SETON                                        78
N48  C   78              GOTO      UTGIO
      *includetester prøver å finne en gyldig
N43  C                   SETOFF                                       80
N43  C     PTRAIO        IFEQ      'I'                                          III
N43  C     1             do        5             NR                2 0
N43  C     TP(NR)        ifne      ' '
N43  C                   movel     TP(NR)        test4             4
N43  C     test4         LOOKUP    TR                                     80
N43  c   80              goto      tilut
N43  C                   end
n43  c                   end
N43  c                   seton                                        78
      *omittester
N43  C                   ELSE
N43   * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
N43  C     1             do        5             NR
N43  C     TP(NR)        ifne      ' '
N43  C                   movel     TP(NR)        test4             4
N43  C     test4         LOOKUP    TR                                     80
N40  C   80              SETON                                        78
N43  c   80              goto      tilut
N43  C                   end
n43  c                   end
N43  C                   END                                                    III
N43  C                   ELSE                                                   II
      * ______________________________
      * trafikk sjø     Include / Omit
      * transportdisp benyttes til *IMP *EKSP *INN
      * """"""""""""""""""""""""""""""
     C     PTRAIO        IFNE      ' '                                          II
     C     PTRAIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     BLTR          LOOKUP    TR                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     BLTR          LOOKUP    TR                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
N40  C                   END                                                    II
N43  C                   END                                                    II
N59  C                   END                                                    III
N43  C     TILUT         TAG
      * ___________________________________
      * Frankatur/Oppdt. er låst ved brutto
      * """""""""""""""""""""""""""""""""""
      *
      * ______________________________
      * Oppdr.type    - Include / Omit
      * """"""""""""""""""""""""""""""
      * ______________________________
      * Landkode     - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKLKIO        IFNE      ' '                                          II
     C     PKLKIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     INOMLK        LOOKUP    LK                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     INOMLK        LOOKUP    LK                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
      *
      * ______________________________
      * Frasted      - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKFSIO        IFNE      ' '                                          II
     C     PKFSIO        IFEQ      'I'                                          III
N42  C     FS(1)         IFEQ      '*FRTI'
N42  C     HESDF         IFLT      FS(2)
N42  C     HESDF         ORGT      FS(3)
N42  C                   SETON                                        78
N42  C                   END
N42  C                   ELSE
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDF         LOOKUP    FS                                     80
     C  N80              SETON                                        78
N42  C                   END
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
N42  C     FS(1)         IFEQ      '*FRTI'
N42  C     HESDF         IFGE      FS(2)
N42  C     HESDF         ANDLE     FS(3)
N42  C                   SETON                                        78
N42  C                   END
N42  C                   ELSE
     C     HESDF         LOOKUP    FS                                     80
     C   80              SETON                                        78
N42  C                   END
     C                   END                                                    III
     C                   END                                                    II
      *
      * ______________________________
      * Tilsted      - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKTSIO        IFNE      ' '                                          II
     C     PKTSIO        IFEQ      'I'                                          III
N42  C     TS(1)         IFEQ      '*FRTI'
N42  C     HESDT         IFLT      TS(2)
N42  C     HESDT         ORGT      TS(3)
N42  C                   SETON                                        78
N42  C                   END
N42  C                   ELSE
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDT         LOOKUP    TS                                     80
     C  N80              SETON                                        78
N42  C                   END
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
N42  C     TS(1)         IFEQ      '*FRTI'
N42  C     HESDT         IFGE      TS(2)
N42  C     HESDT         ANDLE     TS(3)
N42  C                   SETON                                        78
N42  C                   END
N42  C                   ELSE
     C                   SETOFF                                       80
     C     HESDT         LOOKUP    TS                                     80
     C   80              SETON                                        78
N42  C                   END
     C                   END                                                    III
     C                   END                                                    II
      * ______________________________
      * Losssested      - Include / Omit
      *(Gebyrer som er betinget av valgfrie felt tas
      * aldri med dersom feltet i oppdraget er blankt)
      * """"""""""""""""""""""""""""""
     C  N78PKLSIO        IFNE      ' '                                          II
     C     HESDL         IFEQ      *BLANKS                                      III
     C                   SETON                                        78
     C                   ELSE
     C     PKLSIO        IFEQ      'I'                                          IIII
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDL         LOOKUP    LS                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HESDL         LOOKUP    LS                                     80
     C   80              SETON                                        78
     C                   END                                                    IIII
     C                   END                                                    III
     C                   END                                                    II
      *
      * ______________________________
      * Lastested      - Include / Omit
      *(Gebyrer som er betinget av valgfrie felt tas
      * aldri med dersom feltet i oppdraget er blankt)
      * """"""""""""""""""""""""""""""
     C  N78PKL2IO        IFNE      ' '                                          II
     C     HESDLA        IFEQ      *BLANKS                                      III
     C                   SETON                                        78
     C                   ELSE
     C     PKL2IO        IFEQ      'I'                                          IIII
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDLA        LOOKUP    L2                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HESDLA        LOOKUP    L2                                     80
     C   80              SETON                                        78
     C                   END                                                    IIII
     C                   END                                                    III
     C                   END                                                    II
      *
s41  C*                  END                                                    I
      *
      * _______________________________________________________________
      * UTLØPSDATO (indikator 79 varsler at minsten linje i avt utløpt)
      * """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     PKDT          IFGT      0
     C  N78
     CANN79PKDT          COMP      HEDTOP                               79
     C  N78PKDT          COMP      HEDTOP                               78
     C                   END
      * Linjen ok? (indikator 78 varsler at denne linje ikke gjelder)
      *
     C     UTGIO         ENDSR
      *
