      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KURERMANIFEST             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADKML01  IF   E           K DISK    RENAME(SADKMFR:SADKMFR01)
     FSADKML02  IF   E           K DISK    RENAME(SADKMFR:SADKMFR02)
     FSADKML03  IF   E           K DISK    RENAME(SADKMFR:SADKMFR03)
     FSADKML04  IF   E           K DISK    RENAME(SADKMFR:SADKMFR04)
     FSADKML05  IF   E           K DISK    RENAME(SADKMFR:SADKMFR05)
     FSADKMFOPD UF   E           K DISK
     FSADKMCL1  IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSAD110D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  WSATDDD                1      2  0
     D  WSATDDM                3      4  0
     D  WSATDDA                5      6  0
     D  WSATDD                 1      6  0
     D                 DS
     D  XSATDDC                1      2  0
     D  XSATDDA                3      4  0
     D  XSATDDM                5      6  0
     D  XSATDDD                7      8  0
     D  XSATDD                 1      8  0
     D                 DS
     D  W2ATDDD                1      2  0
     D  W2ATDDM                3      4  0
     D  W2ATDDA                5      6  0
     D  W2ATDD                 1      6  0
     D                 DS
     D  X2ATDDC                1      2  0
     D  X2ATDDA                3      4  0
     D  X2ATDDM                5      6  0
     D  X2ATDDD                7      8  0
     D  X2ATDD                 1      8  0
     D                 DS
     D  KMATDDC                1      2  0
     D  KMATDDA                3      4  0
     D  KMATDDM                5      6  0
     D  KMATDDD                7      8  0
     D  KMATDD                 1      8  0
     D                 DS
     D  X_KMATDDD              1      2  0
     D  X_KMATDDM              3      4  0
     D  X_KMATDDA              5      6  0
     D  X_KMATDD               1      6  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  XSDTRC                 1      2  0
     D  XSDTRA                 3      4  0
     D  XSDTRM                 5      6  0
     D  XSDTRD                 7      8  0
     D  XSDTR                  1      8  0
     D                 DS
     D  W2DTRD                 1      2  0
     D  W2DTRM                 3      4  0
     D  W2DTRA                 5      6  0
     D  W2DTR                  1      6  0
     D                 DS
     D  X2DTRC                 1      2  0
     D  X2DTRA                 3      4  0
     D  X2DTRM                 5      6  0
     D  X2DTRD                 7      8  0
     D  X2DTR                  1      8  0
     D                 DS
     D  KMDTRC                 1      2  0
     D  KMDTRA                 3      4  0
     D  KMDTRM                 5      6  0
     D  KMDTRD                 7      8  0
     D  KMDTR                  1      8  0
     D                 DS
     D  X_KMDTRD               1      2  0
     D  X_KMDTRM               3      4  0
     D  X_KMDTRA               5      6  0
     D  X_KMDTR                1      6  0
     D                 DS
     D  xnr11                  1     11  0
     D  xnr11_avd              1      4  0
     D  xnr11_tdn              5     11  0
     D  xnr11_pro              4     11  0
     D                 DS
     D  u_pro_a                1      8
     D  u_pro_n                1      8  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'SAD003'
     d SAD003          pr                  extpgm('SAD003')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d win03_                              like(win03)
      *  Prototype for 'SADKMJSON'
     d SADKMJSON       pr                  extpgm('SADKMJSON')
     d u_pro_a_                            like(u_pro_a)
      *  Prototype for 'SAD111R'
     d SAD111R         pr                  extpgm('SAD111R')
     d u_avd_                              like(u_avd)
     d u_pro_                              like(u_pro)
     d usg_                                like(usg)
     d xtype_                              like(xtype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD112R'
     d SAD112R         pr                  extpgm('SAD112R')
     d kmavd_                              like(kmavd)
     d kmpro_                              like(kmpro)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for SAD110R
     d sad110r         pr
      *  *ENTRY Interface for Main Procedure
     d sad110r         pi
      *
     d i               s              3s 0
     d xrecno          s              5s 0
     d xrecn2          s              5s 0
     d xtype           s              3a
     d x_kmst          s                   like(kmst)
     d x_fortsett      s              1a
     d x_sokok         s              1a
     d wdt             s              8a
     d wtm             s              6a
     d win03           s              1a
     d u_avd           s              4  0
     d u_pro           s              8  0
     d u_tdn           s              7  0
     d uflagg          s              1  0
     d utype           s              3a
      *
      /free

         exsr init;

         dow *in03 = *off;
           exsr srvistur;
         enddo;

         if wsavd <> 0;
           uavd = wsavd;
         endif;

         *inlr = *on;
         return;


      // ********************************************
       begsr srvistur;

       *in93 = *on;
       write subctl;
       *in93 = *off;
       *in94 = *off;
       zrecno = 0;
       x_fortsett = 'J';

       dow x_fortsett = 'J';        // START                                   // 1<b

         exsr setkey;

         dow *in03 = *off;                                                     // 2<b
           if *in94 = *off;          // START1                                 // 3<b
           // les post til max 19 poster pr gang.
             for i = 1 to 19;                                                  // 4<b
               exsr lesrec;
               if *in94 = *off;                                                // 5<b
                 X_KMATDDD = KMATDDD;
                 X_KMATDDM = KMATDDM;
                 X_KMATDDA = KMATDDA;
                 X_KMDTRD  = KMDTRD;
                 X_KMDTRM  = KMDTRM;
                 X_KMDTRA  = KMDTRA;
                 chain kmpro sadkmcl1;
                 if not %found;                                                // 6<b
                   mcnas = *blanks;
                 endif;                                                        // 6<e
                 zrecno = zrecno + 1;
                 write subfil;
                 xrecn2 = zrecno;
               endif;                                                          // 5<e
             endfor;                                                           // 4<e
             xrecno = zrecno;
           endif;                                                              // 3<e

           dow *in03 = *off;             // START3                             // 3<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                    // 4<b
               if *in27 = *off;                                                // 5<b
                 *in27 = *on;
                 x_fortsett = 'N';
                 leave;
               endif;                                                          // 5<e
               exfmt nyvare;
             else;                                                             // 4
               exfmt subctl;
             endif;                                                            // 4<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;

             select;                                                           // 4<b
             when *in03;                                                       // 4
               *inlr = *on;
               return;
             when *in05;                                                       // 4
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               exsr setkey;
               leave;
             when *in06;                                                       // 4
               exsr crttur;
             when *in08 or *in12;                                              // 4
               exsr srsok;      // handle om START0
               leave;           // til start
             other;                                                            // 4
               //if w2nr <> 0 or w2pro <> 0 or w2uuid <> *blanks or w2atdd <> 0    // 4<b
               if w2nr <> 0 or w2pro <> 0 or w2atdd <> 0                       // 4<b
                  or w2st <> *blanks or w2dtr <> 0;
                 if w2nr = 0;                                                  // 6<b
                   exsr nysok;
                   leave;          // til START
                 else;                                                         // 6
                   if w2nr = 1;                                                // 7<b
                     *in06 = *on;
                   endif;                                                      // 7<e
                   if *in06;                                                   // 7<b
                     if w2avd <> 0;                                            // 8<b
                       wsavd = w2avd;
                     endif;                                                    // 8<e
                     wsnr = 0;
                     wsavd = 0;
                     exsr crttur;
                     leave;
                   endif;                                                      // 7<e
                   if w2pro = 0;                                               // 7<b
                     *in98 = *on;
                     *in30 = *on;
                     msgnr = 'YBC2034';
                   else;                                                       // 7
                     exsr nytur;
                     w2pro = 0;
                     //w2uuid = *blanks;
                     w2atdd = 0;
                     w2st = *blanks;
                     w2dtr = 0;
                     leave;
                   endif;                                                      // 7<e

                 endif;                                                        // 6<e
               endif;                                                          // 5<e

               if zrecno <> 0;                                                 // 5<b
                 exsr hent;
               endif;                                                          // 5<e

             endsl;                                                            // 4<e

           enddo;                                                              // 3<e

         enddo;                                                                // 2<e
       enddo;                                                                  // 1<e

       endsr;

      // ********************************************
       begsr srsok;

       x_sokok = 'N';
       dow x_sokok = 'N';
         exfmt bilde01;
         *in98 = *off;
         *in99 = *off;
         *in30 = *off;
         select;
           when *in03 = *on;
             *inlr = *on;
             return;
           when *in04 = *on;
             exsr sokkun;
           other;
           exsr testb1;
         endsl;
       enddo;

       *in93 = *on;
       *in94 = *off;
       write subctl;
       *in93 = *off;
       *in94 = *off;
       zrecno = 0;
       exsr setkey;

       endsr;

      // ********************************************
       begsr nysok;

       clear bilde01;
       syge15c (wdt: wtm);
       if w2avd = 0;
         wsavd = uavd;
       else;
         wsavd = w2avd;
       endif;
       select;
       when w2pro <> 0;
         wspro = w2pro;
       //when w2uuid <> *blanks;
         //wsuuid = w2uuid;
       when w2atdd <> 0;
         wsatdd = w2atdd;
       when w2st <> *blanks;
         wsst = w2st;
       when w2dtr <> 0;
         wsdtr = w2dtr;
       endsl;

       w2nr = 0;
       w2avd = 0;
       w2pro = 0;
       //w2uuid = *blanks;
       w2atdd = 0;
       w2st = *blanks;
       w2dtr = 0;

       *in98 = *off;
       *in99 = *off;
       exsr testb1;

       endsr;

      // ********************************************
       begsr nytur;

       if w2avd = 0;
         w2avd = uavd;
       endif;
       chain (wsavd: w2pro) sadkml01;
       if not %found;
         msgnr = 'YBC2035';
         *in98 = *on;
       else;
         *in90 = *on;
         select;
         when w2nr = 2;
          // kan ikke gå inn på normale oppdrag fra dummy
           if kmst <> ' ' and kmst <> 'C' and kmst <> 'M';
             *in30 = *on;
             msgnr = 'YBC2036';
           else;
             exsr chgtur;
           endif;
         when w2nr = 3;
           exsr cpytur;
         when w2nr = 5;
           exsr dsptur;
         when w2nr = 9;
           exsr dsplog;
         endsl;
       endif;

       if *in98;
         *in30 = *on;
       else;
         w2avd = 0;
         w2pro = 0;
       endif;
       *in90 = *off;
       w2nr = 0;

       endsr;

      // ********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in99 = *off;
       readc subfil;
       dow not %eof;
         select;
         when wsnr = 2;
           *in99 = *on;
           if kmst <> ' ' and kmst <> 'C' and kmst <> 'M';
             *in30 = *on;
             msgnr = 'YBC2036';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           exsr chgtur;
         when wsnr = 3;
           *in99 = *on;
           exsr cpytur;
         when wsnr = 5;
           *in99 = *on;
           exsr dsptur;
         when wsnr = 9;
           *in99 = *on;
           exsr dsplog;
         when wsnr = 90;
           if kmst <> ' ' and kmst <> 'C' and kmst <> 'M';
             *in30 = *on;
             msgnr = 'YBC2036';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           *in99 = *on;
           exsr sndtur;
         endsl;
         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      // ********************************************
       begsr sokkun;

       linnbr = wscloc / 256;
       posnbr = wscloc - (%int(linnbr) * 256);

       endsr;

      // ********************************************
       begsr setcur;

       posnbr = 35;
       select;
       when wspro <> 0;
         linnbr = 8;
       when wsuuid <> *blanks;
         linnbr = 9;
       when wsatdd <> 0;
         linnbr = 10;
       when wsdtr <> 0;
         linnbr = 11;
       when wsst <> *blanks;
         linnbr = 12;
       other;
         linnbr = 8;
       endsl;

       endsr;

      // ********************************************
       begsr dsptur;

       xtype = 'DSP';            // SPØRRING
       u_avd = kmavd;
       u_pro = kmpro;
       sad111r (u_avd: u_pro: usg: xtype: uflagg);
       select;
       when uflagg = 3;
         *inlr = *on;
         return;
       when *in90 = *off;
         wsnr = 0;
         update subfil;
       endsl;

       endsr;

      // ********************************************
       begsr chgtur;

       xtype = 'CHG';                // ENDRING
       u_avd = kmavd;
       u_pro = kmpro;
       sad111r (u_avd: u_pro: usg: xtype: uflagg);

       x_kmst = kmst;
       chain (u_avd: u_pro) sadkmfopd;
       kmst = x_kmst;
       update sadkmfr;
       X_KMATDDD = KMATDDD;
       X_KMATDDM = KMATDDM;
       X_KMATDDA = KMATDDA;
       X_KMDTRD  = KMDTRD;
       X_KMDTRM  = KMDTRM;
       X_KMDTRA  = KMDTRA;
       chain kmpro sadkmcl1;
       if not %found;
         mcnas = *blanks;
       endif;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr cpytur;

       xtype = 'CPY';                // KOPIERING
       u_avd = kmavd;
       u_pro = kmpro;
       sad111r (u_avd: u_pro: usg: xtype: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       if u_pro <> 0 and u_pro <> kmpro;
         chain(n) (u_avd: u_pro) sadkmfopd;
         X_KMATDDD = KMATDDD;
         X_KMATDDM = KMATDDM;
         X_KMATDDA = KMATDDA;
         X_KMDTRD  = KMDTRD;
         X_KMDTRM  = KMDTRM;
         X_KMDTRA  = KMDTRA;
         chain kmpro sadkmcl1;
         if not %found;
           mcnas = *blanks;
         endif;
         xrecn2 = xrecn2 + 1;
         zrecno = xrecn2;
         write subfil;
       endif;

       endsr;

      // ********************************************
       begsr crttur;

       xtype = 'CRT';                // LAGE NY TUR
       if wsavd = 0;
         u_avd = uavd;
       else;
         u_avd = kmavd;
       endif;
       u_pro = 0;
       sad111r (u_avd: u_pro: usg: xtype: uflagg);

       if u_pro > 0;
         chain(n) (u_avd: u_pro) sadkmfopd;
         if %found;
           if u_avd = 0;
             wsavd = u_avd;
           else;
             uavd = u_avd;
           endif;
           X_KMATDDD = KMATDDD;
           X_KMATDDM = KMATDDM;
           X_KMATDDA = KMATDDA;
           X_KMDTRD  = KMDTRD;
           X_KMDTRM  = KMDTRM;
           X_KMDTRA  = KMDTRA;
           chain kmpro sadkmcl1;
           if not %found;
             mcnas = *blanks;
           endif;
           xrecn2 = xrecn2 + 1;
           zrecno = xrecn2;
           write subfil;
         endif;
       endif;

       endsr;

      // ********************************************
       begsr dsplog;

       win03 = '7';                // Vis edi logg
       xnr11 = 0;
       xnr11_pro = kmpro;
       u_avd = xnr11_avd;
       u_tdn = xnr11_tdn;
       sad003 (u_avd: u_tdn: win03);
       if win03 = '1';
         *inlr = *on;
         return;
       endif;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr sndtur;

       if kmst = ' ' or kmst = 'C' or kmst = 'M';
         chain ('S': 'KURERM') ecmsg1;
         if %found;
           u_pro_n = kmpro;
           sadkmjson (u_pro_a);
           chain (kmavd: kmpro) sadkmfopd;
           kmst = 'B';
           kmerr = *blanks;
           update sadkmfr;
         else;
           exfmt errmsg;
         endif;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr testb1;

       koanvn = *blanks;
       if wsavd > 0;
         chain ('A': wsavd) kodta;
         if not %found;
           linnbr = 4;
           posnbr = 26;
           msgnr = 'SPA0001';
           *in30 = *on;
           *in99 = *on;
         else;
           uavd = wsavd;
           chain (fifirm: koaknr) cundl01;
         endif;
       endif;

       *in41 = *off;
       *in42 = *off;
       *in43 = *off;
       *in44 = *off;
       *in45 = *off;
       x_sokok = 'J';
       select;
       when wspro > 0;
         *in41 = *on;
       when wsuuid <> *blanks;
         *in42 = *on;
       when wsatdd > 0;
         *in43 = *on;
       when wsdtr > 0;
         *in44 = *on;
       when wsst <> *blanks;
         *in45 = *on;
       other;
         x_sokok = 'N';
         msgnr = 'DIV0064';
         *in30 = *on;
         *in98 = *on;
         linnbr = 8;
         posnbr = 35;
       endsl;

       endsr;

      // ********************************************
       begsr setkey;

       select;
       when *in41;
       when *in42;
       when *in43;
         if wsatdd = 999999;
           xsatdd = 99999999;
         else;
           xsatddd = wsatddd;
           xsatddm = wsatddm;
           xsatdda = wsatdda;
           xsatddc = 20;
         endif;
       when *in44;
         if wsdtr = 999999;
           xsdtr = 99999999;
         else;
           xsdtrd = wsdtrd;
           xsdtrm = wsdtrm;
           xsdtra = wsdtra;
           xsdtrc = 20;
         endif;
       when *in45;
       endsl;

       exsr mintur;

       endsr;

      // ********************************************
       begsr mintur;

       select;
       when *in41;
         if wsavd = 0;
           setll wspro sadkml02;
         else;
           setll (wsavd: wspro) sadkml01;
         endif;
       when *in42;
         setll wsuuid sadkml03;
       when *in43;
         setll xsatdd sadkml04;
       when *in44;
         setll xsdtr sadkml05;
       when *in45;
       endsl;

       endsr;

      // ********************************************
       begsr lesrec;

       select;
       when *in41;
         if wsavd = 0;
           read sadkml02;
         else;
           reade wsavd sadkml01;
         endif;
       when *in42;
         read sadkml03;
       when *in43;
         if xsatdd = 99999999;
           readp sadkml04;
         else;
           read sadkml04;
         endif;
       when *in44;
         if xsdtr = 99999999;
           readp sadkml05;
         else;
           read sadkml05;
         endif;
       when *in45;
       endsl;

       if %eof;
         *in94 = *on;
         i = 50;
       endif;

       endsr;

      // ********************************************
       begsr init;

       wspro = 0;
       wsavd = uavd;
       wsatdd = 0;
       wsdtr = 999999;
       w2atdd = 0;
       w2dtr = 0;
       *in41 = *off;
       *in42 = *off;
       *in43 = *off;
       *in44 = *on;
       *in45 = *off;

       setll *loval firm;
       read firm;
       chain ('A': wsavd) kodta;
       chain (fifirm: koaknr) cundl01;

       syge15c (wdt: wtm);

       endsr;

       /end-free
