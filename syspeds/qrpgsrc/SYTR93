      *
      ****************************************************************
      *** PROGRAM FOR UTSKRIFT AV DAGLISTE                         ***
      ****************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
10172 * 02 SH  PTF10 ny parameter syge03
10172 * 02 SH  PTF11 tatt bort html kode i printfil
10172 * 03 SH  PTF11 rettet  til 10 dager
********************************************************************
      *
     FKODTFM    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFAKT      IF   E           K DISK
     FTRACKL02  IF A E           K DISK
     FTRACKL03  IF   E           K DISK
     F                                     RENAME(TRACKFR:TRACKFR3)
     FCUNDC03   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FHEAD33    IF   E           K DISK
     FHEADF     IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFRF)
     FECMSG     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FEDTM93A   O  A E             DISK
     FECNT93A   O  A E             DISK
     FERFF93A   O  A E             DISK
     FETCC93A   O  A E             DISK
     FEQTY93A   O  A E             DISK
     FETDT93A   O  A E             DISK
     FENAD93A   O  A E             DISK
     FEMOA93A   O  A E             DISK
     FEGID93A   O  A E             DISK
     FEMEA93A   O  A E             DISK
     FSYTR93P   O    E             PRINTER OFLIND(*IN89)
     F                                     USROPN
NRFA D                 DS
NRFA D  HERFA                  1     35
NRFA D  HERFA15                1     15
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D  UDATO                  7     14
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  HXSNDN                 1     20
     D  HERF20                 1     20
     D  XRF401                 1      3
     D  HERF17                 4     20
     D  HERF10                 1     10
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)                              Num edit
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  UUSR                 254    263
     C*
     C                   EXSR      INIT
      *
      * KOPIUTSKRIFT
      *
     C     KOPI          IFEQ      'J'
     C                   OPEN      SYTR93P
     C                   EXSR      KOPISR
     C                   CLOSE     SYTR93P
     C     ANTORD        IFGT      0
     C                   MOVE      'J'           SPOOL
     C                   ELSE
     C                   MOVE      'N'           SPOOL
     C                   END
     C                   SETON                                        LR
     C                   RETURN
     C                   END
      *-----------------------------------------------------------
      * TAR FØRST ALLE SOM SKAL TIL UTSKRIFT
      * VED TYPE = P
      *-----------------------------------------------------------
     C     TYPE          IFEQ      'P'
     C                   OPEN      SYTR93P
     C     KEYKP         SETLL     CUNDC03
     C*
     C     *IN70         DOWEQ     '0'
     C*
     C     KEYKP         READE     CUNDC03                                70
     C*
     C*
     C     *IN70         IFEQ      '0'
     C     CFAX          ANDEQ     *BLANKS
     C     CEMAIL        ANDEQ     *BLANKS
     C                   EXSR      PRINT
     C                   END
     C                   END
     C                   CLOSE     SYTR93P
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C*
      *-----------------------------------------------------------
      * PRINT VED FORESPØRSEL PÅ 1 KUNDE
      *-----------------------------------------------------------
     C*
     C     CCOMPN        IFNE      *ZEROS
     C*
     C                   OPEN      SYTR93P
     C                   EXSR      PRINT
     C     ANTORD        IFGT      0
     C                   MOVE      'J'           SPOOL
     C                   ELSE
     C                   MOVE      'N'           SPOOL
     C                   END
     C                   END
     C                   CLOSE     SYTR93P
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C     USER          PARM      USER          USER             10
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C     COPY          PARM      COPY          COPY              2 0
     C     TYPE          PARM      TYPE          TYPE              1
     C     UKUND         PARM      UKUND         UKUND             8
     C     SPOOL         PARM      SPOOL         SPOOL             1
     C     KOPI          PARM      KOPI          KOPI              1
     C     DATOK         PARM      DATOK         DATOK             8
     C     TYPE          COMP      'P'                                    80
     C     TYPE          COMP      'F'                                    81
     C     TYPE          COMP      'E'                                    82
     C                   MOVE      UKUND         CCOMPN
      *INITIER DATASTRUKTUR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
      *
      * NØKKEL TIL KONTAKTPERSEN
      *
     C     KEYKP         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KP               30
     C                   MOVEL     '*DAGSLIS'    KP10             10
     C                   MOVE      'TE'          KP10
     C                   MOVEL     KP10          KP
      *
      * NØKKEL TIL KUNDEFIL
      *
     C     KEYKU2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    CCOMPN
      *
      * NØKKEL TIL KUNDEFIL
      *
     C     KEYKU3        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    UKUNDN
      *
      * NØKKEL TIL HEADINGFIL MED OPPDRAGSGIVER
      * Ky for let etter oppdrag max xxx dager gamle som ennå ikke er rapportert
     C     KEYKUNdt      KLIST
     C                   KFLD                    CCOMPN
     C                   KFLD                    FraDt
s03  C*                  Z-ADD     60            DAGER
n03  C                   Z-ADD     10            DAGER
     C                   CALL      'SYGE20'
     C                   PARM      'SUB'         FUNK              3
     C                   PARM      0             INNDT             8 0
     C                   PARM                    FraDt             8 0
     C                   PARM                    DAGER             5 0
     C     KEYKUN        KLIST
     C                   KFLD                    CCOMPN
      *
      * KEY TIL TRACK AND TRACE
      *
     C     KEYTT         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   MOVE      'PDL'         TTACTI
      *
      * KEY TIL TRACK AND TRACE VED KOPI
      *
     C     KEYTTK        KLIST
     C                   KFLD                    TTACTI
     C                   KFLD                    TTDATE
     C                   MOVE      DATOK         TTDATE
      *
      * KEY TIL HEADF VED KOPI
      *
     C     KEYHEA        KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
      *
      * KEY TIL FAKTURA-FIL
      *
     C     KEYFAK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
      * KEY TIL EDI-MELDINGSTYPE-FIL
      *
     C     ECMKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    CCOMPN
     C                   KFLD                    WCSR
     C                   KFLD                    WC0065
     C     *LIKE         DEFINE    ECSR          WCSR
     C     *LIKE         DEFINE    EC0065        WC0065
     C                   MOVE      'S'           WCSR
     C                   MOVEL     'IFTMCS'      WC0065
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    MREP0         WREP0
     C     *LIKE         DEFINE    MREP1         WREP1
     C     *LIKE         DEFINE    MREP2         WREP2
     C     *LIKE         DEFINE    CMN           WMN
     C                   Z-ADD     0             ØX                2 0                      --I
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
      *
      * SKRIV HEADING
      *
     C                   READ      FIRM                                   33
      *
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     KOPISR        BEGSR
     C***********************************************
     C                   MOVE      'N'           Justert           1
     C                   MOVE      *ZEROS        ANTORD            6 0
     C                   MOVE      *ZEROS        SUMMIN           11 2
     C                   MOVE      UKUND         UKUNDN            8 0
     C                   MOVE      TTDATE        ULDT
      *
     C     KEYKU3        CHAIN     CUNDL01                            33
      *
     C                   SETOFF                                       71
     C     KEYTTK        SETLL     TRACKL03
     C     *IN71         DOWEQ     '0'
     C     KEYTTK        READE     TRACKL03                               71
     C     *IN71         IFEQ      '0'
     C     KEYHEA        CHAIN     HEADF                              72
     C  N72UKUNDN        IFEQ      TRKNFA
     C     KEYKU2        CHAIN     CUNDL01                            33
     C                   EXSR      PRINT2
     C                   END
     C                   END
     C                   END
     C*
     C* Hvis minst en sending justert i Cargoscan - vis forklarende linje
     C     Justert       IFEQ      'J'
     C                   WRITE     PDETCS
     C                   END
     C*
     C* SKRIV TOTAL FOR MINIMUMSSENDINGER
     C*
     C     ANTORD        IFGT      0
     C     SUMMIN        ANDGT     *ZEROS
     C                   Z-ADD     SUMMIN        SUMMIX
     C                   WRITE     PTOTMI
     C                   END
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C                   MOVE      'N'           Justert           1
     C                   MOVE      *ZEROS        ANTORD            6 0
     C                   MOVE      *ZEROS        SUMMIN           11 2
     C                   SETOFF                                       71
     C     KEYKU2        CHAIN     CUNDL01                            33
     C*
     C*
     C     KEYKUNdt      SETLL     HEAD33
     C     *IN71         DOWEQ     '0'
     C     KEYKUN        READE     HEAD33                                 71
     C     *IN71         IFEQ      '0'
     C     heur          andeq     'H'
     C     KEYTT         CHAIN     TRACKL02                           72
     C   72              EXSR      PRINT2
     C                   END
     C                   END
     C*
     C* Hvis minst en sending justert i Cargoscan - vis forklarende linje
     C     Justert       IFEQ      'J'
     C                   WRITE     PDETCS
     C                   END
     C*
     C* SKRIV TOTAL FOR MINIMUMSSENDINGER
     C*
     C     ANTORD        IFGT      0
     C     SUMMIN        ANDGT     *ZEROS
     C                   WRITE     PTOTMI
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     PRINT2        BEGSR
     C***********************************************
     C*
     C                   MOVE      *ZEROS        NETTO             7 0
     C                   MOVE      *ZEROS        NETTOT           11 2
     C                   SETOFF                                       73
     C*
     C     KEYFAK        SETLL     FAKT
     C     *IN73         DOWEQ     '0'
     C     KEYFAK        READE     FAKT                                   73
     C     *IN73         IFEQ      '0'
     C     FAKDA         ANDNE     'K'
     C     FAKUNR        ANDEQ     TRKNFA
     C     FASK          IFEQ      'S'
     C     FASK          OREQ      'K'
     C                   ADD       FABELN        NETTO
     C                   ELSE
     C     FASK          IFEQ      'X'
     C     TRSLAG        IFNE      'HO'
     C                   ADD       FABELN        SUMMIN
     C                   ADD       FABELN        NETTOT
     C                   ELSE
     C                   ADD       FABELN        NETTO
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
      * ALLE MED FRAKT MOT S/K/X LOGGES TIL TRACK AND TRACE
      *
     C     KOPI          IFNE      'J'
     C     NETTO         IFGT      0
     C     NETTOT        ORGT      0
     C                   EXSR      TRACK
     C                   END
     C                   END
      *
      * FRAKT MÅ VÆRE INNLAGT MOT S/K FOR Å SKRIVE UT
      *
     C     NETTO         IFGT      0
     C                   EXSR      PRINT3
     C                   END
      *
     C                   ENDSR
     C*
     C***********************************************
     C     TRACK         BEGSR
     C***********************************************
     C*
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'PDL'         TTACTI
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      UUSR          TTUSER
     C                   MOVEL     'Print da'    XXTEXT           16
     C                   MOVE      'ylist   '    XXTEXT
     C                   MOVEL     XXTEXT        TTTEXT
n01  C                   EVAL      TTTEXL='utskrevet dagsliste'
     C                   WRITE     TRACKFR
     C                   ENDSR
     C*
     C***********************************************
     C     PRINT3        BEGSR
     C***********************************************
     C*
      *
      * RUNDE 1
      *
     C     ANTORD        IFEQ      0
     C                   WRITE     PHEAD
     C                   SETOFF                                       89
      * TYPE "I" = IFTMCS - FINNES KUNDEN I EDI-MELDINGS-TYPE-FIL
     C     TYPE          IFEQ      'I'
     C     ECMKEY        CHAIN     ECMSG                              51
     C     *IN51         IFEQ      '0'
     C                   MOVE      'J'           SNDEDI            1
     C                   ELSE
     C                   MOVE      'N'           SNDEDI
     C                   END
     C                   END
     C                   END
      *
     C* OVERFLOW
     C     *IN89         IFEQ      '1'
     C     TYPE          IFEQ      'P'
     C                   WRITE     PHEAD
     C                   ELSE
     C                   WRITE     PHEADE
     C                   END
     C                   SETOFF                                       89
     C                   END
      *
      *  HENTER BRUTTOPRIS
      *
     C                   MOVE      'B'           XKDA              1
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      *ZEROS        KUAVTA            8 0
     C                   MOVE      *BLANKS       USONE
     C                   CALL      'SYGE03'
     C                   PARM                    UGEB
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    KUAVTA
     C                   PARM                    UAVNR             9 0
n02  C                   PARM                    atdum             1
     C                   Z-ADD     UUBELØ        BRUTTO            7 0
     C     HXTERI        COMP      'J'                                    50
     C   50              MOVE      'J'           Justert
     C                   WRITE     PDET
     C                   ADD       1             ANTORD
      *
     C     TYPE          IFEQ      'I'
     C     SNDEDI        ANDEQ     'J'
     C                   EXSR      IFTMCS
     C                   END
      *
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     IFTMCS        BEGSR
     C***********************************************
      *________________________
      * RETREIVE MESSAGE NUMBER
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               51
     C     *IN51         IFEQ      '0'
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   ELSE
     C                   MOVE      'N'           SNDEDI
     C                   GOTO      UTCS
     C                   END
     C* LAGE EDISEGMENTER
     C                   EXSR      DTM00
     C                   EXSR      CNT00
     C                   EXSR      RFF03
     C                   EXSR      TCC07
     C                   EXSR      QTY07
     C                   EXSR      TDT08
     C                   EXSR      NAD10
     C                   EXSR      GID16
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTMCS'      U0065
     C                   MOVEL     'S'           U0052
     C                   MOVEL     '93A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'NOSM12'      U0057
     C     XRF401        IFEQ      '401'
     C                   MOVEL     HERF17        U0068
     C                   ELSE
     C                   MOVEL     HERF10        U0068
     C                   END
     C                   MOVEL     '700'         U1001
     C                   MOVEL     U0068         U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVE      ' '           M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
     C                   MOVEL     U0068         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   TIME                    XULTID                          HENT TID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   Z-ADD     ULDT          MDT
     C                   MOVEL     XULTM         MTM
     C                   Z-ADD     HEAVD         MAVD
     C                   Z-ADD     HEOPD         MTDN
     C                   Z-ADD     0             MFFBNR
     C                   MOVE      ' '           MVEN
     C                   WRITE     EDIMR
      *
     C*
     C     UTCS          ENDSR
     C*
     C***********************************************
     C     DTM00         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         DREP0
      *
     C                   MOVEL     '137'         D2005
     C                   MOVEL     ULDT          D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
      *
     C                   ENDSR
     C***********************************************
     C     CNT00         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
      *
     C                   MOVEL     '11'          C6069
     C                   Z-ADD     HENT          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'PCE'         C6411
     C                   WRITE     CNTR
      *
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '7 '          C6069
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR
      *
     C     HEM3          IFGT      0
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '15'          C6069
     C     HEM3          MULT      1000          ØN153
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'DMQ'         C6411
     C                   WRITE     CNTR
     C                   ENDIF
      *
     C     HELM          IFGT      0
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     'ZLM'         C6069
     C                   Z-ADD     HELM          ØN153
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'MTR'         C6411
     C                   WRITE     CNTR
     C                   ENDIF
      *
     C                   ENDSR
     C***********************************************
     C     RFF03         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
      *
     C                   MOVEL     'CU'          R1153
     C                   MOVEL     HERFA         R1154
     C                   WRITE     RFFR
      *
     C                   ENDSR
     C***********************************************
     C     TCC07         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
     C                   CLEAR                   TCCR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     7             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
      *__________________
      * TRANSPORT CHARGE/RATE CALCULATIONS
      *""""""""""""""""""
     C                   MOVEL     'ACG'         T8023
     C                   MOVEL     '132'         T1131A
     C                   MOVEL     '6'           T3055A
     C                   WRITE     TCCR
      *
     C                   ENDSR
     C***********************************************
     C     QTY07         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     7             QGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         QREP0
      *__________________
      * FRAKTBER.VKT
      *""""""""""""""""""
     C     HEFBV         IFEQ      *ZEROS
     C                   Z-ADD     HEVKT         HEFBV
     C                   END
     C                   MOVEL     '101'         Q6063
     C                   Z-ADD     HEFBV         ØN153
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       Q6060
     C                   MOVEL     ØAN           Q6060
     C                   MOVEL     'KGM'         Q6411
     C                   WRITE     QTYR
      *
     C                   ENDSR
     C***********************************************
     C     TDT08         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     8             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
      *__________________
      * TRANSPORT DETAILS
      *""""""""""""""""""
     C                   MOVEL     '20'          T8051
     C                   MOVEL     '3'           T8067
     C                   MOVEL     HEHAWB        T3127
     C                   MOVEL     '87'          T3055A
     C                   WRITE     TDTR
      *
     C                   ENDSR
     C***********************************************
     C     NAD10         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         NREP0
      *__________________
      * CZ=AVSENDER
      *""""""""""""""""""
     C                   MOVEL     'CZ'          N3035
     C                   MOVEL     HEKNS         N3039
     C                   MOVEL     '87'          N3055
     C                   WRITE     NADR
      *
      *__________________
      * CA=CARRIER
      *""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         NREP0
     C                   MOVEL     'CA'          N3035
     C                   MOVEL     FIFT          N3036A
     C                   WRITE     NADR
      * BELØP...
     C                   EXSR      TCC13
     C                   EXSR      MOA13
      *
     C                   ENDSR
     C***********************************************
     C     TCC13         BEGSR
     C***********************************************
      * REP.NIVÅ 1
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   TCCR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     13            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         TREP1
      *__________________
      * TRANSPORT CHARGE/RATE CALCULATIONS
      *""""""""""""""""""
      * ("HEADER" FOR MOA)
     C                   MOVEL     '100000'      T8023
     C                   MOVEL     '132'         T1131A
     C                   MOVEL     '6'           T3055A
     C                   WRITE     TCCR
      *
     C                   ENDSR
     C***********************************************
     C     MOA13         BEGSR
     C***********************************************
      * REP.NIVÅ 2
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     13            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         MREP2
      *__________________
      * MONETARY AMOUNT
      *""""""""""""""""""
      * BRUTTO BELØP
      * HVIS BRUTTO ER LAVERE ENN KUNDENS PRIS....
      * SETT KUNDENS PRIS SOM BRUTTO
     C     BRUTTO        IFLT      TRFRAB
     C                   Z-ADD(H)  TRFRAB        BRUTTO
     C                   END
      *
     C                   MOVEL     '23 '         M5025
     C                   Z-ADD     BRUTTO        ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      * RABATT
     C     BRUTTO        SUB(H)    TRFRAB        RABATT            7 0
     C     RABATT        IFGT      0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     13            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     '52 '         M5025
     C                   Z-ADD     RABATT        ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   END
     C                   ENDSR
     C***********************************************
     C     GID16         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP0
     C                   CLEAR                   GIDR
     C                   Z-ADD     WMN           GMN
     C                   Z-ADD     16            GGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         GREP0
      *__________________
      * SAMLET VARELINJE
      *""""""""""""""""""
     C                   MOVEL     '99999'       G1496
     C                   Z-ADD     HENT          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G7224A
     C                   MOVEL     'PK'          G7065A
     C                   WRITE     GIDR
      *
     C                   EXSR      MEA19
      *
     C                   ENDSR
     C***********************************************
     C     MEA19         BEGSR
     C***********************************************
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MEAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     19            MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
      *__________________
      * BRUTTO VEKT
      *""""""""""""""""""
     C                   MOVEL     'WT '         M6311
     C                   MOVEL     'G  '         M6313
     C                   MOVEL     'KGM'         M6411
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
      *
      *__________________
      * KUBIKK
      *""""""""""""""""""
     C     HEM3          IFGT      0
     C                   CLEAR                   MEAR
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     'VOL'         M6311
     C                   MOVEL     'DMQ'         M6411
     C     HEM3          MULT      1000          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
     C                   END
      *
      *__________________
      * KUBIKK
      *""""""""""""""""""
     C     HELM          IFGT      0
     C                   CLEAR                   MEAR
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     'LMT'         M6311
     C                   MOVEL     'M'           M6411
     C                   Z-ADD     HELM          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'                                        <-------------I
     C                   MOVE      ØA153H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA153X        ØA12                                         I
     C                   MOVE      ',   '        ØA12                                         I
     C                   MOVE      ØA153D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'                                        <-------------I
     C                   SUB       1             ØX                                           I
     C                   ENDIF                                                  <-------------I
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
