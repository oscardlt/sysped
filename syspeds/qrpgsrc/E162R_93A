********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
     H DATEDIT(*YMD)
     FEDIM      UF   E           K DISK
     FECNI93A   IF   E           K DISK
     FESTS93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FEPCI93A   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHEAD39    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD39R)
     FHEADF     UF   E           K DISK
     FDOKUFM    UF   E           K DISK
     FDOKUFM1   UF   E           K DISK
     F                                     RENAME(DOKUFMR:DOKUF1R)
     FTRACKF    O  A E           K DISK
     FTRACKT    UF   E           K DISK
     D                 DS
     D  D2380                  1     35
     D  XXDATA                 1      8
     D  XXTIDA                 9     12
     D                 DS
     D  FMMRK1                 1     35
     D  FMCLID                 1     18
     D                 DS
     D  XXURL                  1    140
     D  XXURL1                 1     70
     D  XXURL2                71    140
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XUL≈≈                 11     14  0
     D                 DS
     D  UL≈≈                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D*                                       1  70 TTTEXT
     D  TTTDS                  1     70
     D  TXDATE                 1      5
     D  XXDATO                 7     14
     D  TXTIME                17     19
     D  XXKLOK                21     24
     D  TXBY                  27     29
     D  XXSIGN                31     53
     D  TXSTKO                54     57
     D  XXSTKO                59     61
     D  ZLEV                  62     64
     D  ZLEV1                 62     62
     D  ZLEV2                 63     63
     D  TXAV                  66     67
     D  ZTOT                  68     70
     D  ZTOT1                 68     68
     D  ZTOT2                 69     69
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *
     C                   EXSR      STATUS
      *
     C     MMN           CHAIN     EDIM                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'F'           MST
     C                   UPDATE    EDIMR
     C                   END
      *
     C     FEIL          TAG
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
      *                                                     STATUS/
      *////////////////////////////////////////////////////////////
     C     STATUS        BEGSR
      *
      * LES ALLE CNI-REPETISJONER
     C     MMN           SETLL     ECNI93A
     C     MMN           READE     ECNI93A                                33
     C     *IN33         DOWEQ     '0'
      *
      * HENT/LOGG DATA FOR EN SENDING
     C                   EXSR      ENCNI
      *
     C     MMN           READE     ECNI93A                                33
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    ENCNI  /
      *////////////////////////////////////////////////////////////
     C     ENCNI         BEGSR
      * FINNES AKTUELL SENDING
     C                   MOVEL     C1004         SENDNR           35
     C                   MOVEL     SENDNR        SENDNR20         20
     C     SENDNR20      chain     Head39                             34
     C     *IN34         CABEQ     '1'           NXTCNI
      *
      * STATUS-EVENT (KODE, NORMALT 22 - SLUTT-LEVERT)
     C     KEY2          CHAIN     ESTS93A                            34
     C     *IN34         CABEQ     '1'           NXTCNI
     C                   MOVEL     S9011         XXSTKO            3
      * SIGNERT AV,,,
     C                   MOVE      *BLANKS       XXSIGN
     C                   MOVEL     'X'           XXSIGN
     C     KEY2          SETLL     ERFF93A
     C     KEY2          READE     ERFF93A                                35
     C     *IN35         DOWEQ     '0'
     C     R1153         IFEQ      'ZSN'
     C                   MOVEL     R1154         XXSIGN
     C                   END
     C     KEY2          READE     ERFF93A                                35
     C                   END
      * DATO/KLOKKA
     C                   MOVE      *ZEROS        XNDATO            8 0
     C                   MOVE      *ZEROS        XNKLOK            4 0
     C     KEY2          CHAIN     EDTM93A                            34
     C     *IN34         IFEQ      '0'
     C                   TESTN                   XXDATA               35          ≈≈MMDD
     C     *IN35         IFEQ      '1'
     C                   MOVE      XXDATA        XNDATO
     C                   END
     C                   TESTN                   XXTIDA               35
     C     *IN35         IFEQ      '1'
     C                   MOVE      XXTIDA        XNKLOK
     C                   END
     C                   END
      *
      * URL TIL GRAFISK LAGRET SIGNATUR..
     C                   MOVE      *BLANKS       XXURL           140
     C     KEY2          SETLL     EFTX93A
     C     KEY2          READE     EFTX93A                                35
     C     *IN35         DOWEQ     '0'
     C     F4451         IFEQ      'AAA'
     C     F4440A        CAT       F4440B:0      XXURL
     C                   END
     C     KEY2          READE     EFTX93A                                35
     C                   END
      *
      *
      * ER ENKELT-KOLLI OPPGITT
     C                   MOVE      'N'           DETKOL            1
     C     KEY2          SETLL     EPCI93A
     C     KEY2          READE     EPCI93A                                35
     C     *IN35         DOWEQ     '0'
     C     P4233         IFEQ      '18'
     C     P7102A        IFNE      *BLANKS
     C                   MOVEL     P7102A        KOLLID           20
     C                   EXSR      KOLLST
     C                   END
     C     P7102B        IFNE      *BLANKS
     C                   MOVEL     P7102B        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102C        IFNE      *BLANKS
     C                   MOVEL     P7102C        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102D        IFNE      *BLANKS
     C                   MOVEL     P7102D        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102E        IFNE      *BLANKS
     C                   MOVEL     P7102E        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102F        IFNE      *BLANKS
     C                   MOVEL     P7102F        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102G        IFNE      *BLANKS
     C                   MOVEL     P7102G        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102H        IFNE      *BLANKS
     C                   MOVEL     P7102H        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102I        IFNE      *BLANKS
     C                   MOVEL     P7102I        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102J        IFNE      *BLANKS
     C                   MOVEL     P7102J        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C                   END
     C     KEY2          READE     EPCI93A                                35
     C                   END
      *
      * HVIS DETALJKOLLI IKKE  OPPGITT - SETT X P≈ ALLE KLL
     C     DETKOL        IFEQ      'N'
     C                   EXSR      KOLLS2
     C                   END
      *
      * FINN ANTALL LEVERT/TOTAL OPPDATER STATUSER
     C                   EXSR      KOLL2
      *
     C     NXTCNI        TAG
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
      *                                                    KOLLST /
      *////////////////////////////////////////////////////////////
     C     KOLLST        BEGSR
     C                   MOVE      KOLLID        KOLL18           18
     C                   MOVEL     KOLL18        KOLL35           35
      * SETT X=LEVERT P≈ AKTUELT KOLLI
     C     KOLL35        SETLL     DOKUFM1
     C                   READ      DOKUFM1                                33
     C     *IN33         IFEQ      '0'
     C     FMCLID        ANDEQ     KOLL18
     C     FMAVD         ANDEQ     HEAVD
     C     FMOPD         ANDEQ     HEOPD
     C                   MOVE      'J'           DETKOL
     C                   MOVE      'X'           FMLEVE
     C                   UPDATE    DOKUF1R
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    KOLLS2 /
      *////////////////////////////////////////////////////////////
     C     KOLLS2        BEGSR
      * SETT X=LEVERT I ALLE KOLLI P≈ AKTUELT OPPDRAG.
     C     FMKEY         SETLL     DOKUFM
     C     FMKEY         READE     DOKUFM                                 35
     C     *IN35         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   MOVE      'X'           FMLEVE
     C                   END
     C                   UPDATE    DOKUFMR
     C     FMKEY         READE     DOKUFM                                 35
     C                   END
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    KOLL2  /
      *////////////////////////////////////////////////////////////
     C     KOLL2         BEGSR
     C                   MOVE      *ZEROS        LEV               3 0
     C                   MOVE      *ZEROS        TOT               3 0
      * FINN UT OM ALLE HAR X I FELT FOR LEVERT .
     C     FMKEY         SETLL     DOKUFM
     C     FMKEY         READE     DOKUFM                                 35
     C     *IN35         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   ADD       1             TOT
     C     FMLEVE        IFEQ      'X'
     C                   ADD       1             LEV
     C                   END
     C                   END
     C     FMKEY         READE     DOKUFM                                 35
     C                   END
      *
      *
      * SKRIV TIL TRACK & TRACE...
     C     FIURBL        IFEQ      'J'
     C                   EXSR      TTSR
     C                   END
      *
      *
      * SETT LEVERT P≈ OPPDRAGET (OM DET IKKE ER GJORT FÿR.)
      *
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'
      * IKKE MELDT FÿR....
     C     HESGM         IFEQ      *BLANKS
     C                   MOVEL     XXSIGN        HESGM
     C                   MOVEL     XNDATO        HEDTMO
     C                   MOVEL     XNKLOK        HEKLMO
     C                   ELSE
      * MELDT FÿR....(SUPPLERING ETTER TIDLIGERE MANKO.)
      * HER LEGGES INN HANDLING VED ETTERRAPPORT AV TIDL. MELDT MANKO
      * ...
      * ...
     C                   END
      * MANKO (FREMDELES...)
     C     LEV           IFNE      TOT
     C* HER LEGGS INN "MANKOHANDLING"....
      * ...
      * ...
     C                   END
      *
     C                   UPDATE    HEADFR
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
      * LEVERT 0 AV 0 - BETYR AT DET IKKE VAR NOEN KOLLI P≈
      * OPPDRAGET(SOM MATCHET),HENT ANTALL FRA OPPDRAGET, SETT ALT LEV.
     C     LEV           IFEQ      *ZEROS
     C     TOT           ANDEQ     *ZEROS
     C                   Z-ADD     HENT          LEV
     C                   Z-ADD     HENT          TOT
     C                   END
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XUL≈≈         UL≈≈
     C                   MOVE      'POD'         TTACTI
     C                   MOVE      XNDATO        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     XNKLOK        TTTIME
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVEL     'SY400USR'    TTUSER
     C                   MOVE      'Date:'       TXDATE
     C                   MOVE      XNDATO        XXDATO
     C                   MOVE      'Ti:'         TXTIME
     C                   MOVE      XNKLOK        XXKLOK
     C                   MOVE      'Sts:'        TXSTKO
     C                   MOVE      'By:'         TXBY
     C                   MOVE      LEV           ZLEV
     C     ZLEV1         IFEQ      '0'
     C                   MOVE      ' '           ZLEV1
     C     ZLEV2         IFEQ      '0'
     C                   MOVE      ' '           ZLEV2
     C                   END
     C                   END
s01  C*                  MOVE      'Av'          TXAV
n01  C                   MOVE      'Of'          TXAV
     C                   MOVE      TOT           ZTOT
     C     ZTOT1         IFEQ      '0'
     C                   MOVE      ' '           ZTOT1
     C     ZTOT2         IFEQ      '0'
     C                   MOVE      ' '           ZTOT2
     C                   END
     C                   END
     C                   MOVE      TTTDS         TTTEXT
n01  C                   MOVE      'Dato:'       TXDATE
n01  C                   MOVE      'Av:'         TXBY
n01  C                   MOVE      'Av'          TXAV
n01  C                   MOVE      TTTDS         TTTEXL
     C                   MOVEL     XXSIGN        TTNAME
     C                   MOVEL     XXSTKO        TTEDEV
     C                   WRITE     TRACKFR
      * URL....
     C     XXURL1        IFNE      *BLANKS
     C                   MOVE      'POU'         TTACTI
     C                   MOVE      XXURL1        TTTEXT
     C                   WRITE     TRACKFR
     C                   END
     C     XXURL2        IFNE      *BLANKS
     C                   MOVE      'POU'         TTACTI
     C                   MOVE      XXURL2        TTTEXT
     C                   WRITE     TRACKFR
     C                   END
      * OPPDATER ATA (MEN KUN VED FÿRSTE LEVERING. )
     C     HEAKEY        CHAIN     TRACKT                             33
     C     *IN33         IFEQ      *OFF
     C     XXSTKO        IFEQ      '22 '
     C     TXATAD        ANDEQ     *ZEROS
     C                   MOVE      XNDATO        TXATAD
     C                   MOVE      XNKLOK        TXATAK
     C                   END
     C                   UPDATE    TRACKTR
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          MMN
     C     KEY2          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN
     C                   KFLD                    CREP0
     C     *LIKE         DEFINE    CGN           YGN
     C                   Z-ADD     2             YGN
     C     FMKEY         KLIST
     C                   KFLD                    XXRI              1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'F'           XXRI
     C     HEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
      *
     C                   ENDSR
