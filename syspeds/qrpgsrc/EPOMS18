      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS18 MODULE(*LIBL/EPOMS18
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 SJEKK OM OVERSTYRT LEV.ADRESSE (KUN FOR LT)
********************************************************************
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FEPOK02    IF   E           K DISK    USROPN
     FEPOH      IF   E           K DISK    USROPN
N01  FEPOHDP    IF   E           K DISK    USROPN
     FEPOL      IF   E           K DISK    USROPN
     FVADR3     IF   E           K DISK    USROPN
     FKALENDER  IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D WSPRT           S              1
     D WSCLT           S              1
     D BGCOL           S             20
     D KNK             S             36    DIM(100)
N01  D LA1             S              1    DIM(100)
N01  D LA2             S             30    DIM(100)
N01  D LA3             S             30    DIM(100)
     D NT              S              7  0 DIM(100)
     D VKB             S             11  3 DIM(100)
     D VKN             S             11  3 DIM(100)
     D M3              S              9  3 DIM(100)
     D D07             S              1    DIM(100)
     D D14             S              1    DIM(100)
     D VisAChecked     s             10
     D VisAlle         s              1
     D Title           S            100
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')                            Programmets html
      *
      * SUMMER KOLLI I LAGER
     c     BSKN          SETLL     EPOK02
     c     BSKN          READE     EPOK02                                 33
     C                   DOW       *IN33 = *OFF
     C                   SELECT
     C                   WHEN      WSPRT = 'J'
     C                   WHEN      WSCLT = 'J'
     C     EKDTHK        CABEQ     0             LESNESTE
     C                   OTHER
     C     EKDTIN        CABEQ     0             LESNESTE
     C                   ENDSL
     C     1             DO        100           XN                3 0
     C     EPOHK         CHAIN     EPOH                               33
     C  N33EPOLK         CHAIN     EPOL                               33
     C  N33              SELECT
     C                   WHEN      KNK(XN) = *BLANKS
     C                   EVAL      KNK(XN) = EHINV
N01  C                   IF        BIFIRM = 'LT'
N01  C                   EVAL      VAKURE  = EHINV
N01  C     VADR3K        CHAIN     VADR3                              33
N01  C                   EVAL      LA1(XN) = ' '
N01  C     LO:UPC        XLATE     VADRNA        LA2(XN)
N01  C     LO:UPC        XLATE     VADRN1        LA3(XN)
N01  C                   END
     C                   EVAL      NT(XN)  = 1
     C                   IF        ELNTKL = 0
     C                   EVAL(H)   VKB(XN) = 0
     C                   EVAL(H)   VKN(XN) = 0
     C                   EVAL(H)   M3(XN)  = 0
     C                   ELSE
     C                   EVAL(H)   VKB(XN) = ELVKTL / ELNTKL
     C                   EVAL(H)   VKN(XN) = ELVKNL / ELNTKL
     C                   EVAL(H)   M3(XN)  = ELM3L / ELNTKL
     C                   END
     C                   LEAVE
     C                   WHEN      KNK(XN) = EHINV
N01  C                   IF        BIFIRM = 'LT'
N01  C     EPOHK         CHAIN     EPOHDP                             33
N01  C                   IF        *IN33 = *OFF
N01  C     LO:UPC        XLATE     EHDPNA        EHDPNA
N01  C     LO:UPC        XLATE     EHDPA1        EHDPA1
N01  C                   IF        EHDPNA <> LA2(XN) OR EHDPA1 <> LA3(XN)
N01  C                   EVAL      LA1(XN) = '*'
N01  C                   END
N01  C                   END
N01  C                   END
     C                   EVAL      NT(XN)  = NT(XN) + 1
     C                   IF        ELNTKL <> 0
     C                   EVAL(H)   VKB(XN) = VKB(XN) + ELVKTL / ELNTKL
     C                   EVAL(H)   VKN(XN) = VKN(XN) + ELVKNL / ELNTKL
     C                   EVAL(H)   M3(XN)  = M3(XN) + ELM3L / ELNTKL
     C                   END
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
      *
     C                   SELECT
     C*                  WHEN      EHDATO < XDT_14
     C                   WHEN      EKDTIN < XDT_14
     C                   EVAL      D14(XN) = 'J'
     C                   EVAL      D07(XN) = ' '
     C*                  WHEN      EHDATO < XDT_07 AND D14(XN) = *BLANKS
     C                   WHEN      EKDTIN < XDT_07 AND D14(XN) = *BLANKS
     C                   EVAL      D07(XN) = 'J'
     C                   ENDSL
      *
     C     LESNESTE      TAG
     C     BSKN          READE     EPOK02                                 33
     C                   ENDDO
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete saker
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      *
      *
     C     1             DO        100           XN
     C                   IF        KNK(XN) = *BLANKS
     C                   LEAVE
     C                   END
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      *
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
      *
     C                   ENDDO
      *
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')                            programmets Html
     C                   callp     wrtsection('botStd')                         Body/htmlavsl
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSPRT   = zhbgetvar('WSPRT')
     C                   eval      WSCLT   = zhbgetvar('WSCLT')
      * Checkboks for Vis alle
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellers kommer verdi fra siste valg via "VisAlle" som checked / blank(N)
     C     VisAchecked   Ifeq      'checked'
     C                   eval      VisAlle  = 'Y'
     C                   else
     C                   eval      VisAlle  = zhbgetvar('VisAlle')
     C                   end
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     EPOK02
     C                   CLOSE     EPOH
N01  C                   IF        BIFIRM = 'LT'
N01  C                   CLOSE     EPOHDP
N01  C                   END
     C                   CLOSE     EPOL
     C                   CLOSE     VADR3
     C                   CLOSE     KALENDER
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS18.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     c                   eval      Title='POMS Purchase Order Management System'
     c                             + ' (HubList)'
     C                   callp     updhtmlvar('TITLE':Title)
     c
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('WSPRT':WSPRT)
     C                   callp     updHTMLvar('WSCLT':WSCLT)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':BIBESK)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   callp     updHTMLvar('WSKNK':KNK(XN))
     C                   EVAL      VAKURE = KNK(XN)
     C     VADR3K        CHAIN     VADR3                              33
     c   33              EVAL      VADRNA = *BLANKS
     C                   callp     updHTMLvar('VADRNA':VADRNA)
     C                   callp     updHTMLvar('VADRN3':VADRN3)
N01  C                   callp     updHTMLvar('ULIKLA':LA1(XN))
     C                   callp     updHtmlVar('WSNT':
     C                             %trim(%editc(NT(XN):'Z')))
     C                   callp     updHtmlVar('WSVKB':
     C                             %trim(%editc(VKB(XN):'4')))
     C                   callp     updHtmlVar('WSVKN':
     C                             %trim(%editc(VKN(XN):'4')))
     C                   callp     updHtmlVar('WSM3':
     C                             %trim(%editc(M3(XN):'4')))
     C                   SELECT
     C                   WHEN      D14(XN) = 'J'
     C                   callp     updHTMLvar('BGCOL':'bgcolor=red')
     C                   WHEN      D07(XN) = 'J'
     C                   callp     updHTMLvar('BGCOL':'bgcolor=yellow')
     C                   OTHER
     C                   callp     updHTMLvar('BGCOL':'')
     C                   ENDSL
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      EPOK02
     C                   OPEN      EPOH
N01  C                   IF        BIFIRM = 'LT'
N01  C                   OPEN      EPOHDP
N01  C                   END
     C                   OPEN      EPOL
     C                   OPEN      VADR3
     C                   OPEN      KALENDER
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C     EPOHK         klist
     C                   kfld                    EKKUND
     C                   kfld                    EKBES
     C     EPOLK         klist
     C                   kfld                    EKKUND
     C                   kfld                    EKBES
     C                   kfld                    EKLIN
     C     VADR3K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    EKKUND
     C                   kfld                    VAKURE
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           KADATE
     C     KADATE        SETLL     KALENDER
     C     1             DO        7
     C                   READP     KALENDER                               33
     C                   END
     C                   MOVE      KADATE        XDT_07            8 0
     C     1             DO        7
     C                   READP     KALENDER                               33
     C                   END
     C                   MOVE      KADATE        XDT_14            8 0
      *
     C                   endsr
      *
