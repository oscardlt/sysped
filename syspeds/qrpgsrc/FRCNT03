      *********************************************************************
      *  RPG ILE MODULE *LIBL/FRCNT03
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/FRCNT03 MODULE(*LIBL/FRCNT03)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FHEAD11    if   e           K DISK    USROPN
     Fhested    IF   e           K DISK    USROPN
     Ffreetxt2  IF   e           K DISK    USROPN
      *=====================================================================
      * Includes to be used in all CGIs
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      *  Fields used for parsing the input string
     D action          s             10a
     D subaction       s             10a
     D lng             s              2a
     D InAvd           s              4a
     D InAvdN          s              4  0
     D InOpd           s              7a
     D InOpdN          s              7  0
     D Tur             s              8a
     D TurNR           s              8  0
     D xorh01nbr       s             15a
     D xboo01tit       s             70a
     D xboo01aut       s            700a
      *
     D htmlsrclib      s             10a
     D htmlsrcfil      s             10a
     D htmlsrcmbr      s             10a
      * "InitSW"- is blank only the first time pgm is called
     DInitSW           S              1a
      *
     DRowNbr           S              3s 0
     DRtvSW            S              1a
      *=====================================================================
     C                   exsr      SetTimeStr
      *=====================================================================
      * Read remote browser input string and parse it
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prolog3
     C                   exsr      Parse
      *=====================================================================
      * Main line
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
     C                   exsr      LoadHtml
      * Open database files
     C                   exsr      OpenDbf
      * Start response html
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   eval      wrotetop = *on                               For pssr
      *------------------
      * Check the action requested
     C                   if        Action = ' '
     C                   eval      action = 'display'
     C                   endif
     C                   eval      action = uppify(action)
      * =================
      * "Action" analysis
      * Display the contents of the shopping cart
     C                   if        Action ='DISPLAY'
     C                   exsr      Display
     C                   exsr      Exit
     C                   endif
      * Put back a book to its bookshelf
     C                   if        Action ='GIVEBACK'
     C                   exsr      GiveBack
     C                   exsr      Display
     C                   exsr      Exit
     C                   endif
      * =================
      * Flush the html buffer and exit
     C                   exsr      Exit
      *=====================================================================
      * Display the contents of the shopping cart
      *=====================================================================
     C     Display       begsr
     C                   callp     wrtsection('tabstr')
      *----------------
     C     TurNr         setll     Head11
      *
     C     TurNr         reade     Head11
     C                   dow       not%eof
     C                   exsr      SetRow
     C                   callp     wrtsection('row')
     C     TurNr         reade     Head11
     C                   enddo
      *
     C                   callp     wrtsection('tabend')
      *
     C                   endsr
      *=====================================================================
      * Put a book back to its bookshelf
      *=====================================================================
     C     Giveback      begsr
     c                   call      'SYGE21B'
     c                   parm                    Turnr
     c                   parm                    InavdN
     c                   parm                    InopdN
     c                   parm                    In30              1
      *
     C                   endsr
      *=====================================================================
      * Set variables in html section "top"
      *=====================================================================
     C     SetTop        begsr
      * Order number (character)
     C                   callp     updHTMLvar('XORH01NBR':xOrh01Nbr)
      *
     C                   endsr
      *=====================================================================
      * Set variables in html section "row"
      *=====================================================================
     C     SetRow        begsr
      *
     C     HeadfKey      CHAIN     HEsted                             33
      * Linjer fra notisblokk merket G, samles i EN lang streng
     c                   Move      *blanks       Merknad         500
     C     Free2Key      SETLL     FREETXT2
     C     Free2Key      READE     FREETXT2                               33
     C     *in33         doweq     *OFF
     c     Merknad       ifeq      *blanks
     C                   eval      Merknad = 'Merkn: ' + FRTTXT
     C                   else
     C                   eval      Merknad=%trim(Merknad)+'<br>'+%trim(FRTTXT)
     C                   END
     C     Free2Key      READE     FREETXT2                               33
     C                   END
     C                   callp     updHTMLvar('XORH01NBR':xOrh01Nbr)
      * Book title
     C                   move      Heavd         diavda            4
     C                   move      heopd         diopda            7
     C                   movel     HELKA         LKF               2
     C                   movel     HETRI         LKT               2
     C                   eval      XBOO01TIT = DIAVDA+'/'+DIOPDA +
     C                             '  Fra: '+LKF+' '+ HESDF +' '+%trim(HSFROM)+
     C                             '  Til: '+LKT+' '+ HESDT  + ' ' +%trim(HSTO)
     C                   callp     updHTMLvar('XBOO01TIT':XBOO01TIT)
     C                   eval      XBOO01AUT = 'Avs: '+%trim(Henas)+' '+
     C                                         %trim(Heads1)+' '+
     C                                         %trim(Heads3)+'<br>'+
     C                                         'Mot: '+%trim(Henak)+' '+
     C                                         %trim(Headk1)+' '+
     C                                         %trim(Headk3)
     C     Merknad       ifne      *blanks
     C                   eval      XBOO01AUT = %trim(XBOO01AUT)+'<br>'+
     C                                         %trim(Merknad)
     c                   end
     C                   callp     updHTMLvar('vekt':
     C                             %trim(%editc(hevkt:'Z')))
     C                   callp     updHTMLvar('XBOO01AUT':XBOO01AUT)
     C                   callp     updHTMLvar('InAVD':DiAVDA)
     C                   callp     updHTMLvar('InOPD':DiOPDA)
      *
     C                   endsr
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
      *=====================================================================
     C     LoadHtml      begsr
      * Read externally defined output html
     C                   eval      HtmlSrcLib = '*LIBL'
     C                   eval      HtmlSrcFil = 'HTMLSRC' + lng
     C                   eval      HtmlSrcMbr = 'FRCNT03'
     C                   callp     gethtml(HtmlSrcFil:HtmlSrcLib:HtmlSrcMbr:
     C                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================
      * Open database files
      *=====================================================================
     C     OpenDbf       begsr
     C     HeadfKey      klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     Free2Key      klist
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXSKKO            1
     C                   MOVE      'G'           XXSKKO
     C                   open      head11
     C                   open      hested
     C                   open      freetxt2
     C                   endsr
      *=====================================================================
      * Close database files
      *=====================================================================
     C     CloseDbf      begsr
     C                   close     head11
     C                   close     hested
     C                   close     freetxt2
     C                   endsr
      *=====================================================================
      *  Time started
      *=====================================================================
     C     SetTimeStr    begsr
     C                   time                    TimeStart
     C                   endsr
      *=====================================================================
      *  Time ended and time elapsed elapsed
      *=====================================================================
     C     SetTimeEnd    begsr
     C                   time                    TimeEnd
     C     TimeEnd       subdur    TimeStart     MsElaps:*ms
     C                   eval      SecElaps = MsElaps / 1000000
     C                   endsr
      *=====================================================================
      * Send response html and quit
      *=====================================================================
     C     Exit          begsr
     C                   exsr      CloseDbf
      *
     C                   exsr      SetTimeEnd
     C                   callp     updhtmlvar('RUNTIME':
     C                             %trim(%editw(SecElaps:'     0 .   ')))
     C                   callp     wrtsection('runtime')
     C                   callp     wrtsection('end')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   return
     C                   endsr
      *=====================================================================
      * Parse the input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      action     = zhbgetvar('action    ')
     C                   eval      subaction  = zhbgetvar('subaction ')
     C                   eval      lng        = zhbgetvar('lng       ')
     C                   eval      tur        = zhbgetvar('tur       ')
     C                   eval      Turnr   = c2n2(tur)
     C                   eval      InAvd      = zhbgetvar('InAVD')
     C                   eval      InAvdN  = c2n2(InAVD)
     C                   eval      InOpd      = zhbgetvar('InOPD')
     C                   eval      InOpdN  = c2n2(InOPD)
     C                   endsr
