      *
      ***********************************************************
      *** PROGRAM SKAL SPØRRE PÅ XML-FAKTURA LOGG             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32 34-40 50-76 78-89  95   ***
      ***********************************************************
      *
     FXMLUFOPD  UF   E           K DISK
     FXMLUFL1   IF   E           K DISK    RENAME(XMLUFFR:XMLUFFR1)
     FXMLUFL2   IF   E           K DISK    RENAME(XMLUFFR:XMLUFFR2)
     FXMLUFL3   IF   E           K DISK    RENAME(XMLUFFR:XMLUFFR3)
     F
     FEDIM5     IF A E           K DISK
     FEDIM5S    IF   E           K DISK    RENAME(EDIMR:EDIMR5S)
     F                                     PREFIX(M5S_)
     FEDIS      IF A E           K DISK
     FEDIC      UF   E             DISK
     FSYXML12D  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  XSDTÅ                  1      4  0
     D  XSDTM                  5      6  0
     D  XSDTD                  7      8  0
     D  XSDT                   1      8  0
     D                 DS
     D  W2DTD                  1      2  0
     D  W2DTM                  3      4  0
     D  W2DTÅ                  5      6  0
     D  W2DT                   1      6  0
     D                 DS
     D  X2DTÅ                  1      4  0
     D  X2DTM                  5      6  0
     D  X2DTD                  7      8  0
     D  X2DT                   1      8  0
     D                 DS
     D  WXDTSD                 1      2  0
     D  WXDTSM                 3      4  0
     D  WXDTSÅ                 5      6  0
     D  WXDTS                  1      6  0
     D                 DS
     D  XFDTSÅ                 1      4  0
     D  XFDTSM                 5      6  0
     D  XFDTSD                 7      8  0
     D  XFDTS                  1      8  0
     D                 DS
     D  WXDTMD                 1      2  0
     D  WXDTMM                 3      4  0
     D  WXDTMÅ                 5      6  0
     D  WXDTM                  1      6  0
     D                 DS
     D  MDTÅ                   1      4  0
     D  MDTM                   5      6  0
     D  MDTD                   7      8  0
     D  MDT                    1      8  0
     D                SDS
     D  UUSER                254    263
      *
     C                   EXSR      INIT
      *
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '0000'        *IN(96)
     C                   SETOFF                                       30
      *
     C   03              GOTO      SLUTT
      *
     C                   EXSR      TESTB1
     C   30              GOTO      START0
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   EXSR      HENTINFO
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   EXSR      LESREC
     C  N94              END
     C                   END
      *
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C  N27              DO
     C                   MOVE      '1'           *IN27
     C                   GOTO      START0
     C                   END
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C   08              GOTO      START0
     C   12              GOTO      START0
      *
      *** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2ST          IFNE      ' '                                          1<-B
     C     W2DT          ORNE      0
     C     W2FN          ORNE      0
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     NYSØK         BEGSR
      ***********************************************
     C                   CLEAR                   BILDE01
     C                   MOVE      W2DT          WSDT
     C                   MOVE      W2FN          WSFN
     C                   MOVE      W2ST          WSST
      *
     C                   MOVE      *ZEROS        W2DT
     C                   MOVE      *ZEROS        W2FN
     C                   MOVE      *BLANKS       W2ST
      *
     C                   EXSR      TESTB1
      *
     C                   ENDSR
      *
      ***********************************************
     C     HENTINFO      BEGSR
      ***********************************************
     C                   SELECT
     C                   WHEN      XFST = 'M'
     C                   EVAL      WXST = 'OK'
     C                   WHEN      XFST = 'N'
     C                   EVAL      WXST = 'IKKE OK'
     C                   OTHER
     C                   EVAL      WXST = *BLANKS
     C                   ENDSL
      *
     C                   MOVE      XFDTSD        WXDTSD
     C                   MOVE      XFDTSM        WXDTSM
     C                   MOVE      XFDTSÅ        WXDTSÅ
     C                   MOVEL     XFTDS         WXTDS
     C                   MOVE      *BLANKS       M0068
     C                   MOVEL     XFFN          M0068
     C                   MOVE      *ZEROS        WXDTM
     C                   MOVE      *ZEROS        WXTMM
     C                   MOVE      *BLANKS       XREFS
     C                   MOVE      *BLANKS       XREFM
      *
     C     XFDTS         CABEQ     0             SLHINFO
      *
      * SENDINGSREF
     C     M0068         SETLL     EDIM5
     C     M0068         READE     EDIM5                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        MSR = 'S'
     C     MSN           CHAIN     EDIS                               33
     C                   EVAL      XREFS = S0020
     C                   END
     C     M0068         READE     EDIM5                                  33
     C                   ENDDO
      *
      * MOTTAKSREF
     C     M0068         SETLL     EDIM5
     C     M0068         READE     EDIM5                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        MSR = 'R'
     C                   MOVE      MDTD          WXDTMD
     C                   MOVE      MDTM          WXDTMM
     C                   MOVE      MDTÅ          WXDTMÅ
     C                   MOVEL     MTM           WXTMM
     C     MSN           CHAIN     EDIS                               33
     C                   EVAL      XREFM = S0020
     C                   END
     C     M0068         READE     EDIM5                                  33
     C                   ENDDO
      *
     C     SLHINFO       ENDSR
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C                   SELECT
     C                   WHEN      WSST <> *BLANKS
     C                   MOVEA     '100'         *IN(41)
     C                   WHEN      WSDT <> 0
     C                   MOVEA     '010'         *IN(41)
     C                   OTHER
     C                   MOVEA     '001'         *IN(41)
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C                   SELECT
     C     WSNR          WHENEQ    '1'
     C     XFST          ANDEQ     'C'
     C                   EXSR      OPPDAT
     C                   ENDSL
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPPDAT        BEGSR
      ***********************************************
     C     XFFN          CHAIN     XMLUFOPD                           33
     C                   IF        *IN33 = *OFF
     C                   MOVE      *BLANKS       M5S_M0068
     C                   MOVEL     XFFN          M5S_M0068
     C     M5S_M0068     SETGT     EDIM5S
     C     M5S_M0068     READPE    EDIM5S                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        M5S_MSR = 'S'
     C                   LEAVE
     C                   END
     C     M5S_M0068     READPE    EDIM5S                                 33
     C                   ENDDO
     C     *IN33         CABEQ     *ON           SLOPPD
     C     M5S_MSN       CHAIN     EDIS                               33
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      M5S_M0010     S0004
     C                   MOVE      M5S_M0004     S0010
     C                   Z-ADD     CSN           SSN
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   MOVE      'R'           SSR
     C                   MOVE      'C'           SST
     C                   EVAL      S0026 = 'XML-' + %TRIM(UUSER)
     C                   EVAL      S0020 = 'MANUELT MOTTATT'
     C                   WRITE     EDISR
      *
     C                   CLEAR                   EDIMR
     C                   Z-ADD     CMN           MMN
     C                   Z-ADD     CSN           MSN
     C                   MOVEL     MMN           M0062
     C                   MOVE      M5S_M0010     M0004
     C                   MOVE      M5S_M0004     M0010
     C                   MOVE      M5S_M0068     M0068
     C                   MOVE      M5S_M1001     M1001
     C                   MOVE      M5S_M1004     M1004
     C                   MOVE      M5S_M1225     M1225
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      WDT           M0068D
     C                   MOVE      WTM           M0068E
     C                   MOVE      'R'           MSR
     C                   MOVE      'C'           MST
     C                   WRITE     EDIMR
      *
     C                   MOVE      'M'           XFST
     C     SLOPPD        TAG
     C                   UPDATE    XMLUFFR
      *
     C                   EXSR      HENTINFO
     C                   END
      *
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SETKEY        BEGSR
      ***********************************************
     C   41              IF        WSST = 'X'
     C                   MOVE      ' '           XST               1
     C                   ELSE
     C                   EVAL      XST = WSST
     C                   END
     C   41XST           SETGT     XMLUFL1
     C   42              DO
     C                   MOVE      WSDTD         XSDTD
     C                   MOVE      WSDTM         XSDTM
     C                   Z-ADD     WSDTÅ         XSDTÅ
     C                   ADD       2000          XSDTÅ
     C     XSDT          SETLL     XMLUFL3
     C                   END
     C   43WSFN          SETLL     XMLUFL2
      *
     C                   ENDSR
      *
      ***********************************************
     C     LESREC        BEGSR
      ***********************************************
     C   41XST           READPE    XMLUFL1                                94
     C   42              READ      XMLUFL3                                94
     C   43              READ      XMLUFL2                                94
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        XSDT
     C                   MOVE      *ZEROS        W2DT
     C                   MOVE      *ZEROS        X2DT
     C                   MOVE      *ZEROS        WXDTS
     C                   MOVE      *ZEROS        WXDTM
     C                   MOVE      *ZEROS        XFDTS
     C                   MOVE      'C'           WSST
      *
     C                   ENDSR
      *
