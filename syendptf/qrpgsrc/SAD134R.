      *
      ***********************************************************
      *** PROGRAM SKAL REGISTRERE E.F. MANIFEST (OPPDRAG)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-48 50-81          ***
      ***********************************************************
      *
     FSADH      IF   E           K DISK
     FSADEFCL1  UF   E           K DISK
     FSADEFCL2  UF   E           K DISK    RENAME(SADEFCFR:SADEFCFR2)
     FSADEFL02  IF   E           K DISK
     FSVEH      IF   E           K DISK
     FSVZH      IF   E           K DISK
     FTRACKF    O  A E             DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FSAD134D   CF   E             WORKSTN INFDS(FBAREA)
     D                SDS
     D  UUSR                 254    263
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *...............................................
      * BILDE-1 INNLESING AV HOVED OPPLYSNINGER AV TUR
      *'''''''''''''''''''''''''''''''''''''''''''''''
     C     START         TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '00000'       *IN(95)
     C                   SETOFF                                       30
      *
      *  04 = SØK AVSENDER/MOTTAKER
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
      *  12 = AVBRYT, FRIGJØR E-MERKING OG GÅ UT
     C   12              GOTO      SLUTT
      *
      * ----------- T E S T   B I L D E 0 1 ----------
     C                   EXSR      TESTB1
     C   30              GOTO      START
      *
     C                   EXSR      OPDOPD
      *
     C     SLUTT         TAG
     C                   SELECT
     C                   WHEN      *IN12
     C                   EVAL      UFLAGG = 1
     C                   OTHER
     C                   EVAL      UFLAGG = 0
     C                   ENDSL
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SØKDTA        BEGSR
      ***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      * SØK
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     15
     C     POSNBR        ANDLE     19
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSDF
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C     WSSDF         CHAIN     STED2                              33
     C  N33              EVAL      WSSDFT = ST2NVN
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     15
     C     POSNBR        ANDLE     19
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSDT
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C     WSSDT         CHAIN     STED2                              33
     C  N33              EVAL      WSSDTT = ST2NVN
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     15
     C     POSNBR        ANDLE     16
     C                   EVAL      KFTYP  = 'SADEFPR'
     C                   EVAL      UUPDAT = 'N'
     C                   EVAL      KFKOD  = WSPR
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    KFKOD
     C                   EVAL      WSPR = KFKOD
     C     KOFASTK       CHAIN     KOFAST                             97
     C  N97              EVAL      WSPRT = KFTXT
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     15
     C     POSNBR        ANDLE     16
     C                   EVAL      KFTYP  = 'SADEFETYPE'
     C                   EVAL      UUPDAT = 'N'
     C                   EVAL      KFKOD  = WSETYP
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    KFKOD
     C                   EVAL      WSETYP = KFKOD
     C     KOFASTK       CHAIN     KOFAST                             96
     C  N96              EVAL      WSETYPT = KFTXT
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
      *
     C     WSSDF         CHAIN     STED2                              99
     C  N99              EVAL      WSSDFT = ST2NVN
     C     WSSDT         CHAIN     STED2                              98
     C  N98              EVAL      WSSDTT = ST2NVN
      *
     C                   EVAL      KFTYP  = 'SADEFPR'
     C                   EVAL      KFKOD  = WSPR
     C     KOFASTK       CHAIN     KOFAST                             97
     C  N97              EVAL      WSPRT = KFTXT
      *
     C                   EVAL      KFTYP  = 'SADEFETYPE'
     C                   EVAL      KFKOD  = WSETYP
     C     KOFASTK       CHAIN     KOFAST                             96
     C  N96              EVAL      WSETYPT = KFTXT
      *
     C                   IF        EFEID = *BLANKS
     C     WSEID         COMP      *BLANKS                                95
     C                   END
      *
     C                   EXSR      HMSG
      *
     C                   ENDSR
      *
      ***********************************************
     C     HMSG          BEGSR
      ***********************************************
     C   99              DO
     C                   MOVE      'SPS0012'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   END
     C   98
     CANN30              DO
     C                   MOVE      'SPS0013'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   END
     C   97
     CANN30              DO
     C                   MOVE      'YBC2100'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   END
     C   96
     CANN30              DO
     C                   MOVE      'YBC2094'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   END
     C   95
     CANN30              DO
     C                   MOVE      'YBC2096'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVEFW        BEGSR
      ***********************************************
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      UTDN          WSTDN
      *
     C                   MOVE      CLSDF         WSSDF
     C     WSSDF         CHAIN     STED2                              33
     C  N33              EVAL      WSSDFT = ST2NVN
     C   33              EVAL      WSSDFT = *BLANKS
     C                   MOVE      CLSDT         WSSDT
     C     WSSDT         CHAIN     STED2                              33
     C  N33              EVAL      WSSDTT = ST2NVN
     C   33              EVAL      WSSDTT = *BLANKS
      *
     C                   MOVE      CLPR          WSPR
     C                   MOVE      CLPRT         WSPRT
     C                   MOVE      CLETYP        WSETYP
     C                   MOVE      CLETYPT       WSETYPT
     C                   MOVE      CLEID         WSEID
     C                   MOVE      CLESER        WSESER
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWEF        BEGSR
      ***********************************************
     C                   MOVE      WSSDF         CLSDF
     C                   MOVE      WSSDT         CLSDT
     C                   MOVE      WSPR          CLPR
     C                   MOVE      WSPRT         CLPRT
     C                   MOVE      WSETYP        CLETYP
     C                   MOVE      WSETYPT       CLETYPT
     C                   MOVE      WSEID         CLEID
     C                   MOVE      WSESER        CLESER
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVSAW        BEGSR
      ***********************************************
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      UTDN          WSTDN
      *
     C                   MOVE      *BLANKS       WSSDF
     C                   MOVE      *BLANKS       WSSDT
     C                   MOVE      *BLANKS       WSPR
     C                   MOVE      *BLANKS       WSPRT
     C                   MOVE      *BLANKS       WSETYP
     C                   MOVE      *BLANKS       WSETYPT
     C                   MOVE      *BLANKS       WSEID
     C                   MOVE      *BLANKS       WSESER
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDOPD        BEGSR
      ***********************************************
      *
     C     SADEFCL1K     CHAIN     SADEFCL1                           33
     C                   IF        *IN33
     C     SADEFCL2K     CHAIN     SADEFCL2                           33
     C                   EVAL      *IN33 = *ON
     C                   END
      *
     C                   EXSR      TTSR
      *
      * FLYTTER SKJERMVARIABLE TIL FILVARIABLE
      *
     C                   EXSR      MOVWEF
      *
     C                   IF        *IN33
     C                   EVAL      CLPRO  = UPRO
     C                   EVAL      CLST   = 'O'
     C                   UPDATE    SADEFCFR2
     C                   ELSE
     C                   UPDATE    SADEFCFR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADEFL02K     KLIST
     C                   KFLD                    UPRO
     C     SADEFCL1K     KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SADEFCL2K     KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SADHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     KOFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8 0
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C     UTYPE         COMP      'DSP'                                  49
      *
     C     SADEFL02K     CHAIN     SADEFL02                           33
     C     SADEFCL1K     CHAIN(N)  SADEFCL1                           33
     C   33SADEFCL2K     CHAIN(N)  SADEFCL2                           33
     C     SADHK         CHAIN     SADH                               33
     C  N33              IF        CLEID = *BLANKS OR CLETYP = *BLANKS
     C     SADHK         CHAIN     SVEH                               33
     C                   IF        *IN33 = *OFF
     C                   IF        CLETYP = *BLANKS
     C                   SELECT
     C                   WHEN      SVEH_MTYP = 'UGE'
     C                   EVAL      CLETYP = '05'
     C                   ENDSL
     C                   END
     C     SVEH_TUID     CHAIN     SVZH                               33
     C  N33              EVAL      CLEID = SVZH_MRNN
     C                   END
     C                   END
      *
     C*                  IF        UTYPE = 'DSP' OR UTYPE = 'CHG'
     C                   EXSR      MOVEFW
     C*                  ELSE
     C*                  EXSR      MOVSAW
     C*                  END
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
      ***********************************************
     C     TTSR          BEGSR
      ***********************************************
     C                   EVAL      TTAVD  = UAVD
     C                   EVAL      TTOPD  = UTDN
     C                   EVAL      TTTUR  = UPRO
     C                   EVAL      TTACTI = 'EFC'
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      UUSR          TTUSER
     C                   IF        *IN33
     C                   EVAL      TTTEXT = 'Oppdrag er plukket til E.F. '
     C                             + 'manifest.'
     C                   EVAL      TTTEXL = 'Oppdrag er plukket til E.F. '
     C                             + 'manifest.'
     C                   EVAL      TTEDEV = 'CRT'
     C                   ELSE
     C                   EVAL      TTTEXT = 'Info. av oppdrag i E.F. manifest '
     C                             + 'er endret.'
     C                   EVAL      TTTEXL = 'Info. av oppdrag i E.F. manifest '
     C                             + 'er endret.'
     C                   EVAL      TTEDEV = 'CHG'
     C                   END
      *
     C                   WRITE     TRACKFR
     C                   ENDSR
      *
