      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 også T&T-suffix kan ha 2 linjer (Append hvis A siste)
10274 * 02 JOV PTF10 Dersom IFB 401+17 lang f.bnr så lagre kun de 17
10274 * 03 SH  PTF10 Cursor på linje med feil
10317 * 04 YBC PTF10 *IN80 PÅ NÅR FELT MÅ ELLER BØR UTFYLLES.
10317 * 05 SH  PTF12 ikke krav til verdi ved mønskeroppdrag .
10317 * 06 SH  PTF12 generelle krav til fritekstkoden
********************************************************************
      ***********************************************************
      *** PROGRAM SKAL INNLEGGE FRISØKEVEI                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24 26
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FHEADF     IF   E           K DISK
     FFRISOK    UF A E           K DISK
     FFRISOL03  IF   E           K DISK
     F                                     RENAME(FRISOKR:FRISOKR3)
     FKODFRI    IF   E           K DISK
     FCUOTI     IF   E           K DISK
     FCUOTIG    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FDOKUF7    IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYFA64FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
     D                 DS
     D  KFSOTX                 1     30
     D  KFSOTY                 1     14
     D                 DS
     D  FSSOK                  1     35
     D  XDLT                   1      7
     D                 DS
     D  W2DOKK                 1     10
     D  DK                     1     10
     D                                     DIM(10) ASCEND
     D                 DS
s01  D* LINK                   1    128
N01  D  LINK                   1    300
     D  LINK1                  1     70
     D  LINK2                 71    140
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KODE        YKODE
     C                   MOVE      W2SOK         YSOK
     C                   MOVE      *BLANKS       W2KODE
     C                   MOVE      *BLANKS       W2SOK
     C                   MOVE      *BLANKS       W2DOKK
     C                   MOVE      ' '           MXKRAV            1
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N27FSK11         SETLL     FRISOK
     C   27FSK13         SETLL     FRISOL03
     C     START1        TAG
     C  N27FSKEY         READE(N)  FRISOK                                 94
     C   27FSKEY         READE     FRISOL03                               94
      *
     C                   MOVE      'N'           WEBSØK            1
N03  C                   z-add     0             feilr             2 0
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        10
     C                   ADD       1             ZRECNO
     C                   MOVE      FSSOK         WSSOK
     C                   MOVE      FSDOKK        WSDOKK
     C     FSKODE        CHAIN     KODFRI                             33
     C                   EXSR      TSTKRA
     C     KRAV          IFGT      MXKRAV
     C     WSSOK         ANDEQ     *BLANKS
N05  c     uopd          andgt     0
     C                   MOVE      KRAV          MXKRAV
     C                   END
     C     KFSTTU        COMP      *BLANKS                            8181
     C   81              MOVE      'J'           WEBSØK
     C     FSKODE        ifeq      'IFB'
     C                   exsr      TstFbr
     C                   END
n03  c   80feilr         ifeq      0
n03  C     zrecno        add       5             feilr
n03  c                   end
     C                   WRITE     SUBFIL
     C  N27FSKEY         READE(N)  FRISOK                                 94
     C   27FSKEY         READE     FRISOL03                               94
     C  N94              MOVE      FSKODE        YSKODE            3
     C  N94              MOVE      FSSOK         YSSOK            35
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
n03  c     feilr         ifne      0
n03  c                   z-add     feilr         linnbr
n03  c                   z-add     24            posnbr
n03  c                   end
     C     MXKRAV        IFGT      '0'
     C                   MOVE      'JOV0872'     MSGNR
     C                   SETON                                        30
     C                   END
     C     WEBSØK        IFEQ      'J'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'JOV1335'     MSGNR
     C                   SETON                                        30
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C*** AVSLUTT - KAN IKKE AVSLUTTE MED MAXKRAV=2(=MÅ TASTES)
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
      *
     C     MXKRAV        IFEQ      '2'
     C                   MOVE      ' '           MXKRAV            1
     C                   Z-ADD     1             RRECNR            5 0
     C                   SETOFF                                       33
     C     *IN33         DOWEQ     '0'
     C     RRECNR        CHAIN     SUBFIL                             33
     C                   ADD       1             RRECNR            5 0
     C     *IN33         IFEQ      '0'
      *KRAV OG BLANK
     C     KRAV          ANDGT     MXKRAV
     C     WSSOK         ANDEQ     *BLANKS
N05  c     uopd          andgt     0
     C                   MOVE      KRAV          MXKRAV
     C                   END
     C                   END
     C     MXKRAV        IFEQ      '2'
     C                   MOVE      'JOV0873'     MSGNR
     C                   SETON                                        30
     C                   GOTO      START3
     C                   END
     C                   END
      *
     C                   GOTO      SLUTT
     C                   END
     C*** SØKING
     C   04              DO
     C     POSNBR        IFGE      60
     C                   MOVEL     'FSDOKK'      XKFTYP           10
     C                   MOVE      'N'           UUPDAT            1
     C                   MOVE      *BLANKS       XKFKOD            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    XKFKOD
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     60            POSNBR
     C     XKFKOD        IFNE      *BLANKS
     C     1             DO        10            NR                2 0
     C     DK(NR)        IFEQ      *BLANKS
     C                   MOVEL     XKFKOD        DK(NR)
     C                   GOTO      START3
     C                   END
     C                   END
     C                   END
     C                   GOTO      START3
     C                   END
     C* ELLES SØK PÅ PREDEFINERTE TEKSTKODER
     C                   CALL      'SYFA65A'
     C                   PARM                    W2KODE
     C                   Z-ADD     5             LINNBR
     C   90              Z-ADD     10            POSNBR
     C  N90              Z-ADD     24            POSNBR
     C                   GOTO      START3
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94
     CANN27FSK21         SETLL     FRISOK
     C  N94
     CAN 27FSK23         SETLL     FRISOL03
     C  N94
     CANN27FSKEY         READE(N)  FRISOK                                 94
     C  N94
     CAN 27FSKEY         READE     FRISOL03                               94
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C     W2NR          OREQ      '9'
     C                   EXSR      TESTB
     C                   GOTO      START3
     C                   END
     C     W2KODE        IFNE      *BLANKS
     C                   MOVE      '0'           *IN27
     C                   GOTO      START
     C                   END
     C     W2SOK         IFNE      *BLANKS
n05  C     UOPD          andgt     0
     C                   MOVE      '1'           *IN27
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TSTKRA        BEGSR
     C***********************************************
     C                   MOVE      ' '           KRAV
      * TEST OM DET ER ET KUNDEKRAV-FELT
     C                   MOVE      *zeros        OTIKNRtmp         8 0
     C*
     C* FØRST KUNDE 1
     C     OTIKU1        IFNE      *ZEROS
     C                   MOVE      OTIKU1        OTIKNR
     C                   MOVE      FSAVD         OTIAVD
     C     OTIKEY        CHAIN     CUOTI                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      COKRAV        KRAV
     C                   MOVE      OTIKNR        OTIKNRtmp
     C                   ELSE
     C                   MOVE      *ZEROS        OTIAVD
     C     OTIKEY        CHAIN     CUOTI                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      COKRAV        KRAV
     C                   MOVE      OTIKNR        OTIKNRtmp
     C                   END
     C                   END
     C                   END
      *
     C* DERSOM KUNDE 2 OGSÅ HAR KODEN I LISTE... STERKERE KRAV??
      * Om kunde 1 var blank eller kunde 2 hadde sterkere krav er den den som brukes ved opt 9.
     C     OTIKU2        IFNE      *ZEROS
     C                   MOVE      OTIKU2        OTIKNR
     C                   MOVE      FSAVD         OTIAVD
     C     OTIKEY        CHAIN     CUOTI                              33
     C     *IN33         IFEQ      '0'
     C     OTIKNRtmp     ifeq      *zeros
     C                   MOVE      OTIKNR        OTIKNRtmp
     c                   end
     C     COKRAV        IFGT      KRAV
     C                   MOVE      COKRAV        KRAV
     C                   MOVE      OTIKNR        OTIKNRtmp
     C                   END
     C                   ELSE
     C                   MOVE      *ZEROS        OTIAVD
     C     OTIKEY        CHAIN     CUOTI                              33
     C     *IN33         IFEQ      '0'
     C     OTIKNRtmp     ifeq      *zeros
     C                   MOVE      OTIKNR        OTIKNRtmp
     c                   end
     C     COKRAV        IFGT      KRAV
     C                   MOVE      COKRAV        KRAV
     C                   MOVE      OTIKNR        OTIKNRtmp
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      OTIKNRtmp     OTIKNR
     C     WSSOK         COMP      *BLANKS                                80
N04  C   80KRAV          COMP      '0'                                80
N05  c   80UOPD          comp      0                                  80
      *
     C                   ENDSR
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     W2NR          IFEQ      '9'
     C                   MOVE      'P'           UMODE             1
     C                   CALL      'VCUNDO2R'
     C                   PARM                    FIFIRM
     C                   PARM                    OTIKNR
     C                   PARM                    OTIAVD
     C                   PARM                    W2KODE
     C                   PARM                    W2SOK
     C                   PARM                    UMODE
     C                   END
      *
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     W2KODE        IFEQ      *BLANKS
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        9030
     C                   MOVE      'YBC0227'     MSGNR
     C                   ELSE
     C     W2KODE        CHAIN     KODFRI                             89
     C     *IN89         IFEQ      '1'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        8930
     C                   MOVE      'YBC0230'     MSGNR
     C                   END
     C                   END
      *
     C     W2SOK         IFEQ      *BLANKS
n05  C     UOPD          andgt     0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     24            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'YBC0228'     MSGNR
     C                   END
N02  C     W2KODE        IFEQ      'IFB'
N02  C                   if        %subst(W2SOK:1:3)='401'
N02  c                             and %subst(W2SOK:20:1)<>' '
N02  c                   eval      W2SOK = %subst(W2SOK:4:17)
N02  c                   endif
N02  c                   ENDIF
      *
     C     FSKUPD        CHAIN(N)  FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        908930
     C                   MOVE      'YBC0229'     MSGNR
     C                   END
      *
      *HAR KODE GYLDIGHETSTEST
      *
     C     OTIKNR        IFNE      *ZEROS
     C     WTIKEY        CHAIN     CUOTIG                             33
     C     *IN33         IFEQ      '0'
     C     WTIKE2        CHAIN     CUOTIG                             33
     C     *IN33         IFEQ      '1'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     24            POSNBR
     C                   SETON                                        908930
     C                   MOVE      'SHU0600'     MSGNR
     C                   END
     C                   END
     C                   END
N06   *HAR KODE GYLDIGHETSTEST
N06   *
n06  C     WTIKEYG0      CHAIN     CUOTIG                             33
n06  C     *IN33         IFEQ      '0'
N06  C     WTIKEYG       CHAIN     CUOTIG                             33
N06  C     *IN33         IFEQ      '1'
N06  C                   Z-ADD     5             LINNBR
N06  C                   Z-ADD     24            POSNBR
N06  C                   SETON                                        908930
N06  C                   MOVE      'SHU2043'     MSGNR
N06  C                   END
N06  C                   END
      *
      *
     C   30              GOTO      SLTB
      *
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C                   MOVE      W2KODE        FSKODE
     C                   MOVE      W2SOK         FSSOK
     C     W2DOKK        IFNE      *BLANKS
     C     W2DOKK        IFEQ      '0'
     C                   MOVE      *BLANKS       W2DOKK
     C                   END
     C                   MOVE      W2DOKK        FSDOKK
     C                   ELSE
     C                   MOVE      KFSODK        FSDOKK
     C                   END
     C                   MOVE      ' '           KRAV
     C                   MOVE      HEDTOP        FSDTOP
     C                   WRITE     FRISOKR
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      FSSOK         WSSOK
     C                   MOVE      FSDOKK        WSDOKK
     C     WSSOK         COMP      *BLANKS                                80
N05  C   80UOPD          comp      0                                  80
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2KODE
     C                   MOVE      *BLANKS       W2SOK
     C                   MOVE      *BLANKS       W2DOKK
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       5             LINNBR
     C                   MVR                     LINNBR
     C     LINNBR        IFEQ      0
     C                   Z-ADD     5             LINNBR
     C                   END
      *
      *SLETTING
      *
     C     WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     FSKDLT        CHAIN     FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   DELETE    FRISOKR
     C                   MOVE      *BLANKS       FSKODE
     C                   MOVE      *BLANKS       FSSOK
     C                   MOVE      *BLANKS       FSDOKK
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      FSSOK         WSSOK
     C                   MOVE      *BLANKS       KFSOTX
     C                   MOVE      *BLANKS       KRAV
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      *ENDRING
      *
     C     WSNR          IFEQ      '2'
     C     WSNR          OREQ      ' '
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     FSKDLT        CHAIN     FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      WSDOKK        FSDOKK
      * IKKE LOV MANUELT Å BLANKE EN SØKELINJE
     C     WSSOK         IFEQ      *BLANKS
     C                   MOVE      FSSOK         WSSOK
     C                   END
     C     FSSOK         IFEQ      WSSOK
     C                   UPDATE    FRISOKR
     C                   ELSE
     C                   DELETE    FRISOKR
     C                   MOVE      WSSOK         FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C                   WRITE     FRISOKR
     C                   END
     C     WSSOK         IFNE      *BLANKS
     C     OTIKEY        CHAIN     CUOTIG                             33
     C     *IN33         IFEQ      '0'
     C     OTIKE2        CHAIN     CUOTIG                             33
     C  N33              SETOFF                                       80
     C   33              SETON                                        80
     C   33              MOVE      'SHU0600'     MSGNR
     C   33              SETON                                        30
     C                   ELSE
     C                   SETOFF                                       80
     C                   END
     C                   ELSE
     C                   SETON                                        80
     C                   END
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      *9=GYLDIGE
      *
     C     WSNR          IFEQ      '9'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     FSKDLT        CHAIN     FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      WSDOKK        FSDOKK
      * IKKE LOV MANUELT Å BLANKE EN SØKELINJE
     C                   MOVE      'P'           UMODE             1
     C                   CALL      'VCUNDO2R'
     C                   PARM                    FIFIRM
     C                   PARM                    OTIKNR
     C                   PARM                    OTIAVD
     C                   PARM                    FSKODE
     C                   PARM                    WSSOK
     C                   PARM                    UMODE
     C     WSSOK         IFEQ      *BLANKS
     C                   MOVE      FSSOK         WSSOK
     C                   END
     C     FSSOK         IFEQ      WSSOK
     C                   UPDATE    FRISOKR
     C                   ELSE
     C                   DELETE    FRISOKR
     C                   MOVE      WSSOK         FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C                   WRITE     FRISOKR
     C                   END
     C     WSSOK         IFNE      *BLANKS
N05  C     UOPD          ANDGT     0
     C     OTIKEY        CHAIN     CUOTIG                             33
     C     *IN33         IFEQ      '0'
     C     OTIKE2        CHAIN     CUOTIG                             33
     C  N33              SETOFF                                       80
     C   33              SETON                                        80
     C   33              MOVE      'SHU0600'     MSGNR
     C   33              SETON                                        30
     C                   ELSE
     C                   SETOFF                                       80
     C                   END
     C                   ELSE
     C                   SETON                                        80
     C                   END
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      * WEB-Url-link
      *
     C     WSNR          IFEQ      'W'
     C     KFSTTU        comp      *BLANKS                                30
     C  N30              EVAL      KFKOD = KFSTTU
     C  N30KOFASTK1      CHAIN     KOFAST                             30
     C     *in30         IFEQ      *ON
     C                   MOVE      'JOV1336'     MSGNR
     c                   goto      slhent
     C                   End
     C                   exsr      crtlink
      *
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   END
      *
     C                   END
      *
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     TSTFBR        BEGSR
     C***********************************************
      * Test om transportør på fraktbrev overstyrer T&T-URL-kode
     C*
     C     WSSOK         chain     dokuf7                             33
     c     *IN33         cabeq     '1'           UtTstFbr
     c     DFTRAN        cabeq     0             UtTstFbr
     C     TranKey       chain     Tran                               33
     c     *IN33         cabeq     '1'           UtTstFbr
     c     VMINTP        cabeq     *blanks       UtTstFbr
     C                   EVAL      KFKOD = VMINTP
     C     KOFASTK1      CHAIN     KOFAST                             33
     c     *IN33         cabeq     '1'           UtTstFbr
      * fant gyldig url på fraktbrevets transportør - bruk det
     C                   movel     VMINTP        KFSTTU
     C                   move      'J'           WEBSØK
     C                   move      *on           *in81
     C     UtTstFbr      ENDSR
     C*
     C***********************************************
     C     CRTLINK       BEGSR
     C***********************************************
      * Konstruer link/URL
     C                   EVAL      LINK = KFTXT
      * append linje 2 (engelsk tekst når siste tegn er A)
     C                   MOVE      KFTXTE        TstAppend         1
     C     TstAppend     IFEQ      'A'
     c                   move      ' '           KFTXTE
     C                   CAT       KFTXTE:0      LINK
     C                   end
     C                   CAT       WSSOK:0       LINK
      * Suffix i søkestrengen..
     C     KOFASTK2      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF
     C                   CAT       KFTXT:0       LINK
N01   * append linje 2 (engelsk tekst når siste tegn er A)
N01  C                   MOVE      KFTXTE        TstAppend         1
N01  C     TstAppend     IFEQ      'A'
N01  c                   move      ' '           KFTXTE
N01  C                   CAT       KFTXTE:0      LINK
N01  C                   end
     C                   END
     c     StrWeb        Tag
     c     Link2         Ifeq      *blanks
     C                   EXFMT     WEBSOK
     c  N03              goto      strWeb
     c                   else
     c                   call      'SYFA64B'
     c                   parm                    LINK
     c                   end
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     FSK11         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    YKODE             3
     C     FSK13         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    YSOK             35
     C     FSKEY         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FSK21         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    YSKODE            3
     C                   KFLD                    YSSOK            35
     C     FSK23         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    YSSOK
     C     FSKUPD        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    W2KODE
     C                   KFLD                    W2SOK
     C     FSKDLT        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     OTIKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    OTIKNR            8 0
     C                   KFLD                    OTIAVD            4 0
     C                   KFLD                    FSKODE
     C     OTIKE2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    OTIKNR            8 0
     C                   KFLD                    OTIAVD            4 0
     C                   KFLD                    FSKODE
     C                   KFLD                    WSSOK
     C     WTIKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    OTIKNR            8 0
     C                   KFLD                    OTIAVD            4 0
     C                   KFLD                    W2KODE
n06  C     WTIKEYG0      KLIST
n06  C                   KFLD                    FIFIRM
n06  C                   KFLD                    OTIKNR0           8 0
n06  C                   KFLD                    OTIAVD0           4 0
n06  C                   KFLD                    W2KODE
n06  C     WTIKEYG       KLIST
n06  C                   KFLD                    FIFIRM
n06  C                   KFLD                    OTIKNR0           8 0
n06  C                   KFLD                    OTIAVD0           4 0
n06  C                   KFLD                    W2KODE
n06  C                   KFLD                    W2SOK
     C     WTIKE2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    OTIKNR            8 0
     C                   KFLD                    OTIAVD            4 0
     C                   KFLD                    W2KODE
     C                   KFLD                    W2SOK
      * key for track prefix/evt suffix
     C     KOFASTK1      klist
     C                   kfld                    KFTYP1           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP1 = 'TRACKPREFI'
     C     KOFASTK2      klist
     C                   kfld                    KFTYP2           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP2 = 'TRACKSUFFI'
      *
     C     TranKey       klist
     C                   kfld                    FIFIRM
     C                   kfld                    DFTRAN
      *
     C     *ENTRY        PLIST
     C     FSAVD         PARM                    UAVD              4 0
     C     FSOPD         PARM                    UOPD              7 0
     C                   PARM                    OTIKU1            8 0
     C                   PARM                    OTIKU2            8 0
      *
     C                   MOVE      *BLANKS       FSSOK
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   WRITE     WINDOWSZ
      *
     C     FSKEY         CHAIN     HEADF                              33
     C                   ENDSR
     C*
