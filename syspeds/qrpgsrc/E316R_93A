********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 7 LANG FAKTURANR
10XXX * 02 YBC PTF10 SEND FRAKTBREV
********************************************************************
      * SYSPED-SYEDI For DEDIP2 INVOIC D.93A
      *
      * 1. SYSPED SYKS37 legger ut poster i INTFAS
      *
      * 2. Dette programmet leser fra INTFAS
      *
      * 3. Dette programmet legger ut til EDI-filer i SYEDI.
      *
      * 4. SYEDI lager automatisk EDIFACT utifra EDI-filene.
      *
     FINTFAS    UF   E           K DISK
     FEDIC      UF   E             DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
N02  FDOKUF     IF   E           K DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FEUNS      O  A E             DISK
     FECNT93A   O  A E             DISK
     FECTA93A   O  A E             DISK
     FECUX93A   O  A E             DISK
     FEDTM93A   O  A E             DISK
     FEFII93A   O  A E             DISK
     FEIMD93A   O  A E             DISK
     FELIN93A   O  A E             DISK
     FEMEA93A   O  A E             DISK
     FEMOA93A   O  A E             DISK
     FENAD93A   O  A E             DISK
     FEPAT93A   O  A E             DISK
     FEPIA93A   O  A E             DISK
     FEPRI93A   O  A E             DISK
     FEQTY93A   O  A E             DISK
     FERFF93A   O  A E             DISK
     FETAX93A   O  A E             DISK
     FETOD93A   O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTID                   1      6  0
     D  WDATO                  7     14  0
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR                                                  CTA05 /
      *////////////////////////////////////////////////////////////
     C     CTA05         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
      *___________________
      * Purchasing contact
      *"""""""""""""""""""
     C                   MOVEL     'AD'          C3139
     C                   EVAL      C3412 = KOSFNV
     C                   WRITE     CTAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   Z-ADD     WREP0         CREP0
      *_____________________
      * Number of line items
      *"""""""""""""""""""""
     C                   MOVEL     '2'           C6069
     C                   MOVEL     L1082         C6066
     C                   WRITE     CNTR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CUX07 /
      *////////////////////////////////////////////////////////////
     C     CUX07         BEGSR
      *_________
      * Currency
      *"""""""""
     C                   CLEAR                   CUXR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     7             CGN
     C                   Z-ADD     WREP0         CREP0

     C                   EVAL      C6347A = '2'
     C                   EVAL      C6345A = INVAL
     C                   EVAL      C6343A = '4'
     C                   WRITE     CUXR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
      *_____________
      * Message date
      *"""""""""""""
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         D2380
     C     D2380         CAT       WTM:0         D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 DTM08A /
      *////////////////////////////////////////////////////////////
     C     DTM08A        BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *____________
      * Faktoradato
      *""""""""""""
     C                   MOVEL     '7'           D2005
     C                   MOVEL     INDATO        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 DTM08B /
      *////////////////////////////////////////////////////////////
     C     DTM08B        BEGSR
     C                   Z-ADD     2             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_____________
      * Forfallsdato
      *"""""""""""""
     C                   MOVEL     '13'          D2005
     C                   MOVEL     INDAFF        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * EDIM0                                                EDIM0/
      *////////////////////////////////////////////////////////////
     C     EDIM0         BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     INSEID        M0004
     C                   MOVEL     INMOID        M0010
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   WRITE     EDIMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FII02 /
      *////////////////////////////////////////////////////////////
     C     FII02         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   FIIR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     2             FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
      *_______________
      * Receiving Bank
      *"""""""""""""""
     C                   EVAL      F3035 = 'RH'
     C                   EVAL      F3194 = BANKG
     C                   WRITE     FIIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  IMD22 /
      *////////////////////////////////////////////////////////////
     C     IMD22         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   IMDR
     C                   Z-ADD     WMN           IMN
     C                   Z-ADD     22            IGN
     C                   Z-ADD     WREP0         IREP0
     C                   Z-ADD     WREP1         IREP1
     C                   MOVEL     'FL'          I7077
     C     KODTGEK       CHAIN     KODTGE                             33
     C  N33              MOVEL     KGENOT        I7008A
     C   33              MOVEL     INVK          I7008A
     C                   WRITE     IMDR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LIN22 /
      *////////////////////////////////////////////////////////////
     C     LIN22         BEGSR

     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LINR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     22            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP0         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082
     C                   WRITE     LINR

     C                   EXSR      PIA22
     C                   EXSR      IMD22
     C                   EXSR      QTY22
     C                   EXSR      MOA23
     C                   EXSR      PRI25
     C                   EXSR      RFF26
     C                   EXSR      TAX30

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA23 /
      *////////////////////////////////////////////////////////////
     C     MOA23         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     23            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '66'          M5025
     C                   EVAL      ØN153=INBELN
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '203'         M5025
     C                   WRITE     MOAR
      *
     C                   ADD       INBELN        WINBELN
     C                   ADD       INBELV        WINBELV
     C     INMVA         IFEQ      'J'
     C                   ADD       INBELN        WINBELNM
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA30 /
      *////////////////////////////////////////////////////////////
     C     MOA30         BEGSR
     C                   Z-ADD     1             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     30            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     '174'         M5025
     C                   EVAL(H)   ØN153=INBELN*FITAX*100
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA45 /
      *////////////////////////////////////////////////////////////
     C     MOA45         BEGSR
      *_____________
      * VARELINJESUM
      *"""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '66'          M5025
     C                   IF        C6345A='NOK'
     C                   Z-ADD     WINBELN       ØN153
     C                   ELSE
     C                   Z-ADD     WINBELV       ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *_____________
      * LINJESUM
      *"""""""""""""
     C                   ADD       1             MREP0
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '203'         M5025
     C                   WRITE     MOAR
      *_____________
      * NETTO BELØP
      *"""""""""""""
     C                   ADD       1             MREP0
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     'NET'         M5025
     C                   WRITE     MOAR

      *__________
      * MomsGRUNNLAG
      *"""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '125'         M5025
     C                   EVAL      ØN153 = WINBELNM
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *__________
      * Momsbelöp
      *""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '150'         M5025
     C                   IF        INDATO >= FITAXD
     C                   EVAL      ØN153 = WINBELNM * FITAX + 0.005
     C                   ELSE
     C                   EVAL      ØN153 = WINBELNM * FITAX2 + 0.005
     C                   END
     C                   MOVEL     0             ØN153
     C                   EVAL      WMOMS = ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *_____________
      * Fakturabeløp
      *"""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '86'          M5025
     C                   IF        C6345A='NOK'
     C                   Z-ADD     WINBELN       ØN153
     C                   ADD       WMOMS         ØN153
     C                   ELSE
     C                   Z-ADD     WINBELV       ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 MOA47 /
      *////////////////////////////////////////////////////////////
     C     MOA47         BEGSR
      *_____________
      * MVA
      *"""""""""""""
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     47            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '176'         M5025
     C                   Z-ADD     WMOMS         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD02 /
      *////////////////////////////////////////////////////////////
     C     NAD02         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *________________
      * Vendor/Supplier
      *""""""""""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'SU'          N3035
     C                   EVAL      N3039='7080001021306'
?    C                   MOVEL     '9'           N3055
     C                   EVAL      WUNDNR = KOAKNR
     C     CUNKEY        CHAIN     CUNDL01                            61
     C                   EVAL      N3036A = KNAVN
     C                   EVAL      N3042A = ADR1
     C                   EVAL      N3164  = ADR3
     C                   MOVEL     POSTNR        N3251
     C                   EVAL      N3207  = 'NO'
     C                   WRITE     NADR

     C                   EXSR      FII02
     C                   EXSR      RFF03
     C                   EXSR      CTA05
      *______
      * Buyer
      *""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'BY'          N3035
     C                   EVAL      N3039='7080001006693'
?    C                   MOVEL     '9'           N3055
      * hente navn og adresse fra kunderegister.
     C                   EVAL      WUNDNR = INKUNR
     C     CUNKEY        CHAIN     CUNDL01                            61
     C                   EVAL      N3036A = KNAVN
     C                   EVAL      N3042A = ADR1
     C                   EVAL      N3164  = ADR3
     C                   MOVEL     POSTNR        N3251
     C                   IF        SYLAND = *BLANKS
     C                   EVAL      N3207  = 'NO'
     C                   ELSE
     C                   EVAL      N3207  = SYLAND
     C                   END
     C                   WRITE     NADR
      *_________
      * Delivery
      *"""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'DP'          N3035
     C                   EVAL      N3036A = HENAK
     C                   EVAL      N3042A = HEADK1
     C                   IF        HEADK3 = *BLANKS
     C                   EVAL      N3164  = HEADK2
     C                   MOVEL     HEADK2        XA05              5
     C                   ELSE
     C                   EVAL      N3164  = HEADK3
     C                   MOVEL     HEADK3        XA05
     C                   END
     C                   MOVEL     XA05          N3251
     C                   EVAL      N3207  = HELKA
     C                   WRITE     NADR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PAT08 /
      *////////////////////////////////////////////////////////////
     C     PAT08         BEGSR
      *____________
      * Inoice date
      *""""""""""""
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '1'           P4279
     C                   WRITE     PATR
      *
     C                   EXSR      DTM08A
      *______________
      * Payment terms
      *""""""""""""""
     C                   IF        INDAFF <> 0
     C                   ADD       1             WREP0
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '3'           P4279
     C                   WRITE     PATR
      *
     C                   EXSR      DTM08B
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PIA22 /
      *////////////////////////////////////////////////////////////
     C     PIA22         BEGSR

     C                   Z-ADD     1             WREP1
     C                   CLEAR                   PIAR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     22            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   EVAL      P4347 = '5'
     C                   EVAL      P7140A = INVK
     C                   EVAL      P7143A = 'SA'
     C                   EVAL      P3055A = '91'
     C                   WRITE     PIAR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PRI25 /
      *////////////////////////////////////////////////////////////
     C     PRI25         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   PRIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     25            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     'AAB'         P5125
     C                   EVAL      ØN153=INBELN
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           P5118
     C                   MOVEL     'AAK'         P5375
     C                   WRITE     PRIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  QTY22 /
      *////////////////////////////////////////////////////////////
     C     QTY22         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     22            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   MOVEL     '47'          Q6063
     C                   MOVEL     '1'           Q6060
     C                   WRITE     QTYR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   Z-ADD     0             WREP0
      *_____________
      * Order number
      *"""""""""""""
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'ON'          R1153
     C*                  MOVEL     INANNE        R1154
     C                   MOVEL     HERFA         R1154
     C                   WRITE     RFFR
      *________________________________
      * Landkode til mottaker (eksport)
      *""""""""""""""""""""""""""""""""
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'AEP'         R1153
     C                   MOVEL     HELKA         R1154
     C                   WRITE     RFFR
      *_____________
      * OPPDRAGSNR
      *"""""""""""""
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'VN'          R1153
     C                   MOVEL     INUAVD        R1154
     C                   CAT       '/':0         R1154
     C                   MOVEL     INUOPD        XA07              7
     C                   CAT       XA07:0        R1154
     C                   WRITE     RFFR
N02   *_____________
N02   * FRAKTBREVNR
N02   *"""""""""""""
N02  C                   IF        DF1004 <> *BLANKS
N02  C                   CLEAR                   RFFR
N02  C                   Z-ADD     WMN           RMN
N02  C                   Z-ADD     1             RGN
N02  C                   ADD       1             WREP0
N02  C                   Z-ADD     WREP0         RREP0
N02  C                   EVAL      R1153 = 'AAK'
N02  C                   EVAL      R1154 = DF1004
N02  C                   WRITE     RFFR
N02  C                   END
N02   *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
      *________________
      * ORGANISASJONSNR
      *""""""""""""""""
     C                   EVAL      R1153 = 'VA'
     C                   EVAL      R1154 = 'NO'
     C                   CAT       SYRG:0        R1154
     C                   CAT       'MVA':0       R1154
     C                   WRITE     RFFR
      *________________
      * KID-nummer
      *""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         RREP1
     C                   EVAL      R1153 = 'PQ'
     C                   MOVEL     INKID         R1154
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF26 /
      *////////////////////////////////////////////////////////////
     C     RFF26         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     26            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'IV'          R1153
     C                   MOVEL     INFANR        R1154
     C                   MOVE      WREP0         XA02              2
     C                   CAT       '-':0         R1154
     C                   CAT       XA02:0        R1154
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    WINSEID
     C                   PARM                    WINMOID
S01  C*                  PARM                    UUFANR            6
N01  C                   PARM                    UUFANR            7
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO
     C                   PARM                    WTID
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    DREP3         WREP3
     C     *LIKE         DEFINE    UMN           WMN
     C     *LIKE         DEFINE    KUNDNR        WUNDNR
     C     *LIKE         DEFINE    INBELN        WINBELN
     C     *LIKE         DEFINE    INBELV        WINBELV
     C     *LIKE         DEFINE    INBELN        WINBELNM
     C     *LIKE         DEFINE    INBELN        WMOMS
     C     *LIKE         DEFINE    INST          WINST
     C     *LIKE         DEFINE    INSEID        WINSEID
     C     *LIKE         DEFINE    INMOID        WINMOID
     C     *LIKE         DEFINE    INFANR        WINFANR
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W_ØN153          15 3
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVEL     WTID          WTM               4
     C                   MOVE      UUFANR        WINFANR
      * DEF AV KEYS
     C     CUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WUNDNR
     C     INTKEY        KLIST
     C                   KFLD                    WINST
     C                   KFLD                    WINSEID
     C                   KFLD                    WINMOID
     C                   KFLD                    WINFANR
     C     HEADFK        KLIST
     C                   KFLD                    INUAVD
     C                   KFLD                    INUOPD
N02  C     DOKUFK        KLIST
N02  C                   KFLD                    DFRI
N02  C                   KFLD                    INUAVD
N02  C                   KFLD                    INUOPD
N02  C                   KFLD                    DFFBNR
N02  C                   EVAL      DFRI   = 'F'
N02  C                   EVAL      DFFBNR = 1
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    HEAVD
     C                   MOVE      'A'           KOAUNI
     C     KODTGEK       KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    INVK
     C                   MOVE      'GE'          KGEUNI
     C     KODTSFK       KLIST
     C                   KFLD                    KOSFUN
     C                   KFLD                    HESG
     C                   MOVE      'SF'          KOSFUN
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   MOVE      '1'           *IN41
      *_________________________
      * Les fakturatransaksjoner
      *"""""""""""""""""""""""""
     C     INTKEY        SETLL     INTFAS
     C     INTKEY        READE     INTFAS                                 51
      *
     C     *IN51         IFEQ      '0'
     C     HEADFK        CHAIN     HEADF                              33
N02  C     DOKUFK        CHAIN     DOKUF                              33
N02  C   33              EVAL      DF1004 = *BLANKS
     C     KODTAK        CHAIN     KODTA                              33
     C     KODTSFK       CHAIN     KODTSF                             33
     C                   END
      *
     C     *IN51         DOWEQ     '0'
      *____________________________
      * Slett transkasjonspost
      *""""""""""""""""""""""""""""
     C                   EVAL      INST='X'
     C                   UPDATE    INTFASR
      *_________________
      * EDIFACT Detaljer
      *"""""""""""""""""
     C                   IF        INBELN <> 0
     C                   EXSR      LIN22
     C                   END
      *__________________
      * Les neste faktura
      *""""""""""""""""""
     C     INTKEY        READE     INTFAS                                 51
     C                   ENDDO
      *_________________
      * Hoveopplysninger
      *"""""""""""""""""
     C                   EXSR      SRB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      DTM00
     C*                  EXSR      FTX00
     C                   EXSR      RFF01
     C                   EXSR      NAD02
     C                   EXSR      CUX07
     C                   EXSR      PAT08
     C                   EXSR      TOD11
     C                   EXSR      UNS00
     C                   EXSR      CNT00
     C                   EXSR      MOA45
     C                   EXSR      TAX47
      *_____________
      * WRITE TO UNH
      *"""""""""""""
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      EDIM0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Remove neg sign.
     C                   IF        ØN153 < 0
     C                   Z-SUB     ØN153         ØN153
     C                   ENDIF
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'
     C                   MOVE      ØA153H        ØA12
     C                   ELSE
     C                   MOVEL     ØA153X        ØA12
     C                   MOVE      ',   '        ØA12
     C                   MOVE      ØA153D        ØA12
     C                   END
      * search from left and find first sign over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'
     C     ØA153X        IFEQ      '00000000'
     C                   SUB       1             ØX
     C                   ENDIF
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX30 /
      *////////////////////////////////////////////////////////////
     C     TAX30         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     30            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   Z-ADD     WREP1         TREP1
     C                   MOVEL     '7'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   IF        INMVA = 'J'
     C*                  EVAL      ØN153 = FITAX * 100
     C*                  EXSR      SRNE
     C*                  MOVEL     ØAN           T5278
     C                   EVAL      T5305 = 'S'
     C                   EXSR      MOA30
     C                   ELSE
     C*                  MOVEL     '0'           T5278
     C                   EVAL      T5305 = 'E'
     C                   END
     C                   WRITE     TAXR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX47 /
      *////////////////////////////////////////////////////////////
     C     TAX47         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     47            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     '7'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   EVAL      ØN153 = WINBELNM
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           T5286
     C                   EVAL      T5279 = '24'
     C                   EVAL      T5305 = 'S'
     C                   WRITE     TAXR
      *
     C                   EXSR      MOA47
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TOD11 /
      *////////////////////////////////////////////////////////////
     C     TOD11         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *_________
      * Main leg
      *"""""""""
     C                   CLEAR                   TODR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     11            TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   EVAL      T4055 = '6'
     C                   EVAL      T4053 = HEFR
     C                   WRITE     TODR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'INVOIC'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '93A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'EAN007'      U0057
     C                   MOVEL     '380'         U1001
     C                   MOVEL     INFANR        U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  UNS00 /
      *////////////////////////////////////////////////////////////
     C     UNS00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   UNSR
     C                   Z-ADD     WMN           UMN
     C                   Z-ADD     0             UGN
     C                   Z-ADD     WREP0         UREP0
     C                   MOVEL     'S'           U0081
     C                   WRITE     UNSR
     C                   ENDSR
