      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSF04R   MODULE(*LIBL/STSF04R  *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FKODTP     IF   E           K DISK    USROPN
     FSTSCLI    UF   E           K DISK    usropn
     FSTSfbr    UF   E           K DISK    usropn
     FSTSopd    UF   E           K DISK    usropn
     FSTStur    UF   E           K DISK    usropn
     FTRACKK    O  A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D wspro           s              8  0
     DTurnr            s              8
     D User            s             10
     D FullF           s              1
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
     C                   eval      USER   = zhbgetvar('user')
     C                   eval      TURNR  = zhbgetvar('turnr')
     C                   eval      wspro  = c2n2(TURNR)
     C                   eval      YTYPE  = zhbgetvar('YTYPE')
     C                   eval      Fullf  = zhbgetvar('fullf')
      *
     C                   open      BRIDF
     C     User          CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * INITIERING...
     C     *LIKE         DEFINE    SCTYPE        YTYPE
     C     TURKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C     KODTPK        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    SOAVD
     C                   KFLD                    KOPLNR
     C                   MOVE      'P'           KOPUNI
     C                   Z-ADD     113           KOPLNR
      *
     C                   open      KODTP
     c                   open      stscli
     c                   open      stsfbr
     c                   open      stsopd
     c                   open      ststur
     c                   open      TRACKK
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           TKDATE
     C                   MOVE      WTM           TKTIME
      *
     C     wspro         ifne      *zeros
      *-Sett fullført-merke - Y = fullføre
     c     Fullf         Ifeq      'Y'
     C                   exsr      FULL
     C                   else
      *-Delete fra workfiles - N = Nullstill
     c     Fullf         Ifeq      'N'
     C                   exsr      DCLI
     C                   end
     C                   end
     C                   end
      *-A = Avbryte / parkere...
      *
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     c     user          ifeq      'NN'
     C                   eval      Mbr = 'STS01B'
     c                   else
     C                   eval      Mbr = 'STS02'
     c                   end
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('user':user)
     c     user          ifeq      'NN'
     C                   callp     updhtmlvar('user':spaces)
     c                   end
     C                   callp     updHTMLvar('besk':BIBESK)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('FEILTXT':spaces)
      *
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
      *
     c                   close     bridf
     C                   close     KODTP
     c                   close     stscli
     c                   close     stsfbr
     c                   close     stsopd
     c                   close     ststur
     c                   close     trackK
     C                   eval      *inlr = *on
     C                   RETURN
      *
      ***********************************************
     C     FULL          BEGSR
      ***********************************************
      * Set status Fullført manuelt...
     C     TurKey        chain     STStur                             30
     C     *in30         IFEQ      '0'
      * X=fullført lest  / M=avsluttet ed manko
     C     STANOK        IFGE      STANOP                                           5<-B
     C                   move      'X'           ststat
     C                   ELSE                                                       5<-E
     C                   move      'M'           ststat
     C                   END                                                        5<-E
     C                   UPDATE    STSTURR
     C                   EXSR      TTSR
     C                   end
      *
     C     TurKey        SETLL     STSopd
     C     TurKey        READE     STSopd                                 94
     C     *IN94         DOWEQ     '0'
     C     TurKey        READE     STSopd                                 94
     C  N94              ENDDO
      *
     C                   IF        SOAVD <> 0
     C     KODTPK        CHAIN     KODTP                              33
     C   33              EVAL      KOPNVN = '*JOB'
     C                   MOVE      SOPRO         UPRO              8
     C                   CALL      'STSF06C'
     C                   PARM                    STSTAT
     C                   PARM                    SOTYPE
     C                   PARM                    UPRO
     C                   PARM                    KOPNVN
     C                   PARM                    USER
     C                   END
      *
     C                   ENDSR
      *
      *
      ***********************************************
     C     DCLI          BEGSR
      ***********************************************
      *
     C     TurKey        SETLL     STSCLI
     C     TurKey        READE     STSCLI                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    stsclir
     C     TurKey        READE     STSCLI                                 94
     C  N94              ENDDO
      *
     C     TurKey        SETLL     STSfbr
     C     TurKey        READE     STSfbr                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    stsfbrr
     C     TurKey        READE     STSfbr                                 94
     C  N94              ENDDO
      *
     C     TurKey        SETLL     STSopd
     C     TurKey        READE     STSopd                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    stsopdr
     C     TurKey        READE     STSopd                                 94
     C  N94              ENDDO
      *
     C     TurKey        SETLL     STStur
     C     TurKey        READE     STStur                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    ststurr
     C     TurKey        READE     STStur                                 94
     C  N94              ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     TTSR          BEGSR
      ***********************************************
      * HENT DATO/KLOKKESLETT
     C                   MOVE      *BLANKS       TKCLID
     C                   MOVEL     STPRO         TKCLID
     C                   CAT       STTYPE:1      TKCLID
     C                   MOVE      *ZEROS        TKAVD
     C                   MOVE      *ZEROS        TKOPD
     C                   MOVE      'FTR'         TKKODE
     C                   MOVE      USER          TKUSER
     C                   IF        STSTAT = 'X'
     C                   EVAL      TKTEXT = 'FELT KONTROLL FULLFØRT.'
     C                   ELSE
     C                   EVAL      TKTEXT = 'FELT KONTROLL FULLFØRT MED MANKO.'
     C                   END
     C                   WRITE     TRACKKR
      *
     C                   ENDSR
      *
