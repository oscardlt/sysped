********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 02 sh  PTF10 prosentavtale        ...
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 03 YBC PTF12 MULIGHET FOR HELE LAND
********************************************************************
      *
      ***********************************************************
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 05-26                      ***
      *** LEDIGE INDIKATORER 28 29 31 32 35-39 44-50 56-94
      ***********************************************************
      *
     FKODTFM    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTW     IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FHODE      IF   E           K DISK
     FAVFRH     IF   E           K DISK
     FAVFRD     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYPO21FM  CF   E             WORKSTN INFDS(FBAREA)
     FSYPO21PF  O    E             PRINTER OFLIND(*IN34)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  AFGE01                 1      3
     D  AFGE02                 4      6
     D  AFGE03                 7      9
     D  AFGE04                10     12
     D  AFGE05                13     15
     D  AFGE06                16     18
     D  AFGE07                19     21
     D  AFGE08                22     24
     D  AFGE09                25     27
     D  AFGE10                28     30
     D  GE                     1     30
     D                                     DIM(10)
     D                 DS
     D  HOHT1                  1     80
     D  HOHT2                 81    160
     D  HOHT3                161    240
     D  HOHT4                241    320
     D  HOHT5                321    400
     D  HOHT6                401    480
     D  HOHT7                481    560
     D  HO                     1    560
     D                                     DIM(7)
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  PRDT2D                 1      2  0
     D  PRDT2M                 3      4  0
     D  PRDT2Å                 5      6  0
     D  PRDAT2                 1      6  0
     D                 DS
     D  AFGJDÅ                 1      4  0
     D  AFGJDM                 5      6  0
     D  AFGJDD                 7      8  0
     D  AFGJDT                 1      8  0
     D                 DS
     D  PRDT1D                 1      2  0
     D  PRDT1M                 3      4  0
     D  PRDT1Å                 5      6  0
     D  PRDAT1                 1      6  0
     D                 DS
     D  AFAKDÅ                 1      4  0
     D  AFAKDM                 5      6  0
     D  AFAKDD                 7      8  0
     D  AFAKDT                 1      8  0
     D                UDS
     D  UAVD                  15     18  0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C  N30
     CANN04              Z-ADD     4             LINNBR
     C  N30
     CANN04              Z-ADD     18            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVE      '0'           *IN91
     C                   MOVE      '0'           *IN(27)
     C                   MOVE      '0'           *IN(30)
     C                   MOVEA     '00'          *IN(33)
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TESTB
     C   30              GOTO      START
     C                   EXSR      PRINT
     C                   SETOFF                                       9930
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     TESTB         BEGSR
     C******************************************
     C*
     C* TEST FRA OG TIL KUNDENR
     C     WSKNF         IFEQ      0                                            1<-B
     C     WSKNT         IFNE      0                                             2<-B
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      'SPK0018'     MSGNR
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                     2<-E
     C                   SETON                                        27
     C                   ELSE                                                   1
     C     WSKNT         IFEQ      0                                             2<-B
     C                   MOVE      WSKNF         WSKNT
     C                   ELSE                                                    2
     C     WSKNT         IFLT      WSKNF                                          3<-B
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      'SPK0019'     MSGNR
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
      * TEST RPODUKT
     C     WSPROD        IFNE      '*'
     C                   MOVEL     WSPROD        KFKOD
     C     KUFASK        CHAIN     KUFAST                             94
     C     *IN94         IFEQ      '1'
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0360'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
      * TEST AVDELING
     C     WSAVDE        IFNE      *ZEROS
     C     KODTAK        CHAIN     KODTA                              93
     C     *IN93         IFEQ      '1'
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPA0001'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
     C*
     C*
     C* TEST FRA-STED (LAND+STED)
     C     WSLKS         IFNE      *BLANKS
     C     FRALAN        CHAIN     KODTLK                             97
     C     *IN97         IFEQ      '1'
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      'SPL0006'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
      *
N03  C                   IF        WSSDF <> *BLANKS
     C     STE2FK        CHAIN     STED2D                             92
     C     *IN92         IFEQ      '1'
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     21            POSNBR
     C                   MOVE      'SPS0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
N03  C                   END
     C                   END
     C*
     C* TEST TIL-STED (LAND+STED)
     C     WSLKT         IFNE      *BLANKS
     C     WSSDF         ORNE      *BLANKS
     C     TILLAN        CHAIN     KODTLK                             96
     C     *IN96         IFEQ      '1'
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      'SPL0006'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
      *
N03  C                   IF        WSSDT <> *BLANKS
     C     STE2TK        CHAIN     STED2D                             91
     C     *IN91         IFEQ      '1'
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     21            POSNBR
     C                   MOVE      'SPS0013'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
N03  C                   END
     C                   END
     C*
     C*** TEST DATO
     C                   MOVE      *ZEROS        WSDT8
     C     WSDT          IFNE      0
     C     WSVALG        ORNE      ' '
     C                   MOVE      WSDT          XMMÅÅ             4 0
     C                   MOVE      XMMÅÅ         UÅÅ               2 0
     C                   MOVEL     XMMÅÅ         UMM               2 0
     C                   MOVEL     WSDT          UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   MOVE      'SPD0001'     MSGNR
     C                   MOVE      '1'           *IN95
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
     C                   MOVE      XMMÅÅ         XDT               6 0
     C                   MOVEL     UÅÅ           XDT
     C                   MOVE      UDD           XDT
     C                   Z-ADD     XDT           WSDT8             8 0
     C                   END
      *
     C     WSDT8         IFNE      *ZEROS
     C     WSDT8         IFLT      500101
     C                   MOVEL     20            WSDT8
     C                   ELSE
     C                   MOVEL     19            WSDT8
     C                   END
     C                   END
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C                   MOVE      'DIV0782'     MSGNR
     C*
     C     WSKNF         SETLL     AVFRH
     C*
     C     STPR          TAG
     C                   READ      AVFRH                                  30
     C*
     C* TESTER KUNDENUMMER TIL
     C*
     C  N30AFKUND        COMP      WSKNT                              30
     C*
     C   30              GOTO      SLPR
     C*
     C* TESTER LANKKODE FRA
     C*
     C     WSLKS         IFNE      *BLANKS
     C     AFLKFR        ANDNE     WSLKS
S03  C*    AFPNF1        ANDLE     WSSDF
S03  C*    AFPNF2        ANDGE     WSSDF
     C                   GOTO      STPR
     C                   END
N03  C     WSLKS         IFNE      *BLANKS
N03  C     WSSDF         ANDNE     *BLANKS
N03  C     AFPNF1        IFLT      WSSDF
N03  C     AFPNF2        ORGT      WSSDF
N03  C                   GOTO      STPR
N03  C                   END
N03  C                   END
     C*
     C* TESTER LANKKODE TIL
     C*
     C     WSLKT         IFNE      *BLANKS
     C     AFLKTI        ANDNE     WSLKT
S03  C*    AFPNT1        ANDLE     WSSDF
S03  C*    AFPNT2        ANDGE     WSSDF
     C                   GOTO      STPR
     C                   END
N03  C     WSLKT         IFNE      *BLANKS
N03  C     WSSDT         ANDNE     *BLANKS
N03  C     AFPNT1        IFLT      WSSDT
N03  C     AFPNT2        ORGT      WSSDT
N03  C                   GOTO      STPR
N03  C                   END
N03  C                   END
     C*
     C* TESTER AVDELING
     C*
     C     WSAVDE        IFNE      *ZEROS
     C     AFAVDE        ANDNE     WSAVDE
     C                   GOTO      STPR
     C                   END
     C*
     C* TESTER PRODUKTKODE
     C*          WSPROD    IFNE *BLANKS
     C     WSPROD        IFNE      '*'
     C     AFPROD        ANDNE     WSPROD
     C                   GOTO      STPR
     C                   END
     C*
     C* TESTER DATO -
     C     WSVALG        IFNE      *BLANKS
      * KUN UTLØPTE
     C     WSVALG        IFEQ      'U'
     C     AFGJDT        IFEQ      *ZEROS
     C     AFGJDT        ORGE      WSDT8                                        GYLDIG AVT.
     C                   GOTO      STPR                                         ..IKKE SKRIV
     C                   END
     C                   ELSE
      * KUN GJELDENDE (WSVALG = G)
     C     AFAKDT        IFNE      *ZEROS
     C     AFAKDT        ANDGT     WSDT8                                        UGYLDIG(FRA)
     C                   GOTO      STPR                                         ..IKKE SKRIV
     C                   END
     C     AFGJDT        IFNE      *ZEROS
     C     AFGJDT        ANDLT     WSDT8                                        UGYLDIG(TIL)
     C                   GOTO      STPR                                         ..IKKE SKRIV
     C                   END
     C                   END
     C                   END
     C*
     C                   EXSR      PRT01
     C*
     C                   GOTO      STPR
     C*
     C     SLPR          ENDSR
     C*
     C***********************************************
     C     PRT01         BEGSR
     C***********************************************
     C                   MOVE      AFAKDD        PRDT1D
     C                   MOVE      AFAKDM        PRDT1M
     C                   MOVE      AFAKDÅ        PRDT1Å
     C                   MOVE      AFGJDD        PRDT2D
     C                   MOVE      AFGJDM        PRDT2M
     C                   MOVE      AFGJDÅ        PRDT2Å
     C*
     C                   ADD       1             ANTPLU            7 0
     C                   MOVE      'DIV0000'     MSGNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C                   MOVE      *ZEROS        XFFDT             6 0
     C                   MOVE      *BLANKS       BETTXT
     C  N99              DO
     C                   CALL      'SYFA20A'
     C                   PARM                    XFFDT
     C                   PARM                    BETTXT
     C                   PARM                    FIFIRM
     C                   PARM                    BETBET
     C                   END
     C*
     C   99              MOVE      XFEIL1        KNAVN
     C   99              MOVE      *BLANKS       ADR1
     C                   MOVE      *BLANKS       XADR3            30
     C  N99POSTNR        IFEQ      0
     C                   MOVEL     ADR3          XADR3
     C                   ELSE
     C                   MOVEL     POSTNR        XADR3
     C                   MOVE      ADR3          XADR3
     C                   END
     C*
     C*
     C     AFTYPE        COMP      ' '                                    51
     C     AFTYPE        COMP      'M'                                    52
     C     AFTYPE        COMP      'L'                                    53
     C     AFTYPE        COMP      'V'                                    54
     C*
     C     AFMIMA        COMP      '0'                                    40
     C  N40AFMIMA        COMP      ' '                                    40
     C     AFMIMA        COMP      '1'                                    41
     C     AFMIMA        COMP      '2'                                    42
     C*
     C     AFGEBY        COMP      *BLANKS                                43
     C  N43              MOVE      AFGEBY        GEBKOD
     C  N43GEBKEY        CHAIN     KODTGE                             33
     C  N43
     CAN 33              MOVE      *BLANKS       KGENOT
     C                   WRITE     HEADER
     C*
     C     AVFRDK        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    AFCDE1
     C                   MOVE      'H'           AFCDE1
     C*
     C     AFBFRA        IFEQ      'T'
     C                   MOVE      'H'           AFCDE1
     C                   ELSE
     C     AFBFR2        IFEQ      'T'
     C                   MOVE      'V'           AFCDE1
     C                   ELSE
     C     AFBFR1        IFEQ      'T'
     C                   MOVE      'F'           AFCDE1
     C                   END
     C                   END
     C                   END
     C*
     C                   MOVE      *ZEROS        XINTF
N02  C                   MOVE      *ZEROS        PLIN              5 0
     C*
     C     AVFRDK        SETLL     AVFRD
     C     STDET         TAG
     C     AVFRDK        READE     AVFRD                                  99
N02  C   99PLIN          IFEQ      0
N02  C     AFBFRA        ANDEQ     'P'
N02  C     AFPFRA        ANDNE     0
N02  C                   WRITE     DETALJP
N02  C                   END
     C  N99              DO
N02  C                   ADD       1             PLIN
     C*
     C* LASTEMETER KUBIKKAVTALER
     C*
     C   52
     COR 53              DO
     C                   MOVE      AFPRPR        XPRPR             4 2
     C                   MOVE      AFVEKT        AFVEKM            7 2
     C                   MOVE      XINTF         XINTFM            7 2
     C                   END
     C*
     C   54              Z-ADD     1             AFPRPR
     C     AFPRPR        COMP      *ZEROS                             55
     C                   WRITE     DETALJ
     C     AFVEKT        ADD       1             XINTF
     C                   GOTO      STDET
     C                   END
     C*
     C* TESTER INKLUDERTE GEBYRER
     C*
     C                   Z-ADD     0             ANTINK            2 0
     C     AFBFR1        IFEQ      'N'
     C                   MOVE      'FR1'         GEBKOD
     C     GEBKEY        CHAIN     KODTGE                             33
     C  N33              DO
     C                   WRITE     INKLUH1
     C                   WRITE     INKLUH2
     C                   WRITE     INKLUD
     C                   ADD       1             ANTINK
     C                   END
     C                   END
     C*
     C     1             DO        10            NR                3 0
     C     GE(NR)        IFNE      *BLANKS
     C                   MOVE      GE(NR)        GEBKOD
     C     GEBKEY        CHAIN     KODTGE                             33
     C  N33              DO
     C     ANTINK        IFEQ      *ZEROS
     C                   WRITE     INKLUH1
     C                   WRITE     INKLUH2
     C                   END
     C                   WRITE     INKLUD
     C                   ADD       1             ANTINK
     C                   END
     C                   END
     C                   END
     C*
     C     AFBFR2        IFEQ      'N'
     C                   MOVE      'FR2'         GEBKOD
     C     GEBKEY        CHAIN     KODTGE                             33
     C  N33              DO
     C     ANTINK        IFEQ      0
     C                   WRITE     INKLUH1
     C                   WRITE     INKLUH2
     C                   WRITE     INKLUD
     C                   ADD       1             ANTINK
     C                   END
     C                   END
     C                   END
     C*
     C                   WRITE     TOTLI1
     C                   WRITE     TOTLI2
     C                   ENDSR
     C*
     C*
S01  C***********************************************
S01  C*    SØKKUN        BEGSR
S01  C***********************************************
S01  C*                  CALL      'SOEKUN'
S01  C*                  PARM                    FIFIRM
S01  C*                  PARM                    SOANTK
S01  C*                  PARM                    SOTXT1
S01  C*                  PARM                    WSKNF
S01  C*                  PARM                    SOTXT2
S01  C*                  PARM                    WSKNT
S01  C*                  PARM                    SOTXT3
S01  C*                  PARM                    SOKNR3
S01  C*                  PARM                    SOTXT4
S01  C*                  PARM                    SOKNR4
S01  C*                  PARM                    SOTXT5
S01  C*                  PARM                    SOKNR5
S01  C*                  PARM                    SOTXT6
S01  C*                  PARM                    SOKNR6
S01  C*                  PARM                    SOTXT7
S01  C*                  PARM                    SOKNR7
S01  C*                  PARM                    SOTXT8
S01  C*                  PARM                    SOKNR8
S01  C*
S01  C*                  ENDSR
S01  C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     25
S01  C*                  EXSR      SØKKUN
N01  C                   MOVE      *BLANKS       UKNAVN           30
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNF
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    UKNAVN
E01  C                   Z-ADD     5             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     25
S01  C*                  EXSR      SØKKUN
N01  C                   MOVE      *BLANKS       UKNAVN           30
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNT
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    UKNAVN
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDEQ     18
     C                   MOVE      *BLANKS       KFKOD
     C                   MOVE      'N'           UUPDAT            1
     C                   CALL      'SYKUKD'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    KFKOD
     C                   MOVEL     KFKOD         WSPROD
     C                   Z-ADD     8             LINNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     21
     C                   CALL      'SYFA14B'
     C                   PARM                    WSAVDE
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      13
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     19
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLKS
     C                   PARM                    USPED
     C                   Z-ADD     21            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      13
     C     POSNBR        ANDGE     21
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSDF
     C                   PARM                    UNVN             30
     C                   PARM                    WSLKS
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      14
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     19
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLKT
     C                   PARM                    USPED
     C                   Z-ADD     21            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      14
     C     POSNBR        ANDGE     21
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSDT
     C                   PARM                    UNVN             30
     C                   PARM                    WSLKT
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C*
     C     *ENTRY        PLIST
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C     COPYA         PARM      COPYA         COPYA             2
     C                   MOVE      COPYA         COPY              2 0
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
     C     GEBKEY        KLIST
     C                   KFLD                    XGEUNI
     C                   KFLD                    GEBKOD            3
     C                   MOVE      'GE'          XGEUNI            2
     C     FRALAN        KLIST
     C                   KFLD                    XLKUNI
     C                   KFLD                    WSLKS
     C     TILLAN        KLIST
     C                   KFLD                    XLKUNI
     C                   KFLD                    WSLKT
     C                   MOVE      'LK '         XLKUNI            3
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    UAVD
     C     KODTWK        KLIST
     C                   KFLD                    YWUNI
     C                   KFLD                    UAVD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    AFKUND
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    UAVD
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WSAVDE
     C                   MOVE      'A'           KOAUNI
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'PRODTYPE'    KFTYP
     C     STE2FK        KLIST
     C                   KFLD                    WSLKS
     C                   KFLD                    WSSDF
     C     STE2TK        KLIST
     C                   KFLD                    WSLKT
     C                   KFLD                    WSSDT
     C*
     C                   MOVE      'J'           USPED             1
     C                   MOVE      'V'           YVUNI             1
     C                   MOVE      'W'           YWUNI             1
     C                   MOVE      *ZEROS        YKN               8 0
     C                   MOVE      '  '          YLKF              2
     C                   MOVE      '  '          YLKT              2
     C                   MOVE      '   '         YFR               3
     C                   MOVE      '  '          YOT               2
     C                   MOVE      ' '           YSK               1
     C                   MOVE      '     '       YSDF              5
     C                   MOVE      '     '       YSDT              5
     C                   Z-ADD     0             YAVTN             1 0
     C                   MOVE      *BLANKS       YNET              1
     C                   Z-ADD     0             WSDT
     C                   MOVE      'DIV0000'     MSGNR
     C*
S01  C*                  Z-ADD     2             SOANTK            1 0
S01  C*                  MOVE      *BLANKS       SOTXT3            8
S01  C*                  MOVE      *BLANKS       SOTXT4            8
S01  C*                  MOVE      *BLANKS       SOTXT5            8
S01  C*                  MOVE      *BLANKS       SOTXT6            8
S01  C*                  MOVE      *BLANKS       SOTXT7            8
S01  C*                  MOVE      *BLANKS       SOTXT8            8
S01  C*                  MOVE      *ZEROS        SOKNR3            8 0
S01  C*                  MOVE      *ZEROS        SOKNR4            8 0
S01  C*                  MOVE      *ZEROS        SOKNR5            8 0
S01  C*                  MOVE      *ZEROS        SOKNR6            8 0
S01  C*                  MOVE      *ZEROS        SOKNR7            8 0
S01  C*                  MOVE      *ZEROS        SOKNR8            8 0
S01  C*
     C                   EXSR      HMSG
     C*
     C                   READ      FIRM                                   99
     C*
     C     KODTVK        CHAIN     KODTV                              99
     C*
     C                   MOVE      *BLANKS       HO
     C     KODTWK        CHAIN     KODTW                              99
     C   99              MOVE      'E'           YNET
     C   99KODTWK        CHAIN     KODTW                              99
     C   99              MOVE      'T'           YNET
     C   99KODTWK        CHAIN     KODTW                              99
     C  N99KOWHOD        IFEQ      'J'
     C     HODEK         CHAIN     HODE                               99
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HMSG          BEGSR
     C***********************************************
S01  C*                  MOVE      'DIV0779'     XMSGNR            7            MELDING-ID
S01  C*                  EXSR      HMSG2
S01  C*                  MOVEL     XMSG          SOTXT1            8
S01  C*                  MOVE      'DIV0780'     XMSGNR
S01  C*                  EXSR      HMSG2
S01  C*                  MOVEL     XMSG          SOTXT2            8
     C                   MOVE      'SPK0020'     XMSGNR            7
     C                   EXSR      HMSG2
     C                   MOVEL     XMSG          XFEIL1           30
     C                   MOVE      'SPO0012'     XMSGNR
     C                   EXSR      HMSG2
     C                   MOVEL     XMSG          XFEIL3           29
     C                   MOVE      'SPS0019'     XMSGNR
     C                   MOVEL     XMSG          XFEIL4           15
     C                   MOVE      'DIV5041'     XMSGNR
     C                   EXSR      HMSG2
     C                   MOVEL     XMSG          XFEIL5           29
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HMSG2         BEGSR
     C***********************************************
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     132           XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG            132
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
