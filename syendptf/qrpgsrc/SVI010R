********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PKI sækerhetskoncept
10XXX * 02 CB  Les alle for status og signatur
11XXX * 03 CB  Testindikator
      *        Fjernet all gammel merking grunnet ny logikk PKI
     FSVI010D   CF   E             WORKSTN USROPN
     F                                     SFILE(SVI010DA:ZRECNO)
     FSVIH      UF   E           K DISK
S02  F*VIH2     IF   E           K DISK
N02  FSVIH12    IF   E           K DISK
     F                                     RENAME(SVIHR:SVIHR2)
     FSVTFI     IF   E             DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     D                UDS
     D  UAVD                  15     18  0
     D  USG                   19     21
      *   SYEDI data area
     D EDIDTA          DS           256
     D  UELIBF               171    180
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST

     D RcvDtaQ         PR                  EXTPGM('QRCVDTAQ')
     D  dqname                       10A   CONST
     D  dqLib                        10A   CONST
     D  dqLen                         5P 0
     D  dqData                       80A
     D  dqWait                        5P 0 CONST

     D DQLen           S              5P 0
     D TimeOut         S              5P 0
     D DQData          S             80A
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     c                   CLOSE     SVI010D
     c                   CALLP     RUNCMD('DLTOVR FILE(SVI010D)':200)
     c                   CALLP     RUNCMD('DLTDTAQ DTAQ(QTEMP/SVI010DQ)':200)
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN12         WIN12
     C     *LIKE         DEFINE    SVIH_SYST     ZVIH_SYST
     C                   EVAL      ZVIH_SYAV=UAVD
     C                   EVAL      ZVIH_SYSG=USG
     C                   EVAL      ZVIH_SYST='S'
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZVIH_SYST
S02  C*                  KFLD                    ZVIH_SYAV
     C                   KFLD                    ZVIH_SYSG
     C     KEYB          KLIST
     C                   KFLD                    SVIH_SYAV
     C                   KFLD                    SVIH_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
     C     1             CHAIN     SVTFI                              51
      * Åpne dataarea
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
      * Lage dataq i QTEMP
     c                   CALLP     RUNCMD('CRTDTAQ DTAQ(QTEMP/SVI010DQ) -
     c                             MAXLEN(80) SEQ(*FIFO)': 200)
      * Overstyr skjermbildet til å bruke dataq
     c                   CALLP     RUNCMD('OVRDSPF FILE(SVI010D) DTAQ(' +
     c                              'QTEMP/SVI010DQ)': 200)
      * Åpne skjermbildet
     c                   OPEN      SVI010D
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN92=*ON
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVI010DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
S02  C*    KEYA          SETLL     SVIH2
N02  C     KEYA          SETLL     SVIH12
S02  C*    KEYA          READE     SVIH2                                  94
N02  C     KEYA          READE     SVIH12                                 94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVI010DA
S02  C*    KEYA          READE     SVIH2                                  94
N02  C     KEYA          READE     SVIH12                                 94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVI010DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVI010DØ
     C                   WRITE     SVI010DC
     C                   WRITE     SVI010DB
      * Vent på en av to hendelser
      *    1) En post i dataq
      *    2) Et tidsavbrudd i vente på dataq
     c                   EVAL      DQLEN = 5
     c                   EVAL      TIMEOUT = 1800
     c*                  EVAL      TIMEOUT = 5
     c                   CALLP     RCVDTAQ('SVI010DQ':'QTEMP': DQLen: DQData:
     c                                 Timeout)
      * Vis resultatet  (hvis Len=0, ingen data mottat)
     c     DQLEN         IFEQ      0
     C                   EXFMT     SVI010DE
     C                   EVAL      *IN03 = *ON
     c                   ELSE
     c                   READ      SVI010DB                             9999
     c                   ENDIF
     C                   EVAL      *IN30=*OFF

     C                   EVAL      *IN30=*OFF

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVI010DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '4'           SRBD                           Återkalla
     C     ZOP           CASEQ     '5'           SRBE                           Visa edifact
     C     ZOP           CASEQ     'S'           SRBS                           Skicka till tullverk
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Återkalla                                           SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *_____________
      * Ændra status
      *"""""""""""""
     C     KEYB          CHAIN     SVIH                               52
     C                   EVAL      SVIH_SYST=' '
     C                   UPDATE    SVIHR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVI010DA
     C                   UPDATE    SVI010DA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Visa EDIFACT                                        SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      *BLANKS       WFILNAM         256
     C                   MOVE      SVIH_SYAV     WAVD              4
     C                   MOVE      SVIH_SYOP     WOPD              7
     C                   EVAL      WfilNam = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + 'SVIU'
     C                             + WAVD
     C                             + '-'
     C                             + WOPD
      * Vise fil
     C                   MOVE      *BLANKS       QCMDA           256
     C                   EVAL      QCMDA = 'DSPF STMF(' + X'7D'
     C                             + %trimr(WFILNAM)
     C                             + X'7D'
     C                             + ')'
     c                   CALLP     RUNCMD(QCMDA:256)
      *_____________
      * Ændra status
      *"""""""""""""
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVI010DA
     C                   UPDATE    SVI010DA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skicka till tullverket                              SRBS /
      *////////////////////////////////////////////////////////////
     C     SRBS          BEGSR
      *_________________________
      * Bekræfta genom egen bild.
      *"""""""""""""""""""""""""
     C                   EXFMT     SVI010DF
     C   12              GOTO      SRBS9
      *_____________
      * Ændra status
      *"""""""""""""
     C     KEYB          CHAIN     SVIH                               52
     C                   EVAL      SVIH_SYST='A'
     C                   UPDATE    SVIHR
      *_____________________________
      * Hæmta næsta meddelandenummer
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *_______________
      * Skriv til EDIM
      *"""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=SVTF_0004
     C                   EVAL      M0010=SVTF_0010
S03  C*                  EVAL      M0035=SVTF_0035
N03  C                   EVAL      M0035=SVIH_0035
     C                   MOVEL     SVIH_SYAV     W0062            11
     C                   MOVE      SVIH_SYOP     W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001=SVIH_MTYP
     C                   EVAL      M1004=SVIH_TUID
     C                   EVAL      M1225=SVIH_MTYP
     C                   EVAL      M1N07='SVI'
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=SVIH_SYAV
     C                   EVAL      MTDN=SVIH_SYOP

     C                   WRITE     EDIMR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBS9         TAG
     C                   CLEAR                   SVI010DA
     C                   UPDATE    SVI010DA
     C                   ENDSR
