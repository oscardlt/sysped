      *
********************************************************************
      * program som slår sammen PDF'er
      * RETTINGER I DETTE PROGRAM:
********************************************************************
      *
     Farkepol2  UF   E           K DISK
      *  Prototype for tvi40r3
     d tvi40r3         pr
     d uuser_                              like(uuser)
     d dokmerg                             like(dokmerg)
      *  *ENTRY Interface for Main Procedure
     d tvi40r3         pi
     d uuser                         10a
     d dokmerg                       40a
      *  Prototype for 'QCMDEXC'
     d QCMDEXC         pr                  extpgm('QCMDEXC')
     d CmdLine_                            like(CmdLine)
     d CmdLen_                             like(CmdLen)
      *
     d nei             s              4a
     d epn             s              3s 0
     d xpos            s              3s 0
     d epbane1         s            100a
     d utstreng        s          60000a
     d CmdLine         s          32000a
     d CmdLen          s             15  5
      *
     D KOMMSTR         C                   CONST('CONCATPDF -
     D                                     INPDF(')
     D KOMMEND         C                   CONST(')')
     D PAR1            C                   CONST('INPDF(')
     D PAR2            C                   CONST('OUTPDF(')
     D PAR3            C                   CONST('JDEBUG(')
      * Definere  hex appostrof
     D Ap              c                   x'7D'

      /free

       nei = '*NO';

       utstreng = 'CONCATPDF INPDF(';
       exsr lespdf;

       // HVIS FLERE DOKUMENT SLÅ SAMMEN

       if epn > 1;
         utstreng = %trim(utstreng) + ') OUTPDF(' + %trim(ap) + %trim(dokmerg)
                   + %trim(ap) +   ') JDEBUG(' + %trim(ap) + %trim(nei)
                   + %trim(ap) + ')';
         CmdLine = utstreng;
         CmdLen = %len(%trim(CmdLine));
         QCMDEXC (CmdLine: CmdLen);
         exsr opdpdf;
       endif;

       *inlr = *on;
       return;

       //***********************************************************************
       begsr lespdf;
       //***********************************************************************

       // skriver linker

       epn = 0;
       setll (uuser) arkepol2;
       reade (uuser) arkepol2;
       dow not %eof;
       // dette er reklamevedlegg
         if epmain <> 'V' and epbane <> *blanks;

       // utelater også ikke PDF'er
           xpos=%Scan('.pdf':epbane);
           if xpos = 0;
             xpos=%Scan('.PDF':epbane);
           endif;
           if xpos > 0;
             epn = epn + 1;
             if epn = 1;
               eval epbane1 = epbane;
             endif;
             if epn > 1;
               utstreng = %trim(utstreng) + '  ' + %trim(Ap);
             else;
               utstreng = %trim(utstreng) + %trim(Ap);
             endif;
             utstreng = %trim(utstreng) + %trim(epbane) + %trim(ap);
           endif;
         endif;
         reade (uuser) arkepol2;
       enddo;

       endsr;

       //***********************************************************************
       begsr opdpdf;
       //***********************************************************************

       // skriver linker

       epn = 0;

       setll (uuser) arkepol2;
       reade (uuser) arkepol2;
       dow not %eof;
       // start record lest bane ne blank
         if epmain <> 'V' and epbane <> *blanks and epbane <> dokmerg;
           xpos=%Scan('.pdf':epbane);
           if xpos = 0;
             xpos=%Scan('.PDF':epbane);
           endif;
           if xpos > 0;
             epn = epn + 1;
             if epbane1 = epbane;
               eptex2 = *blanks;
               epbane = '/' + %trim(dokmerg);
               update arkepor;
             else;
               if eptex1 = *blanks and eptex2 = *blanks;
                 delete arkepor;
               else;
                 eptex2 = *blanks;
                 epbane = *blanks;
                 eplink = *blanks;
                 epline = *blanks;
                 update arkepor;
               endif;
             endif;    // if xpos = 0;
       // end   record lest  bane ne blank
             if epn > 99;
               leave;
             endif;
           endif;
         endif;  // if epmain <> 'V' and epbane <> *blanks and epbane <> dokmerg;

         reade (uuser) arkepol2;
       enddo;

       endsr;

      /end-free
