      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNCE004R MODULE(*LIBL/TNCE004R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVXH      UF   E           K DISK    USROPN
     FSVXHL     IF   E           K DISK    USROPN
     FSVXV      IF   E           K DISK    USROPN
     FSVXVMN    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"oneorder" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   MOVE      *ZEROS        WSVPOS
     C                   MOVE      *ZEROS        WSVKB
     C                   MOVE      *ZEROS        WSNTK
     C                   MOVE      'N'           XENHETS           1            SPESIELL ENHET
     C     SvxhKey       SETLL     SVXV
     C     SvxhKey       READE     SVXV                                   33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             WSVPOS
     C                   ADD       TVVKTB        WSVKB
     C                   IF        TVNT = 0
     C                   ADD       TVNTEH        WSNTK
     C                   ELSE
     C                   ADD       TVNT          WSNTK
     C                   END
     C     TVEH          IFEQ      'VQ'
     C     TVEH          OREQ      'VG'
     C     TVEH          OREQ      'VL'
     C     TVEH          OREQ      'VY'
     C     TVEH          OREQ      'VR'
     C     TVEH          OREQ      'VO'
     C*                  ADD       1             WSNTK
     C                   MOVE      'J'           XENHETS
     C                   END
     C     SvxhKey       READE     SVXV                                   33
     C  N33              ENDDO
      *
     C     SvxhKey       SETLL     SVXVMN
     C     SvxhKey       READE     SVXVMN                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        TVNT = 0
     C                   ADD       TVNTEH        WSNTK
     C                   ELSE
     C                   ADD       TVNT          WSNTK
     C                   END
     C     TVEH          IFEQ      'VQ'
     C     TVEH          OREQ      'VG'
     C     TVEH          OREQ      'VL'
     C     TVEH          OREQ      'VY'
     C     TVEH          OREQ      'VR'
     C     TVEH          OREQ      'VO'
     C*                  ADD       1             WSNTK
     C                   MOVE      'J'           XENHETS
     C                   END
     C     SvxhKey       READE     SVXVMN                                 33
     C  N33              ENDDO
      *
     C                   IF        WSNTK = 0 AND XENHETS = 'J'
     C                   Z-ADD     1             WSNTK
     C                   END
      *
     C     THLSTL        IFNE      0
     C                   MOVE      *ZEROS        WSVPOS
     C     SvxhKey       SETLL     SVXHL
     C     SvxhKey       READE     SVXHL                                  33
     C     *IN33         DOWEQ     '0'
     C                   ADD       1             WSVPOS
     C     SvxhKey       READE     SVXHL                                  33
     C  N33              ENDDO
     C                   END
      *
     C     SvxhKey       CHAIN     SVXH                               55
     C                   MOVE      WSVPOS        THVPOS
     C                   MOVE      WSVKB         THVKB
     C                   MOVE      WSNTK         THNTK
     C                   UPDATE    SVXHR
      *
     C     *IN55         IFEQ      '0'
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬thavd¬ : ¬'+%trim(%editc(THAVD:'Z'))+'¬, '
          +'¬thtdn¬ : ¬'+%trim(%editc(THTDN:'Z'))+'¬, '
          +'¬thvpos¬ : ¬'+%trim(%editc(THVPOS:'Z'))+'¬, '
          +'¬thntk¬ : ¬'+%trim(%editc(THNTK:'Z'))+'¬, '
          +'¬thvkb¬ : ¬'+%trim(%editc(THVKB:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C                   ENDIF
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVXH
     C  N50              CLOSE     SVXHL
     C  N50              CLOSE     SVXV
     C  N50              CLOSE     SVXVMN

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   eval      OPDN    = c2n2(OPD)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SvxhKey       klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
      *
     C     *LIKE         DEFINE    THVPOS        WSVPOS
     C     *LIKE         DEFINE    THVKB         WSVKB
     C     *LIKE         DEFINE    THNTK         WSNTK
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVXH
     C  N50              OPEN      SVXHL
     C  N50              OPEN      SVXV
     C  N50              OPEN      SVXVMN

     c                   endsr
      *
