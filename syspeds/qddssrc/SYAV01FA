     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            WSNRO          1A  B  8  2DSPATR(HI)
     A                                      VALUES(' ' '6' '7' '8' '9' 'O')
     A            SFAVD          4Y 0O  8  4EDTCDE(4)
     A  42                                  DSPATR(HI)
     A  46                                  COLOR(TRQ)
     A  47                                  COLOR(YLW)
     A            SFOPD          7Y 0O  8  9EDTCDE(4)
     A  42                                  DSPATR(HI)
     A  46                                  COLOR(TRQ)
     A  47                                  COLOR(YLW)
     A            SFPOST         5A  O  8 17
     A            SFLI           5Y 0H
     A            SFNT           7Y 0O  8 23EDTCDE(4)
     A            SFVKT          7Y 0O  8 31EDTCDE(4)
     A            SFM3           6Y 3O  8 39EDTCDE(4)
     A            SFNAS          9A  O  8 47
     A            SFBELI         7Y 2O  8 57EDTCDE(M)
     A                                      DSPATR(HI)
     A            SFKDA          1A  O  8 67
     A            SFBELA         7Y 2B  8 69EDTCDE(M)
     A                                      DSPATR(HI)
     A  40
     AO 46                                  DSPATR(PR)
     A            SHKDPL         1A  H
     A            SFKDPL         1A  B  8 79DSPATR(HI)
     A  40
     AO 46                                  DSPATR(PR)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYAV01A'
     A                                      COLOR(BLU)
     A N40                              1 22'Endring av beløp for avregning'
     A                                      DSPATR(HI)
     A  40                              1 22'Spørring på detaljer, avregning'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Transportør:'
     A            TUKNT          8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  2 24COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2'Turnr......:'
     A            TUPRO          8Y 0O  3 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 27'Status.....:'
     A  44N45                           3 40'ÅPEN/IKKE KONTROLLERT'
     A                                      COLOR(WHT)
     A  44 45                           3 40'FERDIG/KONTROLLERT '
     A                                      COLOR(WHT)
     A N44                              3 40'**** AVREGNET ******'
     A                                      COLOR(RED)
     A N44        TUAVNR         7Y 0O  3 62COLOR(RED)
     A                                      EDTCDE(4)
     A                                  4  2'Bilnr......:'
     A            TUBILN        10A  O  4 15DSPATR(HI)
     A                                  4 27'Dato.......:'
EV5  A            TUDT           8Y 0H
NV5  A            WSDT           6Y 0O  4 40DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  5  3'Velg og trykk Enter'
     A                                      COLOR(BLU)
     A                                  6  3'6=Oppdrag'
     A                                      COLOR(BLU)
     A                                  6 14'7=Faktura'
     A                                      COLOR(BLU)
     A                                  6 25'8=Frie søkeveier'
     A                                      COLOR(BLU)
     A N40                              6 44'9=Sammenslåing'
     A N40                                  COLOR(BLU)
     A                                  6 60'INI         AVR'
     A                                      DSPATR(HI)
     A N43                              7  2'O --- Ref. --- Post     Ant.     K-
     A                                      g.    M3   Avsender     Beløp  A   -
     A                                       Beløp Pr'
     A                                      DSPATR(HI)
     A  43                              7  2'O --- Ref. --- Post     Ant.     K-
     A                                      g.    M3   Mottager     Beløp  A   -
     A                                       Beløp Pr'
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CA07(07 'Vis avs./mott.')
     A                                      CF12(12 'FORIGE BILDE')
     A N40                                  CF10(10 'FERDIGMELDE')
     A                                      CF03(03 'AVSLUTT')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1 47'Sum:'
     A            SMBELI         9Y 2O  1 54EDTCDE(M)
     A                                      DSPATR(HI)
     A            SMBELA         9Y 2O  1 67EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N43                              2 21MSGCON(018 YBC0572 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  43                              2 21MSGCON(018 YBC0573 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40N45                           2 40'F10=Ferdigmeld    '
     A                                      COLOR(BLU)
     A N40 45                           2 40'F10=Opphev f.meld'
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'FORIGE BILDE')
     A                                      CF12(12 'FORIGE BILDE')
     A N40                                  CF10(10 'FERDIGMELDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAV01A'
     A                                      COLOR(BLU)
     A N40                              1 22'Endring av beløp for avregning'
     A                                      DSPATR(HI)
     A  40                              1 22'Spørring på detaljer, avregning'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 65DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Transportør:'
     A            TUKNT          8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  2 24COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2'Turnr......:'
     A            TUPRO          8Y 0O  3 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 27'Status.....:'
     A  44N45                           3 40'ÅPEN/IKKE KONTROLLERT'
     A                                      COLOR(WHT)
     A  44 45                           3 40'FERDIG/KONTROLLERT '
     A                                      COLOR(WHT)
     A N44                              3 40'**** AVREGNET ******'
     A                                      COLOR(RED)
     A N44        TUAVNR         7Y 0O  3 62COLOR(RED)
     A                                      EDTCDE(4)
     A                                  4  2'Bilnr......:'
     A            TUBILN        10A  O  4 15DSPATR(HI)
     A                                  4 27'Dato.......:'
EV5  A            TUDT           8Y 0H
NV5  A            WSDT           6Y 0O  4 40DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  5  3'Velg og trykk Enter'
     A                                      COLOR(BLU)
     A                                  6  3'6=Oppdrag'
     A                                      COLOR(BLU)
     A                                  6 14'7=Faktura'
     A                                      COLOR(BLU)
     A                                  6 25'8=Frie søkeveier'
     A                                      COLOR(BLU)
     A                                  6 44'9=Sammenslåing'
     A                                      COLOR(BLU)
     A                                  6 60'INI         AVR'
     A                                      DSPATR(HI)
     A                                  7  2'O --- Ref. --- Post     Ant.     K-
     A                                      g. FB vekt Avsender     Beløp  A   -
     A                                       Beløp Pr'
     A                                      DSPATR(HI)
     A                                 22 47'Sum:'
     A            SMBELI         9Y 2O 22 54EDTCDE(M)
     A                                      DSPATR(HI)
     A            SMBELA         9Y 2O 22 67EDTCDE(M)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40N45                          23 40'F10=Ferdigmeld    '
     A                                      COLOR(BLU)
     A N40 45                          23 40'F10=Opphev f.meld'
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
