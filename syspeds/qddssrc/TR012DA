     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 72 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R TR012DA1
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF04(04 'Søking')
     A                                      CA12(12 'Gå til forrige bilde')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/TR012RA'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med forhåndsvarsling'
     A                                      COLOR(WHT)
     A                                  1 50'Ref.'
     A            UAVD           4Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A            ULI            5Y 0O  1 68COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Tr.bet.måte:'
     A            WSTKBM         1A  B  2 15DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 20'Farlig gods KD:'
     A            WSFGKD         4A  B  2 36TEXT('Farlig gods kode')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  3  2'K. ref:'
     A            WSKREF        60A  B  3 10DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  4  2'Avsender sikkerhet:'
     A            WSKNSS         8Y 0B  4 22TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINSS       17A  B  4 31TEXT('Tin kode selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNASS        30A  B  5  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADSS1       30A  B  6  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNSS         9A  B  7  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSSS        24A  B  7 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8  2'Landkode'
     A            WSLKSS         2A  B  8 12TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8 22'Språkkode'
     A            WSSKSS         2A  B  8 32TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Mottaker sikkerhet:'
     A            WSKNKS         8Y 0B  9 22TEXT('Kundenr kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINKS       17A  B  9 31TEXT('Tin kode kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNAKS        30A  B 10  2TEXT('Navn kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADKS1       30A  B 11  2TEXT('Adresse-1 kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNKS         9A  B 12  2TEXT('Postnr kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSKS        24A  B 12 12TEXT('Poststed kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 13  2'Landkode'
     A            WSLKKS         2A  B 13 12TEXT('Landkode kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13 22'Språkkode'
     A            WSSKKS         2A  B 13 32TEXT('Språkkode kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A N27                             14  2'F4=Kodesøking'
     A                                      COLOR(BLU)
     A N27                             14 22'F12=Gå til forrige u/oppdatering'
     A                                      COLOR(BLU)
     A  27                             14 44'F12=Gå til forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
