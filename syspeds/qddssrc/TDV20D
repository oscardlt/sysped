     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/HEADF HEADFR)
     A                                      PRINT
     A                                      HLPTITLE('General Help')
     A                                      HLPPNLGRP(TDV2010 TDV20PNL)
     A                                      ALTHELP
     A                                      HELP
     A*
     A          R TDV20B10
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Search Participant')
     A                                      CF10(10 'Send Request')
     A                                      CF08(08 'Search Sysped    customer')
     A                                      CA12(12 'Avbryt')
     A                                      OVERLAY
     A          H                           HLPPNLGRP(TDV2011 TDV20PNL)
     A                                      HLPARA(06 001 08 080)
     A          H                           HLPPNLGRP(TDV2012 TDV20PNL)
     A                                      HLPARA(09 001 11 080)
     A          H                           HLPPNLGRP(TDV2013 TDV20PNL)
     A                                      HLPARA(12 001 12 080)
     A          H                           HLPPNLGRP(TDV2014 TDV20PNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP(TDV2015 TDV20PNL)
     A                                      HLPARA(14 001 17 080)
     A          H                           HLPPNLGRP(TDV2016 TDV20PNL)
     A                                      HLPARA(18 001 20 080)
     A          H                           HLPPNLGRP(TDV2017 TDV20PNL)
     A                                      HLPARA(21 001 21 080)
     A          H                           HLPPNLGRP(TDV2010 TDV20PNL)
     A                                      HLPARA(01 001 22 080)
     A          H                           HLPPNLGRP(TDV2099 TDV20PNL)
     A                                      HLPARA(23 001 24 080)
     A            MSGNR          7A  P
     A                                  1  2'TDV20 '
     A                                  1 20'Agent to Agent Sending Information'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 33'Details'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                  4  2'Enter Id / Address and press F10 t-
     A                                      o send Document'
     A                                      COLOR(BLU)
     A                                  7  4'Identification'
     A                                      DSPATR(HI)
     A                                  7 23'Agent/Customer'
     A                                      DSPATR(HI)
     A            WIDENT        12A  O  8  4DSPATR(UL)
     A            WAGENT        30A  O  8 23DSPATR(UL)
     A                                 10  4'Type of Message'
     A                                      DSPATR(HI)
     A                                 10 23'No. of Pieces'
     A                                      DSPATR(HI)
     A                                 10 50'Weight'
     A                                      DSPATR(HI)
     A            WTYPE          8A  O 11  4DSPATR(UL)
     A            WENT      R        O 11 27REFFLD(HEADFR/HENT *LIBL/HEADF)
     A                                      EDTCDE(1)
     A                                      DSPATR(UL)
     A            WEVKT     R        O 11 45REFFLD(HEADFR/HEVKT *LIBL/HEADF)
     A                                      EDTCDE(1)
     A                                      DSPATR(UL)
     A                                 13  4'Id...............  Address .......-
     A                                      ................... Mode Name......-
     A                                      .......'
     A                                      DSPATR(HI)
     A            WID1          17A  B 14  4
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            WADR1         35A  B 14 23
     A  31                                  DSPATR(RI)
     A            WMODE1         1A  B 14 60
     A  31                                  DSPATR(RI)
     A            WNAME1        17A  O 14 63DSPATR(UL)
     A            WID2          17A  B 15  4
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            WADR2         35A  B 15 23
     A  32                                  DSPATR(RI)
     A            WMODE2         1A  B 15 60
     A  32                                  DSPATR(RI)
     A            WNAME2        17A  O 15 63DSPATR(UL)
     A            WID3          17A  B 16  4
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            WADR3         35A  B 16 23
     A  33                                  DSPATR(RI)
     A            WMODE3         1A  B 16 60
     A  33                                  DSPATR(RI)
     A            WNAME3        17A  O 16 63DSPATR(UL)
     A  44                             18  4'Several messages will be sent to t-
     A                                      hese addresses when pressing F10'
     A                                      COLOR(BLU)
     A                                 22  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 12'F8=Search Sysped    customer'
     A                                      COLOR(BLU)
     A                                 23 43'F9=Search Part.'
     A                                      COLOR(BLU)
     A                                 23 61'F10=Send Documents'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78   O 24  1MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R TDV20B20
     A                                      CF03(03 'Exit')
     A                                      CF10(10 'Send Request')
     A                                      OVERLAY
     A          H                           HLPPNLGRP(TDV2099 TDV20PNL)
     A                                      HLPARA(23 001 24 080)
     A            MSGNR          7A  P
     A                                 23  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 24'Enter=Send one by one'
     A                                      COLOR(BLU)
     A                                 23 57'F10=Send selected parts'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78   O 24  1MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R TDV20H50                  SFL
     A                                      SFLNXTCHG
     A            SFRRN         10S 0H
     A            RECNUM        10S 0H
     A            WTYPE          8A  H
     A            SFAVD          4  0H
     A            SFOPD          7  0H
     A            SFHAWB         9   H
     A            SFSD           3   H
     A            SFLNR          3  0H
     A            SFAWB         11   H
     A            WSNR           1A  B  7  4VALUES(' ' '1')
     A            WIDENT        12A  O  7  6
     A            WENT      R        O  7 19REFFLD(HEADFR/HENT *LIBL/HEADF)
     A                                      EDTCDE(4)
     A            WEVKT     R        O  7 27REFFLD(HEADFR/HEVKT *LIBL/HEADF)
     A                                      EDTCDE(4)
     A            WESDF     R        O  7 37REFFLD(HEADFR/HESDF *LIBL/HEADF)
     A            WESDT     R        O  7 43REFFLD(HEADFR/HESDT *LIBL/HEADF)
     A            WAGENT        30A  O  7 49
     A          R TDV20K60                  SFLCTL(TDV20H50)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      CF10(10 'Send all marked')
     A                                      OVERLAY
     A                                      PUTOVR
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(TDV2060 TDV20PNL)
     A                                      HLPARA(01 001 22 080)
     A          H                           HLPPNLGRP(TDV2099 TDV20PNL)
     A                                      HLPARA(23 001 24 080)
     A                                  1  3'TDV20'
     A                                  1 17'Selection of Master Airwaybill com-
     A                                      ponents'
     A                                      DSPATR(HI)
     A                                  1 61'Date:'
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 61'Time:'
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Select the wanted parts of the m-
     A                                      aster with 1 and press Enter or F10'
     A                                      COLOR(BLU)
     A                                  6  2'Opt Ident         Pieces    Weight-
     A                                       From  To    Agent/Customer .......-
     A                                      ........'
     A                                      DSPATR(HI)
