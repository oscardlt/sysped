      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KURERMANIFEST - LISTE     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADKMCL1  UF   E           K DISK
     FSADKMCL2  IF   E           K DISK    RENAME(SADKMCFR:SADKMFCR2)
     F                                     PREFIX(M2_)
     FSAD112D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  MCTRF                  1     70
     D  MCTRF_35               1     35
     D                SDS
     D  UUSR                 254    263
      *  Prototype for 'SAD005A'
     d SAD005A         pr                  extpgm('SAD005A')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD114R'
     d SAD114R         pr                  extpgm('SAD114R')
     d u_avd_                              like(u_avd)
     d upro_                               like(upro)
     d u_tdn_                              like(u_tdn)
     d u_cn_                               like(u_cn)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD113R'
     d SAD113R         pr                  extpgm('SAD113R')
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d uuuid_                              like(uuuid)
     d uflagg_                             like(uflagg)
      *  Prototype for SAD112R
     d sad112r         pr
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad112r         pi
     d uavd                           4  0
     d upro                           8  0
     d utype                          3a
     d uflagg                         1  0
      *
     d xtype           s              3a
     d xangre          s              1a
     d yrecno          s                   like(zrecno)
     d u_avd           s              4  0
     d u_tdn           s              7  0
     d u_cn            s              2  0
      *
      /free

         exsr init;

         *in93 = *on;
         write subctl;
         *in93 = *off;
         *in94 = *off;
         zrecno = 0;

         dow *in03 = *off;            // START                                   // 1<b

           setll upro sadkmcl1;
           reade(n) upro sadkmcl1;

           dow not %eof;                                                         // 2<b
             zrecno = zrecno + 1;
             write subfil;
             reade(n) upro sadkmcl1;
           enddo;                                                                // 2<e
           yrecno = zrecno;
           if zrecno > 19;                                                       // 2<b
             zrecno = 1;
           endif;                                                                // 2<e

           dow *in03 = *off;               // START3                             // 2<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                      // 3<b
               exfmt nyvare;
             else;                                                               // 3
               exfmt subctl;
             endif;                                                              // 3<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;
             select;                                                             // 3<b
             when *in03;                                                         // 3
               uflagg = 3;
               *inlr = *on;
               return;
             when *in05;                                                         // 3
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in06;                                                         // 3
               exsr lagopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in16;                                                         // 3
               exsr plkopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in12;                                                         // 3
               uflagg = 1;
               *inlr = *on;
               return;
             when zrecno <> 0;                                                   // 3
               exsr hent;
             endsl;                                                              // 3<e

           enddo;                                                                // 2<e

         enddo;                                                                  // 1<e

         *inlr = *on;
         return;

      //*********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in98 = *off;
       *in99 = *off;

       readc subfil;
       dow not %eof;
         if mcnak <> '* SLETTET *';
           select;
           when wsnr = 2 and mcst <> 'S' and *in49 = *off;
             *in99 = *off;
             exsr chgopd;
           when wsnr = 3 and mcst <> 'S' and mcavd = 0 and *in49 = *off;
             *in99 = *off;
             exsr cpyopd;
           when wsnr = 4 and mcst <> 'S' and *in49 = *off;
             *in99 = *off;
             exsr sltopd;
           when wsnr = 5;
             *in99 = *off;
             exsr dspopd;
           when wsnr = 9 and mcst = 'S' and *in49 = *off;
             *in99 = *off;
             exsr angslt;
           when wsnr = 15;
             *in99 = *off;
             exsr vissad;
           endsl;
         endif;

         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      //*********************************************
       begsr dspopd;

       xtype = 'DSP';            // SPØRRING
       u_avd = mcavd;
       u_tdn = mctdn;
       u_cn = mccn;
       sad114r (u_avd: upro: u_tdn: u_cn: xtype: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr vissad;

       u_avd = mcavd;
       u_tdn = mctdn;
       sad005a (u_avd: u_tdn: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr chgopd;

       xtype = 'CHG';            // ENDRING
       u_avd = mcavd;
       u_tdn = mctdn;
       u_cn = mccn;
       sad114r (u_avd: upro: u_tdn: u_cn: xtype: uflagg);

       chain(n) (upro: mcavd: mctdn: mccn) sadkmcl1;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr cpyopd;

       xtype = 'CPY';            // KOPIERING
       u_avd = mcavd;
       u_tdn = mctdn;
       u_cn = mccn;
       sad114r (u_avd: upro: u_tdn: u_cn: xtype: uflagg);

       wsnr = 0;
       update subfil;
       if u_cn <> mccn;
         mccn = u_cn;
         chain(n) (upro: mcavd: mctdn: mccn) sadkmcl1;
         yrecno = yrecno + 1;
         zrecno = yrecno;
         write subfil;
       endif;

       endsr;

      //*********************************************
       begsr sltopd;

       chain (upro: mcavd: mctdn: mccn) sadkmcl1;
       if mcst = ' ';
       // Ikke sendt, slettes.
         mcnak = '* SLETTET *';
         delete sadkmcfr;
       else;
       // Er sendt. Merke kansellert.
         mcst = 'S';
         update sadkmcfr;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr angslt;

       chain (mcavd: mctdn: mccn) sadkmcl2;
       xangre = 'J';
       dow not %eof;
         if m2_mcpro <> upro and (m2_mcst = ' ' or m2_mcst = 'C');
         // EN ANNEN TUR HAR PLUKKET DETTE OPPDRAGET.
           xangre = 'N';
           leave;
         endif;
         reade (mcavd: mctdn: mccn) sadkmcl2;
       enddo;

       if xangre = 'J';
         chain (upro: mcavd: mctdn: mccn) sadkmcl1;
         mcst = 'C';
         update sadkmcfr;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr plkopd;

       sad113r (uavd: upro: uuuid: uflagg);

       endsr;

      //*********************************************
       begsr lagopd;

       xtype = 'MAN';                // MMANUELL REGISTRERING
       u_avd = 0;
       u_tdn = 0;
       u_cn = 0;
       sad114r (u_avd: upro: u_tdn: u_cn: xtype: uflagg);

       endsr;

      //***********************************************
       begsr init;

       if utype = 'DSP';
         *in49 = *on;
       endif;

       endsr;

      /end-free
