      *
      ***************************************************************
      *** PROGRAM SKAL BEREGNE FRAKT (LINJEGODS)                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER        ***
      *** LEDIGE INDIKATORER 01-26                                ***
      *** LEDIGE INDIKATORER 27-32 34-99                          ***
      ***************************************************************
      *
     FFRTABLG   IF   E           K DISK
     FTRAN      IF   E           K DISK
     D                 DS
     D  LGPR                   1     90
     D  PR                     1     90P 0
     D                                     DIM(30)
     D                UDS
     D  UFIRM                  1      2
      *
     C     STLGK         KLIST
     C                   KFLD                    YPNF
     C                   KFLD                    YPNT
     C     TRANK         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    UKNT
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPNF              4 0
     C                   PARM                    UPNT              4 0
     C                   PARM                    UVKT              9 0
     C                   PARM                    ULM               7 3
     C                   PARM                    UM3               7 3
     C                   PARM                    UKNT              8 0
     C                   PARM                    UBL               9 0
     C                   PARM                    UVKT2             9 0
     C                   PARM                    UFLAGG            1 0
      *
     C                   Z-ADD     0             UFLAGG
     C                   MOVE      *ZEROS        YPNF              4
     C                   MOVE      '9999'        YPNT              4
      *
      *** Hente transportørsnr. til Linjegods
     C     STLGK         CHAIN     FRTABLG                            33
     C   33              Z-ADD     1             UFLAGG
     C   33              GOTO      SLUTT
      *
      *** Hente vektomregningsfaktorer
     C                   MOVEL     LGPR          UKNT
     C     TRANK         CHAIN     TRAN                               33
     C   33              MOVE      *ZEROS        VMLMOM
     C   33              MOVE      *ZEROS        VMM3OM
      *
      *** Omregne lastemeter/kubikkmeter til vekt
     C     ULM           MULT(H)   VMLMOM        XVKTLM            9 0
     C     UM3           MULT(H)   VMM3OM        XVKTM3            9 0
      *** Teste/velge den største vekten
     C                   SELECT
     C     UVKT          WHENGE    XVKTLM
     C     UVKT          ANDGE     XVKTM3
     C                   MOVE      UVKT          XVKT              9 0
     C     XVKTLM        WHENGE    XVKTM3
     C                   MOVE      XVKTLM        XVKT
     C                   OTHER
     C                   MOVE      XVKTM3        XVKT
     C                   ENDSL
     C                   Z-ADD     XVKT          UVKT2
      *
      *** Finne indeks (XN) til pristabell
     C                   SELECT
     C     XVKT          WHENLT    20
     C                   Z-ADD     4             XN                2 0
     C     XVKT          WHENLT    30
     C                   Z-ADD     5             XN
     C     XVKT          WHENLT    40
     C                   Z-ADD     6             XN
     C     XVKT          WHENLT    60
     C                   Z-ADD     7             XN
     C     XVKT          WHENLT    80
     C                   Z-ADD     8             XN
     C     XVKT          WHENLT    100
     C                   Z-ADD     9             XN
     C     XVKT          WHENLT    120
     C                   Z-ADD     10            XN
     C     XVKT          WHENLT    140
     C                   Z-ADD     11            XN
     C     XVKT          WHENLT    160
     C                   Z-ADD     12            XN
     C     XVKT          WHENLT    180
     C                   Z-ADD     13            XN
     C     XVKT          WHENLT    200
     C                   Z-ADD     14            XN
     C     XVKT          WHENLT    250
     C                   Z-ADD     15            XN
     C     XVKT          WHENLT    300
     C                   Z-ADD     16            XN
     C     XVKT          WHENLT    350
     C                   Z-ADD     17            XN
     C     XVKT          WHENLT    400
     C                   Z-ADD     18            XN
     C     XVKT          WHENLT    450
     C                   Z-ADD     19            XN
     C     XVKT          WHENLT    500
     C                   Z-ADD     20            XN
     C     XVKT          WHENLT    600
     C                   Z-ADD     21            XN
     C     XVKT          WHENLT    700
     C                   Z-ADD     22            XN
     C     XVKT          WHENLT    800
     C                   Z-ADD     23            XN
     C     XVKT          WHENLT    900
     C                   Z-ADD     24            XN
     C     XVKT          WHENLT    1000
     C                   Z-ADD     25            XN
     C                   OTHER
     C                   Z-ADD     26            XN
     C                   ENDSL
      *
      *** Hente pristabell
     C                   MOVE      UPNF          YPNF
     C                   MOVE      UPNT          YPNT
     C     STLGK         CHAIN     FRTABLG                            33
     C   33              Z-ADD     1             UFLAGG
     C   33              GOTO      SLUTT
      *** Beregne frakt
     C     XVKT          IFLT      1000
     C                   Z-ADD     PR(XN)        UBL
      *** Beregne frakt (vekt = 1000), samenligne med fraktene
      *** velge den minste frakt.
     C     10            MULT      PR(26)        XBL               9 0
     C     XBL           IFLT      UBL
     C                   MOVE      XBL           UBL
     C                   Z-ADD     1000          UVKT2
     C                   END
     C                   ELSE
     C     XVKT          ADD       99            XVKT
     C     XVKT          DIV       100           XVKT
     C     XVKT          MULT      PR(26)        UBL               9 0
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
