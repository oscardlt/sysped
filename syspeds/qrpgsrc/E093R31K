     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
     FEDISE1K   IF   E           K DISK
     FEDII      IF   E           K DISK
      *__________
      * Variabler
      *""""""""""
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Codepage        s              5a   Varying
      *___________
      * Konstanter
      *"""""""""""
     D LF              c                   x'25'
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
      *________________________________
      * Definiera variablar och nycklar
      *""""""""""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    W0010            35
     C                   PARM                    WSEMBR           10
     C                   PARM                    WFILNAM         256
     C     W0010         CHAIN     EDII
     C     INCODPAG      IFGT      '1'
     C     INCODPAG      ANDLE     '32767'
     C                   EVAL      Codepage = %TrimR(INCODPAG)
     C                   ELSE
     C                   EVAL      Codepage = '850'
     C                   ENDIF
      *_____________________________
      * Lag fil og set codepage (pc)
      *"""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w, codepage=' + Codepage )
     C*                            : 'w, codepage=850' )

     C                   EVAL      rc = close( FILE_o )
      *________________________________________
      * Åpne fil, med tegnkonvertering for jobb
      *""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w' )

     C                   IF        FILE_o <> *Null

     C                   EVAL      *IN51=*OFF
     C     WSEMBR        SETLL     EDISE1K
     C     WSEMBR        READE     EDISE1K                                51
     C     *IN51         DOWEQ     *OFF
     C                   IF        INENDLIN = '*LF'
     C                   EVAL      rc = fputs(SDATA + LF : File_o)
     C                   ELSE
     C                   EVAL      rc = fputs(SDATA  : File_o)
     C                   ENDIF
     C     WSEMBR        READE     EDISE1K                                51
     C                   ENDDO

     C                   EVAL      rc = close( FILE_o )
     C                   ENDIF

     C                   EVAL      *INLR = *On
     C                   RETURN
