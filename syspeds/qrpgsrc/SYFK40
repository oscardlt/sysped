********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 innført arkiv      .....
N02   * 02 Jov PTF10 skriv barkode for arkiv?? foreløpig hardkodet type ZO
N02   * 03 sh  PTF10 hele avsenderadresse er med
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SKRIVE UT KOPI KJØREKVITTERING/OPD.FORM.  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01-24                               ***
      *** LEDIGE INDIKATORER 28 29 31 32 35-45 47-49 51-76 78-   ***
      ***                    91-98                               ***
      *** IND. 99 SKAL ALLTID VÆRE PÅ (BETYR KOPI)               ***
      **************************************************************
      *
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FKODFRI    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FHODE      IF   E           K DISK
     FHODEFAX   IF   E           K DISK
     F                                     RENAME(HODER:HODERF)
     FFAK2      IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTH     IF   E           K DISK
     FFREETXT2  IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FBILLOF    IF   E           K DISK
     FBLV1LF    IF   E           K DISK
     FBLV2LF    IF   E           K DISK
     FTURER14   IF   E           K DISK
     FSYFK40FM  CF   E             WORKSTN USROPN
     FSYFK40PF  O    E             PRINTER OFLIND(*IN26)
     FSYFK40P2  O    E             PRINTER OFLIND(*IN25)
     D MN1             S             17    DIM(200)
     D MN2             S             51    DIM(200)
     D                 DS
     D  FS                     1     42
     D                                     DIM(2)
     D  FS1                    1     21
     D  FS2                   22     42
     D                 DS
     D  DFS                    1     10
     D                                     DIM(10)
     D  FSDOKK                 1     10
NRFA D                 DS
NRFA D  HERFA                  1     35
NRFA D  HERFA15                1     15
     D                 DS
     D  HEFT1                  1     51
     D  HEFT1A                 1     30
     D  HEFT1B                31     45
     D  HEFT1C                 1     30
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
     D                 DS
     D  XEDTD                  1      2  0
     D  XEDTM                  3      4  0
     D  XEDTÅ                  5      6  0
     D  XEDTOP                 1      6  0
     D                 DS
     D  WSOP1                  1      7  0
     D  WSOP2                  8     14  0
     D  WSOP3                 15     21  0
     D  WSOP4                 22     28  0
     D  WSOP5                 29     35  0
     D  WSOP6                 36     42  0
     D  WSOP7                 43     49  0
     D  WSOP8                 50     56  0
     D  WSOP9                 57     63  0
     D  WSOP10                64     70  0
     D  WSOP11                71     77  0
     D  WSOP12                78     84  0
     D  WSOP13                85     91  0
     D  WSOP14                92     98  0
     D  WSOP15                99    105  0
     D  WSOP16               106    112  0
     D  WSOP17               113    119  0
     D  WSOP18               120    126  0
     D  WSOP19               127    133  0
     D  WSOP20               134    140  0
     D  WSOP21               141    147  0
     D  WSOP22               148    154  0
     D  WSOP23               155    161  0
     D  WSOP24               162    168  0
     D  WSOP25               169    175  0
     D  WSOP26               176    182  0
     D  WSOP27               183    189  0
     D  WSOP28               190    196  0
     D  WSOP29               197    203  0
     D  WSOP30               204    210  0
     D  WSOP31               211    217  0
     D  WSOP32               218    224  0
     D  WSOP33               225    231  0
     D  WSOP34               232    238  0
     D  WSOP35               239    245  0
     D  WF                     1    245  0 DIM(35)
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UHOD                  47     47
     D  UBBS                  48     48
     D  UHOD2                 49     49
     D  UOPD                 101    107  0
     D  UGN                  981    995
     C*
     C                   EXSR      INIT
     C*
     C     UFAX          IFEQ      'J'
     C     UFAX          orEQ      'E'
     C                   MOVE      UOPD          WSOP1
     C                   EXSR      TESTB
     C                   EXSR      PRINT1
     C                   ELSE
     C                   OPEN      SYFK40FM
     C     START         TAG
     C                   EXFMT     BILDE
     C                   SETOFF                                       30
     C   03              CLOSE     SYFK40FM
     C   03              GOTO      SLUTT
     C                   EXSR      TESTB
     C   30              GOTO      START
     C*
     C                   EXSR      PRINT1
     C                   CLOSE     SYFK40FM
     C                   END
     C*
     C     SLUTT         TAG
     C                   MOVE      XPRANT        UPRANT
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     TESTB         BEGSR
     C******************************************
     C                   Z-ADD     1             XN                2 0
     C     XN            DOWLE     35
     C     WF(XN)        IFNE      0
     C                   MOVE      WF(XN)        YOPD
     C     HEADFK        CHAIN     HEADF                              30
     C   30              MOVEL     XN            MSGTXT
     C   30              MOVE      YOPD          MSGTXT
     C   30              GOTO      SLTB
     C                   END
     C                   ADD       1             XN
     C                   END
     C*
     C     SLTB          ENDSR
     C*
     C******************************************
     C     PRINT1        BEGSR
     C******************************************
     C                   Z-ADD     1             XN
     C     XN            DOWLE     35
     C     WF(XN)        IFNE      0
     C                   MOVE      WF(XN)        YOPD
     C                   MOVE      *BLANKS       KFMT1
     C                   MOVE      *BLANKS       KFMT2
     C     KOPFM         SETLL     KODTFM
     C     KOPFM         READE     KODTFM                                 98
     C     FLEREK        TAG
     C     HEADFK        CHAIN     HEADF                              33
     C  N33              EXSR      PRINT
     C                   END
     C  N98KOPFM         READE     KODTFM                                 98
     C  N98              GOTO      FLEREK
     C                   ADD       1             XN
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     PRINT         BEGSR
     C******************************************
      *
N02   * skriv barkode for arkiv?? foreløpig hardkodet type - sett type/Åpne *in55 ON så skrives den
N02  c                   movel     HEAVD         AVDOPD           11
N02  c                   move      HEOPD         AVDOPD
N02  c                   movel     'ZO'          SKANBAR
N02  c                   move      AVDOPD        SKANBAR
N02  c     KOPLAS        COMP      'F'                                    55
N02  c  N55KOPLAS        COMP      'P'                                    55
      *
     C     HEVS2         COMP      *BLANKS                            2727
     C  N27HEGM2         COMP      *BLANKS                            2727
     C     HEKNK         COMP      0                                      77
     C  N77KUND1K        CHAIN     CUNDL01                            77
     C  N77DKUND         COMP      'D'                                    77
      *
     C                   EXSR      KDTFMT
     C     BILLK         CHAIN     BILLOF                             29
      * fra web-oversjø KAN det bli skapt ufullstendige BL-s
     C  N29BLKNR         COMP      *zeros                                 29
     C     *IN29         IFEQ      '0'
     C                   MOVE      *ON           *IN29
     C                   EXSR      SEA
N01  C                   MOVE      'S'           UFAX
     C                   ELSE
     C                   MOVE      *OFF          *IN29
     C                   EXSR      NOSEA
     C                   END
      *
     C                   ADD       1             XPRANT
     C*
     C                   ENDSR
     C*
     C******************************************
     C     SEA           BEGSR
     C******************************************
     C                   EXSR      BÅTLIN
     C                   MOVE      '0'           *IN25
     C                   MOVE      *ZEROS        XSIDE
     C                   EXSR      UTHDS
     C  N33              EXSR      BLTXT
     C                   SETOFF                                       5034
     C                   MOVE      *ZEROS        XSUM             11 2
     C     FAK2K         SETLL     FAK2
     C     STSEA         TAG
     C     FAK2K         READE     FAK2                                   34
     C  N34KODGEK        CHAIN     KODTGE                             34
     C  N34KGEKK         IFEQ      'J'
     C     FAKDA         ANDNE     'K'
     C                   ADD       FABELN        XSUM
     C                   END
     C  N34              GOTO      STSEA
     C                   WRITE     SKJØPB
     C*
     C                   ENDSR
     C*
     C******************************************
     C     UTHDS         BEGSR
     C******************************************
     C     *IN25         IFEQ      '1'
     C                   WRITE     SKJØPNYS
     C                   ADD       1             XSIDE
     C                   MOVE      '0'           *IN25
     C                   END
     C     KFMT1         IFNE      *BLANKS
     C                   WRITE     SKJØPHH1
     C                   ELSE
     C     KFMT2         IFNE      *BLANKS
     C                   WRITE     SKJØPHH2
     C                   END
     C                   END
     C                   WRITE     SKJØP
     C*
     C                   ENDSR
     C*
     C******************************************
     C     BÅTLIN        BEGSR
     C******************************************
     C*
     C* HENTE OPPLYSNINGER FRA BÅT-REGISTRER
     C*
     C                   MOVE      *BLANKS       MN1
     C                   MOVE      *BLANKS       MN2
     C     KODFRK        CHAIN     KODTFR                             33
     C   33              MOVE      *BLANKS       KFRNTX
     C     BILLK         CHAIN     BILLOF                             33
     C   33              DO
     C                   MOVE      *BLANKS       XLBN
     C                   MOVE      *BLANKS       XVHN
     C                   MOVE      *BLANKS       XLSSN
     C                   MOVE      *BLANKS       XLOSN
     C                   MOVE      *BLANKS       XOBN
     C                   MOVE      *BLANKS       BLREIS
     C                   GOTO      SLBL
     C                   END
     C*
     C     BLOB          CHAIN     CDBOAT                             33
     C   33              MOVE      *BLANKS       XOBN
     C  N33              MOVEL     CBNAB         XOBN
     C     BLLB          CHAIN     CDBOAT                             33
     C   33              MOVE      *BLANKS       XLBN
     C  N33              MOVEL     CBNAB         XLBN
     C     BLLSS         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XLSSN
     C  N33              MOVEL     ST2NVN        XLSSN
     C     BLLOS         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XLOSN
     C  N33              MOVEL     ST2NVN        XLOSN
     C     BLVH          CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XVHN
     C  N33              MOVEL     ST2NVN        XVHN
     C*
     C                   SETOFF                                       33
     C*
     C     SLBL          ENDSR
     C*
     C******************************************
     C     BLTXT         BEGSR
     C******************************************
     C                   SETOFF                                       33
     C     BLV1K         SETLL     BLV1LF
     C*
     C     STBLT1        TAG
     C     BLV1K         READE     BLV1LF                                 33
     C  N33              DO
     C     BVVKT         COMP      0                                  8989
     C     BVM3          COMP      0                                  9090
     C   25              EXSR      UTHDS
     C                   WRITE     DETBL1
     C                   GOTO      STBLT1
     C                   END
     C*
     C  N25              WRITE     TOMLIN
     C     BLV2K         SETLL     BLV2LF
     C                   SETOFF                                       33
     C                   Z-ADD     0             N3                3 0
     C     STBLT2        TAG
     C     BLV2K         READE     BLV2LF                                 33
     C  N33BVMN          IFEQ      *BLANKS
     C                   GOTO      STBLT2
     C                   END
     C  N33              ADD       1             N3
     C  N33              MOVE      BVMN          MN1(N3)
     C  N33              GOTO      STBLT2
     C                   MOVE      N3            N4                3 0
     C*
     C     BLV2K         SETLL     BLV2LF
     C                   Z-ADD     0             GVLN              3 0
     C                   Z-ADD     0             GVTY              1 0
     C                   SETOFF                                       33
     C                   Z-ADD     0             N3
     C     STBLT3        TAG
     C     BLV2K         READE     BLV2LF                                 33
     C  N33BVKD          IFNE      'O'
     C     BVKD          ANDNE     'X'
     C     BVKD          ANDNE     'N'
     C                   GOTO      STBLT3
     C                   END
     C  N33              DO
     C     GVLN          COMP      0                                      88
     C   88GVTY          COMP      0                                      88
     C   88              MOVE      BVLI          GVLN
     C   88              MOVE      '1'           GVTY
     C     BVLI          COMP      GVLN                                   88
     C  N88GVTY          DOWLE     4
     C                   ADD       1             N3
     C                   ADD       1             GVTY
     C                   END
     C  N88              MOVE      BVLI          GVLN
     C  N88              MOVE      '1'           GVTY
     C     GVTY          DOWLT     BVLI2
     C                   ADD       1             N3
     C                   ADD       1             GVTY
     C                   END
     C                   ADD       1             GVTY
     C                   ADD       1             N3
     C                   MOVE      BVFT          MN2(N3)
     C                   GOTO      STBLT3
     C                   END
     C*
     C     STBLT4        TAG
     C     N3            IFGT      N4
     C                   MOVE      N3            N4
     C                   END
     C                   Z-ADD     1             N3
     C     N4            DOWGE     N3
     C                   MOVE      MN1(N3)       XMN1
     C                   MOVE      MN2(N3)       XMN2
     C   25              EXSR      UTHDS
     C                   WRITE     DETBL2
     C                   ADD       1             N3
     C                   END
      * MELLOM B.L. OG   MERKN....
      * FRIE SØKEVEIER (KUN DE SOM HAR DOK.KODE LIK K)
     C                   MOVE      *BLANKS       FRISTX                          FORIGE...
     C                   MOVEA     *BLANKS       FS                              2 STK
     C                   MOVE      *ZEROS        FN                1 0           INDEX
     C                   MOVE      'J'           FSLIN1            1             1.LINJE
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 91
     C     *IN91         DOWEQ     '0'
     C                   SORTA     DFS
     C                   MOVE      '0'           *IN34
     C     DFS(10)       IFNE      ' '
     C     'K'           LOOKUP    DFS                                    34
     C     *IN34         IFEQ      '1'
     C     FSKODE        CHAIN     KODFRI                             34
     C     *IN34         IFEQ      '0'
     C   25              EXSR      UTHDS
      * TEST LEST FRITEKST, SKRIV HVIS ULIK FORIGE KODE EL. 2  LEST.
     C                   EXSR      TSTFSL
     C                   END
     C                   END
     C                   END
      *
     C  N91HEADFK        READE     FRISOK                                 91
     C                   END
      * HVIS SISTE LESTE ENNÅ IKKE ER SKREVET-PRINT DEN NÅ (28=LEDETXT)
     C     FS(1)         IFNE      *BLANKS
     C     FSLIN1        IFEQ      'J'
     C                   MOVE      *ON           *IN28
     C                   END
     C                   WRITE     DETFS
     C                   END
      *
      * OG UTLEV.FORBEHOLD
     C     HEKFT1        IFEQ      *BLANKS
     C     HEFT1A        ANDNE     *BLANKS
     C   25              EXSR      UTHDS
     C                   WRITE     DETUFB
     C                   END
      *
      *
     C     FREETK        SETLL     FREETXT2                           91
     C     STSFT         TAG
     C     FREETK        READE     FREETXT2                               91
     C  N91
     CAN 25              EXSR      UTHDS
     C  N91              WRITE     DETFRT
     C  N91              GOTO      STSFT
     C*
     C     SLBLT         ENDSR
     C*
     C******************************************
     C     NOSEA         BEGSR
     C******************************************
     C                   SETOFF                                       88
     C  N77TLF           COMP      *BLANKS                            8888
     C*
     C  N77HEKNK         COMP      0                                      77
     C                   MOVE      '0'           *IN26
     C                   MOVE      *ZEROS        XSIDE
     C                   EXSR      UTHDL
      * MELLOM HEAD (--> KJØP2) OG MERKN.(KJØP3/4)....
      * FRIE SØKEVEIER (KUN DE SOM HAR DOK.KODE LIK K)
     C                   MOVE      *BLANKS       FRISTX                          FORIGE...
     C                   MOVEA     *BLANKS       FS                              2 STK
     C                   MOVE      *ZEROS        FN                1 0           INDEX
     C                   MOVE      'J'           FSLIN1            1             1.LINJE
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 91
     C     *IN91         DOWEQ     '0'
     C                   SORTA     DFS
     C                   MOVE      '0'           *IN34
     C     DFS(10)       IFNE      ' '
     C     'K'           LOOKUP    DFS                                    34
     C     *IN34         IFEQ      '1'
     C     FSKODE        CHAIN     KODFRI                             34
     C     *IN34         IFEQ      '0'
     C   26              EXSR      UTHDL
      * TEST LEST FRITEKST, SKRIV HVIS ULIK FORIGE KODE EL. 2  LEST.
     C                   EXSR      TSTFSL
     C                   END
     C                   END
     C                   END
      *
     C  N91HEADFK        READE     FRISOK                                 91
     C                   END
      * HVIS SISTE LESTE ENNÅ IKKE ER SKREVET-PRINT DEN NÅ (28=LEDETXT)
     C     FS(1)         IFNE      *BLANKS
     C     FSLIN1        IFEQ      'J'
     C                   MOVE      *ON           *IN28
     C                   END
     C                   WRITE     KJØPFS
     C                   END
      *
      * OG UTLEV.FORBEHOLD
     C     HEKFT1        IFEQ      *BLANKS
     C     HEFT1A        ANDNE     *BLANKS
     C   26              EXSR      UTHDL
     C                   WRITE     KJØPUFB
     C                   END
      *
      * ....
     C                   MOVE      'J'           TXLIN1            1             1.LINJE
     C     FREETK        SETLL     FREETXT2
     C     FRIST         TAG
     C     FREETK        READE     FREETXT2                               91
     C  N91TXLIN1        IFEQ      'J'
     C                   WRITE     KJØP3
     C                   MOVE      'N'           TXLIN1
     C                   END
     C  N91
     CAN 26              EXSR      UTHDL
     C  N91              WRITE     KJØP4
     C  N91              GOTO      FRIST
     C   26              EXSR      UTHDL
     C                   WRITE     KJØP5
     C                   EXSR      KUNFRA
     C                   WRITE     KJØPB
     C*
     C                   ENDSR
     C*
     C******************************************
     C     TSTFSL        BEGSR
     C******************************************
     C* INITIER LEDETEKST FØRSTE GANG
     C     FRISTX        IFEQ      *BLANKS
     C                   MOVE      KFSOTX        FRISTX
     C                   END
      * ANTALL SOM ER LEST SIDEN SISTE PRINT..
     C                   ADD       1             FN
      *
      * NY KODE ELLER MER EN 2 LEST,. SKRIV UT FOREGÅENDE POST
     C     FRISTX        IFNE      KFSOTX
     C     FN            ORGT      2
     C     FSLIN1        IFEQ      'J'
     C                   MOVE      *ON           *IN28
     C                   MOVE      'N'           FSLIN1
     C                   END
     C* 29 ON=SEA,   29 OFF=NOSEA
     C   29              WRITE     DETFS
     C  N29              WRITE     KJØPFS
     C* 28 ON = SKRIV LEDETEKST VED NESTE UTSKRIFT
     C     FRISTX        COMP      KFSOTX                             2828
     C                   MOVEA     *BLANKS       FS
     C                   Z-ADD     1             FN
     C                   END
     C*
     C                   MOVE      KFSOTX        FRISTX
     C                   MOVEL     FSSOK         FS(FN)
     C*
     C                   ENDSR
     C******************************************
     C     UTHDL         BEGSR
     C******************************************
     C     *IN26         IFEQ      '1'
     C                   WRITE     KJØPNYS
     C                   ADD       1             XSIDE
     C                   MOVE      '0'           *IN26
     C                   END
     C     KFMT1         IFNE      *BLANKS
     C                   WRITE     KJØPHH1
     C                   ELSE
     C     KFMT2         IFNE      *BLANKS
     C                   WRITE     KJØPHH2
     C                   END
     C                   END
      * TRANSPORTMODUL - HENT TRM. / LANDKODE MM FRA TUR
     C     HEUR          IFEQ      'H'
     C     HEPRO         ANDNE     *ZEROS
     C     HEPRO         CHAIN     TURER14                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      TULK          HETRI
     C                   MOVE      TUTRMA        HETRM
     C                   MOVE      TUCON1        HETRCN
     C                   MOVE      0             HETRC
     C     TUCON1        IFNE      *BLANKS
     C                   MOVE      1             HETRC
     C     TULKC1        IFNE      *BLANKS
     C                   MOVE      TULKC1        HETRI
     C                   END
     C                   END
     C                   END
     C                   END
     C                   WRITE     KJØP
     C                   WRITE     KJØP1
     C                   WRITE     KJØP2
     C*
     C                   ENDSR
     C*
     C******************************************
     C     KUNFRA        BEGSR
     C******************************************
     C*
     C* SUMMERER FRAKTBELØP PÅ KJØPER
     C*
     C                   SETOFF                                       5034
     C     FAK2K         SETLL     FAK2
     C     STKF          TAG
     C     FAK2K         READE     FAK2                                   34
     C  N34KODGEK        CHAIN     KODTGE                             34
     C  N34KGEKK         IFEQ      'J'
     C     FAKDA         ANDNE     'K'
     C  N50
     CANN26              WRITE     DLINJE
     C  N50              SETON                                        50
     C     FAVT          IFEQ      *BLANKS
     C                   MOVE      KGENOT        FAVT
     C                   END
     C   26              EXSR      UTHDL
     C                   WRITE     DEL
     C                   END
     C  N34              GOTO      STKF
     C   50
     CANN26              WRITE     DLINJE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    WSPLNR
     C                   MOVE      'P'           WSPUNI            1
     C                   Z-ADD     13            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C     UFAX          IFEQ      'J'
     C     UFAX          orEQ      'E'
     C                   MOVE      *BLANKS       KOPFM
     C                   END
     C*
     C     FREETK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXSKKO            1
     C                   MOVE      'K'           XXSKKO
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YOPD
     C     BILLK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     BLV1K         KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     BLV2K         KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YSK
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    HEKNK
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    UAVD
     C     KODGEK        KLIST
     C                   KFLD                    YGEUNI
     C                   KFLD                    FAVK
     C     KODTHK        KLIST
     C                   KFLD                    YHUNI
     C                   KFLD                    UAVD
     C     KODFRK        KLIST
     C                   KFLD                    YFRUNI
     C                   KFLD                    HEFR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UPRANT            2
     C                   PARM                    UFAX              1
     C                   MOVE      *ZEROS        XPRANT            2 0
     C*
     C                   SETON                                        99
n01  C     UFAX          IFEQ      'J'
n01  C     UFAX          orEQ      'E'
n01  C                   setoff                                       99
n01  C                   END
     C                   MOVE      *ZEROS        YOPD              7 0
     C                   MOVE      'K'           YSK               1
     C                   MOVE      ' '           YNET              1
     C                   MOVE      'GE'          YGEUNI            2
     C                   MOVE      'FR'          YFRUNI            2
     C                   MOVE      'H'           YHUNI             1
     C*
     C     KODTHK        CHAIN     KODTH                              33
     C     KOHKK         COMP      'J'                                    46
     C     UFAX          IFNE      'J'
     C     UFAX          andne     'E'
     C                   seton                                        87
     C   46HODEK         CHAIN     HODE                               33
     C                   ELSE
     C     ufax          ifeq      'J'
     C     HODEK         CHAIN     HODEFAX                            33
     C                   END
     C                   END
     C*
     C                   MOVE      *ZEROS        WF
     C*
     C                   MOVE      'DIV0196'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     78            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    MSGTXT           78
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      HEDTD         UDT1              2 0
     C                   MOVE      HEDTM         UDT2              2 0
     C                   MOVE      HEDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XEDTD
     C                   MOVE      UDT2          XEDTM
     C                   MOVE      UDT3          XEDTÅ
     C*
     C                   ENDSR
     C*
