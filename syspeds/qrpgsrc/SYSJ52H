      *
      *****************************************************************
      *** PROGRAM SKAL SPØRRE PÅ BILL OF LADING (HENTE SØKEVEI)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01 02 05-26                            ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-87                      ***
      *****************************************************************
      *
     FKODTA     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FBILLOFL7  IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYSJ52FH  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  XDTÅ                   1      2  0
     D  XDTM                   3      4  0
     D  XDTD                   5      6  0
     D  XDT                    1      6  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C  N30
     CANN04WSAVD         IFEQ      0
     C                   Z-ADD     3             LINNBR
     C                   ELSE
     C                   Z-ADD     7             LINNBR
     C                   END
     C  N30
     CANN04              Z-ADD     28            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C   03              GOTO      SLUTT
     C                   EXSR      KDTFMT
     C                   SETOFF                                       30
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVEA     '0000'        *IN(88)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TESTB
     C   30              GOTO      START
     C*
     C                   MOVE      WSDTD         XDTD
     C                   MOVE      WSDTM         XDTM
     C                   MOVE      WSDTÅ         XDTÅ
     C                   MOVE      XDT           UDT               6 0
     C  N27              Z-ADD     0             UFLAGG
     C   27              Z-ADD     1             UFLAGG
     C                   CALL      'SYSJ52K'
     C                   PARM                    WSAVD
     C                   PARM                    UDT
     C                   PARM                    WSNR
     C                   PARM                    WSBLNR
     C                   PARM                    WSREIS
     C                   PARM                    WSOB
     C                   PARM                    WSLB
     C                   PARM                    WSKNR
     C                   PARM                    WSÅR
     C                   PARM                    WSUKE
     C                   PARM                    WSUKET
     C                   PARM                    WSTR
     C                   PARM                    WSOPD
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      -3
     C                   MOVE      WSAVD         UAVD2
     C                   MOVE      WSOPD         UOPD2
     C                   ELSE
     C     UFLAGG        IFNE      3
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSAVD         IFNE      0
     C     KODTAK        CHAIN     KODTA                              99
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C                   END
     C*
     C     WSDT          IFNE      0
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      98
     C   98
     CANN30              MOVE      'SPD0001'     MSGNR
     C   98              SETON                                        30
     C                   END
     C*
     C     WSNR          COMP      0                                      97
     C  N97WSNR          COMP      8                                  97
     C   97
     CANN30              MOVE      'SPN0104'     MSGNR
     C   97              SETON                                        30
     C*
     C     WSNR          IFEQ      1
     C     WSBLNR        COMP      *BLANKS                                96
     C   96
     CANN30              MOVE      'SPB0105'     MSGNR
     C   96              SETON                                        30
     C                   END
     C*
     C     WSNR          IFEQ      2
     C     WSREIS        COMP      *BLANKS                                95
     C   95
     CANN30              MOVE      'SPR0101'     MSGNR
     C   95              SETON                                        30
     C                   END
     C*
     C     WSNR          IFEQ      3
     C     WSOB          CHAIN     CDBOAT                             94
     C   94
     CANN30              MOVE      'SPB0106'     MSGNR
     C   94              SETON                                        30
     C                   END
     C*
     C     WSNR          IFEQ      4
     C     WSLB          CHAIN     CDBOAT                             93
     C   93
     CANN30              MOVE      'SPB0102'     MSGNR
     C   93              SETON                                        30
     C                   END
     C*
     C     WSNR          IFEQ      5
     C     KUNDRK        CHAIN     CUNDL01                            92
     C   92
     CANN30              MOVE      'SPK0121'     MSGNR
     C   92              SETON                                        30
     C                   END
     C*
     C     WSUKE         COMP      53                                 91
     C   91
     CANN30              MOVE      'SPU0101'     MSGNR
     C   91              SETON                                        30
     C*
     C     WSUKET        IFEQ      0
     C                   MOVE      WSUKE         WSUKET
     C                   ELSE
     C     WSUKET        COMP      53                                 90
     C   90
     CANN30              MOVE      'SPU0101'     MSGNR
     C   90              SETON                                        30
     C                   END
     C*
     C  N91WSNR          IFEQ      6
     C     WSUKE         COMP      0                                      91
     C   91
     CANN30              MOVE      'SPU0101'     MSGNR
     C   91              SETON                                        30
     C                   END
     C*
     C     WSNR          IFEQ      7
     C     WSAVD         IFEQ      0
     C     WSTR          COMP      *BLANKS                                89
     C                   ELSE
     C     BILL7K        SETLL     BILLOFL7
     C     WSAVD         READE     BILLOFL7                               89
     C  N89WSTR          COMP      BLTR                               8989
     C                   END
     C   89
     CANN30              MOVE      'SPT0102'     MSGNR
     C   89              SETON                                        30
     C                   END
     C*
     C     WSNR          IFEQ      8
     C     WSOPD         COMP      0                                      88
     C   88
     CANN30              MOVE      'SPO0016'     MSGNR
     C   88              SETON                                        30
     C                   END
     C*
     C   30              DO
     C   88
     COR 89              Z-ADD     28            POSNBR
     C   90              Z-ADD     59            POSNBR
     C   91              Z-ADD     42            POSNBR
     C   92
     COR 93
     COR 94
     COR 95
     COR 96
     COR 97
     COR 98
     COR 99              Z-ADD     28            POSNBR
     C   88              Z-ADD     17            LINNBR
     C   89              Z-ADD     16            LINNBR
     C   90
     COR 91              Z-ADD     15            LINNBR
     C   92              Z-ADD     14            LINNBR
     C   93              Z-ADD     13            LINNBR
     C   94              Z-ADD     12            LINNBR
     C   95              Z-ADD     11            LINNBR
     C   96              Z-ADD     10            LINNBR
     C   97              Z-ADD     8             LINNBR
     C   98              Z-ADD     7             LINNBR
     C   99              Z-ADD     3             LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      14
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     35
     C                   EXSR      SØKKUN
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      16
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     29
     C                   CALL      'SYSJ17A'
     C                   PARM                    WSKNR
     C                   PARM                    WSTR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      13
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     32
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSLB
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     32
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSOB
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C     KUNDRK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNR
     C     BILL7K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSTR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD2             4 0
     C                   PARM                    UOPD2             7 0
     C                   PARM                    UFLAGG            1 0
     C*
     C     UFLAGG        COMP      1                                      27
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        XDT
     C                   MOVE      UYEAR         WSÅR
      *
     C     UAVD2         IFEQ      0
     C                   TESTN                   UAVD                 33
     C   33              MOVE      UAVD          WSAVD
     C  N33              MOVE      *ZEROS        WSAVD
     C                   ELSE
     C                   MOVE      UAVD2         WSAVD
     C                   END
     C*
     C                   SETOFF                                       33
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
