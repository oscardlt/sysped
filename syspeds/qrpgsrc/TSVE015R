      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE015R MODULE(*LIBL/TSVE015R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FEDIM2     IF   E           K DISK    USROPN
     FEDIM      IF   E           K DISK    USROPN
     F                                     RENAME(EDIMR:EDIMR0)
     FTVINF     IF   E           K DISK    USROPN
     FEDIS      IF   E           K DISK    USROPN
     FEDISX     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D TYP             S              3
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
     D WURL            S            512
     D WTXT            S             70
     D WMORE           S              1

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SYOP:'Z'))+'¬, '
        +'¬typ¬ : ¬'+%trim(TYP)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"logg" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C     KEYA          SETLL     EDIM2
     C     SRB2          TAG
     C     KEYA1         READE     EDIM2                                  55
     C     *IN55         DOWEQ     '0'
     C     M1N07         IFNE      TYP
     C                   GOTO      SRB2
     C                   ENDIF
     C                   EXSR      SRBB
     C                   EXSR      SRBC
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬mavd¬ : ¬'+%trim(%editc(MAVD:'Z'))+'¬, '
          +'¬mtdn¬ : ¬'+%trim(%editc(MTDN:'Z'))+'¬, '
          +'¬msn¬ : ¬'+%trim(%editc(MSN:'Z'))+'¬, '
          +'¬mmn¬ : ¬'+%trim(%editc(MMN:'Z'))+'¬, '
          +'¬msr¬ : ¬'+%trim(MSR)+'¬, '
          +'¬m0004¬ : ¬'+%trim(M0004)+'¬, '
          +'¬m0010¬ : ¬'+%trim(M0010)+'¬, '
          +'¬mven¬ : ¬'+%trim(MVEN)+'¬, '
          +'¬m0068¬ : ¬'+%trim(M0068)+'¬, '
          +'¬m0035¬ : ¬'+%trim(M0035)+'¬, '
          +'¬m0065¬ : ¬'+%trim(M0065)+'¬, '
          +'¬m1225¬ : ¬'+%trim(M1225)+'¬, '
          +'¬mdt¬ : ¬'+%trim(%editc(MDT:'Z'))+'¬, '
          +'¬mtm¬ : ¬'+%trim(%editc(MTM:'Z'))+'¬, '
          +'¬wtxt¬ : ¬'+%trim(WTXT)+'¬, '
          +'¬wmore¬ : ¬'+%trim(WMORE)+'¬, '
          +'¬wurl¬ : ¬'+%trim(WURL)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C     KEYA1         READE     EDIM2                                  55
     C                   ENDDO
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     EDIM2
     C  N50              CLOSE     EDIM
     C  N50              CLOSE     TVINF
     C  N50              CLOSE     EDIS
     C  N50              CLOSE     EDISX

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA = zhbgetvar('OPD')
     C                   EVAL      SYOP  = c2n2(SYOPA)
     C                   EVAL      TYP = zhbgetvar('TYP')
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    M0068A        W0068A
     C     *LIKE         DEFINE    M0068B        W0068B
     C     *LIKE         DEFINE    M0068C        W0068C
     C     *LIKE         DEFINE    FLN           WLN
     C                   MOVE      *ZEROS        W0068A
     C                   MOVE      *ZEROS        W0068B
     C                   MOVE      *ZEROS        W0068C
     C                   MOVE      *ZEROS        WLN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    SYAV
     C                   KFLD                    SYOP
     C                   KFLD                    W0068A
     C                   KFLD                    W0068B
     C                   KFLD                    W0068C
     C     KEYA1         KLIST
     C                   KFLD                    SYAV
     C                   KFLD                    SYOP
     C     KEYB          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    WLN
     C     KEYC          KLIST
     C                   KFLD                    MSN
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      EDIM2
     C  N50              OPEN      EDIM
     C  N50              OPEN      TVINF
     C  N50              OPEN      EDIS
     C  N50              OPEN      EDISX

     c                   endsr
      *
      *////////////////////////////////////////////////////////////
      *  Hent ev. feiltext                                   SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   EVAL      WTXT=*BLANKS
     C                   EVAL      WMORE=*BLANKS
     C                   EVAL      WLN=1
     C     KEYB          CHAIN     TVINF                              84
     C     *IN84         IFEQ      '0'
     C                   MOVEL     F0078B        WTXT             70
     C     KEYB          READE     TVINF                                  84
     C     *IN84         IFEQ      '0'
     C                   EVAL      WMORE = 'X'
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Lænk til edifact                                    SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *___________________
      * Hente sendningsref
      *"""""""""""""""""""
     C                   EVAL      WURL = *BLANKS
     C     KEYC          CHAIN     EDIS                               20
     C                   IF        SIFS <> *BLANKS
     C                   EVAL      WURL = SIFS
     C                   ELSE
     C     SMBR          CHAIN     EDISX                              20
     C  N20              EVAL      WURL = SXIFSOBJ
     C                   ENDIF
     C                   ENDSR
