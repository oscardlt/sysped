     FCUNDL01   IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FLLGRPK05  IF   E           K DISK
     FLL065D    CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSTYP1                 1      5
     D  WSTYP2                 6     10
     D  WSTYP3                11     15
     D  WSTYP4                16     20
     D  WSTYP5                21     25
     D  WSTYP6                26     30
     D  WSTYP7                31     35
     D  WSTYP8                36     40
     D  WSTYP9                41     45
     D  WSTYPA                46     50
     D  LT                     1     50
     D                                     DIM(10)
     D  XLT                    1     50
     D                UDS
     D  UFIRM                  1      2
      *
     C                   EXSR      INIT
      *
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C     START         TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVEA     '00000'       *IN(87)
     C                   MOVE      '0'           *IN30
     C   03              MOVE      'N'           UOK
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
      *
     C                   EXSR      TESTB
     C     *IN30         CABEQ     '1'           START
      *
     C                   MOVE      WSGRUP        UGRUP
     C                   MOVE      WSPNR         UPNR
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      'DIV0000'     MSGNR
      *
     C     WSPNR         CHAIN     LLGRPK05                           98
     C     *IN98         IFEQ      '1'
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'JOV0825'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   ELSE
     C                   MOVE      GKGRP         WSGRUP
     C                   END
      *
     C     WSGRUP        IFGT      0
     C     CUN1KG        CHAIN     CUNDL01                            99
     C     *IN99         IFEQ      '1'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'SPK0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
     C     CUN1KP        CHAIN     CUNDL01                            98
     C     *IN98         IFEQ      '1'
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'SPK0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
      *
     C     XLT           COMP      *BLANKS                                97
     C     *IN97         IFEQ      '1'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'YBC1723'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
      *
     C     1             DO        10            XN                3 0          1<-B
     C     LT(XN)        IFNE      *BLANKS                                       2<-B
     C                   MOVE      LT(XN)        KFKOD
     C     KUFASK        CHAIN     KUFAST                             30
     C     *IN30         IFEQ      '1'                                            3<-B
     C     98            SUB       XN            X2                3 0
     C                   MOVE      '1'           *IN(X2)
     C     XN            IFGT      5                                               4<-B
     C                   Z-ADD     11            LINNBR
     C                   ELSE                                                      4
     C                   Z-ADD     10            LINNBR
     C                   END                                                       4<-E
     C     XN            IFGT      5                                               4<-B
     C     XN            SUB       5             X2
     C                   ELSE                                                      4
     C                   Z-ADD     XN            X2
     C                   END                                                       4<-E
     C                   MULT      7             X2
     C     X2            ADD       23            POSNBR
     C                   MOVE      'YBC0865'     MSGNR
     C                   GOTO      SLTB
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSPRT         IFEQ      *BLANKS
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      '1'           *IN(87)
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0675'     MSGNR
     C                   GOTO      SLTB
     C                   END
      *
     C     SLTB          ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     30
     C     POSNBR        ANDLE     37
     C                   MOVE      'SØKGRP  '    UTYPE             8
     C                   CALL      'LL004R'
     C                   PARM                    UTYPE
     C                   PARM                    WSGRUP
     C                   PARM                    WSPNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     30
     C     POSNBR        ANDLE     37
     C                   MOVE      WSGRUP        UTYPE
     C                   CALL      'LL004R'
     C                   PARM                    UTYPE
     C                   PARM                    WSGRUP
     C                   PARM                    WSPNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     30            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFGE      10
     C     LINNBR        ANDLE     11
     C     POSNBR        ANDGE     30
     C     POSNBR        ANDLE     62
     C     POSNBR        SUB       23            XN
     C                   DIV       7             XN
     C     LINNBR        IFEQ      11
     C                   ADD       5             XN
     C                   END
     C                   MOVE      LT(XN)        KFKOD
     C                   CALL      'SYKUKD'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    KFKOD
     C                   MOVE      KFKOD         LT(XN)
     C     XN            IFEQ      10
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     30            POSNBR
     C                   ELSE
     C                   ADD       1             XN
     C     XN            IFGT      5
     C                   SUB       5             XN
     C                   Z-ADD     11            LINNBR
     C                   ELSE
     C                   Z-ADD     10            LINNBR
     C                   END
     C                   MULT      7             XN
     C     XN            ADD       23            POSNBR
     C                   END
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    WSPRT            10
     C                   PARM                    UOK               1
     C                   PARM                    UGRUP             8
     C                   PARM                    UPNR              8
     C                   PARM      XLT           ULT              50
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'LAGDETAL'    KFTYP
     C                   MOVE      'JT'          KFTYP
     C                   MOVE      'J'           UUPDAT            1
     C     CUN1KG        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSGRUP
     C     CUN1KP        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSPNR
      *
     C                   MOVE      'J'           UOK
     C                   MOVE      *BLANKS       LT
     C                   MOVE      'LAGOT'       LT(1)
      *
     C                   ENDSR
      *
