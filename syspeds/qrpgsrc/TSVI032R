      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVI032R MODULE(*LIBL/TSVI032R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVIF      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D SVIF_SYAV       S              4S 0
     D SVIF_SYOP       S              7S 0
     D SVIF_FATY       S              4
     D SVIF_FATX       S             17
     D SVIF_VAKD       S              3
     D SVIF_VAKU       S              7S 4
     D SVIF_FABL       S             11S 3

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SVIF_SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SVIF_SYOP:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(SVIF_FATX)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invoiceUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVIF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      SVIF_SYAV  = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      SVIF_SYOP  = c2n2(WA)
     C                   EVAL      SVIF_FATX  = zhbgetvar('FAK')
     C                   EVAL      SVIF_FATY  = zhbgetvar('SVIF_FATY')
     C                   EVAL      SVIF_VAKD  = zhbgetvar('SVIF_VAKD')
     C                   EVAL      WA      = zhbgetvar('SVIF_VAKU')
N01  C                   EVAL      SVIF_VAKU  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SVIF_FABL')
N01  C                   EVAL      SVIF_FABL  = c2n2(WA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVIFKEY       KLIST
     C                   KFLD                    SVIF_SYAV
     C                   KFLD                    SVIF_SYOP
     C                   KFLD                    SVIF_FATX
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVIF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     SVIFKEY       CHAIN     SVIF                               55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   SVIFR
     C                   EVAL      XSVIF_SYAV=SVIF_SYAV
     C                   EVAL      XSVIF_SYOP=SVIF_SYOP
     C                   EVAL      XSVIF_FATX=SVIF_FATX
     C                   WRITE     SVIFR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SVIFR
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XSVIF_FATY=SVIF_FATY
     C                   EVAL      XSVIF_VAKD=SVIF_VAKD
     C                   EVAL      XSVIF_VAKU=SVIF_VAKU
     C                   EVAL      XSVIF_FABL=SVIF_FABL
     C                   UPDATE    SVIFR
     C                   ENDSR
