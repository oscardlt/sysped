*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P1001A* 01 TH  PTF10 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A                                      HLPPNLGRP(HLPSCR SYFA68PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV AVDELINGER-
     A                                      S HODE PÅ FAKTURA')
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4')
     A            KOHAVD         4  0O  6  5TEXT('AVDELINGSNR')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOHFAK         1   O  6 11TEXT('FAKTURA SPEDISJON')
     A            KOHLAS         1   O  6 15TEXT('LASTELISTE')
     A            KOHGOD         1   O  6 19TEXT('GODSLISTE')
     A            KOHBOU         1   O  6 23TEXT('BORDEREAU')
     A            KOHKK          1   O  6 27TEXT('KJØREKVITERING')
     A            KOHLOS         1   O  6 31TEXT('LOSSELISTE')
     A            KOHMAN         1   O  6 35TEXT('MANIFEST')
     A            KOHLS1         1   O  6 42TEXT('LASTELISTE LIGGENDE/STÅENDE')
     A            KOH421         1   O  6 49TEXT('4224 PRINTER ?')
     A            KOHLS2         1   O  6 56TEXT('LOSSELISTE LIGGENDE/STÅENDE')
     A            KOH422         1   O  6 63TEXT('4224 PRINTER ?')
     A            KOHLS3         1   O  6 70TEXT('GODSLISTE LIGGENDE/STÅENDE')
     A            KOH423         1   O  6 77TEXT('4224 PRINTER ?')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA68'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1246 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV1247 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  5MSGCON(075 DIV1248 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSHAVD         4Y 0B  2  5DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      TEXT('AVDELINGSNR')
     A            WSHFAK         1A  B  2 11DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('FAKTURA SPEDISJON')
     A            WSHLAS         1A  B  2 15DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('LASTELISTE')
     A            WSHGOD         1A  B  2 19DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('GODSLISTE')
     A            WSHBOU         1A  B  2 23DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('BORDEREAU')
     A            WSHKK          1A  B  2 27DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('KJØREKVITERING')
     A            WSHLOS         1A  B  2 31DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('LOSSELISTE')
     A            WSHMAN         1A  B  2 35DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('MANIFEST')
     A            WSHLS1         1A  B  2 42DSPATR(UL)
     A                                      VALUES(' ' 'L' 'S')
     A                                      TEXT('LASTELISTE LIGGENDE/STÅENDE')
     A            WSH421         1A  B  2 49DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('4224 PRINTER ?')
     A            WSHLS2         1A  B  2 56DSPATR(UL)
     A                                      VALUES(' ' 'L' 'S')
     A                                      TEXT('LOSSELISTE LIGGENDE/STÅENDE')
     A            WSH422         1A  B  2 63DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('4224 PRINTER ?')
     A            WSHLS3         1A  B  2 70DSPATR(UL)
     A                                      VALUES(' ' 'L' 'S')
     A                                      TEXT('GODSLISTE LIGGENDE/STÅENDE')
     A            WSH423         1A  B  2 77DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      TEXT('4224 PRINTER ?')
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2HAVD         4Y 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(075 DIV1248 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KOHAVD         4Y 0O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                      TEXT('AVDELINGSNR')
     A            KOHFAK         1A  O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('FAKTURA SPEDISJON')
     A            KOHLAS         1A  O  2 15DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('LASTELISTE')
     A            KOHGOD         1A  O  2 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('GODSLISTE')
     A            KOHBOU         1A  O  2 23DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('BORDEREAU')
     A            KOHKK          1A  O  2 27DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('KJØREKVITERING')
     A            KOHLOS         1A  O  2 31DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('LOSSELISTE')
     A            KOHMAN         1A  O  2 35DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('MANIFEST')
     A            KOHLS1         1A  O  2 42DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('LASTELISTE LIGGENDE/STÅENDE')
     A            KOH421         1A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('4224 PRINTER ?')
     A            KOHLS2         1A  O  2 56DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('LOSSELISTE LIGGENDE/STÅENDE')
     A            KOH422         1A  O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('4224 PRINTER ?')
     A            KOHLS3         1A  O  2 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('GODSLISTE LIGGENDE/STÅENDE')
     A            KOH423         1A  O  2 77DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      TEXT('4224 PRINTER ?')
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA68'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1246 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV1247 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
