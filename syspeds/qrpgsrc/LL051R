      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE GRUPPE/KUNDE-AVTALER      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FLLKAFL01  UF A E           K DISK
     FLLTKF     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FLL051D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KADTUT                 1      8  0
     D  KADTUÅ                 1      4  0
     D  KADTUM                 5      6  0
     D  KADTUD                 7      8  0
     D                 DS
     D  XXDTUT                 1      6  0
     D  XXDTUD                 1      2  0
     D  XXDTUM                 3      4  0
     D  XXDTUÅ                 5      6  0
     D                 DS
     D  WSDTUT                 1      6  0
     D  WSDTUD                 1      2  0
     D  WSDTUM                 3      4  0
     D  WSDTUÅ                 5      6  0
     D                 DS
     D  XDTUT                  1      8  0
     D  XDTUÅ                  1      4  0
     D  XDTUM                  5      6  0
     D  XDTUD                  7      8  0
     D                 DS
     D  WSLTTX                 1     35
     D  WSLTT1                 1      4
     D  WSLTT2                 5     35
     D                 DS
     D  WSBMTX                 1     40
     D  WSBMT1                 1      4
     D  WSBMT2                 5     40
     D                 DS
     D  WSGETX                 1     24
     D  WSGET1                 1      4
     D  WSGET2                 5     24
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LKA1K1        SETLL     LLKAFL01
     C     LKA1K1        READE(N)  LLKAFL01                               94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      KADTUD        XXDTUD
     C                   MOVE      KADTUM        XXDTUM
     C                   MOVE      KADTUÅ        XXDTUÅ
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LKA1K1        READE(N)  LLKAFL01                               94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** FRISK OPP
     C   06              DO
     C                   EXSR      CRTKA
     C                   GOTO      START3
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     WSTYPE        IFEQ      'U'
     C     WSLPT         ANDEQ     'B10'
     C     WSDAG         IFEQ      0
     C                   Z-ADD     1             WSDAG
     C                   END
     C     WSMDAG        IFEQ      0
     C                   MOVE      WSDAG         WSMDAG
     C                   ELSE
     C     WSMDAG        IFGT      WSDAG
     C                   MOVE      WSMDAG        WSDAG
     C                   END
     C                   END
     C                   END
      *
     C     WSTYPE        IFEQ      'A'
     C     WSDAG         ANDEQ     0
     C                   Z-ADD     1             WSDAG
     C                   END
     C     WSTYPE        IFNE      'A'
     C                   MOVE      *BLANKS       WSLLT
     C     WSTYPE        IFNE      'U'
     C     WSLPT         ORNE      'B10'
     C                   MOVE      *ZEROS        WSMDAG
     C                   MOVE      *ZEROS        WSDAG
     C                   END
     C                   END
      *
     C     *IN40         IFEQ      '0'
      * VED NY REGISTRERING
     C                   MOVE      *BLANKS       WSLTTX
     C                   MOVE      *BLANKS       WSBMTX
      *
      * TEST TYPE FOR AVTALE
     C     WSTYPE        IFEQ      *BLANKS
     C                   MOVE      'YBC0776'     MSGNR
     C                   MOVE      '1'           *IN90
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSTYPE        IFEQ      'A'
     C                   IF        ((WSLPT >= 'J11') AND (WSLPT <= 'J11'))
     C                   MOVE      *BLANKS       WSLLT
     C                   ELSE
      * TEST KODE FOR LAGERLOKASJON
     C     WSLLT         CHAIN     LLTKF                              89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'YBC0775'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   GOTO      SLTB1
     C                   END
     C                   MOVEL     WSLLT         WSLTT1
     C                   MOVE      '='           WSLTT1
     C                   MOVEL     TKTXT         WSLTT2
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       WSLLT
     C                   END
      *
      * TEST KODE FOR PRISBEREGNINGSMÅTE
     C                   MOVEL     WSLPT         KFKOD
     C                   SELECT
     C     WSTYPE        WHENEQ    'I'
     C     KFASKI        CHAIN     KOFAST                             88
     C     WSTYPE        WHENEQ    'U'
     C     KFASKU        CHAIN     KOFAST                             88
     C     WSTYPE        WHENEQ    'A'
     C     KFASKA        CHAIN     KOFAST                             88
     C                   ENDSL
     C     *IN88         IFEQ      '1'
     C                   MOVE      'YBC0777'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     15            POSNBR
     C                   GOTO      SLTB1
     C                   END
     C                   MOVEL     WSLPT         WSBMT1
     C                   MOVE      '='           WSBMT1
     C                   MOVEL     KFTXT         WSBMT2
      *
     C                   END
      *
      * TEST OM GEBYRKODE
     C                   MOVE      *BLANKS       WSGETX
     C     KODGEK        CHAIN     KODTGE                             87
     C     *IN87         IFEQ      '1'
     C                   MOVE      'SPG0002'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     21            POSNBR
     C                   GOTO      SLTB1
     C                   END
     C                   MOVEL     WSGEB         WSGET1
     C                   MOVE      '='           WSGET1
     C                   MOVEL     KGENOT        WSGET2
      *
      * TEST OM PRIS ER OPPGITT.
     C     WSPR          IFEQ      0
     C                   MOVE      'YBC0778'     MSGNR
     C                   MOVE      '1'           *IN86
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   GOTO      SLTB1
     C                   END
      *
      * TEST OM PRIS PR DAG ELLER PR MÅNED
     C     WSTYPE        IFEQ      'A'
     C     WSMDAG        ANDEQ     0
     C                   MOVE      'YBC0782'     MSGNR
     C                   MOVE      '1'           *IN85
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     45            POSNBR
     C                   GOTO      SLTB1
     C                   END
      *
      * TEST MOMSKODE
     C     WSMVA         IFEQ      ' '
     C                   MOVE      'SPM0004'     MSGNR
     C                   MOVE      '1'           *IN84
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     61            POSNBR
     C                   GOTO      SLTB1
     C                   END
      *
      * TEST VALUTAKODE
     C     WSVAL         IFEQ      *BLANKS
     C                   MOVE      FICURR        WSVAL
     C                   ELSE
     C     VALU1K        CHAIN     VALUL01                            83
     C     *IN83         IFEQ      '1'
     C                   MOVE      'SPV0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     65            POSNBR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
      * TEST UTLØPSDATO
     C     WSDTUT        IFEQ      999999
     C                   MOVE      99999999      XDTUT
     C                   ELSE
     C                   MOVE      WSDTUD        UDD               2 0
     C                   MOVE      WSDTUM        UMM               2 0
     C                   MOVE      WSDTUÅ        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN82
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     72            POSNBR
     C                   MOVE      'SPD0001'     MSGNR
     C                   GOTO      SLTB1
     C                   END
     C                   MOVE      WSDTUD        XDTUD
     C                   MOVE      WSDTUM        XDTUM
     C     WSDTUÅ        ADD       2000          XDTUÅ
     C                   END
      *
      * TEST OM AVTALE FINNES FRA FØR
     C     *IN40         IFEQ      '1'
     C     XDTUT         CABEQ     GDTUT         STTB1
     C                   END
     C     LKA1K2        CHAIN(N)  LLKAFL01                           33
     C     *IN33         IFEQ      '0'
     C  N40              MOVE      '1'           *IN90
     C   40              MOVE      '1'           *IN85
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C  N40              Z-ADD     5             POSNBR
     C   40              Z-ADD     72            POSNBR
     C                   MOVE      'YBC0779'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     STTB1         TAG
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C   40LKA1K3        CHAIN     LLKAFL01                           33
     C                   EXSR      MOVWKA
      *
     C                   MOVE      KADTUD        XXDTUD
     C                   MOVE      KADTUM        XXDTUM
     C                   MOVE      KADTUÅ        XXDTUÅ
     C     *IN40         IFEQ      '1'
     C                   UPDATE    LLKAFR
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   WRITE     LLKAFR
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   END
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     KALLT         IFEQ      '*DL'
     C                   MOVE      '1'           *IN98
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C                   EXSR      CHGKA
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '3'
     C                   EXSR      CPYKA
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C                   EXSR      DLTKA
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       3             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C     SLH           ENDSR
      *
     C***********************************************
     C     CRTKA         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN40
     C                   Z-ADD     21            LINNBR
     C                   SELECT
     C     WSTYPE        WHENEQ    'I'
     C     WSTYPE        OREQ      'U'
     C                   Z-ADD     15            POSNBR
     C     WSTYPE        WHENEQ    'A'
     C                   Z-ADD     9             POSNBR
     C                   OTHER
     C                   Z-ADD     5             POSNBR
     C                   END
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVE      '0'           *IN82
     C                   MOVE      '0'           *IN30
      *
     C   12              EXSR      MOVB2
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCRT
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCRT
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGKA         BEGSR
     C***********************************************
     C                   EXSR      MOVKAW
     C                   MOVE      '1'           *IN40
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     21            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVE      '0'           *IN82
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYKA         BEGSR
     C***********************************************
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVKAW
     C                   MOVE      '0'           *IN40
     C                   Z-ADD     21            LINNBR
     C                   SELECT
     C     WSTYPE        WHENEQ    'I'
     C     WSTYPE        OREQ      'U'
     C                   Z-ADD     15            POSNBR
     C     WSTYPE        WHENEQ    'A'
     C                   Z-ADD     9             POSNBR
     C                   OTHER
     C                   Z-ADD     5             POSNBR
     C                   END
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVE      '0'           *IN82
     C                   MOVE      '0'           *IN30
      *
     C   12              EXSR      MOVB2
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCPY
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCPY
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTKA         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     LKA1K4        CHAIN     LLKAFL01                           33
     C  N33              DELETE    LLKAFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWKA        BEGSR
     C***********************************************
     C                   MOVE      UGRUP         KAGRUP
     C                   MOVE      UKJED         KAKJED
     C                   MOVE      WSTYPE        KATYPE
     C                   MOVE      WSLLT         KALLT
     C                   MOVE      WSLPT         KALPT
     C                   MOVE      WSGEB         KAGEB
     C                   MOVE      WSPR          KAPR
     C                   MOVE      WSPRM         KAPRM
     C                   MOVE      WSMDAG        KAMDAG
     C                   MOVE      WSDAG         KADAG
     C                   MOVE      WSMVA         KAMVA
     C                   MOVE      WSVAL         KAVAL
     C                   MOVE      XDTUT         KADTUT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVKAW        BEGSR
     C***********************************************
     C                   MOVE      KATYPE        WSTYPE
     C                   MOVE      KALLT         WSLLT
     C                   MOVE      KALPT         WSLPT
     C                   MOVE      KAGEB         WSGEB
     C                   MOVE      KAPR          WSPR
     C                   MOVE      KAPRM         WSPRM
     C                   MOVE      KAMDAG        WSMDAG
     C                   MOVE      KADAG         WSDAG
     C                   MOVE      KAMVA         WSMVA
     C                   MOVE      KAVAL         WSVAL
     C                   MOVE      XXDTUT        WSDTUT
     C                   MOVE      KADTUT        GDTUT             8 0
      *Hent tekst for Lokasj.type/beregningsmåte/varekode
     C                   MOVE      *BLANKS       WSLTTX
     C     WSTYPE        IFEQ      'A'
     C     WSLLT         CHAIN     LLTKF                              89
     C  N89              MOVEL     WSLLT         WSLTT1
     C  N89              MOVE      '='           WSLTT1
     C  N89              MOVEL     TKTXT         WSLTT2
     C                   END
     C                   MOVEL     KALPT         KFKOD
     C                   SELECT
     C     WSTYPE        WHENEQ    'I'
     C     KFASKI        CHAIN     KOFAST                             88
     C     WSTYPE        WHENEQ    'U'
     C     KFASKU        CHAIN     KOFAST                             88
     C     WSTYPE        WHENEQ    'A'
     C     KFASKA        CHAIN     KOFAST                             88
     C                   ENDSL
     C  N88              MOVEL     WSLPT         WSBMT1
     C  N88              MOVE      '='           WSBMT1
     C  N88              MOVEL     KFTXT         WSBMT2
     C     KODGEK        CHAIN     KODTGE                             87
     C  N87              MOVEL     WSGEB         WSGET1
     C  N87              MOVE      '='           WSGET1
     C  N87              MOVEL     KGENOT        WSGET2
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     21            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KATYPE
     C                   MOVE      *BLANKS       KALLT
     C                   MOVE      '*DL'         KALLT
     C                   MOVE      *BLANKS       KALPT
     C                   MOVE      *BLANKS       KAGEB
     C                   MOVE      *ZEROS        KAPR
     C                   MOVE      *ZEROS        KAPRM
     C                   MOVE      *ZEROS        KAMDAG
     C                   MOVE      *ZEROS        KADAG
     C                   MOVE      *BLANKS       KAMVA
     C                   MOVE      *BLANKS       KAVAL
     C                   MOVE      *ZEROS        KADTUT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSLLT
     C                   MOVE      *BLANKS       WSLPT
     C                   MOVE      *BLANKS       WSGEB
     C                   MOVE      *ZEROS        WSPR
     C                   MOVE      *ZEROS        WSPRM
     C                   MOVE      *BLANKS       WSLTTX
     C                   MOVE      *BLANKS       WSBMTX
     C                   MOVE      *BLANKS       WSGETX
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     9             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C  N40LINNBR        IFEQ      21
     C     POSNBR        ANDGE     9
     C     POSNBR        ANDLE     11
     C                   CALL      'LL008R'
     C                   PARM                    WSLLT
     C                   Z-ADD     15            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C  N40LINNBR        IFEQ      21
     C     POSNBR        ANDEQ     15
     C                   MOVE      *BLANKS       UTYPK            10
     C                   MOVEL     WSLPT         UKOD              5
     C                   SELECT
     C     WSTYPE        WHENEQ    'I'
     C                   MOVEL     'LAGERMOT'    UTYPK
     C     WSTYPE        WHENEQ    'U'
     C                   MOVEL     'LAGERUTT'    UTYPK
     C                   MOVE      'AK'          UTYPK
     C     WSTYPE        WHENEQ    'A'
     C                   MOVEL     'LAGERLEI'    UTYPK
     C                   MOVE      'ER'          UTYPK
     C                   OTHER
     C                   MOVE      'JOV1153'     MSGNR
     C                   SETON                                        30
     C                   GOTO      SLSØK
     C                   ENDSL
     C                   CALL      'SYFAKDA'
     C                   PARM                    UTYPK
     C                   PARM                    UUPDAT
     C                   PARM                    UKOD
     C                   MOVEL     UKOD          WSLPT
     C                   Z-ADD     21            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     21
     C     POSNBR        ANDLE     23
     C                   CALL      'SYFI10D'
     C                   PARM                    WSGEB
     C                   Z-ADD     27            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     65
     C     POSNBR        ANDLE     70
     C                   MOVE      'J'           USØK              1
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    WSVAL
     C                   Z-ADD     72            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     SLSØK0        TAG
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LKA1K1        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C     LKA1K2        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    WSTYPE
     C                   KFLD                    WSLLT
     C                   KFLD                    WSLPT
     C                   KFLD                    XDTUT
     C     LKA1K3        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    WSTYPE
     C                   KFLD                    WSLLT
     C                   KFLD                    WSLPT
     C                   KFLD                    GDTUT
     C     LKA1K4        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    KATYPE
     C                   KFLD                    KALLT
     C                   KFLD                    KALPT
     C                   KFLD                    KADTUT
     C     KFASKI        KLIST
     C                   KFLD                    KFTYPI           10
     C                   KFLD                    KFKOD
     C                   MOVEL     'LAGERMOT'    KFTYPI
     C     KFASKU        KLIST
     C                   KFLD                    KFTYPU           10
     C                   KFLD                    KFKOD
     C                   MOVEL     'LAGERUTT'    KFTYPU
     C                   MOVE      'AK'          KFTYPU
     C     KFASKA        KLIST
     C                   KFLD                    KFTYPA           10
     C                   KFLD                    KFKOD
     C                   MOVEL     'LAGERLEI'    KFTYPA
     C                   MOVE      'ER'          KFTYPA
     C     KODGEK        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    WSGEB
     C                   MOVE      'GE'          KGEUNI
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSVAL
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRUP
     C                   PARM                    UGNV
     C                   PARM                    UKJED
     C                   PARM                    UPNV
      *
     C                   MOVE      ' '           UUPDAT            1
     C                   MOVE      *ZEROS        WSDTUT
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
