     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF12(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYFA67B'
     A                                      COLOR(BLU)
     A                                  1 12'Endring KUN av gebyr m/%-sats (VAL-
     A                                      /OLJ mm) '
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  4  5'ENDRINGER KJØRES KUN FOR AVTALELIN-
     A                                      JER MED..'
     A                                      COLOR(WHT)
     A                                  6  5'Gebyrkode..:'
     A            WSGEB          3A  B  6 25
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A  85                                  ERRMSG('Gebyrkode ugyldig')
     A                                  8  5'Gammel'
     A                                  8 12'sats:'
S01  A*           WSGMSA         4Y 2B  8 25EDTCDE(M)
     A            WSGMSA         5Y 3B  8 25EDTCDE(M)
     A  86                                  DSPATR(RI)
     A  86                                  DSPATR(PC)
     A  86                                  ERRMSG('Gammel sats kan ikke være 0-
     A                                      ')
     A                                 10  5'Avtaletype.:   '
     A            WSAVTY         1A  B 10 25
     A  87                                  DSPATR(RI)
     A  87                                  DSPATR(PC)
     A  87                                  ERRMSG('Ugyldig avtaletype')
     A                                 17 33'( 0,001 regnes som null)          -
     A                                              '
     A                                 11 33'( B = Kun Brutto/Hvem belastes hva-
     A                                       )      '
     A                                 12 33'( Blank = Alle linjer )           -
     A                                              '
     A                                 15  5'Linjer som tilfredsstiller kriteri-
     A                                      ene over endres til..'
     A                                      COLOR(WHT)
     A                                 17  5'Ny sat'
     A                                 17 12'sats:'
S01  A*           WSNYSA         4Y 2B 17 25EDTCDE(M)
     A            WSNYSA         5Y 3B 17 25EDTCDE(M)
     A  88                                  DSPATR(RI)
     A  88                                  DSPATR(PC)
     A  88                                  ERRMSG('Ny sats kan ikke være 0')
     A  50        ANTLIN         5Y 0O 20  5EDTCDE(4)
     A                                      COLOR(WHT)
     A  50                             20 11'linjer ble endret i siste kjøring'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Avslutt   Enter = Kjør endring'
     A                                      COLOR(BLU)
     A                                  8 33'( 0,001 regnes som null)          -
     A                                              '
     A                                 10 33'( K = Kun Kundeavtaler )'
