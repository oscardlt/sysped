      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD130R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. manifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  4 13MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 26TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 32TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20MSGCON(035 DIV1901 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 20'Turnummer:'
     A            WSPRO          8Y 0B  8 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 20'UUID:'
     A            WSUUID        36A  B  9 35TEXT('Registreringsdato')
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 20'ETA:'
     A            WSETA          6Y 0B 10 35TEXT('Passerkingsdato')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Reg.dato:'
     A            WSDTR          6Y 0B 11 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Status:'
     A            WSST           1A  B 12 35TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 26 20'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            EFAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            EFPRO          8Y 0O  6 10TEXT('Turnr')
     A                                      EDTCDE(4)
     A  41                                  COLOR(TRQ)
     A            EFSG           3A  O  6 19TEXT('Signatur')
     A            EFST           1A  O  6 23TEXT('Status')
     A            EFTSD          4Y 0O  6 26TEXT('Passerings tollsted')
     A                                      EDTCDE(4)
     A            EF3039E        6Y 0O  6 31TEXT('Ekspedisjonsted')
     A                                      EDTCDE(4)
     A            EFST2          1A  O  6 38TEXT('Status passering')
     A            EFETA          8Y 0H
     A            X_EFETA        6Y 0O  6 41TEXT('Passeringsdato ETA')
     A  43                                  COLOR(TRQ)
     A                                      EDTWRD('  .  .  ')
     A            EFETM          6Y 0O  6 50TEXT('Passeringsdato ETA')
     A                                      EDTWRD('0 .  .  ')
     A            X_EFATA        6Y 0O  6 59TEXT('Passert ATA')
     A                                      EDTWRD('  .  .  ')
     A            EFATM          6Y 0O  6 68TEXT('Passert tid')
     A                                      EDTWRD('0 .  .  ')
     A            EFKMRK        30A  H
     A            EFKMRK_10     10A  O  6 77TEXT('Bilnr')
     A            EFSJAF        30A  O  6 88TEXT('Sjåfør')
     A            X_EFDTR        6Y 0O  6122TEXT('Reg.dato')
     A                                      EDTWRD('  .  .  ')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD130R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. manifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Se manifest 9=EDI-logg 25=T&T 90=Se-
     A                                      nd 99=Kanseller'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Turnr.   Sig St T.sd E.enh-
     A                                      . St Passering ETA/tid Passert ATA/-
     A                                      tid   Bilnr      Sjåførs fornavn   -
     A                                                      Reg.Dato'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2PRO          8Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2ST           1A  B  5 23DSPATR(UL)
     A            W2ETA          6Y 0B  5 41DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2DTR          6Y 0B  5122DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R BUNN
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD110R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Se manifest 9=EDI-logg 25=T&T 90=Se-
     A                                      nd 99=Kanseller'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Turnr.   Sig St T.sd E.enh-
     A                                      . St Passering ETA/tid Passert ATA/-
     A                                      tid   Bilnr      Sjåførs fornavn   -
     A                                                      Reg.Dato'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2PRO          8Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2ST           1A  B  5 23DSPATR(UL)
     A            W2ETA          6Y 0B  5 41DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2DTR          6Y 0B  5122DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R ERRMSG
     A                                      WINDOW(*DFT 4 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Standardmelding er ikke definert.'
     A                                      COLOR(RED)
     A                                  2  2'Start pkt. 5 på meny EDIMNU3.'
     A                                      COLOR(RED)
     A                                  3  2'Meldingstype = EXPREM'
     A                                      COLOR(YLW)
     A                                  4  2'Meldingsutgavenr = 001'
     A                                      COLOR(YLW)
      *
     A          R SENDTUR
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'Status for sending'
     A                                      COLOR(WHT)
     A                                  2  2'Status:'
     A            WSST2          1A  B  2 10DSPATR(UL)
     A                                      VALUES(' ' 'S' 'R' 'D')
     A  51                              2 13'S=Submitted'
     A N51                              2 13'R=Reopened'
     A N51                              3 13'D=Deleted'
     A                                  4  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R ERRSND
     A                                      WINDOW(*DFT 3 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Feil: Minst et oppdrag mangler opp-
     A                                      lysning'
     A                                      COLOR(RED)
     A                                  2  2'på kravfelt eller mangler oppdrag.'
     A                                      COLOR(RED)
     A                                  3  2'Vennligst sjekk alle oppdragene.'
     A                                      COLOR(YLW)
      *
     A          R COMPTUR
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Sett manifest avslutt'
     A                                      COLOR(WHT)
     A                                  2  2'Turnr.:'
     A            EFPRO          8Y 0O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  3  2'Sett manifest avsluttet?'
     A            WSJN           1A  B  3 27DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
