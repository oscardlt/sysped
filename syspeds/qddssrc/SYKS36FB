     A*%%TS  SD  20160211  142546  JOVO        REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            CMSEID        35   H
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            ZZSEID        20A  O  6  7TEXT('AVS-ID')
     A                                      COLOR(WHT)
     A            CMKUNR         8  0O  6 28TEXT('KUNDE')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            CMLEV          8  0O  6 37TEXT('LEVER')
     A                                      EDTCDE(4)
     A            ZZLNAV        20A  O  6 46TEXT('NAVN')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYKS36B'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikeh. parter en MOTTAR INTERNE-
     A                                      DI FAKTURA FRA'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Option EDI-ID avsender    Vårt knr-
     A                                         Lev.nr Navn                     -
     A                                              '
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20160211  142546  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(11)
     A                                      CLRL(14)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Vedlikehold parter som en mottar E-
     A                                      DI-SYSPED FAKTURA fra              -
     A                                             '
     A                                      COLOR(WHT)
     A                                  2  2'Avs. EDI-ID:'
     A            WSSEID        35A  B  2 15TEXT('AVS-ID')
     A  89                                  DSPATR(RI)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A                                  2 55'VÅRT knr hos avs:'
     A            WSKUNR         8Y 0B  2 73TEXT('KUNDE')
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A                                  3  2'Leverandør.:'
     A            WSLEV     R        B  3 15REFFLD(CMFAMOR/CMLEV *LIBL/CUIFAMO)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            LNAVN         30A  O  3 24COLOR(WHT)
     A  88                                  DSPATR(RI)
     A                                  3 55'Bet.bet:'
     A            WSBETB    R        B  3 64REFFLD(CMFAMOR/CMBETB *LIBL/CUIFAMO)
     A                                      DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  4  2'Kunde-resk.:'
     A            WSKRES    R        B  4 15REFFLD(CMFAMOR/CMKRES *LIBL/CUIFAMO)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  4 24COLOR(WHT)
     A  87                                  DSPATR(RI)
     A                                  4 55'(Til KUNDE-resk. når utf.)'
     A                                  5  2'Bilagserie.:'
     A            WSBISE    R        B  5 15REFFLD(CMFAMOR/CMBISE *LIBL/CUIFAMO)
     A                                  5 38'Automatisk ferdigmelding:'
     A            WSAUTF    R        B  5 64REFFLD(CMFAMOR/CMAUTF *LIBL/CUIFAMO)
     A                                  6  2'Printkode..:'
     A            WSPRKO    R        B  6 15REFFLD(CMFAMOR/CMPRKO *LIBL/CUIFAMO)
     A                                  6 17'(0=Manglende match, mm., 1=Også ma-
     A                                      nglende geb-konvert.)        '
     A                                  7  2'E-mail.....:'
     A            WSMAIL    R        B  7 15REFFLD(CMFAMOR/CMMAIL *LIBL/CUIFAMO)
     A                                  8 15'(Når utfylt sendes mail istedenfor-
     A                                       print) ENNÅ IKKE FERDIG!!'
     A                                  9  2'Gebyrkonv.'
     A                                  9 13'FRA:'
     A            WSVK1F    R        B  9 18REFFLD(CMFAMOR/CMVK1F *LIBL/CUIFAMO)
     A            WSVK2F    R        B  9 22REFFLD(CMFAMOR/CMVK2F *LIBL/CUIFAMO)
     A            WSVK3F    R        B  9 26REFFLD(CMFAMOR/CMVK3F *LIBL/CUIFAMO)
     A            WSVK4F    R        B  9 30REFFLD(CMFAMOR/CMVK4F *LIBL/CUIFAMO)
     A            WSVK5F    R        B  9 34REFFLD(CMFAMOR/CMVK5F *LIBL/CUIFAMO)
     A            WSVK6F    R        B  9 38REFFLD(CMFAMOR/CMVK6F *LIBL/CUIFAMO)
     A            WSVK7F    R        B  9 42REFFLD(CMFAMOR/CMVK7F *LIBL/CUIFAMO)
     A            WSVK8F    R        B  9 46REFFLD(CMFAMOR/CMVK8F *LIBL/CUIFAMO)
     A            WSVK9F    R        B  9 50REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A            WSVK10F   R        B  9 54REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A            WSVK11F   R        B  9 58REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A            WSVK12F   R        B  9 62REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A            WSVK13F   R        B  9 66REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A            WSVK14F   R        B  9 70REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A            WSVK15F   R        B  9 74REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A                                 10 13'TIL:'
     A            WSVK1T    R        B 10 18REFFLD(CMFAMOR/CMVK1T *LIBL/CUIFAMO)
     A            WSVK2T    R        B 10 22REFFLD(CMFAMOR/CMVK2T *LIBL/CUIFAMO)
     A            WSVK3T    R        B 10 26REFFLD(CMFAMOR/CMVK3T *LIBL/CUIFAMO)
     A            WSVK4T    R        B 10 30REFFLD(CMFAMOR/CMVK4T *LIBL/CUIFAMO)
     A            WSVK5T    R        B 10 34REFFLD(CMFAMOR/CMVK5T *LIBL/CUIFAMO)
     A            WSVK6T    R        B 10 38REFFLD(CMFAMOR/CMVK6T *LIBL/CUIFAMO)
     A            WSVK7T    R        B 10 42REFFLD(CMFAMOR/CMVK7T *LIBL/CUIFAMO)
     A            WSVK8T    R        B 10 46REFFLD(CMFAMOR/CMVK8T *LIBL/CUIFAMO)
     A            WSVK9T    R        B 10 50REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A            WSVK10T   R        B 10 54REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A            WSVK11T   R        B 10 58REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A            WSVK12T   R        B 10 62REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A            WSVK13T   R        B 10 66REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A            WSVK14T   R        B 10 70REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A            WSVK15T   R        B 10 74REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A                                 11  2'Standard'
     A                                 11 11'gebyr:'
     A            WSVK      R        B 11 18REFFLD(CMFAMOR/CMVK *LIBL/CUIFAMO)
     A  86                                  DSPATR(RI)
     A                                 13  4'F4=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             14 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(11)
     A                                      CLRL(14)
     A                                      OVERLAY
     A                                  1  2'SLETTE !!!  part   som en mottar E-
     A                                      DI-SYSPED FAKTURA fra              -
     A                                             '
     A                                      COLOR(WHT)
     A                                  2  2'Avs. EDI-ID:'
     A            WSSEID        35A  O  2 15TEXT('AVS-ID')
     A  40                                  DSPATR(HI)
     A                                  2 55'VÅRT knr hos avs:'
     A            WSKUNR         8Y 0O  2 73TEXT('KUNDE')
     A  40                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  3  2'Leverandør.:'
     A            WSLEV     R        O  3 15REFFLD(CMFAMOR/CMLEV *LIBL/CUIFAMO)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            LNAVN         30A  O  3 24COLOR(WHT)
     A                                  3 55'Bet.bet:'
     A            WSBETB    R        O  3 64REFFLD(CMFAMOR/CMBETB *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                  4  2'Kunde-resk.:'
     A            WSKRES    R        O  4 15REFFLD(CMFAMOR/CMKRES *LIBL/CUIFAMO)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  4 24COLOR(WHT)
     A                                  4 55'(Til KUNDE-resk. når utf.)'
     A                                  5  2'Bilagserie.:'
     A            WSBISE    R        O  5 15REFFLD(CMFAMOR/CMBISE *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                  5 38'Automatisk ferdigmelding:'
     A            WSAUTF    R        O  5 64REFFLD(CMFAMOR/CMAUTF *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                  6  2'Printkode..:'
     A            WSPRKO    R        O  6 15REFFLD(CMFAMOR/CMPRKO *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                  6 17'(0=Manglende match, mm., 1=Også ma-
     A                                      nglende geb-konvert.)        '
     A                                  7  2'E-mail.....:'
     A            WSMAIL    R        O  7 15REFFLD(CMFAMOR/CMMAIL *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                  8 15'(Når utfylt sendes mail istedenfor-
     A                                       print) ENNÅ IKKE FERDIG!!'
     A                                  9  2'Gebyrkonv.'
     A                                  9 13'FRA:'
     A            WSVK1F    R        O  9 18REFFLD(CMFAMOR/CMVK1F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK2F    R        O  9 22REFFLD(CMFAMOR/CMVK2F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK3F    R        O  9 26REFFLD(CMFAMOR/CMVK3F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK4F    R        O  9 30REFFLD(CMFAMOR/CMVK4F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK5F    R        O  9 34REFFLD(CMFAMOR/CMVK5F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK6F    R        O  9 38REFFLD(CMFAMOR/CMVK6F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK7F    R        O  9 42REFFLD(CMFAMOR/CMVK7F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK8F    R        O  9 46REFFLD(CMFAMOR/CMVK8F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK9F    R        O  9 50REFFLD(CMFAMOR/CMVK9F *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                 10 13'TIL:'
     A            WSVK1T    R        O 10 18REFFLD(CMFAMOR/CMVK1T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK2T    R        O 10 22REFFLD(CMFAMOR/CMVK2T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK3T    R        O 10 26REFFLD(CMFAMOR/CMVK3T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK4T    R        O 10 30REFFLD(CMFAMOR/CMVK4T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK5T    R        O 10 34REFFLD(CMFAMOR/CMVK5T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK6T    R        O 10 38REFFLD(CMFAMOR/CMVK6T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK7T    R        O 10 42REFFLD(CMFAMOR/CMVK7T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK8T    R        O 10 46REFFLD(CMFAMOR/CMVK8T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A            WSVK9T    R        O 10 50REFFLD(CMFAMOR/CMVK9T *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                 11  2'Standard'
     A                                 11 11'gebyr:'
     A            WSVK      R        O 11 18REFFLD(CMFAMOR/CMVK *LIBL/CUIFAMO)
     A                                      COLOR(WHT)
     A                                 13 18'TAST ENTER for å slette eller F12 -
     A                                      for å avbryte'
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/SYKS36B'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikeh. parter en MOTTAR INTERNE-
     A                                      DI FAKTURA FRA'
     A                                      COLOR(WHT)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Option EDI-ID avsender    Vårt knr-
     A                                         Lev.nr Navn                     -
     A                                              '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
