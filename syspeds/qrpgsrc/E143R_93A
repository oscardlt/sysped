********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
     FEDIM      IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FEQTY93A   IF   E           K DISK
     FETCC93A   IF   E           K DISK
     FDOKUF7    UF   E           K DISK
     FFREETXT   IF A E           K DISK
     FTRACKF    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WM5004_23N             1      6S 0
     D  WM5004_23A             1      6
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N20              EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT93A                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     C6069         WHENEQ    '11'
     C                   MOVEL     C6066         WC6066_11        10            No of pkg
     C     C6069         WHENEQ    '7'
     C                   MOVEL     C6066         WC6066_7         10            Weight
     C     C6069         WHENEQ    '15'
     C                   MOVEL     C6066         WC6066_15        10            Wolume
     C     C6069         WHENEQ    'ZLM'
     C                   MOVEL     C6066         WC6066_ZLM       10
     C                   ENDSL

     C  N51KEYMS0        READE     ECNT93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     OPDKEYHIGH    KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     c                   KFLD                    HIGHDAT           8 0
     c                   Move      99999999      HIGHDAT
     C     OPDKEY        KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    DF6060        WDF6060
     C     *LIKE         DEFINE    DF500A        WDF500A
     C     *LIKE         DEFINE    DF500B        WDF500B

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get time
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Get msg
     C     ZMN           CHAIN     EDIM                               50
      * Get DOKUF
     C     M1004         CHAIN(N)  DOKUF7                             20

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA13                                               MOA13 /
      *////////////////////////////////////////////////////////////
     C     MOA13         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     13            ZGN2
     C     KEYMS2        SETLL     EMOA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EMOA93A                                53
     C     *IN53         DOWEQ     '0'

     C                   MOVEA     *BLANKS       A
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM

     C                   SELECT
     C     M5025         WHENEQ    '23'
     C                   MOVEL     M5004         WM5004_23        10            Freight amount
     C                   Z-ADD     ZNUM          WDF500A
     C     M5025         WHENEQ    '52'
     C                   MOVEL     M5004         WM5004_52        10            Discount amount
     C                   Z-ADD     ZNUM          WDF500B
     C                   ENDSL

     C  N53KEYMS2        READE     EMOA93A                                53
     C  N53              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C     NAD10         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'

      *Les TCC under NAD
     C                   Z-ADD     NREP0         ZREPB0
     C                   EXSR      TCC13


     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * QTY07                                               QTY07 /
      *////////////////////////////////////////////////////////////
     C     QTY07         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     7             ZGN1
     C     KEYMS1        SETLL     EQTY93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EQTY93A                                52
     C     *IN52         DOWEQ     '0'

     C                   MOVEL     Q6060         WQ6060           10            Freight weight

     C                   MOVEA     *BLANKS       A
     C                   MOVEA     Q6060         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          WDF6060

     C  N52KEYMS1        READE     EQTY93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TCC07                                               TCC07 /
      *////////////////////////////////////////////////////////////
     C     TCC07         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     7             ZGN0
     C     KEYMS0        SETLL     ETCC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETCC93A                                51
     C     *IN51         DOWEQ     '0'

      *Les QTY under TCC
     C                   Z-ADD     TREP0         ZREPB0
     C                   EXSR      QTY07

     C  N51KEYMS0        READE     ETCC93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TCC13                                               TCC13 /
      *////////////////////////////////////////////////////////////
     C     TCC13         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     13            ZGN1
     C     KEYMS1        SETLL     ETCC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETCC93A                                52
     C     *IN52         DOWEQ     '0'

      *Les MOA under TCC
     C                   Z-ADD     TREP0         ZREPC0
     C                   Z-ADD     TREP1         ZREPC1
     C                   EXSR      MOA13

     C  N52KEYMS1        READE     ETCC93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      CNT00
     C                   EXSR      TCC07
     C                   EXSR      NAD10
     C                   EXSR      UNT00
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNT00   Write to SYSPSED                            UNT0D /
      *////////////////////////////////////////////////////////////
     C     UNT00         BEGSR
      *_________________
      * Write to FREETXT
      *"""""""""""""""""
      * Finn sist benyttede linjenr.
     C     OPDKEYHIGH    SETLL     FREETXT
     C     OPDKEY        READPE    FREETXT                                33
     c     *in33         IFEQ      *OFF
     c                   z-add     FRTLI         LINNR             2 0
     c                   else
     c                   Move      *zeros        LINNR             2 0
     c                   end
     C                   CLEAR                   FREETXTR
     C                   EVAL      FRTLI=LINNR
     C                   EVAL      FRTAVD=DFAVD
     C                   EVAL      FRTOPD=DFOPD
     C                   MOVEL     WDATO         FRTDT
     C                   MOVE      DFFBNR        AAFBNR            3
      * Concat freetxt-1
     c                   ADD       1             FRTLI
     C                   EVAL      FRTTXT='CS Fbr'
     C     FRTTXT        CAT       AAFBNR:1      FRTTXT

     C     FRTTXT        CAT       'Ant=':1      FRTTXT
     C     FRTTXT        CAT       WC6066_11:0   FRTTXT

     C     FRTTXT        CAT       'Vkt=':1      FRTTXT
     C     FRTTXT        CAT       WC6066_7:0    FRTTXT

     C     FRTTXT        CAT       'Vol=':1      FRTTXT
     C     FRTTXT        CAT       WC6066_15:0   FRTTXT

     C     FRTTXT        CAT       'Lm=':1       FRTTXT
     C     FRTTXT        CAT       WC6066_ZLM:0  FRTTXT

     C     FRTTXT        CAT       'F.V=':1      FRTTXT
     C     FRTTXT        CAT       WQ6060:0      FRTTXT

     C                   WRITE     FREETXTR
      * Concat freetxt-2
     c                   ADD       1             FRTLI
     C                   EVAL      FRTTXT=*BLANKS
     C                   EVAL      FRTTXT='CS Fbr'
     C     FRTTXT        CAT       AAFBNR:1      FRTTXT


     C     FRTTXT        CAT       'F.bel=':1    FRTTXT
     C     FRTTXT        CAT       WM5004_23:0   FRTTXT

     C     FRTTXT        CAT       'Avg=':1      FRTTXT
     C     FRTTXT        CAT       WM5004_52:0   FRTTXT

     C     WDF500A       SUB       WDF500B       WM5004_23N

     C     FRTTXT        CAT       'N.bel=':1    FRTTXT
     C     FRTTXT        CAT       WM5004_23A:0  FRTTXT

     C                   WRITE     FREETXTR
      *________________
      * Write to TRACKF
      *""""""""""""""""
     C                   CLEAR                   TRACKFR
     C                   EVAL      TTACTI='ICS'
     C                   EVAL      TTAVD=DFAVD
     C                   EVAL      TTOPD=DFOPD
     C                   EVAL      TTFBNR=DFFBNR
     C                   MOVEL     WDATO         TTDATE
     C                   MOVEL     WTID          TTTIME
     C                   MOVEL     TTDATE        TTDATL
     C                   MOVEL     TTTIME        TTTIML
s01  C*                  EVAL      TTTEXT='Fraktavtale mottatt fra transportør'+
s01  C*                            ' (IFTMCS)'
n01  C                   EVAL      TTTEXL='Fraktavtale mottatt fra transportør'+
n01  C                             ' (IFTMCS)'
n01  C                   EVAL      TTTEXT='Freightagreement received from tran'+
n01  C                             'sporter (IFTMCS)'
     C                   EVAL      TTUSER='EDI'

     C                   WRITE     TRACKFR
      *_____________
      * Update DOKUF
      *"""""""""""""
     C     M1004         CHAIN     DOKUF7                             20
     C     *IN20         IFEQ      *OFF
     C                   EVAL      DF6060=WDF6060                               F.vekt i retur
     C                   EVAL      DF500A=WDF500A                               B.frakt i retur
     C                   EVAL      DF500B=WDF500B                               Rabatt i retur
     C                   UPDATE    DOKUFR
     C                   ENDIF

     C                   ENDSR
