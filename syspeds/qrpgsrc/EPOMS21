      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS21 MODULE(*LIBL/EPOMS21
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KUN JOBBE MED EGNE TURER. TUKNA = BIKUNR
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FTURER1    IF   E           K DISK    USROPN
     FHEAD14    IF   E           K DISK    USROPN
     FEPOH02    IF   E           K DISK    USROPN
     FEPOL02    IF   E           K DISK    USROPN
     FEPOL      IF   E           K DISK    USROPN
     F                                     RENAME(EPOLR:EPOLR00)
     FCUNDL01   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D WSPO            S             10
     D WSPOMAX         S             10
     D WSART           S             35
     D WSARTMAX        S             35
     D Frames          s              1
     D WSSST           S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D OddEven         s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
     D                 DS
     D  HEPOS1                 1      7
     D  POS                    1      7    DIM(7)
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
      * Frames = Y = init / første runde
     C     FRAMES        IFeq      'Y'
      * BRUKER-NIVÅ
     C                   EVAL      PABRID = BIBRID
     C                   EVAL      PAKUNR = *ZEROS
     C                   EVAL      PAID   = 'POMCL'
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 33
      * FIRMA-NIVÅ
     C   33              EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C   33BrParKey      SETLL     BRPARF
     C   33BrParKey      READE     BRPARF                                 33
      *
     C                   IF        *IN33 = *OFF
     C                   EVAL      BSKN = PAVRDN                                Velg første
     C                   END
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     wrtsection('all')
     c                   goto      slutt
     C                   END
      *
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      *
     C                   EXSR      DSPKN
      * HOVEDRUTINE
      * Vis ordre på trip.
     C     TURER1K       SETLL     TURER1
     C     TURER1K       READE     TURER1                                 35
     C                   DOW       *IN35 = *OFF
     C*                  IF        TUATD = 0
     C                   IF        TUATD = 0 AND TUSG = '*PM'
     C                   EXSR      SRFILTER
     C                   IF        XVIS = 'J'
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   END
     C                   END
     C     TURER1K       READE     TURER1                                 35
     C                   ENDDO
      *
     C                   callp     wrtsection('bot')
      *
      * Quit
     c     Slutt         tag
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSSST    = zhbgetvar('WSSST')
     C     LC:UC         XLATE     WSSST         WSSST
     C                   eval      WSPO     = zhbgetvar('WSPO')
     C     LC:UC         XLATE     WSPO          WSPO
     C                   eval      WSPOMAX  = %TRIM(WSPO) + '999999999'
     C                   eval      WSART    = zhbgetvar('WSART')
     C     LC:UC         XLATE     WSART         WSART
     C                   eval      WSARTMAX = %TRIM(WSART) + '99999999999999999'
     C                   eval      FRAMES   = zhbgetvar('FRAMES')
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     TURER1
     C                   CLOSE     HEAD14
     C                   CLOSE     EPOH02
     C                   CLOSE     EPOL02
     C                   CLOSE     EPOL
     C                   CLOSE     CUNDL01
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   IF        FRAMES = 'Y'
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS21F.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   ELSE
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS21.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   END
     C                   endsr
      *=====================================================================******
      *  FILTRERE POSTER
      *=====================================================================******
     C     SRFILTER      begsr
N01  C                   IF        TUKNA <> 0 AND TUKNA <> BSKN
N01  C                   MOVE      'N'           XVIS
N01  C                   ELSE
     C                   IF        WSPO = *BLANKS AND WSART = *BLANKS           1<-B
     C                   MOVE      'J'           XVIS              1
     C                   ELSE                                                   1
     C                   MOVE      'N'           XVIS              1
     C     TUPRO         SETLL     HEAD14
     C     TUPRO         READE     HEAD14                                 33
     C                   DOW       *IN33 = *OFF                                  2<-B
      *
      * SJEKK PO-HEADING HVIS HELE PO ER PLUKKET
     C     EPOH02K       SETLL     EPOH02
     C     EPOH02K       READE     EPOH02                                 33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        WSPO <> *BLANKS AND                             4<-B
     C                             EHBES >= WSPO AND EHBES <= WSPOMAX
     C                   MOVE      'J'           XVIS
     C                   GOTO      SLFILTER
     C                   END                                                       4<-E
     C                   IF        WSART <> *BLANKS                                4<-B
     C     EPOLK         SETLL     EPOL
     C     EPOLK         READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF                                     5<-B
     C                   IF        ELART >= WSART AND ELART <= WSARTMAX             5<-B
     C                   MOVE      'J'           XVIS
     C                   GOTO      SLFILTER
     C                   END                                                        5<-E
     C     EPOLK         READE     EPOL                                   33
     C                   ENDDO                                                      5<-E
     C                   END                                                       4<-E
     C     EPOH02K       READE     EPOH02                                 33
     C                   ENDDO                                                    3<-E
      *
      * SJEKK PO-LINJE HVIS LINJE ER PLUKKET
     C     EPOH02K       SETLL     EPOL02
     C     EPOH02K       READE     EPOL02                                 33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        WSPO <> *BLANKS AND                             4<-B
     C                             ELBES >= WSPO AND ELBES <= WSPOMAX
     C                   MOVE      'J'           XVIS
     C                   GOTO      SLFILTER
     C                   END                                                       4<-E
     C                   IF        WSART <> *BLANKS AND                            4<-B
     C                             ELART >= WSART AND ELART <= WSARTMAX
     C                   MOVE      'J'           XVIS
     C                   GOTO      SLFILTER
     C                   END                                                       4<-E
     C     EPOH02K       READE     EPOL02                                 33
     C                   ENDDO                                                    3<-E
      *
     C     TUPRO         READE     HEAD14                                 33
     C                   ENDDO                                                   2<-E
     C                   END                                                    1<-E
N01  C                   END
      *
     C     SLFILTER      TAG
     C                   ENDSR
      *
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('FRAMES':FRAMES)
     C                   callp     updHTMLvar('WSSST':WSSST)
     C                   callp     updHTMLvar('WSPO':WSPO)
     C                   callp     updHTMLvar('WSART':WSART)
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C                   callp     updHTMLvar('TUPRO':
     C                             %trim(%editc(TUPRO:'Z')))
     C                   MOVE      TUPRO         TUPRO2            8
     C                   callp     updHTMLvar('TUPRO2':TUPRO2)
     C                   callp     updHTMLvar('TUSTEF':TUSTEF)
     C                   callp     updHTMLvar('TUSTET':TUSTET)
     C                   callp     updHTMLvar('TUETD':
     C                             %trim(%editW(TUETD:'    .  .  ')))
     C                   callp     updHTMLvar('TUETA':
     C                             %trim(%editW(TUETA:'    .  .  ')))
     C                   callp     updHTMLvar('TUATD':
     C                             %trim(%editW(TUATD:'    .  .  ')))
     C                   callp     updHTMLvar('TUCON1':TUCON1)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display Customer Number to select from
      *=====================================================================******
     C     DSPKN         begsr
      *
     C                   callp     wrtsection('DspClTop')
      * BRUKER-NIVÅ
     C                   EVAL      PABRID = BIBRID
     C                   EVAL      PAKUNR = *ZEROS
     C                   EVAL      PAID   = 'POMCL'
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 33
      * FIRMA-NIVÅ
     C   33              EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C   33BrParKey      SETLL     BRPARF
     C   33BrParKey      READE     BRPARF                                 33
      *
     C                   DOW       *IN33 = *OFF
     C                   IF        BSKN = 0                                     Ennå ikke valgt
     C                   EVAL      BSKN = PAVRDN                                Velg første
     C                   END
     C                   IF        BSKN = PAVRDN
     C                   callp     updhtmlvar('KNSEL':'SELECTED')
     C                   ELSE
     C                   callp     updhtmlvar('KNSEL':'')
     C                   END
     C                   Z-ADD     PAVRDN        KUNDNR
     C                   callp     updHTMLvar('SLTKN1':
     C                             %trim(%editc(KUNDNR:'Z')))
     C                   IF        PAVRDA = *BLANKS
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('SLTKN2':KNAVN)
     C                   ELSE
     C                   callp     updhtmlvar('SLTKN2':PAVRDA)
     C                   END
     C                   callp     wrtsection('DspClRow')
     C     BrParKey      READE     BRPARF                                 33
     C                   ENDDO
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
      *
     C                   callp     wrtsection('DspClBot')
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      TURER1
     C                   OPEN      HEAD14
     C                   OPEN      EPOH02
     C                   OPEN      EPOL02
     C                   OPEN      EPOL
     C                   OPEN      CUNDL01
      *
     C     TURER1K       klist
     C                   kfld                    WSSST
     C                   kfld                    XAVD
     C     EPOH02K       klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     EPOLK         klist
     C                   kfld                    EHKUND
     C                   kfld                    EHBES
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    KUNDNR
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVE      'OSAVD'       PAID
     C     BrParKey      chain     BRPARF                             33
     C  N33              Z-ADD     PAVRDN        XAVD              4 0
     C*                  IF        BSKN = 0
     C*tull              EVAL      BSKN = BIKUNR
     C*                  END
      *
     C                   endsr
      *
