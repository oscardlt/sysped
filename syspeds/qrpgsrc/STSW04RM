      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSW04RM   MODULE(*LIBL/STSW04RM *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Vekt, M3, LM  ....
      *              Håpløst å legge til nye felt i HTML så jeg bygger på SISTE /%ANTM%/
      *              dvs ANTM er f.eks: 3;150;0,450;0,00 (ant manko,vekt,M3,LM......)
      *%%%%%%%%
10XXX * 02 JOV PTF11 Nytt felt - I=Important = Prioritert gods
11XXX * 03 JOV PTF11 Info om Prioritert gods hentes nå fra T&T
      * 04 JOV PTF11 Kan merke manuelt via avd_opd dersom ikke innland (og ikke kollimerket)
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     Fstsopd    if   e           K DISK    usropn
     Fstscli    if   e           K DISK    usropn
     Fheadf     if   e           k disk    usropn
N03  FTRACKL02  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     DwsPro            s              8  0
     DTA1              s              4
     DTN1              s              8
     DwsPro1           s              8  0
     DTA2              s              4
     DTN2              s              8
     DwsPro2           s              8  0
     DTA3              s              4
     DTN3              s              8
     DwsPro3           s              8  0
     DBILN             s             10
     D quit            s              4
     D UserId          s             10
     D User            s             10
     D VisAlle         s              1
     D SEMI            s              1
N01  D VERSJ           s             10
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D GmAvOp          s             10
     D antclk          s              5  0
     D antclm          s              5  0
     D CliFinnes       s              1
N01  D ANTMpluss       s            200
      *
     D                 DS
     D  HXSNDN                 1     20
     D  Sndnr1                 1     10
     D  Sndnr2                11     20
     D                 DS
     D  Henas                  1     30
     D  xxnas                  1      6
     D                 DS
     D  Henak                  1     30
     D  xxnak                  1     10
     D                 DS
     D  Headk3                 1     30
     D  xxadk3                 1     10
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅÅ                  1      4  0
     D  XDTMM                  5      6  0
     D  XDTDD                  7      8  0
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      TN1      = zhbgetvar('TN1   ')
     C                   eval      TN2      = zhbgetvar('TN2   ')
     C                   eval      TN3      = zhbgetvar('TN3   ')
     C                   eval      TA1      = zhbgetvar('TA1   ')
     C                   eval      TA2      = zhbgetvar('TA2   ')
     C                   eval      TA3      = zhbgetvar('TA3   ')
     C                   eval      WSPRO    = c2n2(TN1   )
     C                   eval      WSPRO1   = c2n2(TN1   )
     C                   eval      WSPRO2   = c2n2(TN2   )
     C                   eval      WSPRO3   = c2n2(TN3   )
     C                   eval      BILN   = zhbgetvar('BILN')
     C                   eval      VisAlle = zhbgetvar('Alle')
     C                   eval      userid = zhbgetvar('user')
     C                   eval      SEMI    = zhbgetvar('SEMI')
N01   * versjonsnr av klientprogramvare
N01  C                   eval      VERSJ = zhbgetvar('V')
      * Set libl/open files
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis sendinger
     C                   exsr      HSEND
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     stsopd
     C                   close     stscli
     C                   close     headf
N03  C                   close     TRACKL02
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     HSEND         BEGSR
     C***********************************************
      * Repeat UserId for the next invocation
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('TN1   ':TN1   )
     C                   callp     updhtmlvar('TN2   ':TN2   )
     C                   callp     updhtmlvar('TN3   ':TN3   )
     C                   callp     updhtmlvar('TA1   ':TA1   )
     C                   callp     updhtmlvar('TA2   ':TA2   )
     C                   callp     updhtmlvar('TA3   ':TA3   )
     C                   callp     updhtmlvar('BILN':biln)
      *
     C                   callp     wrtsection('top')
      * tur 1
     C                   exsr      HSEND2
      * + tur 2
     c     wspro2        ifne      *zeros
     c                   move      wspro2        wspro
     C                   exsr      HSEND2
     c                   end
      * + tur 3
     c     wspro3        ifne      *zeros
     c                   move      wspro3        wspro
     C                   exsr      HSEND2
     c                   end
     c                   move      wspro1        wspro
      *
     C                   callp     wrtsection('tablebot')
      *
     C                   ENDSR
      *
     C***********************************************
     C     HSEND2        BEGSR
     C***********************************************
      *
     C     TurKey        SETLL     stsopd
     C     TurKey        READE     stsopd                                 94
     C     *IN94         DOWEQ     '0'
     C     VisAlle       Ifne      'J'
     C     SOSTAT        Andne     ' '
     C                   goto      NEXT
     C                   end
     c     heakey        chain     headf                              33
     C     hepro         ifne      sopro
     C                   goto      NEXT
     C                   end
     c     sndnr2        Ifeq      *blanks
     C                   callp     updhtmlvar('OpID':Sndnr1)
     c                   else
     C                   callp     updhtmlvar('OpID':Sndnr2)
     c                   end
     C     sostat        ifeq      *blanks
     C                   callp     updhtmlvar('STAT':' ')
     C                   else
     C                   callp     updhtmlvar('STAT':SOSTAT)
     C                   end
     C                   callp     updhtmlvar('ANT':
     C                             %trim(%editc(HENT:'3')))
     C                   callp     updhtmlvar('TURNR':
     C                             %trim(%editc(sopro:'3')))
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(soavd:'3')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(soopd:'3')))
      *
     C                   callp     updhtmlvar('AVS':xxnas)
     C                   callp     updhtmlvar('MOT':xxnak)
     C                   callp     updhtmlvar('PNR':xxadk3)
     C                   callp     updhtmlvar('TKAVS':henas)
     C                   callp     updhtmlvar('TKMOT':henak)
     C                   callp     updhtmlvar('TKMAD':headk1)
     C                   callp     updhtmlvar('TKMPNST':headk3)
N04  c     HXSNDN        ifeq      *blanks
N04  c                   move      HEAVD         HEAVDA            4
N04  c                   move      HEOPD         HEOPDA            7
N04  c                   eval      HXSNDN = HEAVDA+'_'+HEOPDA
N04  c                   endif
     C                   callp     updhtmlvar('HXSNDN':HXSNDN)
      * ant. clid lest
     c                   Move      *zeros        antclk
     c                   Move      *zeros        antclm
     C                   Move      'N'           clifinnes
     C     cliKey        SETLL     stscli
     C     cliKey        READE     stscli                                 34
     C  N34              Move      'J'           clifinnes
     C     *IN34         DOWEQ     '0'
     c     Scstat        ifne      ' '
     c                   add       1             antclk
     c                   end
     C     cliKey        READE     stscli                                 34
     C  N34              ENDDO
     c     CliFinnes     ifeq      'J'
     C     HENT          sub       antclk        antclm
     c     Antclm        iflt      *zeros
     C                   move      *zeros        antclm
     c                   end
     c                   end
      * I manko vises blank/ferdig-status når CLI ikke finnes.
     c     CliFinnes     ifne      'J'
     C     sostat        ifeq      *blanks
     C     SEMI          andne     'Y'
     C                   callp     updhtmlvar('ANTM':spaces)
     C                   else
     C                   callp     updhtmlvar('ANTM':SOSTAT)
     C                   end
      * Når CLI finnes vises reelt manko-tall ( 0 = ferdig lest...)
     c                   else
     C                   callp     updhtmlvar('ANTM':
     C                             %trim(%editc(antclm:'3')))
     c                   end
      *
      * KRØKKETE Å LEGGE TIL NYE HTML-FELT - ETTER 01.20 UTVIDES BARE SISTE FELT (ANTM)
N01  c     Versj         ifge      '01.20'
N01  c     Semi          andeq     'Y'
N01  c     CliFinnes     ifne      'J'
N01  C                   eval      ANTMpluss=SOSTAT
N01  c                   else
N01  C                   eval      ANTMpluss=%triml(%editc(antclm:'3'))
N01  c                   endif
N01  C                   eval      ANTMpluss=%trim(ANTMpluss)
N01  c                             +';'+%triml(%editc(HEVKT:'3'))
N01  c                             +';'+%triml(%editc(HEM3:'L'))
N01  c                             +';'+%triml(%editc(HELM:'L'))
N02  c     Versj         ifge      '01.26'
N02  c                   move      *blanks       PRIKOD            1
S03  c*                  if        HEGM2='PRIO'
N03  C                   MOVE      'URG'         XXACTI
N03  c     TTKey         Chain     TRACKl02                           33
N03  c                   if        *in33=*off and %subst(TTTEXT:1:8)='*URGENT*'
N02  c                   move      'I'           PRIKOD
N02  c                   endif
N02  C                   eval      ANTMpluss=%trim(ANTMpluss)
N02  c                             +';'+%trim(PRIKOD)
N02  c                   endif
N01  C                   callp     updhtmlvar('ANTM':ANTMpluss)
N01  c                   endif
      *
     C                   callp     wrtsection('tablerow')
     C     Next          tag
     C     TurKey        READE     stsopd                                 94
     C  N94              ENDDO
      *
     C                   ENDSR
      *
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C     SEMI          IFNE      'Y'
     C                   eval      Mbr = 'STSW04M'
     C                   else
     C                   eval      Mbr = 'STSW04T'
     C                   end
      *------------------
     C                   endsr
      *=====================================================================******
      * Set Keys mm
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      stsopd
     C                   open      stscli
     C                   open      headf
N03  C                   open      TRACKL02
      *
      *  keys mm
     C     *LIKE         DEFINE    SOTYPE        YTYPE
     C     TURKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   MOVE      *BLANKS       YTYPE
     C     CLIKEY        KLIST
     C                   KFLD                    sotype
     C                   KFLD                    sopro
     C                   KFLD                    soavd
     C                   KFLD                    soopd
     C     HEAKEY        KLIST
     C                   KFLD                    soavd
     C                   KFLD                    soopd
N03  C     TTKey         KLIST
N03  C                   KFLD                    HEAVD
N03  C                   KFLD                    HEOPD
N03  C                   KFLD                    XXFBNR            3 0
N03  C                   KFLD                    XXACTI            3
     C                   endsr
