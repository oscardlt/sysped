********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 SVIH_MTYP ER ENDRET TIL 5A
********************************************************************
     A*%%TS  SD  20111110  135447  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPTITLE('ED För Import')
     A                                      HLPPNLGRP(HLPSCR SVI000PNL)
     A                                      ALTHELP
     A                                      HELP
     A          R SVI000DA                  SFL
     A*%%TS  SD  20111110  135447  CB          REL-V7R1M0  5770-WDS
S01  A*           SVIH_MTYP      3A  H
N01  A            SVIH_MTYP      5A  H
     A            ZOP            2A  I  9  2
     A  31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A                                      VALUES(' ' '5' '9' '6' 'I' 'A' 'X')
     A            SVIH_SYAV      4Y 0O  9  5EDTCDE(4)
     A            SVIH_SYOP      7Y 0O  9 10EDTCDE(4)
     A            SVIH_TUID     10A  O  9 18
     A            SVIH_SYSG      3A  O  9 29
     A            SVIH_SYDT      8Y 0O  9 33EDTCDE(4)
     A            SVIH_SYST      1A  O  9 42
     A            SVIH_AVLK      2A  O  9 44
     A            SVIH_AVNA     35A  O  9 47
     A  40                                  COLOR(WHT)
     A            SVIH_MONA     35A  O  9 83
     A          R SVI000DB                  SFLCTL(SVI000DA)
     A*%%TS  SD  20111110  135447  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      INVITE
     A                                      FRCDTA
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF05(05 'Refresh')
     A                                      CF08(08 'Ny avd/sign')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(SVI0000110 SVI000PNL)
     A                                      HLPARA(08 002 08 080)
     A          H                           HLPPNLGRP(SVI0000010 SVI000PNL)
     A                                      HLPARA(05 002 06 080)
     A          H                           HLPPNLGRP(SVI0000010 SVI000PNL)
     A                                      HLPARA(09 002 21 002)
     A          H                           HLPPNLGRP(SVI0000019 SVI000PNL)
     A                                      HLPARA(09 004 21 007)
     A          H                           HLPPNLGRP(SVI0000020 SVI000PNL)
     A                                      HLPARA(09 009 21 015)
     A          H                           HLPPNLGRP(SVI0000030 SVI000PNL)
     A                                      HLPARA(09 017 21 026)
     A          H                           HLPPNLGRP(SVI0000035 SVI000PNL)
     A                                      HLPARA(09 028 21 030)
     A          H                           HLPPNLGRP(SVI0000040 SVI000PNL)
     A                                      HLPARA(09 032 21 039)
     A          H                           HLPPNLGRP(SVI0000060 SVI000PNL)
     A                                      HLPARA(09 041 21 041)
     A          H                           HLPPNLGRP(SVI0000070 SVI000PNL)
     A                                      HLPARA(08 043 21 050)
     A          H                           HLPPNLGRP(SVI0000090 SVI000PNL)
     A                                      HLPARA(08 054 21 080)
     A          H                           HLPPNLGRP(SVI0000100 SVI000PNL)
     A                                      HLPARA(23 002 23 068)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI000'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 45'Förfrågan av Ärenden - Översikt'
     A                                      COLOR(WHT)
     A                                  1114DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1123TIME
     A                                      COLOR(BLU)
     A                                  2 56'Import'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Avdelning/signatur:'
     A            ZVIH_SYAV      4Y 0O  3 22EDTCDE(4)
     A            ZVIH_SYSG      3A  O  3 27
     A                                  4  2'Välj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'5=Visa   9=Logg   6=Skriv ut   A=A-
     A                                      rkiv   X=Extern referans   I=Intras-
     A                                      tat utskrift'
     A                                      COLOR(BLU)
     A                                  7  6'Avd'
     A                                      COLOR(WHT)
     A                                  7 11'Ärende'
     A                                      COLOR(WHT)
     A                                  7 18'Tullid'
     A                                      COLOR(WHT)
     A                                  7 29'Sig'
     A                                      COLOR(WHT)
     A                                  7 33'Datum'
     A                                      COLOR(WHT)
     A                                  7 42'S'
     A                                      COLOR(WHT)
     A                                  7 47'Avsändare'
     A                                      COLOR(WHT)
     A                                  7 83'Mottagare'
     A                                      COLOR(WHT)
     A            Z2OP           1A  B  8  2DSPATR(UL)
     A            Z2SYAV         4Y 0B  8  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            Z2SYOP         7Y 0B  8 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            Z2TUID        10A  B  8 18DSPATR(UL)
     A            Z2SYSG         3A  B  8 29DSPATR(UL)
     A            Z2SYDT         8Y 0B  8 33DSPATR(UL)
     A                                      EDTCDE(4)
     A            Z2SYST         1A  B  8 42DSPATR(UL)
     A            Z2AVKN         8Y 0B  8 47DSPATR(UL)
     A                                      EDTCDE(4)
     A            Z2MOKN         8Y 0B  8 83DSPATR(UL)
     A                                      EDTCDE(4)
     A          R SVI000DC
     A*%%TS  SD  20040928  095831  CB          REL-V5R2M0  5722-WDS
     A                                      OVERLAY
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F5=Uppdatera'
     A                                      COLOR(BLU)
     A                                 26 44'F8=Selektera'
     A                                      COLOR(BLU)
     A                                 26 59'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI000DD
     A*%%TS  SD  20081017  111304  CB          REL-V5R4M0  5722-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Sök kunde/leverandör')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVI000'
     A                                      COLOR(BLU)
     A                                  1 16'DD'
     A                                      COLOR(BLU)
     A                                  1 51'Underhåll av Ärenden - Selektion'
     A                                      COLOR(WHT)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2 62'Import'
     A                                      COLOR(WHT)
     A                                  3  2'Avdelning:'
     A            ZSSYAV         4Y 0B  3 15TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  3 21TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  5  4'Välj sökväg:'
     A                                      COLOR(WHT)
     A                                  6  4'Ext. ref :'
     A            WFSSOK        35A  B  6 17COLOR(YLW)
     A                                  7  4'Datum  . :'
     A            ZSSYDT         8Y 0B  7 17TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  8  4'Ärende . :'
     A            ZSSYOP         7Y 0B  8 17TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9  4'Avsändare:'
     A            ZSAVKN         8Y 0B  9 17TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 30'Datum:'
     A            ZSDTA          8Y 0B  9 37TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10  4'Mottagare:'
     A            ZSMOKN         8Y 0B 10 17TEXT('Kundenr Kjöper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  44
     AO 96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 30'Datum:'
     A            ZSDTM          8Y 0B 10 37TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11  4'Signatur :'
     A            ZSSYSG         3A  B 11 17TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 11 30'Datum:'
     A            ZSDTS          8Y 0B 11 37TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12  4'Tullid . :'
     A            ZSTUID        10A  B 12 17TEXT('Toll-ID')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13  4'Status . :'
     A            ZSSYST         1A  B 13 17TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R SVI000DE
     A*%%TS  SD  20091104  222904  CB          REL-V5R4M0  5722-WDS
     A                                  1  2'SYSPED/SVI000'
     A                                      COLOR(BLU)
     A                                  1 16'DE'
     A                                      COLOR(BLU)
     A                                  1 51'Underhåll av Ärenden - Tidsavbrott'
     A                                      COLOR(WHT)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2 65'Import'
     A                                      COLOR(WHT)
     A                                  4  3'Pogramet avslutas på grund av tids-
     A                                      avbrott. Det har gått 30 minuter se-
     A                                      dan ni sist var aktiv i detta progr-
     A                                      am.'
     A                                  5  4'Tryck ENTER för att avsluta, välj -
     A                                      menypunkt för TDS Import igen.'
     A          R SVI000DF
     A*%%TS  SD  20091104  224227  CB          REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 7 25 *NOMSGLIN)
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'Prompt')
     A                                      CF12(12 'Cancel')
     A                                      CF22(22 'Hemlig tast')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  1'SVI000D'
     A                                      COLOR(BLU)
     A                                  1  9'F'
     A                                      COLOR(BLU)
     A                                  1 15'Sigill'
     A                                      COLOR(WHT)
     A                                  3  1'Till    '
     A                                      COLOR(WHT)
     A                                  3 12'Från     '
     A                                      COLOR(WHT)
     A            ZVIH_SGDK      6A  B  4  1DSPATR(PR)
     A            ZVIH_SGUT      6A  B  4 12DSPATR(PC)
     A  30                                  COLOR(RED)
     A                                  6  1'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                  6 14'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                  2  1'---Sigillator----'
     A                                      COLOR(WHT)
     A            ZFELTXT       24      7  1
     A  30                                  COLOR(RED)
     A          R SVI000DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
