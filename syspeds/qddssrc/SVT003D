     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
      * "NOERASE" MÅ HA FOR Å IKKE TÖMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 65 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            SVTX03_03     10A  O  5  5
     A            WVTX03_04     50A  O  5 16
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT003R'
     A                                      COLOR(BLU)
     A            XFTXT         30A  O  1 10COLOR(WHT)
     A                                  2  2'Välj option:'
     A                                      COLOR(BLU)
     A                                  2 16'1=Plocka'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kod        Beskrivning'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A            W2SØK1        10A  I  4  5DSPATR(UL)
     A            W2SØK2        20A  I  4 16DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F3/F12=Avbryt   F5=Förnya'
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT003R'
     A                                      COLOR(BLU)
     A            XFTXT         30A     1 10COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kode       Beskrivelse'
     A                                      COLOR(WHT)
     A            W2SØK         20A  I  4 16DSPATR(UL)
     A                                 15  2'F3/F12=Avbryt   F5=Frisk'
     A                                      COLOR(BLU)
