********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004 * 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
      *              INGEN MERKING
10029 * 02 YBC PTF10 ENDRING GÅR DIREKT TIL "ARBEID MED ORDRE"
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R GSS023DA                  SFL
S02  A*           ORCLI          5Y 0H
S02  A*           ORWT           7Y 1H
S02  A*           ORVOL          5Y 2H
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '5' '8' 'L' 'B' -
     A                                      '6')
     A            ORONO          7Y 0O  5  5EDTCDE(4)
     A  35                                  COLOR(WHT)
     A  34                                  COLOR(YLW)
     A            POCONO        20A  H
     A            WXCONO        18A  O  5 13
S01  A* 38                                  COLOR(RED)
N01  A  38                                  COLOR(YLW)
N01  A  37                                  COLOR(RED)
     A            ORSNO          5Y 0O  5 32EDTCDE(4)
S02  A*           WXCLI          5Y 0B  5 38EDTCDE(4)
N02  A            ORCLI          5Y 0O  5 38EDTCDE(4)
S02  A* 29                                  DSPATR(PR)
S02  A*N29                                  COLOR(YLW)
S02  A*           WXWT           7Y 1B  5 44EDTCDE(4)
N02  A            ORWT           7Y 1O  5 44EDTCDE(4)
S02  A* 29                                  DSPATR(PR)
S02  A*N29                                  COLOR(YLW)
S02  A*           WXVOL          5Y 2B  5 53EDTCDE(4)
N02  A            ORVOL          5Y 2O  5 53EDTCDE(4)
S02  A* 29                                  DSPATR(PR)
S02  A*N29                                  COLOR(YLW)
     A            ORPOS1         5A  O  5 60
     A            ORLC           5A  H
     A            ORBCD          5A  H
     A            XLCBCD         5A  O  5 66
S02  A*           WXCLNF         3Y 0B  5 72EDTCDE(4)
N02  A            ORCLNF         3Y 0O  5 72EDTCDE(4)
S02  A* 29                                  DSPATR(PR)
S02  A*N29                                  COLOR(YLW)
S02  A*           WXCLNT         3Y 0B  5 76EDTCDE(4)
N02  A            ORCLNT         3Y 0O  5 76EDTCDE(4)
S02  A* 29                                  DSPATR(PR)
S02  A*N29                                  COLOR(YLW)
      *
     A          R GSS023DB                  SFLCTL(GSS023DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS023D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with orders on trip'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Trip No.'
     A            TRTNO         11Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 24'Clients No.'
     A            TRCNO          8Y 0O  2 36EDTCDE(4)
     A                                      COLOR(WHT)
     A N40                              2 45'Vessel'
     A N40        TRBCD          5A  O  2 52COLOR(WHT)
     A  40                              2 45'locat.'
     A  40        TRLOCF         5A  O  2 52COLOR(WHT)
     A                                  2 60'Tot. weight:'
     A            WSWT           7Y 1O  2 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
S02  A*N29                              3  2'Type opt:  2=Chg colli no.  4=Dlt -
N02  A N29                              3  2'Type opt:  2=Chg  4=Dlt -
     A                                      5=Dsp  6=Label  8=Inv.basis  L/B=Lo-
     A                                      k/Box'
     A                                      COLOR(BLU)
     A  29                              3  2'Type options:  5=Display  6=Label'
     A                                      COLOR(BLU)
     A N40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Locat-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A  40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Vesse-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
      *
     A          R GSS023DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N29                                  CF06(06 'Puck up')
     A                                      CF08(08 'SØK')
     A N29                                  CF10(10 'Print')
     A                                      CF12(12 'Cancel')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N29N27                           1  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print packinglist  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A N29 27                           1  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print proforma  F12=Ca-
     A                                      ncel'
     A                                      COLOR(BLU)
     A  29                              1  2'F3=End  F5=Refresh  F8=Search  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS023DE
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Search from --> '
     A  40                              2 19'Vessel:'
     A  40        W2BCD          5A  I  2 27DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2 36'Order No.'
     A            W2ONO          7Y 0I  2 46DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS023DG
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N29                                  CF06(06 'Puck up')
     A                                      CF08(08 'SØK')
     A N29                                  CF10(10 'Print')
     A                                      CF12(12 'Cancel')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS023D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with orders on trip'
     A                                      DSPATR(HI)
     A                                  2  2'Trip No.'
     A            TRTNO         11Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 24'Clients No.'
     A            TRCNO          8Y 0O  2 36EDTCDE(4)
     A                                      COLOR(WHT)
     A N40                              2 45'Vessel'
     A N40        TRBCD          5A  O  2 52COLOR(WHT)
     A  40                              2 45'locat.'
     A  40        TRLOCF         5A  O  2 52COLOR(WHT)
S02  A*N29                              3  2'Type opt:  2=Chg colli no.  4=Dlt -
N02  A N29                              3  2'Type opt:  2=Chg  4=Dlt -
     A                                      5=Dsp  6=Label  8=Inv.basis  L/B=Lo-
     A                                      k/Box'
     A                                      COLOR(BLU)
     A  29                              3  2'Type options:   5=Display  6=Label'
     A                                      COLOR(BLU)
     A N40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Locat-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A  40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Vesse-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A                                  6  6'No order found'
     A                                      COLOR(RED)
     A N29N27                          23  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print packinglist  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A N29 27                          23  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print proforma  F12=Ca-
     A                                      ncel'
     A                                      COLOR(BLU)
     A  29                             23  2'F3=End  F5=Refresh  F8=Search  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS023DK
     A                                      WINDOW(*DFT 3 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  2  2'Keep box-label?'
     A                                      COLOR(WHT)
     A            WSKPBN         1A  B  2 18DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  2 21'(Y/N)'
      *
     A          R GSS023DX
     A                                      WINDOW(*DFT 7 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRT         10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'No. of copies:'
     A            WSCPY          1  0B  4 17DSPATR(UL)
     A                                  5  2'Send fax ?    '
     A            WSFAX          1A  B  5 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  5 20'Y/N'
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A     7  2MSGID(GSS0042 GSSMSGF)
      *
     A          R GSS023DY
     A                                      WINDOW(*DFT 12 73 *NOMSGLIN)
     A                                      CF04(04 'Søking')
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A            MSGHOD        25A  O  1  2MSGID(&MSGHN SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'HEADING TEXT'
     A            WSFT1         70A  B  3  2DSPATR(UL)
     A N98N99                               DSPATR(PC)
     A            WSFT2         70A  B  4  2DSPATR(UL)
     A                                  5  2'Printer name :'
     A            WSPRT         10A  B  5 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
     A                                  6  2'No. of copies:'
     A            WSCPY          1  0B  6 17DSPATR(UL)
     A                                  7  2'Layout:       '
     A            WSNYL          1A  B  7 17DSPATR(UL)
     A                                      VALUES('P' 'L' 'O')
     A                                  7 20'P=Portrait L=Landscape O=Portrait2'
     A                                  8  2'Curr. code   :'
     A            WSCUR          3A  B  8 17DSPATR(UL)
     A  98                                  DSPATR(HI)
     A  98                                  DSPATR(PC)
     A                                  9  2'Send fax ?    '
     A            WSFAX          1A  B  9 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  9 20'Y/N'
     A N28                             10  2'Labels?'
     A N28        WSML           1A  B 10 10DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A N28                             10 12'P.name:'
     A N28        WSMLP         10A  B 10 20DSPATR(UL)
     A N28                             10 31'Free text:'
     A N28        WSFTXT        30A  B 10 42DSPATR(UL)
     A                                 11  2'Comm.invoice?:'
     A            WSCOMM         1A  B 11 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                 11 22'Clientsnr:'
     A            WSKNC          8  0B 11 34DSPATR(UL)
     A  97                                  DSPATR(HI)
     A  97                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N99N98N97                       12  2'F4= Søk   F12=Cancel'
     A                                      COLOR(BLU)
     A  99                             12  2MSGCON(25 GSS0042 SYMSGF)
     A                                      COLOR(WHT)
     A  98                             12  2MSGCON(25 GSS0066 SYMSGF)
     A                                      COLOR(WHT)
     A  97                             12  2MSGCON(25 SPK0001 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGHN          7A  P
      *
     A          R GSS023DZ
     A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print labels'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRTM        10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'Barcode     :'
     A            WSSK           1A  B  4 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  4 21'(Y/N)'
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A     6  2MSGID(GSS0042 SYMSGF)
      *
