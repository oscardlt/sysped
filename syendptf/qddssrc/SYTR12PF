*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
10261 * 02 SH  PTF12 FLYTTET LIT PÅ VEKT
10261 * 03 SH  PTF12 ENGELSK SPRÅK IND 70  IKKE MERKET
*****A**************************************************************
      *_____________________________________________
      * Format for hovedopplysninger
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R KJØP
     A                                  2  2MSGCON(119 JOV0003 SYMSGF)
     A                                      HIGHLIGHT
     A N70                              3  2MSGCON(119 JOV0002 SYMSGF)
     A                                      HIGHLIGHT
     A  70                              3  2MSGCON(119 SHU1775 SYMSGF)
     A                                      HIGHLIGHT
     A            XSIDE          2  0   3106EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                  4  2MSGCON(119 JOV0003 SYMSGF)
     A                                      HIGHLIGHT
     A            YNAVN     R           5 11REFFLD(KNAVN CUNDF)
     A                                      HIGHLIGHT
     A N70                              5 78MSGCON(15 DIV1668 SYMSGF)
     A  70                              5 78MSGCON(15 SHU1776 SYMSGF)
     A            YKUNDN    R           5100REFFLD(KUNDNR CUNDF)
     A                                      EDTCDE(Z)
     A            YADR1     R           6 11REFFLD(ADR1 CUNDF)
     A                                      HIGHLIGHT
     A N70                              6 78MSGCON(15 DIV2254 SYMSGF)
     A  70                              6 78MSGCON(15 SHU1777 SYMSGF)
     A            THTNR          8S 0   6100EDTCDE(Z)
     A            YADR2     R           7 11REFFLD(ADR2 CUNDF)
     A                                      HIGHLIGHT
     A N70                              7 78MSGCON(15 JOV0005 SYMSGF)
     A  70                              7 78MSGCON(15 SHU1778 SYMSGF)
     A            IDAGX          6S 0   7100EDTCDE(Y)
     A            YADR3         35      8 11
     A                                      HIGHLIGHT
     A N70                              8 78MSGCON(15 JOV0007 SYMSGF)
     A  70                              8 78MSGCON(15 JOV0007 SYMSGF)
     A            W1PERM         2  0   8103
     A                                  8105'/'
     A            W1PERÅ         2  0   8106
     A            KLKNVN    R           9 11REFFLD(KLKNVN KODTLK)
     A                                      HIGHLIGHT
S01  A*                                10  2MSGCON(119 JOV0004 SYMSGF)
N01  A N70                             10  2MSGCON(119 YBC2000 SYMSGF)
     A  70                             10  2MSGCON(119 SHU1780 SYMSGF)
     A                                      HIGHLIGHT
     A                                 10  2'________________________'
     A                                 10 26'________________________'
     A                                 10 50'________________________'
     A                                 10 74'________________________'
     A                                 10 98'______________________'
      *_______________________________
      * Format for faktura-varelinjer.
      *"""""""""""""""""""""""""""""""
     A          R DET                       SPACEB(2)
     A            TURUND         8  0      1EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUAVD          4  0     10EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUPRO          8  0     15EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUDATO         6  0     24EDTWRD('  .  .  ')
     A                                      HIGHLIGHT
     A            TUSDF         20        33
     A                                      HIGHLIGHT
     A            TUSDT         20        54
     A                                      HIGHLIGHT
     A            TUBILN        10        76
     A                                      HIGHLIGHT
     A            TUHENG        10        86
     A                                      HIGHLIGHT
S01  A*           TUTVKT         5  0     96EDTCDE(3)
N01  A            TUTVKT         9  0     96EDTCDE(Z)
     A                                      HIGHLIGHT
S01  A*N96        TUTLM          4  2    102EDTWRD(' 0,  ')
S02  A*N96        TUTLM          4  2    105EDTWRD(' 0,  ')
N02  A N96        TUTLM          4  2    106EDTWRD(' 0,  ')
     A                                      HIGHLIGHT
S01  A*N97        TUTM3          5  2    108EDTWRD('  0,  ')
S02  A*N97        TUTM3          7  3    110EDTWRD('   0,   ')
N02  A N97        TUTM3          7  3    112EDTWRD('   0,   ')
     A                                      HIGHLIGHT
     A          R DETST                     SPACEB(1)
     A N70                                 2MSGCON(15 JOV0006 SYMSGF)
     A  70                                 2MSGCON(15 SHU1781 SYMSGF)
     A  99N70                             20MSGCON(35 JOV0013 SYMSGF)
     A                                      HIGHLIGHT
     A  99 70                             20MSGCON(35 SHU1782 SYMSGF)
     A                                      HIGHLIGHT
     A N99N70                             20MSGCON(35 JOV0014 SYMSGF)
     A N99 70                             20MSGCON(35 SHU1783 SYMSGF)
     A                                      HIGHLIGHT
     A          R DET2H                     SPACEB(1)
     A N70                                 2MSGCON(119 JOV0008 SYMSGF)
     A  70                                 2MSGCON(119 SHU1784 SYMSGF)
     A          R DET2                      SPACEB(1)
     A  51N61N70                           2MSGCON(13 SHU1787 SYMSGF)
     A  51N61 70                           2MSGCON(13 SHU1786 SYMSGF)
     A  52N62N70                           2MSGCON(13 JOV0154 SYMSGF)
     A  52N62 70                           2MSGCON(13 SHU1785 SYMSGF)
     A  53N63N70                           2MSGCON(13 JOV0155 SYMSGF)
     A  53N63 70                           2MSGCON(13 SHU1788 SYMSGF)
     A  54N64N70                           2MSGCON(13 JOV0156 SYMSGF)
     A  54N64 70                           2MSGCON(13 SHU1789 SYMSGF)
     A  55N65N70                           2MSGCON(13 JOV0157 SYMSGF)
     A  55N65 70                           2MSGCON(13 SHU1790 SYMSGF)
     A  56N66N70                           2MSGCON(13 JOV0158 SYMSGF)
     A  56N66 70                           2MSGCON(13 SHU1791 SYMSGF)
     A  57N67N70                           2MSGCON(13 JOV0159 SYMSGF)
     A  57N67 70                           2MSGCON(13 SHU1792 SYMSGF)
     A            TVVK           3        16
     A            TVTXT         35        20
     A            TVOAVD         4  0     55EDTCDE(Z)
     A                                    59'/'
     A            TVOPD          7  0     60EDTCDE(Z)
     A  55
     AO 56        TVBNRB         7  0     68EDTCDE(Z)
     A  52 50N70                          68MSGCON(7 JOV0172 SYMSGF)
     A  52 50 70                          68MSGCON(7 SHU1793 SYMSGF)
     A  52        TVBLLB        11  2     76EDTCDE(J)
     A  52        TVPRO          5  2     91EDTWRD('   .  ')
     A            TVBLL         11  2     97EDTCDE(J)
     A          R DET2UFAK                  SPACEB(1)
     A  52N62N70                           2MSGCON(13 JOV0154 SYMSGF)
     A  52N62 70                           2MSGCON(13 SHU1785 SYMSGF)
     A            TVVK           3        16
     A            TVTXT         35        20
     A            TVOAVD         4  0     55EDTCDE(Z)
     A                                    59'/'
     A            TVOPD          7  0     60EDTCDE(Z)
     A  52 50N70                          68MSGCON(7 JOV0172 SYMSGF)
     A  52 50 70                          68MSGCON(7 SHU1793 SYMSGF)
     A          R DETTOT                    SPACEB(1)
     A            THTOT         11  2    104EDTCDE(J)
     A                                      HIGHLIGHT
     A  34                               120'*'
      *_______________________________
      * POSTER  FOR GODSKRIVING VED REKV.
      *"""""""""""""""""""""""""""""""
     A          R GHEAD                     SPACEB(1)
     A N70                                 2MSGCON(79 DIV2409 SYMSGF)
     A  70                                 2MSGCON(79 SHU1794 SYMSGF)
     A                                      HIGHLIGHT
     A          R GDET                       SPACEB(1)
     A            BUBILN        10         2
     A            BUDATO         6  0     11EDTWRD('  .  .  ')
     A            TVTXT         35        20
     A                                    55MSGCON(4 JOV0161 SYMSGF)
     A            BUOAVD         4  0     59EDTCDE(Z)
     A                                    63'/'
     A            BUOPD          7  0     64EDTCDE(Z)
     A N70                                72MSGCON(4 JOV0162 SYMSGF)
     A  70                                72MSGCON(4 SHU1795 SYMSGF)
     A            BUTAVD         4  0     76EDTCDE(Z)
     A                                    80'/'
     A            BUTUNR         8  0     81EDTCDE(Z)
     A            TVBLL         11  2     90EDTCDE(J)
     A  34                               120'*'
     A          R GDETT                      SPACEB(1)
     A N70                                11MSGCON(80 JOV0153 SYMSGF)
     A  70                                11MSGCON(80 SHU1796 SYMSGF)
     A            THTOT         11  2    104EDTCDE(J)
     A                                      HIGHLIGHT
      *_______________________________
      * POSTER  FOR TREKK (UTLEGG)  REKV.
      *"""""""""""""""""""""""""""""""
     A          R THEAD                     SPACEB(1)
     A N70                                 2MSGCON(79 JOV0150 SYMSGF)
     A                                      HIGHLIGHT
     A  70                                 2MSGCON(79 SHU1797 SYMSGF)
     A                                      HIGHLIGHT
     A          R TDET                       SPACEB(1)
     A            BUBILN        10         2
     A            BUDATO         6  0     11EDTWRD('  .  .  ')
     A            TVTXT         35        20
     A N70                                55MSGCON(8 JOV0151 SYMSGF)
     A  70                                55MSGCON(8 SHU1798 SYMSGF)
     A            BUBNR          7  0     64EDTCDE(Z)
     A N70                                72MSGCON(8 JOV0152 SYMSGF)
     A  70                                72MSGCON(8 SHU1799 SYMSGF)
     A            BUBNR2         7  0     80EDTCDE(Z)
     A            TVBLL         11  2     90EDTCDE(J)
     A  34                               120'*'
     A          R TOTA                      SPACEB(2)
     A N70                                 2MSGCON(16 DIV1682 SYMSGF)
     A  70                                 2MSGCON(16 SHU1800 SYMSGF)
     A            XMVAGR        13  2     22EDTCDE(J)
     A N70                                41MSGCON(20 DIV1683 SYMSGF)
     A  70                                41MSGCON(20 SHU1801 SYMSGF)
     A            PRITAX         4  2     54EDTCDE(J)
     A            XVAL           3       100
     A            XMOMS         13  2    102EDTCDE(J)
     A                                      HIGHLIGHT
      *_________________________________________________
      * Format for faktura-total. without Norwegian giro.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     A          R TOTC                      SPACEB(2)
     A N70                                88MSGCON(12 DIV1685 SYMSGF)
     A                                      HIGHLIGHT
     A  70                                88MSGCON(12 SHU1802 SYMSGF)
     A                                      HIGHLIGHT
     A            XVAL           3       100
     A                                      HIGHLIGHT
     A            XBRUTT        13  2    102EDTCDE(J)
     A                                      HIGHLIGHT
     A                                     2' '
     A                                      SPACEB(1)
     A                                    88'================================'
     A                                      HIGHLIGHT
