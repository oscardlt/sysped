      *
      *****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BILL OF LADING (INNGANNGS PARM.)***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01 02 05-08 10-26                      ***
      *** LEDIGE INDIKATORER 29 31 34-94                            ***
      *****************************************************************
      *
     FBILLOF    UF   E           K DISK
     FBOOKING   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FTELL      IF   E           K DISK
     FSYSJ50FH  CF   E             WORKSTN
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
     D  UOPD                  22     28
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C  N30              DO
     C                   MOVE      BLOPD         WSOPDG
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *ZEROS        BLAVD
     C                   MOVE      *ZEROS        BLOPD
     C                   END
     C                   EXFMT     BILDE
     C                   SETOFF                                       30
     C                   MOVEA     '00000'       *IN(95)
     C                   MOVE      WSAVD         UAVD
     C                   MOVE      WSOPD         UOPD
     C                   MOVE      WSSG          USG
     C                   OUT       SAVUDS
     C*
     C   03              GOTO      SLUTT
     C   04              DO
     C                   Z-ADD     1             UFLAGG
     C                   CALL      'SYSJ52H'
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    UFLAGG
     C     WSAVD         IFEQ      0
     C     WSOPD         OREQ      0
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      START
     C*
     C*** N27 ---> IMPORT
     C***  27 ---> EXPORT
     C  N27              Z-ADD     1             UFLAGG
     C   27
     CANN32              Z-ADD     2             UFLAGG
     C   27
     CAN 32              Z-ADD     3             UFLAGG
     C   27WSOPD         IFNE      0
     C                   MOVE      ' '           WSKUNB
     C                   END
     C                   CALL      'SYSJ50I'
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    WSSG
     C                   PARM                    WSKUNB
     C                   PARM                    BLAVD
     C                   PARM                    BLOPD
     C                   PARM                    UFLAGG
  1  C                   PARM      0             ULUNIK            9 0
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   MOVE      WSAVD         UAVD
     C                   MOVE      WSOPDG        UOPD
     C                   MOVE      WSSG          USG
     C                   OUT       SAVUDS
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   SETOFF                                       32
     C     WSOPD         IFGT      0                                            1<-B
     C     BLOPD         IFGT      0                                             2<-B
     C     BLAVD         ORGT      0
     C                   MOVE      'SPO0015'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLT1
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C     BLOPD         COMP      0                                  28
     C  N28BLAVD         COMP      0                                  28
     C*
     C*** TEST SIGNATUR
     C     KODSFK        CHAIN     KODTSF                             99
     C   99              MOVE      'SPS0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLT1
     C*
     C*** TEST AVDELING
     C     KODTAK        CHAIN     KODTA                              98
     C  N98KODTVK        CHAIN     KODTV                              98
     C  N98              DO
     C     WSAVD         CHAIN     TELL                               98
     C   98XAVD0         CHAIN     TELL                               98
     C                   END
     C   98              MOVE      'SPA0006'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      SLT1
     C*
     C*** TEST OPPDRAGSNR
     C  N28              DO                                                     1<-B
     C     WSOPD         COMP      *ZEROS                                 33
     C  N33BLK           CHAIN     BILLOF                             97
     C  N33
     CAN 97
     CAN 27              DO
     C                   SETON                                        32
     C     BLK           CHAIN     BOOKING                            97
     C  N97              MOVE      'G'           BLUR
     C  N97              MOVE      BOST          BLST
     C  N97              MOVE      BOSG          BLSG
     C                   END
     C   97              MOVE      'SPO0002'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      SLT1
     C  N33
     CANN09WSSG          COMP      BLSG                               9999
     C  N33
     CANN99BLST          IFNE      ' '
     C     BLST          COMP      'P'                                9797
     C                   END
     C   97              MOVE      'SPO0006'     MSGNR
     C   99              MOVE      'SPS0014'     MSGNR
     C   97
     COR 99              SETON                                        30
     C  N30
     CANN33              DO                                                      2<-B
     C  N27BLUR          COMP      'F'                                9797
     C  N27
     CAN 97              MOVE      'SPO0104'     MSGNR
     C   27BLUR          COMP      'G'                                9797
     C   27
     CAN 97              MOVE      'SPO0105'     MSGNR
     C   97              SETON                                        30
     C                   END                                                     2<-E
     C                   MOVE      *ZEROS        BLAVD
     C                   MOVE      *ZEROS        BLOPD
     C  N33
     CANN30
     CAN 09              MOVE      WSSG          BLSG
     C  N33
     CANN32              EXCEPT    NYSG
     C   97
     COR 99              GOTO      SLT1
     C                   END                                                    1<-E
     C*
     C*** TEST AVDELING+OPPDRAGSNR FOR KOPIERING
     C   28              DO                                                     1<-B
     C     BLK2          CHAIN     BILLOF                             33
     C  N33              EXCEPT    DUMBL
     C   33              SETON                                        9630
     C   96              MOVE      'SPA0007'     MSGNR
     C   96              GOTO      SLT1
     C                   MOVE      *ZEROS        WSOPD
     C                   END                                                    1<-E
     C*
     C     SLT1          ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BLK           KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     BLK2          KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    WSAVD
     C     KODSFK        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    WSSG
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                  2727
     C*
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C                   IN        SAVUDS
     C*
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      'V'           YVUNI             1
     C                   MOVE      'SF'          YSFUNI            2
     C                   MOVE      *ZEROS        XAVD0             4 0
     C*
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      USG           WSSG
     C                   MOVE      'N'           WSKUNB
     C*
     C                   ENDSR
     C*
     OBILLOFR   E            NYSG
     O                       BLSG
     OBILLOFR   E            DUMBL
