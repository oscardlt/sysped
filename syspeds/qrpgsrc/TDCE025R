      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCE025R MODULE(*LIBL/TDCE025R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3

     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)

     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)

     D Varpgm          Pr                  ExtPgm(BILIBL)

     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7S 0
     D LIN             S              5
     D LINN            S              5S 0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d MyPoDS          ds                  Qualified  dim(1)
     d  TVAVD                         4S 0
     d  TVTDN                         7S 0
     d  TVLI                          5S 0
     d  TVTKBM                        1
     d  TVKREF                       70
     d  TVFGKD                        5
     d  TVKNSS                        8S 0
     d  TVNASS                       30
     d  TVADSS1                      30
     d  TVPNSS                        9
     d  TVPSSS                       24
     d  TVLKSS                        2
     d  TVSKSS                        2
     d  TVTINSS                      17
     d  TVKNKS                        8S 0
     d  TVNAKS                       30
     d  TVADKS1                      30
     d  TVPNKS                        9
     d  TVPSKS                       24
     d  TVLKKS                        2
     d  TVSKKS                        2
     d  TVTINKS                      17
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(OPD)+'¬, '
        +'¬lin¬ : ¬'+%trim(LIN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"securityline" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' TVAVD  = ' + AVD;
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TVTDN  = ' + OPD;
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select * ' +
                             'from DKXVFV ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'TVAVD desc ' +
                             'FOR READ ONLY ';

         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avd     = zhbgetvar('AVD');
         avdn    = c2n2(avd);
         opd     = zhbgetvar('OPD');
         opdn    = c2n2(opd);
         lin     = zhbgetvar('LIN');
         linn    = c2n2(lin);

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).tvtkbm = *blanks;
         leave;
       endif;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tvavd¬ : ¬'+%trim(%editc(mypods(x).TVAVD:'Z'))+'¬, '
          +'¬tvtdn¬ : ¬'+%trim(%editc(mypods(x).TVTDN:'Z'))+'¬, '
          +'¬tvli¬ : ¬'+%trim(%editc(mypods(x).TVLI:'Z'))+'¬, '
          +'¬tvtkbm¬ : ¬'+%trim(mypods(x).TVTKBM)+'¬, '
          +'¬tvkref¬ : ¬'+%trim(mypods(x).TVKREF)+'¬, '
          +'¬tvfgkd¬ : ¬'+%trim(mypods(x).TVFGKD)+'¬, '
          +'¬tvknss¬ : ¬'+%trim(%editc(mypods(x).TVKNSS:'Z'))+'¬, '
          +'¬tvnass¬ : ¬'+%trim(mypods(x).TVNASS)+'¬, '
          +'¬tvadss1¬ : ¬'+%trim(mypods(x).TVADSS1)+'¬, '
          +'¬tvpnss¬ : ¬'+%trim(mypods(x).TVPNSS)+'¬, '
          +'¬tvpsss¬ : ¬'+%trim(mypods(x).TVPSSS)+'¬, '
          +'¬tvlkss¬ : ¬'+%trim(mypods(x).TVLKSS)+'¬, '
          +'¬tvskss¬ : ¬'+%trim(mypods(x).TVSKSS)+'¬, '
          +'¬tvtinss¬ : ¬'+%trim(mypods(x).TVTINSS)+'¬, '
          +'¬tvknks¬ : ¬'+%trim(%editc(mypods(x).TVKNKS:'Z'))+'¬, '
          +'¬tvnaks¬ : ¬'+%trim(mypods(x).TVNAKS)+'¬, '
          +'¬tvadks1¬ : ¬'+%trim(mypods(x).TVADKS1)+'¬, '
          +'¬tvpnks¬ : ¬'+%trim(mypods(x).TVPNKS)+'¬, '
          +'¬tvpsks¬ : ¬'+%trim(mypods(x).TVPSKS)+'¬, '
          +'¬tvlkks¬ : ¬'+%trim(mypods(x).TVLKKS)+'¬, '
          +'¬tvskks¬ : ¬'+%trim(mypods(x).TVSKKS)+'¬, '
          +'¬tvtinks¬ : ¬'+%trim(mypods(x).TVTINKS)+'¬}';

         jsonfix (JSONstring);
         updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
