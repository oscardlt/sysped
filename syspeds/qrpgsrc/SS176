      *********************************************************************
      * Endre total og valutakode
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS176 MODULE(*LIBL/SS176
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSORF    UF   E           k DISK    USROPN
     FGSSPOF    IF   E           k DISK    USROPN
     FGSSLGF    O    E             DISK    USROPN
     FTRACKF    O    E             DISK    USROPN
     FVADR      IF   E           k DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSONO           S              7
     D XXONO           S              7  0
     D DRIVER          S             15
     D SIGN            S             30
     D DATO            S              8
     D KLOKKE          S              4
     D XXKLOKKE        S              4  0
     D LAGREPOD        S              1
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Driver can not be blank.')
     D FEIL02          C                   CONST('Signatur of receiver has to -
     D                                     given.')
     D FEIL03          C                   CONST('Receiver date is not valid.')
     D FEIL04          C                   CONST('Receiver time is not valid.')
     D*XXTXT01         C                   CONST('Put POD on order.')
     D XXTXT01         C                   CONST('Delivered to end customer.')
     D XXTXT02         C                   CONST('Driver:')
     D XXTXT03         C                   CONST('Sign:')
     D XXTXT01L        C                   CONST('Levert endelig mottakerr.')
     D XXTXT02L        C                   CONST('Sjåfør:')
     D XXTXT03L        C                   CONST('Sign:')
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D XXDATO                  1      8  0
     D XXDATOÅÅ                1      4  0
     D XXDATOMM                5      6  0
     D XXDATODD                7      8  0
     D                 DS
     D ORBCD                   1      5
     D ORBCD2                  2      5
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * hvis en har tastet submit fra vedlikehodsbildet, test/lagre
     c     LAGREPOD      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * Test om sjøfør er oppgitt.
     C     DRIVER        IFEQ      *BLANKS
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * Test om signatur er oppgitt.
     C     SIGN          IFEQ      *BLANKS
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UTTEST
     C                   ENDIF
      * Test om mottaksdato.
     C                   MOVE      XXDATOÅÅ      UDD               2 0
     C                   MOVE      XXDATOMM      UMM               2 0
     C                   MOVE      XXDATODD      UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      UTTEST
     C                   ENDIF
      * Test om mottakstid.
     C     XXKLOKKE      IFNE      0
     C                   MOVEL     XXKLOKKE      XN02              2 0
     C     XN02          IFGT      23
     C                   MOVEL     FEIL04        FEILMELDING
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      UTTEST
     C                   ENDIF
     C                   MOVE      XXKLOKKE      XN02              2 0
     C     XN02          IFGT      59
     C                   MOVEL     FEIL04        FEILMELDING
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      UTTEST
     C                   ENDIF
     C                   ENDIF
      *
      * ALT OK - Oppdater  - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      * Hvis endring..
     c     XXONO         chain     GSSORF                             55
     c     *in55         IFeq      *OFF
     c                   MOVE      '6'           ORST
     c                   MOVE      'POD  '       ORST2
     c                   update    gssorfr
      *
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS176'       LGPGM
     C                   Z-ADD     3             LGNIVÅ
     C                   MOVE      XXDATO        LGHDT
     C                   MOVEL     XXKLOKKE      LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      XXONO         LGONO
     C                   MOVE      *ZEROS        LGLNO
     C                   EVAL      LGTXT = XXTXT01
     C                   EVAL      LGANY = XXTXT02
     C                   CAT       DRIVER:1      LGANY
     C                   WRITE     GSSLGFR
     C                   EVAL      LGTXT = XXTXT03
     C                   CAT       SIGN:1        LGTXT
     C                   EVAL      LGANY = *BLANKS
     C                   ADD       1             LGLN
     C                   WRITE     GSSLGFR
      *
     C                   MOVEL     ORTNO         TTAVD
     C                   MOVE      ORTNO         TTOPD
     C                   MOVE      XXDATO        TTDATE
     C                   MOVEL     XXKLOKKE      TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'POD'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT = XXTXT01
     C                   CAT       XXTXT02:3     TTTEXT
     C                   CAT       DRIVER:1      TTTEXT
     C                   CAT       XXTXT03:3     TTTEXT
     C                   CAT       SIGN:1        TTTEXT
n01  C                   EVAL      TTTEXL = XXTXT01L
n01  C                   CAT       XXTXT02l:3    TTTEXT
n01  C                   CAT       DRIVER:1      TTTEXT
n01  C                   CAT       XXTXT03l:3    TTTEXT
n01  C                   CAT       SIGN:1        TTTEXT
     C                   WRITE     TRACKFR
      *
     c                   end
      *
     C     UtTest        endsr
      *************************************
     C     GETORD        BEGSR
      *************************************
     c     XXONO         chain(N)  GSSORF                             55
     c     XXONO         chain     GSSPOF                             55
     C                   MOVE      ORBCD2        VADRNR
     C     VADRK         CHAIN     VADR                               55
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LAGREPOD= zhbgetvar('LAGREPOD')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      DRIVER  = zhbgetvar('DRIVER')
     C                   eval      SIGN    = zhbgetvar('SIGN')
     C                   eval      DATO    = zhbgetvar('DATO')
     C                   eval      XXDATO  = c2n2(DATO)
     C                   eval      KLOKKE  = zhbgetvar('KLOKKE')
     C                   eval      XXKLOKKE= c2n2(KLOKKE)
     C                   eval      WSONO   = zhbgetvar('ORONO')
     C                   eval      XXONO   = c2n2(WSONO)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(XXONO:'Z')))
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   callp     updhtmlvar('VADRNA':VADRNA)
     C                   callp     updhtmlvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updhtmlvar('ORWT':
     C                             %trim(%editc(ORWT:'3')))
     C                   callp     updhtmlvar('ORVOL':
     C                             %trim(%editc(ORVOL:'3')))
     C                   callp     updhtmlvar('DRIVER':DRIVER)
     C                   callp     updhtmlvar('SIGN':SIGN)
     C                   callp     updhtmlvar('DATO':DATO)
     C                   callp     updhtmlvar('KLOKKE':KLOKKE)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS176'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
      * Test bruker
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   EXSR      EXIT
     C                   return
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSORF
     C                   OPEN      GSSLGF
     C                   OPEN      GSSPOF
     C                   OPEN      VADR
     C                   OPEN      TRACKF
      *
     C     VADRK         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    ORCNO
     C                   KFLD                    VADRNR
      *
      * Ved første kall - hent verdier fra fil.
     c     XXONO         IFNE      *zeros
     c     LAGREPOD      ANDNE     'Y'
     C                   EXSR      GETORD
     c                   END
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSLGF
     C                   close     GSSPOF
     C                   close     VADR
     C                   close     TRACKF
     C                   eval      *inlr = *on
      *
     C                   endsr
