      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 ikke ta med alinjer i beregene kunde utestående
********************************************************************
     FFIRM      IF   E           K DISK
     FFAK3      UF   E           K DISK
     FHEADF     UF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FHEAD1     IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR1)
     FKODTV     IF   E           K DISK
     D                 DS
     D  FAAVD                  1      4  0
     D  FAOPD                  5     11  0
     D  FAAVOP                 1     11  0
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK3
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        XXOPKO
     C     *LIKE         DEFINE    FAFAKT        XXFAKT
     C     *LIKE         DEFINE    FAKUNR        XXKUNR
     C     *LIKE         DEFINE    FAAVD         XXAVD
     C     *LIKE         DEFINE    FAOPD         XXOPD
     C*
     C     KEYFA3        KLIST
     C                   KFLD                    XXOPKO
     C                   KFLD                    XXFAKT
     C                   KFLD                    XXKUNR
     C                   KFLD                    XXAVD
     C                   KFLD                    XXOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK5
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        YYOPKO
     C     *LIKE         DEFINE    FASK          YYSK
     C*
     C     KEYFA5        KLIST
     C                   KFLD                    YYOPKO
     C                   KFLD                    YYSK
     C*
     C     KEYHEA        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C*
     C* READ ALL INVOICED RECORDS
     C*
     C                   READ      FIRM                                   33
     C                   MOVE      'F'           XXOPKO
     C     KEYFA3        SETLL     FAK3
     C     START         TAG
     C                   READ      FAK3                                 5333
     C   53              GOTO      START
     C  N33FAOPKO        COMP      'F'                                3333
     C  N33              MOVE      'G'           FAOPKO
     C  N33              UPDATE    FAKTR
     C  N33FAAVOP        IFNE      GAMMEL
     C     FASK          ANDNE     'X'
     C     KEYHEA        CHAIN     HEADF                              44
     C  N44              MOVE      'G'           HEST
     C  N44              UPDATE    HEADFR
     C  N44              MOVE      FAAVOP        GAMMEL           11 0
     C                   END
     C  N33              GOTO      START
     C                   EXSR      STATG
     C*
     C* MERKER INTERNDEBITERINGER
     C*
     C                   EXSR      SUMKUN
     C*
     C                   SETON                                        LR
     C                   RETURN
     C******************************************
     C     STATG         BEGSR
     C******************************************
     C* LESER ALLE OPPDRAG MED STATUS = F
     C* MERKER DISSE MED STATUS 'G'
     C*
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    KOVUNI        WRKUNI
     C     *LIKE         DEFINE    KOVAVD        WRKAVD
     C     KODTKE        KLIST
     C                   KFLD                    WRKUNI
     C                   KFLD                    WRKAVD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL SAMLEFAKTURAFIL
     C*--------------------------------------------------------------
     C     KODTSA        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEAD1
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    HEST          WRST
     C     HEA1KE        KLIST
     C                   KFLD                    KOVAVD
     C                   KFLD                    WRST
     C*
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEADINGREGISTER  HEAD
     C*--------------------------------------------------------------
     C     XEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*
     C*
     C**** FINNER ALLE AVDELINGER PÅ ØNSKET FIRMA OG GÅR I LØKKE
     C*
     C                   MOVE      'V'           WRKUNI
     C                   MOVE      *ZEROS        WRKAVD
     C     KODTKE        SETLL     KODTV
     C*
     C     STAG          TAG
     C                   SETOFF                                       33
     C                   READ      KODTV                                  33
     C   33              GOTO      SLG
     C*
     C* AVDELING FUNNET LESER FØRST DE MED STATUS = 'F'
     C*
     C                   MOVE      'F'           WRST
     C     HEA1KE        SETLL     HEAD1
     C                   SETOFF                                       31
     C*
     C     STAG2         TAG
     C     HEA1KE        READE     HEAD1                                  31
     C  N31XEAKEY        CHAIN     HEADF                              32
     C  N31
     CANN32              MOVE      'G'           HEST
     C  N31
     CANN32              UPDATE    HEADFR
     C  N31              GOTO      STAG2
     C*
     C* LESER NESTE AVDELING
     C*
     C                   GOTO      STAG
     C*
     C     SLG           ENDSR
     C*
     C******************************************
     C     SUMKUN        BEGSR
     C******************************************
     C* AKKUMULERER ÅPNE FAKTURALINJER INN I ET EGET FELT FOR
     C* "SALDOTILLEGG". BRUKES I TILLEGG TIL ØKO.-SALDO V/KREDITT.SJEKK
     C*
     C* OBS!!!! Tar IKKE med åpne linjer til kredit. Dette for å hindre
     C*         at kreative saksbehandlere kan "lure kredittgrensen"
     C*         hvis de har "driti seg ut" f.eks. ved å utlevere varer
     C*         oppdraget er registrert.
     C*        (ved at de lager en SVÆR ÅPEN KREDIT på et annet oppdrag
     C*         - venter til neste dag - så kan de registrere/fakturere
     C*         fritt på oppdraget der de har "driti seg ut".
     C*         Når ferdig - kan en så slette linja og det finnes ikke
     C*         et spor av hvordan det er gjort. SYSTEMA kan få skylden
     C*         for at det kan faktureres på kunder med overskredet
     C*         saldo.)
     C*
     C* NULLSTILLER FØRST
     C*
     C     FIFIRM        SETLL     CUNDL01
     C     *IN33         DOUEQ     '1'
     C     FIFIRM        READE     CUNDL01                                33
     C  N33              MOVE      *ZEROS        SYSALU
     C  N33              EXCEPT    DET
     C                   END
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FAKUNR
     C*
     C     FAKEYB        KLIST
     C                   KFLD                    FAOPKO
     C                   KFLD                    FAFAKT
     C*
     C                   MOVE      ' '           FAOPKO
     C                   MOVE      *ZEROS        FAFAKT
     C*
     C     FAKEYB        SETLL     FAK3
     C                   SETOFF                                       33
     C     *IN33         DOUEQ     '1'
     C     FAKEYB        READE(N)  FAK3                                   33
     C  N33KUNKEY        CHAIN     CUNDL01                            34
     C  N33
     CANN34FASK          IFNE      'F'
     C                   ADD(H)    FABELN        SYSALU
     C                   END
     C  N33
     CANN34              EXCEPT    DET
     C                   END
     C*
     C* SUMMERER OGSÅ FAKTURALINJER TIL SAMLEFAKTURERING
     C*
     C                   MOVE      'C'           FAOPKO
     C*
     C     FAKEYB        SETLL     FAK3
     C                   SETOFF                                       33
     C     *IN33         DOUEQ     '1'
     C     FAKEYB        READE(N)  FAK3                                   33
     C  N33KUNKEY        CHAIN     CUNDL01                            34
     C  N33
     CANN34FASK          IFNE      'F'
N01  C     FASK          IFNE      'A'
     C                   ADD(H)    FABELN        SYSALU
N01  C                   END
     C                   END
     C  N33
     CANN34              EXCEPT    DET
     C                   END
     C                   ENDSR
     C*
     OKUNDR     E            DET
     O                       SYSALU
