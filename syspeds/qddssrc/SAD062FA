      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXX * 01 YBC PTF6 VIS SISTE INNGÅENDE POST FOR HVERT TARIFFNR
P7XXX * 02 YBC PTF9 F6 = LAGE NYE
10262 * 02 YBC PTF10 SATS ER UTVIDERT TIL 7.2, INGEN MERKING
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      HLPDOC('VEDLTAR' 'HELPNU' 'HLP400')
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 47 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '5')
     A  88                                  COLOR(PNK)
     A            TARIFF         8S 0O  5 38
     A  88                                  COLOR(PNK)
     A            BESKR1        31A  O  5  5DSPATR(UL)
     A  88                                  COLOR(PNK)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD062A'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(030 YBC0251 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A            W2NR           1A  I  4  2VALUES(' ' '1' '5')
     A                                      DSPATR(UL)
     A            W2KOD          8D 0B  4 38DSPATR(UL)
     A            W2SØK1        20A  I  4  5DSPATR(UL)
     A                                  3  2'Op Beskrivelse                    -
     A                                        Tariffnr.'
     A                                      DSPATR(HI)
     A                                  2 28MSGCON(015 SHU0980 *LIBL/SYMSGF)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
N02  A                                  1 23'F6=Lage ny'
N02  A                                      COLOR(BLU)
     A                                  1 35'F12=Tilbake'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD062A'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC0251 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          8D 0B  4  5DSPATR(UL)
     A N30                             15  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CA12(12 'Forrige')
     A                                  2  2'-- IFORMASJON FRA TOLLTARIFFEN ----
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(RI)
     A                                  3 10MSGCON(070 DIV1773 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TATANR         8Y 0O  4 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            TAORDK         1A  O  4 21DSPATR(UL)
     A                                      COLOR(YLW)
     A            TAORDB         7Y 2O  4 24DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAEFTB         7Y 2O  4 33DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAEFB          7Y 2O  4 42DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TASTK          1A  O  4 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            TAKAP          1A  O  4 55DSPATR(UL)
     A                                      COLOR(YLW)
     A            TAALFA        15A  O  4 58DSPATR(UL)
     A                                      COLOR(YLW)
     A            TAENHE         3A  O  4 75DSPATR(UL)
     A                                      COLOR(YLW)
     A            TATXT         80A  O  5  1DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(079 YBC2002 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            TAEØSB         7Y 2O  7  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TATSJB         7Y 2O  7 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TATYRB         7Y 2O  7 20DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAISRB         7Y 2O  7 29DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAELLB         7Y 2O  7 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TABULB         7Y 2O  7 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAPOLB         7Y 2O  7 56DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAROMB         7Y 2O  7 65DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(078 YBC2003 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            TAUNGB         7Y 2O  9  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TASLOB         7Y 2O  9 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAMULB         7Y 2O  9 20DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAOULB         7Y 2O  9 29DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAGRLB         7Y 2O  9 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAFÆRB         7Y 2O  9 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAISTB         7Y 2O  9 56DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAMARB         7Y 2O  9 65DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(078 YBC2004 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            TAMEXB         7Y 2O 11  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAVGAB         7Y 2O 11 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12  5MSGCON(015 DIV1774 *LIBL/SYMSGF)
     A            TAREST         2A  O 12 21DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 31MSGCON(015 DIV1775 *LIBL/SYMSGF)
     A            TAKAPA         4A  O 12 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 60MSGCON(005 YBC1791 *LIBL/SYMSGF)
     A            TADATO         8Y 0O 12 66DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 14  2'-- IFORMASJON FRA TOLLTARIFFEN ----
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(RI)
