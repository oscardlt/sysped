      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""" *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXX * 01 YBC PTF6 ENDRE PÅ LEDER-TEKSTER. INGEN MERKING
PXXXX * 02 YBC PTF6 LAGERLOKASJONS TYPE
P6XXX * 03 YBC PTF6 OPSJON 9 = DEFINERE UGYLDIGE LAGERLOKASJONER
P8061 * 04 YBC PTF8 PREFERERT PLUKKLAGER
P8068A* 06 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
N06  A                                      HLPPNLGRP(HLPSCR LL005PNL)
N06  A                                      HLPTITLE('VEDLIKEHOLD LAGERSTEDER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
E03  A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '9')
     A            LSKD           5A  O  5  5
     A            LSTXT         30A  O  5 11
     A            LSTYPE         1A  O  5 42
     A            LSLENG         3  0O  5 48EDTCDE(4)
     A            LSBRED         3  0O  5 54EDTCDE(4)
     A            LSHØYD         3  0O  5 60EDTCDE(4)
N02  A            LSLLT          3A  O  5 65
N04  A            LSPRE          1A  O  5 72
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      OVERLAY
S06  A*         H                           HLPDOC('LL00501' 'LL005' 'SY400LO/L-
S06  A*                                     OGI')
N06  A          H                           HLPPNLGRP(HLPSCR LL005PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/LL005R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC0953 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
E03  A                                  3  2MSGCON(70 YBC0954 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0955 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  5MSGCON(75 YBC0956 SYMSGF)
     A                                      DSPATR(HI)
     A            WSKD           5A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSTXT         30A  B  2 11DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSTYPE         1A  B  2 42DSPATR(UL)
     A            WSLENG         3Y 0B  2 48DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSBRED         3Y 0B  2 54DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSHØYD         3Y 0B  2 60DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
N02  A            WSLLT          3A  B  2 65DSPATR(UL)
N02  A  85                                  DSPATR(RI)
N04  A            WSPRE          1A  B  2 72DSPATR(UL)
N04  A                                      VALUES('J' 'N' ' ')
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(14 YBC0957 SYMSGF)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC0956 SYMSGF)
     A                                      DSPATR(HI)
     A            LSKD           5A  O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            LSTXT         30A  O  2 11DSPATR(UL)
     A                                      DSPATR(HI)
     A            LSTYPE         1A  O  2 42DSPATR(UL)
     A                                      DSPATR(HI)
     A            LSLENG         3Y 0O  2 48DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LSBRED         3Y 0O  2 54DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LSHØYD         3Y 0O  2 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N02  A            LSLLT          3A  O  2 65DSPATR(UL)
N02  A                                      DSPATR(HI)
N04  A            LSPRE          1A  O  2 72DSPATR(UL)
N04  A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(14 YBC0957 SYMSGF)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA06(06 'LAGE NY')
S06  A*         H                           HLPDOC('LL00501' 'LL005' 'SY400LO/L-
S06  A*                                     OGI')
N06  A          H                           HLPPNLGRP(LL00501 LL005PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL005R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC0953 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
S03  A*                                 3  2MSGCON(18 DIV0024 SYMSGF)
S03  A*                                     COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0955 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
