     A*%%TS  SD  20150607  155700  CB          REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 CB  PTF10 PKI, IKKE MERKET
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11133 * 02 YBC PTF11 VEKT ER ENDRET FRA 9.1 TIL 11.3, EGET FELT FOR GARANTI
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SV010PNL)
     A                                      HLPTITLE('REGISTRERING AV TET EKSPO-
     A                                      RT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Sök kunde/leverandör')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVX010R'
     A                                      COLOR(BLU)
     A                                  1 24'Underhåll NCTS Export'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4 12'Avdelning:'
     A            WSAAVD         4Y 0B  4 23TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 30TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20'Välj sökväg med startpunkt'
     A                                      COLOR(WHT)
     A                                  8 20'Ekstern ref:'
     A            WSSOK         35A  B  8 35TEXT('CL-REF')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 20'Datum.'
     A            WSDT           8Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 20'Ärende.nr.'
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Avsändarnr.'
     A            WSKNS          8Y 0B 11 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 47'Datum:'
     A            WSDTS          8Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Mottagarnr.'
     A            WSKNK          8Y 0B 12 35TEXT('Kundenr Kjöper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  44
     AO 96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 47'Datum:'
     A            WSDTK          8Y 0B 12 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20'Signatur:'
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 47'Datum:'
     A            WSDTT          8Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20'Transitnr.:'
     A            WSTRNR        18A  B 14 35TEXT('Tollnr Löpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 16 20MSGCON(014 YBC0053 *LIBL/SYMSGF)
     A            WSST           1A  B 16 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 26  2'F3=Avsluta   F4=Sök avsändare/mott-
     A                                      agare'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 16 39'Blank=alla    Q=Utgående kö  S=Ski-
     A                                      ckat till signering  A=Utgående kö -
     A                                        C=Övefört Evry'
     A                                 17 39'E=Misslyckat till Evry  F=Edifactt-
     A                                      ekniskt fel   N=Kvitterat från TMF -
     A                                        M=Tulltekniska fel'
     A                                 18 39'D=Kanselering innvilgad (IE09)   U-
     A                                      =Har fått MRN-nummer (IE28)  P=Har -
     A                                      fått frigivelse (IE29)'
     A                                 19 39'V=VArukontroll (IE60)'
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20150607  155700  CB          REL-V7R1M0  5770-WDS
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            THAVD          4Y 0O  6  5TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  29                                  COLOR(RED)
     A            THTDN          7Y 0O  6 10TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A            THSG           3A  O  6 18TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            THDT           8S 0O  6 22TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
     A            THTRNR        18A  H
     A            XMRN           1A  H
     A            THGVK          3A  H
     A            THGBL         13S 2H
     A            THGKD          1S 0H
     A            WSMLRN        18A  O  6 31TEXT('MRN/LRN')
     A  55                                  DSPATR(HI)
     A  46                                  COLOR(TRQ)
     A            THST           1A  O  6 50TEXT('Status')
     A N49                                  COLOR(RED)
     A  49                                  COLOR(TRQ)
     A            THNAK         30A  O  6 52
     A  43
     AO 40
     AO 44                                  COLOR(TRQ)
S02  A*           THVKB          9Y 1H
N02  A            THVKB         11Y 3H
     A            WSVKB          6Y 1O  6 83TEXT('Bruttovekt')
     A                                      EDTCDE(4)
     A            THNTK          7Y 0H
     A            WSNTK          5Y 0O  6 71TEXT('KOLLI')
     A                                      EDTCDE(4)
     A            THENKL         1A  O  6 97COLOR(YLW)
     A            WSPLL          1A  O  6 99
     A            FSSOK17       17A  O  6101
     A  40                                  COLOR(TRQ)
     A            THDK           5A  O  6119TEXT('DEKLARASJONSKODE')
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20150607  155353  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Sök')
     A                                      CA12(12 'Forrige')
     A                                      CF15(15 'Skriv ut')
     A                                      CF23(23 'FLERE OPSJON')
     A                                      CF24(24 'FLERE TASTER')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SVX010010 SVX010PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SVX010020 SVX010PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SVX010030 SVX010PNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(SVX010030 SVX010PNL)
     A                                      HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(SVX010040 SVX010PNL)
     A                                      HLPARA(04 004 04 030)
     A          H                           HLPPNLGRP(SVX010055 SVX010PNL)
     A                                      HLPARA(04 031 04 048)
     A          H                           HLPPNLGRP(SVX010060 SVX010PNL)
     A                                      HLPARA(04 050 04 050)
     A          H                           HLPPNLGRP(SVX010040 SVX010PNL)
     A                                      HLPARA(04 051 04 076)
     A          H                           HLPPNLGRP(SVX010070 SVX010PNL)
     A                                      HLPARA(04 077 04 077)
     A          H                           HLPPNLGRP(SVX010075 SVX010PNL)
     A                                      HLPARA(04 078 04 080)
     A          H                           HLPPNLGRP(SVX010030 SVX010PNL)
     A                                      HLPARA(05 001 05 003)
     A          H                           HLPPNLGRP(SVX010040 SVX010PNL)
     A                                      HLPARA(05 004 05 030)
     A          H                           HLPPNLGRP(SVX010055 SVX010PNL)
     A                                      HLPARA(05 031 05 048)
     A          H                           HLPPNLGRP(SVX010060 SVX010PNL)
     A                                      HLPARA(05 050 05 050)
     A          H                           HLPPNLGRP(SVX010040 SVX010PNL)
     A                                      HLPARA(05 051 05 076)
     A          H                           HLPPNLGRP(SVX010070 SVX010PNL)
     A                                      HLPARA(05 077 05 077)
     A          H                           HLPPNLGRP(SVX010075 SVX010PNL)
     A                                      HLPARA(05 078 05 080)
     A          H                           HLPPNLGRP(SVX010040 SVX010PNL)
     A                                      HLPARA(06 001 06 030)
     A          H                           HLPPNLGRP(SVX010055 SVX010PNL)
     A                                      HLPARA(06 031 06 048)
     A          H                           HLPPNLGRP(SVX010060 SVX010PNL)
     A                                      HLPARA(06 050 06 050)
     A          H                           HLPPNLGRP(SVX010040 SVX010PNL)
     A                                      HLPARA(06 051 06 076)
     A          H                           HLPPNLGRP(SVX010070 SVX010PNL)
     A                                      HLPARA(06 077 06 077)
     A          H                           HLPPNLGRP(SVX010075 SVX010PNL)
     A                                      HLPARA(06 078 06 080)
     A          H                           HLPPNLGRP(SVX010080 SVX010PNL)
     A                                      HLPARA(07 001 23 080)
     A                                  1  2'SYSPED/SVX010R'
     A                                      COLOR(BLU)
     A                                  1 25'Underhåll NCTS Export'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Avdelning:'
     A            WSAAVD         4Y 0O  2 13COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 18TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  2 54'51=Arkiv'
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A N70                              3  2'1=Skapa ny 2=Ändra 3=Kopiera  5=Vi-
     A                                      sa 6=Skriv ut 8=Web 9=Logg 11=Skick-
     A                                      a'
     A                                      COLOR(BLU)
     A  70                              3  2'12=Skicka fax/mail  51=Dokumentark-
     A                                      iv'
     A                                      COLOR(BLU)
     A                                  3 73'13=Byt signatur'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd.  Ärende Sig .Datum.. MRN-n-
     A                                      r / LRN-nr    S Mottagare          -
     A                                                   B.vikt Kolli T L Ekste-
     A                                      rn referanse'
     A                                      COLOR(WHT)
     A                                  4119'Typ'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          8Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRNR        18A  B  5 31DSPATR(UL)
     A            W2ST           1A  B  5 50DSPATR(UL)
     A            W2SOK         17A  B  5101DSPATR(UL)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            MSGB31        18A  O  1  2MSGID(&MSGF31 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB32        18A  O  1 22MSGID(&MSGF32 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB33        18A  O  1 42MSGID(&MSGF33 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGF31         7A  P
     A            MSGF32         7A  P
     A            MSGF33         7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Sök')
     A                                      CA12(12 'Forrige')
     A                                      CF15(15 'Skriv ut')
     A                                      CF23(23 'FLERE OPSJON')
     A                                      CF24(24 'FLERE TASTER')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVX010R'
     A                                      COLOR(BLU)
     A                                  1 24'Underåll NCTS Export'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Avdelning:'
     A            WSAAVD         4Y 0O  2 13COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 18TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  2 67'51=Arkiv'
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                  3  2'1=Skapa ny 2=Ändra 3=Kopiera 5=Vis-
     A                                      a  6=Skriv ut 8=Web 9=Logg 11=Skick-
     A                                      a'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd.  Ärende Sig .Datum.. MRN-n-
     A                                      r / LRN-nr    S Mottagare          -
     A                                                   B.vikt Kolli T L Ekste-
     A                                      rn referanse'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          8Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRNR        18A  B  5 31DSPATR(UL)
     A            W2ST           1A  B  5 50DSPATR(UL)
     A            W2SOK         17A  B  5101DSPATR(UL)
     A                                 26  2'F3=Avsluta F5=Uppdatera F6=Skapa n-
     A                                      y F8=Sök F15=Skriv ut'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 8 68 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2'SVX010R'
     A                                      COLOR(BLU)
     A                                  1 10'Ändra status NCTS Export'
     A                                      COLOR(WHT)
     A            THAVD          4Y 0O  1 36TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            THTDN          7Y 0O  1 41TEXT('Oppdragsnr')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            THSG           3A  O  1 49TEXT('Signatur')
     A                                      COLOR(TRQ)
     A                                  2  2'Status:'
     A            WAST           1A  B  2 11DSPATR(UL)
     A                                  2 13'Signatur:'
     A            WAUSER        10A  O  2 23DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 35'Datum:'
     A            WADT           8Y 0O  2 42DSPATR(UL)
     A                                      EDTWRD('    .  .  ')
     A                                      COLOR(WHT)
     A N89                              2 53'Frigi garanti:'
     A N89        WAFGAR         1A  B  2 68DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  3  2'Orsak:  '
     A            WAFT1         50A  B  3 11DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            WAFT2         50A  B  4 11DSPATR(UL)
     A            WAFT3         50A  B  5 11DSPATR(UL)
     A            WAFT4         50A  B  6 11DSPATR(UL)
     A            WAFT5         50A  B  7 11DSPATR(UL)
     A N30                              8  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE03
     A                                      WINDOW(*DFT 10 66 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF10(10 'UTFØR')
     A                                      CA12(12 'Forrige')
     A                                  1  2'SVX010R'
     A                                      COLOR(BLU)
     A                                  1 10'Friställ garanti manuellt'
     A                                      COLOR(WHT)
     A                                  2  2'Garantibelopp tilhörande detta äre-
     A                                      nde blir normalt '
     A                                  3  2'friställt automatisk vid mottag av-
     A                                       IE45.'
     A                                  4  2'Före du manuellt friställer skulde-
     A                                      n måste DU HA KONTROLLERAT'
     A                                      COLOR(RED)
     A                                  5  2'OM ÄRENDET ÄR:    "Arrived at dest-
     A                                      ination".'
     A                                      COLOR(RED)
     A                                  6  2'Om du vill friställa manuellt'
     A            THGVK          3A  O  6 32COLOR(YLW)
     A            THGBL         13Y 2O  6 36DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2'ange evt. kommentar och tryck F10'
     A                                  7 66' '
     A                                  8  2'OBS  Garantien är inte friställd ä-
     A                                      ven om skulden är friställd   '
     A                                      COLOR(YLW)
     A                                  9  2' Kommentar'
     A            WSFT          35A  B  9 13DSPATR(UL)
     A                                  9 66' '
     A N30                             10  2'F10=Friställ  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30                             10  2'Kommentar må oppgis.'
     A                                      COLOR(WHT)
     A                                 10 63' '
      *
     A          R BILDE04
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'SVX010R'
     A                                      COLOR(BLU)
     A                                  1 10'Reservrutin '
     A                                      COLOR(WHT)
     A                                  3  2'Starta reservrutin ? '
     A            WSJN           1A  B  3 24DSPATR(UL)
     A                                      VALUES('J' 'N')
