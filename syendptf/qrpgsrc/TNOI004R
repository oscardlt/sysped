      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI004R MODULE(*LIBL/TNOI004R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
     h debug(*yes)
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd

     FBRIDF     IF   E           K DISK    USROPN
     FSADH      IF   E           K DISK    USROPN
     FSADHOM    IF   E           K DISK    USROPN
     F                                     RENAME(SADHR:SADHROM)
     F                                     PREFIX(OM_)
     FSADOMTH   IF   E           K DISK    USROPN
     F                                     PREFIX(OMT_)
     FSTANDI    IF   E           K DISK    USROPN
     FEDIM2     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D MAX0B           S              8S 0 INZ(99999999)
     D MAX0C           S              6S 0 INZ(666666)
     D MAX0D           S              2S 0 INZ(99)
     D XINST           S              1
     D WM1N07          S              3
     D WM3039E         S              6S 0
     D WM3039EO1       S              6S 0
     D WM3039EO2       S              6S 0
     D WM2005B         S              8S 0 INZ(0)
     D WM5004D         S             10S 0 INZ(0)
     D WMVEN           S              1
     D WM0035          S              1
     D WM9N01          S              1S 0 INZ(3)
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      /free
         exsr Parse;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'M'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"getcmn" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

      /end-free
     c                   If        Error = *blanks
     C                   IF        OPDN < 0
     C     SADOMTHK      SETGT     SADOMTH
     C     SADKEY        READPE    SADOMTH                                33
     C                   IF        *IN33 = *OFF
     C                   MOVE      OMT_SIBEL7    OR_SIBEL7
     C                   ELSE
     C                   EVAL      OPDN = OPDN * -1
     C     SADKEY        CHAIN(N)  SADH                               33
     C     *LIKE         DEFINE    SIBEL7        OR_SIBEL7
     C                   MOVE      SIBEL7        OR_SIBEL7
     C                   EVAL      OPDN = OPDN * -1
     C                   END
     C                   END
     C     SADKEY        Chain     SADH

     C                   If        %Found(SADH)

     C                   If        SITDN < 0
     C                   SELECT
     C                   WHEN      SIBEL7 < OR_SIBEL7
     C                   EVAL      WM1N07 = 'DRE'
     C                   EVAL      WM3039E = S3039EO2
     C                   WHEN      SIBEL7 > OR_SIBEL7
     C                   EVAL      WM1N07 = 'DEB'
     C                   EVAL      WM3039E = S3039EO2
     C                   OTHER
     C     SADKEY        CHAIN     SADHOM                             33
     C                   SELECT
     C                   WHEN      OM_SITYPE = '0021' OR OM_SITYPE = '0022'
     C                   EVAL      WM1N07 = 'DRE'
     C                   EVAL      WM3039E = S3039EO2
     C                   WHEN      OM_SITYPE = '0098'
     C                   EVAL      WM1N07 = 'DSO'
     C                   EVAL      WM3039E = S3039EO1
     C                   OTHER
     C                   EVAL      WM1N07 = 'DEB'
     C                   EVAL      WM3039E = S3039EO2
     C                   ENDSL
     C                   ENDSL

     C     AVDN          Chain     STANDI
     C                   If        %Found(STANDI)
     C                   Eval      WM0035 = S0035
     C                   Eval      WM3039E = S3039E
     C                   Eval      WM3039EO1 = S3039EO1
     C                   Eval      WM3039EO2 = S3039EO2
     C                   Endif
     C                   else
     C                   Exsr      GETVAR
     C                   endif
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬m1N07¬  : ¬'+%trim(WM1N07)+'¬, '
          +'¬m3039e¬ : ¬'+%trim(%editc(WM3039E:'Z'))+'¬, '
          +'¬m3039eo1¬ : ¬'+%trim(%editc(WM3039EO1:'Z'))+'¬, '
          +'¬m3039eo2¬ : ¬'+%trim(%editc(WM3039EO2:'Z'))+'¬, '
          +'¬m2005b¬ : ¬'+%trim(%editc(WM2005B:'Z'))+'¬, '
          +'¬m5004d¬ : ¬'+%trim(%editc(WM5004D:'4'))+'¬, '
          +'¬m0035¬  : ¬'+%trim(WM0035)+'¬, '
          +'¬m9n01¬  : ¬'+%trim(%editc(WM9N01:'Z'))+'¬}';

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));

       wrtsection('JSONlin');

      /end-free

     C                   Endif
     c                   Endif
     c                   Eval      JSONstring =']'
     C                   Callp     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   Callp     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     c                   Eval      JSONstring ='}'
     C                   Callp     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   Callp     wrtsection('JSONlin')

     C                   Exsr      Exit

      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          Begsr
      * Lukk filer
     C                   If        %Found(BRIDF)
     C                   Close     SADH
     C                   Close     SADHOM
     C                   Close     SADOMTH
     C                   Close     STANDI
     C                   Close     EDIM2
     C                   Endif
     C                   Close     BRIDF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   Callp     wrtsection('*fini')
      * Quit
     C                   Eval      *INLR = *on
     C                   Endsr
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   Eval      WSUSER  = zhbgetvar('USER')
     C                   Eval      AVD     = zhbgetvar('AVD')
     C                   Eval      AVDN    = c2n2(AVD)
     C                   Eval      OPD     = zhbgetvar('OPD')
     C                   Eval      OPDN    = c2n2(OPD)
     c                   Endsr
      *********************************************************
     C     INIT          Begsr
      *********************************************************
      * Test innlogging
     C                   Open      BRIDF
     C     WSUSER        Chain     BRIDF
     C                   If        %Found
     C                   Open      SADH
     C                   Open      SADHOM
     C                   Open      SADOMTH
     C                   Open      EDIM2
     C                   Open      STANDI
     C                   Else
     C                   Eval      Error = 'Invalid userID'
     C                   Endif

     C     BILIBL        Ifeq      *blanks
     C                   Callb     'INCGI'
     C                   Parm                    BISTDL
     C                   Else
     C                   Call      BILIBL
     C                   Endif

     C     SADKEY        Klist
     C                   Kfld                    AVDN
     C                   Kfld                    OPDN
     C     SADOMTHK      KLIST
     C                   Kfld                    AVDN
     C                   Kfld                    OPDN
     C                   KFLD                    OMT_SIOMBN
     C                   Z-ADD     99            OMT_SIOMBN
     C     M2KDCK        Klist
     C                   Kfld                    AVDN
     C                   Kfld                    OPDN
     C                   Kfld                    MAX0B
     C                   Kfld                    MAX0C
     C                   Kfld                    MAX0D

     c                   Endsr
      *********************************************************
     C     GETVAR        Begsr
      *********************************************************
     C                   Eval      XINST = 'N'

      * FINNER SANSYNLIG MELDINGSTYPE UTFRA LOGGELEMENT

     C     M2KDCK        Setll     EDIM2

     C     STFMTY        Tag

     C     SADKEY        Readpe    EDIM2                                  33
     C  N33M1001         Ifeq      '830'
     C     MST           Oreq      'D'
     C     MSR           Oreq      'S'
     C     M1N07         Oreq      'DFI'
     C     M1N07         Oreq      'DII'
     C     M1N07         Oreq      'DPU'
     C     M1N07         Oreq      'DKO'
     C     M0068C        Oreq      0
     C     M0068C        Cabeq     0             STFMTY

     C     MSR           Ifeq      'S'
     C     XINST         Andeq     'N'

     C     M1N07         Ifeq      'DMA'
     C     M1N07         Oreq      'DEN'
     C                   Eval      XINST = 'J'
     C                   Endif

     C                   Endif

     C                   Goto      STFMTY

     C                   Else

     C     M1N07         Ifeq      'DME'

     C     SIMI          Ifeq      'I'
     C                   Goto      STFMTY
     C                   Else
     C                   Move      'DKO'         WM1N07
     C                   Endif

     C                   Else

     C     M0065         Ifeq      'NCUMUA'
     C     M1N07         Oreq      'DUA'

     C     XINST         Ifeq      'J'

     C     SIDP          Ifeq      40
     C     SIDP          Oreq      41
     C                   Move      'DEN'         WM1N07
     C                   Else
     C                   Move      'DMA'         WM1N07
     C                   Endif

     C                   Else

     C                   Move      'DKO'         WM1N07

     C                   Endif

     C                   Else
     C                   Eval      *IN33 = *On
     C                   Endif
     C                   Endif
     C                   Endif

     C     *IN33         Ifeq      *On
     C     SIMI          Andeq     'I'
     C                   Move      'DFO'         WM1N07

     C                   Else

     C     SIDP          Ifne      40
     C     SIDP          Andne     41
     C     SIDP          Andne     49
     C                   Move      'DMA'         WM1N07
     C                   Else
     C                   Move      'DFU'         WM1N07
     C                   Endif

     C                   Endif

     C     AVDN          Chain     STANDI
     C                   If        %Found(STANDI)
     C                   Eval      WM0035 = S0035
     C                   Eval      WM3039E = S3039E
     C                   Eval      WM3039EO1 = S3039EO1
     C                   Eval      WM3039EO2 = S3039EO2
     C                   Endif

     C                   Endsr
