      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE USERID -> STANDARDVERDIER ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FSTSTUR    UF   E           K DISK
     FSTSTUR1   IF   E           K DISK
     F                                     RENAME(STSTURR:STSTUR1R)
     FSTSTUR2   IF   E           K DISK
     F                                     RENAME(STSTURR:STSTUR2R)
     FTURER14   IF   E           K DISK
     FSTSOPD    IF   E           K DISK
     FTRACKK01  IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSTSTURFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SFBESK1                1     30
     D  TUBILN                 1     10
     D  TUDT                  12     19  0
     D                 DS
     D  TKDATE                 1      8  0
     D  TKDATEÅ                3      4
     D  TKDATEM                5      6
     D  TKDATED                7      8
     D                 DS
     D  TKTIME                 1      6  0
     D  TKTIMEH                1      2
     D  TKTIMEM                3      4
     C*
     C     STRKEY        KLIST
     C                   KFLD                    W2STAT
     C                   KFLD                    W2PRO
     C     UPDKEY        KLIST
     C                   KFLD                    STTYPE
     C                   KFLD                    STPRO
     C     HeaKey        KLIST
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
      *
     C                   CLEAR                   SFBESK
      * STARTVERDI FOR ALFA??
     C                   MOVE      99999999      W2PRO
     C                   MOVE      'M'           W2STAT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     w2stat        Ifeq      '*'
     C     W2PRO         SETGT     STSTUR1
     C                   READP     STSTUR1                                94
     C                   else
     C     STRKEY        SETGT     STSTUR2
     C     W2STAT        READPE(N) STSTUR2                                94
     C                   end
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C     STANOP        SUB       STANOK        MANKO
     C     STPRO         CHAIN     TURER14                            33
     C   33              CLEAR                   SFBESK
     C                   CLEAR                   SFBESK1
      * ved imptur hent turnr/dato fra første ioodrag
     c     *in33         ifeq      *on
     c     UPDKEY        chain     STSOPD                             33
     c     HeaKey        chain     Headf                              33
     c     *in33         ifeq      *off
     C                   IF        HEPRO = 0
     C                   EXSR      HDATTID
     C                   ELSE
     c                   movel     HEPRO         TUBILN
     c                   movel     HEDTOP        TUDT
     C                   MOVE      SFBESK1       SFBESK
     C                   END
     c                   end
     c                   end
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     w2stat        Ifeq      '*'
     C                   READP     STSTUR1                                94
     C                   else
     C     W2STAT        READPE(N) STSTUR2                                94
     C                   end
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0            NESTE SIDE
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   move      *off          *in30
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C**N94      W2STAT    REDPESTSTUR2             N    94
     C                   GOTO      START2
     C                   END
     C*
     C                   MOVE      'N'           REFRES            1
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C     REFRES        IFEQ      'J'
     C                   GOTO      START
     C                   END
     C                   GOTO      START3
     C*
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '1'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSF
     C                   END
     C     WSNR          IFEQ      '9'                                          FULLFØRE
     C                   MOVE      ' '           WSNR
     C                   EXSR      FULSF
     C                   END
     C     WSNR          IFEQ      'Å'                                          ÅPNE
     C     WSNR          OREQ      'O'
     C                   MOVE      ' '           WSNR
     C                   EXSR      ÅPNE
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
      ***********************************************
     C     HDATTID       BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       TKCLID
     C                   MOVEL     STPRO         TKCLID
     C                   CAT       STTYPE:1      TKCLID
     C     TKCLID        SETGT     TRACKK01
     C     TKCLID        READPE    TRACKK01                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        TKKODE = 'FTS'
     C                   LEAVE
     C                   END
     C     TKCLID        READPE    TRACKK01                               33
     C                   ENDDO
      *
     C                   IF        *IN33 = *OFF
     C                   EVAL      SFBESK = TKDATEÅ + '.'
     C                             + TKDATEM + '.'
     C                             + TKDATED + ' '
     C                             + TKTIMEH + ':'
     C                             + TKTIMEM
     C     TKCLID        SETGT     TRACKK01
     C     TKCLID        READPE    TRACKK01                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        TKKODE = 'FTR'
     C                   EVAL      SFBESK = %TRIM(SFBESK) + '  '
     C                             + TKDATEÅ + '.'
     C                             + TKDATEM + '.'
     C                             + TKDATED + ' '
     C                             + TKTIMEH + ':'
     C                             + TKTIMEM
     C                   LEAVE
     C                   END
     C     TKCLID        READPE    TRACKK01                               33
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   MOVE      SFBESK        XXBESK           30
     C                   CALL      'STSOPD'
     C                   PARM                    STTYPE
     C                   PARM                    STPRO
     C                   PARM                    STANOP
     C                   PARM                    MANKO
     C                   PARM                    XXBESK
      * RETURNERER EVT ENDREDE VERDIER
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     FULSF         BEGSR
     C***********************************************
     C     STSTAT        IFNE      'M'
     C     STSTAT        Andne     'G'
     C                   MOVE      'JOV1362'     MSGNR
     C                   move      *on           *in30
     C                   else
     C                   Call      'STSOPDB'
     C                   Parm                    sttype
     C                   Parm                    stpro
     C                   Parm                    xIN30             1
     C     xIN30         IFEQ      '1'
     C                   MOVE      'JOV1365'     MSGNR
     c                   MOVE      *ON           *IN30
     C                   Else
     C                   CALL      'STSTURB'
     C                   PARM                    STTYPE
     C                   PARM                    STPRO
     C                   END
     C                   end
      *
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C*
     C                   MOVE      'J'           REFRES
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     ÅPNE          BEGSR
     C***********************************************
     C     UPDKEY        CHAIN     STSTUR                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      ' '           STSTAT
     C                   UPDATE    STSTURR
     C                   END
      *
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C*
     C                   MOVE      'N'           REFRES
     C*
     C                   ENDSR
     C*
