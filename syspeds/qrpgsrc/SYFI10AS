********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Tilbakefør også INTERNE kolli-IDs
10XXX * 02 JOV PTF10 Tilbakefør også M/O-kolli-IDs
10XXX * 03 JOV PTF10 Tillate tilbakekalling også av status Blank
********************************************************************
     FHEADF     UF   E           K DISK
     FDISP      UF   E           K DISK
     FKOSBU9    UF   E           K DISK    USROPN
     FFAK4      UF   E           K DISK    USROPN
     FDOKUF     UF   E           K DISK    USROPN
     FDOKUFM    UF A E           K DISK    USROPN
S01  F*FIRFB     IF   E           K DISK    USROPN
S01   *
     D                 DS
     D  DFBREF                 1     17
     D  DFBRAV                 6      9
     D  DFBROP                11     17
S01  D*                DS
S01  D* FMMRK1                 1     35
S01  D* CLID1_8                1      8
S01  D*                DS
S01  D* DSIDPK                 1      1
S01  D* DSIDLA                 2      3
S01  D* DSIDLE                 4      8
S01  D* DSID1_8                1      8
     C
     C                   exsr      INIT
     C
     C                   exsr      CHECKSR
     c     Test          ifne      'J'
     c     UUFUNK        andne     '1'
     C                   exsr      SLETTO
     c                   endif
     C
     c                   seton                                        LR
     c                   return
      *
      ***********************************************
     C     CHECKSR       BEGSR
      ***********************************************
      * ordrestatus må være blank / disp = E
      * og den må ikke være satt på tur (lite sansynlig med status E :=)
      *
     C     KKKHEA        CHAIN(N)  HEADF                              33
     C  N33HEST          COMP      ' '                                3333
     C  N33HEPRO         COMP      *ZEROS                             3333
     C  N33KKKHEA        CHAIN(N)  DISP                               33
S03  C* N33DISTA1        COMP      'E'
N03  C  N33DISTA1        ifne      'E'
N03  C     DISTA1        andne     ' '
N03  c                   move      *on           *in33
N03  c                   endif
      *
     c                   move      *IN33         UUFUNK
      *
     c                   endsr
      *
      ***********************************************
     C     SLETTO        BEGSR
      ***********************************************

     c                   open      KOSBU9
     c                   open      FAK4
     c                   open      DOKUF
     c                   open      DOKUFM
s01  c*                  open      FIRFB
      *
      *
     C     KKKHEA        CHAIN     HEADF                              33
     c   33              move      *IN33         UUFUNK
     c   33              goto      UtSl
      *
      * sletting kan utføres
     C                   Move      'F'           UUFUNK
      * Headf
     C                   MOVE      'S'           HEST
     C                   MOVE      *BLANKS       HENAS
     C                   MOVEL     '*SLETTET*'   HENAS
     C                   MOVE      'J'           HEPK3
     C                   MOVEL     '*D*'         HXSNDN
     C                   MOVE      *ZEROS        TRAVD0
     C                   MOVE      *ZEROS        TROPD0
     C                   UPDATE    HEADFR
      * Disp
     C     KKKHEA        CHAIN     DISP                               33
     C  N33              MOVE      'D'           DISTA1
     C  N33              UPDATE    DISPR
      *
      * Slett REKVISISJONER
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      'S'           BUST
     C     BUBLST        IFEQ      'T'
     C                   MOVE      'E'           BUBLST
     C                   END
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C                   END
      *
      * Slett fakturalinjer
     C     KKKHEA        SETLL     FAK4
     C     KKKHEA        READE     FAK4                                   33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    FAKTR
     C     KKKHEA        READE     FAK4                                   33
     C                   END
      *
     C* SLETT EVT FRAKTBREV på innland  (lite sansynlig når status er E, men...)
     C     DOKUFK        CHAIN     DOKUF                              33
     C     *IN33         ifeq      *OFF
     C                   delete    dokufr
     C                   endif                                                  E1
      * Fjern DFBref mm i bestillende
     C     DOKUF0K       CHAIN     DOKUF                              33
     C     *IN33         ifeq      *OFF
     C                   eval      DFBREF=*blanks
     c                   eval      DFST  =*blanks
     c     DFKDME        ifeq      'P'
     c                   eval      DFKDME='S'
     c                   endif
     C                   UPDATE    DOKUFR
     C                   endif                                                  E1
      *
      * Slette kolli IDS( MEN DERSOM EKSTERN /IKKE EGENPRODUSERTE - TIlbakefør de)
s01  C*    UUFIRM        CHAIN     FIRFB                              33
s01  C*                  MOVE      '3'           DSIDPK                         Konstant
s01  C*                  MOVE      FIIDLA        DSIDLA                         Land
s01  C*                  MOVE      FIIDLE        DSIDLE                         Levnr
     C     DOKUFK        SETLL     DOKUFM
     C     DOKUFK        READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    DOKUFMR
     C     FMST          IFEQ      'E'                                          Eksterne
N01  C     FMST          oreq      'I'                                          Interne
N02  C     FMST          oreq      'M'                                          Mankomerkede
N02  C     FMST          oreq      'O'                                          Ompakkede/originaler
s01  C*    CLID1_8       ANDNE     DSID1_8
     c                   move      UFAVD         FMAVD
     c                   move      UFOPD         FMOPD
     c                   move      UFFBNR        FMFBNR
     C                   write     DOKUFMR
     C                   ENDIF
     C     DOKUFK        READE     DOKUFM                                 33
     C                   END
      *
      *
     C     Utsl          Tag
     c                   close     KOSBU9
     c                   close     FAK4
     c                   close     DOKUF
     c                   close     DOKUFM
s01  c*                  close     FIRFB
      *
     C                   ENDSR

      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UUFIRM            2
      * fra avd/opd
     C                   PARM                    UFAVD             4 0
     C                   PARM                    UFOPD             7 0
     C                   PARM                    UFFBNR            3 0
      * DUP-en
     C                   PARM                    UUAVD             4
     C                   PARM                    UUOPD             7
      *
     C                   PARM                    UUFUNK            1
      *
      * UUFUNK INN   = T = Test (for å se om Ftast er tillat/skal vises  i SYFI10A)
      * UUFUNK RETUR = 1 = IKKE TILLATT  0=TILLATT(v/test) F=Fullført/Ferdig slettet.
     C                   move      UUAVD         DUPAVD            4 0
     C                   move      UUOPD         DUPOPD            7 0
     c     UUFUNK        ifeq      'T'
     c                   move      'J'           Test              1
     c                   endif
      * Keys
      * DUP-en...
     C     KKKHEA        KLIST
     C                   KFLD                    DUPAVD
     C                   KFLD                    DUPOPD
     C     KOSB9K        KLIST
     C                   KFLD                    YYST              1
     C                   KFLD                    DUPAVD
     C                   KFLD                    DUPOPD
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    DUPAVD
     C                   KFLD                    DUPOPD
     C                   move      'F'           DFRI
      * Overordnet
     C     KKKHE0        KLIST
     C                   KFLD                    UFAVD
     C                   KFLD                    UFOPD
     C     DOKUF0K       KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    UFAVD
     C                   KFLD                    UFOPD
     C                   KFLD                    UFFBNR
     C                   endsr
