      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSL02 MODULE(*LIBL/STSL02
CRTPGM*        *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FLLWW161   UF A E           K DISK    usropn
     FLLWW162   UF A E           K DISK    usropn
     FLLGRPK05  IF   E           K DISK    USROPN
     FLLHEAD15  IF   E           K DISK    usropn
     FLLSERL02  UF   E           K DISK    usropn
     FLLAGJD02  IF   E           K DISK    usropn
     FLLAGJL98  IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     DwsALN            s              7
     DBwsALN           s              7  0
     DWSUser           s             10
     DwsNULL           s              1
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D MsgId           s              7
     D State           ds                  based(StateP)
     D  XMR                 1001   1200  0 DIM(40)
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D Feiltxt         s             50
     D FeilKode        s              1
     D XTXT01          C                   CONST('OPPGITT PLUKKLISTENR FINNES I-
     D                                     KKE.')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      * Set libl
     C                   exsr      Init
     C     FEILTXT       CABNE     *BLANKS       SLUTT
      ***
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      *
     C                   MOVE      'J'           XFULLFØR          1
     C     LHMN          SETLL     LLWW161
     C     LHMN          READE     LLWW161                                50
     C                   DOW       *IN50 = '0'
     C     LW1WNT        SUB       LW1PNT        LW1RNT            7 0
     C                   IF        LW1RNT <> 0
     C                   MOVE      'N'           XFULLFØR
     C                   END
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C     LHMN          READE     LLWW161                                50
     C  N50              ENDDO                                                  *hival
      *
     C                   callp     wrtsection('tableend')
     C                   IF        XFULLFØR = 'J'
     C                   callp     wrtsection('tabfull')
     C                   END
     C                   callp     wrtsection('tabslutt')
      *
     C     SLUTT         TAG
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   CLOSE     LLWW161
     C                   CLOSE     LLWW162
     C                   CLOSE     LLSERL02
     C                   CLOSE     LLHEAD15
     C                   CLOSE     LLGRPK05
     C                   CLOSE     LLAGJD02
     C                   CLOSE     LLAGJL98
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
     C                   eval      WSALN    = zhbgetvar('WSALN')
     C                   eval      BWSALN   = c2n2(WSALN)
     C                   eval      WSNULL   = zhbgetvar('WSNULL')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      StateP = RtvUsrSpcPtr(UsrSpcName:
     C                             UsrSpcLib)
      *
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
      * Repeat UserId for the next invocation
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('User':WSuser)
     C                   callp     updhtmlvar('WSALN':WSALN)
     C                   callp     updhtmlvar('WSMN':
     C                             %trim(%editc(LHMN:'Z')))
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editc(LHGRP:'3')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editc(LHPNR:'3')))
     C                   callp     updhtmlvar('UsrSpcName':UsrSpcName)
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     WSUser        CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      LLWW161
     C                   open      LLWW162
     C                   open      LLSERL02
     C                   open      LLHEAD15
     C                   open      LLGRPK05
     C                   open      LLAGJD02
     C                   open      LLAGJL98
      *
     C     LLHEAD15K     KLIST
     C                   KFLD                    GKGRP
     C                   KFLD                    GKPNR
     C                   KFLD                    BWSALN
     C     LLAGJD02K     KLIST
     C                   KFLD                    GKGRP
     C                   KFLD                    GKPNR
     C                   KFLD                    LHMN
     C     LLWW161K      KLIST
     C                   KFLD                    SNMNU
     C                   KFLD                    SNVNU
     C     LLWW161K2     KLIST
     C                   KFLD                    VDMNL
     C                   KFLD                    VDVNL
      *
     C                   EXSR      INIT2
      *
     C                   endsr
      *
      *=====================================================================******
      *  Initier filer
      *=====================================================================******
     C     INIT2         BEGSR
      *
     C     BIKUNR        CHAIN     LLGRPK05                           33
     C     LLHEAD15K     CHAIN     LLHEAD15                           33
     C  N33LHSTA         COMP      ' '                                3333
     C                   IF        *IN33
      * FEIL
     C                   exsr      SetSklERR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   EVAL      FEILKODE = '1'
     C                   EVAL      FEILTXT  = XTXT01
     C                   callp     updHTMLvar('FEILTXT':FEILTXT)
     C                   callp     updHTMLvar('FEILKOD':FEILKODE)
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
     C                   GOTO      SLINIT2
     C                   END
      *
     C                   IF        WSNULL = 'J'
      * NULLSTILL TIDLIGERE SKANNET SERIENR
     C     LHMN          SETLL     LLSERL02
     C     LHMN          READE     LLSERL02                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      *ZEROS        SNMNU
     C                   MOVE      *ZEROS        SNVNU
     C                   MOVE      *ZEROS        SNVNI
     C                   UPDATE    LLSERR
     C     LHMN          READE     LLSERL02                               33
     C                   ENDDO
     C                   END
      *
      * TØMME ARBEIDSFILER
     C     LHMN          SETLL     LLWW161
     C     LHMN          READE     LLWW161                                33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    LLWW161R
     C     LHMN          READE     LLWW161                                33
     C                   ENDDO
     C                   CLEAR                   LLWW161R
      *
     C     LHMN          SETLL     LLWW162
     C     LHMN          READE     LLWW162                                33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    LLWW162R
     C     LHMN          READE     LLWW162                                33
     C                   ENDDO
     C                   CLEAR                   LLWW162R
      *
      * OPPDATER ARBEIDSFILER
     C                   MOVE      *ZEROS        XVNL              3 0
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        ((VDVNL <> XVNL) OR (VDVNL = 0))
     C     VDMN          CHAIN     LLAGJL98                           33
     C                   MOVE      GKGRP         LW1GRP
     C                   MOVE      GKPNR         LW1PNR
     C                   MOVE      BWSALN        LW1ALN
     C                   MOVE      VAANR         LW1ANR
     C                   MOVE      VDMNL         LW1MNU
     C                   MOVE      VDVNL         LW1VNU
     C                   MOVE      VALLOK        LW1LC
     C                   MOVE      VDLANT        LW1WNT
     C                   WRITE     LLWW161R
     C                   ELSE
     C     LLWW161K2     CHAIN     LLWW161                            33
     C                   ADD       VDLANT        LW1WNT
     C                   UPDATE    LLWW161R
     C                   END
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO
      *
     C     LHMN          SETLL     LLSERL02
     C     LHMN          READE(N)  LLSERL02                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      SNMNU         LW2MNU
     C                   MOVE      SNVNU         LW2VNU
     C                   MOVE      SNMNI         LW2MNI
     C                   MOVE      SNVNI         LW2VNI
     C                   MOVE      SNSN          LW2SN
     C     SNMNI         CHAIN     LLAGJL98                           33
     C                   MOVE      VALLOK        LW2LC
     C                   WRITE     LLWW162R
      *
     C     LLWW161K      CHAIN     LLWW161                            33
     C                   DOW       *IN33 = *OFF
     C                   IF        LW1WNT = LW1PNT
     C                   UPDATE    LLWW161R
     C                   ELSE
     C                   LEAVE
     C                   END
     C     LLWW161K      READE     LLWW161                                33
     C                   ENDDO
     C                   ADD       1             LW1PNT
     C                   UPDATE    LLWW161R
      *
     C     LHMN          READE(N)  LLSERL02                               33
     C                   ENDDO
      *
     C     SLINIT2       endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklERR     begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSL02'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSL03'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('WSALN':WSALN)
     C                   callp     updhtmlvar('LW1MNU':
     C                             %trim(%editc(LW1MNU:'Z')))
     C                   callp     updhtmlvar('LW1VNU':
     C                             %trim(%editc(LW1VNU:'Z')))
     C                   callp     updhtmlvar('LW1ANR':LW1ANR)
     C                   callp     updhtmlvar('LW1LC':LW1LC)
     C                   callp     updhtmlvar('LW1WNT':
     C                             %trim(%editc(LW1WNT:'Z')))
     C                   callp     updhtmlvar('LW1PNT':
     C                             %trim(%editc(LW1PNT:'Z')))
     C                   callp     updhtmlvar('LW1RNT':
     C                             %trim(%editc(LW1RNT:'Z')))
     C                   callp     updhtmlvar('UsrSpcName':UsrSpcName)
      *
     C                   endsr
