      *
      ***********************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV FLYFRAKTPRISER           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-69 73-77    ***
      ***                    81-90 95-97 99                   ***
      ***********************************************************
     FSTED2     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTFS    IF   E           K DISK
     FAIRLH     UF A E           K DISK
     FAIRLD     UF A E           K DISK
     FSYEF03FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START0        TAG
     C*
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     4             LINNBR
     C     STAR0B        TAG
     C                   SETOFF                                       2076
     C                   EXFMT     BILDE01
     C                   SETOFF                                       707172
     C                   SETOFF                                       30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C* SEARCH
     C   04              DO
     C                   EXSR      SØKDTA
     C   30              GOTO      START0
     C  N30              GOTO      STAR0B
     C                   END
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C*
     C                   EXSR      TESTB0
     C   30
     COR 12              GOTO      STAR0B
     C   03              GOTO      SLUTT
     C     *IN11         IFEQ      *ON
     C                   EXSR      SLTAVT
     C   30              GOTO      START0
     C  N30              GOTO      STAR0B
     C                   ENDIF
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIKU         SETLL     AIRLD
     C     START1        TAG
     C     PRIKU         READE(N)  AIRLD                                  94
     C   94              MOVE      *ZEROS        AIRWEI
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
     C                   MOVE      *BLANKS       XDLT
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  AIRLD                                  94
     C  N94              MOVE      AIRWEI        YIRWEI            5 0
     C  N94              MOVE      AIRCCP        YIRCCP            1
     C  N94              MOVE      AIRCOD        YIRCOD            1
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
      *
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              EXSR      MOVB1
     C   06              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C   12              GOTO      START0
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94PRISK3        SETLL     AIRLD
     C  N94PRIKU         READE     AIRLD                                  94
     C                   GOTO      START2
     C                   END
     C*
     C     START4        TAG
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISKS        CHAIN     AIRLD                              33
     C  N33              DELETE    AIRLDR
     C                   EXSR      MOVB1
     C                   EXSR      MOVB2
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START4
     C                   END
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   MOVE      'B'           WSRCCP
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(78)
     C                   SETOFF                                       30
      *
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB1
     C                   GOTO      START3
     C                   END
     C   03              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB1
     C                   GOTO      START0
     C                   END
      * Søk på gyldige koder
     C     *IN04         IFEQ      *ON
     C                   EXSR      SOEK
     C                   GOTO      START5
     C                   ENDIF
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     NY            IFEQ      'J'
     C                   GOTO      START5
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   12              GOTO      START0
     C   03              GOTO      SLUTT
     C     PRISK1        CHAIN     AIRLH                              30
     C   30              GOTO      START6
     C                   MOVE      W2RLIN        AIRLIN
     C                   MOVE      W2RPFR        AIRPFR
     C                   MOVE      W2RPTO        AIRPTO
     C     KEYFLY        CHAIN     KODTFS                             33
     C  N33              MOVE      KFSNAV        WSAIRL
     C                   MOVEL     AIRPFR        KEYSTE            5
     C     KEYSTE        CHAIN     STED2                              33
     C  N33              MOVE      ST2NVN        WSFROM
     C                   MOVEL     AIRPTO        KEYSTE            5
     C     KEYSTE        CHAIN     STED2                              33
     C  N33              MOVE      ST2NVN        WSTO
     C                   EXSR      FLYT2
     C                   UPDATE    AIRLHR
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB0        BEGSR
     C***********************************************
      * TEST AIRLINER
     C     KEYFLY        CHAIN     KODTFS                             70
     C  N70              MOVE      KFSNAV        WSAIRL
     C   70              DO
     C                   SETON                                            30
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     4             LINNBR
     C                   MOVE      'SPF0009'     MSGNR
     C                   GOTO      SLTB0
     C                   END
      *
      * TEST FROM
     C                   MOVEL     AIRPFR        KEYSTE            5
     C     KEYSTE        CHAIN     STED2                              71
     C  N71              MOVE      ST2NVN        WSFROM
     C   71              DO
     C                   SETON                                        30
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     6             LINNBR
     C                   MOVE      'SPD0016'     MSGNR
     C                   GOTO      SLTB0
     C                   END
      *
      * TEST TO
     C                   MOVEL     AIRPTO        KEYSTE            5
     C     KEYSTE        CHAIN     STED2                              72
     C  N72              MOVE      ST2NVN        WSTO
     C   72              DO
     C                   SETON                                        30
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     8             LINNBR
     C                   MOVE      'SPD0016'     MSGNR
     C                   GOTO      SLTB0
     C                   END
      *
      * HENTER FRA HEADERFILE
     C     KEYH          CHAIN(N)  AIRLH                              33
     C   33              MOVE      FICURR        WSRCUR
     C   33              MOVE      *ZEROS        WSROMR
     C  N33              MOVE      AIROMR        WSROMR
     C  N33              MOVE      AIRCUR        WSRCUR
     C**N33                MOVE AIRIAT    WSRIAT
     C                   SETON                                        20
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     10            LINNBR
     C*
     C     FEIL0         TAG
      *
     C                   EXFMT     BILDE01
     C                   SETOFF                                       30
     C   12
     COR 03              GOTO      SLTB0
     C* SEARCH
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   EXSR      SØKDTA
     C                   GOTO      FEIL0
     C                   END
      *
      * F2 = KOPIERING
      *
     C   02              DO
     C                   MOVE      AIRLIN        GMRLIN
     C                   MOVE      AIRPFR        GMRPFR
     C                   MOVE      AIRPTO        GMRPTO
     C     KEYH2         CHAIN     AIRLH                              76
     C   76              SETON                                        30
     C   76              Z-ADD     23            POSNBR
     C   76              Z-ADD     16            LINNBR
     C   76              MOVE      'SHU0232'     MSGNR
     C   76              GOTO      FEIL0
     C                   EXSR      KOPIE
     C                   GOTO      FEIL0
     C                   END
      *
      * OPPDATERER REGISTER
     C     KEYH          CHAIN     AIRLH                              33
     C                   EXSR      FLYT2
     C   33              WRITE     AIRLHR
     C  N33              UPDATE    AIRLHR
     C*
     C     SLTB0         ENDSR
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C                   MOVEL     'AIRCOD'      TYPE             10
     C                   MOVEL     WSRCOD        VERDI             5
     C                   MOVEL     '0'           WIN30             1
     C                   CALL      'SYFAKDT'
     C                   PARM                    TYPE
     C                   PARM                    VERDI
     C                   PARM                    WIN30
     C     WIN30         IFEQ      '1'
     C                   MOVE      'BBA0132'     MSGNR
     C                   MOVE      *ON           *IN80
     C                   MOVE      *ON           *IN30
     C                   ENDIF
     C  N30WSRWEI        IFEQ      0
     C                   MOVE      'SHU0160'     MSGNR
     C                   SETON                                        3079
     C                   END
     C  N30PRISK         CHAIN     AIRLD                              33
     C  N30
     CANN33              MOVE      'SHU0132'     MSGNR
     C  N30
     CANN33              SETON                                        3080
     C   30              GOTO      SLTB1
     C                   END
      *
     C     WSRPRP        IFEQ      0
     C                   Z-ADD     1             WSRPRP
     C                   END
      *
     C     PRISK         CHAIN     AIRLD                              33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    AIRLDR
     C   33              WRITE     AIRLDR
     C                   MOVE      *BLANKS       XDLT
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C   40              MOVE      'N'           NY                1
     C  N40              MOVE      'J'           NY
     C                   SETOFF                                       40
     C                   EXSR      MOVB1
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          ANDNE     '*DELETE'
     C                   EXSR      MOVPRW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XDLT          ANDNE     '*DELETE'
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      WSRCCP        AIRCCP
     C                   MOVE      WSRCOD        AIRCOD
     C                   MOVE      WSRGRN        AIRGRN
     C                   MOVE      WSRWEI        AIRWEI
     C                   MOVE      WSRAWB        AIRAWB
     C                   MOVE      WSRHAW        AIRHAW
     C                   MOVE      WSRHAM        AIRHAM
     C                   MOVE      WSRPRP        AIRPRP
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      AIRCCP        WSRCCP
     C                   MOVE      AIRCOD        WSRCOD
     C                   MOVE      AIRGRN        WSRGRN
     C                   MOVE      AIRWEI        WSRWEI
     C                   MOVE      AIRAWB        WSRAWB
     C                   MOVE      AIRHAW        WSRHAW
     C                   MOVE      AIRHAM        WSRHAM
     C                   MOVE      AIRPRP        WSRPRP
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSRCOD
     C                   MOVE      *ZEROS        WSRGRN
     C                   MOVE      *ZEROS        WSRWEI
     C                   MOVE      *ZEROS        WSRAWB
     C                   MOVE      *ZEROS        WSRHAW
     C                   MOVE      *ZEROS        WSRHAM
     C                   MOVE      *BLANKS       WSRPRP
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       AIRCOD                                     AIRLD)
     C                   MOVE      *ZEROS        AIRWEI                                     AIRLD)
     C                   MOVE      *ZEROS        AIRGRN                                     AIRLD)
     C                   MOVE      *ZEROS        AIRAWB                                     AIRLD)
     C                   MOVE      *ZEROS        AIRHAW                                     AIRLD)
     C                   MOVE      *ZEROS        AIRHAM                                     AIRLD)
     C                   MOVE      *ZEROS        AIRPRP                                     AIRLD)
     C                   MOVE      *BLANKS       AIRCO1                                     AIRLD)
     C                   MOVE      *ZEROS        AIRBK1                                     AIRLD)
     C                   MOVE      *BLANKS       AIRCO2                                     AIRLD)
     C                   MOVE      *ZEROS        AIRBK2                                     AIRLD)
     C                   MOVE      *BLANKS       AIRCO3                                     AIRLD)
     C                   MOVE      *ZEROS        AIRBK3                                     AIRLD)
     C                   MOVE      *BLANKS       AIRCO4                                     AIRLD)
     C                   MOVE      *ZEROS        AIRBK4                                     AIRLD)
     C                   MOVE      '*DELETE'     XDLT
      *
     C                   ENDSR
      *
     C***********************************************
     C     FLYT2         BEGSR
     C***********************************************
     C                   MOVE      WSRCUR        AIRCUR
     C                   MOVE      WSROMR        AIROMR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C  N20LINNBR        IFEQ      4
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   CALL      'SYFI10K'
     C                   PARM                    AIRLIN
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C  N20LINNBR        IFEQ      6
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    AIRPFR
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C  N20LINNBR        IFEQ      8
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    AIRPTO
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C   20LINNBR        IFEQ      10
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      'J'           USØK              1
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    WSRCUR
     C                   Z-ADD     12            POSNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C   20LINNBR        IFEQ      16
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   CALL      'SYFI10K'
     C                   PARM                    FLYLIN
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C   20LINNBR        IFEQ      18
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    FLYPFR
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C   20LINNBR        IFEQ      20
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    FLYPTO
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     SOEK          BEGSR
     C***********************************************
     C                   MOVEL     'AIRCOD'      WFTYP1           10
     C                   MOVE      *BLANKS       WFKOD1            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    WFTYP1
     C                   PARM                    UUPDAT            1
     C                   PARM                    WFKOD1
     C                   MOVEL     WFKOD1        WSRCOD
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KEYFLY        KLIST
     C                   KFLD                    FSUNIK
     C                   KFLD                    AIRLIN
     C                   MOVE      'FS'          FSUNIK            2
     C     KEYH          KLIST
     C                   KFLD                    AIRLIN
     C                   KFLD                    AIRPFR
     C                   KFLD                    AIRPTO
     C     KEYH2         KLIST
     C                   KFLD                    FLYLIN
     C                   KFLD                    FLYPFR
     C                   KFLD                    FLYPTO
     C     KEYH3         KLIST
     C                   KFLD                    GMRLIN
     C                   KFLD                    GMRPFR
     C                   KFLD                    GMRPTO
     C     PRIKU         KLIST
     C                   KFLD                    AIRLIN
     C                   KFLD                    AIRPFR
     C                   KFLD                    AIRPTO
     C     PRISK         KLIST
     C                   KFLD                    AIRLIN
     C                   KFLD                    AIRPFR
     C                   KFLD                    AIRPTO
     C                   KFLD                    WSRCCP
     C                   KFLD                    WSRCOD
     C                   KFLD                    WSRWEI
     C     PRISKS        KLIST
     C                   KFLD                    AIRLIN
     C                   KFLD                    AIRPFR
     C                   KFLD                    AIRPTO
     C                   KFLD                    AIRCCP
     C                   KFLD                    AIRCOD
     C                   KFLD                    AIRWEI
     C     PRISK1        KLIST
     C                   KFLD                    W2RLIN
     C                   KFLD                    W2RPFR
     C                   KFLD                    W2RPTO
     C     PRISK3        KLIST
     C                   KFLD                    AIRLIN
     C                   KFLD                    AIRPFR
     C                   KFLD                    AIRPTO
     C                   KFLD                    YIRCCP
     C                   KFLD                    YIRCOD
     C                   KFLD                    YIRWEI
      *
     C                   MOVE      'B'           WSRCCP            1
     C                   Z-ADD     0             WSRPRP            5 0
      *
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
     C***********************************************
     C     KOPIE         BEGSR
     C***********************************************
     C                   Z-ADD     0             ANTKOP
     C     *LIKE         DEFINE    AIRLIN        GMRLIN
     C     *LIKE         DEFINE    AIRPFR        GMRPFR
     C     *LIKE         DEFINE    AIRPTO        GMRPTO
     C                   SETOFF                                       33
     C     KEYH3         SETLL     AIRLD
     C* SLETTER GAMLE LINJER
     C     *IN33         DOWEQ     '0'
     C     KEYH3         READE     AIRLD                                  33
     C  N33              DELETE    AIRLDR
     C                   END
     C*
     C* LEGGER INN NYE LINJER
     C*
     C                   SETOFF                                       33
     C     KEYH2         SETLL     AIRLD
     C     *IN33         DOWEQ     '0'
     C     KEYH2         READE     AIRLD                                  33
     C  N33              MOVE      GMRLIN        AIRLIN
     C  N33              MOVE      GMRPFR        AIRPFR
     C  N33              MOVE      GMRPTO        AIRPTO
     C  N33              WRITE     AIRLDR
     C  N33              ADD       1             ANTKOP
     C                   END
     C                   MOVE      GMRLIN        AIRLIN
     C                   MOVE      GMRPFR        AIRPFR
     C                   MOVE      GMRPTO        AIRPTO
     C                   MOVE      *ZEROS        FLYLIN
     C                   MOVE      *BLANKS       FLYPFR
     C                   MOVE      *BLANKS       FLYPTO
     C                   ENDSR
      ***********************************************
     C     SLTAVT        BEGSR
      ***********************************************
     C     KEYH          SETLL     AIRLH
     C     KEYH          READE     AIRLH                                  33
     C     *IN33         IFEQ      *OFF
     C                   DELETE    AIRLHR
     C                   ENDIF
      *
     C     KEYH          SETLL     AIRLD
     C     KEYH          READE     AIRLD                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    AIRLDR
     C     KEYH          READE     AIRLD                                  33
     C                   ENDDO
      *
     C                   ENDSR
