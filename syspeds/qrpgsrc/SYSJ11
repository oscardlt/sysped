********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 innført 2 edibåtkoder  .
********************************************************************
      *
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BÅTKODER                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FCDBOAT    UF A E           K DISK
     FCDBOATLF  IF   E           K DISK
     F                                     RENAME(CDBOATR:CDBOATRL)
     FCDBINFO   UF A E           K DISK
     FKODTLK    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYSJ11FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  CBNAB                  1     30
     D  XDLT                   1      7
     D                 DS
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D  XIDAG                  1      8  0
     D                 DS
     D  WSTX1                  1     62
     D  WSTX2                 63    124
     D  WSTX3                125    186
     D  WSTX4                187    248
     D  WSTX5                249    310
     D  WSTX6                311    372
     D  WSTX7                373    434
     D  WSTX8                435    496
     D  WSTX9                497    558
     D  WSTX10               559    620
     D  WSTX11               621    682
     D  WSTX12               683    744
     D  WSTX13               745    806
     D  WSTX14               807    868
     D  WSTX15               869    930
     D  WSTX16               931    992
     D  WSTX17               993   1054
     D  TX                     1   1054
     D                                     DIM(17)
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2NAB         SETLL     CDBOATLF
     C     START1        TAG
     C                   READ      CDBOATLF                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C     KUND2K        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C     CBKDB         SETLL     CDBINFO                            33
     C     CBKDB         READE(N)  CDBINFO                                33
     C  N33              MOVEL     CBITXT        XINFO
     C   33              MOVE      *BLANKS       XINFO
     C                   WRITE     SUBFIL
     C                   READ      CDBOATLF                               94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     4             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*** AVSLUTT
     C   12              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE BÅTKODE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                           30
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA BÅTNAVN
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKDB         COMP      *BLANKS                                90
     C   90              MOVE      'SPK0101'     MSGNR
     C   90              SETON                                        30
     C  N90WSKDB         CHAIN     CDBOAT                             33
     C  N90
     CANN33              DO
     C                   UPDATE    CDBOATR
     C                   MOVE      'SPK0102'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     22            LINNBR
     C   90              Z-ADD     12            POSNBR
     C                   END
     C*
     C     WSNAB         IFEQ      *BLANKS
     C  N30              MOVE      'SPN0101'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     21            POSNBR
     C                   SETON                                        8930
     C                   END
     C*
     C     WSKN          IFNE      0
     C     KUND1K        CHAIN     CUNDL01                            88
     C   88
     CANN30              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     54            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSLK          IFNE      *BLANKS
     C     KODLKK        CHAIN     KODTLK                             87
     C   87
     CANN30              DO
     C                   MOVE      'SPL0006'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     65            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSKDB         CHAIN     CDBOAT                             33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWCB
     C  N33              UPDATE    CDBOATR
     C   33              WRITE     CDBOATR
      * OPPDATERE INFORMASJONER
     C                   MOVE      *BLANKS       XINFO
     C     1             DO        17            XN                3 0
     C     CDBINK        CHAIN     CDBINFO                            33
     C     TX(XN)        IFEQ      *BLANKS
     C  N33              DELETE    CDBINFOR
     C                   ELSE
     C                   MOVEL     CBITXT        XINFO
     C                   MOVE      TX(XN)        CBITXT
     C  N33              UPDATE    CDBINFOR
     C   33              MOVE      CBKDB         CBIBK
     C   33              MOVE      XN            CBILI
     C   33              WRITE     CDBINFOR
     C                   END
     C                   END
      *
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   MOVE      CBKDB         UKDB
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCB
     C   12              GOTO      SLHENT
     C                   END
     C  N27WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCB
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPCB
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGCB         BEGSR
     C***********************************************
     C                   EXSR      MOVCBW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                         30
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTCB         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXSR      HINFO
     C                   EXFMT     VAREL3
     C  N12              DO
     C     CBKDB         CHAIN     CDBOAT                             33
     C  N33              DELETE    CDBOATR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DSPCB         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXSR      HINFO
     C                   EXFMT     VAREL3
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWCB        BEGSR
     C***********************************************
     C                   MOVE      WSKDB         CBKDB
     C                   MOVE      WSNAB         CBNAB
N01  C                   MOVE      WSKDE1        CBKDE1
N01  C                   MOVE      WSKDE2        CBKDE2
     C                   MOVE      WSFAX         CBFAX
     C                   MOVE      WSEPOS        CBEPOS
     C                   MOVE      WSKN          CBKN
     C                   MOVE      WSLK          CBLK
     C                   MOVE      XIDAG         CBDTR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVCBW        BEGSR
     C***********************************************
     C                   MOVE      CBKDB         WSKDB
     C                   MOVE      CBNAB         WSNAB
N01  C                   MOVE      CBKDE1        WSKDE1
N01  C                   MOVE      CBKDE2        WSKDE2
     C                   MOVE      CBFAX         WSFAX
     C                   MOVE      CBEPOS        WSEPOS
     C                   MOVE      CBKN          WSKN
     C                   MOVE      CBLK          WSLK
     C                   EXSR      HINFO
     C                   SETON                                        40
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     15            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       CBKDB
     C                   MOVE      *BLANKS       CBNAB
N01  C                   MOVE      *BLANKS       CBKDE1
N01  C                   MOVE      *BLANKS       CBKDE2
     C                   MOVE      *BLANKS       CBFAX
     C                   MOVE      *BLANKS       CBEPOS
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *ZEROS        CBKN
     C                   MOVE      *BLANKS       CBLK
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKDB
     C                   MOVE      *BLANKS       WSNAB
N01  C                   MOVE      *BLANKS       WSKDE1
N01  C                   MOVE      *BLANKS       WSKDE2
     C                   MOVE      *BLANKS       WSFAX
     C                   MOVE      *BLANKS       WSEPOS
     C                   MOVE      *ZEROS        WSKN
     C                   MOVE      *BLANKS       WSLK
     C                   MOVE      *BLANKS       TX
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     6             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HINFO         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       TX
     C     CBKDB         SETLL     CDBINFO                            33
     C     STHINF        TAG
     C     CBKDB         READE(N)  CDBINFO                                33
     C     *IN33         IFEQ      '0'
     C                   MOVE      CBILI         XN
     C                   MOVE      CBITXT        TX(XN)
     C                   GOTO      STHINF
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C*
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     48
     C     POSNBR        ANDLE     55
     C                   EXSR      SØKKUN
     C                   Z-ADD     59            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     59
     C     POSNBR        ANDLE     60
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLK
     C                   PARM                    USPED
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     KUND2K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    CBKN
     C     CDBINK        KLIST
     C                   KFLD                    CBKDB
     C                   KFLD                    XN
     C     KODLKK        KLIST
     C                   KFLD                    KLKUNI
     C                   KFLD                    WSLK
     C                   MOVE      'LK '         KLKUNI
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UKDB              5
     C* UFLAGG = 1 ---> CALL SØKING FRA ET ANNET PROGRAM
     C     UFLAGG        COMP      1                                  27  27
      * UFLAGG = 2 ---> CALL FRA SYSJ10/SYSE10 FOR Å SE OPPLYSNINGER
     C     UFLAGG        IFEQ      2
     C     UKDB          ANDNE     *BLANKS
     C     UKDB          CHAIN(N)  CDBOAT                             33
     C     *IN33         IFEQ      '0'
     C                   EXSR      HINFO
     C                   EXFMT     VAREL3
     C                   MOVEL     'YES'         XINFO
     C                   END
     C                   END
     C*
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          XIDAGÅ
     C                   ELSE
     C     UYEAR         ADD       2000          XIDAGÅ
     C                   END
     C                   MOVE      UMONTH        XIDAGM
     C                   MOVE      UDAY          XIDAGD
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C     XINFO         IFEQ      'YES'
     C                   MOVE      CBNAB         W2NAB
     C                   ELSE
     C   27              WRITE     NYVARE
     C   27              EXFMT     VAREL2
     C                   END
     C*
     C                   ENDSR
     C*
