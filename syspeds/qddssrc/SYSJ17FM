     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  5VALUES(' ' '1' '2' '4' '7' '8')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            CTKN           8Y 0O  5 10EDTCDE(4)
     A            WSNA          20A  O  5 19
     A            CTKD           4A  O  5 42
     A            CTFT          30A  O  5 48
NV5  A            CTAKT          1A  O  5 79
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ17'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19MSGCON(40 DIV3100 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
SV5  A*N27                              3 34MSGCON(30 DIV0017 SYMSGF)
NV5  A N27                              3 34MSGCON(40 YBC0574 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3101 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A  27                                  CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              1  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 10MSGCON(70 DIV3102 SYMSGF)
     A                                      DSPATR(HI)
     A            WSKN           8Y 0B  2 10DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNA          20A  O  2 19DSPATR(HI)
     A            WSKD           4A  B  2 42DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A            WSFT          30A  B  2 48DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                  3  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 27MSGCON(24 DIV3104 SYMSGF)
     A                                  2  2MSGCON(24 DIV3103 SYMSGF)
     A            W2KN           8Y 0B  2 27DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A            W2FT          30A  I  2 38DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 10MSGCON(70 DIV3102 SYMSGF)
     A                                      DSPATR(HI)
     A            CTKN           8Y 0O  2 10DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSNA          20A  O  2 19DSPATR(HI)
     A            CTKD           4A  O  2 42DSPATR(UL)
     A                                      DSPATR(HI)
     A            CTFT          30A  O  2 48DSPATR(UL)
     A                                      DSPATR(HI)
NV5  A            CTAKT          1A  O  2 79DSPATR(UL)
NV5  A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A  27                                  CF12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ17'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3100 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
SV5  A*N27                              3 34MSGCON(30 DIV0017 SYMSGF)
NV5  A N27                              3 34MSGCON(40 YBC0574 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3101 SYMSGF)
     A                                      DSPATR(HI)
     A N27                             23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                             23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
