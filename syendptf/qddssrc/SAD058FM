     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 33 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4')
     A N27                                  DSPATR(PR)
     A            SVCNR         17A  O  4  5
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A  27                                  CA06(06 'Lag ny')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD058'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med contanernr'
     A                                      COLOR(WHT)
     A  27                              2  2'2=Endre 4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Op Containernr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                 11  2'F3/12=Avbryt F5=Forny'
     A                                      COLOR(BLU)
     A  27                             11 24'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(09)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Containernr'
     A                                      COLOR(WHT)
     A            WSCNR         17A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A N30                              4  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(09)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'   Containernr'
     A                                      COLOR(WHT)
     A            SVCNR         17A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A N30                              4  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD058'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med containernr'
     A                                      COLOR(WHT)
     A  27                              2  2'2=Endre 4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Op Containernr'
     A                                      COLOR(WHT)
     A                                 11  2'F3/12=Avbryt F5=Forny'
     A                                      COLOR(BLU)
     A  27                             11 24'F6=Lag ny'
     A                                      COLOR(BLU)
