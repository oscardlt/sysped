      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE23RV MODULE(*LIBL/TJGE23RV *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
11XXX * 01 JOV PTF11 .........
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
N01  FADRUNNRF  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D AVD             S              4S 0
     D OPD             S              7S 0
     D FMMRK1          S             35
     D FVANT           S              7P 0
     D FVPAKN          S              7
     D FVVT            S             35
     D FVVKT           S              9P 0
     D FVVOL           S              7P 3
     D FVLM            S              4P 2
     D FVLM2           S              4P 2
     D FVLEN           S              4P 0
     D FVBRD           S              4P 0
     D FVHOY           S              4P 0
     D FFUNNR          S              4
     D FFEMBG          S              3
     D FFKLAS          S              4
     D FFSEDD          S             10
     D FFTRES          S             10
     D FFFAKT          S              4S 0
     D FFANTK          S              5S 0
     D FFANTE          S              7S 0
     D FFENH           S              3
     D FFPOEN          S              5S 0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"awblineValidate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬fmmrk1¬ : ¬'+%trim(fmmrk1)+'¬, '
          +'¬fvant¬ : ¬'+%trim(%editc(fvant:'Z'))+'¬, '
          +'¬fvpakn¬ : ¬'+%trim(fvpakn)+'¬, '
          +'¬fvvt¬ : ¬'+%trim(fvvt)+'¬, '
          +'¬fvvkt¬ : ¬'+%trim(%editc(fvvkt:'Z'))+'¬, '
          +'¬fvvol¬ : ¬'+%trim(%editc(fvvol:'4'))+'¬, '
          +'¬fvlm¬ : ¬'+%trim(%editc(fvlm:'4'))+'¬, '
          +'¬fvlm2¬ : ¬'+%trim(%editc(fvlm2:'4'))+'¬, '
          +'¬fvlen¬ : ¬'+%trim(%editc(fvlen:'Z'))+'¬, '
          +'¬fvbrd¬ : ¬'+%trim(%editc(fvbrd:'Z'))+'¬, '
          +'¬fvhoy¬ : ¬'+%trim(%editc(fvhoy:'Z'))+'¬, '
          +'¬ffunnr¬ : ¬'+%trim(ffunnr)+'¬, '
          +'¬ffembg¬ : ¬'+%trim(ffembg)+'¬, '
          +'¬ffklas¬ : ¬'+ %trim(ffklas)+'¬, '
          +'¬ffsedd¬ : ¬'+%trim(ffsedd)+'¬, '
          +'¬fftres¬ : ¬'+%trim(fftres)+'¬, '
          +'¬fffakt¬ : ¬'+%trim(%editc(fffakt:'Z'))+'¬, '
          +'¬ffantk¬ : ¬'+%trim(%editc(ffantk:'Z'))+'¬, '
          +'¬ffante¬ : ¬'+%trim(%editc(ffante:'Z'))+'¬, '
          +'¬ffenh¬  : ¬'+%trim(ffenh)+'¬, '
          +'¬ffpoen¬ : ¬'+%trim(%editc(ffpoen:'Z'))+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
N01  C  N50              CLOSE     HEADF
N01  C  N50              CLOSE     ADRUNNRF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      AVD        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      OPD        = c2n2(WA)
     C                   EVAL      FMMRK1  = zhbgetvar('FMMRK1')
     C                   EVAL      WA      = zhbgetvar('FVANT')
     C                   EVAL      FVANT   = c2n2(WA)
     C                   EVAL      FVPAKN     = zhbgetvar('FVPAKN')
     C                   EVAL      FVVT       = zhbgetvar('FVVT')
     C                   EVAL      WA      = zhbgetvar('FVVKT')
     C                   EVAL      FVVKT   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVVOL')
     C                   EVAL      FVVOL   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLM')
     C                   EVAL      FVLM    = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLM2')
     C                   EVAL      FVLM2   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLEN')
     C                   EVAL      FVLEN   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVBRD')
     C                   EVAL      FVBRD   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVHOY')
     C                   EVAL      FVHOY   = c2n2(WA)
     C                   EVAL      FFUNNR  = zhbgetvar('FFUNNR')
     C                   EVAL      FFEMBG  = zhbgetvar('FFEMBG')
     C                   EVAL      FFKLAS  = zhbgetvar('FFKLAS')
     C                   EVAL      FFSEDD  = zhbgetvar('FFSEDD')
     C                   EVAL      FFTRES  = zhbgetvar('FFTRES')
     C                   EVAL      WA      = zhbgetvar('FFFAKT')
     C                   EVAL      FFFAKT  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FFANTK')
     C                   EVAL      FFANTK  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FFANTE')
     C                   EVAL      FFANTE  = c2n2(WA)
N01  C                   EVAL      FFENH   = zhbgetvar('FFENH')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     HeadfKey      KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
N01  C     UnNrKey       KLIST
N01  C                   KFLD                    FFUNNR
N01  C                   KFLD                    FFEMBG
      *
     C   50              eval      Error = 'Invalid userID'
N01  C  N50              OPEN      ADRUNNRF
N01  C  N50              OPEN      HEADF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      *
      * Input av - ant / vareslag / vekt valideres i eSpedsg
     C                   eval      Error = *blanks
      *
     c     HeadfKey      chain     HEADF                              33
     C   33              eval      Error = 'Unknown order'
      * ant/vekt
OX   c                   if        FVANT =0 or FVVKT=0
OX   C                   eval      Error = 'No of pieces/collies and weight '
OX   c                             +'is mandatory'
OX   c                   endif
      *
OX    * beregning av LM ut fra Formel er ren Bring / olgeexpress-spesial
OX   c                   if        HEOT='OX' or HEOT='LO' or HEOT='OB'
OX   c                   if        FVLEN=0 or FVBRD=0 or FVHOY=0
OX   C                   eval      Error = 'L x B x H is mandatory on '
OX   c                             +'ordertype '+HEOT
OX   c                   else
OX    * Hovedreget : LM=LenxBe/2,4    MEN  - div unntak
OX   c                   if        FVLM =0
OX   C                   Z-ADD     0             XWORK            15 4
OX   c                   eval(h)   XWORK = FVLEN*FVBRD/2.4/10000                Hoved = Len*Bre/2.4
OX   c                   eval(h)   FVLM  = XWORK                                Hoved = Len*Bre/2.4
OX   c                   if        FVBRD > 125
OX   c                   eval(h)   FVLM =FVLEN/100                              Unnt 1= Len
OX   c                   if        FVLEN <= 125
OX   c                   eval(h)   FVLM =FVBRD/2/100                            Unnt 2= Bre/2
OX   c                   endif
OX   c                   else
OX   c                   if        FVBRD <= 120 and FVLEN > 130
OX   c                   eval(h)   FVLM=FVLEN/2/100                             Unnt 3= Len/2
OX   c                   endif
OX   c                   endif
OX   c     FVLM          ifne      0
OX   c                   mult      FVANT         FVLM
OX   c                   end
OX   c                   endif
OX   c                   endif
OX   c                   endif
      *
     c     FVLM2         ifeq      0
     c                   eval      FVLM2 = FVLM
     c                   endif
      *
     C                   IF        FFUNNR <> *blanks
      * Sjekk kombinasjon UnNr/Emb.grp (om det på gitt UNnr fins kun en emb-grp, autovelg )
     C     UnNrKey       chain     ADRUNNRF                           33
     C     *in33         ifeq      '1'
     C     FFUNNR        chain     ADRUNNRF                           33
     C     *in33         ifeq      '1'
     C                   eval      Error = 'Invalid UnNo'
     c                   goto      UtUnnr
     c                   endif
      * kommer hit kun når gitt UNNR+EMBG IKKE finnes men det finnes minst en på gitt UNNR
     C     FFUNNR        reade     ADRUNNRF                               33
     C     *in33         ifeq      *on
     C                   eval      FFEMBG = ADEMBG
     C                   else
     C                   eval      Error = 'Invalid Emb.Grp for this UnNo'
     c                   goto      UtUnnr
     c                   endif
     c                   endif
      * OK UNnr/emb  - returner div verdier
     C                   EVAL      FFKLAS=ADKLAS
     C                   EVAL      FFSEDD=ADSEDD
     C                   EVAL      FFTRES=ADTRES
     C                   EVAL      FFFAKT=ADFAKT
     c     UtUnnr        tag
     C                   if        FFANTK = 0
     C                   eval      Error = 'Number of collies (containing ADR) '
     c                             +'cannot be zero'
     c                   endif
     C                   if        FFANTE = 0
     C                   eval      Error = 'Number of units (LTR/KG/GR) '
     c                             +'cannot be zero'
     c                   endif
     C* enhet via drop-down - KAN IKKE være fei
     C****               if        FFENH ......
     C* Returverdi for Poend
S01  C                   EVAL      FFPOEN=FFFAKT * FFANTE
N01  C                   ENDIF
     C                   ENDSR
