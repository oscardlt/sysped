      *
     FWSAML     UF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FTURER14   IF   E           K DISK
     FSYFA27FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  WASORT                 1     20
     D  XXSORT                 1     15
     D                 DS
     D  KNAVN                  1     30
     D  XXNAVN                 1     15
     D  XDLT                   1      7
     D                 DS
     D  WADATO                 1      8  0
     D  WACC                   1      2  0
     D  WAYY                   3      4  0
     D  WAMM                   5      6  0
     D  WADD                   7      8  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      * Sett ind, for underutvalg  (*IN50 brukes i DSPF)
     C                   MOVE      '0'           *IN50                          EKSKLUDERT??
     C                   MOVE      'N'           UNDUTV            1
     C                   MOVE      'J'           INCL              1
     C     W2KN          IFNE      *ZEROS
     C     W2VAL         ORNE      *BLANKS
     C     W2SORT        ORNE      *BLANKS
     C     W2AVD         ORNE      *ZEROS
     C     W2OPD         ORNE      *ZEROS
     C     W2PRO         ORNE      *ZEROS
     C     W2UR          ORNE      *BLANKS
     C     W2GRP         ORNE      *BLANKS
     C                   MOVE      'J'           UNDUTV
     C                   MOVE      ' '           INCL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     XXUNIK        SETLL     WSAML
     C*
     C     XXUNIK        READE(N)  WSAML                                  94
     C     *IN94         DOWEQ     '0'
     C     UNDUTV        IFEQ      'J'
     C                   EXSR      INCLUD
     C                   END
     C     INCL          IFEQ      'J'
     C                   ADD       1             ZRECNO
     C     KUNKEY        CHAIN     CUNDL01                            77
     C                   MOVEL     *BLANKS       WAPER
     C     WAPRO         CHAIN     TURER14                            66
     C     *IN66         IFEQ      *OFF
     C                   MOVEL     '  /  '       WAPER
     C                   MOVEL     TURMND        WAPER
     C                   MOVE      TURAAR        WAPER
     C                   ELSE
     C                   MOVEL     '  /  '       WAPER
     C                   MOVEL     WAMM          WAPER
     C                   MOVE      WAYY          WAPER
     C                   ENDIF
     C                   WRITE     SUBFIL
     C                   ELSE
     C                   MOVE      '1'           *IN50                           EKSKLUDERT
     C                   END
     C     XXUNIK        READE(N)  WSAML                                  94
     C  N94              END
      *
     C                   MOVE      'J'           FRTOPP            1
      * Blank Underutvalg (slik at F5 gi alle)
     C                   EXSR      MOBLW2
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C* Ingen underutvalg, likevel ingen poster ->FORIGE uansett tast..
     C     *IN50         IFEQ      '0'
     C                   MOVE      '1'           *IN12
     C                   GOTO      SLUTT
     C                   END
     C                   ELSE
     C     FRTOPP        IFEQ      'J'
     C                   Z-ADD     1             ZRECNO
     C                   MOVE      'N'           FRTOPP            1
     C                   END
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9899
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C   10              GOTO      SLUTT
     C   11              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   16              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** Underutvalg
     C     *IN08         IFEQ      '1'
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C                   GOTO      START
     C                   END
     C*
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
      * Enter - også uten utført valg - vis samme bilde/poster
     C                   GOTO      START3
     C*
     C*
     C*
     C     SLUTT         TAG
     C   03              MOVE      '3'           URETUR                           AVBRUDD
     C   10              MOVE      'P'           URETUR                           PRØVE
     C   11              MOVE      'S'           URETUR                           PRØVE SPES
     C   12              MOVE      'Y'           URETUR                           NYTT UTVAL
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     INCLUD        BEGSR
     C***********************************************
     C                   MOVE      'J'           INCL
      *
     C     W2KN          IFNE      *ZEROS
     C     W2KN          ANDNE     WAKN
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2VAL         IFNE      *BLANKS
     C     W2VAL         ANDNE     WAVAL
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2SORT        IFNE      *BLANKS
     C     W2SORT        ANDNE     XXSORT
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2AVD         IFNE      *ZEROS
     C     W2AVD         ANDNE     WAAVD
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2OPD         IFNE      *ZEROS
     C     W2OPD         ANDNE     WAOPD
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2PRO         IFNE      *ZEROS
     C     W2PRO         ANDNE     WAPRO
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2UR          IFNE      *BLANKS
     C     W2UR          ANDNE     WAUR
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2GRP         IFNE      *BLANKS
     C     W2GRP         ANDNE     WAGRP
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C*
     C     ENDINC        ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'                                          SE OPPDRAG
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      SEOPD
     C                   END
     C     WSNR          IFEQ      '6'                                          SE FAKTURA
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      SEFAK
     C                   END
     C*
     C                   END
     C*
     C     SLHENT        TAG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C  N12              DO
     C     WSAMLK        CHAIN     WSAML                              33
     C  N33              DELETE    WSAMLR
     C                   EXSR      MOBLWS
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
      *//////////////////////////////////////////////////////////////////////
      * VIEW ORDER                                                   CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SEOPD         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   Z-ADD     WAOPD         XXOPD             7 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    WAAVD
     C                   PARM                    XXOPD
     C                   PARM                    DUMFLA
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *//////////////////////////////////////////////////////////////////////
      * VIEW Invoice picture                                         CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SEFAK         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   Z-ADD     WAOPD         XXOPD             7 0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    WAAVD
     C                   PARM                    XXOPD
     C                   PARM                    DUMFLA
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
     C*
     C***********************************************
     C     MOBLW2        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        W2KN
     C                   MOVE      *ZEROS        W2AVD
     C                   MOVE      *ZEROS        W2OPD
     C                   MOVE      *ZEROS        W2PRO
     C                   MOVE      *BLANKS       W2VAL
     C                   MOVE      *BLANKS       W2SORT
     C                   MOVE      *BLANKS       W2UR
     C                   MOVE      *BLANKS       W2GRP
     C                   ENDSR
     C***********************************************
     C     MOBLWS        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WAKN
     C                   MOVE      *ZEROS        WAAVD
     C                   MOVE      *ZEROS        WAOPD
     C                   MOVE      *ZEROS        WAPRO
     C                   MOVE      *BLANKS       WAVAL
     C                   MOVE      *BLANKS       WASORT
     C                   MOVE      *BLANKS       WAUR
     C                   MOVE      *BLANKS       WAGRP
     C                   MOVE      *BLANKS       XXNAVN
     C                   MOVEL     '*DELETE'     XXNAVN
     C*
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    URETUR            1
     C                   MOVE      'N'           URETUR
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WAKN
     C     WSAMLK        KLIST
     C                   KFLD                    XXUNIK
     C                   KFLD                    WAKN
     C                   KFLD                    WAVAL
     C                   KFLD                    WASORT
     C                   KFLD                    WAAVD
     C                   KFLD                    WAOPD
     C*
     C                   MOVEL     ZJOBNR        XXUNIK            6 0
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
