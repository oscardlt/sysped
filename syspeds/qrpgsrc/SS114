      *********************************************************************
      * TRANSP.PLANLEGGING   - Endre ordre (kolli / vekt / volum)
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ss114 MODULE(*LIBL/ss114
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSORF    UF   E           k disk    usropn
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSOLF    UF A E           K DISK    USROPN
     FGSSLGF    O    E             DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D ORD             S              7
     D ORDN            S              7  0
     D KUNDE           S              8
     D KUNDEN          S              8  0
     D KLL             S              5
     D KLLN            S              5  0
     D VKT             S              7
     D VKTN            S              7  1
     D M3              S              7
     D M3N             S              7  2
     D LM              S              5
     D LMN             S              4  2
     D WS155           S             15  5
     D LAGREORD        S              1
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Nbr of collies must be -
     D                                      entered')
     D FEIL02          C                   CONST('Weight (in kilos, one -
     D                                      decimal) must be entered')
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D XXTXT01         C                   CONST('No. of colli changed at')
     D XXTXT02         C                   CONST('Weight is changed at')
     D XXTXT03         C                   CONST('Volume is changed at')
     D XXTXT04         C                   CONST('L.metre is changed at')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  ORFT4                  1     30
     D  ORLMA                 11     14
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * hvis en har tastet submit fra vedlikehodsbildet, test/lagre
     c     lagreord      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * her legges evt tester - ved feil, sett kode / hopp ut
      *
      *
      * ALT OK - Oppdater  - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      * Hvis endring..
     c                   Move      'N'           FixC              1
     c     Ordn          chain     GSSORF                             55
     c     *in55         IFeq      *OFF
     c     KLLN          ifne      ORCLI
     c                   Move      'J'           FixC
     c                   end
     C                   EXSR      OPDLOGG
     c                   z-add     KLLN          ORCLI
     c                   z-add     VKTN          ORWT
     c                   z-add     M3N           ORVOL
     c                   MOVE      LMN           ORLMA
     c                   update    gssorfr
     c                   end
      *
     c     FIXC          ifeq      'J'
     c                   exsr      fixcli
     c                   end
      *
     C     UtTest        endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS114'       LGPGM
     C                   Z-ADD     5             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      ORDN          LGONO
     C                   MOVE      *ZEROS        LGLNO
      *
     C                   IF        ORCLI <> KLLN
     C                   EVAL      LGTXT = XXTXT01
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGNNY = KLLN
     C                   EVAL      LGNGM = ORCLI
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   IF        ORWT <> VKTN
     C                   EVAL      LGTXT = XXTXT02
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGNNY = VKTN
     C                   EVAL      LGNGM = ORWT
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   IF        ORVOL <> M3N
     C                   EVAL      LGTXT = XXTXT03
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGNNY = M3N
     C                   EVAL      LGNGM = ORVOL
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   IF        ORLM <> LMN
     C                   EVAL      LGTXT = XXTXT04
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGNNY = LMN
     C                   EVAL      LGNGM = ORLM
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   endsr
      *************************************
     C     GETORD        BEGSR
      *************************************
     c     OrdN          chain(N)  GSSORF                             55
      * Uavh. om endring eller ny...
     c     *in55         ifeq      *OFF
     c     ORLMA         IFEQ      *BLANKS
     c                   MOVE      *ZEROS        ORLM              4 2
     c                   ELSE
     c                   MOVE      ORLMA         ORLM
     c                   END
     c                   z-add     ORCLI         KLLN
     c                   z-add     ORWT          VKTN
     c                   z-add     ORVOL         M3N
     c                   z-add     ORLM          LMN
     C     ORONO         CHAIN     GSSPOF                             33
     C   33              MOVE      *BLANKS       POCONO
     C     CUNDL1K       CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN = '*UNKNOW'
     c                   end
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lagreord = zhbgetvar('lagreord')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      ORD     = zhbgetvar('ORD')
     C                   eval      ORDN    = c2n2(ORD)
     C                   eval      KLL     = zhbgetvar('KLL')
     C                   eval      KLLN    = c2n2(KLL)
     C                   eval      VKT     = zhbgetvar('VKT')
     C                   eval      WS155   = c2n2(VKT)
     C                   IF        WS155 > 999999
     C                   EVAL      VKTN = 999999.9
     C                   ELSE
     C                   EVAL(H)   VKTN = WS155
     C                   END
     C                   eval      M3      = zhbgetvar('M3')
     C                   eval      WS155   = c2n2(M3)
     C                   IF        WS155 > 99999
     C                   EVAL      M3N = 99999.99
     C                   ELSE
     C                   EVAL(H)   M3N = WS155
     C                   END
     C                   eval      LM      = zhbgetvar('LM')
     C                   eval      WS155   = c2n2(LM)
     C                   IF        WS155 > 99
     C                   EVAL      LMN = 99.99
     C                   ELSE
     C                   EVAL(H)   LMN = WS155
     C                   END
     C                   eval      KNAVN   = zhbgetvar('KNAVN')
     C                   eval      POCONO  = zhbgetvar('POCONO')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('ORD':
     C                             %trim(%editc(ORDN:'3')))
     C                   callp     updhtmlvar('KNAVN':KNAVN)
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   callp     updhtmlvar('KLL':
     C                             %trim(%editc(KLLN:'Z')))
     C                   callp     updhtmlvar('VKT':
     C                             %trim(%editc(VKTN:'M')))
     C                   callp     updhtmlvar('M3':
     C                             %trim(%editc(M3N:'M')))
     C                   callp     updhtmlvar('LM':
     C                             %trim(%editc(LMN:'M')))
     C                   callp     updHtmlVar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS114'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSORF
     C                   open      GSSPOF                               50
     C                   open      GSSOLF                               50
     C                   open      GSSLGF                               50
     C                   OPEN      Cundl01
      *
     C     CUNDL1K       klist
     C                   kfld                    BIFIRM
     C                   kfld                    ORCNO
      *
      *
      * Ved første kall - hent verdier fra fil.
     c     ordn          IFNE      *zeros
     c     LAGREORD      ANDNE     'Y'
     C                   EXSR      GETORD
     c                   END
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSPOF
     C                   close     GSSOLF
     C                   CLOSE     GSSLGF
     C                   close     Cundl01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *=====================================================================******
      *  FIXCLI - Ant poster i kollifil GSSOLF tilsvarende ant KLL
      *=====================================================================******
     C     FIXCLI        begsr
     C                   Move      *zeros        KllNr             3 0
      * slett hvis det finnes flere poster enn KOLLI...
     c     ORONO         SETLL     GSSOLF
     c     ORONO         READE     GSSOLF                                 33
     c     *in33         DOWEQ     *OFF
     C                   add       1             KllNr
     c     Kllnr         ifgt      ORCLI
     c                   DELETE    GSSOLFR
     c                   else
     c                   move      ORTNO         OLTNO
     c                   move      ORPOS1        OLSTD
     c                   update    GSSOLFR
     c                   end
     c     ORONO         READE     GSSOLF                                 33
     c                   end
      * Legg till hvis det finnes FÆRRE poster enn KOLLI...
     c     Kllnr         DOWLT     ORCLI
     c     Kllnr         IFEQ      99
     C                   leave
     c                   end
     C                   add       1             Kllnr
     c                   clear                   GSSOLFR
     c                   move      ORONO         OLONO
     c                   z-add     kllnr         OLLNO
     c                   move      ORTNO         OLTNO
     c                   move      ORPOS1        OLSTD
     c                   WRITE     GSSOLFR
     C                   enddo
      *
     C                   endsr
