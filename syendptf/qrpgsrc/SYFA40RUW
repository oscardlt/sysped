      *********************************************************************
      * MERKE RUCKSATTEST   - XML-basert webservice
      *     Denne skal kunne funke likt som SYFA40RU..
      *     Kjøres kun hos kunder som ikke vil/kan kjøre SYFA40RU på web (eks Leman)
      *     Speditøren/mottakeren får da meil som viser til systeme ASPWEB / firma ASS
      *     Fra ASS kaller SYFA40RU denne webtjenesten (hvis vilkår xxx) i stedet for lokal kjøring
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/SYFA40RUW MODULE(*LIBL/SYFA40RUW
CRTPGM*        *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF12  NYTT PROG
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
     FSADH      UF   E           K DISK    USROPN
     FRUCKP     UF   E           K DISK    USROPN
     FTRACKl02  UF A E           K DISK    USROPN
     FFRISOK    UF A E           K DISK    USROPN
     FKODFRI    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10

     D WSUSER          S             10
     D MailAdr         S             70
     D MerkTxt         S            170
     D feilTxt         S             70
     D AVD             S              4
     D OPD             S              7
     D UNIK            S             10
     D Funk            S              1
     D WSGNN           S             15
     D WSTLE           S              6
     D WSTLL           S             10
     D EXTREF          S             35
     D EKSPENH         S            100
     D LOPENR          S            100
     D NYTTGNR         S            100
     D OKBUTTON        S            100
     D TTTEX2          S             60
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D i               s             10i 0
      *
     D string          s          32000
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
     D                 DS
     D  WDsÅMD                 1      8  0
     D  WDsÅMDÅ                1      4  0
     D  WDsÅMDM                5      6  0
     D  WDsÅMDD                7      8  0
     D                 DS
     D  WDsDMÅ                 1      8  0
     D  WDsDMÅD                1      2  0
     D  WDsDMÅM                3      4  0
     D  WDsDMÅÅ                5      8  0
     D                 DS
     D  WSETD                  1      6
     D   WSETDD                1      2
     D   WSETDM                3      4
     D   WSETDÅ                5      6
     D   WSETD1_1              1      1
     D   WSETD2_5              2      5
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
      /free
         gethtmlifs('/syspeddev/html/XML.html':'/CGITAG/');
         wrtsection('top');

         string=
           '<?xml version="1.0" encoding="utf-8" standalone="yes"?>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<GetSetRuckAttest>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         exsr Hoved;

         string=
           '</GetSetRuckAttest>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         exsr Exit;
         *inlr = *on;
         return;
      /end-free
      *********************************************************************
     C     Hoved         begsr
      *********************************************************************
     C                   eval      feilTxt = *blanks
     C                   eval      merkTxt = *blanks
      *
     C     HEADFK        chain     HEADF                              33
     c                   if        not %found
     C                   EVAL      feilTxt = 'Angitt oppdrag finnes ikke!!'
     c                   goto      utMerk
     c                   endif
      *
      * &unik må samsvare med oppdraget (Trackf/RAS(første) / TTDEPO )
     c                   MOVE      'RAS'         TTACTI
     c     TT02KEY       chain     TRACKL02
     c                   if        UNIK = *blanks or
     c                             UNIK <> TTDEPO
     c                   eval      MerkTxt = 'Benyttet URL er ikke korrekt.'
     c                   eval      feilTxt = 'Unik kode samsvarer ikke med '
     c                                     + 'oppdraget. '
     c                   goto      utMerk
     c                   endif
      *
     c                   eval      FSKODE = '*CR'
     c     FriKey        chain(n)  FRISOK
     c                   if        %found (FRISOK)
     c                   eval      EXTREF = FSSOK
     c                   endif
      *  ER alt løpenr/el godsnr inne
     c                   if        HETLL <> *blanks or HEGNN <> *blanks
     c                   eval      MerkTxt = 'Ruckattest er allerede '
     c                             +' lagt inn.'
      *  ER den bekreftet via WEB ??
     C                   MOVE      'RUW'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe(N) TRACKL02                               34
     c     *in34         ifeq      '0'
     c                   movel     TTTIME        TTTIM4            4 0
     c                   eval      MerkTxt = 'Ruckattest er allerede '
     c                             +' lagt inn via web - '
     c                             +%trim(%editw(TTDATE:'    .  .  '))
     c                             + ' / ' + %trim(%editw(TTTIM4:'  :  '))
     c                   endif
     c                   else
     c                   eval      MerkTxt = 'Fyll ut verdier og trykk '
     c                             +'på OK-knappen for å avgi Ruckattest.'
     c                   endif
      *
      * Bekreft tildeling
     C     FUNK          IFEQ      'B'
     c                   exsr      TstBekr
     c                   if        feilTxt = *blanks
     c                   exsr      Bekreft
     c                   endif
     c                   endif
      *
     c     UtMerk        tag
      /free
         string=
           '<HEGN>'+%trim(HEGN)+'</HEGN>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HEPOS1>'+%trim(HEPOS1)+'</HEPOS1>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HEPOS2>'+%trim(%editc(HEPOS2:'3'))+'</HEPOS2>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HERFA>'+%trim(HERFA)+'</HERFA>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<EXTREF>'+%trim(EXTREF)+'</EXTREF>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HENAK>'+%trim(HENAK)+'</HENAK>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HENT>'+%trim(%editc(HENT:'3'))+'</HENT>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HEVS1>'+%trim(HEVS1)+'</HEVS1>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HEVKT>'+%trim(%editc(HEVKT:'3'))+'</HEVKT>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HETLE>'+%trim(HETLE)+'</HETLE>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HETLL>'+%trim(HETLL)+'</HETLL>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<HEGNN>'+%trim(HEGNN)+'</HEGNN>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<FeilTxt>'+%trim(feiltxt)+'</FeilTxt>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');
         //
         string=
           '<MerkTxt>'+%trim(merktxt)+'</MerkTxt>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

      /end-free
      *
     C                   endsr
      *
      *********************************************************************
     C     TstBekr       begsr
      *********************************************************************
     c                   IF        WSTLE =  *BLANKS AND
     c                             WSGNN =  *BLANKS
     C                   EVAL      feilTxt = 'Eksp.enhet/Løpenr ELLER nytt '
     C                                     + 'godsnr må angis.'
     c                   goto      utTst
     c                   ENDIF
      *
     c                   IF        WSTLE <> *BLANKS AND
     c                             WSTLL =  *BLANKS
     C                   EVAL      feilTxt = 'BÅDE Eksp.enhet og Løpenr '
     C                                     + 'godsnr må angis.'
     c                   goto      utTst
     c                   ENDIF
      *
     c                   IF        WSTLE =  *BLANKS AND
     c                             WSTLL <> *BLANKS
     C                   EVAL      feilTxt = 'BÅDE Eksp.enhet og Løpenr '
     C                                     + 'godsnr må angis.'
     c                   goto      utTst
     c                   ENDIF
      *
     C     UtTst         endsr
      *
      *********************************************************************
     C     Bekreft       begsr
      *********************************************************************
     c     HEADFK        chain     HEADF
     c                   if        %found
     c                   eval      HETLE = WSTLE
     c                   eval      HETLL = WSTLL
     c                   eval      HEGNN = WSGNN
     c                   update    HEADFR
      *
      *
     c                   eval      MerkTxt = 'Ruckattest er nå lagt inn - '
     c                             +' steng vinduet (e/evt print av kvittering)'
     C                   eval      TTTEX2 = 'E:'+HETLE+' L:'+HETLL
     c                             +' N.Gn:'+HEGNN
     c*
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'RUW'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT ='Ruckattest received via web '
     c                             + TTTEX2
     C                   EVAL      TTTEXL ='Ruckattest mottatt via web '
     c                             + TTTEX2
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
      * oppdater loggen, O=Oppgjort (ville fikses  av syru02 neste natt, men greit å ta fortløpende)
     c     HEADFK        chain     RUCKP
     c                   if        %found
     C                   eval      RUST = 'O'
     C                   update    RUCKPR
     c                   endif
      * Oppdater sadh
     c     HEADFK        chain     SADH
     c                   if        %found
     c                   eval      SITLE = WSTLE
     c                   eval      SITLL = WSTLL
     c                   update    SADHR
     c                   endif
      * Oppdater Frisok TLE /TLØ
     C                   if        WSTLL <> *blanks
      * eksp-enh i TLE
     c                   eval      FSKODE = 'TLE'
     c                   move      *blanks       KFSODK
     c     FSKODE        CHAIN     KODFRI
     c     FriKey        chain     FRISOK
     c                   eval      FSAVD = HEAVD
     c                   eval      FSOPD = HEOPD
     c                   eval      FSDTOP= HEDTOP
     c                   eval      FSDOKK= KFSODK
     c                   eval      FSSOK = WSTLE
     c                   if        %found
     c                   update    FRISOKR
     c                   else
     c                   write     FRISOKR
     c                   endif
      * løpenumm i TLØ
     c                   eval      FSKODE = 'TLØ'
     c                   move      *blanks       KFSODK
     c     FSKODE        CHAIN     KODFRI
     c     FriKey        chain     FRISOK
     c                   eval      FSAVD = HEAVD
     c                   eval      FSOPD = HEOPD
     c                   eval      FSDTOP= HEDTOP
     c                   eval      FSDOKK= KFSODK
     c                   eval      FSSOK = WSTLL
     c                   if        %found
     c                   update    FRISOKR
     c                   else
     c                   write     FRISOKR
     c                   endif
     c                   endif
      *
     c                   endif
      *
     C                   endsr
      *
      *
      *********************************************************************
     C     Exit          begsr
      *********************************************************************
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
     C                   CLOSE     SADH
     C                   CLOSE     RUCKP
     C                   CLOSE     TRACKL02
     C                   CLOSE     FRISOK
     C                   CLOSE     KODFRI
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      AVD     = zhbgetvar('AVD')
     C                   eval      HEAVD   = c2n2(AVD)
     C                   eval      OPD     = zhbgetvar('OPD')
     C                   eval      HEOPD   = c2n2(OPD)
     C                   eval      UNIK    = zhbgetvar('UNIK')
     C                   eval      FUNK    = zhbgetvar('FUNK')
     C                   eval      WSTLE   = zhbgetvar('WSTLE')
     C                   eval      WSTLL   = zhbgetvar('WSTLL')
     C                   eval      WSGNN   = zhbgetvar('WSGNN')

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      HEADF
     C                   open      SADH
     C                   open      RUCKP
     C                   open      TRACKL02
     C                   open      FRISOK
     C                   open      KODFRI
      *
     c     HEADFK        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c     FRIKEY        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c                   kfld                    FSKODE
     C     TT02MAX       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   KFLD                    Maxdate
     C                   move      99999999      Maxdate           8 0
     C     TT02KEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C                   endsr
      *
