      *********************************************************************
      * Spørring på ISO (vis meny)
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/EISO00 MODULE(*LIBL/EISO00
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NYTT SØKEFELT FOR KUNDENR
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FISOKDA    IF   E           K DISK    USROPN
     FISOKDP    IF   E           K DISK    USROPN
     FISOKDT    IF   E           K DISK    USROPN
     FISOKDS    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FKOFAST    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D UsrSpcName      s             10
     D WSLEDET1        S             15
     D WSLEDET2        S             15
     D WSLEDET3        S             15
N01  D WSKNA           S              8
N01  D WSDTF           S             10
N01  D WSSAKN          S              7
N01  D WSAVD           S              4
N01  D WSOPD           S              7
N01  D WSFN            S              7
N01  D WSFNIN          S             17
N01  D WSTN            S              8
N01  D WSKNT           S              8
N01  D WSFKN           S              8
N01  D WSOIT           S              1
N01  D WSPT            S              5
N01  D WSAA            S              5
N01  D WSAP            S              5
N01  D WSST            S              5
N01  D WSUSRS          S             10
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  DFS                    1     10
     D                                     DIM(10)
     D  KFSODK                 1     10
      * For å konvertere uheldige bokstaver
     D UC              C                   CONST(' ')
     D LC              C                   CONST('&')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      * 2-Prod.koder -pulldown (+bunn)
     C                   exsr      DspPT
     C                   exsr      DspAA
     C                   exsr      DspAP
     C                   exsr      DspST
     C                   exsr      DspUS
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
N01   *
N01  C                   EVAL      WSKNA   = zhbgetvar('WSKNA')
N01  C                   EVAL      WSDTF   = zhbgetvar('WSDTF')
N01  C                   EVAL      WSSAKN  = zhbgetvar('WSSAKN')
N01  C                   EVAL      WSAVD   = zhbgetvar('WSAVD')
N01  C                   EVAL      WSOPD   = zhbgetvar('WSOPD')
N01  C                   EVAL      WSFN    = zhbgetvar('WSFN')
N01  C                   EVAL      WSFNIN  = zhbgetvar('WSFNIN')
N01  C                   EVAL      WSTN    = zhbgetvar('WSTN')
N01  C                   EVAL      WSKNT   = zhbgetvar('WSKNT')
N01  C                   EVAL      WSFKN   = zhbgetvar('WSFKN')
N01  C                   EVAL      WSOIT   = zhbgetvar('WSOIT')
N01  C                   EVAL      WSPT    = zhbgetvar('WSPT')
N01  C                   EVAL      WSAA    = zhbgetvar('WSAA')
N01  C                   EVAL      WSAP    = zhbgetvar('WSAP')
N01  C                   EVAL      WSST    = zhbgetvar('WSST')
N01  C                   EVAL      WSUSRS  = zhbgetvar('WSUSRS')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C     LC:UC         XLATE     KNAVN         KNAVN
     C                   callp     updHtmlVar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
S01  C*                  callp     updHTMLvar('WSKNA':'')
S01  C*                  callp     updHTMLvar('WSDTF':'')
S01  C*                  callp     updHTMLvar('WSSAKN':'')
S01  C*                  callp     updHTMLvar('WSAVD':'')
S01  C*                  callp     updHTMLvar('WSOPD':'')
S01  C*                  callp     updHTMLvar('WSFN':'')
S01  C*                  callp     updHTMLvar('WSFNIN':'')
S01  C*                  callp     updHTMLvar('WSTN':'')
S01  C*                  callp     updHTMLvar('WSKNT':'')
N01  C                   callp     updHTMLvar('WSKNA':WSKNA)
N01  C                   callp     updHTMLvar('WSDTF':WSDTF)
N01  C                   callp     updHTMLvar('WSSAKN':WSSAKN)
N01  C                   callp     updHTMLvar('WSAVD':WSAVD)
N01  C                   callp     updHTMLvar('WSOPD':WSOPD)
N01  C                   callp     updHTMLvar('WSFN':WSFN)
N01  C                   callp     updHTMLvar('WSFNIN':WSFNIN)
N01  C                   callp     updHTMLvar('WSTN':WSTN)
N01  C                   callp     updHTMLvar('WSKNT':WSKNT)
N01  C                   callp     updHTMLvar('WSFKN':WSFKN)
      *
     C     KOFASTK1      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET1 = 'posttype'
     C                   ELSE
     C                   EVAL      WSLEDET1 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET1':WSLEDET1)
     C     KOFASTK2      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET2 = 'avvikets art'
     C                   ELSE
     C                   EVAL      WSLEDET2 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET2':WSLEDET2)
     C     KOFASTK3      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET3 = 'ansvarlig part'
     C                   ELSE
     C                   EVAL      WSLEDET3 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET3':WSLEDET3)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspPT         begsr
      *
     C                   callp     updHtmlVar('WSPT1':'*ALL')
     C                   callp     updhtmlvar('WSPT2':'ALL')
     C                   callp     wrtsection('PTsltrow')
     C     *loval        setll     ISOKDT
     C                   read      ISOKDT                                 40
     C                   DOW       *IN40 = *OFF
N01  C                   IF        ((WSPT <> *BLANKS) AND (WSPT = TKODE))
N01  C                   callp     updHtmlVar('PTselected':'selected')
N01  C                   ELSE
N01  C                   callp     updHtmlVar('PTselected':'')
N01  C                   END
     C                   callp     updHtmlVar('WSPT1':TKODE)
     C                   callp     updhtmlvar('WSPT2':TTEXT)
     C                   callp     wrtsection('PTsltrow')
     C                   read      ISOKDT                                 40
     C                   enddo
      *
     C                   callp     wrtsection('PTsltend')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspAA         begsr
      *
     C                   callp     updHtmlVar('WSAA1':'*ALL')
     C                   callp     updhtmlvar('WSAA2':'ALL')
     C                   callp     wrtsection('AAsltrow')
     C     *loval        setll     ISOKDA
     C                   read      ISOKDA                                 40
     C                   DOW       *IN40 = *OFF
     C                   callp     updHtmlVar('WSAA1':AKODE)
     C                   callp     updhtmlvar('WSAA2':ATEXT)
     C                   callp     wrtsection('AAsltrow')
     C                   read      ISOKDA                                 40
     C                   enddo
      *
     C                   callp     wrtsection('AAsltend')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspAP         begsr
      *
     C                   callp     updHtmlVar('WSAP1':'*ALL')
     C                   callp     updhtmlvar('WSAP2':'ALL')
     C                   callp     wrtsection('APsltrow')
     C     *loval        setll     ISOKDP
     C                   read      ISOKDP                                 40
     C                   DOW       *IN40 = *OFF
     C                   callp     updHtmlVar('WSAP1':PKODE)
     C                   callp     updhtmlvar('WSAP2':PTEXT)
     C                   callp     wrtsection('APsltrow')
     C                   read      ISOKDP                                 40
     C                   enddo
      *
     C                   callp     wrtsection('APsltend')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspST         begsr
      *
     C                   callp     updHtmlVar('WSST1':'*ALL')
     C                   callp     updhtmlvar('WSST2':'ALL')
     C                   callp     wrtsection('STsltrow')
     C     *loval        setll     ISOKDS
     C                   read      ISOKDS                                 40
     C                   DOW       *IN40 = *OFF
     C                   callp     updHtmlVar('WSST1':SKODE)
     C                   callp     updhtmlvar('WSST2':STEXT)
     C                   callp     wrtsection('STsltrow')
     C                   read      ISOKDS                                 40
     C                   enddo
      *
     C                   callp     wrtsection('STsltend')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display user-id to select from
      *=====================================================================******
     C     DspUS         begsr
      *
     C                   callp     updHtmlVar('WSUSRS1':'*ALL')
     C                   callp     updhtmlvar('WSUSRS2':'ALL')
     C                   callp     wrtsection('USsltrow')
     C                   EVAL      PAID = 'ISOW'
     C     *LOVAL        SETLL     BRIDF
     C                   READ(N)   BRIDF                                  40
     C                   DOW       *IN40 = *OFF
     C     BrParKey2     Chain     BRPARF                             33
     C  N33              IF        ((PAVRDA = 'J') OR (PAVRDA = 'j'))
     C                   callp     updhtmlvar('WSUSRS1':BIBRID)
     C                   callp     updhtmlvar('WSUSRS2':BIBESK)
     C                   callp     wrtsection('USsltrow')
     C                   END
     C                   READ(N)   BRIDF                                  40
     C                   enddo
      *
     C                   callp     wrtsection('USsltend')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EISO00'
     C*                  CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   OPEN      CUNDL01
     C                   OPEN      ISOKDA
     C                   OPEN      ISOKDP
     C                   OPEN      ISOKDT
     C                   OPEN      ISOKDS
     C                   OPEN      KOFAST
     C     KOFASTK1      klist
     C                   kfld                    KFTYP
     C                   kfld                    X1KFKOD           5
     C     KOFASTK2      klist
     C                   kfld                    KFTYP
     C                   kfld                    X2KFKOD           5
     C     KOFASTK3      klist
     C                   kfld                    KFTYP
     C                   kfld                    X3KFKOD           5
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     BrParKey2     klist
     C                   kfld                    BIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      KFTYP   = 'AVLETEKST'
     C                   EVAL      X1KFKOD = 'ISTYP'
     C                   EVAL      X2KFKOD = 'ISAVA'
     C                   EVAL      X3KFKOD = 'ISANS'
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C                   open      BRPARF                               50
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     CUNDL01
     C                   close     ISOKDA
     C                   close     ISOKDP
     C                   close     ISOKDT
     C                   close     ISOKDS
     C                   close     KOFAST
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
