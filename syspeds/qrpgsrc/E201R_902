     FEDIS      UF   E           K DISK
     FEUNB      UF   E           K DISK
     FEDIMC     UF   E           K DISK
     FEUNH      UF   E           K DISK
     FEDIJ2     IF   E           K DISK
     FEDII      IF   E           K DISK
     FEDTM902   IF   E           K DISK
     FEFTX902   IF   E           K DISK
     FECTA902   IF   E           K DISK
     FERFF902   IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FEDISE     O  A E             DISK
     D A               S              1    DIM(70)
     D B               S              1    DIM(80)
      *________________________________________
      * DS for å venstrestille numeriske felter
      *""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15  2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
     D                 DS
     D  WUMN                   1      9  0
     D  ZGN0                  10     11  0
      *____________
      * DS for Dato
      *""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTM                    1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDT                    1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
     C                   EXSR      SRD
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Define workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    SSN           WSN
     C     *LIKE         DEFINE    S0036         W0036
     C     *LIKE         DEFINE    JCLFE         WCLFE
     C                   MOVE      *BLANKS       WCLFE
     C     *LIKE         DEFINE    MST           WST
     C                   MOVE      '+'           WST
     C     *LIKE         DEFINE    MVEN          WVEN
     C                   MOVE      *BLANKS       WVEN
     C     *LIKE         DEFINE    UIN           WUIN
     C     *LIKE         DEFINE    INNETW        WNNETW
     C     *LIKE         DEFINE    SSR           WSR
     C                   MOVE      'S'           WSR
     C                   MOVE      '0'           ECFLAG            1
     C                   MOVE      *BLANKS       W0004            35
     C                   MOVE      *BLANKS       W0010            35
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W0074             6 0
     C                   Z-ADD     0             AX                2 0
     C                   Z-ADD     1             BX                2 0
     C                   Z-ADD     0             AXV               2 0
     C                   Z-ADD     0             AXH               2 0
     C                   MOVE      'R'           WDIR              1
     C                   MOVE      *BLANKS       WCHAR             1
     C                   MOVE      'Y'           WCHART            1
     C                   MOVE      *BLANKS       WCONT             5
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
     C                   MOVE      '0'           *IN39
     C                   MOVE      '1'           *IN32
      *____________
      * Define keys
      *""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSN
     C     KEYUNB        KLIST
     C                   KFLD                    WUIN
     C     KEYUNH        KLIST
     C                   KFLD                    WUMN
     C     KEYB          KLIST
     C                   KFLD                    SSR
     C                   KFLD                    WST
     C                   KFLD                    WVEN
     C                   KFLD                    S0004
     C                   KFLD                    S0010
     C                   KFLD                    S0035
     C     KEYC          KLIST
     C                   KFLD                    S0004
     C                   KFLD                    S0010
     C                   KFLD                    WCLFE
     C     KEYMS0        KLIST
     C                   KFLD                    WUMN
     C                   KFLD                    ZGN0
     C     KEYMSG        KLIST
     C                   KFLD                    WSR
     C                   KFLD                    U0065
     C                   KFLD                    U0054
     C                   KFLD                    M0010
     C                   KFLD                    M0004
      *__________________________
      * Retreive System Date/Time
      *""""""""""""""""""""""""""
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WSN           PARM                    WSN
     C                   Z-ADD     WSN           WUIN
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start of file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________________________________
      * Get data from interchange log file
      *"""""""""""""""""""""""""""""""""""
     C     KEYA          CHAIN     EDIS                               81
     C     KEYUNB        CHAIN     EUNB                               81
     C                   Z-ADD     0             W0036
     C                   MOVEL     U0004         W0004
     C                   MOVEL     U0010         W0010
     C     W0010         CHAIN     EDII                               20
     C                   MOVEL     INNETW        WNNETW
      *_________________
      * Move member name
      *"""""""""""""""""
     C                   EVAL      SEMBR=SMBR
     C                   SELECT
      *___________________
      * Create JCL for NIT
      *"""""""""""""""""""
     C     WNNETW        WHENEQ    '1'
      *
     C                   MOVE      'F'           WCLFE
     C     KEYC          CHAIN     EDIJ2                              81
     C     *IN81         DOWEQ     '0'
     C                   MOVE      JCLC          SDATA
     C                   WRITE     EDISER
     C     KEYC          READE     EDIJ2                                  81
     C  N81              END
     C                   ENDSL
      *________________
      * Add UNB-segment
      *""""""""""""""""
     C                   EXSR      UNB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Read message log                                     SRC /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C     KEYB          CHAIN     EDIMC                              8182
     C                   Z-ADD     MMN           WUMN
     C     *IN81         DOWEQ     '0'
     C  N81
     CANN82KEYUNH        CHAIN     EUNH                               8384
     C     *IN83         IFEQ      '0'
     C     M0065         ANDEQ     'IFTMBC'
     C                   Z-ADD     SSN           MSN
     C                   MOVE      'A'           MST
     C                   Z-ADD     WDT           MDT
     C                   Z-ADD     WTM           MTM
     C                   ADD       1             W0036
     C                   EXSR      SRCA
     C                   Z-ADD     WUIN          UIN
     C                   UPDATE    EDIMR
     C                   UPDATE    UNHR
      * Oppslag i ECMSG for å se om det skal sendes en og en melding
      * eller akumuleres opp på vanlig måte.
     C     ECFLAG        IFEQ      '0'
     C     KEYMSG        CHAIN     ECMSG1                             90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *BLANK        ECSEND
     C                   ENDIF
     C                   MOVE      '1'           ECFLAG
      * Forlater løkken hvis det skal sendes en og en melding.
     C     ECSEND        IFNE      *BLANKS
     C                   LEAVE
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
     C     KEYB          READE     EDIMC                                8281
     C                   Z-ADD     MMN           WUMN
     C  N81              END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  End of interchange                                   SRD /
      *////////////////////////////////////////////////////////////
     C     SRD           BEGSR
     C                   Z-ADD     W0036         S0036
     C                   MOVE      W0036         U0036
      *____________________
      * Interchange trailer
      *""""""""""""""""""""
     C                   EXSR      UNZ
      *____________________________
      * Update interchange log file
      *""""""""""""""""""""""""""""
     C                   MOVE      'A'           SST
     C                   UPDATE    EDISR
     C                   UPDATE    UNBR
      *___________________
      * Create JCL for NIT
      *"""""""""""""""""""
     C     WNNETW        IFEQ      '1'
     C                   MOVE      'E'           WCLFE
     C     KEYC          CHAIN     EDIJ2                              81
     C     *IN81         DOWEQ     '0'
     C                   MOVE      JCLC          SDATA
     C                   WRITE     EDISER
     C     KEYC          READE     EDIJ2                                  81
     C  N81              END
     C                   END
     C                   ENDSR
      */////////////////////////////////////////////////////////////
      * Add segments                                           SRCA
      */////////////////////////////////////////////////////////////
     C     SRCA          BEGSR
     C                   EXSR      UNH
     C                   EXSR      BGM
      *
     C                   EXSR      DTM00
     C                   EXSR      FTX00
     C                   EXSR      CTA00
     C                   EXSR      RFF02
      *
     C                   EXSR      UNT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  DTM GROUP:00                                       DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      DTM
      *
     C     KEYMS0        READE     EDTM902                                51
     C  N51              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  FTX GROUP:00                                       FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      FTX
      *
     C     KEYMS0        READE     EFTX902                                51
     C  N51              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  CTA GROUP:00                                       CTA00 /
      *////////////////////////////////////////////////////////////
     C     CTA00         BEGSR
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        CHAIN     ECTA902                            51
     C     *IN51         IFEQ      '0'
      *
     C                   EXSR      CTA
      *
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  RFF GROUP:02                                       RFF02 /
      *////////////////////////////////////////////////////////////
     C     RFF02         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ERFF902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RFF
      *
     C     KEYMS0        READE     ERFF902                                51
     C  N51              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_______________
      * Find positions
      *""""""""""""""""
      * Find first non-0 position
     C     WDIR          IFEQ      'L'
     C     AXV           DO        AXH           AXV
     C     A(AXV)        COMP      '0'                                  8383
     C   83              END
      * Find last non-blank position                                     I
     C                   ELSE
      *                                                                  I
     C     SRÅ01         TAG
      *                                                                  I
     C     AXH           IFGT      AXV
     C     A(AXH)        IFEQ      *BLANKS
     C                   SUB       1             AXH
     C                   GOTO      SRÅ01
     C                   END
     C                   END
     C                   END
     C                   MOVE      'R'           WDIR
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV
     C                   MOVE      A(AXV)        WCHAR
      * If invalid character (' + : ?) then replace with 'Ø'             I
     C     WCHART        IFEQ      'Y'
     C     WDIR          IFEQ      'R'
N01  C     WCHAR         IFEQ      ';'
N01  C                   MOVE      ':'           WCHAR
N01  C                   ENDIF
     C     WCHAR         COMP      WAPSTR                                 83
     C  N83WCHAR         COMP      '+'                                    83
     C  N83WCHAR         COMP      ':'                                    83
     C  N83WCHAR         COMP      '?'                                    83
     C   83              EXSR      SRÅA
     C                   END
     C                   END
      * Move to output string                                            I
     C                   MOVE      WCHAR         B(BX)
     C                   ADD       1             BX
      * If arrayindex for output array is over 80 Then add a record      I
     C     BX            IFGT      80
     C                   MOVEA     B             SDATA
     C                   WRITE     EDISER
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       B
     C                   END
      *                                                                  I
     C                   END
      *
     C                   MOVE      'Y'           WCHART
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for replace control characters                      SRÅA
      *////////////////////////////////////////////////////////////
     C     SRÅA          BEGSR
      * Move '?' before the character
     C                   MOVE      '?'           B(BX)
     C                   ADD       1             BX
     C     BX            IFGT      80
     C                   MOVEA     B             SDATA
     C                   WRITE     EDISER
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       B
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify numerical fields                        SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * MOve correct value into Alfa 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'
     C                   MOVE      ØA152H        ØA12
     C                   ELSE
     C                   MOVEL     ØA152X        ØA12
     C                   MOVE      ',  '         ØA12
     C                   MOVE      ØA152D        ØA12
     C                   END
      * Scan from left and find first num over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
      /COPY QEDSRC,BGM_O
      /COPY QEDSRC,UNB_O
      /COPY QEDSRC,UNH_O
      /COPY QEDSRC,UNT_O
      /COPY QEDSRC,UNZ_O
      /COPY QEDSRC,DTM_O_902
      /COPY QEDSRC,FTX_O_902
      /COPY QEDSRC,CTA_O_902
      /COPY QEDSRC,RFF_O_902
