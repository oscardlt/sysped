      *********************************************************************
      * Add/upfdate/delete av frisøkevei
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE31R MODULE(*LIBL/TJGE31R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODFRI    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FKODFRILF  IF   E           K DISK    USROPN
     F                                     RENAME(KODFRIR:KODFRIRL)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
          +'¬oko¬ : ¬'+%trim(KFSOKO)+'¬, '
          +'¬otx¬ : ¬'+%trim(KFSOTX)+'¬, '
          +'¬odk¬ : ¬'+%trim(KFSODK)+'¬, '
          +'¬ttu¬ : ¬'+%trim(KFSTTU)+'¬, '
          +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"freesearchUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KODFRI
     C  N50              CLOSE     KODFRILF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WMODE      = zhbgetvar('MODE')
      * WMODE = A --> Lage ny post
      *         U --> Oppdater post
      *         D --> Slett post
      *         I --> Spør post
     C                   EVAL      KFSOKO     = zhbgetvar('OKO')
     C     LO:UPC        XLATE     KFSOKO        KFSOKO
     C                   EVAL      KFSOTX     = zhbgetvar('OTX')
     C     LO:UPC        XLATE     KFSOTX        KFSOTX
     C                   EVAL      KFSODK     = zhbgetvar('OTX')
     C     LO:UPC        XLATE     KFSODK        KFSODK
     C                   EVAL      KFSTTU     = zhbgetvar('TTU')
     C     LO:UPC        XLATE     KFSTTU        KFSTTU
     C                   EVAL      O_KFSOKO     = zhbgetvar('O_KFSOKO')
     C     LO:UPC        XLATE     O_KFSOKO      O_KFSOKO
      *
     C                   ENDSR
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
      *
     C     *LIKE         DEFINE    KFSOKO        O_KFSOKO
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KODFRI
     C  N50              OPEN      KODFRILF
      * dummy for å få feltnavn som arbeids/inputvariable
     C     *IN50         IFEQ      *OFF
     C     *IN50         ANDEQ     *ON
     C                   OPEN      KODFRILF
     C                   READ      KODFRILF
     C                   ENDIF
      *
     c                   endsr
      *
      *********************************************************
     C     TEST          BEGSR
      *********************************************************
     C                   IF        WMODE = 'A'
      * Feil hvis kode for frisøkevei finnes fra før
     C     KFSOKO        CHAIN(N)  KODFRI                             33
     C                   IF        *IN33 = *OFF
     C                   eval      Error = 'kode for frisøkevei finnes allerede'
     c                   goto      UtTest
     C                   ENDIF
     C                   ELSE
      * Feil hvis opperinnelig kode for frisøkevei ikke finnes ved endring/sletting/spørring
     C     O_KFSOKO      CHAIN(N)  KODFRI                             33
     C                   IF        *IN33=*ON
     C                   eval      Error = 'Kode for frisøkevei finnes ikke'
     c                   goto      UtTest
     C                   ENDIF
     C                   ENDIF
      *
     C                   IF        WMODE = 'A' OR WMODE = 'U'
      * Kode for frisøkevei
     C     KFSOKO        COMP      *BLANKS                                33
     C  N33KFSOKO        CHAIN(N)  KODFRI                             33
     C                   IF        *IN33 = *OFF
     C                   eval      Error = 'Ugyldig kode for frisøkevei'
     c                   goto      UtTest
     C                   ENDIF
      * Beskrivelse
     C                   IF        KFSOTX = *BLANKS
     C                   eval      Error = 'Ugyldig tekst for kode'
     c                   goto      UtTest
     C                   ENDIF
      *
     C                   ENDIF
      *
      * MODE A=Add   U=Update   D=Delete
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C     WMODE         OREQ      'U'
     C                   EXSR      SRAU
     C     WMODE         WHENEQ    'D'
     C                   EXSR      SRD
     C     WMODE         WHENEQ    'I'
     C                   EXSR      SRI
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
      *
     C     UtTest        ENDSR
      *
      *********************************************************
     C     SRAU          BEGSR
      *********************************************************
      * forberede visse felter
     c                   exsr      FixData
      * Add/update
     C     WMODE         ifeq      'A'
     C                   else
     C     O_KFSOKO      CHAIN     KODFRI                             33
     c                   endif
     C                   EVAL      XKFSOKO  = KFSOKO
     C                   EVAL      XKFSOTX  = KFSOTX
     C                   EVAL      XKFSODK  = KFSODK
     C                   EVAL      XKFSTTU  = KFSTTU

     C     WMODE         ifeq      'A'
     C                   WRITE     KODFRIR
     C                   else
     C                   UPDATE    KODFRIR
     C                   end
      *

     C                   ENDSR
      *********************************************************
     C     FixData       BEGSR
      *********************************************************
      *

     C                   ENDSR
      *
      *********************************************************
     C     SRD           BEGSR
      *********************************************************
      * Delete
     C     O_KFSOKO      CHAIN     KODFRI                             33
     C  N33              DELETE    KODFRIR
      *
     C                   ENDSR
      *********************************************************
     C     SRI           BEGSR
      *********************************************************
      * Inquiry
     C     O_KFSOKO      CHAIN(N)  KODFRI                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      KFSOKO  = XKFSOKO
     C                   EVAL      KFSOTX  = XKFSOTX
     C                   EVAL      KFSODK  = XKFSODK
     C                   EVAL      KFSTTU  = XKFSTTU
     C                   END
      *
     C                   ENDSR
