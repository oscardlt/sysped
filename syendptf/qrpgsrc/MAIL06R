********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 NUSTILLER CAVD
********************************************************************
      *
      ***********************************************************
      *** PROGRAM FOR ALFABETISK SØKING PÅ KUNDER             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 02 06-11 13-24
      *** LEDIGE INDIKATORER 27 28 31 32 34-41 43-77 79 85-90 95-97
      ***********************************************************
     FCUNDC01   IF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FCUNDL02   IF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FCUNDL03   IF   E           K DISK
     FCUNDL04   IF   E           K DISK
     FCUNDL05   IF   E           K DISK
     FCUNDL06   IF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR6)
     FVADR      IF   E           K DISK
     FMAIL06FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  W2POST                 1      4
     D  W2PO11                 1      1
     D  W2PO12                 1      2
     D  W2PO13                 1      3
     D  W2PO14                 1      4  0
     D  W2PO24                 2      4
     D  W2PO34                 3      4
     D  W2PO44                 4      4
     D                 DS
     D  W2ALF2                 1     10
     D  AL                     1     10
     D                                     DIM(10)
     D                 DS
     D  SYRG                   1     14
     D  SYFORE                 1      9
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2ALFA        X2ALFA           10
     C                   MOVE      W2POST        X2POST            4
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   80PRIKU         SETLL     CUNDL02
     C   81KUND3K        SETLL     CUNDL03
     C   82KUND4K        SETLL     CUNDL04
     C   83KUND5K        SETLL     CUNDL05
     C   84KUND6K        SETLL     CUNDL06
     C     START1        TAG
     C   80UFIRMA        READE     CUNDL02                                94
     C   81WFIPO3        READE     CUNDL03                                94
     C   82WFIPO4        READE     CUNDL04                                94
     C   83WFIPO5        READE     CUNDL05                                94
     C   84WFIPO6        READE     CUNDL06                                94
     C  N94W2ALF2        COMP      SONAVN                               94
      *
     C     START2        TAG
     C                   MOVE      'N'           AKUNJN            1
      *
     C  N94              DO
     C                   MOVE      X2ALFA        W2ALFA
     C                   MOVE      X2POST        W2POST
     C                   DO        18
     C                   ADD       1             ZRECNO
     C                   MULT(H)   -1            ODDE              1 042
     C     VADRK         CHAIN     VADR                               78
     C     *IN78         IFEQ      '0'
     C                   MOVE      *ZEROS        WSPNR
     C                   MOVEL     VADRN3        WSADR3
     C                   ELSE
     C                   MOVE      POSTNR        WSPNR
     C                   MOVEL     ADR3          WSADR3
     C                   MOVE      *BLANKS       VADRNA
     C                   MOVE      *BLANKS       VADRN1
     C                   MOVE      *BLANKS       VADRN2
     C                   MOVE      *BLANKS       VADRN3
     C                   END
     C     AKTKOD        COMP      'I'                                    70
     C   70              MOVE      'J'           AKUNJN
     C                   WRITE     SUBFIL
     C   80UFIRMA        READE     CUNDL02                                94
     C   81WFIPO3        READE     CUNDL03                                94
     C   82WFIPO4        READE     CUNDL04                                94
     C   83WFIPO5        READE     CUNDL05                                94
     C   84WFIPO6        READE     CUNDL06                                94
     C  N94W2ALF2        COMP      SONAVN                               94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     *IN29         IFEQ      '0'
     C     W2ALFA        ANDEQ     *BLANKS
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      '1'           *IN29
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
      *
     C   05              DO
     C                   MOVE      X2ALFA        W2ALFA
     C                   MOVE      X2POST        W2POST
     C                   GOTO      START
     C                   END
     C*
     C* frisøk
     C   10              DO
     C                   CALL      'SYGE22'
     C                   PARM                    USØKKN           30
     C                   PARM                    UKUNR             8 0
     C                   GOTO      slutt
     C                   END
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C     W2KUNR        ANDNE     0
     C     KUND1K        CHAIN     CUNDL01                            33
     C   33              GOTO      START
     C                   MOVE      W2KUNR        UKUNR
     C                   MOVE      KNAVN         USØKKN
     C                   GOTO      SLUTT
     C                   END
     C     W2ALFA        IFNE      *BLANKS
     C                   EXSR      SETMAX
     C                   MOVEA     '000'         *IN(82)
     C     W2POST        COMP      *BLANKS                            818180
     C   81W2PO24        COMP      *BLANKS                            828281
     C   82W2PO34        COMP      *BLANKS                            838382
     C   83W2PO44        COMP      *BLANKS                            848483
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   EXSR      HENT
     C                   GOTO      START3
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     1             LINNBR
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      '0'           *IN99
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   Z-ADD     KUNDNR        UKUNR
     C                   MOVE      KNAVN         USØKKN
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C  N98WSNR          IFEQ      '5'
     C     ZRECNO        DIV       5             LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     5             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   MULT      2             LINNBR
     C                   MOVE      '1'           *IN99
     C                   EXFMT     BILDE01
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '8'
     C     ZRECNO        DIV       5             LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     5             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   MULT      2             LINNBR
     C                   MOVE      '1'           *IN99
     C                   EXSR      CUNDCA
     C                   CALL      'VCUNDCR'
     C                   PARM                    FIRMA
     C                   PARM                    KUNDNR
     C                   PARM                    RRN              10 0
     C                   PARM                    UEMFA             1
     C                   PARM                    UCONTA           30
     C     RRN           IFNE      *ZEROS
     C                   MOVEL     'KP'          UPAR12           12
     C                   MOVE      RRN           UPAR12
     C                   MOVEL     UPAR12        UPAR13           13
     C                   MOVE      UEMFA         UPAR13
     C                   MOVEL     UPAR13        USØKKN
     C                   Z-ADD     KUNDNR        UKUNR
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C                   END
     C                   END
     C*
     C                   ADD       3             LINNBR
     C                   Z-ADD     2             POSNBR
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     PRIKU         KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2ALFA
     C     KUND3K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO11
     C                   KFLD                    W2ALFA
     C     KUND4K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO12
     C                   KFLD                    W2ALFA
     C     KUND5K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO13
     C                   KFLD                    W2ALFA
     C     KUND6K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO14
     C                   KFLD                    W2ALFA
     C     WFIPO3        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO11
     C     WFIPO4        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO12
     C     WFIPO5        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO13
     C     WFIPO6        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2PO14
     C     KUNKEY        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    KUNDNR
     C     KUND1K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    W2KUNR
     C     VADRK         KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    XVADRN
     C                   Z-ADD     1             XVADRN            5 0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UKUNR             8 0
     C                   PARM                    UFIRMA            2
     C                   PARM                    USØKKN           30
     C*
     C                   Z-ADD     1             ODDE
     C                   MOVEL     USØKKN        W2ALFA
     C                   EXSR      SETMAX
     C     PRIKU         SETLL     CUNDL02
     C     UFIRMA        READE     CUNDL02                                33
      * ALTSEQ I FIL GJØR AT EN KAN LESE SEG FRAM TIL LAVERE SONAVN!!
     C  N33SONAVN        COMP      W2ALF2                             33
     C  N33SONAVN        COMP      W2ALFA                               33
     C  N33              DO
     C                   MOVE      KUNDNR        XKN               8 0
     C                   MOVE      KNAVN         XNAVN            30
     C     UFIRMA        READE     CUNDL02                                33
     C  N33SONAVN        COMP      W2ALF2                             33
     C  N33SONAVN        COMP      W2ALFA                               33
     C   33              MOVE      XKN           UKUNR
     C   33              MOVE      XNAVN         USØKKN
     C   33              MOVE      '1'           *INLR
     C   33              RETURN
     C                   END
      *
     C                   MOVE      '1'           *IN80
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SETMAX        BEGSR
     C***********************************************
     C                   MOVEL     W2ALFA        WALFA1            1
     C     WALFA1        IFEQ      '*'
     C                   MOVEL     '99999999'    W2ALF2
     C                   ELSE
     C                   MOVE      W2ALFA        W2ALF2
     C                   Z-ADD     10            NR                2 0
     C     STSMAX        TAG
     C     AL(NR)        IFEQ      ' '
     C                   MOVE      '9'           AL(NR)
     C                   SUB       1             NR
     C     NR            IFNE      0
     C                   GOTO      STSMAX
     C                   END
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CUNDCA        BEGSR
     C***********************************************
      * ADD POST I CUNDC HVIS MANGLER
     C     KUNKEY        SETLL     CUNDC01                                51
     C     *IN51         IFEQ      *OFF
     C                   MOVEL     FIRMA         CFIRMA
     C                   Z-ADD     KUNDNR        CCOMPN
     C                   MOVEL     KPERS         CCONTA
     C     TFAXNR        IFNE      *BLANKS
     C                   MOVEL     TFAXNR        CFAX
     C                   ELSE
     C                   MOVEL     TLF           CFAX
     C                   ENDIF
N01  C                   MOVE      *ZEROS        CAVD
     C                   WRITE     CUNDCR
     C                   ENDIF
     C                   ENDSR
