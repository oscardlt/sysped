      *
     FSYAV01D   CF   E             WORKSTN
     D                 DS
     D  ZDT                    1      6  0
     D  ZDTM                   3      4  0
     D  ZDTD                   5      6  0
     D                 DS
     D  WTIME                  1     12  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     12  0
     D                 DS
     D  WDT                    1      6  0
     D  WÅ                     1      2  0
     D  WM                     3      4  0
     D  WD                     5      6  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN12         WIN12
     C     *LIKE         DEFINE    *IN30         WIN30
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM      ZAVDF         ZAVDF
     C                   PARM      ZAVDT         ZAVDT
     C                   PARM      ZFLYF         ZFLYF
     C                   PARM      ZFLYT         ZFLYT
     C                   PARM      ZDTF          ZDTF
     C                   PARM      ZDTT          ZDTT
     C                   PARM      ZEI           ZEI
     C                   PARM      ZDEV          ZDEV
     C                   PARM      *IN03         WIN03
     C                   PARM      *IN12         WIN12
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM      ZDEV          ZDEV
     C     *IN30         PARM                    WIN30
     C                   PARM      ZOBJTY        ZOBJTY
     C                   MOVEL     '*OUTQ'       ZOBJTY           10
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   TIME                    WTIME
     C     WÅÅ           IFGT      50
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   ELSE
     C                   Z-ADD     WDD           WÅ
     C                   Z-ADD     WÅÅ           WD
     C                   END
     C                   Z-ADD     WMM           WM
     C                   Z-ADD     0             ZAVDF
     C                   Z-ADD     9999          ZAVDT
     C                   Z-ADD     0             ZFLYF
     C                   Z-ADD     999           ZFLYT
     C                   Z-ADD     510101        ZDTF
     C                   Z-ADD     WDT           ZDTT
     C                   MOVE      'E'           ZEI
     C                   MOVEL     '*JOB'        ZDEV
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BILD09
     C                   EXFMT     BILD01
     C                   MOVEA     '00000000'    *IN(30)
     C                   MOVE      '0'           *IN38
     C     *IN03         CABEQ     '1'           SRB9
      * Avdeling til må vare større en til.
     C     ZAVDF         IFGT      ZAVDT
     C                   MOVE      'SPA0003'     ZMSGNO
     C                   MOVEA     '111'         *IN(30)
     C                   GOTO      SRB1
     C                   END
      * Flyselskap til må vare større en til.
     C     ZFLYF         IFGT      ZFLYT
     C                   MOVE      'SPF0000'     ZMSGNO
     C                   MOVEA     '10011'       *IN(30)
     C                   GOTO      SRB1
     C                   END
      * Teste dato fra:
     C                   Z-ADD     ZDTF          ZDT
     C     ZDTM          COMP      1                                    30
     C  N30ZDTM          COMP      12                                 30
     C  N30ZDTD          COMP      1                                    30
     C  N30ZDTD          COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVE      '1'           *IN35
     C   30              GOTO      SRB1
      * Teste dato til:
     C                   Z-ADD     ZDTT          ZDT
     C     ZDTM          COMP      1                                    30
     C  N30ZDTM          COMP      12                                 30
     C  N30ZDTD          COMP      1                                    30
     C  N30ZDTD          COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVE      '1'           *IN36
     C   30              GOTO      SRB1
      * Fra dato større en til dato.
     C                   MOVE      ZDTF          ZDTF8             8 0
     C     ZDTF          IFGT      500000
     C                   MOVEL     '19'          ZDTF8
     C                   ELSE
     C                   MOVEL     '20'          ZDTF8
     C                   END
     C                   MOVE      ZDTT          ZDTT8             8 0
     C     ZDTT          IFGT      500000
     C                   MOVEL     '19'          ZDTT8
     C                   ELSE
     C                   MOVEL     '20'          ZDTT8
     C                   END
     C     ZDTF8         IFGT      ZDTT8
     C                   MOVE      'SPD0002'     ZMSGNO
     C                   MOVEA     '11'          *IN(35)
     C                   GOTO      SRB1
     C                   END
      * Valid skriverenhet ?
     C     ZDEV          IFNE      '*JOB'
     C                   CALL      'EDI33C'      PLISTA
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPS0018'     ZMSGNO
     C                   MOVE      '1'           *IN38
     C                   GOTO      SRB1
     C                   END
     C                   END
      *
     C     SRB9          ENDSR
