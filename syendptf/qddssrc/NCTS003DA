      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLAVD0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '3' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            THAVD          4Y 0O  6  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  6  9
     A            THCATS         8A  O  6 43
     A            X0004          8A  O  6 52
     A            THTINA        17A  O  6 61
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS003RA'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A  27                              2 23'   FORHÅNDSVARSLING   '
     A                                      COLOR(WHT)
     A N27                              2 23'       N C T S  -  AVGANG       '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd Avdelingsnavn              -
     A                                             Avs.T.sd Avs.NODI Foretaksnr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2AAVD         4Y 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2022  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'Bekreft sletting')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1  2MSGCON(075 SHU0983 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  2  2'   Avd Avdelingsnavn              -
     A                                             Avs.T.sd Avs.NODI Foretaksnr'
     A                                      COLOR(WHT)
     A            THAVD          4Y 0O  3  4EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(PC)
     A            KOANVN        33A  O  3  9
     A            THCATS         8A  O  3 43
     A            X0004          8A  O  3 52
     A            THTINA        17A  O  3 61
     A                                  4  2MSGCON(075 SHU0983 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  2022  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS003RA'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A  27                              2 23'   FORHÅNDSVARSLING   '
     A                                      COLOR(WHT)
     A N27                              2 23'       N C T S  -  AVGANG       '
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd Avdelingsnavn              -
     A                                             Avs.T.sd Avs.NODI Foretaksnr'
     A                                      COLOR(WHT)
     A                                 26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             27 40'(C) COPYRIGHT  SYSTEMA A/S  2022  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(7 25 4 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'søking')
     A                                      CF12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2MSGCON(018 SHU0984 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 SHU0985 *LIBL/SYMSGF)
     A            THAVD          4  0O  2 14COLOR(WHT)
     A                                  3  2MSGCON(008 SHU0986 *LIBL/SYMSGF)
     A            WSAVDT         4Y 0B  3 14DSPATR(UL)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A N30                              4  2MSGCON(24 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
