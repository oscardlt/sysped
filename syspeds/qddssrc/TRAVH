     A*____________________________
     A* SYSPED / TRANSPORTØRANREGNING - HODE
     A*""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R TRAVHR
     A            THST           1          COLHDG('STATUS')
     A            THTNR          8S 0       COLHDG('TRANSPORTØRNR.')
     A            THRTNR    R               REFFLD(PRO)
     A                                      COLHDG('RUDNTURSNR')
     A            THKDM     R               REFFLD(KDM)
     A                                      COLHDG('Momskode')
     A            THTAVD    R               REFFLD(AVD)
     A                                      COLHDG('AVDELING TUR')
     A            THTUNR    R               REFFLD(PRO)
     A                                      COLHDG('TUR (DELTUR)')
     A            THAVT     R               REFFLD(BEL11)
     A                                      COLHDG('AVTALT        (A)')
     A            THBRT     R               REFFLD(BEL11)
     A                                      COLHDG('BRUTT-PROVI.  (B)')
     A            THINH     R               REFFLD(BEL11)
     A                                      COLHDG('INNHENTING    (C)')
     A            THUTK     R               REFFLD(BEL11)
     A                                      COLHDG('UTKJØRING     (D)')
     A            THRKV     R               REFFLD(BEL11)
     A                                      COLHDG('REKV. KOSTN.  (E)')
     A            THBUD     R               REFFLD(BEL11)
     A                                      COLHDG('BUDSJ/I.PÅLØ. (F)')
     A            THJUS     R               REFFLD(BEL11)
     A                                      COLHDG('EKSTRA JUST.  (G)')
     A            THTOT     R               REFFLD(BEL11)
     A                                      COLHDG('TOTALT AVR.BELØP')
     A* ANTALL POSTER AV TYPE (A-G) I SUBREGISTER (TRAVV)
      * PFT V12 = LENGDE 3 -> 5
     A            THAVTA         5P 0       COLHDG('ANT. AVTALT   (A)')
     A            THBRTA         5P 0       COLHDG('ANT. BRU.-PRO.(B)')
     A            THINHA         5P 0       COLHDG('ANT. INNHENT. (C)')
     A            THUTKA         5P 0       COLHDG('ANT. UTKJØR.  (D)')
     A            THRKVA         5P 0       COLHDG('ANT. REKV.KOST(E)')
     A            THBUDA         5P 0       COLHDG('ANT. BUDS.KOST(F)')
     A            THJUSA         5P 0       COLHDG('ANT. EKSTR.JUS(G)')
     A* AVREGNINGSNUMMER BEHOV BORTFALLER ???
     A* BURDE VÆRE KREDITNOTANR / BILNR I KOSTA ?????????
     A*(SOM SÅ IGJEN KUNNE LAGRES I "TUAVRE" I FIL TURER)
     A            THAVNR         7P 0       COLHDG('AVREGNINGSNR')
     A            THKUNR    R               REFFLD(KN)
     A                                      COLHDG('Kundnr')
     A            THFAKT    R               REFFLD(FN)
     A                                      COLHDG('Fakt.nr')
     A            THJOUR    R               REFFLD(JNR)
     A                                      COLHDG('journalnr')
     A            THCC      R               REFFLD(DTC)
     A                                      COLHDG('periode.århundre')
     A            THÅR      R               REFFLD(DTÅ)
     A                                      COLHDG('periode.år')
     A            THMND     R               REFFLD(DTM)
     A                                      COLHDG('periode.mnd')
     A            THDATO    R               REFFLD(DT)
     A                                      COLHDG('fakturadato')
     A            THDAFF    R               REFFLD(DT)
     A                                      COLHDG('forfallsdato')
     A            THSTAR         1          COLHDG('IARBEID STATUS')
     A*
     A          K THST
     A          K THTNR
     A          K THRTNR
     A          K THKDM
     A          K THTAVD
     A          K THTUNR
