      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS137 MODULE(*LIBL/SS137 *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FGSSBXF    UF A E           K DISK    USROPN
     FGSSOLL01  IF   E           K DISK    USROPN
     FGSSTRL01  if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D UsrSpcName      s             10
     D PDF             S              1
     D CURTUR          S             11
     D CURTURN         S             11  0
     D IFSFIL          S             30
     D UNIK            S              7
     D UNIK6           S              6  0
     D LTyp            S              1
     D Dialog          S              1
     D FMELDNR         S              2
     D FEILMELDING     S             80
     D INDEX           S              3  0
     D i               s             10i 0
      *
     D Boxtyp          S              1
     D TMPALF          S             19
     D TMPNUM          S             19  8
      * Number of records matching search criteria
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D BX              S              3  0 DIM(999)                             INPUT STRING
     D BXV             S              7  1 DIM(999)                             INPUT STRING
     D BXL             S              4  0 DIM(999)                             INPUT STRING
     D BXB             S              4  0 DIM(999)                             INPUT STRING
     D BXH             S              4  0 DIM(999)                             INPUT STRING
     D BXC             S             17    DIM(999)                             OUTPUT STRING
      * BX3/BXM er M3 /LM fra DDS (når lastet i kasse 901.. =BIL/UNIT)
     D BX3             S              6  3 DIM(999)                             OUTPUT STRING
     D BXM             S              4  2 DIM(999)                             OUTPUT STRING
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      ** Dialog=K (Kopi av manifest) "sklir tvers igjennom" og kaller CL.
      *
      ** 1. "syklus" - hent inputdata fra fil....
     c     Dialog        IFEQ      'J'
     c                   exsr      FINNBOX
     c                   end
      ** Kommer fra foregående dialogbilde....
      ** Test inputdata -  Ved feil settes Dialog= J -> Ny visning !!
     c     Dialog        IFEQ      'F'
     c                   exsr      tstfeil
     c                   end
      *
      ** Behov for (ny..) dialog FØR spool ..
     C     Dialog        IFeq      'J'
     c     Ltyp          andne     'F'
     C                   exsr      SetSklDial
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('CURTUR':
     C                             %trim(%editc(CURTURN:'Z')))
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('ltyp':ltyp)
     C                   callp     updhtmlvar('pdf':pdf)
     C                   callp     updhtmlvar('Boxtyp':Boxtyp)
     C                   callp     updhtmlvar('TOTBOX':
     C                             %trim(%editc(TOTBOX:'Z')))
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     updhtmlvar('INDEX':
     C                             %trim(%editc(INDEX:'3')))
     C                   callp     wrtsection('Boxtop')
      *
      *  Om turens status er feil er ikke box-innhold initiert..
     c     FeilStatus    CabEQ     'Y'           UTBOX
      *
      * Skriv en linje pr BOX / Bil..
      * Behandle en og en boks. (dummy neg. nr er sluttflagg)
     C     1             DO        999           NR
     C     BX(NR)        CABLT     0             utbox
     C                   callp     updhtmlvar('BOX':
     C                             %trim(%editc(NR:'Z')))
     C                   callp     updhtmlvar('TOTBOX':
     C                             %trim(%editc(TOTBOX:'Z')))
     C                   callp     updhtmlvar('CID':BXC(NR))
     C                   callp     updhtmlvar('VKT':
     C                             %trim(%editc(BXV(NR):'M')))
     C                   callp     updhtmlvar('M3':
     C                             %trim(%editc(BX3(NR):'M')))
     C                   callp     updhtmlvar('LM':
     C                             %trim(%editc(BXM(NR):'M')))
     C                   callp     updhtmlvar('LEN':
     C                             %trim(%editc(BXL(NR):'Z')))
     C                   callp     updhtmlvar('BRD':
     C                             %trim(%editc(BXB(NR):'Z')))
     C                   callp     updhtmlvar('HOY':
     C                             %trim(%editc(BXH(NR):'Z')))
     C                   callp     wrtsection('Boxlin')
     C                   END
      *
     C     UTBOX         TAG
     C                   callp     wrtsection('Boxbot')
      *
      *
      *
     C                   else
      *
     c     Ltyp          IFEQ      'P'
     c     Dialog        OREQ      'K'
     c     Ltyp          ANDEQ     'F'
      * ALT OK - aktuell printjobb skal kjøres...
      * "Boks-tall" lagres i oppdrags fritext  (hvis ikke KOPI)
     C     Dialog        IFne      'K'
     C                   EXSR      SAVEBOX
     C                   end
      *  Kall prog for å generere html-fil i IFS.
     C                   MOVE      CURTURN       TUR              11
     c     PDF           IFEQ      'J'
     C                   eval      IFSFil = 'PackLst'+TUR+LTYP+'.pdf'
     c                   else
     C                   eval      IFSFil = 'PackLst'+TUR+LTYP+'.txt'
     c                   end
     c                   eval      Unik = UsrSpcName
     c     Unik          ifeq      *blanks
     c                   eval      Unik6    = random(1:999999)
     c                   move      Unik6         Unik
     c                   movel     'W'           Unik
     c                   end
     C                   call      'SS137C'
     c                   parm                    IFSFIL
     c                   parm                    PDF
     c                   parm                    TUR
     c                   parm                    LTyp
     c                   parm                    WSUSER
     C                   parm                    WSPOACK           1
     C                   parm                    WSKLDET           1
     C                   parm                    WSM3LM            1
     c                   parm                    UNIK
      * Set output skeleton html member name
      *-dummy html for å få kjørt prog                                      ******
      * "bytter ut seg selv" med Html-fil i IFS umiddelbart                 ******
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     wrtsection('top')
      *
     c                   ELSE
      *
     C                   exsr      SetSklDial2
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('CURTUR':
     C                             %trim(%editc(CURTURN:'Z')))
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('ltyp':ltyp)
     C                   callp     updhtmlvar('pdf':pdf)
     C                   callp     updHtmlVar('STEDTIL':TRMFT1)
     C                   callp     updHtmlVar('TRANSP':TRMFT2)
     C                   callp     updhtmlvar('ATDDT':
     C                             %trim(%editw(TRDTS:'    .  .  ')))
     C                   EVAL      TRDTL = 0
     C                   callp     updhtmlvar('ETADT':
     C                             %trim(%editw(TRDTL:'    .  .  ')))
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      *
     c                   end
      *
     c                   end
      *------------------                                                   ******
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C*
     C                   close     BRIDF
     C                   close     GSSBXF
     C                   close     GSSOLL01
     C                   close     GSSTRL01
     C                   eval      *inlr = *on
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      PDF    = zhbgetvar('PDF')
     C                   EVAL      CURTUR  = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
     C                   EVAL      TOTBOXA = zhbgetvar('TOTBOX')
     C                   eval      TOTBOX  = c2n2(TOTBOXA)
     C                   MOVE      TOTBOX        TOTBOXN           3 0
     C                   eval      Dialog = zhbgetvar('Dialog')
     C                   eval      BoxTyp = zhbgetvar('BoxTyp')
     C                   eval      LTyp   = zhbgetvar('LTyp')
     c     LTyp          Ifeq      ' '
     c                   move      'P'           LTyp
     c                   end
     c     PDF           IfNE      'J'
     c                   move      'N'           PDF
     c                   end
      *
      * Når en kommer FRA inputbilde med TOTBOX ulik 0 - lagre Inp i Array
     C     Dialog        Ifeq      'F'
     C     TotBox        andne     *zeros
     C     1             DO        TotBox        i
     C                   eval      BX(i) = i
     C                   eval      TMPALF = ZhbGetVar('VKT':i)
     C                   eval      TMPNUM = c2n2(TMPALF)
     c     TMPNUM        IFLE      999999
     C                   eval      BXV(i) = TMPNUM
     c                   else
     c                   move      'Y'           Feilvkt           1
     c                   move      i             FVi               3 0
     c                   end
     C                   eval      TMPALF = ZhbGetVar('LEN':i)
     C                   eval      TMPNUM = c2n2(TMPALF)
     C                   eval      BXL(i) = TMPNUM
     C                   eval      TMPALF = ZhbGetVar('BRD':i)
     C                   eval      TMPNUM = c2n2(TMPALF)
     C                   eval      BXB(i) = TMPNUM
     C                   eval      TMPALF = ZhbGetVar('HOY':i)
     C                   eval      TMPNUM = c2n2(TMPALF)
     C                   eval      BXH(i) = TMPNUM
     C                   eval      TMPALF = ZhbGetVar('M3':i)
     C                   eval      TMPNUM = c2n2(TMPALF)
     c     TMPNUM        IFLE      999
     C                   eval      BX3(i) = TMPNUM
     c                   else
     c                   move      'Y'           FeilM3            1
     c                   move      i             F3i               3 0
     c                   end
     C                   eval      TMPALF = ZhbGetVar('LM':i)
     C                   eval      TMPNUM = c2n2(TMPALF)
     c     TMPNUM        IFLE      99
     C                   eval      BXM(i) = TMPNUM
     c                   else
     c                   move      'Y'           FeilLM            1
     c                   move      i             FMi               3 0
     c                   end
     C                   eval      BXC(i) = ZhbGetVar('CID':i)
     c                   end
      * Når en kommer ut av loopen er nr 1 høyere enn TOTBOX.
      * Negativ BoxNr er sluttmerke for array.
     C                   eval      BX(i) = -1
     c                   end
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklDial    begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SS137'
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklDial2   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SS137A'
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      * Test data -  ulike tester ved box (001-900) og bil (901-998)
      *=====================================================================******
     C     TstFeil       begsr
      * Feil status på turen
     c     FeilStatus    Ifeq      'Y'
     C                   eval      FEILMELDING='The shipping list been ' +
     C                                 'closed. Pls push the Cancel button.'
     c                   eval      Fmeldnr='99'
     C                   Move      'J'           Dialog
     C                   goto      UtTest
     c                   end
      * Format-feil i input (se Parse - feilaktig verdi er IKKE lagret..)
     c     FeilVkt       Ifeq      'Y'
     C                   eval      FEILMELDING='Format of weight:  6+1!  ' +
     C                                 '(6 digits, Decimal pt., 1 decimal.)'
     c                   eval      Fmeldnr='03'
     C                   Move      'J'           Dialog
     c                   eval      index=FVi-1
     C                   goto      UtTest
     c                   end
     c     FeilM3        Ifeq      'Y'
     C                   eval      FEILMELDING='Format of cube mtr:  3+3!  ' +
     C                                 '(3 digits, Decimal pt., 3 decimals)'
     c                   eval      Fmeldnr='05'
     C                   Move      'J'           Dialog
     c                   eval      index=F3i-1
     C                   goto      UtTest
     c                   end
     c     FeilLm        Ifeq      'Y'
     C                   eval      FEILMELDING='Format of load mtr:  2+2!  ' +
     C                                 '(2 digits, Decimal pt., 2 decimals)'
     c                   eval      Fmeldnr='07'
     C                   Move      'J'           Dialog
     c                   eval      index=FMi-1
     C                   goto      UtTest
     c                   end
      *
      * test Alle linjene = postene i Arrayene...
     C     1             DO        999           NR
     C     BX(NR)        CABLT     0             uttest
      * Tester ved vanlig BOX...
     C     Boxtyp        Ifeq      'B'
     C     BXV(NR)       IFeq      *zeros
     C                   eval      FEILMELDING='Weight must be entered!'
     c                   eval      Fmeldnr='03'
     C                   Move      'J'           Dialog
     c                   eval      index=nr-1
     C                   goto      UtTest
     C                   end
     C     BXL(NR)       IFeq      *zeros
     C     BXB(NR)       OReq      *zeros
     C     BXH(NR)       OReq      *zeros
     C                   eval      FEILMELDING='All 3 dimensions for the BOX ' +
     C                                         'must be entered!'
     c                   eval      Fmeldnr='04'
     C                   Move      'J'           Dialog
     c                   eval      index=nr-1
     C                   goto      UtTest
     C                   end
     c                   else
      * tester ved boxtype U - Unit = Bil....
     C     BXC(NR)       IFeq      *blanks
     C                   eval      FEILMELDING='Unit ID must be entered!'
     c                   eval      Fmeldnr='06'
     C                   Move      'J'           Dialog
     c                   eval      index=nr-1
     C                   goto      UtTest
     C                   end
     C     BXV(NR)       IFeq      *zeros
     C                   eval      FEILMELDING='Weight must be entered!'
     c                   eval      Fmeldnr='03'
     C                   Move      'J'           Dialog
     c                   eval      index=nr-1
     C                   goto      UtTest
     C                   end
     C     BX3(NR)       IFeq      *zeros
     C                   eval      FEILMELDING='Cube meters must be entered!'
     c                   eval      Fmeldnr='05'
     C                   Move      'J'           Dialog
     c                   eval      index=nr-1
     C                   goto      UtTest
     C                   end
      *
     C                   end
      *
     C                   END
      *
     C     Uttest        endsr
     C***********************************************
     C     FINNBOX       BEGSR
     C***********************************************
     C                   move      '0'           *IN30
     C                   move      'B'           BOXTYP
     C                   SETOFF                                       30
      * FINN antall bokser i bruk
     C                   MOVE      *ZEROS        TOTBOX            3 0
     C                   MOVE      *ZEROS        GMBOX             3 0
     C                   MOVE      *ZEROS        NR                3 0
     C                   CLEAR                   BX
     C     curturn       SETLL     GSSOLL01                           33
     C     curturn       READE     GSSOLL01                               33
     C                   DOW       *IN33 = *OFF
     c     OLBOX         IFGT      900
     c     OLBOX         ANDLT     999
     c                   sub       900           OLBOX
     C                   move      'U'           BOXTYP
     c                   end
     C     OLBOX         IFEQ      *ZEROS
     C                   SETON                                        30
     C                   END
     C     OLBOX         IFNE      GMBOX
     C                   ADD       1             NR
     C                   MOVE      OLBOX         BX(NR)
     C                   END
     C                   MOVE      OLBOX         GMBOX
     C     curturn       READE     GSSOLL01                               33
     C  N33              ENDDO
      * NOEN UTEN BOX-NR PÅ.....
     C     *IN30         IFEQ      *ON
     C                   eval      FEILMELDING='Some clls NOT assigned to UNIT!'
     c                   eval      Fmeldnr='01'
     C                   goto      UtFinnBox
     C                   END
      * TA VARE PÅ ANTALL - LEGG PÅ "SLUTT-FLAGG"
     C                   MOVE      NR            TOTBOX
     C                   ADD       1             NR
     C                   Z-ADD     -1            BX(NR)
      *
      * SLETT GAMMEL (VED KJØRING TYPE P) INFO OM BOX-NR, TA VARE PÅ I ARRAY
     C                   MOVE      *ZEROS        ANTFTX            3 0
     C                   CLEAR                   BXL
     C                   CLEAR                   BXB
     C                   CLEAR                   BXH
     C                   CLEAR                   BX3
     C                   CLEAR                   BXM
     C                   CLEAR                   BXC
     C                   MOVE      TOTBOX        TOTBOXA           3
     C     CURTURN       SETLL     GSSBXF
     C     CURTURN       READE     GSSBXF                                 33
     C     *IN33         DOWEQ     '0'
     C                   ADD       1             ANTFTX
     C     BXBOX         IFLE      TOTBOXN
     C                   MOVE      BXBOX         NR
     C                   Z-ADD     BXVKT         BXV(NR)
     C                   Z-ADD     BXLEN         BXL(NR)
     C                   Z-ADD     BXBRE         BXB(NR)
     C                   Z-ADD     BXHØY         BXH(NR)
     C                   Z-ADD     BXM3          BX3(NR)
     C                   IF        BXCN = *BLANKS
     C                   MOVEL     BXID          BXC(NR)
     C                   ELSE
     C                   MOVEL     BXCN          BXC(NR)
     C                   END
     C                   Z-ADD     BXLM          BXM(NR)
     C                   UPDATE    GSSBXFR
     C                   ELSE
      * Bokser i fritekst med høyere nr en TOT slettes
     C                   DELETE    GSSBXFR
     C                   END
     C     CURTURN       READE     GSSBXF                                 33
     C                   ENDDO
      * ANTALL BOX-LINJER I FTX OK??  VED FINAL
     C     LTYP          IFNE      'P'
     C     ANTFTX        ANDNE     TOTBOX
     C                   eval      FEILMELDING='Nbr of UNITs not correct'+
     c                             ' Run Packinglist first'
     c                   eval      Fmeldnr='02'
     c                   move      'P'           Ltyp
     C                   goto      UtFinnBox
     C                   END
     C     UtFinnBox     ENDSR
      *
      *=====================================================================******
      *  SAVEBOX    OPPDATER BOKS-VERDIER I OPPDRAGETS NOTIS-BOKK.
      *=====================================================================******
     C     SAVEBOX       begsr
      * fjern gamle box-linjer, Finn høyeste brukte nr (blant ikke slettede)
     C     CURTURN       SETLL     GSSBXF
     C     CURTURN       READE     GSSBXF                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    GSSBXFR
     C     CURTURN       READE     GSSBXF                                 33
     C                   ENDDO
      *
     C                   MOVE      ' '           BXST
     C                   MOVE      CURTURN       BXTNO
     C                   MOVE      *BLANKS       BXCN
     C                   MOVE      *BLANKS       BXID
     C                   MOVE      ' '           BXGSL
     C     1             DO        999           NR
     C     BX(NR)        CABLT     0             UTSAVEBOX
     C                   MOVE      BX(NR)        BXBOX
     C                   MOVE      TOTBOX        BXTOT
     C                   Z-ADD     BXV(NR)       BXVKT
     C                   Z-ADD     BXL(NR)       BXLEN
     C                   Z-ADD     BXB(NR)       BXBRE
     C                   Z-ADD     BXH(NR)       BXHØY
     C                   Z-ADD     BX3(NR)       BXM3
     C                   MOVE      BXC(NR)       BXID
     C                   Z-ADD     BXM(NR)       BXLM
     C                   WRITE     GSSBXFR
     C                   ENDDO
     C     UTSaveBOX     ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      *
     C                   OPEN      GSSBXF
     C                   open      GSSOLL01                             50
     C                   open      GSSTRL01
      * arbeider med turnr som ikke lenger har status ' '/'1'(når ikke KOPI)
      * Sett flagg slik at en kommer inn i feilmeld-rutine og refresher
     c                   move      'N'           FeilStatus        1
     c     Dialog        ifne      'K'
     c     curturn       chain     gsstrl01                           33
     C     *in33         Ifeq      *on
     c                   move      'Y'           FeilStatus
     c                   move      'F'           Dialog
     c                   move      'P'           Ltyp
     C                   end
     C                   end
     C                   endsr
