********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 må ha ep = J ved opsjon 8
10XXX * 02 YBC PTF10 FEILRETTING PÅ NY REGISTRERING AV KODE
10XXX * 03 JOV PTF10 vedlikeholde label  J/blanlk
10XXX * 04 SH  PTF10 MERGE DOKUMENTER
""""""*V11
11XXX * 05 SH  PTF11 kan bli plukkeprogram
11XXX * 06 SH  PTF11 legg inn fast vedlegg
11XXX * 09 SH  PTF11 XML-mottagere
11XXX * 10 SH  PTF11 vise uploadbane
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 11 JOV PTF11 nye felt for Skann-regler   - F10= se kun scann / upload
      * 12 JOV PTF11 nye felt for Skann Avvikende innmappe/Type,flyttet noen felt,SkanUp->SkanUpl
      * 13 SH  PTF12 Oppheve ptf 02
      * 14 SH  PTF12 SJEKKE OM VEDLEGGSDOKUMENT FINNES
      * 15 JOV PTF12 DDS - feil i visning etter utvidet 132x27 - BUNN begynner nå i 26 / blank 2
12XXX * 16 YBC PTF12 Opsjon 9 er samme som opsjon 8. Arkivtype fa får denne opsjonen.
********************************************************************
      * VEDLIKEHOLD arkivdokumenter
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29    32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FKOFAST    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKTXT    UF A E           K DISK
     FARKEXT    IF   E           K DISK
N12  FFIRMARCSI IF   E           K DISK
     FARC007FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *  GYLDIGE BANER
     D KF              S             50    DIM(20)
     D                 DS
     D  ARTXT                  1     15
     D  KOSFN7                 1      7
N10  D                 DS
N10  D  ARSBAN                 1     50
N10  D  ARSB30                 1     30
     D                 DS
     D  V                      1     60
     D                                     DIM(30)
     D  arkved                 1     60
     D  V01                    1      2
     D  V02                    3      4
     D  V03                    5      6
     D  V04                    7      8
     D  V05                    9     10
     D  V06                   11     12
     D  V07                   13     14
     D  V08                   15     16
     D  V09                   17     18
     D  V10                   19     20
     D  V11                   21     22
     D  V12                   23     24
     D  V13                   25     26
     D  V14                   27     28
     D  V15                   29     30
     D  V16                   31     32
     D  V17                   33     34
     D  V18                   35     36
     D  V19                   37     38
     D  V20                   39     40
     D  V21                   41     42
     D  V22                   43     44
     D  V23                   45     46
     D  V24                   47     48
     D  V25                   49     50
     D  V26                   51     52
     D  V27                   53     54
     D  V28                   55     56
     D  V29                   57     58
     D  V30                   59     60
     D                 DS
     D  w                      1     60
     D                                     DIM(30)
     D  WSKVED                 1     60
     D  W01                    1      2
     D  W02                    3      4
     D  W03                    5      6
     D  W04                    7      8
     D  W05                    9     10
     D  W06                   11     12
     D  W07                   13     14
     D  W08                   15     16
     D  W09                   17     18
     D  W10                   19     20
     D  W11                   21     22
     D  W12                   23     24
     D  W13                   25     26
     D  W14                   27     28
     D  W15                   29     30
     D  W16                   31     32
     D  W17                   33     34
     D  W18                   35     36
     D  W19                   37     38
     D  W20                   39     40
     D  W21                   41     42
     D  W22                   43     44
     D  W23                   45     46
     D  W24                   47     48
     D  W25                   49     50
     D  W26                   51     52
     D  W27                   53     54
     D  W28                   55     56
     D  W29                   57     58
     D  W30                   59     60
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       YSTYPE            2
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF4        SETLL     ARKTXT
     C     START1        TAG
     C                   READ(N)   ARKTXT                                 94
N11  c  N94              if        KunSkanUpl = 'J'
N12  c                   move      ARTYPE        TstTyp            2
N11  c                   exsr      TstSkanUpl
N11  c                   if        SkanUpl<> 'J'
N11  c                   goto      Start1
N11  c                   endif
N11  c                   endif
      *
     C     START2        TAG
      *
     C  N94              DO
S11  C*                  DO        12
N11  C                   DO        20
     C                   ADD       1             ZRECNO
     C                   move      *blanks       arcane
     C     ARKLAGK       CHAIN     ARKEXT                             33
     C                   WRITE     SUBFIL
N11  c     NextRec       tag
     C                   READ(N)   ARKTXT                                 94
N11  c  N94              if        KunSkanUpl= 'J'
N12  c                   move      ARTYPE        TstTyp
N11  c                   exsr      TstSkanUpl
N11  c                   if        SkanUpl<> 'J'
N11  c                   goto      NextRec
N11  c                   endif
N11  c                   endif
N05  c  N94uvedl         ifeq      'P'
n05  c                   movel     artype        typ1              1
n05  c     typ1          comp      'z'                                94
n05  c                   end
     C  N94              MOVE      ARTYPE        YSTYPE
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
N11  C*** se kun skann/upload-koder
N11  C     *IN10         ifeq      '1'
N11  c                   if        KunSkanUpl= 'J'
N11  c                   move      'N'           KunSkanUpl        1
N11  C                   else
N11  c                   move      'J'           KunSkanUpl
     C   41              MOVE      'JOV1747'     MSGNR
     C   41              seton                                        30
N11  C                   endif
N11  C                   GOTO      Start
N11  C                   endif
     C*
     C*** ny
     C   06              GOTO      STARTNY
     C*
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     ARKTXT
     C  N94              READ(N)   ARKTXT                                 94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*** NY
     C     STARTNY       TAG
     C                   movel     'Z '          wstype
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     5             POSNBR
     c                   exsr      movb2
     C     STARTN2       TAG
     c                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   04              do
     C                   MOVE      *BLANKS       KFTYP
     C                   MOVEL     'ARKIVU '     KFTYP
     C                   MOVE      *BLANKS       KFKOD
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP            10
     C                   PARM                    UUPDAT            1
     C                   PARM                    KFKOD             5
     C     KOFAKE2       CHAIN     KOFAST                             33
E30  c  N33              movel     kftxt         WSSBAN
     c                   goto      startn2
     c                   enddo
     C   12              DO
     C                   GOTO      START
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      STARTN2
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
N14  c                   setoff                                       85
     C                   SETOFF                                       8790
N12  C                   SETOFF                                       868889
     C  N40              DO
     C*
     C     WSTYPE        COMP      *BLANKS                                90
s13  C*                  IF        *IN90 = *OFF
s13  C*    WSTYPE        CHAIN(N)  ARKTXT                             33
s13  C* N33              EVAL      *IN90 = *ON
s13  C*                  END
      *
      * FEIL  ved blank i typerubrikk
      *
     C   90
     CANN30              MOVE      'SHU0881'     MSGNR
     C   90
     CANN30              Z-ADD     4             POSNBR
     C   90              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C                   END
     C*
      * FEIL  kode ved ny
      *
     C                   movel     wstype        wstyp1            1
     C                   move      wstype        wstyp2            1
     c     *IN40         IFEQ      '0'
     C     WSTYP1        COMP      *BLANKS                                90
     C  N90WSTYP2        COMP      *BLANKS                                90
S11  C* N90WSTYP1        COMP      'Z'                                9090
N11  C  N90              IF        WSTYP1<>'Z' and WSTYP1 <> '*'
N11  c                   SETON                                        90
N11  c                   ENDIF
     C   90
S11  C*NN30              MOVE      'SHU1214'     MSGNR
N11  CANN30              MOVE      'JOV1717'     MSGNR
     C   90
     CANN30              Z-ADD     4             POSNBR
     C   90
     CANN30              Z-ADD     20            LINNBR
     C   90              SETON                                        30
     C*
     C   30              GOTO      SLTB
     c                   end
     C*
      * FEIL  ved blank i tekstrubrikk
      *
     C     WSTXT         COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SHU0882'     MSGNR
     C   89
     CANN30              Z-ADD     7             POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
      * tekst kan kun endres på "Z..koder"
     c     *IN40         IFEQ      '1'
     c     *IN30         andeq     '0'
     c     WSTYP1        andne     'Z'
     c     WSTXT         andne     ARTXT
     C                   MOVE      'JOV1487'     MSGNR
     C                   Z-ADD     7             POSNBR
N12  C                   Z-ADD     20            LINNBR
     C                   SETON                                        3089
     C   30              GOTO      SLTB
     c                   end
     C*
      * FEIL  ved hvis eksternlager mangler
      *
     c                   setoff                                       88
     C     WSKLAG        ifne      *blanks
     C     wskLAGK       CHAIN     ARKEXT                             88
     C   88              MOVE      'SHU1013'     MSGNR
     C   88              seton                                        30
     C   30              Z-ADD     33            POSNBR
     C   30              GOTO      SLTB
     c                   end
      *
      * test eventuelt vedleggsdokument
      *    indikator 85
      *
N14  c     wsvedl        ifne      *blanks
N14  c                   call      'ARC007V'
N14  c                   parm                    wsvedl
N14  c                   parm                    fins              1
N14  c     fins          ifeq      'N'
N14  C                   MOVE      'SHU1736'     MSGNR
N14  C                   seton                                        3085
N14  C                   Z-ADD     22            POSNBR
N14  C                   Z-ADD     22            LINNBR
N14  C                   GOTO      SLTB
N14  C                   END
N14  C                   END
      *
      * Upload mappe 87
      *
N11  c     WSSBAN        IFEQ      *BLANKS
s12  c*                  move      WSTYPE        ARTYPE
N12  c                   move      WSTYPE        TstTyp
N11  c                   exsr      TstSkanUpl
N11  c                   IF        SkanUpl= 'J'
N11  C                   MOVE      'JOV1718'     MSGNR
N11  C                   seton                                        30
S12  C*                  Z-ADD     25            POSNBR
N12  C                   Z-ADD     22            POSNBR
N11  C                   Z-ADD     23            LINNBR
N11  C                   GOTO      SLTB
N11  C                   END
N11  C                   END

E11  c     WSSBAN        IFNE      *BLANKS
E11  C     WSSBAN        LOOKUP    KF                                     33
     C  N33              SETON                                        8730
     C   87              MOVE      'SHU1213'     MSGNR
     C   87              seton                                        30
S12  C*  30              Z-ADD     25            POSNBR
N12  C   30              Z-ADD     22            POSNBR
S11  C*  30              Z-ADD     22            LINNBR
N11  C   30              Z-ADD     23            LINNBR
     C   30              GOTO      SLTB
     C                   END
      *
N12   * krav om avvikende mappe/type ved visse koder
N12  c                   IF        *IN41=*ON AND WSTYPE>='*G' and WSTYPE<='*Z'
N12   *
N12  c                   IF        WSSAIN=*blanks or WSAVTY=*blanks
N12  C                   MOVE      'JOV1745'     MSGNR
N12  c                   IF        WSSAIN=*blanks
N12  C                   Z-ADD     63            POSNBR
N12  C                   Z-ADD     24            LINNBR
N12  c                   ELSE
N12  C                   Z-ADD     111           POSNBR
N12  C                   Z-ADD     25            LINNBR
N12  c                   ENDIF
N12  C                   SETON                                        3086
N12  C                   GOTO      SLTB
N12  c                   ENDIF
N12   * (Tekst og) avviknde type ER oppgitt , men er den gyldig ( og av type SkanUpload..)
N12  C     WSAVTY        CHAIN(N)  ARKTXT                             86
N12  C     *in86         ifeq      *OFF
N12  c                   move      WSAVTY        TstTyp
N12  c                   exsr      TstSkanUpl
N12  c                   if        SkanUpl<> 'J'
N12  C                   eval      *in86 = *ON
N12  c                   endif
N12  c                   endif
N12  C     *in86         ifeq      *ON
N12  C                   eval      *in30 = *ON
N12  C                   MOVE      'JOV1746'     MSGNR
N12  C                   Z-ADD     111           POSNBR
N12  C                   Z-ADD     25            LINNBR
N12  C                   GOTO      SLTB
N12  c                   endif
N12   *
N12  c                   ENDIF
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSFK        CHAIN     ARKTXT                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    ARKTXTR
     C   33              WRITE     ARKTXTR
     C                   move      *blanks       arcane
     C     WSKLAG        ifne      *blanks
     C     ARKLAGK       CHAIN     ARKEXT                             88
     c                   end
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
N05  c     utype         ifne      'P'
      *
      * 2=ENDRING
      *
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '4'                                          SLETTE
     c                   movel     artype        artyp1            1
     c                   move      *blanks       wsnr
     c     artyp1        ifeq      'Z'
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
      * 8=VEDLIKEHOLD EPOSTMOTTAGERE TIL DOKUMENT
      *
     C     WSNR          IFEQ      '8'                                          ENDRE
N01  c     arksnd        ifne      'J'
N01  C                   MOVE      'SHU1239'     MSGNR
N01  C                   SETON                                        30
N01  C                   GOTO      SLHENT
N01  c                   end
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      EPOMOT
     C   12              GOTO      SLHENT
     C                   END
n09   *
n09   * 9=VEDLIKEHOLD XML mottagere
n09   *
n09  C     WSNR          IFEQ      '9'                                          ENDRE
n09  c     artype        ifne      'X1'
n09  c     artype        andne     'X2'
N16  c     artype        andne     'fa'
n09  C                   MOVE      'SHU1611'     MSGNR
n09  C                   SETON                                        30
n09  C                   GOTO      SLHENT
n09  c                   end
n09  C                   MOVE      ' '           WSNR
n09  C     KOSFN7        COMP      '*DELETE'                              98
n09  C   98              GOTO      SLHENT
n09  C                   EXSR      EPOMOT
n09  C   12              GOTO      SLHENT
n09  C                   END
n09  C                   end
      *
N05  c     utype         ifne      'P'
N05  C     WSNR          andeq     '1'                                          ENDRE
N05  c                   move      artype        utype
N05  c                   move      artxt         utxt
N05  c                   seton                                        lr
N05  c                   return
N05  c                   end
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
N01  C     *in98         ifeq      '1'
N01  C                   MOVE      'SHU1239'     MSGNR
N01  C                   SETON                                        30
N01  C                   end
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   04              do
     C                   MOVE      *BLANKS       KFTYP
     C                   MOVEL     'ARKIVU '     KFTYP
     C                   MOVE      *BLANKS       KFKOD
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP            10
     C                   PARM                    UUPDAT            1
     C                   PARM                    KFKOD             5
     C     KOFAKE2       CHAIN     KOFAST                             33
E11  c  N33              movel     kftxt         WSSBAN
     c                   goto      STCHG
     c                   enddo
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C*
      ***********************************************
     C     DLTSF         BEGSR
      ***********************************************
     C                   MOVE      ARTYPE        WSTYPE
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSFK        CHAIN     ARKTXT                             33
     C  N33              DELETE    ARKTXTR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     EPOMOT        BEGSR
     C***********************************************
     C                   MOVE      ARTXT         U16              16
     C                   MOVEL     '*'           U16
     C                   MOVEL     U16           UDOC             30
     C                   CALL      'VCUNDC3R'
     C                   PARM                    FIFIRM
     C                   PARM                    UDOC
     C                   PARM                    USOK80           10 0
     C                   PARM                    EPOFAX            1
     C*
     C                   UPDATE    SUBFIL
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       ARTYPE
     C                   MOVEL     '*DELETE'     KOSFN7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *BLANKS       WSKJN
     C                   MOVE      *BLANKS       WSKSND
     C                   MOVE      *BLANKS       WSKLAG
     C                   MOVE      *zeros        WSKdag
     C                   MOVE      *BLANKS       WSKVED
E11  C                   MOVE      *BLANKS       WSSBAN
E11  C                   MOVE      *BLANKS       WSSLAB
N04  C                   MOVE      *BLANKS       WSMRG
N06  C                   MOVE      *BLANKS       WSVEDL
N11  C                   MOVE      *BLANKS       WSSFSK
N11  C                   MOVE      *BLANKS       WSSCTS
N11  C                   MOVE      *BLANKS       WSSRLE
N11  C                   MOVE      *BLANKS       WSSRST
N11  C                   MOVE      *BLANKS       WSSRPA
N11  C                   MOVE      *BLANKS       WSSRNO
N12  C                   MOVE      *BLANKS       WSSAIN
N12  C                   MOVE      *BLANKS       WSAVTY
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      ARTYPE        WSTYPE
     C                   MOVE      ARTXT         WSTXT
     C                   MOVE      ARKJN         WSKJN
     C                   MOVE      ARKSND        WSKSND
     C                   MOVE      ARKLAG        WSKLAG
     C                   MOVE      ARKdag        WSKdag
     C                   MOVE      ARKVED        WSKVED
E11  C                   MOVE      ARSBAN        WSSBAN
E11  C                   MOVE      ARSLAB        WSSLAB
N04  C                   MOVE      ARMRG         WSMRG
N06  C                   MOVE      ARVEDL        WSVEDL
N11  C                   MOVE      ARSFSK        WSSFSK
N11  C                   MOVE      ARSCTS        WSSCTS
N11  C                   MOVE      ARSRLE        WSSRLE
N11  C                   MOVE      ARSRST        WSSRST
N11  C                   MOVE      ARSRPA        WSSRPA
N11  C                   MOVE      ARSRNO        WSSRNO
N12  C                   MOVE      ARSAIN        WSSAIN
N12  C                   MOVE      ARAVTY        WSAVTY
     C                   SETON                                        40
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     23            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C                   MOVE      WSTYPE        ARTYPE
     C                   MOVE      WSTXT         ARTXT
     C                   MOVE      WSKJN         ARKJN
     C                   MOVE      WSKSND        ARKSND
     C                   MOVE      WSKLAG        ARKLAG
     C                   MOVE      WSKdag        ARKdag
     C                   MOVE      WSKVED        ARKVED
E11  C                   MOVE      WSSBAN        ARSBAN
E11  C                   MOVE      WSSLAB        ARSLAB
N04  C                   MOVE      WSMRG         ARMRG
N06  C                   MOVE      WSVEDL        ARVEDL
N11  C                   MOVE      WSSFSK        ARSFSK
N11  C                   MOVE      WSSCTS        ARSCTS
N11  C                   MOVE      WSSRLE        ARSRLE
N11  C                   MOVE      WSSRST        ARSRST
N11  C                   MOVE      WSSRPA        ARSRPA
N11  C                   MOVE      WSSRNO        ARSRNO
N12  C                   MOVE      WSSAIN        ARSAIN
N12  C                   MOVE      WSAVTY        ARAVTY
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
N05  c     *entry        plist
N05  c                   parm                    uvedl             1
N05  c                   parm                    utype             2
N05  c                   parm                    uTXT             15
N05  c     uvedl         comp      'P'                                    31
     C     KOFAKEY       KLIST
     C                   KFLD                    KFTYP
     C                   MOVEL     'ARKIVU'      KFTYP
     C     KOFAKE2       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C     KODSFK        KLIST
     C                   KFLD                    WSTYPE
     C     arklagk       KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    arklag
     C     wsklagk       KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    wsklag
     C     KODSF3        KLIST
     C                   KFLD                    YSTYPE
     C     KODSF4        KLIST
     C                   KFLD                    YSTYPE
     C*
     C                   READ      FIRM                                   33
      *
     C     KOFAKEY       SETLL     KOFAST
     C     *IN33         DOWEQ     '0'
     C     KOFAKEY       READE     KOFAST                                 33
     C     *IN33         IFEQ      '0'
     C     KFKOD         ANDNE     ' DEFN'
     C                   ADD       1             NR                2 0
     C                   MOVE      KFTXT         KF(NR)
     C                   END
     C                   ENDDO
N12  c                   move      *blanks       ASIANE
N12  c     'KJ'          chain     firmarcsi                          33
N12  c     ASIANE        comp      'J'                                    41
     C*
     C                   ENDSR
N11  C***********************************************
N11  C     TstSkanUpl    BEGSR
N11  C***********************************************
N12   * Er aktuell type en scan/upload-type
N11  C                   Move      'N'           SkanUpl           1
N12  c*                  IF        %subst(ARTYPE:1:1)='Z' or ARTYPE='*F'
N12  c*                            or (ARTYPE>='*0' and ARTYPE<='*9')
N12  c                   IF        %subst(TstTyp:1:1)='Z' or TstTyp='*F'
N12  c                             or (TstTyp>='*0' and TstTyp<='*9')
N12  c                             or (TstTyp>='*A' and TstTyp<='*Z')
N11  C                   Move      'J'           SkanUpl
N11  C                   END
N11  C*
N11  C                   ENDSR
