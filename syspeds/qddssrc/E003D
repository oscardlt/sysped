     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LOGF)
     A                                      PRINT
     A                                      ALTHELP
     A                                      ALTPAGEUP
     A                                      ALTPAGEDWN
     A                                      HELP
      *
     A          R E003B10
     A                                      CF03(03 'Exit')
     A                                      CF04(04 'Søk')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A            MSGNR          7A  P
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(041 EDI0358 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E003D'
     A                                      COLOR(BLU)
     A                                  2 31MSGCON(016 EDI0359 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(034 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(027 EDI0360 *LIBL/SYMSGF)
     A            WWIIDC        35A  B  7 32CHECK(LC)
     A                                  7 69MSGCON(012 EDI0373 *LIBL/SYMSGF)
     A                                  9  2MSGCON(027 EDI0362 *LIBL/SYMSGF)
     A            WWIIDI        35A  B  9 32CHECK(LC)
     A                                  9 69MSGCON(012 EDI0373 *LIBL/SYMSGF)
     A                                 11  2MSGCON(027 EDI0363 *LIBL/SYMSGF)
     A            WW0065         6A  B 11 32
     A                                 11 69MSGCON(012 EDI0373 *LIBL/SYMSGF)
     A                                 12  4MSGCON(025 EDI0364 *LIBL/SYMSGF)
     A            WW0054         3A  B 12 32
     A                                 12 69MSGCON(012 EDI0373 *LIBL/SYMSGF)
     A                                 14  2MSGCON(027 EDI0366 *LIBL/SYMSGF)
     A            WWSR           1A  B 14 32
     A                                 14 69'S/R, blank'
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(027 JOV1594 *LIBL/SYMSGF)
     A            WWCNO          8Y 0B  8 32EDTCDE(Z)
     A            KNAVN         30   O  8 41COLOR(WHT)
      *
     A          R E003B20
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Cancel')
     A N44                                  CF04(04 'Søk')
     A  44                                  CF11(11 'Slette')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A            MSGNR          7A  P
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(041 EDI0358 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 75'E003D'
     A                                      COLOR(BLU)
     A  42                              2 36MSGCON(006 EDI0620 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  43                              2 36MSGCON(006 EDI0621 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  44                              2 36MSGCON(006 EDI0622 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  45                              2 36MSGCON(007 EDI0623 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  46                              2 36MSGCON(007 EDI0624 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(027 EDI0360 *LIBL/SYMSGF)
     A            ÅCIIDC    R        B  5 32REFFLD(ECMSGR/ECIIDC *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                  5 70'F4'
     A                                  7  2MSGCON(027 EDI0362 *LIBL/SYMSGF)
     A            ÅCIIDI    R        B  7 32REFFLD(ECMSGR/ECIIDI *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                  7 70'F4'
     A                                  9  2MSGCON(027 EDI0363 *LIBL/SYMSGF)
     A            ÅC0065    R        B  9 32REFFLD(ECMSGR/EC0065 *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                 10  4MSGCON(025 EDI0364 *LIBL/SYMSGF)
     A            ÅC0054    R        B 10 32REFFLD(ECMSGR/EC0054 *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A                                 10 42MSGCON(010 YBC0009 *LIBL/SYMSGF)
     A            ÅC0001    R        B 10 53REFFLD(ECMSGR/EC0001 *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                 10 62MSGCON(012 YBC0010 *LIBL/SYMSGF)
     A            ÅC0002    R        B 10 75REFFLD(ECMSGR/EC0002 *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  41                                  DSPATR(RI)
     A                                 12  2MSGCON(027 EDI0366 *LIBL/SYMSGF)
     A            ÅCSR      R        B 12 32REFFLD(ECMSGR/ECSR *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                 12 43'(S,R)'
     A                                 14  2MSGCON(027 EDI0365 *LIBL/SYMSGF)
     A            ÅCCNO     R        B 14 32REFFLD(ECMSGR/ECCNO *LIBL/ECMSG)
     A                                      EDTCDE(4)
     A  88                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A            KNAVN         30A  O 14 42COLOR(WHT)
     A                                 16  2MSGCON(027 EDI0376 *LIBL/SYMSGF)
     A            ÅCSEND    R        B 16 32REFFLD(ECMSGR/ECSEND *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A                                 16 43MSGCON(018 EDI0377 *LIBL/SYMSGF)
     A                                 18  2MSGCON(076 EDI0382 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 19  2MSGCON(027 EDI0378 *LIBL/SYMSGF)
     A            ÅCPGM     R        B 19 32REFFLD(ECMSGR/ECPGM *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                 20  5MSGCON(024 EDI0379 *LIBL/SYMSGF)
     A            ÅCDTAQ    R        B 20 32REFFLD(ECMSGR/ECDTAQ *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                 21  2MSGCON(027 EDI0381 *LIBL/SYMSGF)
     A            ÅCLIB     R        B 21 32REFFLD(ECMSGR/ECLIB *LIBL/ECMSG)
     A  88                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N44                             23 15MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  44                             23 32MSGCON(020 EDI0811 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 55MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E003B30
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Subset')
     A                                      CF06(06 'Lage ny')
     A                                      CF09(09)
     A                                      CF05(05 'Frisk opp')
     A            MSGNR          7A  P
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(041 EDI0358 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E003D'
     A                                      COLOR(BLU)
     A                                  2 36MSGCON(008 EDI0625 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  5MSGCON(008 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 16MSGCON(008 EDI0703 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 27MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 38MSGCON(009 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(072 JOV1591 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSONA20       20A  I  7  6CHECK(LC)
     A                                 10 17MSGCON(038 EDI0384 *LIBL/SYMSGF)
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 28MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50                             23 41MSGCON(018 JOV1592 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50                             23 41MSGCON(018 JOV1593 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 69MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E003B40
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Subset')
     A                                      SLNO(23)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 28MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50                              1 41MSGCON(018 JOV1592 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50                              1 41MSGCON(018 JOV1593 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 69MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E003H50                   SFL
     A                                      SFLNXTCHG
     A            ECFIR          2A  H
     A*           ECCNO          8S 0H
     A            ECIIDC        35A  H
     A            ECIIDI        35A  H
     A            ECPGM         10A  H
     A            ECDTAQ        10A  H
     A            ECLIB         10A  H
     A            ECSEND         1A  H
     A            EC0001         4A  H
     A            EC0002         1A  H
     A            RECNUM        10S 0H
     A            WSNR           1A  I  8  3VALUES(' ' '2' '3' '4' '5')
     A            SFNAVN        20A  O  8  6
     A            ECCNO          8Y 0O  8 28EDTCDE(Z)
     A            ECSR      R        O  8 38REFFLD(ECMSGR/ECSR *LIBL/ECMSG)
     A            EC0065    R        O  8 41REFFLD(ECMSGR/EC0065 *LIBL/ECMSG)
     A            EC0054    R        O  8 48REFFLD(ECMSGR/EC0054 *LIBL/ECMSG)
     A            WWWIDC        28A  O  8 52
      *
     A          R E003K60                   SFLCTL(E003H50)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Subset')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Ny post')
     A                                      CF09(09)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(041 EDI0358 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E003D'
     A                                      COLOR(BLU)
     A                                  2 36MSGCON(008 EDI0625 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            CURS1          4S 0O  2 76SFLRCDNBR(CURSOR)
     A                                      DSPATR(ND)
     A                                  3  3MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  5MSGCON(008 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 16MSGCON(008 EDI0703 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 27MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 38MSGCON(009 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(072 JOV1591 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSONA20       20A  I  7  6CHECK(LC)
