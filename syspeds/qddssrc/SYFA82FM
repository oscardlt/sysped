********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
S01  A*                                     HLPDOC('SYFA8201' 'SYFA82 ' 'SY400-
S01  A*                                     LO/MAINT')
N01  A                                      HLPPNLGRP(HLPSCR SYFA82PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD POSTNR/AVDELI-
N01  A                                      NG')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            SFPNRF    R    4Y00O  6  8REFFLD(KLKMIN)
     A                                      EDTCDE(4)
     A            SFPNRT    R    4Y00O  6 16REFFLD(KLKMIN)
     A                                      EDTCDE(4)
     A            SFAVD     R    4Y00O  6 24REFFLD(KLKMIN)
     A                                      EDTCDE(4)
     A            SFDEL         10   O  6 33
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA82'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(040 TRO0149 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  3 33MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(079 TRO0150 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(079 TRO0151 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A          R BUNN
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  1  2MSGCON(079 TRO0150 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(079 TRO0151 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            SFPNRF         4Y 0B  3  8DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A            SFPNRT         4Y 0B  3 16DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A            SFAVD          4Y 0B  3 24DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            W2PNRF         4S 0B  2 16DSPATR(PC)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  1  2MSGCON(079 TRO0150 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(079 TRO0151 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            SFPNRF         4Y 0O  3  8DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            SFPNRT         4Y 0O  3 16DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            SFAVD          4Y 0O  3 24DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA82'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(040 TRO0149 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 33MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(079 TRO0150 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(079 TRO0151 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
