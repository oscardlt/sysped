     FSI02D     CF   E             WORKSTN
     F                                     SFILE(SI02DA:ZRECNO)
     FSIF       UF A E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    IUSR          WUSR
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WUSR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SI02DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C                   MOVE      *BLANKS       WUSR
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     SIF                                9494
     C                   READ      SIF                                    94
      *
     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO
      *
     C  N94              DO        13
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     SI02DA
     C                   READ      SIF                                    94
     C  N94              END
      *__________________________________________________
      * Legg ut dummy subfilerecord hvis subfilen er tomm
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     C     WRECNO        IFEQ      0
     C                   MOVE      *BLANKS       IUSR
     C                   MOVE      *BLANKS       INA
     C                   MOVE      *ZEROS        IKN
     C                   MOVE      *ZEROS        IAVD
     C                   MOVE      *BLANKS       ISG
     C                   MOVE      *BLANKS       ISK
     C                   MOVE      *BLANKS       IEUR
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     SI02DA
     C                   END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI02DØ
     C                   WRITE     SI02DC
     C                   EXFMT     SI02DB
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SI02DA                                 52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
      *
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      *____________
      * Blanke felt
      *""""""""""""
     C                   MOVE      '0'           *IN41
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      *BLANKS       IUSR
     C                   MOVE      *BLANKS       INA
     C                   MOVE      *ZEROS        IKN
     C                   MOVE      *ZEROS        IAVD
     C                   MOVE      *BLANKS       ISG
     C                   MOVE      *BLANKS       ISK
     C                   MOVE      *BLANKS       IEUR
      *
     C     SRBA1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI02DØ
     C                   EXFMT     SI02DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
      * Post må ikke finnes fra før.
     C                   MOVE      ZUSR          WUSR
     C     KEYA          CHAIN     SIF                                53
     C     *IN53         IFEQ      '0'
     C                   MOVE      'SY00100'     ZMSGNO
     C                   MOVEA     '1110'        *IN(30)
     C                   GOTO      SRBA1
     C                   END
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   MOVE      ZUSR          IUSR
     C                   MOVE      ZNA           INA
     C                   MOVE      ZKN           IKN
     C                   MOVE      ZAVD          IAVD
     C                   MOVE      ZSG           ISG
     C                   MOVE      ZSK           ISK
     C                   MOVE      ZEUR          IEUR
     C                   WRITE     SIFR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   WRITE     SI02DA
      *
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Flytte felt
      *""""""""""""
     C                   MOVE      '1'           *IN41
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      IUSR          ZUSR
     C                   MOVE      INA           ZNA
     C                   MOVE      IKN           ZKN
     C                   MOVE      IAVD          ZAVD
     C                   MOVE      ISG           ZSG
     C                   MOVE      ISK           ZSK
     C                   MOVE      IEUR          ZEUR
      *
     C     SRBB1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI02DØ
     C                   EXFMT     SI02DD
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
     C   30              GOTO      SRBB1
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   MOVE      ZUSR          WUSR
     C     KEYA          CHAIN     SIF                                53
     C                   MOVE      ZNA           INA
     C                   MOVE      ZKN           IKN
     C                   MOVE      ZAVD          IAVD
     C                   MOVE      ZSG           ISG
     C                   MOVE      ZSK           ISK
     C                   MOVE      ZEUR          IEUR
     C                   UPDATE    SIFR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SI02DA
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   MOVE      IUSR          WUSR
     C     KEYA          CHAIN     SIF                                53
     C                   DELETE    SIFR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       IUSR
     C                   MOVE      *BLANKS       INA
     C                   MOVE      *ZEROS        IKN
     C                   MOVE      *ZEROS        IAVD
     C                   MOVE      *BLANKS       ISG
     C                   MOVE      *BLANKS       ISK
     C                   MOVE      *BLANKS       IEUR
     C                   UPDATE    SI02DA
     C                   MOVE      '1'           *IN95
      *
     C                   ENDSR
