********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BARKODE
XXXXXX*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11128 * 02 YBC PTF11 VEKT ER ENDRET FRA 9.0 TIL 11.3
********************************************************************
      *_________________________________
      * VARELISTE A    OBS! SAMME SOM B
      *"""""""""""""""""""""""""""""""""
     A          R TR014V1
     A N96N50     PVFM1         75      1  2
     A  96N50     PVFM12        75      1  2
     A N96N50     PVFM2         75      2  2
     A  96N50     PVFM22        75      2  2
     A                                  3  4'_______________________________'
     A                                  3  4'_______________________________'
     A                                  3 84'MRN'
     A                                      HIGHLIGHT
     A                                  3 88'(Transitteringsnummer)'
     A                                  4  4'|VARELISTE                    |'
     A                                  4  4'|VARELISTE                    |'
     A                                      UNDERLINE
     A                                  4 37'Avgangstollsted:'
     A            THCATS         8      4 55
S01  A* 50        XHTRNR        18      4 82BARCODE(CODE128 2 *HRI *HRZ +
S01  A*                                     (*WIDTH 0.010) (*RATIO 3) X'02')
N01  A  50        XHTRNR        18      4 82BARCODE(CODE128 2 *HRI *HRZ +
N01  A                                      (*WIDTH 0.008) (*RATIO 3) X'02')
     A N96                              5  4'|Eksemplar   A    |     |     |'
     A N96                              5  4'|Eksemplar   A    |     |     |'
     A  96                              5  4'|Eksemplar   B    |     |     |'
     A  96                              5  4'|Eksemplar   B    |     |     |'
     A                                  5  4'_______________________________'
     A                                  5  4'_______________________________'
     A            XSIDEN         3  0   5 25EDTCDE(Z)
     A            XSIDET         3  0   5 31EDTCDE(Z)
     A                                  5 37'Dato:'
     A            THTRDT         8  0   5 42EDTWRD('    .  .  ')
     A                                  6 37'Vår referanse:'
     A            THAVD          4  0   6 55
     A                                  6 59'/'
     A            THTDN          7  0   6 60
     A                                  6 67'/'
     A            THSG           3      6 68
     A                                  7  4'____________________________'
     A                                  7 32'____________________________'
     A                                  7 60'____________________________'
     A                                  7 88'____________________________'
     A                                  8  4'|Varepostnr.  |Merke og nummer  |'
     A                                  8 37'Antall og type |Containernr. |'
     A                                  8 67'Varebeskrivelse'
     A                                  8115'|'
     A                                  9  4'|(32)         |(31.1)           |'
     A                                      UNDERLINE
     A                                  9 37'(31.2)         |(31.3)       |'
     A                                      UNDERLINE
     A                                  9 67'(31.4)                            '
     A                                      UNDERLINE
     A                                  9101'              |'
     A                                      UNDERLINE
     A                                 10  4'|Status       |Varenummer       |'
     A                                 10 37'Følsomhetskode |Følsom mengde|'
     A                                 10 67'Summarisk deklarasjon/'
     A                                 10 89'Tidligere dokument'
     A                                 10115'|'
     A                                 11  4'|(1/3)        |(33)             |'
     A                                      UNDERLINE
     A                                 11 37'(31.5)         |(31.6)       |'
     A                                      UNDERLINE
     A                                 11 67'(40)                              '
     A                                      UNDERLINE
     A                                 11101'              |'
     A                                      UNDERLINE
     A                                 12  4'|Avsender-    |Bestemmelsesland |'
     A                                 12 37'    Bruttovekt |   Nettovekt |'
     A                                 12 67'Tilleggsopplysninger/Fremlagte '
     A                                 12 98'Sertifikater og'
     A                                 12115'|'
     A                                 13  4'|utførselsland|                 |'
     A                                 13 37'          (kg) |        (kg) |'
     A                                 13 67'autorisasjoner'
     A                                 13115'|'
     A                                 14  4'|(15)         |(17)             |'
     A                                      UNDERLINE
     A                                 14 37'(35)           |(38)         |'
     A                                      UNDERLINE
     A                                 14 67'(44)                              '
     A                                      UNDERLINE
     A                                 14101'              |'
     A                                      UNDERLINE
     A                                 15  4'|Avsender/Eksportør'
     A                                 15 59'|Mottaker'
     A                                 15115'|'
     A                                 16  4'|(2)                              '
     A                                      UNDERLINE
     A                                 16 38'                     '
     A                                      UNDERLINE
     A                                 16 59'|(8)                              '
     A                                      UNDERLINE
     A                                 16 93'                      |'
     A                                      UNDERLINE
     A                                 17  4'____________________________'
     A                                 17 32'____________________________'
     A                                 17 60'____________________________'
     A                                 17 88'____________________________'
      *
     A          R TR014V2                   SPACEB(1)
     A                                     4'|'
     A N98        XLN            4  0      6EDTCDE(Z)
     A                                    18'|'
     A            TVMN          17        19
     A                                    36'|'
     A N98        TVNT           5  0     38EDTCDE(Z)
     A N98        TVEH           2        45
     A                                    48'|'
     A            TVCNR         17        49
     A                                    66'|'
     A            TVVT          48        67
     A                                   115'|'
      *
     A          R TR014V3
     A                                     4'____________________________'
     A                                    32'____________________________'
     A                                    60'____________________________'
     A                                    88'____________________________'
      *
     A          R TR014V4                   SPACEB(1)
     A                                     4'|'
     A N98        TVDK           5         6
     A                                    18'|'
     A N98N27     TVVNT          8  0     20
     A N98 27                             20'--------'
     A                                    36'|'
     A            TVFV           2        40
     A                                    48'|'
     A            TVFVNT        11  3     50EDTWRD('        ,   ')
     A                                    66'|'
     A            TVTDT          6        67
     A            TVTDR         20        75
     A                                   115'|'
      *
     A          R TR014V5                   SPACEB(1)
     A                                     4'|'
     A N98        TVALK          2         6
     A                                    18'|'
     A N98        TVBLK          2        20
     A                                    36'|'
S02  A*N98        TVVKTB         9  0     38EDTCDE(Z)
N02  A N98        TVVKTB103     10  3     37EDTCDE(4)
     A                                    48'|'
S02  A*N98        TVVKTN         9  0     51EDTCDE(Z)
N02  A N98        TVVKTN        11  3     51EDTCDE(4)
     A                                    66'|'
     A            XVDTY         28        67
     A            TVDREF        20        95
     A                                   115'|'
      *
     A          R TR014V6                   SPACEB(1)
     A                                     4'|'
     A N98        TVALK          2         6
     A                                    18'|'
     A N98        TVBLK          2        20
     A                                    36'|'
S02  A*N98        TVVKTB         9  0     38EDTCDE(Z)
N02  A N98        TVVKTB103     10  3     37EDTCDE(4)
     A                                    48'|'
S02  A*N98        TVVKTN         9  0     51EDTCDE(Z)
N02  A N98        TVVKTN        11  3     51EDTCDE(4)
     A                                    66'|'
     A            TVDO          26        67
     A                                   115'|'
      *
     A          R TR014V7                   SPACEB(1)
     A                                     4'|'
     A N98        TVALK          2         6
     A                                    18'|'
     A N98        TVBLK          2        20
     A                                    36'|'
S02  A*N98        TVVKTB         9  0     38EDTCDE(Z)
N02  A N98        TVVKTB103     10  3     37EDTCDE(4)
     A                                    48'|'
S02  A*N98        TVVKTN         9  0     51EDTCDE(Z)
N02  A N98        TVVKTN        11  3     51EDTCDE(4)
     A                                    66'|'
     A            XVTLO         35        67
     A            XVEXKD         2       107
     A            TVEXLK         2       111
     A                                   115'|'
      *
     A          R TR014V8                   SPACEB(1)
     A                                     4'|'
     A N98        TVALK          2         6
     A                                    18'|'
     A N98        TVBLK          2        20
     A                                    36'|'
S02  A*N98        TVVKTB         9  0     38EDTCDE(Z)
N02  A N98        TVVKTB103     10  3     37EDTCDE(4)
     A                                    48'|'
S02  A*N98        TVVKTN         9  0     51EDTCDE(Z)
N02  A N98        TVVKTN        11  3     51EDTCDE(4)
     A                                    66'|'
     A            TVMTXT        48        67
     A                                   115'|'
      *
     A          R TR014V9                   SPACEB(1)
     A                                     4'|'
     A            TVNAS         30         6
     A                                    59'|'
     A            TVNAK         30        61
     A                                   115'|'
      *
     A          R TR014VA                   SPACEB(1)
     A                                     4'|'
     A            TVADS1        30         6
     A                                    59'|'
     A            TVADK1        30        61
     A                                   115'|'
      *
     A          R TR014VB                   SPACEB(1)
     A                                     4'|'
     A            TVPNS          9         6
     A            TVPSS         24        16
     A                                    59'|'
     A            TVPNK          9        61
     A            TVPSK         24        70
     A                                   115'|'
      *
     A          R TR014VC                   SPACEB(1)
     A                                     4'|'
     A            TVPSS         24         6
     A                                    59'|'
     A            TVPSK         24        61
     A                                   115'|'
      *
     A          R TR014VD
     A N96N50     PVFM3         75     64  2
     A  96N50     PVFM32        75     64  2
