     A*%%TS  SD  20180508  064934  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0509A* 01 JOV Y2K  IKKE EDITKODE PÅ PERIODE   M.
     A* 03 JOV      TUR UTEN POD  (BRINGTMS)
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A*%%TS  SD  20180508  064109  SVEIN       REL-V7R1M0  5770-WDS
     A            THDTD          2S 0H
     A            WSNRO          1A  B  8  2VALUES(' ' '1' '2' '4' '6' '7' '9')
     A            THSTAR    R        O  8  4REFFLD(TRAVHR/THSTAR *LIBL/TRAVH)
     A                                      COLOR(RED)
     A            THRTNR    R        O  8  6REFFLD(TRAVHR/THRTNR *LIBL/TRAVH)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(RED)
     A                                      EDTCDE(4)
     A            THTAVD    R        O  8 15REFFLD(TRAVHR/THTAVD *LIBL/TRAVH)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            THTUNR    R        O  8 20REFFLD(TRAVHR/THTUNR *LIBL/TRAVH)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            THKDM          1A  O  8 29COLOR(YLW)
     A            TURDAT         6Y 0O  8 32EDTCDE(4)
     A                                      COLOR(YLW)
     A            TUBILN        10A  O  8 40COLOR(YLW)
     A            THBRT     R        O  8 52REFFLD(TRAVHR/THBRT *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A            THTOT     R        O  8 66REFFLD(TRAVHR/THAVT *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            TUTAKO         1A  O  8 80COLOR(RED)
     A            THINH     R        O  9  4REFFLD(TRAVHR/THINH *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A  86                                  COLOR(RED)
     A            THUTK     R        O  9 18REFFLD(TRAVHR/THUTK *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A  87                                  COLOR(RED)
     A            THRKV     R        O  9 32REFFLD(TRAVHR/THRKV *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A            THBUD     R        O  9 46REFFLD(TRAVHR/THBUD *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A            THJUS     R        O  9 60REFFLD(TRAVHR/THJUS *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A            TUTVAL         3A  O  8 82
     A            TUTBEL        11Y 2O  8 87EDTWRD('         .  ')
     A            TUANT1    R        B  8101REFFLD(TURERR/TUANT1 *LIBL/TURER)
     A                                      EDTCDE(Z)
     A            TUENH1    R        B  8107REFFLD(TURERR/TUENH1 *LIBL/TURER)
     A            TUANT2    R        B  8111REFFLD(TURERR/TUANT2 *LIBL/TURER)
     A                                      EDTCDE(3)
     A            TUENH2    R        B  8118REFFLD(TURERR/TUENH2 *LIBL/TURER)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20180508  064109  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYTR12'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2253 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(015 DIV2254 *LIBL/SYMSGF)
     A            VMTRAN    R        O  2 19REFFLD(TRANR/VMTRAN *LIBL/TRAN)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VMNAVN    R        O  2 28REFFLD(TRANR/VMNAVN *LIBL/TRAN)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(015 DIV2256 *LIBL/SYMSGF)
     A            XVMTYP        20A  O  3 19COLOR(WHT)
     A                                  3 42MSGCON(015 DIV2255 *LIBL/SYMSGF)
     A            W1PERM         2S 0O  3 58COLOR(WHT)
     A            W1PERÅ         2S 0O  3 61COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 59MSGCON(018 JOV0009 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A                                  5 22MSGCON(015 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 42MSGCON(015 DIV2227 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 59MSGCON(015 DIV2414 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 DIV2251 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(130 DIV2252 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 42MSGCON(011 SHU1102 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A*%%TS  SD  20150323  212313  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF07(07 'I ARB.')
     A N95                                  CF10(10 'FERDIGM')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            XTOTXT        50A  O  1  2COLOR(WHT)
     A            XTOTBL    R        O  1 67REFFLD(TRAVHR/THAVT *LIBL/TRAVH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  2 62MSGCON(018 JOV0010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 43MSGCON(018 DIV2261 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  95                                  DSPATR(ND)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98N30                           4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  98 30     MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            IPODT1         8   O  2 20COLOR(RED)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(078 DIV2251 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            THRTNR    R        O  2  4REFFLD(TRAVHR/THRTNR *LIBL/TRAVH)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            THTAVD    R        O  2 13REFFLD(TRAVHR/THTAVD *LIBL/TRAVH)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            THTUNR    R        O  2 18REFFLD(TRAVHR/THTUNR *LIBL/TRAVH)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            THMVA          1   O  2 28COLOR(WHT)
     A            TURDAT         6  0O  2 31COLOR(WHT)
     A            TUBILN        10   O  2 39COLOR(WHT)
     A            THBRT     R        O  2 52REFFLD(TRAVHR/THBRT *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            THTOT     R        O  2 66REFFLD(TRAVHR/THAVT *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            THINH     R        O  3  4REFFLD(TRAVHR/THINH *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            THUTK     R        O  3 18REFFLD(TRAVHR/THUTK *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            THRKV     R        O  3 32REFFLD(TRAVHR/THRKV *LIBL/TRAVH)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            THBUD     R        O  3 46REFFLD(TRAVHR/THBUD *LIBL/TRAVH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A N95                                  CF10(10 'FERDIGM')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR12'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2253 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(015 DIV2254 *LIBL/SYMSGF)
     A            VMTRAN    R        O  2 19REFFLD(TRANR/VMTRAN *LIBL/TRAN)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VMNAVN    R        O  2 28REFFLD(TRANR/VMNAVN *LIBL/TRAN)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(015 DIV2256 *LIBL/SYMSGF)
     A            XVMTYP        20A  O  3 19COLOR(WHT)
     A                                  3 42MSGCON(015 DIV2255 *LIBL/SYMSGF)
     A            W1PERM         2S 0O  3 58COLOR(WHT)
     A            W1PERÅ         2S 0O  3 61COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A                                  5 22MSGCON(015 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 42MSGCON(015 DIV2227 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 DIV2251 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(130 DIV2252 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            XTOTXT        50A  O 21  2COLOR(WHT)
     A            XTOTBL    R        O 21 67REFFLD(TRAVHR/THAVT *LIBL/TRAVH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 43MSGCON(018 DIV2261 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  95                                  DSPATR(ND)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BILDE1
     A*%%TS  SD  20151020  094825  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'SØKING')
     A                                      CF07(07 'ARB.LISTE')
     A                                      CF23(23 'ALLE OK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTR12'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2253 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4  3MSGCON(025 DIV2358 *LIBL/SYMSGF)
     A            W1TNR     R        B  4 29REFFLD(TURERR/TUKNT *LIBL/TURER)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                  5  3MSGCON(025 DIV2265 *LIBL/SYMSGF)
S01  A*           W1PERM         2Y 0B  6 29CHECK(RB)
     A            W1PERM         2Y 0B  5 29
     A  71                                  DSPATR(RI)
S01  A*           W1PERÅ         2Y 0B  6 32CHECK(RB)
     A            W1PERÅ         2Y 0B  5 32
     A  71                                  DSPATR(RI)
     A                                  6  3MSGCON(025 SHU1608 *LIBL/SYMSGF)
     A            W1OPDT         2   B  6 29
     A  74                                  DSPATR(RI)
     A                                  7  3MSGCON(025 SHU1609 *LIBL/SYMSGF)
     A            W1DAG          2Y 0B  7 29EDTCDE(Z)
     A  75                                  DSPATR(RI)
     A            W1MND          2Y 0B  7 32EDTCDE(Z)
     A  75                                  DSPATR(RI)
     A            W1AAR          2Y 0B  7 35EDTCDE(Z)
     A  75                                  DSPATR(RI)
     A                                  7 38'(DDMMYY)'
     A                                  9  3MSGCON(025 JOV0221 *LIBL/SYMSGF)
     A            WSREGE         1A  B  9 29VALUES('Y' 'J' 'N' ' ')
     A  72                                  DSPATR(RI)
     A                                  9 37MSGCON(015 JOV0222 *LIBL/SYMSGF)
     A                                 12  3MSGCON(025 SHU0460 *LIBL/SYMSGF)
     A            WSPRT         10A  B 12 29
     A  73                                  DSPATR(RI)
     A                                 23  2MSGCON(015 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 43MSGCON(020 JOV0001 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
