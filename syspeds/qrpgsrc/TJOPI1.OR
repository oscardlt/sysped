V11TM * V11TMP - Obs ved inst. hos kunder m/70 lang 'ubane' i kall på BHRURLCL - aktiver/snu V11TM
      *********************************************************************
      *  Enkel spørring/innland - basert på SYOPI1 men med JSON output
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJOPI1.OR MODULE(*LIBL/TJOPI1.OR *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FHEAD39    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEAF39R)
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEAF17R)
     FDOKUFM    IF   E           K DISK    usropn
     FDOKUFM1   IF   E           K DISK    usropn
     F                                     RENAME(DOKUFMR:DOKUFM1R)
     FTRACKL01  IF   E           K DISK    usropn
     FKOFAST    IF   E           K DISK    usropn
     FFRISOK    IF   E           K DISK    usropn
     FFRISOL01  IF   E           K DISK    usropn
     F                                     RENAME(FRISOKR:FRISOKR2)
     FKODFRI    IF   E           K DISK    usropn
     FDOKUF7    IF   E           K DISK    usropn
     FTRAN      IF   E           K DISK    usropn
     FTurer14   IF   E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D SndNr           s             20
     D KllNr           s             20
     D JSONstring      S          32000
     D Error           S            150
     D KVURL           s            140
     D UtText          s            500
     D WSSOK           S             80
     D WSSOKURL        S            200
      *
     D AvsA            s              4
     D OpdA            s              7
     D KVdato          s              8  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
     D                 DS
     D  DSAVOP                 1     12
     D  DSAVD                  1      4
     D  Strek                  5      5
     D  DSOPD                  6     12
     D                 DS
     D  KFTXT                  1     50
     D  KFTXTB                 5     49
     D  VisWEB                50     50
     D                 DS
     D  DFS                    1     10    DIM(10)
     D  KFSODK                 1     10
      *
      * Prototype
     D JSONescape      PR                  ExtPgm('JSONESC')
     D   JSONstr                  32000A
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      *
     C                   exsr      SetAll
      * Quit
     C     Slutt         tag
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SNDNR   = zhbgetvar('sndnr')
     C                   eval      KLLNR   = zhbgetvar('kllnr')
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
     C                   CLOSE     HEAD17
     C                   CLOSE     HEAD39
     C                   CLOSE     DOKUFM
     C                   CLOSE     DOKUFM1
     C                   CLOSE     TRACKL01
     C                   CLOSE     KOFAST
     C                   CLOSE     FRISOK
     C                   CLOSE     FRISOL01
     C                   CLOSE     KODFRI
     C                   CLOSE     DOKUF7
     C                   CLOSE     TRAN
     C                   CLOSE     Turer14
     C                   endsr
      ****************************************************************
     C     SetAll        begsr
      ****************************************************************
     c                   exsr      TSTINPUT
      * fiks gitte data
     c                   move      *zeros        KVDATO
     C                   if        HEDTMO>'00000000' and  HEDTMO<'99999999'
     c                   move      HEDTMO        KVDATO
     c                   endif
      *
      /free
        JSONstring='{'
        +'¬inp_user¬ : ¬'+%trim(USER)+'¬, '
          +'¬inp_sndnr¬ : ¬'+%trim(SNDNR)+'¬, '
          +'¬inp_kllnr¬ : ¬'+%trim(KLLNR)+'¬ ';
        exsr SkrivJSON;

        //info om ett oppdrag
        if Error=*blanks;
          JSONstring=', '
          +'¬sendingsNr¬ : ¬'+%trim(HXSNDN)+'¬, '
          +'¬sendersRef¬ : ¬'+%trim(HERFA)+'¬, '
          +'¬avdeling¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬,'
          +'¬oppdrag¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬,'
          +'¬saksbeh¬ : ¬'+%trim(HESG)+'¬, '
          +'¬ordredato¬ : ¬'+%trim(%editw(HEDTOP:'    .  .  '))+'¬,'
          +'¬mottaker¬ : ¬'+%trim(HENAK)+'¬, '
          +'¬mottAdr1¬ : ¬'+%trim(HEADK1)+'¬, '
          +'¬mottAdr2¬ : ¬'+%trim(HEADK2)+'¬, '
          +'¬mottAdr3¬ : ¬'+%trim(HEADK3)+'¬, '
          +'¬antall¬ : ¬'+%trim(%editc(HENT:'3'))+'¬,'
          +'¬varebesk¬ : ¬'+%trim(HEVS1)+'¬, '
          +'¬vekt¬ : ¬'+%trim(%editc(HEVKT:'3'))+'¬,'
          +'¬volM3¬ : ¬'+%trim(%editc(HEM3:'3'))+'¬,'
          +'¬lastem¬ : ¬'+%trim(%editc(HELM:'3'))+'¬,'
          +'¬kvittert¬ : ¬'+%trim(HESGM)+'¬, '
          +'¬kvDato¬ : ¬'+%trim(%editw(KVDATO:'    .  .  '))+'¬,'
          +'¬kvTid¬ : ¬'+%trim(%editw(HEKLMO:'  :  '))+'¬';
        exsr SkrivJSON;
        //diverse tabellbasert info referanser / T&T
        exsr DspFriSok;
        exsr TrackInfo;
        endif;

        //avslutning
        JSONstring=', ¬errMsg¬ : ¬'+%trim(Error)+'¬ }';
        exsr SkrivJSON;
      /end-free
      *
     C                   endsr
      *************************************************************
     C     TSTINPUT      begsr
      *************************************************************
     c                   select
      * Sendingsnr
     c                   when      sndnr <> *blanks
     c                   if        %subst(sndnr:17:1)<>' ' and                  17 lang-legg på AI
     c                             %subst(sndnr:18:3)='   '
     c                   eval      sndnr='401'+sndnr
     c                   endif
      * 1. via innl.send.nr
     c     Sndnr         chain     HEAD39                             35
      * 2. via fbr-nr frisok
     c                   if        *in35 = '1'
     C                   eval      FSSOK    =    %subst(SNDNR:4:17)
     C     Fri1Key       CHAIN     FRISOL01                           35
     c     *IN35         ifeq      *OFF
     C                   MOVE      FSAVD         FMAVD
     C                   MOVE      FSOPD         FMOPD
     C     HeadfKey      CHAIN     HEADF                              35
     c                   endif
     c                   endif
      * 3. via avd/oppdr.nr
     c                   if        *in35 = '1' and
     c                             %subst(sndnr:5:1)='/'
     c                   eval      AvsA=%subst(sndnr:1:4)
     c                   eval      OpdA=%subst(sndnr:6:7)
     c                   move      AvsA          FMAVD
     c                   move      OpdA          FMOPD
     C     HeadfKey      CHAIN     HEADF                              35
     c                   endif
      * 35 ON= fant ikke sendingen
     c                   if        *in35 = '1'
     c                   eval      Error = 'Finner ikke sending ' + sndnr
     c                   endif
      *
      * Kollinr
     c                   when      kllnr <> *blanks
     c                   if        %subst(kllnr:18:1)<>' ' and                  18 lang-legg på AI
     c                             %subst(kllnr:19:2)='  '
     c                   eval      kllnr='00'+Kllnr
     c                   endif
     C                   eval      FMMRK1   =    %subst(kllnr:3:18)
     C     FMMRK1        CHAIN     DOKUFM1                            35
     c     *IN35         ifeq      *OFF
     C     HeadfKey      CHAIN     HEADF                              35
     c                   endif
      * 35 ON= fant ikke kolliet/sendingen
     c                   if        *in35 = '1'
     c                   eval      Error = 'Finner ikke kolli ' + kllnr
     c                   endif
     c                   other
     c                   eval      Error = 'Sendingsnr eller kollinr må '
     c                             +'angis som søkeparameter'
     c                   endsl
      *
     c                   endsr
      *=====================================================================******
      *  TRACKINFO
      *=====================================================================******
     C     TRACKINFO     begsr
      /free
        JSONstring=', ¬trackTraceEvents¬ : [';
        exsr SkrivJSON;
      /end-free
      * INFO FRA T&T -LOGG
     c                   move      'Y'           FirstTT           1
     c                   move      *blanks       KFKOD
      * finnes det Originalt Fraktbrev - i så fall aller først ??
     c                   eval       uttext = 'ZS'
     c                   CALL      'BHRKVFB'
     c                   parm                    HEavd
     c                   parm                    HEopd
     c                   Parm                    UTTEXT
     C                   parm                    ttdate
     C                   parm                    tttime
     c     UTTEXT        ifne      *blanks
      /free
       if FirstTT='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          FirstTT  = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬dato¬ : ¬¬,'
          +'¬tid¬ : ¬¬,'
          +'¬tekst¬ : ¬'+%trim(utText)+'¬} ';
       exsr SkrivJson;
      /end-free
     c                   move      *blanks       uttext
     c                   end
      * HENDELSER I T&T
     c     TrackKey      setll     trackl01
     c     TrackKey      reade     trackl01                               50
     c     *in50         doweq     *OFF
     c                   move      ' '           VisWEB
     c                   movel     TTACTI        KFKOD
     c     ACTKey        chain     KOFAST
     C     TTACTI        IFEQ      'POD'
     C     TTACTI        OREQ      'AVV'
     C     TTFBNR        IFNE      0
     c                   move      ' '           VisWEB
     C                   end
     C                   end
     c     VisWEB        ifeq      'J'
     c     TTacti        ifeq      'POD'
     c                   eval      UTTEXT = KFTXTB + TTTEXT
     c                   else
     c                   eval      UTTEXT = TTTEXT
     C                   end
     c                   movel     TTTIME        TTTIM4            4 0
      /free
       if FirstTT='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          FirstTT  = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬dato¬ : ¬'+%trim(%editw(TTDATE:'    .  .  '))+'¬,'
          +'¬tid¬ : ¬'+%trim(%editw(TTTIM4:'  :  '))+'¬,'
          +'¬tekst¬ : ¬'+%trim(utText)+'¬} ';
       exsr SkrivJson;
      /end-free
     c     TTacti        ifeq      'POD'
      * normalt er evt signatur sendingsnr.gif
     c                   movel     HXSNDN        SNR              17
     c                   if        %subst(HXSNDN:20:1)<>' '
     c                   eval      SNR = %subst(HXSNDN:4:17)
     c                   endif
     C                   exsr      getgif
     C                   end
      * VEDL TRMODUL/UTLAND KAN SENDNR VÆRE  AVD_OPPDNR
     c     TTacti        ifeq      'POD'
     c     GIFOK         andne     'J'
     c                   movel     Snr           SnrGm            17
     c                   move      HEAVD         DSAVD
     C                   move      '_'           Strek
     c                   move      HEOPD         DSOPD
     c                   eval      snr    = DSAVOP
     C                   exsr      getgif
     c                   movel     SnrGm         Snr
     C                   end
      * vis evt gif ved COL/TEI/TEU/TE2
     c     TTacti        ifeq      'COL'
     c     TTacti        oreq      'TEI'
     c     TTacti        oreq      'TEU'
     c     TTacti        oreq      'TE2'
     C                   exsr      getgif2
     c     GIFOK         Ifne      'J'
     c                   movel     Snr           SnrGm
     c                   move      HEAVD         DSAVD
     C                   move      '_'           Strek
     c                   move      HEOPD         DSOPD
     c                   eval      snr    = DSAVOP
     C                   exsr      getgif2
     c                   movel     SnrGm         Snr
     C                   endif
     C                   endif
      *
     c                   end
     c     TrackKey      reade     trackl01                               50
     c                   end
      *
      * finnes det kvittert fraktbrev??  (fra 14.02.2009 - sjekk uansett ..
     c                   move      *blanks       uttext
     c                   CALL      'BHRKVFB'
     c                   parm                    HEavd
     c                   parm                    HEopd
     c                   Parm                    UTTEXT
     C                   parm                    ttdate
     C                   parm                    tttime
     c     UTTEXT        ifne      *blanks
      /free
       if FirstTT='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          FirstTT  = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬dato¬ : ¬¬,'
          +'¬tid¬ : ¬¬,'
          +'¬tekst¬ : ¬'+%trim(utText)+'¬} ';
       exsr SkrivJson;
      /end-free
     c                   move      *blanks       uttext
     c                   end
      *
      * avslutt liste med Ikke trackbare søkeveier
      /free
        JSONstring=']';
        exsr SkrivJSON;
      /end-free
     C                   endsr
      *
      *=====================================================================******
      *  Display TRACK & TRACE
      *=====================================================================******
     C     GETGIF        begsr
      *
     c                   MOVE      'N'           GIFOK             1
     c                   move      *blanks       ubane           256
V11TMc**                 move      *blanks       ubane            70
     C                   movel     '/syspeddev/' ubane
     C     ubane         CAT       'signatur/':0 ubane
     C     ubane         CAT       SNR:0         ubane
     C     ubane         CAT       '.gif':0      ubane
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane
     c                   Parm                    GIFOK
      * gif fantes ikke, prøv med bmp.
     c     GIFOK         ifne      'J'
     c                   move      *blanks       ubane
     C                   movel     '/syspeddev/' ubane
     C     ubane         CAT       'signatur/':0 ubane
     C     ubane         CAT       SNR:0         ubane
     C     ubane         CAT       '.bmp':0      ubane
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane
     c                   Parm                    GIFOK
     c                   end
     c     GIFOK         ifeq      'J'
     c                   move      *blanks       uttext
     c                   movel     '<p><img bord'uttext
     C     uttext        CAT       'er="0" sr':0 uttext
     C     uttext        CAT       'c="':0       uttext
     C     uttext        CAT       ubane:0       uttext
     C     uttext        CAT       '"></p>':0    uttext
      /free
       if FirstTT='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          FirstTT  = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬dato¬ : ¬'+%trim(%editw(TTDATE:'    .  .  '))+'¬,'
          +'¬tid¬ : ¬'+%trim(%editw(TTTIM4:'  :  '))+'¬,'
          +'¬tekst¬ : ¬'+%trim(utText)+'¬} ';
       exsr SkrivJson;
      /end-free
     c                   move      *blanks       uttext
     c                   end
     C                   endsr
      *=================================================================
     C     GETGIF2       begsr
      *
     c                   MOVE      'N'           GIFOK             1
     c                   move      *blanks       ubane
     C                   movel     '/syspeddev/' ubane
     C     ubane         CAT       'signatur/':0 ubane
     C     ubane         CAT       TTACTI:0      ubane
     C     ubane         CAT       '_':0         ubane
     C     ubane         CAT       SNR:0         ubane
     C     ubane         CAT       '.gif':0      ubane
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane
     c                   Parm                    GIFOK
     c     GIFOK         ifeq      'J'
     c                   move      *blanks       uttext
     c                   movel     '<p><img bord'uttext
     C     uttext        CAT       'er="0" sr':0 uttext
     C     uttext        CAT       'c="':0       uttext
     C     uttext        CAT       ubane:0       uttext
     C     uttext        CAT       '"></p>':0    uttext
      /free
       if FirstTT='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          FirstTT  = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬dato¬ : ¬'+%trim(%editw(TTDATE:'    .  .  '))+'¬,'
          +'¬tid¬ : ¬'+%trim(%editw(TTTIM4:'  :  '))+'¬,'
          +'¬tekst¬ : ¬'+%trim(utText)+'¬} ';
       exsr SkrivJson;
      /end-free
     c                   move      *blanks       uttext
     c                   end
     C                   endsr
      *=====================================================================******
      *  Display FriSok   tracbare / andre
      *=====================================================================******
     C     DspFriSok     begsr
      *
      /free
        JSONstring=', ¬trackbarRef¬ : [';
        exsr SkrivJSON;
      /end-free
      * VIS TRACKBAR SØKEVEI
     c                   move      'Y'           Firstfri          1
      * Innland sendt med transportør som er trackbar..
     c     HXsndn        Ifne      *blanks
     c     HEPRO         andne     *zeros
     c                   exsr      InnlTransp
     c                   end
      * frie søkeveier som er trackbare.. (herunder IFB fraktbrev..)
     C     TrackKey      SETLL     FRISOK
     C     TrackKey      READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     c                   move      *blanks       KFSTTU
     c     FSKODE        Chain     KODFRI                             33
     C     *IN33         CABEQ     '1'           NextFri1
     C                   IF        FSKODE = 'IFB'                                2<-B
     C                   exsr      TstFbr
     C                   END                                                     2<-E
      * Logikk som tester om ref er trackbar/lager link er fluttet til egen sr uten merking.
     c                   exsr      TstLaglink
     C     NextFri1      TAG
     C     TrackKey      READE     FRISOK                                 33
     C  N33              ENDDO                                                  1<-E
      * avslutt liste med trackbare søkeveier
      /free
        JSONstring=']';
        exsr SkrivJSON;
      /end-free
      *
      *
      *
      *
      * VIS IKKE-TRACKBAR SØKEVEI
      *
      /free
        JSONstring=', ¬andreRef¬ : [';
        exsr SkrivJSON;
      /end-free
     C                   move      'Y'           Firstfri          1
     C     TrackKey      SETLL     FRISOK
     C     TrackKey      READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     c     FSKODE        Chain     KODFRI                             33
     C     *IN33         CABEQ     '1'           NextFri2
     C     'A'           LOOKUP    DFS                                    33
     C     *IN33         CABEQ     '0'           NextFri2
     C                   IF        FSKODE = 'IFB'                                2<-B
     c     FSSOK         cabeq     SNR           NextFri2
     C                   exsr      TstFbr
     C                   END                                                     2<-E
      * Har denne frisok-kode peker til Track&Trace-URL-definisjon?
     C                   EVAL      KFKOD = KFSTTU
     C     KOFASTK1      CHAIN     KOFAST                             33
     C                   IF        *IN33                                         2<-B
      /free
       if FirstFri='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          Firstfri = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬type¬ : ¬'+%trim(KFSOTX)+'¬, '
          +'¬verdi¬ : ¬'+%trim(FSSOK)+'¬} ';
       exsr SkrivJson;
      /end-free
     C                   END                                                     2<-E
     C     NextFri2      TAG
     C     TrackKey      READE     FRISOK                                 33
     C  N33              ENDDO                                                  1<-E
      * avslutt liste med Ikke trackbare søkeveier
      /free
        JSONstring=']';
        exsr SkrivJSON;
      /end-free
     C                   endsr
     C***********************************************
     C     TSTFBR        BEGSR
     C***********************************************
      * Test om transportør på fraktbrev overstyrer T&T-URL-kode
     C*
     c     FSSOK         cabeq     SNR           UtTstFbr
     C     FSSOK         chain     dokuf7                             33
     C     *IN33         doweq     '0'
     C     DFOPD         andne     HEOPD
     C     FSSOK         reade     dokuf7                                 33
     c                   end
     c     *IN33         cabeq     '1'           UtTstFbr
     c     DFTRAN        cabeq     0             UtTstFbr
     C     TranKey       chain     Tran                               33
     c     *IN33         cabeq     '1'           UtTstFbr
     c     VMINTP        cabeq     *blanks       UtTstFbr
     C                   EVAL      KFSTTU  = VMINTP
     C                   EVAL      KFSOTX  = 'Fr.brevsnr ' + DFNAT
     C     UtTstFbr      ENDSR
     C*
     C***********************************************
     C     InnlTransp    BEGSR
     C***********************************************
      * Sendingsnr i tr.modul innland - transportør med T&T-URL-kode ??
     C*
     c     Hepro         chain     Turer14                            33
     c     *IN33         cabeq     '1'           UtInnlTran
     c                   move      Tuknt2        DFtran
     C     TranKey       chain     Tran                               33
     c     *IN33         cabeq     '1'           UtInnlTran
     c     VMINTP        cabeq     *blanks       UtInnlTran
     C                   EVAL      KFSTTU  = VMINTP
     C                   EVAL      KFSOTX  = 'Fr.brevsnr ' + VMNAVN
     c                   eval      FSSOK   = SNR
     c                   exsr      TstLaglink
     C     UtInnlTran    ENDSR
     C*
     C***********************************************
     C     TstLagLink    BEGSR
     C***********************************************
     C* Denne subrutine er kun kode som er flyttet ut fra DspTTFRI
      * Har denne frisok-kode peker til Track&Trace-URL-definisjon?
     C                   EVAL      KFKOD = KFSTTU
     C     KOFASTK1      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF                                  2<-B
      * Verdi som vises i link..
     C                   EVAL      WSSOK = FSSOK
      * URL som skal utføres
     C                   EVAL      WSSOKURL = KFTXT
      * append linje 2 (engelsk tekst når siet tegn er A)
     C                   MOVE      KFTXTE        TstAppend         1
     C     TstAppend     IFEQ      'A'                                            3<-B
     c                   move      ' '           KFTXTE
     C                   CAT       KFTXTE:0      WSSOKURL
     C                   end                                                      3<-E
     C                   CAT       FSSOK:0       WSSOKURL
      * Suffix i søkestrengen..
     C     KOFASTK2      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF                                   3<-B
     C                   CAT       KFTXT:0       WSSOKURL
     C                   END                                                      3<-E
      *
      /free
       if FirstFri='Y';
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
          Firstfri = 'N';
       JSONstring=%trim(JSONstring)+'{'
          +'¬type¬ : ¬'+%trim(KFSOTX)+'¬, '
          +'¬verdi¬ : ¬'+%trim(WSSOK)+'¬ '
          +'¬url¬ : ¬'+%trim(WSSOKURL)+'¬} ';
       exsr SkrivJson;
      /end-free
     C                   END                                                     2<-E
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      HEADF
     C                   OPEN      HEAD17
     C                   OPEN      HEAD39
     C                   OPEN      DOKUFM
     C                   OPEN      DOKUFM1
     C                   OPEN      TRACKL01
     C                   OPEN      KOFAST
     C                   OPEN      FRISOK
     C                   OPEN      FRISOL01
     C                   OPEN      KODFRI
     C                   OPEN      DOKUF7
     C                   OPEN      TRAN
     C                   OPEN      Turer14
      *
     C     Fri1Key       KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     IFBKey        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FSKODE
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HeadfKey      KLIST
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C     DokufmKey     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'F'           FMRI
      *
     C     ActKey        KLIST
     C                   KFLD                    KFTYP0           10
     C                   KFLD                    KFKOD
     c                   MOVEL     'TRACKT'      KFTYP0

     C     KOFASTK1      klist
     C                   kfld                    KFTYP1           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP1 = 'TRACKPREFI'
     C     KOFASTK2      klist
     C                   kfld                    KFTYP2           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP2 = 'TRACKSUFFI'
     C     TranKey       klist
     C                   kfld                    BIFIRM
     C                   kfld                    DFTRAN
      *
      *
     C                   endsr

      **************************************************************
     C     SkrivJSON     begsr
      **************************************************************
      /free
        callp JSONescape(JSONstring);
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');
      /end-free
     C                   endsr
      *
