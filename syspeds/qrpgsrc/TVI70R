     FTVI70D    CF   E             WORKSTN
     F                                     SFILE(TVI70DB:ZRECNO)
     FEDIM8     IF   E           K DISK
     FEDIMH     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMRH)
     FSTANDI    IF   E           K DISK
     FTVI70P    O    E             PRINTER OFLIND(*IN98)
     D TABA            S              2  0 DIM(12) CTDATA PERRCD(1)
     D TABB            S              3  0 DIM(12) ALT(TABA)
     D                 DS
     D  ZDTTM                  1     14  0
     D  ZDTT                   1      8  0
     D  ZTMT                   9     14  0
     D                 DS
     D  MDTTM                  1     14  0
     D  MDT                    1      8  0
     D  MTM                    9     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅ                   1      4  0
     D  XDTM                   5      6  0
     D  XDTD                   7      8  0
     D  XDT6                   3      8  0
     D  XTM                    9     12  0
     D  XTMH                   9     10  0
     D  XTMM                  11     12  0
     D                 DS
     D  YDT                    1      8  0
     D  YDTÅ                   1      4  0
     D  YDTM                   5      6  0
     D  YDTD                   7      8  0
     D  YDT6                   3      8  0
     D  YTM                    9     12  0
     D  YTMH                   9     10  0
     D  YTMM                  11     12  0
     D                 DS
     D  WTID                   1      6  0
     D  WTIDH                  1      2  0
     D  WTIDM                  3      4  0
     D  WTIDS                  5      6  0
     D                 DS
     D  M0068A                 1      8  0
     D  M068A6                 3      8  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZDTF
     C                   KFLD                    ZTMF
     C     KEYB          KLIST
     C                   KFLD                    W0068A
     C                   KFLD                    W0068B
     C                   KFLD                    W0068C
     C                   KFLD                    WMN
      *______________________
      * Definiere Arbeidsfelt
      *""""""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    M0068A        W0068A
     C     *LIKE         DEFINE    M0068B        W0068B
     C     *LIKE         DEFINE    M0068C        W0068C
     C     *LIKE         DEFINE    MSR           WSR
     C     *LIKE         DEFINE    MMN           WMN
     C                   Z-ADD     0             ÅDTÅ              1 0
     C                   Z-ADD     0             ÅDGR              3 0
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM               6
     C                   Z-ADD     0             ZAVD
     C                   MOVEL     WDT           ZDTF
     C                   MOVEL     WDT           ZDTT
     C                   Z-ADD     000000        ZTMF
     C                   Z-ADD     235959        ZTMT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI70DØ
     C                   EXFMT     TVI70DA
     C                   MOVEA     '0000000'     *IN(30)
     C     *IN03         CABEQ     '1'           SRB9
      * Teste avdeling, hvis tastet.
     C     ZAVD          IFGT      0
     C     ZAVD          CHAIN     STANDI                             30
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPA0001'     ZMSGNO
     C                   MOVEA     '1100000'     *IN(30)
     C                   GOTO      SRB1
     C                   END
     C                   END
      * Teste dato fra:
     C                   MOVEL     ZDTF          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1010000'     *IN(30)
     C   30              GOTO      SRB1
      * Teste dato til:
     C                   MOVEL     ZDTT          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1001000'     *IN(30)
     C   30              GOTO      SRB1
      * Fra dato større en til dato.
     C     ZDTF          IFGT      ZDTT
     C                   MOVE      'SPD0002'     ZMSGNO
     C                   MOVEA     '1011000'     *IN(30)
     C                   GOTO      SRB1
     C                   END
      * Teste tid fra:
     C                   Z-ADD     ZTMF          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000100'     *IN(30)
     C   30              GOTO      SRB1
      * Teste tid til:
     C                   Z-ADD     ZTMT          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000010'     *IN(30)
     C   30              GOTO      SRB1
      * Klokkeslett fra får ikke vare større en klokkeslett til.
     C     ZDTF          IFEQ      ZDTT
     C     ZTMF          ANDGT     ZTMT
     C                   MOVE      'SPK0007'     ZMSGNO
     C                   MOVEA     '1001010'     *IN(30)
     C                   GOTO      SRB1
     C                   END
     C     SRB2          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     TVI70DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIM8
     C                   READ      EDIM8                                  94
     C  N94MDTTM         COMP      ZDTTM                              94
      *
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN21
     C     ZAVD          COMP      0                                  20
     C   20MAVD          COMP      ZAVD                               2020
     C  N20M0065         IFEQ      'CUSDEC'
     C     MSR           ANDEQ     'S'
     C     M0068A        ANDNE     0
     C                   IF        ZTYPE1 <> *BLANKS
     C     M1225         CABEQ     ZTYPE1        XOK
     C                   END
     C                   IF        ZTYPE2 <> *BLANKS
     C     M1225         CABEQ     ZTYPE2        XOK
     C                   END
     C                   IF        ZTYPE3 <> *BLANKS
     C     M1225         CABEQ     ZTYPE3        XOK
     C                   END
     C                   IF        (((ZTYPE1 <> *BLANKS) OR
     C                               (ZTYPE2 <> *BLANKS)) OR
     C                               (ZTYPE3 <> *BLANKS))
     C                   GOTO      XNESTE
     C                   END
     C     XOK           TAG
     C                   EXSR      SRBB
     C                   END
     C     XNESTE        TAG
     C                   READ      EDIM8                                  94
     C  N94MDTTM         COMP      ZDTTM                              94
     C  N94              END
     C     ZRECNO        IFEQ      0
     C                   MOVE      'SPR0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRB1
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI70DØ
     C                   WRITE     TVI70DD
     C                   EXFMT     TVI70DC
      *
     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         CABEQ     '1'           SRB9
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB2
      *_________
      * Skriv ut
      *"""""""""
     C     *IN13         IFEQ      '1'
     C                   EXSR      SRBA
     C                   IF        ZRECNO > 0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   GOTO      SRB3
     C                   END
      *
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB2
     C     *IN95         CABEQ     '0'           SRB3
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv ut                                            SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C                   WRITE     TVI70PA
     C                   Z-ADD     1             WRECNO
     C     WRECNO        CHAIN     TVI70DB                            20
      *
     C     *IN20         DOWEQ     '0'
      *
     C     *IN98         IFEQ      '1'
     C                   WRITE     TVI70PA
     C                   MOVE      '0'           *IN98
     C                   END
      *
     C                   WRITE     TVI70PB
     C                   ADD       1             WRECNO
     C     WRECNO        CHAIN     TVI70DB                            20
      *
     C                   END
      *
     C                   WRITE     TVI70PØ
     C                   CLOSE     TVI70P
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile                                        SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   SELECT
     C     M1001         WHENEQ    '929'
     C                   MOVE      'SAI'         WSIMEX
     C     M1001         WHENEQ    '830'
     C                   MOVE      'SAE'         WSIMEX
     C     M1001         WHENEQ    'TEI'
     C                   MOVE      'TEI'         WSIMEX
     C     M1001         WHENEQ    'TET'
     C                   MOVE      'TEE'         WSIMEX
     C                   OTHER
     C                   MOVE      '***'         WSIMEX
     C                   ENDSL
     C                   MOVE      M1N07         X1N07
     C                   Z-ADD     MDT           XDT
     C                   MOVEL     MTM           XTM
     C                   Z-ADD     0             YDT
     C                   Z-ADD     0             YTM
     C                   Z-ADD     0             ÅTIM
     C                   Z-ADD     0             ÅMIN
      *________________
      * Lete etter svar
      *""""""""""""""""
     C                   Z-ADD     M0068A        W0068A
     C                   Z-ADD     M0068B        W0068B
     C                   Z-ADD     M0068C        W0068C
     C                   MOVE      'R'           WSR
     C                   EVAL      WMN = MMN + 1
     C                   ADD       1             WMN
     C                   MOVE      '0'           *IN20
     C     KEYB          SETLL     EDIMH
     C     W0068A        READE     EDIMH                                  20
     C                   DOW       *IN20 = '0'
     C                   IF        MSR = 'S'
     C                   LEAVE
     C                   END
     C     M0068B        IFEQ      W0068B
     C     M0068C        ANDEQ     W0068C
     C     MMN           ANDGT     WMN
      *
     C                   SELECT
     C                   WHEN      ZTYPE1 = '007'
     C     M1225         CABEQ     '008'         XINGEN
     C     M1225         CABEQ     'CTL'         XINGEN
     C                   WHEN      ZTYPE1 = '015'
     C     M1225         CABEQ     '016'         XINGEN
     C     M1225         CABEQ     'CTL'         XINGEN
     C                   ENDSL
     C                   SELECT
     C                   WHEN      ZTYPE2 = '007'
     C     M1225         CABEQ     '008'         XINGEN
     C     M1225         CABEQ     'CTL'         XINGEN
     C                   WHEN      ZTYPE2 = '015'
     C     M1225         CABEQ     '016'         XINGEN
     C     M1225         CABEQ     'CTL'         XINGEN
     C                   ENDSL
     C                   SELECT
     C                   WHEN      ZTYPE3 = '007'
     C     M1225         CABEQ     '008'         XINGEN
     C     M1225         CABEQ     'CTL'         XINGEN
     C                   WHEN      ZTYPE3 = '015'
     C     M1225         CABEQ     '016'         XINGEN
     C     M1225         CABEQ     'CTL'         XINGEN
     C                   ENDSL
      *
     C                   MOVE      M1N07         Y1N07
     C                   Z-ADD     MDT           YDT
     C                   MOVEL     MTM           YTM
      *__________________
      * Regne ut svarstid
      *""""""""""""""""""
     C     YDTÅ          SUB       XDTÅ          ÅDTÅ
     C     ÅDTÅ          MULT      365           ÅDGR
     C     XDTM          LOOKUP    TABA          TABB                     21
     C   21              SUB       TABB          ÅDGR
     C                   SUB       XDTD          ÅDGR
     C     YDTM          LOOKUP    TABA          TABB                     21
     C   21              ADD       TABB          ÅDGR
     C                   ADD       YDTD          ÅDGR
     C                   SUB       XTMH          ÅTIM
     C                   ADD       YTMH          ÅTIM
     C     YTMM          SUB       XTMM          ÅMIN                   21
     C   21              SUB       1             ÅTIM
     C   21              ADD       60            ÅMIN
     C                   IF        ÅTIM < 0
     C                   SUB       1             ÅDGR
     C                   ADD       24            ÅTIM
     C                   END
      *____________
      * tilføy post
      *""""""""""""
     C                   ADD       1             ZRECNO
     C                   WRITE     TVI70DB
     C                   END
     C     W0068A        READE     EDIMH                                  20
     C                   ENDDO
     C     XINGEN        TAG
     C                   ENDSR
** TABA/TABB, HVOR MANGE DAGER FRA NYTTÅR ?
01000
02031
03059
04090
05120
06151
07181
08212
09243
10273
11304
12334
