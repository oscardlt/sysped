     FLLHEAD01  IF   E           K DISK    RENAME(LLHEADR:LLHEADR01)
     FLLHEAD15  UF   E           K DISK
     FLLLINE    UF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLAGJFEU  IF   E           K DISK
     FLLLISF    IF   E           K DISK
     FLLMRF     IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FDKTVK     IF   E           K DISK
     FDKIA      UF   E           K DISK
     FDKIH      UF A E           K DISK
     FDKIV      O  A E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     D UAN             S              7    DIM(90)
     D UMN             S              7    DIM(90)
     D FA              S             17    DIM(900)
     D DT              S              8  0 DIM(900)
     D VK              S              3    DIM(900)
     D BL              S             11  2 DIM(900)
     D FN1             S             17    DIM(100)
     D FN2             S              8  0 DIM(100)
      *
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UDSAVD                15     18  0
     D  UDSSG                 19     21
      *
     D                 DS
     D  DKIH_SYAV              1      4  0
     D  DKIH_SYOP              5     11  0
     D  DKIH_SYSG             12     14
     D  X14S                   1     14
     D                 DS
     D  VAFAR                  1     10
     D  VAVAL                  1      3
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   Z-ADD     0             XN                3 0
     C                   MOVE      *BLANKS       UAN
     C                   MOVE      *BLANKS       UMN
     C                   MOVE      *ZEROS        XANTL             3 0
     C     LLHEAD01K     SETLL     LLHEAD01
     C     LLHEAD01K     READE     LLHEAD01                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   EXSR      FILTER
     C                   IF        XANTL > 250                                   2<-B
     C                   GOTO      START2
     C                   END                                                     2<-E
     C                   IF        OMIT <> 'J'                                   2<-B
     C                   ADD       1             XN
     C                   MOVE      LHALN         UAN(XN)
     C                   MOVE      LHMN          UMN(XN)
     C                   IF        XN = 90                                        3<-B
     C                   LEAVE
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     LLHEAD01K     READE     LLHEAD01                               33
     C  N33              ENDDO                                                  1<-E
     C                   MOVE      'J'           XFERDIG
      *
     C     START2        TAG
     C                   IF        UAN(1) <> *BLANKS                            1<-B
     C                   MOVE      UMN(1)        LLMN
     C     LLAGJD02K2    SETLL     LLAGJD02
     C     START3        TAG
     C     LLAGJD02K2    READE(N)  LLAGJD02                               33
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C*                  IF        %SUBST(VALLOK:1:5) <> 'TOLD'                  2<-B
     C                   IF        %SUBST(VALLOK:1:5) <> 'KTTL'                  2<-B
     C                   GOTO      START3
     C                   END                                                     2<-E
     C     LLMRFK        CHAIN     LLMRF                              33
      *
     C                   Z-ADD     1             DKIH_SYAV
     C     DKIH_SYAV     CHAIN     DKIA                               33
     C                   ADD       1             DKIA_SYOP
     C                   UPDATE    DKIAR
      *
      * FORTOLLING HEADING
jov  c                   move      *zeros        XLI                            Nullstill linjetell.
     C                   EVAL      DKIH_SYOP = DKIA_SYOP
     C                   EVAL      DKIH_SYSG = UDSSG
     C                   EVAL      DKIH_SYDT = WDT08
     C                   EVAL      DKIH_A    = '4700'
     C                   EVAL      DKIH_DTM1 = LHTDT * 10000
     C                   EVAL      DKIH_AJOU = '1'
     C                   EVAL      DKIH_R011 = 'IM'
     C                   EVAL      DKIH_R012 = 'A'
     C                   EVAL      DKIH_AART = '04'
     C                   IF        MRLNA = *BLANKS OR MRLNA = 'Unknown'
     C                   EVAL      DKIH_02B  = 'KINGSLAND CHINA OFFICE'
     C                   EVAL      DKIH_02C  = 'ROOM 609'
     C                   EVAL      DKIH_02D  = '-'
     C                   EVAL      DKIH_02E  = 'WENHUI ROAD'
     C                   EVAL      DKIH_02F  = 'CN'
     C                   ELSE
     C                   EVAL      DKIH_02B  = MRLNA
     C                   EVAL      DKIH_02C  = MRLAD1
     C                   EVAL      DKIH_02D  = '-'
     C                   EVAL      DKIH_02E  = MRLAD2
     C                   EVAL      DKIH_02F  = MRLAD3
     C                   END
JOV   * skal egentig vær 'diverse avsendere'når det er flere, fra llmrf nå det er kun EN..
JOV   * landkode bør vel være første og beste - nå hardkodet CN..
JOV  C                   EVAL      DKIH_02B  = 'Diverse avsendere'
JOV  C                   EVAL      DKIH_02C  = 'Diverse '
JOV  C                   EVAL      DKIH_02D  = '-'
JOV  C                   EVAL      DKIH_02E  = '.'
JOV  C                   EVAL      DKIH_02F  = 'CN'
      *
     C                   EVAL      DKIH_07   = MRGN
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      DKIH_08A  = EORI
     C                   EVAL      DKIH_08B  = KNAVN
     C                   EVAL      DKIH_08C  = ADR1
     C                   IF        POSTNR = 0                                    2<-B
     C                   EVAL      DKIH_08D  = SYPOGE
     C                   ELSE                                                    2
     C                   MOVEL     POSTNR        DKIH_08D
     C                   END                                                     2<-E
     C                   EVAL      DKIH_08E  = ADR3
     C                   IF        SYLAND = *BLANKS                              2<-B
     C                   EVAL      DKIH_08F  = FILAND
     C                   ELSE                                                    2
     C                   EVAL      DKIH_08F  = SYLAND
     C                   END                                                     2<-E
     C                   EVAL      DKIH_14A  = DKIA_14A
     C                   EVAL      DKIH_14B  = DKIA_14B
     C                   IF        %SUBST(MRTRNR:17:2) = *BLANKS                 2<-B
     C                   EVAL      DKIH_15   = 'CN'
     C                   ELSE                                                    2
     C                   EVAL      DKIH_15   = %SUBST(MRTRNR:17:2)
     C                   END                                                     2<-E
     C*                  EVAL      DKIH_181  = 'BCS6026'
     C*                  EVAL      DKIH_18A  = 'DE'
     C*                  EVAL      DKIH_211  = 'DE'
     C*                  EVAL      DKIH_25   = '9'
     C                   EVAL      DKIH_25   = '3'
     C                   EVAL      DKIH_26   = '3'
JOV  C                   EVAL      DKIH_S29  = 'H'
     C                   EVAL      DKIH_49   = 'A470303DK'
      *
     C                   WRITE     DKIHR
      * FINN VALUTAKODE
     C     1             DO        90            XN                              2<-B
     C                   IF        UAN(XN) = *BLANKS                              3<-B
     C                   LEAVE
     C                   END                                                      3<-E
     C                   MOVE      UAN(XN)       LHALN
     C     LLHEAD15K     CHAIN(N)  LLHEAD15                           33
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        LLWNT0 <> 0                                     4<-B
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                     5<-B
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C*                  IF        %SUBST(VALLOK:1:5) = 'TOLD'                       6<-B
     C                   IF        %SUBST(VALLOK:1:5) = 'KTTL'                       6<-B
     C                   SELECT                                                       7<-B
     C                   WHEN      DKVK_KD = *BLANKS                                  7
     C                   IF        VAVAL   = *BLANKS                                   8<-B
     C                   EVAL      DKVK_KD = 'DKK'
     C                   ELSE                                                          8
     C                   EVAL      DKVK_KD = VAVAL
     C                   END                                                           8<-E
     C                   WHEN      ((DKVK_KD <> VAVAL) AND (VAVAL <> *BLANKS))        7
     C                   EVAL      DKVK_KD = 'DKK'
     C                   ENDSL                                                        7<-E
      * HENT FAKTURANR
     C     VAMN          CHAIN     LLLISF                             33
     C                   DOW       *IN33 = *OFF                                       7<-B
     C                   IF        LIKD = 'FAK'                                        8<-B
     C                   EVAL      XX_28B  = LITXT
     C                   IF        %SUBST(LITXT:28:8) = *BLANKS                         9<-B
     C                   EVAL      XX_28C  = WDT08
     C                   ELSE                                                           9
     C                   MOVE      LITXT         XX_28C
     C                   END                                                            9<-E
     C                   LEAVE
     C                   END                                                           8<-E
     C     VAMN          READE     LLLISF                                 33
     C                   ENDDO                                                        7<-E
     C     1             DO        100           XN2               3 0                7<-B
     C                   SELECT                                                        8<-B
     C                   WHEN      FN1(XN2) = *BLANKS                                  8
     C                   EVAL      FN1(XN2) = XX_28B
     C                   EVAL      FN2(XN2) = XX_28C
     C                   LEAVE
     C                   WHEN      XX_28B = FN1(XN2) AND XX_28C = FN2(XN2)             8
     C                   LEAVE
     C                   ENDSL                                                         8<-E
     C                   ENDDO                                                        7<-E
      *
     C                   END                                                         6<-E
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   ENDDO                                                      5<-E
     C                   END                                                       4<-E
     C     LHMN          READE(N)  LLLINE                                 33
     C                   ENDDO                                                    3<-E
      *
     C                   ENDDO                                                   2<-E
      *
      * OPPDATER ALLE VARELINJER
     C     1             DO        90            XN                3 0           2<-B
     C                   IF        UAN(XN) = *BLANKS                              3<-B
     C                   LEAVE
     C                   END                                                      3<-E
     C                   MOVE      UAN(XN)       LHALN
     C     LLHEAD15K     CHAIN     LLHEAD15                           33
     C*                  ADD       LHKLL         XNTK
     C*                  ADD       LHWVEK        XVKB
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        LLWNT0 <> 0                                     4<-B
     C                   EXSR      OPPDLIN
     C                   MOVE      'SAD'         LLHK
     C                   END                                                       4<-E
     C                   UPDATE    LLLINER
     C     LHMN          READE     LLLINE                                 33
     C                   ENDDO                                                    3<-E
      *
     C                   MOVE      'Z'           LHSTA
     C                   MOVE      DKIH_SYAV     LHAVD
     C                   MOVE      DKIH_SYOP     LHOPD
     C                   UPDATE    LLHEADR
     C                   ENDDO                                                   2<-E
      *
     C     DKIHK         CHAIN     DKIH                               33
     C*                  EVAL      DKIH_28B  = XX_28B
     C*                  EVAL      DKIH_28C  = XX_28C
     C                   EVAL      DKIH_28A  = 'N380'
     C                   EVAL      DKIH_28B  = FN1(1)
     C                   EVAL      DKIH_28C  = FN2(1)
     C                   EVAL      DKIH_222  = XX_222
     C                   EVAL      DKIH_12   = XX_12
     C                   EVAL      DKIH_12E  = XX_12E
     C                   MOVE      XX_06         DKIH_06
     C                   MOVE      XX_BRUT       DKIH_BRUT
     C                   EVAL      DKIH_221  = DKVK_KD
     C     DKTVKK        SETGT     DKTVK
     C     DKVK_KD       READPE    DKTVK                                  33
     C                   EVAL      DKIH_221B = DKVK_KRS
     C                   EVAL      DKIH_221C = DKVK_OMR
     C                   UPDATE    DKIHR
     C                   END                                                    1<-E
      *
     C                   IF        XFERDIG = 'N'                                1<-B
      * FLERE PLUKKLISTER PGA. FLERE ENN 250 VARELINJER
     C                   GOTO      START
     C                   END                                                    1<-E
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     FILTER        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      'N'           OMIT              1
      *
jov  C                   IF        LHALN = 0 or LHST1 <> 'K'
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     KODTS2K       CHAIN     KODTS2                             33
     C  N33              IF        KS2PRE <> 'A' AND KS2PRE <> 'B'
     C                   EVAL      *IN33 = *ON
     C                   END
     C                   IF        *IN33
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C                   MOVE      'J'           OMIT
     C     LLAGJD02K2    SETLL     LLAGJD02
     C     LLAGJD02K2    READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C*                  IF        %SUBST(VALLOK:1:5) = 'TOLD'
     C                   IF        %SUBST(VALLOK:1:5) = 'KTTL'
     C                   ADD       1             XANTL
     C                   MOVE      'N'           OMIT
     C                   END
     C     LLAGJD02K2    READE     LLAGJD02                               33
     C                   ENDDO
      *
     C     UTFILT        ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPPDLIN       BEGSR
      *////////////////////////////////////////////////////////////
     C                   CLEAR                   DKIVR
     C                   EVAL      DKIV_SYAV = DKIH_SYAV
     C                   EVAL      DKIV_SYOP = DKIH_SYOP
      *
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C*                  IF        %SUBST(VALLOK:1:5) = 'TOLD'
     C                   IF        %SUBST(VALLOK:1:5) = 'KTTL'
      *
     C     LLAGJFEUK     READE     LLAGJFEU                               33
     C                   IF        *IN33
jov  C                   EVAL      VAOPLK = *blanks
     C                   EVAL      VAFVUE = *BLANKS
     C                   EVAL      VAFRUE = 0
     C                   EVAL      VAFVIE = *BLANKS
     C                   EVAL      VAFRIE = 0
     C                   END
      *
     C                   EVAL      XLI       = XLI + 1
     C                   EVAL      DKIV_SYLI = XLI
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      DKIH_SYAV     VDAVD
     C                   MOVE      DKIH_SYOP     VDOPD
     C                   EVAL      VDTEXT = LHBNA
     C                   EVAL      VDST   = 'R'
     C                   EVAL      VDBUSE = 'DK-FORTOL'
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDST   = 'R'
     C                   UPDATE    LLAGJDR
      *
     C                   EVAL      DKIV_311A = 'ADR.'
     C                   EVAL      DKIV_313A = LLWNT0
     C                   EVAL      DKIV_412  = LLWNT0
     C                   EVAL      DKIV_314A = 'PK'
     C                   EVAL      DKIV_315A = LLVB
     C                   EVAL      DKIV_32   = DKIV_SYLI
     C                   EVAL      DKIV_331  = *BLANKS
     C     VAMN          CHAIN     LLLISF                             33
     C                   DOW       *IN33 = *OFF
     C                   IF        LIKD = 'TAR'
     C                   EVAL      DKIV_331  = LITXT
     C                   LEAVE
     C                   END
     C     VAMN          READE     LLLISF                                 33
     C                   ENDDO
     C                   IF        %SUBST(MRTRNR:17:2) = *BLANKS
     C                   EVAL      DKIV_34   = 'CN'
     C                   ELSE
     C                   EVAL      DKIV_34   = %SUBST(MRTRNR:17:2)
     C                   END
jov  c                   IF        VAOPLK <> *blanks
jov  C                   EVAL      DKIV_34   = VAOPLK
jov  c                   endif
     C                   EVAL      DKIV_35   = LLWNT0 * VAVKT0
     C                   EVAL      DKIV_36   = '100'
jov  C*                  IF        MRTRNR = *BLANKS
JOV  C*                  EVAL      DKIV_37   = '7100099'
JOV  C*                  ELSE
JOV  C*                  EVAL      DKIV_37   = %SUBST(MRTRNR:1:7)
JOV  C*                  END
jov  C*                  IF        VAVKF0 = 0
JOV  C*                  EVAL      DKIV_38   = DKIV_35 * 0,9
JOV  C*                  ELSE
JOV  C*                  EVAL      DKIV_38   = LLWNT0 * VAVKF0
JOV  C*                  END
JOV  C                   EVAL      DKIV_37   = '4071000'
YBC  C*                  EVAL(H)   DKIV_38   = VAVKT0 * VASALD                  Netto Vkt
YBC  C                   EVAL(H)   DKIV_38   = VAVKT0 * LLWNT0                  Netto Vkt
JOV  C                   IF        DKIV_38 < 1
JOV  C                   EVAL      DKIV_38   = 1
JOV  C                   EVAL      DKIV_35   = 1
JOV  C                   ELSE
YBC  C*                  EVAL(H)   DKIV_35   = VAVKF0 * VASALD                  Brutto Vkt
YBC  C                   EVAL(H)   DKIV_35   = VAVKF0 * LLWNT0                  Brutto Vkt
JOV  C                   IF        DKIV_35 = 0
JOV  C                   EVAL(H)   DKIV_35   = DKIV_38 * 1,1
JOV  C                   END
JOV  C                   END
     C* HVOR KAN HENTE OPPLYSNING TIL RUBR. 40?
     C                   EVAL      DKIV_401A = 'Y'
     C                   EVAL      DKIV_402A = 'CLE'
JOV  C*                  EVAL      DKIV_403A = '20140317/20141077486'
JOV  C                   EVAL      DKIV_403A = MRGN
      * Fakturanr på DKIV_403B
     C     VAMN          CHAIN     LLLISF                             33
     C                   DOW       *IN33 = *OFF
     C                   IF        LIKD = 'FAK'
     C                   EVAL      DKIV_403B = %SUBST(LITXT:1:20)
     C                   LEAVE
     C                   END
     C     VAMN          READE     LLLISF                                 33
     C                   ENDDO
     C                   EVAL      DKIV_X02  = DKIV_403B
      *
     C                   EVAL      DKIV_411  = 'CEN'
JOV  C*                  EVAL      DKIV_412  = 25
     C                   IF        VAVAL = DKVK_KD
     C                   EVAL(H)   DKIV_42   = VAVERD * LLWNT0
     C                   ELSE
     C     DKTVKK2       SETGT     DKTVK
     C     VAVAL         READPE    DKTVK                                  33
     C                   EVAL(H)   DKIV_42   = VAVERD*LLWNT0*DKVK_KRS/DKVK_OMR
     C                   END
      *
      * Beregn frakt
     C                   IF        VAFRUE = 0
     C                   EVAL      DKIV_T17A = 0
     C                   ELSE
     C     DKTVKKUE      SETGT     DKTVK
     C     VAFVUE        READPE    DKTVK                                  33
     C                   EVAL(H)   DKIV_T17A=VAFRUE*LLWNT0*DKVK_KRS/DKVK_OMR
     C                   END
     C                   EVAL      DKIV_T18  = DKIV_T17A
     C                   IF        VAFRIE = 0 OR
     C                             DKIH_201 = 'DDU' OR DKIH_201 = 'DDP'
     C                   EVAL      DKIV_T19  = 0
     C                   ELSE
     C     DKTVKKIE      SETGT     DKTVK
     C     VAFVIE        READPE    DKTVK                                  33
     C                   EVAL(H)   DKIV_T19=VAFRIE*LLWNT0*DKVK_KRS/DKVK_OMR
     C                   END
     C                   EVAL      DKIV_T23  = DKIV_T19
      *
      * Beregn statistisk verdi
     C     DKTVKK        SETGT     DKTVK
     C     DKVK_KD       READPE    DKTVK                                  33
     C                   EVAL(H)   DKIV_T12  = DKIV_42 * DKVK_KRS / DKVK_OMR
     C                   EVAL      DKIV_T24  = DKIV_T12 + DKIV_T18 - DKIV_T23
      *
     C                   EVAL      DKIV_28B  = LHPAK
     C                   WRITE     DKIVR
     C                   EVAL(H)   XX_222  = XX_222  + DKIV_42
     C                   EVAL      XX_12   = XX_12   + DKIV_T23
     C                   EVAL      XX_12E  = XX_12E  + DKIV_T18
     C                   EVAL      XX_06   = XX_06   + DKIV_313A
     C                   EVAL      XX_BRUT = XX_BRUT + DKIV_35
      *
     C                   END
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UPNR              8
      *
     C     DKIHK         KLIST
     C                   KFLD                    DKIH_SYAV
     C                   KFLD                    DKIH_SYOP
     C     DKTVKK        KLIST
     C                   KFLD                    DKVK_KD
     C                   KFLD                    XX_DTS
     C     DKTVKK2       KLIST
     C                   KFLD                    VAVAL
     C                   KFLD                    XX_DTS
     C     DKTVKKUE      KLIST
     C                   KFLD                    VAFVUE
     C                   KFLD                    XX_DTS
     C     DKTVKKIE      KLIST
     C                   KFLD                    VAFVIE
     C                   KFLD                    XX_DTS
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LHPNR
     C     LLHEAD01K     KLIST
     C                   KFLD                    XX_LHSTA
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C     LLHEAD15K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHALN
     C     LLAGJL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLAGJFEUK     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMN
     C     LLAGJD02K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGJD02K2    KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHMN
     C     LLMRFK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMRNR
     C     KODTS2K       KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    LHBLK
      *
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C     *LIKE         DEFINE    LHSTA         XX_LHSTA
     C     *LIKE         DEFINE    DKIH_28B      XX_28B
     C     *LIKE         DEFINE    DKIH_28C      XX_28C
     C     *LIKE         DEFINE    DKIH_222      XX_222
     C     *LIKE         DEFINE    DKIH_12       XX_12
     C     *LIKE         DEFINE    DKIH_12E      XX_12E
     C     *LIKE         DEFINE    DKIH_06       XX_06
     C     *LIKE         DEFINE    DKIH_BRUT     XX_BRUT
     C     *LIKE         DEFINE    DKIH_221      XVAL
     C     *LIKE         DEFINE    DKIV_SYLI     XLI
     C     *LIKE         DEFINE    DKVK_DTS      XX_DTS
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C                   Z-ADD     1             UDSAVD
     C                   MOVE      'CB '         UDSSG
     C                   OUT       SAVUDS
     C                   MOVE      UPNR          LHPNR
     C                   MOVE      ' '           XX_LHSTA
     C                   EVAL      KS2UNI = 'S2'
     C                   EVAL      XX_DTS = 99999999
     C                   MOVE      'N'           XFERDIG           1
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               8
     C                   MOVE      WDT           WDT08             8 0
      *
     C                   ENDSR
      *
