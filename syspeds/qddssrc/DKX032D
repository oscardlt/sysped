     A*%%TS  SD  20140619  145153  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 11 42 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '4')
     A  28                                  DSPATR(PR)
     A            TDAK           4A  O  6  5TEXT('ADGANGSKODE')
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20140619  145153  CB          REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0006)
     A  *DS4                                SFLSIZ(0006)
     A  *DS3                                SFLPAG(0005)
     A  *DS4                                SFLPAG(0005)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Forny')
     A                                      CA12(12 'Forrige')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 37SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/DKX032R'
     A                                      COLOR(BLU)
     A                                  1 17'Underhold tilgangskode'
     A                                      COLOR(WHT)
     A                                  2  2'Garantinr.'
     A            UGNR          24A  O  2 13COLOR(WHT)
     A N28                              3  2'Vælg opsjon:  1=Oprette  4=Slette'
     A                                      COLOR(BLU)
     A                                  4  2'Op Tilgangskode'
     A                                      COLOR(WHT)
     A N28        W2NR           1A  B  5  2DSPATR(UL)
     A                                      COLOR(YLW)
     A N28        W2AK           4A  B  5  5TEXT('Navn H.ansvalig')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A*
     A          R BUNN
     A*%%TS  SD  20140619  145153  CB          REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      SLNO(11)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3=Afslut   F5=Oppdater   F12=Annu-
     A                                      ler'
     A                                      COLOR(BLU)
     A  30        MSGTXT        40A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R NYWRK
     A*%%TS  SD  20140619  145153  CB          REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Forny')
     A                                      CA12(12 'Forrige')
     A                                  1  2'SYSPED/DKX032R'
     A                                      COLOR(BLU)
     A                                  1 17'Underhold tilgangskode'
     A                                      COLOR(WHT)
     A                                  2  2'Garantinr.'
     A            UGNR          24A  O  2 13COLOR(WHT)
     A N28                              3  2'Vælg Opsjon:  1=Oprette  4=Slette'
     A                                      COLOR(BLU)
     A                                  4  2'Op Tilgangskode'
     A                                      COLOR(WHT)
     A N28        W2NR           1A  B  5  2DSPATR(UL)
     A                                      COLOR(YLW)
     A N28        W2AK           4A  B  5  5TEXT('Navn H.ansvalig')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A N30                             11  2'F3=Afslut   F5=Oppdater   F12=Annu-
     A                                      ler'
     A                                      COLOR(BLU)
     A  30        MSGTXT        40A  O 11  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
