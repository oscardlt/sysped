      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE27RU MODULE(*LIBL/TJGE27RU *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKOSBU     UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FKOSBU1    if   E             DISK    USROPN
     F                                     RENAME(kosbur:kosbur1)
     FKOSTT     UF A E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
     FLEVEL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D BNR             S              7  0
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬bnr¬ : ¬'+%trim(%editc(bnr:'Z'))+'¬, '
          +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
          +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"budgetLineUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KOSBU
     C  N50              CLOSE     KOSTT
     C  N50              CLOSE     LEVEL01
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     TURER14

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('BNR')
     C                   EVAL      BNR        = c2n2(WA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
      *
     C                   EVAL      WA       = zhbgetvar('bupCc  ')
     C                   EVAL      bupCc   = c2n2(WA)
OBS   * input = bupAr  filvariabel= BUPÅR
OBS  C                   EVAL      WA       = zhbgetvar('bupAr  ')
OBS  C                   EVAL      bupÅr   = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('bupMn  ')
     C                   EVAL      bupMn   = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('butnr  ')
     C                   EVAL      butnr   = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('bulnr  ')
     C                   EVAL      bulnr   = c2n2(WA)
     C                   EVAL      buval    = zhbgetvar('buval  ')
     C                   EVAL      WA       = zhbgetvar('bubl   ')
     C                   EVAL      bubl    = c2n2(WA)
     C                   EVAL      buvk     = zhbgetvar('buvk   ')
     C                   EVAL      WA       = zhbgetvar('butavd ')
     C                   EVAL      butavd  = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('butunr ')
     C                   EVAL      butunr  = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('buoavd ')
     C                   EVAL      buoavd  = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('buopd  ')
     C                   EVAL      buopd   = c2n2(WA)
     C                   EVAL      bublst   = zhbgetvar('bublst ')
     C                   EVAL      butype   = zhbgetvar('butype ')
     C                   EVAL      butxt    = zhbgetvar('butxt  ')
     C     LO:UPC        XLATE     butxt         butxt
     C                   EVAL      bubilk   = zhbgetvar('bubilk ')
     C     LO:UPC        XLATE     bubilk        bubilk
     C                   EVAL      bubiln   = zhbgetvar('bubiln ')
     C     LO:UPC        XLATE     bubiln        bubiln
     C                   EVAL      WA       = zhbgetvar('bufedt ')
     C                   EVAL      bufedt  = c2n2(WA)
     C                   EVAL      bufer    = zhbgetvar('bufer  ')
     C                   EVAL      WA       = zhbgetvar('bubnr2 ')
     C                   EVAL      bubnr2  = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('bubnr3 ')
     C                   EVAL      bubnr3  = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('bulnr3 ')
     C                   EVAL      bulnr3  = c2n2(WA)
     C                   EVAL      WA       = zhbgetvar('bublfd ')
     C                   EVAL      bublfd  = c2n2(WA)
     C                   EVAL      bukdm    = zhbgetvar('bukdm  ')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     HeaKey        KLIST
     C                   KFLD                    BUOAVD
     C                   KFLD                    BUOPD
     C     LEVEL01K      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BULNR
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KOSBU
     C  N50              OPEN      KOSTT
     C  N50              OPEN      LEVEL01
     C  N50              OPEN      HEADF
     C  N50              OPEN      TURER14
      * dummy for å få feltnavn som arbeids/inputvariable
     C     *IN50         IFEQ      *OFF
     C     *IN50         ANDEQ     *ON
     C                   OPEN      KOSBU1
     C                   READ      KOSBU1
     C                   ENDIF
      *
     C                   move      *zeros        HEDTOP
      *
     c                   endsr
      *
      *********************************************************
     C     TEST          BEGSR
      *********************************************************
     C     WMODE         IFNE      'A'
     C     BNR           CHAIN     KOSBU                              55
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Finner ikke busjettpost ( '
     c                                   + %trim(%editc(BNR:'3')) +' )'
     c                   goto      UtTest
     C                   ENDIF
     C                   ENDIF
      * div tester som er felles for update/add
     C     WMODE         IFEQ      'A'
     C     WMODE         OREQ      'U'
      * leverandør
     C     BULNR         ifne      *zeros
     c     LEVEL01K      CHAIN     LEVEL01                            33
     C                   IF        *IN33 = *on
     C                   eval      Error = 'Ugyldig leverandørnummer'
     c                   goto      UtTest
     C                   ENDIF
     C                   ENDIF
      * avd/opd
     C     BUOPD         ifne      *zeros
     c     HEAKEY        CHAIN     HEADF                              33
     C                   IF        *IN33 = *on
     C                   eval      Error = 'Ugyldig avd/oppdragsnr'
     c                   goto      UtTest
     C                   ELSE
     c                   IF        BUTUNR=0 and HEPRO <> 0
     c     HEPRO         CHAIN     TURER14                            33
     c  N33              move      TUAVD         BUTAVD
     c  N33              move      TUPRO         BUTUNR
     C                   ENDIF
      *     periode fra oppdrak kun om tur mangler
     C                   IF        BUPMN=0 and BUTUNR=0                          2B
     C                   MOVE      HEDTM         BUPMN
     C                   MOVE      HEDTÅ         BUPÅR
     C                   MOVEL     HEDTÅ         BUPCC
     C                   END                                                     2E
     C                   ENDIF
     C                   ENDIF
      * Turnr
     c                   IF        BUTUNR<>0
     c     BUTUNR        CHAIN     TURER14                            33
     C                   IF        *IN33 = *on
     C                   eval      Error = 'Ugyldig turnummer'
     c                   goto      UtTest
     C                   ELSE
     c                   IF        BUPMN=0
     C                   MOVE      TURACC        BUPCC
     C                   MOVE      TURAAR        BUPÅR
     C                   MOVE      TURMND        BUPMN
     C                   ENDIF
     C     BUBILN        IFEQ      *BLANKS
     C     BUTNR         ANDEQ     *ZEROS
     C                   MOVE      TUBILN        BUBILN
     C                   END
     C     BUBILK        IFEQ      *BLANKS
     C     BUTNR         ANDEQ     *ZEROS
     C                   MOVE      TUBILK        BUBILK
     C                   END
      * VED TURNR SOM ER AVREGNET, KODE J=JUSTER
     C     BUBLST        IFEQ      'T'
     C     TUAVRE        ANDEQ     'X'
     C                   MOVE      'J'           BUBLST
     C                   END
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      * MODE A=Add   U=Update   D=Delete
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C     WMODE         OREQ      'U'
     C                   IF        XBUST <> ' ' AND WMODE = 'U'
     C                   eval      Error = 'Budsjettposten har feil status for '
     c                                   + 'endring'
     c                   goto      UtTest
     C                   ENDIF
     C                   EXSR      SRAU
     C     WMODE         WHENEQ    'D'
     C                   IF        XBUST <> ' ' AND XBUST <> 'S'
     C                   eval      Error = 'Budsjettposten har feil status for '
     c                                   + 'sletting (eller oppheving av slett)'
     c                   goto      UtTest
     C                   ENDIF
     C                   EXSR      SRD
     C                   OTHER
     C                   eval      Error = 'Feil Mode'
     C                   ENDSL
      *
     C     UtTest        ENDSR
      *
      *********************************************************
     C     SRAU          BEGSR
      *********************************************************
      * Add/update
     C     WMODE         ifeq      'A'
     c                   eval      KTTYP  = 'Å'
     C     KTTYP         CHAIN     KOSTT                              33
     C   33              Z-ADD     1000000       KTNR
     C   33              WRITE     KOSTTR
     C  N33              ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
     C                   EVAL      Xbubnr   =  KTNR
     C                   EVAL      Xbust    = ' '
     c                   endif
     C                   EVAL      XbupCc   = bupCc
     C                   EVAL      XbupÅr   = bupÅr
     C                   EVAL      XbupMn   = bupMn
     C                   EVAL      Xbutnr   = butnr
     C                   EVAL      Xbulnr   = bulnr
     C                   EVAL      Xbuval   = buval
     C                   EVAL      Xbubl    = bubl
     C                   EVAL      Xbuvk    = buvk
     C                   EVAL      Xbutavd  = butavd
     C                   EVAL      Xbutunr  = butunr
     C                   EVAL      Xbuoavd  = buoavd
     C                   EVAL      Xbuopd   = buopd
     C                   EVAL      Xbublst  = bublst
     C                   EVAL      Xbutype  = butype
     C                   EVAL      Xbutxt   = butxt
     C                   EVAL      Xbubilk  = bubilk
     C                   EVAL      Xbubiln  = bubiln
     C                   EVAL      Xbufedt  = bufedt
     C                   EVAL      Xbufer   = bufer
     C                   EVAL      Xbubnr2  = bubnr2
     C                   EVAL      Xbubnr3  = bubnr3
     C                   EVAL      Xbulnr3  = bulnr3
     C                   EVAL      Xbublfd  = bublfd
     C                   EVAL      Xbukdm   = bukdm

     C     WMODE         ifeq      'A'
     C                   WRITE     KOSBUR
     C                   else
     C                   UPDATE    KOSBUR
     C                   end

     C                   ENDSR
      *
      *********************************************************
     C     SRD           BEGSR
      *********************************************************
      * Delet
     c                   if        XBUST=' '
     c                   move      'S'           XBUST
     c                   else
     c                   move      ' '           XBUST
     c                   endif
     C                   UPDATE    KOSBUR
     C                   ENDSR
