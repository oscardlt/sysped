     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(*DFT 18 75 *NOMSGLIN)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '7' '8' '9')
     A            XLKF           2A  O  6  5
     A            XSDF           5A  O  6  8
     A            XLKT           2A  O  6 14
     A            XSDT           5A  O  6 17
     A            XANTOP         3Y 0O  6 24EDTCDE(4)
     A            XATA0          3Y 0O  6 51EDTCDE(4)
     A            XATD0          3Y 0O  6 55EDTCDE(4)
     A            XETA0          3Y 0O  6 59EDTCDE(4)
     A            XETD0          3Y 0O  6 63EDTCDE(4)
     A            XEX10          3Y 0O  6 67EDTCDE(4)
     A            XEX20          3Y 0O  6 71EDTCDE(4)
     A            XATAD          8Y 0H
     A            XATAK          4Y 0H
     A            XATDD          8Y 0H
     A            XATDK          4Y 0H
     A            XETAD          8Y 0H
     A            XETAK          4Y 0H
     A            XETDD          8Y 0H
     A            XETDK          4Y 0H
     A            XEX1D          8Y 0H
     A            XEX1K          4Y 0H
     A            XEX2D          8Y 0H
     A            XEX2K          4Y 0H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTT03'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 YBC0638 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 YBC0639 *LIBL/SYMSGF)
     A            UPRO2          8  0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21'Bilnr.'
     A            TUBILN        10   O  2 28
     A                                      COLOR(WHT)
     A                                  2 39'Avg:'
     A            WSDT           6  0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUTM           4  0O  2 51EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  2 57'Ank:'
     A            WSDTT          6  0O  2 62EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUTMT          4  0O  2 69EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(073 YBC0640 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 51MSGCON(024 YBC0652 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(073 YBC0641 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'AVSLUTT')
     A                                      SLNO(17)
     A                                      CLRL(02)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        73A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(*DFT 10 65 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTT03'
     A                                      COLOR(BLU)
     A N27                              1 19MSGCON(040 YBC0642 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(040 YBC0643 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2'Fra:'
     A            XLKF           2A  O  2  8COLOR(WHT)
     A            XSDF           5A  O  2 11COLOR(WHT)
     A                                  2 18'Til:'
     A            XLKT           2A  O  2 24COLOR(WHT)
     A            XSDT           5A  O  2 27COLOR(WHT)
     A                                  2 34'Antall oppdrag:'
     A            XANTOP         3Y 0O  2 50EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 30MSGCON(008 YBC0650 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 57MSGCON(008 YBC0650 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 18MSGCON(011 YBC0644 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 30MSGCON(008 YBC0651 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 45MSGCON(011 YBC0644 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 57MSGCON(008 YBC0651 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(010 YBC0633 *LIBL/SYMSGF)
     A                                  5 14'ATA'
     A            WSATAD         6Y 0B  5 18DSPATR(UL)
     A  90
     AON30                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSATAK         4Y 0B  5 25DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            XATA0          3Y 0O  5 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5 41'ATD'
     A            WSATDD         6Y 0B  5 45DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSATDK         4Y 0B  5 52DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            XATD0          3Y 0O  5 59EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(010 YBC0630 *LIBL/SYMSGF)
     A                                  6 14'ETA'
     A            WSETAD         6Y 0B  6 18DSPATR(UL)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSETAK         4Y 0B  6 25DSPATR(UL)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            XETA0          3Y 0O  6 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  6 41'ETD'
     A            WSETDD         6Y 0B  6 45DSPATR(UL)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSETDK         4Y 0B  6 52DSPATR(UL)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            XETD0          3Y 0O  6 59EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(010 YBC0634 *LIBL/SYMSGF)
     A                                  7 14'EX1'
     A            WSEX1D         6Y 0B  7 18DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSEX1K         4Y 0B  7 25DSPATR(UL)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            XEX10          3Y 0O  7 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7 41'EX2'
     A            WSEX2D         6Y 0B  7 45DSPATR(UL)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSEX2K         4Y 0B  7 52DSPATR(UL)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            XEX20          3Y 0O  7 59EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(022 YBC0648 *LIBL/SYMSGF)
     A                                  8 25MSGCON(025 YBC0649 *LIBL/SYMSGF)
     A                                  9  2MSGCON(060 YBC0645 *LIBL/SYMSGF)
     A N30                             10  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTT03'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 YBC0638 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(008 YBC0639 *LIBL/SYMSGF)
     A            UPRO2          8Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21'Bilnr.'
     A            TUBILN        10   O  2 28
     A                                      COLOR(WHT)
     A                                  2 39'Avg:'
     A            WSDT           6  0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUTM           4  0O  2 51EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  2 57'Ank:'
     A            WSDTT          6  0O  2 62EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUTMT          4  0O  2 69EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(073 YBC0640 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 51MSGCON(024 YBC0652 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(073 YBC0641 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 17  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             18  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
