********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 917-MELDING HAR MRN-NR I BGM-SEGMENT
10010 * 02 YBC PTF10 907-MELDING HAR SYSPED-REF I UCM-SEGMENT
10XXX * 03 CB  PTF10 DANSK CONTRL
11XXX * 04 CB  PTF11 DANSK CUSRES
11XXX * 05 CB  PTF11 DANSK NCTS
11XXX * 06 CB  PTF11 NY SVENSK LAYOUT PÅ UTL
11XXX * 07 CB  PTF11 LES FORBI LF
11XXX * 08 CB  PTF11 Tillat lengre ref i UNH
11XXX * 09 CB  PTF11 Rename av Svenske felter i NCTS
********************************************************************
     FEDI85F    IF   E             DISK
     FEDIM5     IF   E           K DISK
     FEDIM6     IF   E           K DISK      RENAME(EDIMR:EDIMR6)
     FEDIS4     IF   E           K DISK
N01  FTRIH05    IF   E           K DISK      PREFIX(NO_)
N01  FTRUH05    IF   E           K DISK      PREFIX(NO_)
S09  F*VXH05    IF   E           K DISK
N09  FSVXH05    IF   E           K DISK      PREFIX(SE_)
S09  F*VNH05    IF   E           K DISK
N09  FSVNH05    IF   E           K DISK      PREFIX(SE_)
N05  FDKXH05    IF   E           K DISK
N05  FDKNH05    IF   E           K DISK
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
     D  AA                     1     80
     D                                     DIM(80)
     D  AADS                   1     80
     D                 DS
     D  AB                     1     80
     D                                     DIM(80)
     D  ABDS                   1     80
     D                 DS
     D  M0065                  1      6
     D  X0065                  1      3
     C*___________
     C* HODELOGIKK
     C*"""""""""""
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  NLR              EXSR      GETSEG
     C   LR              GOTO      SLUTT
     C     SEGNAM        CASEQ     'UNB'         SRUNB
     C                   END
     C     SEGNAM        CASEQ     'UNH'         SRUNH
     C                   END
     C                   GOTO      SEGSTA
     C     SLUTT         TAG
     C*
     C*__________________________________________________________
     C* SR for initiering                                  SRINIT
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C* Parametre fra CL= MESSAGE TYPE OG MESSAGE NA,E
     C     *ENTRY        PLIST
     C                   PARM      W0065         W0065
      * INIT AV ARBEIDSFELT
     C                   MOVE      'N'           OK                1            UNB--> J    elt.
     C                   MOVE      *ZEROS        NUMVRD           15 0          Gener. num felt.
     C                   MOVE      *ZEROS        NUTEGN            1 0          "TEGN" numerisk rep.
     C                   MOVE      *BLANKS       SEGNAM            3            Segment navn
     C                   MOVE      *BLANKS       W0065             6            MSG TYPE
N07  C                   MOVE      X'25'         XLF               1
     C                   READ      EDI85F                                 LR
     C   LR              GOTO      ENDINI
     C                   MOVE      RDATA         ABDS
     C     AB(1)         IFEQ      'ø'
     C     1             DO        2
     C                   READ      EDI85F                                 LR
     C   LR              GOTO      ENDINI
     C                   END
     C                   END
     C*
     C                   MOVE      RDATA         ABDS
     C                   Z-ADD     81            TN                2 0
     C                   MOVE      'NO '         EOF               3             * FLAGG FOR EOF
     C                   MOVE      ' '           TEGN              1
     C                   MOVE      ' '           LOKAHE            1
     C*
     C* Spesielt reserverte tegn defineres her med både grafisk verdi og
     C* med en logisk verdi. Den grafiske verdien testes det mot i rutinen
     C* som henter tegn. Finner denne rutinen at at tegnet er ment som en
     C* spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
     C* (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
     C* innkommende datastroemmen). '?' når brukt som opphevelse foran annet
     C* spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
     C* kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.
     C*
     C* Ved andre standarder for spesialtegn er det bare å bytte verdiene til
     C* de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
     C* Logiske verdier for spesialtegn
     C* LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
     C* HVIS KOMMUNEDATA, INITIERE MED LOGAPP (GÅ RETT PÅ UNB)
     C                   MOVE      LOGAPP        TEGN
     C* LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   BITON     '16'          LOGPLU            1
     C                   BITOFF    '023457'      LOGPLU
     C* LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   BITON     '167'         LOGKOL            1
     C                   BITOFF    '02345'       LOGKOL
     C*
     C     ENDINI        ENDSR
     C*
     C*__________________________________________________________
     C* SR for å hente neste tegn i datastroem             GETCH
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETCH         BEGSR
     C* Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
     C* i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
     C* LR settes når en ved "Toemming" finner at AB-arrayet er tomt.
     C*
     C* "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
     C*Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
     C*    98 ON -> kun grafisk betydning (uforandret)
     C*    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   SETOFF                                           98
     C     STACH         TAG
     C* Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            LR
     C                   GOTO      ENDCH
     C                   END
     C                   MOVE      ABDS          AADS
     C  N99              READ      EDI85F                                 99     *hent ny til AB
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF                             * FLAGG FOR EOF
     C                   Z-ADD     1             TN                              * Tegn-index-nr
     C                   END
     C* Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      80
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   END
     C* Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
     C* Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
     C*        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
     C* Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
     C* foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   END
     C                   END
     C                   END
     C*
     C     ENDCH         ENDSR
     C*__________________________________________________________
     C* SR for å hente neste Segment                       GETSEG
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETSEG        BEGSR
     C* "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   ENDDO
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
N07  C     TEGN          OREQ      XLF
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
     C* Har nå lest appostrof og kansje blank- de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C                   MOVEL     FELT          SEGNAM
     C* La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
     C* Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
      *__________________________________________________________
      * SR for å behandle UNB-segmentet                    SRUNB
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUNB         BEGSR
     C                   MOVE      *BLANKS       TEGN
     C                   DOW       TEGN <> LOGPLU
     C                   EXSR      GETCH
     C   LR              RETURN
     C     TEGN          CABEQ     LOGPLU        UNB010
     C                   ENDDO
     C     UNB010        TAG
     C                   MOVE      *BLANKS       FE
     C     1             DO        35            NR
     C                   EXSR      GETCH
     C   LR              RETURN
     C                   IF        ((TEGN = LOGPLU) OR (TEGN = LOGKOL))
     C                   LEAVE
     C                   ENDIF
     C                   MOVE      TEGN          FE(NR)
     C                   ENDDO
     C                   MOVEL     FELT          X0004            35
N06   * Er dette Svensk TDS UTL eller ZEM eller ingen av delene ?
N06  C     'BGM+UTL'     SCAN      ABDS                                   91
N06  C     'BGM+ZEM'     SCAN      ABDS                                   92
     C     ENDUNB        ENDSR
     C*__________________________________________________________
     C* SR for å behandle UNH-segmentet                    SRUNH
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUNH         BEGSR
     C                   MOVEA     *BLANKS       FE
S08  C*    1             DO        15            NR
N08  C     1             DO        35            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          IFEQ      LOGPLU
     C                   GOTO      UNH010
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     UNH010        TAG
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        6             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          W0065
      *
     C     W0065         IFEQ      'CUSDEC'
     C     W0065         OREQ      'CUSRES'
      * Lese neste 6 tegn for å skille om import/eksport eller transittering.
      * ':D:96B' --> transittering,    ':1:902' --> import/eksport
     C                   MOVE      *BLANKS       FE
     C     1             DO        6             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          IFEQ      LOGPLU
     C                   GOTO      UNH012
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     UNH012        TAG
     C                   MOVEL     FELT          XAN06             6
     C                   MOVE      XAN06         XAN03             3
     C     XAN03         IFEQ      '96B'
     C     W0065         OREQ      'CONTRL'
      * TEST OM TET-IMPORT ELLER TET-EKSPORT
      * LES FORBI ':UN:CC' ELLER ':UN:SE'
     C                   MOVE      *BLANKS       FE
     C     1             DO        6             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C     TEGN          CABEQ     LOGPLU        ENDUNH
     C                   END
     C                   MOVEL     FELT          XAN06             6
     C                   MOVE      XAN06         XAN02             2
     C                   MOVE      *BLANKS       FE
      * LES 008, 009, 016, 025, 028, 029, 043, 051, 055, 058, 060, 045, 917, 907
     C     1             DO        3             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          CABEQ     LOGPLU        ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          XAN03             3

     C     XAN03         IFEQ      '505'
     C     W0065         IFEQ      'CUSRES'
     C                   MOVE      'SV-CUS'      W0065
     C                   ENDIF
     C     W0065         IFEQ      'CONTRL'
     C                   MOVE      'SV-KON'      W0065
     C                   ENDIF
     C     W0065         IFEQ      'CUSDEC'
     C                   MOVE      'SV-ZEM'      W0065
N06  C*    *IN91         IFEQ      *ON
N06  C                   MOVE      'SV-UTL'      W0065
     C*                  ENDIF
N06  C                   ENDIF
     C                   GOTO      ENDUNH
     C                   ENDIF
     C     XAN03         IFEQ      '500'
     C     W0065         IFEQ      'CUSRES'
     C                   MOVE      'SV-UTL'      W0065
     C                   ENDIF
     C                   GOTO      ENDUNH
     C                   ENDIF
     C     XAN03         IFEQ      '917'
     C                   SELECT
     C                   WHEN      ((X0004='NO000175') OR (X0004='NO000180'))
     C                   EXSR      SJTYPE
N05  C                   WHEN      (X0004='NTA.DK')
N05  C                   EXSR      SJTYPEDK
     C                   OTHER
     C                   EXSR      SJTYPESV
     C                   ENDSL
     C                   ELSE
     C     XAN03         IFEQ      '009'
     C     XAN03         OREQ      '016'
     C     XAN03         OREQ      '028'
     C     XAN03         OREQ      '029'
     C     XAN03         OREQ      '045'
     C     XAN03         OREQ      '051'
     C     XAN03         OREQ      '055'
     C     XAN03         OREQ      '060'
S05  C*                  IF        XAN02 = 'SE'
S05  C*                  MOVE      'SV-NCE'      W0065
S05  C*                  ELSE
S05  C*                  MOVE      'TET-EX'      W0065
S05  C*                  END
N05  C                   SELECT
N05  C     XAN02         WHENEQ    'SE'
N05  C                   MOVE      'SV-NCE'      W0065
N05  C     XAN02         WHENEQ    'DK'
N05  C                   MOVE      'DK-NCE'      W0065
N05  C                   OTHER
N05  C                   MOVE      'TET-EX'      W0065
N05  C                   ENDSL
     C                   ELSE
S05  C*                  IF        XAN02 = 'SE'
S05  C*                  MOVE      'SV-NCI'      W0065
S05  C*                  ELSE
S05  C*                  MOVE      'TET-IM'      W0065
S05  C*                  END
N05  C                   SELECT
N05  C     XAN02         WHENEQ    'SE'
N05  C                   MOVE      'SV-NCI'      W0065
N05  C     XAN02         WHENEQ    'DK'
N05  C                   MOVE      'DK-NCI'      W0065
N05  C                   OTHER
N05  C                   MOVE      'TET-IM'      W0065
N05  C                   ENDSL
     C                   END
     C                   GOTO      ENDUNH
     C                   END
     C                   END
      * Lese forbi ':UN:'
     C     1             DO        4
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   END
      *
      * Lese NEP-I eller NODI
     C                   MOVE      *BLANKS       FE
     C     1             DO        5             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          IFEQ      LOGPLU
     C                   GOTO      UNH011
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     UNH011        TAG
     C                   MOVEL     FELT          X0057             5
      *
     C                   SELECT
     C     X0057         WHENEQ    'NEP-I'
     C                   MOVE      'IMPORT'      W0065
      *
     C     X0057         WHENEQ    'NEP  '
     C                   MOVE      'EXPORT'      W0065
     C     X0057         WHENEQ    '14505'
     C                   MOVE      'SV-CUS'      W0065
     C                   OTHER
     C                   EXSR      TSTVID
      *
     C                   ENDSL
      *
     C                   END
      *
     C     W0065         IFEQ      'CONTRL'
      * LES FORBI ':D:3:UN:??'
     C     1             DO        10            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          CABEQ     LOGPLU        ENDUNH
     C                   END
     C                   MOVE      *BLANKS       FE
      * LES 917, C01
     C     1             DO        3             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          CABEQ     LOGPLU        ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          XAN03             3
      *
     C                   IF        (((XAN03 = 'C01') OR
     C                               (XAN03 = 'C02') OR
     C                               (XAN03 = '505') OR
     C                               (XAN03 = '907')))
N02  C                   IF        ((X0004<>'NO000175') AND (X0004<>'NO000180'))
     C                   MOVE      'SV-KON'      W0065
N02  C                   GOTO      ENDUNH
N02  C                   END
N02  C                   END
N02  C*                  ELSE
      *
     C     SEGNAM        DOWNE     'UCM'
     C  NLR              EXSR      GETSEG
     C   LR              GOTO      ENDUNH
     C                   END
     C                   MOVE      *BLANKS       TEGN
     C     TEGN          DOWNE     LOGPLU
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   END
      * LES FORBI 'CUSDEC:D:96B:UN:CC'
     C     1             DO        18            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   END
      * LES 007, 014, 015, 044
     C     1             DO        3             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          CABEQ     LOGPLU        ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          XAN03             3
     C     XAN03         IFEQ      '014'
     C     XAN03         OREQ      '015'
     C                   MOVE      'TET-EX'      W0065
     C                   ELSE
     C                   MOVE      'TET-IM'      W0065
     C                   END
      *
N02  C*                  END
      *
     C                   GOTO      ENDUNH
     C                   END
      *
      * SVENSK FORTOLLING: KONTRL
     C     W0065         IFEQ      'CONTRL'
      * LES FORBI 'KONTRL:0:910:SE:'
      *            CONTRL:D:3:UN:145050
     C     1             DO        8             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   ENDDO
      * Er dette fra Svenska Tullen ?
     C     1             DO        6             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          CABEQ     LOGPLU        ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   ENDDO
     C                   MOVEL     FELT          XAN06             6
     C     XAN06         IFEQ      'E:1450'
     C     XAN06         OREQ      '145050'
     C                   MOVE      'SV-KON'      W0065
     C                   GOTO      ENDUNH
     C                   ENDIF
     C                   ENDIF
      *
     C     ENDUNH        TAG
N03   * DANSK TOLD CONTRL
N03  C     W0065         IFEQ      'CONTRL'
N03  C     X0004         ANDEQ     'TOLDSKAT'
N03  C                   MOVE      'DK-CON'      W0065
N03  C                   ENDIF
N04   * DANSK TOLD CUSRES
N04  C     W0065         IFEQ      'TET-IM'
N04  C     X0004         ANDEQ     'TOLDSKAT'
N04  C                   MOVE      'DK-CUS'      W0065
N04  C                   ENDIF

     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C                   ENDSR
     C*__________________________________________________________
     C* MIDLETIG SUBRUTINE
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TSTVID        BEGSR
     C     TVIM5K        KLIST
     C                   KFLD                    M0068
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        22            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDTST
     C     TEGN          IFEQ      LOGAPP
     C     TEGN          OREQ      LOGPLU
     C                   GOTO      STTST0
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     STTST0        TAG
     C                   MOVEL     FELT          M0068
      *
     C     TVIM5K        SETLL     EDIM5                              33
     C     STTST         TAG
     C     TVIM5K        READE     EDIM5                                  33
     C  N33MSR           IFEQ      'S'                                          1<-B
     C     M1001         IFEQ      '830'                                         2<-B
     C                   MOVE      'EXPORT'      W0065
     C                   ELSE                                                    2
     C     M1001         IFEQ      '929'                                          3<-B
     C                   MOVE      'IMPORT'      W0065
     C                   ELSE                                                     3
     C     X0065         IFEQ      'CUS'                                           4<-B
     C                   MOVE      'EXPORT'      W0065
     C                   ELSE                                                      4
     C     X0065         IFNE      'IFT'                                            5<-B
     C                   MOVE      'IMPORT'      W0065
     C                   ELSE                                                       5
     C                   GOTO      STTST
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   ELSE                                                   1
     C                   GOTO      STTST
     C                   END                                                    1<-E
      *
     C     ENDTST        ENDSR
     C*__________________________________________________________
     C* SJEKK OM DET ER TET-IMPORT ELLER EKSPORT
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SJTYPE        BEGSR
N01   *
N01  C     SEGNAM        DOWNE     'BGM'
N01  C  NLR              EXSR      GETSEG
N01  C   LR              GOTO      SLSJT
N01  C                   END
N01   *
N01  C     TEGN          CABEQ     LOGAPP        STSJT
N01  C                   MOVE      *BLANKS       TEGN
N01  C     TEGN          DOWNE     LOGPLU
N01  C                   EXSR      GETCH
N01  C   LR              GOTO      SLSJT
N01  C                   ENDDO
N01   *
N01  C                   MOVE      *BLANKS       FE
N01  C     1             DO        18            NR
N01  C                   EXSR      GETCH
N01  C   LR              GOTO      SLSJT
N01  C     TEGN          IFEQ      LOGKOL
N01  C     TEGN          OREQ      LOGPLU
N01  C     TEGN          OREQ      LOGAPP
N01  C                   LEAVE
N01  C                   END
N01  C                   MOVE      TEGN          FE(NR)
N01  C                   END
N01  C                   MOVEL     FELT          XAN18            18
N01  C     XAN18         CHAIN     TRIH05                             33
N01  C  N33              MOVE      'TET-IM'      W0065
N01  C  N33              GOTO      SLSJT
N01  C     XAN18         CHAIN     TRUH05                             33
N01  C  N33              MOVE      'TET-EX'      W0065
N01  C  N33              GOTO      SLSJT
N01   *
     C     STSJT         TAG
     C     SEGNAM        DOWNE     'RFF'
     C  NLR              EXSR      GETSEG
     C   LR              GOTO      SLSJT
     C                   END
      *
     C     TEGN          DOWNE     LOGPLU
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJT
     C                   ENDDO
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        3             NR
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJT
     C     TEGN          CABEQ     LOGKOL        STSJT
     C     TEGN          CABEQ     LOGPLU        STSJT
     C     TEGN          CABEQ     LOGAPP        STSJT
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          XAN03
      *
     C                   IF        ((XAN03 <> 'ABO') AND (XAN03 <> 'ABE'))
     C                   GOTO      STSJT
     C                   END
      *
     C     TEGN          DOWNE     LOGKOL
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJT
     C                   ENDDO
      *
     C                   IF        XAN03 = 'ABO'
     C                   MOVE      *BLANKS       FE
     C     1             DO        10            NR
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJT
     C     TEGN          CABEQ     LOGAPP        STSJT2
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     STSJT2        TAG
     C                   MOVEL     FELT          XAN10            10
      *
     C     XAN10         CHAIN     EDIS4                              33
     C  N33SSN           CHAIN     EDIM6                              33
     C  N33M1001         IFEQ      'TEI'
     C                   MOVE      'TET-IM'      W0065
     C                   ELSE
     C                   MOVE      'TET-EX'      W0065
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       FE
     C     1             DO        22            NR
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJT
     C     TEGN          CABEQ     LOGAPP        STSJT3
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     STSJT3        TAG
     C                   MOVEL     FELT          XAN35            35
     C     XAN35         CHAIN     EDIM5                              33
     C  N33              SELECT
     C                   WHEN      M1001 = 'TEI'
     C                   MOVE      'TET-IM'      W0065
     C                   WHEN      M1001 = 'TET'
     C                   MOVE      'TET-EX'      W0065
     C                   WHEN      M1001 = 'STE'
     C                   MOVE      'SV-NCE'      W0065
     C                   WHEN      M1001 = 'STI'
     C                   MOVE      'SV-NCI'      W0065
N05  C                   WHEN      M1001 = 'DTE'
N05  C                   MOVE      'DK-NCE'      W0065
N05  C                   WHEN      M1001 = 'DTI'
N05  C                   MOVE      'DK-NCI'      W0065
     C                   END
     C                   END
      *
     C     SLSJT         ENDSR
      *__________________________________________________________
      * SJEKK OM DET ER NCTS-IMPORT ELLER EKSPORT
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SJTYPESV      BEGSR
     C     SEGNAM        DOWNE     'BGM'
     C  NLR              EXSR      GETSEG
     C   LR              GOTO      SLSJTSV
     C                   END
      *
     C                   MOVE      *BLANKS       TEGN
     C     TEGN          DOWNE     LOGPLU
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJTSV
     C                   ENDDO
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        35            NR
     C                   EXSR      GETCH
     C   LR              GOTO      SLSJTSV
     C     TEGN          CABEQ     LOGKOL        STSJTSV1
     C     TEGN          CABEQ     LOGPLU        STSJTSV1
     C     TEGN          CABEQ     LOGAPP        STSJTSV1
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     STSJTSV1      TAG
     C                   MOVEL     FELT          XAN18            18
      *
     C     XAN18         CHAIN     SVNH05                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      'SV-NCI'      W0065
     C                   ELSE
     C     XAN18         CHAIN     SVXH05                             33
     C  N33              MOVE      'SV-NCE'      W0065
     C                   END
      *
     C     SLSJTSV       ENDSR
N05   *__________________________________________________________
N05   * SJEKK OM DET ER NCTS-IMPORT ELLER EKSPORT i DANMARK
N05   *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
N05  C     SJTYPEDK      BEGSR
N05  C     SEGNAM        DOWNE     'BGM'
N05  C  NLR              EXSR      GETSEG
N05  C   LR              GOTO      SLSJTDK
N05  C                   END
N05   *
N05  C                   MOVE      *BLANKS       TEGN
N05  C     TEGN          DOWNE     LOGPLU
N05  C                   EXSR      GETCH
N05  C   LR              GOTO      SLSJTDK
N05  C                   ENDDO
N05   *
N05  C                   MOVE      *BLANKS       FE
N05  C     1             DO        35            NR
N05  C                   EXSR      GETCH
N05  C   LR              GOTO      SLSJTDK
N05  C     TEGN          CABEQ     LOGKOL        STSJTDK1
N05  C     TEGN          CABEQ     LOGPLU        STSJTDK1
N05  C     TEGN          CABEQ     LOGAPP        STSJTDK1
N05  C                   MOVE      TEGN          FE(NR)
N05  C                   END
N05  C     STSJTDK1      TAG
N05  C                   MOVEL     FELT          XAN18            18
N05   *
N05  C     XAN18         IFNE      *BLANKS
N05  C     XAN18         CHAIN     DKNH05                             33
N05  C                   IF        *IN33 = *OFF
N05  C                   MOVE      'DK-NCI'      W0065
N05  C                   ELSE
N05  C     XAN18         CHAIN     DKXH05                             33
N05  C* N33              MOVE      'DK-NCE'      W0065
N05  C                   MOVE      'DK-NCE'      W0065
N05  C                   END
N05  C                   ELSE
N05  C                   MOVE      'DK-NCE'      W0065
     C                   ENDIF
N05   *
N05  C     SLSJTDK       ENDSR
