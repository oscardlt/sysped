     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/UTSKRS)
     A                                      PRINT
     A                                      HLPDOC('VEDLUTSK' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '2')
     A            UTPNR     R    3  0O  6  8EDTCDE(4)
     A                                      DSPATR(HI)
     A            UTPTXT    R   30   O  6 14
     A            UTPTY     R   10   O  6 45
     A            UTPL      R    3  0O  6 60EDTCDE(4)
     A            UTPK      R    3  0O  6 72EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA01'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold av utskrifts typer'
     A                                      DSPATR(HI)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(78 DIV1218 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20010406  101809  JOVO        REL-V4R4M0  5769-PW1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  6MSGCON(074 DIV1219 *LIBL/SYMSGF)
     A            WSPNR          3Y 0B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSPTXT        30A  B  2 14DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            WSPTY         10A  B  2 45DSPATR(UL)
     A            WSPL           3Y 0B  2 60DSPATR(UL)
     A                                      EDTCDE(4)
     A  88                                  DSPATR(RI)
     A            WSPK           3Y 0B  2 72DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(12 DIV1216 SYMSGF)
N01  A            WSØKN          3  0B  2 16DSPATR(UL)
N01  A                                      DSPATR(PC)
N01  A                                      EDTCDE(4)
S01  A*           WSØK          30A  I  2 16DSPATR(UL)
S01  A*                                     DSPATR(PC)
N01  A            WSØK          30A  B  2 20DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA01'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold av utskrifts typer'
     A                                      DSPATR(HI)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(78 DIV1218 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
