      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SYMN1 OG VISER LISTE
      *
CRTPG+*  CRTPGM SYSPED/SYL001 MODULE(*LIBL/SYL001
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FLLHEAD    UF   E           K DISK    USROPN
     FLLHEAD01  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR1)
     FLLLINE    IF   E           K DISK    USROPN
     FLLGrpK05  if   e           k disk    USROPN
     FLLTELL    UF   E             DISK    USROPN
     FLLW013    O  A E             DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSMN            s              7
     D W2MN            s              7
     D XXMN            S              7
     D KUNDEREF1       S             50
     D KUNDEREF2       S             50
     D KUNDEREF3       S             50
     D GrpNr           s              8
     D IFSFIL          S             50
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D TITLE           C                   CONST('Arbeid med plukklister')
     D TITLEEN         C                   CONST('Work with packinglist')
      *
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C                   MOVE      BIBESK        WINT              3
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTPL
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   EVAL      XXMN    = zhbgetvar('MN')
     C                   EVAL      LHMN    = c2n2(XXMN)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':TITLEEN)
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':TITLE)
     C                   END
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('WSNA':KNAVN)
     C                   callp     updhtmlvar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('KUNDEREF1':KUNDEREF1)
     C                   callp     updhtmlvar('KUNDEREF2':KUNDEREF2)
     C                   callp     updhtmlvar('KUNDEREF3':KUNDEREF3)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updhtmlvar('MN':
     C                             %trim(%editc(LHMN:'Z')))
      * MOTTAKERsnr
     C                   callp     updhtmlvar('LHBNR':LHBNR)
      * MOTTAKER
     C                   callp     updhtmlvar('LHBNA':LHBNA)
      * TRANS.DATO
     C                   callp     updhtmlvar('LHTDT':
     C                             %trim(%editW(LHTDT:'    .  .  ')))
      * KUNDEREF 1
     C                   callp     updhtmlvar('LHPAK':LHPAK)
      * KUNDEREF 2
     C                   callp     updhtmlvar('LHORDR':LHORDR)
      * KUNDEREF 3
     C                   callp     updhtmlvar('LHFLN':LHFLN)
      *
     C                   callp     updhtmlvar('XXOK':'PLUKK')
      * TOTAL STYKK
     C                   callp     updhtmlvar('LHANT0':
     C                             %trim(%editc(LHANT0:'Z')))
      * TOTAL VEKT
     C                   callp     updhtmlvar('LHWVEK':
     C                             %trim(%editc(LHWVEK:'3')))
      * TOTAL KUBIKK
     C                   callp     updhtmlvar('LHWKUB':
     C                             %trim(%editc(LHWKUB:'3')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     begsr
     C                   move      'N'           selected          1
     C                   move      'Y'           selected
     C                   IF        ((XBNR <> *BLANKS) AND (XBNR <> LHBNR))
     C                   MOVE      'N'           SELECTED
     C                   END
     C*
     C     RecordSltX    tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   IF        XSPRÅK = 'EN'
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYL001EN.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   ELSE
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYL001.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTPL        BEGSR
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C     LH01K         SETLL     LLHEAD01
     C     LH01K         READE     LLHEAD01                               50
     C     *in50         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTPL
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTPL
     C                   endif
     C     LH01K         READE     LLHEAD01                               50
     C                   ENDDO                                                  *hival
      *
     C     SLHENTPL      ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   open      LLHEAD01                             50
     C                   open      LLGrpK05                             50
     C                   open      LLTELL                               50
     C                   open      BRPARF                               50
     C                   OPEN      CUNDL01
      *
     C     LH01K         klist
     C                   kfld                    LHSTA
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     CUNDL01K      CHAIN     CUNDL01                            50
     C     BIKUNR        chain     LLGrpK05                           33
     C                   OPEN      LLHEAD                               33
     C     LHMN          CHAIN     LLHEAD                             33
     C  N33LHSTA         COMP      'W'                                3333
     C                   IF        *IN33 = *OFF
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           LHRDT
     C                   MOVE      WTM           LHRTD
     C                   MOVE      ' '           LHSTA
     C                   UPDATE    LLHEADR
      *
     C                   MOVE      LHPNR         UXPNR             8
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *ZEROS        PAKUNR
      *
     C                   MOVE      'LLPLL'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        ((*IN33=*OFF) AND (PAVRDA = 'J'))
     C                   EXSR      SRPRTPLL
     C                   ELSE
      *
      * SEND E-POST TIL BRUKER
     C                   MOVE      'EPOST'       PAID
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      LHPNR         PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   DOW       *IN50 = *OFF
     C                   CALL      'SYL001C1'
     C                   PARM                    UXPNR
     C                   PARM                    LHPAK
     C                   PARM                    LHORDR
     C                   PARM                    LHFLN
     C                   PARM                    PAVRDA
     C     BRPARFK       READE     BRPARF                                 50
     C                   ENDDO
      * SEND MELDING TIL BRUKER
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'BPOST'       PAID
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      LHPNR         PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   DOW       *IN50 = *OFF
     C                   CALL      'SYL001C2'
     C                   PARM                    UXPNR
     C                   PARM                    LHPAK
     C                   PARM                    LHORDR
     C                   PARM                    LHFLN
     C                   PARM                    PAVRDA
     C     BRPARFK       READE     BRPARF                                 50
     C                   ENDDO
      *
     C                   END
      *
     C                   END
      *
      * HENT LEDERTEKST
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *ZEROS        PAKUNR
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVE      'LLRF1'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF1 = 'Kunderef.1'
     C                   ELSE
     C                   EVAL      KUNDEREF1 = PAVRDA
     C                   END
      *
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'LLRF2'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF2 = 'Kunderef.2'
     C                   ELSE
     C                   EVAL      KUNDEREF2 = PAVRDA
     C                   END
      *
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'LLRF3'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF3 = 'Kunderef.3'
     C                   ELSE
     C                   EVAL      KUNDEREF3 = PAVRDA
     C                   END
      *
     C                   CLOSE     LLHEAD
     C                   CLOSE     LLTELL
     C                   MOVE      *ZEROS        LHMN
     C                   MOVE      *ZEROS        XXMN
     C                   MOVE      'W'           LHSTA
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'LLVMO'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      LHPNR         PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   END
     C                   IF        (*IN50 OR (PAVRDA = *BLANKS))
     C                   MOVE      *BLANKS       XBNR             17
     C                   ELSE
     C                   MOVEL     PAVRDA        XBNR
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  PRINT PLUKKLISTE
      *=====================================================================******
     C     SRPRTPLL      begsr
     C                   IF        LHALN = 0
     C     2             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C     LHMN          CHAIN     LLHEAD                             33
     C                   MOVE      LLREC1        LHALN
     C                   UPDATE    LLHEADR
     C                   END
      *
     C                   CALL      'SYL001C0'
     C                   OPEN      LLW013
     C                   OPEN      LLLINE
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C                   WRITE     LLW013R
     C     LHMN          READE     LLLINE                                 33
     C  N33              ENDDO                                                  1<-E
     C                   CLOSE     LLW013
     C                   CLOSE     LLLINE
      *
     C                   MOVE      LHPNR         UPNR              8
     C                   MOVE      UsrSpcName    UUSN             10
     C                   MOVE      LHALN         UALNG             7
     C                   MOVEL     '*JOB'        WSPRT            10
     C                   MOVE      'L'           USORT             1
     C                   MOVE      'P'           ULTYPE            1
     C                   CALL      'SYL001C'
     C                   PARM                    UPNR
     C                   PARM                    UUSN
     C                   PARM                    BIFIRM
     C                   PARM                    UALNG
     C                   PARM                    USORT
     C                   PARM                    ULTYPE
      *
      *
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   eval      IFSFil='PLL'+UPNR+UALNG+UsrSpcName+'.pdf'
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     wrtsection('top')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     LLHEAD01
     C                   close     LLGrpK05
     C                   close     BRPARF
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
