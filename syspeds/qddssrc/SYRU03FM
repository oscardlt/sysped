     A*                                     DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLLIHO' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4' '5' 'S' 'A' 'M')
     A            RUAVD     R        O  6  4REFFLD(RUCKPR/RUAVD *LIBL/RUCKP)
     A  51                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            RUOPD     R        O  6  9REFFLD(RUCKPR/RUOPD *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            SFDTOP         6  0O  6 17EDTCDE(4)
     A            SFGN          15A  O  6 24
     A            SFPOS          7A  O  6 40
     A            RUKU      R        O  6 48REFFLD(RUCKPR/RUKU *LIBL/RUCKP)
     A  50                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            SFDTP1         4  0O  6 57EDTCDE(4)
     A            SFDTP2         4  0O  6 62EDTCDE(4)
     A            SFDTP3         4  0O  6 67EDTCDE(4)
     A            RUANTP         2  0O  6 72EDTCDE(4)
     A            SFDTPS         4  0O  6 75EDTCDE(4)
     A            RUST      R        O  6 80REFFLD(RUCKPR/RUST *LIBL/RUCKP)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYRU03'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 JOV1390 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 62SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3 51'A=Avsender (Kpers på avdel.)'
     A                                      COLOR(BLU)
     A                                  4  2'2=Endre 4=Slette 5=Se oppdrag S=Se-
     A                                      nd EN          M=Mottaker (Kpers på-
     A                                       kunde) '
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(079 JOV1391 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  55                              1  2'Uthevet'
     A                                      DSPATR(RI)
     A  55                              1 10'Avd/Kunde betyr at k.pers av type -
     A                                      *RUCKATTEST mangler på Avs./Mottak'
     A  55                              2  2'Opt A/M for å legge inn, S=Send EN-
     A                                      , (ved F3 sendes alle som da er kla-
     A                                      re).'
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(079 JOV1391 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      TEXT('AVDELINGSNR')
     A            RUAVD     R        O  2  4REFFLD(RUCKPR/RUAVD *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            RUOPD     R        O  2  9REFFLD(RUCKPR/RUOPD *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            SFDTOP         6Y 0O  2 17EDTCDE(4)
     A            SFGN          15A  O  2 24
     A            SFPOS          7A  O  2 40
     A            WSKU      R        B  2 48REFFLD(RUCKPR/RUKU *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            SFDTP1         4Y 0O  2 57EDTCDE(4)
     A            SFDTP2         4Y 0O  2 62EDTCDE(4)
     A            SFDTP3         4Y 0O  2 67EDTCDE(4)
     A            RUANTP         2Y 0O  2 72EDTCDE(4)
     A            SFDTPS         4Y 0O  2 75EDTCDE(4)
     A            RUST      R        O  2 80REFFLD(RUCKPR/RUST *LIBL/RUCKP)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3 48'F4=Søk'
     A                                      COLOR(BLU)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2HAVD         4Y 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2HOPD         7Y 0I  2 21DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2 29'Status'
     A            W2HST          1A  I  2 37
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  2 39'Blank=Åpen, P=Purret, O=OK, D=Slet-
     A                                      tet.'
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(075 JOV1392 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            RUAVD     R        O  2  4REFFLD(RUCKPR/RUAVD *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            RUOPD     R        O  2  9REFFLD(RUCKPR/RUOPD *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            SFDTOP         6  0O  2 17EDTCDE(4)
     A            SFGN          15A  O  2 24
     A            SFPOS          7A  O  2 40
     A            RUKU      R        O  2 48REFFLD(RUCKPR/RUKU *LIBL/RUCKP)
     A                                      EDTCDE(4)
     A            SFDTP1         4  0O  2 57EDTCDE(4)
     A            SFDTP2         4  0O  2 62EDTCDE(4)
     A            SFDTP3         4  0O  2 67EDTCDE(4)
     A            RUANTP         2  0O  2 72EDTCDE(4)
     A            SFDTPS         4  0O  2 75EDTCDE(4)
     A            RUST      R        O  2 80REFFLD(RUCKPR/RUST *LIBL/RUCKP)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYRU03'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 JOV1390 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 JOV1391 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
