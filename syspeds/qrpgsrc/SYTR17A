      *
      **************************************************************
      *** PROGRAM FOR SLETTE SE PÅ FERJEOVERFARTER (UTENOM TRAFIKK)*
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-96       ***
      **************************************************************
      *
     FKOSBU     UF   E           K DISK
     FTUFE2     UF   E           K DISK
     FTUFE4     IF   E           K DISK
     F                                     RENAME(TUFER:TUFE4R)
     FTRAN      IF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYTR17FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  FEFROM                 1      5
     D  XDLT                   1      5
     D                 DS
     D  WSDATE                 1      6  0
     D  XEDAY2                 1      2  0
     D  XEMON2                 3      4  0
     D  XEYEA2                 5      6  0
     D                 DS
     D  FEDAT2                 1      8  0
     D  FEYEA2                 1      4  0
     D  FEMON2                 5      6  0
     D  FEDAY2                 7      8  0
     D                 DS
     D  W2DATE                 1      6  0
     D  W2DAY2                 1      2  0
     D  W2MON2                 3      4  0
     D  W2YEA2                 5      6  0
     D                 DS
     D  Y2DATE                 1      8  0
     D  Y2YEA2                 1      4  0
     D  Y2MON2                 5      6  0
     D  Y2DAY2                 7      8  0
     D                 DS
     D  BUTXT                  1     35
     D  DSDATE                21     26
     D  DSTIME                28     31
     C*
     C     STINI         TAG
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C* 50 ON - SØK VIA BILNR
     C     W2BILN        COMP      *BLANKS                            5050
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C   50              MOVE      W2DAY2        Y2DAY2
     C   50              MOVE      W2MON2        Y2MON2
     C   50W2YEA2        IFGT      50
     C     W2YEA2        ADD       1900          Y2YEA2
     C                   ELSE
     C     W2YEA2        ADD       2000          Y2YEA2
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N50KODSI1        SETLL     TUFE2
     C   50TUFE41        SETLL     TUFE4
     C     START1        TAG
     C  N50KODSI1        READE(N)  TUFE2                                  94
     C   50TUFE42        READE     TUFE4                                  94
      *
     C  N94              DO
     C                   DO        7
     C                   ADD       1             ZRECNO
     C     LEVEK         CHAIN     LEVEL01                            33
     C  N33              MOVEL     LNAVN         FENAME
     C  N33              MOVE      FEYEA2        XEYEA2
     C  N33              MOVE      FEMON2        XEMON2
     C  N33              MOVE      FEDAY2        XEDAY2
     C  N33              MOVE      FETIME        WSTIME
     C  N33              MOVE      FEPRI1        WSPRI1
     C  N33              MOVE      FEPRI2        WSPRI2
     C* ENDRING/OPTION IKKE TILLATT NÅR INNGÅENDE KOSTN. ER ATT./FØRT
     C     FETREF        CHAIN     KOSBU                              33
     C   33              SETOFF                                       90
     C  N33BUST          COMP      ' '                                9090
     C     FRAFIX        IFEQ      'J'
     C                   SETOFF                                       90
     C                   END
     C                   WRITE     SUBFIL
     C  N50KODSI1        READE(N)  TUFE2                                  94
     C   50TUFE42        READE     TUFE4                                  94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       989975
     C*
     C*** AVSLUTT
     C   03              GOTO      STINI
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C* PLUKK FERJER
     C*
     C   06              DO
     C                   CALL      'SYTR16'
     C                   PARM                    FEAVD
     C                   PARM                    FETUR
     C                   PARM                    FERUND
     C                   GOTO      START
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C* LES FØST EN BAKLENGS FOR Å SETTE FILPEKER RIKTIG
     C  N94ZRECNO        IFNE      0
     C  N50              READP(N)  TUFE2                                  94
     C   50              READP     TUFE4                                  94
     C                   END
     C                   GOTO      START1
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C   98              GOTO      START3
     C*
     C*
     C                   GOTO      START
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   MOVE      *BLANKS       W2BILN
     C                   MOVE      *ZEROS        W2DATE
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C                   SETOFF                                       7576
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     7             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '4'                                          PLUKK
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSI
     C   98              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'                                          PLUKK
     C*                    EXSR TESTDA
     C     WSTIME        COMP      *ZEROS                                 75
     C  N75WSDATE        COMP      *ZEROS                                 75
     C   75              MOVE      'DIV4017'     MSGNR
     C   75              SETON                                        30
     C   75              GOTO      SLHENT
     C     *IN23         IFEQ      '0'
     C     WSPRI1        COMP      *ZEROS                                 76
     C   76              MOVE      'JOV0887'     MSGNR
     C     *IN76         IFEQ      '0'
     C     WSPRI2        COMP      *ZEROS                                 76
     C   76              MOVE      'JOV0888'     MSGNR
     C                   END
     C   76              MOVE      ' '           WSNR
     C   76              UPDATE    SUBFIL
     C   76              SETON                                        98
     C                   END
     C   76              SETON                                        30
     C   76              GOTO      SLHENT
     C  N98              DO
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSI2
     C   98              GOTO      SLHENT
     C                   END
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   SETOFF                                       30
     C     TUFE2K        CHAIN     TUFE2                              70
     C   70              SETON                                        98
     C  N70              MOVE      ' '           WSNR
     C  N70              MOVE      '*DEL*'       XDLT
     C  N70              UPDATE    SUBFIL
     C  N70              DELETE    TUFER
     C  N70FETREF        IFNE      *ZEROS
     C                   Z-ADD     FETREF        BUBNR
     C     BUBNR         CHAIN     KOSBU                              34
     C  N34              DELETE    KOSBUR
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     7             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI2        BEGSR
     C***********************************************
     C                   MOVE      XEDAY2        FEDAY2
     C                   MOVE      XEMON2        FEMON2
     C     XEYEA2        IFGT      50
     C     XEYEA2        ADD       1900          FEYEA2
     C                   ELSE
     C     XEYEA2        ADD       2000          FEYEA2
     C                   END
     C                   SETON                                        99
     C                   SETOFF                                       30
     C     TUFE2K        CHAIN     TUFE2                              70
     C   70              SETON                                        98
     C  N70              MOVE      ' '           WSNR
     C  N70              UPDATE    SUBFIL
     C  N70              MOVE      WSTIME        FETIME
     C  N70              MOVE      WSPRI1        FEPRI1
     C  N70              MOVE      WSPRI2        FEPRI2
     C  N70              UPDATE    TUFER
     C  N70FETREF        IFNE      *ZEROS
     C                   Z-ADD     FETREF        BUBNR
     C     BUBNR         CHAIN     KOSBU                              34
     C  N34              MOVE      WSDATE        DSDATE
     C  N34              MOVE      FETIME        DSTIME
     C  N34              MOVE      FEDAT2        BUFEDT
     C  N34              MOVE      FECURR        BUVAL
     C  N34              Z-ADD     FEPRI1        BUBL
     C  N34              UPDATE    KOSBUR
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     7             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    FEAVD
     C                   PARM                    FETUR
     C                   PARM                    FERUND
     C                   PARM                    UKOST             1
     C     UKOST         IFEQ      'X'
     C                   MOVE      'J'           FRAFIX            1
     C                   MOVE      'Y'           UKOST
     C                   END
     C* PRISFELTENE ER PROTECTET NÅR EN IKKE KOMMER FRA KOST.MODUL
     C     UKOST         COMP      'Y'                                9797
     C* TUFE2 - FOR SETLL / READE
     C     KODSI1        KLIST
     C                   KFLD                    W2OPKO
     C                   KFLD                    FEAVD
     C                   KFLD                    FETUR
     C* TUFE2 - FOR CHAIN
     C     TUFE2K        KLIST
     C                   KFLD                    FEOPKO
     C                   KFLD                    FEAVD
     C                   KFLD                    FETUR
     C                   KFLD                    FEDAT2
     C                   KFLD                    FEFROM
     C                   KFLD                    FETO
     C                   KFLD                    FEBILN
     C* TUFE4 - FOR SETLL
     C     TUFE41        KLIST
     C                   KFLD                    W2OPKO
     C                   KFLD                    FEAVD
     C                   KFLD                    FETUR
     C                   KFLD                    W2BILN
     C                   KFLD                    Y2DATE
     C*
     C* TUFE4 - FOR  READE
     C     TUFE42        KLIST
     C                   KFLD                    W2OPKO
     C                   KFLD                    FEAVD
     C                   KFLD                    FETUR
     C                   KFLD                    W2BILN
     C*
     C     LEVEK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FEFIRM
     C     TRANK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FETUR
     C                   READ      FIRM                                   33
     C*
     C     TRANK         CHAIN     TRAN                               33
     C*
     C                   MOVE      ' '           W2OPKO
     C                   MOVE      *ZEROS        W2DATE
     C                   MOVE      *ZEROS        WSDATE
     C*
     C     UTI           ENDSR
     C*
