     FEDI85F    IF   E             DISK
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
     D  AA                     1     80
     D                                     DIM(80)
     D  AADS                   1     80
     D                 DS
     D  AB                     1     80
     D                                     DIM(80)
     D  ABDS                   1     80
      *___________
      * HODELOGIKK
      *"""""""""""
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  NLR              EXSR      GETSEG
     C   LR              GOTO      SLUTT
     C     SEGNAM        CASEQ     'UNB'         SRUNB
     C                   END
     C     SEGNAM        CASEQ     'UNH'         SRUNH
     C                   END
     C                   GOTO      SEGSTA
      *
     C     SLUTT         TAG
     C                   IF        ((XCUS = 'J') AND (XINV = 'J'))
     C                   EVAL      W0065 = 'CUSINV'
     C                   END
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *__________________________________________________________
      * SR for initiering                                  SRINIT
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
      * Parametre fra CL= MESSAGE TYPE OG MESSAGE NA,E
     C     *ENTRY        PLIST
     C                   PARM      W0065         W0065
      * INIT AV ARBEIDSFELT
     C                   MOVE      'N'           XCUS              1
     C                   MOVE      'N'           XINV              1
     C                   MOVE      'N'           OK                1            UNB--> J    elt.
     C                   MOVE      *ZEROS        NUMVRD           15 0          Gener. num felt.
     C                   MOVE      *ZEROS        NUTEGN            1 0          "TEGN" numerisk rep.
     C                   MOVE      *BLANKS       SEGNAM            3            Segment navn
     C                   MOVE      *BLANKS       W0065             6            MSG TYPE
     C                   READ      EDI85F                                 LR
     C   LR              GOTO      ENDINI
     C                   MOVE      RDATA         ABDS
     C     AB(1)         IFEQ      'ø'
     C     1             DO        2
     C                   READ      EDI85F                                 LR
     C   LR              GOTO      ENDINI
     C                   END
     C                   END
      *
     C                   MOVE      RDATA         ABDS
     C                   Z-ADD     81            TN                2 0
     C                   MOVE      'NO '         EOF               3             * FLAGG FOR EOF
     C                   MOVE      ' '           TEGN              1
     C                   MOVE      ' '           LOKAHE            1
      *
      * Spesielt reserverte tegn defineres her med både grafisk verdi og
      * med en logisk verdi. Den grafiske verdien testes det mot i rutinen
      * som henter tegn. Finner denne rutinen at at tegnet er ment som en
      * spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
      * (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
      * innkommende datastroemmen). '?' når brukt som opphevelse foran annet
      * spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
      * kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.
      *
      * Ved andre standarder for spesialtegn er det bare å bytte verdiene til
      * de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      * Logiske verdier for spesialtegn
      * LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
      * HVIS KOMMUNEDATA, INITIERE MED LOGAPP (GÅ RETT PÅ UNB)
     C                   MOVE      LOGAPP        TEGN
      * LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   BITON     '16'          LOGPLU            1
     C                   BITOFF    '023457'      LOGPLU
      * LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   BITON     '167'         LOGKOL            1
     C                   BITOFF    '02345'       LOGKOL
      *
     C     ENDINI        ENDSR
      *
      *__________________________________________________________
      * SR for å hente neste tegn i datastroem             GETCH
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETCH         BEGSR
      * Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
      * i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
      * LR settes når en ved "Toemming" finner at AB-arrayet er tomt.
      *
      * "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
      *Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
      *    98 ON -> kun grafisk betydning (uforandret)
      *    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   SETOFF                                           98
     C     STACH         TAG
      * Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            LR
     C                   GOTO      ENDCH
     C                   END
     C                   MOVE      ABDS          AADS
     C  N99              READ      EDI85F                                 99     *hent ny til AB
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF                             * FLAGG FOR EOF
     C                   Z-ADD     1             TN                              * Tegn-index-nr
     C                   END
      * Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      80
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   END
      * Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
      * Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
      *        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
      * Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
      * foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   END
     C                   END
     C                   END
      *
     C     ENDCH         ENDSR
      *
      *__________________________________________________________
      * SR for å hente neste Segment                       GETSEG
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETSEG        BEGSR
      * "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   ENDDO
      * "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
      * Har nå lest appostrof og kansje blank- de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C                   MOVEL     FELT          SEGNAM
      * La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
      * Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
      *__________________________________________________________
      * SR for å behandle UNB-segmentet                    SRUNB
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUNB         BEGSR
     C                   MOVE      *BLANKS       TEGN
     C                   DOW       TEGN <> LOGPLU
     C                   EXSR      GETCH
     C   LR              RETURN
     C     TEGN          CABEQ     LOGPLU        UNB010
     C                   ENDDO
      *
     C     UNB010        TAG
     C                   MOVE      *BLANKS       FE
     C     1             DO        35            NR
     C                   EXSR      GETCH
     C   LR              RETURN
     C                   IF        ((TEGN = LOGPLU) OR (TEGN = LOGKOL))
     C                   LEAVE
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   IF        ((FELT<>'NO000101') AND (FELT<>'NO000102'))
     C                   EVAL      *INLR = *ON
     C                   RETURN
     C                   END
      *
     C                   ENDSR
      *__________________________________________________________
      * SR for å behandle UNH-segmentet                    SRUNH
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUNH         BEGSR
     C     1             DO        15            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          IFEQ      LOGPLU
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        6             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      ((FELT = 'CUSDEC') OR (FELT = 'CUSRES'))
     C                   EVAL      XCUS = 'J'
     C                   WHEN      FELT = 'INVOIC'
     C                   EVAL      XINV = 'J'
     C                   ENDSL
      *
     C     ENDUNH        ENDSR
