*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
*****A**************************************************************
P8068 * 01 TH  PTF8 NY HELPFUNKSJON KUN DDS
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('SYKS130000' 'SYKS13' 'SY400-
S01  A*                                     LO/TRANS')
N01  A                                      HLPPNLGRP(HLPSCR SYKS13PNL)
N01  A                                      HLPTITLE('AVSETNINGSFORSLAG VED P-
N01  A                                      ERIODESLUTT')
     A                                      ALTHELP
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                  1  2'SYSPED/SYKS13A'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 JOV0228 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(024 JOV0227 *LIBL/SYMSGF)
     A            WSPERF         4Y 0B  4 28
     A  50                                  DSPATR(PC)
     A N50                                  DSPATR(PR)
     A            WSPERT         4Y 0B  4 33
     A N50                                  DSPATR(PR)
     A                                  6  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSVAVD         4Y 0B  6 28DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A N50                                  DSPATR(PR)
     A                                  6 35MSGCON(010 DIV1518 *LIBL/SYMSGF)
     A                                  9  2MSGCON(024 JOV0229 *LIBL/SYMSGF)
     A            WSDET          1A  B  9 28
     A N50                                  DSPATR(PR)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                  9 35MSGCON(005 JOV0066 *LIBL/SYMSGF)
     A                                 11  2MSGCON(024 JOV0060 *LIBL/SYMSGF)
     A            WSSORT         1A  B 11 28VALUES('1' '2')
     A N50                                  DSPATR(PR)
     A                                 11 35MSGCON(025 JOV0230 *LIBL/SYMSGF)
     A                                 12 35MSGCON(025 JOV0231 *LIBL/SYMSGF)
     A                                 14  2MSGCON(024 JOV0364 *LIBL/SYMSGF)
     A            WSFIL          1   B 14 28
     A N50                                  DSPATR(PR)
     A                                      VALUES(' ' 'J' 'Y' 'F')
     A                                 14 35MSGCON(025 JOV0365 *LIBL/SYMSGF)
     A                                 15 35MSGCON(025 JOV0366 *LIBL/SYMSGF)
     A                                 17  2MSGCON(024 DIV4058 *LIBL/SYMSGF)
     A  55
     AO 50                                  DSPATR(ND)
     A            WSPRT         10A  B 17 28
     A N50N55                               DSPATR(PC)
     A  55
     AO 50                                  DSPATR(PR)
     A  55
     AO 50                                  DSPATR(ND)
     A                                 18  2MSGCON(024 DIV4059 *LIBL/SYMSGF)
     A  55
     AO 50                                  DSPATR(ND)
     A            WSFORM        10A  B 18 28
     A  55
     AO 50                                  DSPATR(PR)
     A  55
     AO 50                                  DSPATR(ND)
     A                                 20  2MSGCON(024 DIV4060 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSJOBQ         1A  B 20 28
     A  55                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 22  2MSGCON(078 JOV0307 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
