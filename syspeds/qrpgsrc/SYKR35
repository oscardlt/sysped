********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 lagrer konto i faktfilen
10225 * 02 JOV PTF10 Melder turen avsluttet når avregningkjøres
      * 03 JOV tekster i FAKT ved godskriving
10178 * 04 JOV når avr-grunnlag er 0 (på hele turen) ble ikke cost oppdatert i fakt
10178 * 05 SH  FORDELING AVF LINJER ETTER DIREKTE FØRINGER
10178 * 06 SH  rettelse fordeling
10178 * 07 SH  UNNGÅ DIVIDE BY ZEROS TOTBEL
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 08 JOV rettelse ptf 06
10XXX * 09 SH  PTF10 eu-konteringer
********************************************************************
      *
      **    OPPDATERE TRANSPORTØRAVREGNING
      **    I IBM REGNSKAPSSYSTEM
      *
     H DATEDIT(*DMY)
N09  FKODTS2    IF   E           K DISK
N09  FKODTG3    IF   E           K DISK
N09  FSTED2     IF   E           K DISK
     FKODIL01   IF   E           K DISK
     FKODJL01   IF   E           K DISK
     FTRAVH3    UF   E           K DISK
     FTRAVV2    UF   E           K DISK
     FTRAN      IF   E           K DISK
     FTURER     IF   E           K DISK
     FDISP      IF   E           K DISK
     FKONTL01   IF   E           K DISK
     FKOSBU     IF   E           K DISK
     FKOSBL01   UF A E           K DISK
     FKOSAL02   UF A E           K DISK
     FKOSAL01   IF   E           K DISK
     F                                     RENAME(KOSAR:KOSARX)
     FKODTÅ     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTG2    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFIRMTEL   UF A E           K DISK
     FFIRTR     IF   E           K DISK
     FSYSRL01   UF   E           K DISK                                         X010
     FNUMML01   UF   E           K DISK                                         X010
     FKHEDL01   IF   E           K DISK
     FKUNDL01   UF   E           K DISK
     FCUNDL01   IF   E           K DISK
     F                                     RENAME(KUNDR:CUNDR)
     FLEVEL01   IF   E           K DISK
     FTRAVV5    IF   E           K DISK
     F                                     RENAME(TRAVVR:TRAVV5R)
     FWFAKTOR   UF A E           K DISK
     FFAKT      IF A E           K DISK
     FFAI3      IF A E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEA11R)
N02  FAVSLUTL2  IF A E           K DISK
     FSYKR35P   O    F  132        PRINTER OFLIND(*INOF)
     D AVA             S              4  0 DIM(90)
     D AVB             S             13  2 DIM(90)
     D*
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D AVRTXD          C                   CONST('*AVR*DIREKTE KOSTN. ')
     D AVRTXF          C                   CONST('*AVF*FORDELT KOSTN. ')
     D AVRTGD          C                   CONST('*AGD*GODSKR DIREKTE ')
     D AVRTGF          C                   CONST('*AGF*GODSKR FORDELT ')
     D                 DS
     D  DG                     1      2  0
     D  MN                     3      4  0
     D  AR                     5      6  0
     D  IBMDAT                 1      6  0
     D                 DS
     D  THDATO                 1      8  0
     D  THDTÅÅ                 1      4  0
     D  THDTMM                 5      6  0
     D  THDTDD                 7      8  0
     D                 DS
     D  IBMFAR                 1      4  0
     D  IBMFMN                 5      6  0
     D  IBMFDG                 7      8  0
     D  IBMFFD                 1      8  0
     D                 DS
     D  THDAFF                 1      8  0
     D  THDFÅÅ                 1      4  0
     D  THDFMM                 5      6  0
     D  THDFDD                 7      8  0
n02  D                 DS
n02  D  XULTID                 1     14  0
n02  D  XULTM                  1      6  0
n02  D  XULDD                  7      8  0
n02  D  XULMM                  9     10  0
n02  D  XULÅÅ                 11     14  0
n02  D                 DS
n02  D  UDATO                  1      8  0
n02  D  UAAR                   1      4  0
n02  D  UMND                   5      6  0
n02  D  UDAG                   7      8  0
     D                UDS
     D  UFIRMA                 1      2
     D*--------------------------------------------------------------
     D* DEFINERER DATASTRUKTURER
     D*--------------------------------------------------------------
     D                 DS
     D  MINPER                 1      6  0
     D  KOÅCC                  1      2  0
     D  KOÅÅR                  3      4  0
     D  KOÅMND                 5      6  0
     D                 DS
     D  KNAVN                  1     30
     D  KNAV25                 1     25
     D                 DS
     D  KG2LEV                 1      8  0
     D  KG2PRO                 1      4  4
     D  KG2BÆR                 5      8  0
     D                 DS
     D  BILTX2                 1     15
     D  BILT27                 1     27
     D                 DS
     D  IDAG1                  1      4  0
     D  IDAG2                  5      6  0
     D  IDAG3                  7      8  0
     D  IDAG                   1      8  0
     ICUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C                   EXSR      INIT
     C*HENT FØRSTE FAKTURA SOM SKAL OVERFØRES
     C                   SETOFF                                       30
     C     'F'           SETLL     TRAVH3
     C     'F'           READE(N)  TRAVH3                                 30
     C     *IN30         DOWEQ     '0'
     C                   EXSR      HOVED
     C* FLERE ??
     C     'F'           SETLL     TRAVH3
     C     'F'           READE(N)  TRAVH3                                 30
     C  N30              END
     C**
     C* PRINT TOTALER - HVIS MINST EN ER SKREVET.
     C     HEADOK        IFNE      ' '
     C                   EXSR      TOTSR
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C*
     C******************************************
     C     HOVED         BEGSR
     C******************************************
     C* DENNE RUTINE OVERFØRER EN HEL AVREGNING (FOR 1 TRANSP.)
     C* KOMMER INN HIT ETTER Å HA LEST 1. POST (TUR) PÅ AKT. AVREGN.
     C*
     C* SKJEKKER OM PERIODE VARELINJE ER ÅPEN
     C* I REGNSKAPET
     C                   MOVE      THMND         KOÅMND
     C                   MOVE      THÅR          KOÅÅR
     C                   MOVE      THCC          KOÅCC
     C* VARIABLE VAT
     C* SJEKK EVT AVVIKENDE MOMSSATS MOT DATO SAMMENSATT AV PERIODE/ÅR
     C* HVIS PERIODE MEDFØRER AVVIKENDE MOMSSATS, MÅ OGSÅ BILAGSDATO
     C* OVERSTYRES FOR Å FÅ RIKTIG MOMSSATS I LØSNING 1....
     C                   MOVE      31            IDAG3
     C     THMND         IFEQ      4
     C     THMND         OREQ      6
     C     THMND         OREQ      9
     C     THMND         OREQ      11
     C                   MOVE      30            IDAG3
     C                   ELSE
     C     THMND         IFEQ      2
     C                   MOVE      28            IDAG3
     C                   END
     C                   END
     C                   MOVE      THMND         IDAG2
     C                   Z-ADD     THÅR          IDAG1
     C                   MOVEL     THCC          IDAG1
     C     FITAX         ADD       1             FITAXN            5 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     IDAG          ANDLT     FITAXD
     C     FITAX2        ADD       1             FITAXN
     C                   MOVE      IDAG1         BILAAR
     C                   MOVE      IDAG2         BILMND
     C                   MOVE      IDAG3         BILDAG
     C                   END
     C*
     C     KODÅKE        CHAIN     KODTÅ                              33
     C  N33              MOVEL     KOÅCC         SÅR               4 0
     C  N33              MOVE      KOÅÅR         SÅR
     C  N33              MOVE      KOÅMND        SMND              2 0
     C   33              MOVE      MINPE2        SMND
     C   33              MOVEL     MINPE2        SÅR
     C                   MOVE      SÅR           SÅR2              2 0
     C*
     C* SKJEKK OM DENNE MÅNED HAR BILAGSNR
     C*
     C                   MOVE      SÅR           YYÅR
     C                   Z-ADD     SMND          YYMND
      * 69 ON -> SYSTEMTILDELT BILAGSNR FOR HOVEDBOK
     C     *IN69         IFEQ      '1'
     C     KEYBI         CHAIN(N)  KOSAL02                            44
     C  N44              MOVE      BILNR         SBNRNU
     C   44              EXSR      HENTBI
     C                   ELSE
      * 69 OFF-> BILAGSNR RESKONTRO--> BILAGSNR FOR HOVEDBOK
     C                   Z-ADD     THFAKT        BILNR
     C     KEYBI01       CHAIN     KOSAL01                            44
     C                   MOVE      BILNR         SBNRNU
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      FIFIRM        FIRMA
     C                   MOVE      SÅR           PERAAR
     C                   MOVE      SMND          PERNR
     C                   MOVE      *BLANKS       VALKOD
     C   44              EXSR      GETSUR
     C   44              WRITE     KOSAR
     C                   END
     C*
     C* SKRIV HEADER VED FØRSTE POSTERING
     C     HEADOK        IFEQ      ' '
     C                   EXCEPT    PHEAD
     C                   MOVE      'Y'           HEADOK            1
     C                   END
     C*
     C                   Z-ADD     THFAKT        IBMFAK            7 0
     C                   MOVE      THDTDD        DG
     C                   MOVE      THDTMM        MN
     C                   MOVE      THDTÅÅ        AR
     C                   MOVE      THDAFF        IBMFFD
     C     TRANKE        CHAIN     TRAN                               33
     C     FAVREG        IFEQ      'L'
     C                   MOVE      VMTRLE        VMTRKU
     C                   END
     C* ALLTID AVREGNING   I LOKAL VALUTA
     C                   MOVE      FICURR        RESVAL            3
     C                   MOVE      *BLANKS       TUBILK
     C                   MOVE      *ZEROS        RESVAB           11 2
     C                   MOVE      *ZEROS        MVAVAL
     C* INITIER KEY (STATUS F/BILAGSNR) FOR LES AV ALLE TURER FOR DENNE
     C* TRANSPORTØR (KUNDE/LEVERANDØR)
     C                   MOVE      THFAKT        XXFAKT
     C*
     C     KEYTH3        SETLL     TRAVH3
     C* GÅR I LØKKE OG LESER VARELINJER (EN SUMLINJE PR TUR)
     C     KEYTH3        READE     TRAVH3                                 33
     C     *IN33         DOWEQ     '0'
     C* OPPDATER REKV.POSTEN SOM OVERFØRT
     C     THRTNR        IFEQ      99999998
     C                   DELETE    TRAVHR
     C                   ELSE
     C                   MOVE      'O'           THST
     C                   MOVEL     SÅR           THCC
     C                   MOVE      SÅR           THÅR
     C                   Z-ADD     SMND          THMND
     C                   Z-ADD     FIJOU4        THJOUR
     C                   UPDATE    TRAVHR
N02   * Leg turen i avslut..
N02  C     THTUNR        CHAIN     AVSLUTL2                           33
n02  C     *in33         ifeq      '1'
n02  C                   CLEAR                   AVSLUTFR
n02  C                   TIME                    XULTID           14 0
n02  C                   MOVE      XULTM         AVTIME
n02  C                   MOVE      XULDD         UDAG
n02  C                   MOVE      XULMM         UMND
n02  C                   MOVE      XULÅÅ         UAAR
n02  C                   MOVE      UDATO         AVDATE
n02  C                   MOVE      ZUSER         AVUSER
n02  C                   MOVE      THTAVD        AVAVD
n02  C                   MOVE      THTUNR        AVPRO
n02  C                   WRITE     AVSLUTFR
n02  C                   END
     C                   END
     C*
     C* LEGG UT HOVEDBOKSPOSTEN PÅ LISTE / POSTERINGSFIL
     C     THRTNR        IFNE      99999998
     C* ORDINÆR AVREGNET TUR.....
     C                   EXSR      SKRTUR
     C                   ELSE
     C* VED UTFØRTE REKVISISJONER - BEHANDLING PÅ DETALJNIVÅ
     C                   EXSR      GODSKR                                       REKVIS.
     C                   END
     C* FLERE??
     C     KEYTH3        READE     TRAVH3                                 33
     C                   END
     C*
     C* __________________________________________________
     C* HAR NÅ BEHANDLET ALLE TURER FOR EN TRANSPORTØR
     C* """"""""""""""""""""""""""""""""""""""""""""""""""
     C*
     C* PLUSSER MVA  PÅ UTENSLANDSK BELØP
     C*(MEN FORELØPIG ER ALLTID TRANSP KRED.NOTA I LOKAL VALUTA)
     C     RESVAL        IFNE      FICURR
     C                   MOVE      MVAVAL        MVAGRL           13 2
     C                   MULT(H)   FITAXN        MVAVAL
     C                   SUB       MVAGRL        MVAVAL
     C                   ADD       MVAVAL        RESVAB
     C                   END
     C*
     C* LEGG UT RESKONTROPOSTEN PÅ LISTE / POSTERINGSFIL
     C     KUNNOK        IFNE      *ZEROS
     C                   EXSR      IBMTOT
     C                   ELSE
     C                   EXCEPT    DETNUL
     C                   END
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SKRTUR        BEGSR
     C***********************************************
     C                   EXSR      DECI
     C                   Z-ADD     THTOT         THTORI           13 2            ORIGINAL
     C                   MOVE      *ZEROS        THTHIT           13 2            HITTIL
      * FORDEL PÅ OPPDRAGENE.. FINN ANDEL AV KOSTN. PÅ FREMMEDE AVD.
     C                   EXSR      STALIN
     C*
     C     1             DO        90            AN                2 0
     C     AVA(AN)       CABEQ     *ZEROS        UTAVD
     C                   MOVE      AVA(AN)       KOVAVD
     C                   Z-ADD(H)  AVB(AN)       THTOT
     C                   ADD       THTOT         THTHIT
     C                   EXSR      SKRTU2
     C                   END
     C     UTAVD         TAG
     C* RESTEN SKAL PÅ TURENS AVDELING.
     C     THTORI        SUB       THTHIT        THTOT
     C     THTOT         IFNE      *ZEROS
     C                   MOVE      THTAVD        KOVAVD
     C                   EXSR      SKRTU2
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     SKRTU2        BEGSR
     C***********************************************
     C                   Z-SUB(H)  THTOT         XPFNOK           13 2
     C     TURKTH        CHAIN     TURER                              11        HENT BILKODE
     C* HENT STED,BÆRER,PROSJ FRA AVD.REG. / KONTO FRA VAREREGISTER
     C                   EXSR      KONTER
     C                   EXSR      IBM
     C                   ENDSR
     C***********************************************
     C     GODSKR        BEGSR
     C***********************************************
     C*
     C                   MOVE      THTAVD        XXTAVD                         (=*ZEROS)
     C                   SETON                                        67        *FOR TEKST
     C     TRAVVK        SETLL     TRAVV2
     C     TRAVVK        READE     TRAVV2                                 95
     C     *IN95         DOWEQ     '0'
     C                   MOVE      'O'           TVST
     C                   MOVE      THFAKT        TVFAKT
     C                   UPDATE    TRAVVR
     C* SPESIFIK TEKSTING FRA AVREGNINGS DETALJLINJE
     C                   MOVE      *BLANKS       BILT27
     C                   MOVEL     TVTXT         BILT27
     C* LEGG BELØP I FELT SOM BRUKES I SR IBM
     C                   Z-ADD     TVBLL         THTOT
     C* VIA KOSBU FOR Å SETTE TURERK
     C     TVBNRB        CHAIN     KOSBU                              11
     C   11              MOVE      *ZEROS        BUOAVD
     C   11              MOVE      *ZEROS        BUTAVD
     C   11              MOVE      *ZEROS        BUTUNR
     C   11              MOVE      *BLANKS       BUBILK
     C   11              MOVE      *BLANKS       BUBILN
     C     BUOAVD        IFNE      *ZEROS
     C                   MOVE      BUOAVD        BUTAVD
     C                   END
     C                   MOVE      BUTAVD        THTAVD
     C                   MOVE      BUTAVD        KOVAVD
     C* Ved transportørutlegg- Varekode/mva fra KOSBU
     C     BUOPD         IFEQ      *ZEROS
     C     BUTUNR        ANDEQ     *ZEROS
     C                   MOVE      BUVK          TUBILK
     C     TUBILK        IFEQ      *BLANKS
     C                   MOVE      'FRA'         TUBILK
     C                   END
     C                   MOVE      BUKDM         THKDM
      *Utlegg (just. utenom oppd/tur)  - dft Mvakode er = N
     C     BUKDM         IFNE      'J'
     C                   MOVE      'N'           THKDM
     C                   END
     C                   ELSE
     C* Godskriving utførte innh. m.m.
     C* DISSE TESTENE LÅ TIDLIGERE LENGER OPP(OGSÅ NÅR DET VAR UTLEGG)
     C* BILKODE FOR HOVEDTUREN ER ALLTID DEN RIKTIGE Å HENTE KONTO FRA
     C     TURERK        CHAIN     TURER                              11
     C* NÅR HENFØRING TIL ETT ENKELT OPPDRAG - før kostnad mot den avd.
     C     TVOPD         IFNE      *ZEROS
     C     DISPK         CHAIN     DISP                               11
     C  N11              MOVE      DIAVD         THTAVD
     C  N11              MOVE      DIAVD         KOVAVD
     C                   END
     C* Godskriving utførte innh. m.m. Antar i Norge DEFAULT= "J"
     C     BUKDM         IFEQ      'N'
     C                   MOVE      'N'           THKDM
     C                   ELSE
     C                   MOVE      'J'           THKDM
     C                   END
     C                   END
     C                   MOVEL     BUBILN        TUBILN
     C                   Z-SUB     THTOT         XPFNOK                         NEG.FORTEGN
     C                   EXSR      KONTER
     C                   EXSR      IBM
      * UTFØR OPPDAT. AV STATISTIKKLINJER.
     C                   EXSR      STALI2
     C     TRAVVK        READE     TRAVV2                                 95
     C  N95              END
     C*
     C                   SETON                                        67        *FOR TEKST
     C                   ENDSR
     C***********************************************
     C     STALI2        BEGSR
     C***********************************************
     C                   Z-ADD     TVBLL         OPDBEL
      * JUSTERING HENFØRT TIL OPPDRAG
     C     TVOPD         IFNE      *ZEROS
     C                   MOVE      TVOAVD        GMOAVD
     C                   MOVE      TVOPD         GMOPD
     C                   MOVE      'N'           DIFFJN            1
     C                   EXSR      DETLIN
     C                   MOVE      'N'           FINNAVD
     C                   EXSR      DELWF
     C                   ELSE
      * JUSTERING HENFØRT TIL TUR...
     C     BUTUNR        IFNE      *ZEROS
     C                   EXSR      TURLIN
     C                   END
      * hverken tur eller oppdrag - skal ikke lage FAKT-linjer
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DECI          BEGSR
     C***********************************************
     C* TEST FIRMS NUMBER OF DECIMALS
     C*
     C* NO DECIMALS
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  THTOT         FABEL0           11 0
     C                   Z-ADD     FABEL0        THTOT
     C                   ELSE
     C* 1 DECIMAL
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  THTOT         FABEL1           12 1
     C                   Z-ADD     FABEL1        THTOT
     C                   END
     C                   END
     C*
     C                   ENDSR
     C******************************************
     C     KONTER        BEGSR
     C******************************************
     C***                  Z-ADDTHTAVD    KOVAVD
     C     KODVKE        CHAIN     KODTV                              11
     C   11              MOVE      'N'           KOVPRO                         *PROSJ??
     C*
     C*  HENTER AVDELINGSOPPLYSNINGER OM KONTERING  STED/BÆRER
     C                   MOVE      KOVAVD        KOAAVD
     C     KODAKE        CHAIN     KODTA                              11
     C   11              MOVE      *ZEROS        KOABÆR
     C   11              MOVE      *ZEROS        KOAKON
     C*
     C* OPPLYSNING OM KONTERING FRA VAREKODEREG.
     C     GEBKEY        CHAIN     KODTGE                             11
     C  N11GEBKE2        CHAIN     KODTG2                             11
N09  C  N33EUKODE        IFNE      *BLANKS
N09  C     GEBKEYE       CHAIN     KODTG3                             33
N09  C                   SETOFF                                       33
N09  C                   END
     C*
     C*
     C*HVIS GEBYRERT ER BORTE BENYTTES KONTO 99999
     C*
     C   11              DO
     C                   MOVE      99999         KG2MOT
     C                   MOVE      99999         KG2UTG
     C                   MOVE      'J'           KGEPR
     C                   MOVE      *ZEROS        KGEUTK
     C                   MOVE      *ZEROS        KG2AVD
     C                   MOVE      *ZEROS        KG2BÆR
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C******************************************
     C     IBM           BEGSR
     C******************************************
     C*
     C* LEGGER OVER DEN AVDELING SOM KONTERES I
     C* DEN INTERNE SPEDISJONSAVDELING
     C*
     C                   MOVE      *ZEROS        IBMNOK
     C                   MOVE      KOAKON        FAVD              4 0
     C                   MOVE      KOABÆR        SBÆR
     C                   MOVE      'H'           IBMTYP            1
     C* HVIS GEBYRET FAST FØRES EN AVDELING
     C* TAST DETTE HENSYN TIL HER
     C*
     C     KG2AVD        IFNE      *ZEROS
     C                   MOVE      KG2AVD        FAVD
     C                   END
     C*
     C* HVIS GEBYRET FAST FØRES EN BÆRER
     C* TAS  DETTE HENSYN TIL HER
     C     KG2BÆR        IFNE      *ZEROS
     C                   MOVE      KG2BÆR        SBÆR
     C                   END
     C*
     C* EGEN KONTO UTEN MOMS
     C*
     C     KG2UTG        IFNE      *ZEROS
     C     KG2UTG        ANDNE     99999
     C     THKDM         ANDNE     'J'
     C                   MOVE      KG2UTG        KG2MOT
     C                   END
N01  C                   Z-ADD     KG2MOT        FAKTKONT          5 0
     C*
     C     KONTKE        CHAIN     KONTL01                            66
     C* FLYTTER 0 I BÆRER HVIS IKKE LOV
     C  N66STEDK         IFEQ      'N'
     C                   MOVE      *ZEROS        FAVD
     C                   END
     C*
     C* FLYTTER 0 I BÆRER HVIS IKKE LOV
     C*
     C  N66BARERK        IFEQ      'N'
     C                   MOVE      *ZEROS        SBÆR
     C                   END
     C*
     C* SNUR BELØPET PÅ POSTERINGEN
     C*
     C                   Z-SUB     XPFNOK        IBMNOK           11 2
     C*
     C* ØKER POST MED MOMS
     C     THKDM         IFEQ      'J'
     C                   MULT(H)   FITAXN        IBMNOK
     C                   ADD       XPFNOK        MVAVAL           13 2
     C                   Z-ADD     1             IBMMVA            1 0
     C                   ELSE
     C                   Z-ADD     0             IBMMVA
     C*
     C* VED AKTIVA/PASSIVA SKAL MOMS VÆRE 0
     C     KONTY         IFNE      'I'
     C     KONTY         ANDNE     'U'
     C                   Z-ADD     0             IBMMVA
     C                   END
     C*
     C                   END
     C*
     C                   MOVE      IBMMVA        SMVA
     C*
     C* OPPDATERER REGNSKAP HVIS IKKE BELØP = 0
     C*
     C     IBMNOK        IFNE      *ZEROS
     C                   ADD       IBMNOK        KUNNOK           11 2
     C*
     C*
     C* LEGGER UT VANLIG POST TIL INNTEKTER
     C*
     C* SKJEKKER OM PROSJEKTNUMMER SKAL LEGGES UT
     C*
     C     KOVPRO        IFEQ      'J'
     C     KGEPR         ANDNE     'N'
     C                   MOVE      'J'           XXPROS            1
     C                   ADD       1             IBMPNR
     C                   EXSR      GETSUR
     C                   EXSR      SRPOST
     C                   MOVE      THRTNR        PROSNR
     C                   MOVEL     'RUNDTUR '    BILTXT
     C                   MOVE      THRTNR        BILTXT
     C                   EXSR      STEBAR
     C                   EXCEPT    IBMNY1
     C                   GOTO      UTUTL
     C                   END
     C*
     C* ELLERS VANLIG SUMPOST TIL INNTEKTSKONTO   (SAMMENSLÅTT)
     C*
     C                   MOVE      'N'           XXPROS            1
     C                   Z-ADD     SMND          SMNPC
     C* HENTER BILAGSNR KUN VED SAMMENSLÅING
     C     FISADK        IFNE      'N'
     C     SKEY          CHAIN     KOSBL01                            44
     C                   ELSE
     C                   SETON                                        44
     C                   END
     C*
     C* HENTER REGLER FRA KONTOPLAN
     C*
     C   44              DO
     C     KONTKE        CHAIN     KONTL01                            66
     C                   ADD       1             IBMPNR
     C                   EXSR      GETSUR
     C                   EXSR      SRPOST
     C     FISADK        IFNE      'N'
     C                   MOVEL     'SAMLEPOS'    BILTXT
     C                   MOVE      'T      '     BILTXT
     C                   ELSE
     C                   MOVEL     'RUNDTUR '    BILTXT
     C                   MOVE      THRTNR        BILTXT
     C                   END
     C                   MOVE      *ZEROS        PROSNR
     C                   EXSR      STEBAR
     C                   EXCEPT    IBMNY1
     C                   END
     C  N44              ADD       IBMNOK        BBELOP
     C  N44              UPDATE    KOSBR
     C*
     C     UTUTL         TAG
     C*
     C*
     C* SKRIVER UT LISTE OVER POSTERING
     C*
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C                   EXSR      KDTFMT
     C     IBMNOK        COMP      *ZEROS                             66  66
     C                   EXCEPT    DET
     C                   EXSR      KDTFMT
     C*
     C                   END
     C*
     C     UTIBM         ENDSR
     C******************************************
     C     SRPOST        BEGSR
     C******************************************
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      FIFIRM        FIRMA
     C                   MOVE      SBNRNU        BILNR
     C                   Z-ADD     IBMPNR        POSNR
     C     AR            IFGT      50
     C     AR            ADD       1900          KRDAAR
     C                   ELSE
     C     AR            ADD       2000          KRDAAR
     C                   END
     C                   MOVE      MN            KRDMND
     C                   MOVE      DG            KRDDAG
     C                   MOVE      SÅR           PERAAR
     C                   MOVE      SMND          PERNR
     C                   MOVE      IBMTYP        TYPE
     C                   MOVE      BILTX2        BILTXT
     C                   MOVE      KG2MOT        KONTO
     C                   MOVE      FAVD          KSTED
     C                   MOVE      SBÆR          KBARER
     C                   MOVE      IBMMVA        MOMSK
     C                   Z-ADD     IBMNOK        BBELOP
     C                   Z-ADD     THRTNR        PROSNR
     C                   MOVE      'J'           OPDKOD
     C                   ENDSR
     C******************************************
     C     IBMTOT        BEGSR
     C******************************************
     C*
     C* OPPDATERER TOTALPOST I KUNDE/LEVERANDØRREGISTER
     C*
     C     FAVREG        IFNE      'L'
     C                   MOVE      'D'           IBMTYP
     C     KUNKEY        CHAIN     CUNDL01                            33
     C     KUNKEY        CHAIN     KUNDL01                            33
     C  N33UYEAR         IFGT      50
     C     UYEAR         ADD       1900          SISAAR
     C                   ELSE
     C     UYEAR         ADD       2000          SISAAR
     C                   END
     C  N33              MOVE      UMONTH        SISMND
     C  N33              MOVE      UDAY          SISDAG
     C  N33              UPDATE    KUNDR
     C  N33KHEKEY        CHAIN     KHEDL01                            33
     C  N33              MOVE      KONTOH        KG2MOT
     C   33              MOVE      99999         KG2MOT
     C                   ELSE
     C* Til LEVRESK-Feltet VMTRKU inneholder nå ikke kundenr men LEV.NR
     C                   MOVE      'K'           IBMTYP
     C     KUNKEY        CHAIN     LEVEL01                            33
     C  N33LHEKEY        CHAIN     KHEDL01                            33
     C  N33              MOVE      KONTOH        KG2MOT
     C   33              MOVE      99999         KG2MOT
     C                   MOVE      LNAVN         KNAVN
     C                   END
     C                   Z-SUB     KUNNOK        IBMNOK
     C                   MOVE      *ZEROS        KUNNOK
     C                   MOVE      *ZEROS        IBMMVA
     C                   MOVE      ' '           NOKKEL
     C   33              SETOFF                                       4546
     C  N33SPRAAK        COMP      'E'                                    45
     C  N33SPRAAK        COMP      'T'                                    46
     C*
     C*
     C*  LOKAL  VALUTA - VALUTAFELT I RESKONTRO SKAL IKKE OPPDATERES
     C*
     C     RESVAL        IFEQ      '   '
     C                   MOVE      *BLANKS       RESVAL
     C                   MOVE      *ZEROS        RESVAB
     C                   END
     C     RESVAL        IFEQ      FICURR
     C                   MOVE      *BLANKS       RESVAL
     C                   MOVE      *ZEROS        RESVAB
     C                   END
     C*
     C*  IKKE PROSJ PÅ RESKONTROPOST
     C                   MOVE      *ZEROS        THRTNR
     C                   MOVE      *BLANKS       TUBILN
     C                   MOVE      *ZEROS        FAVD
     C                   MOVE      *ZEROS        SBÆR
     C                   MOVE      KNAV25        BILT27
     C                   MOVEL     '**'          BILT27
     C* LEGG UT EN RESKONTPOST - ALDRI SNAKK OM SAMLEFAKTURASAMMENSLÅING
     C                   ADD       1             IBMPNR
     C                   EXSR      GETSUR
     C                   EXSR      SRPOST
     C                   MOVE      RESVAL        VALKOX
     C                   Z-ADD     RESVAB        BELOPV
     C                   MOVE      *ZEROS        REFNR
     C                   MOVE      IBMFAK        KRNR
     C                   MOVE      IBMFAR        FFDAAR
     C                   MOVE      IBMFMN        FFDMND
     C                   MOVE      IBMFDG        FFDDAG
     C                   MOVE      *BLANKS       BILTXT
     C     FAVREG        IFNE      'L'
     C                   MOVE      VMTRKU        RESNR
     C  N45
     CANN46              MOVEL     'Transp.k'    BILTXT
     C  N45
     CANN46              MOVE      'reditn.'     BILTXT
     C   45              MOVEL     'Transp.c'    BILTXT
     C   45              MOVE      'reditn.'     BILTXT
     C   46              MOVEL     'Transp.g'    BILTXT
     C   46              MOVE      'utschr.'     BILTXT
     C                   ELSE
     C                   MOVE      VMTRLE        RESNR
     C  N45
     CANN46              MOVEL     'Transp.a'    BILTXT
     C  N45
     CANN46              MOVE      'vregn. '     BILTXT
     C   45              MOVEL     'Transp. '    BILTXT
     C   45              MOVE      'costs  '     BILTXT
     C   46              MOVEL     'Transp. '    BILTXT
     C   46              MOVE      'kosten '     BILTXT
     C                   END
     C                   EXCEPT    IBMNY
     C*
     C* SETT INDIKATOR FOR TEKST PÅ LISTE...
     C                   SETON                                        67
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C                   EXSR      KDTFMT
     C     IBMNOK        COMP      *ZEROS                             66  66
     C                   EXCEPT    DET
     C                   EXSR      KDTFMT
     C                   SETOFF                                       67
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      DG            UDT1              2 0
     C                   MOVE      MN            UDT2              2 0
     C                   MOVE      AR            UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          DG
     C                   MOVE      UDT2          MN
     C                   MOVE      UDT3          AR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TOTSR         BEGSR
     C***********************************************
     C                   SETOFF                                       33
     C                   MOVE      *ZEROS        SMNPC
     C                   MOVE      *ZEROS        SÅR
     C                   MOVE      *ZEROS        SÅR2
     C     SKEY          SETLL     KOSBL01
     C*
     C                   READ(N)   KOSBL01                                33
     C     *IN33         DOWEQ     '0'
     C     BBELOP        IFGT      *ZEROS
     C                   ADD       BBELOP        DEBTOT           11 2
     C                   ELSE
     C                   ADD       BBELOP        KRETOT           11 2
     C                   END
     C                   READ(N)   KOSBL01                                33
     C  N33              END
     C*
     C                   EXCEPT    DEBKRE
     C*
     C     *IN69         IFEQ      '1'
     C   OF              EXCEPT    PHEAD
     C                   EXCEPT    PNYBIH
     C     *LOVAL        SETLL     KOSAL02
     C     LESA1         TAG
     C                   READ(N)   KOSAL02                                33
     C  N33
     CAN OF              EXCEPT    PHEAD
     C  N33              EXCEPT    PNYBI
     C  N33              GOTO      LESA1
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C******************************************
     C     HENTBI        BEGSR
     C******************************************
     C*
     C                   MOVE      'BILS'        NOK1              4
     C                   MOVE      '    '        NOK2              4
     C     NUMMKY        CHAIN     NUMML01                            61
     C     *IN61         IFEQ      '1'
     C                   EXSR      GMLNUM
     C                   END
     C  N61              DO
     C                   ADD       1             SBNRNU
     C                   UPDATE    NUMMR
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      FIFIRM        FIRMA
     C                   MOVE      SBNRNU        BILNR
     C                   MOVE      SÅR           PERAAR
     C                   MOVE      SMND          PERNR
     C                   MOVE      *BLANKS       VALKOD
     C                   EXSR      GETSUR
     C                   WRITE     KOSAR
     C* 1.SIDE HEADER ER IKKE SKREVET ENNÅ ??
     C     HEADOK        IFEQ      ' '
     C                   EXCEPT    PHEAD
     C                   MOVE      'Y'           HEADOK            1
     C                   END
     C* SKRIV PERIODENS BILAGSNR PÅ JOURNAL
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     GMLNUM        BEGSR
     C******************************************
     C     *LIKE         DEFINE    SÅR           TMPÅR
     C                   MOVE      SÅR           TMPÅR
      *
     C     SÅR           DOWGT     2002
     C                   SUB       1             SÅR
     C     NUMMKY        CHAIN     NUMML01                            61
     C  N61              LEAVE
     C                   END
      *
     C                   MOVE      TMPÅR         SÅR
     C                   ENDSR
     C*
     C*
     C******************************************
     C     GETSUR        BEGSR
     C******************************************
     C*
     C* HENT NYTT SURRUGATNR - FRA SYSRF LEGG I "POSLNR"
     C*
     C*
     C     SYSRKE        CHAIN     SYSRL01                            30
     C  N30              ADD       1             SISSGT
     C  N30              UPDATE    SYSRR
     C                   Z-ADD     SISSGT        POSLNR
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KOSAL02  (TILSV POSAL01 I SYFA35)
     C* (KOSAL01 BENYTTES I KOSTNADSOVERFØRING - DEN HAR IDENTISK
     C*  LAYOUT SOM IBM'S POSAL01 (FIRMA/BILNR) -
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    PERAAR        YYÅR
     C     *LIKE         DEFINE    PERNR         YYMND
     C     KEYBI         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    AKTKODK
     C                   KFLD                    YYÅR
     C                   KFLD                    YYMND
     C                   MOVE      'A'           AKTKODK           1
     C     KEYBI01       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    BILNR
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KUNDL01
     C*--------------------------------------------------------------
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VMTRKU
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL TRAN
     C*--------------------------------------------------------------
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    THTNR
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODTÅ   ÅPNE PERIODER
     C*--------------------------------------------------------------
     C     KODÅKE        KLIST
     C                   KFLD                    KOÅUNI
     C                   KFLD                    KOÅCC
     C                   KFLD                    KOÅÅR
     C                   KFLD                    KOÅMND
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE V
     C*--------------------------------------------------------------
     C     KODVKE        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    KOVAVD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE A
     C*--------------------------------------------------------------
     C     KODAKE        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    KOAAVD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE GE (GEBYRKODER)
     C*--------------------------------------------------------------
     C     GEBKEY        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    TUBILK
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE G2 (GEBYRKODER)
     C*--------------------------------------------------------------
     C     GEBKE2        KLIST
     C                   KFLD                    KG2UNI
     C                   KFLD                    TUBILK
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KONTL01
     C*--------------------------------------------------------------
     C     KONTKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KG2MOT
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KHEDL01
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    RECTYP        KHEKE2
     C     KHEKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KHEKE2
     C                   KFLD                    KHENV
     C                   MOVE      'D'           KHEKE2
     C     LHEKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LHEKE2
     C                   KFLD                    KHENV
     C     *LIKE         DEFINE    RECTYP        LHEKE2
     C                   MOVE      'F'           LHEKE2
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL POSTERINGSREGISTER KOSBL01
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    KBARER        SBÆR
     C     *LIKE         DEFINE    PERNR         SMNPC
     C     *LIKE         DEFINE    MOMSK         SMVA
     C     *LIKE         DEFINE    PROSNR        NULPRO
     C     SKEY          KLIST
     C                   KFLD                    SMNPC
     C                   KFLD                    SÅR
     C                   KFLD                    KG2MOT
     C                   KFLD                    FAVD
     C                   KFLD                    SBÆR
     C                   KFLD                    SMVA
     C                   KFLD                    NULPRO
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: TRAVH3
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    THST          XXOPKO
     C     *LIKE         DEFINE    THFAKT        XXFAKT
     C     KEYTH3        KLIST
     C                   KFLD                    XXOPKO
     C                   KFLD                    XXFAKT
     C                   MOVE      'F'           XXOPKO
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL TRAVV
     C*--------------------------------------------------------------
     C* SAMME "TUR" FOR UTFØRTE REKVISISJONER VIL GÅ IGJEN FRA
     C* AVREGNING  TIL AVREGNING - MÅ DERFOR BRUKE STATUS/FAKTNR FOR
     C* IKKE Å FÅ MED GAMLE - ALLEREDE OVERFØRTE DATA..
     C     *LIKE         DEFINE    THTAVD        XXTAVD
     C     TRAVVK        KLIST
     C                   KFLD                    XXTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    XXOPKO
     C                   KFLD                    XXFAKT
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL DISP
     C*--------------------------------------------------------------
     C     DISPK         KLIST
     C                   KFLD                    TVOAVD
     C                   KFLD                    TVOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL TURER V.ORDINÆR TUR
     C*--------------------------------------------------------------
     C     TURKTH        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL TURER V.GODSKRIV...
     C*--------------------------------------------------------------
     C     TURERK        KLIST
     C                   KFLD                    BUTAVD
     C                   KFLD                    BUTUNR
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL SYSRL01
     C*--------------------------------------------------------------
     C     SYSRKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    REGNA
     C                   MOVE      'SYS   '      REGNA             6
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL NUMML01
     C*--------------------------------------------------------------
     C     NUMMKY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    SÅR
     C                   KFLD                    NOK1
     C                   KFLD                    NOK2
N09  C     GEBKEYE       KLIST
N09  C                   KFLD                    EUKODE
N09  C                   KFLD                    KGEKOD
N09  C     GEBKEY3E      KLIST
N09  C                   KFLD                    EUKODE
N09  C                   KFLD                    TUBILK
      * TRAVV M/NULL I AVD/OPD SKAL IKKE LESES (REFORDELES SOM "DIFF")
     C     TRAV5K        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    XXTVST            1
     C                   KFLD                    XXOAVD
     C     *LIKE         DEFINE    TVOAVD        XXOAVD
     C                   Z-ADD     1             XXOAVD
     C     TRAV5E        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    XXTVST            1
     C     FAKKEY        KLIST
     C                   KFLD                    GMOAVD
     C                   KFLD                    GMOPD
     C                   KFLD                    XXLI
     C     *LIKE         DEFINE    FALI          XXLI
     C     HEAKEY        KLIST
     C                   KFLD                    GMOAVD
     C                   KFLD                    GMOPD
     C     *LIKE         DEFINE    FAAVD         GMOAVD
     C     *LIKE         DEFINE    FAOPD         GMOPD
     C     HEAKEW        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   MOVE      ZJOBNR        WFUNIK
     C                   MOVE      'N'           FINNAVD           1
     C                   EXSR      DELWF
     C*
     C* HENTER FIRMAOPPLYSNINGER
     C*
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRMA            2
     C     FITAX         ADD       1             FITAXN            5 4
     C* OVERFØR TIL DEBITOR/KREDITOR RESKONTRO ?????
     C                   READ      FIRTR                                  33
     C* HENTER JOURNALNUMMER
     C*
     C     FIFIRM        CHAIN     FIRMTEL                            33
     C                   ADD       1             FIJOU4
     C  N33              UPDATE    FIRMTR
     C   33              WRITE     FIRMTR
     C*
     C*
     C     FAVREG        COMP      'L'                                    68
     C*
     C*SY* INN SH HENTER MINSTE ÅPNE PERIODE
     C*
     C*
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          BILAAR
     C                   ELSE
     C     UYEAR         ADD       2000          BILAAR
     C                   END
     C                   MOVE      UMONTH        BILMND
     C                   MOVE      UDAY          BILDAG
     C                   MOVE      'K'           KOÅUNI
     C                   MOVE      *ZEROS        KOÅMND
     C                   MOVE      *ZEROS        KOÅÅR
     C                   MOVE      *ZEROS        KOÅCC
     C     KODÅKE        SETLL     KODTÅ
     C     'K'           READE     KODTÅ                                  33
     C     *IN33         IFEQ      '1'
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          MINPE2            6 0
     C                   ELSE
     C     UYEAR         ADD       2000          MINPE2
     C                   END
     C                   MULT      100           MINPE2
     C                   MOVE      UMONTH        MINPE2
     C                   ELSE
     C                   MOVE      MINPER        MINPE2
     C                   END
     C*
     C                   MOVE      *ZEROS        NULPRO
     C                   MOVE      'V'           KOVUNI
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      'GE'          KGEUNI
     C                   MOVE      'G2'          KG2UNI
     C*
     C                   MOVE      *ZEROS        IBMDAT
     C                   MOVE      *ZEROS        IBMFFD
     C                   MOVE      *ZEROS        THRTNR
     C                   MOVE      *ZEROS        KG2LEV
     C                   MOVE      *ZEROS        IBMPNR            5 0
     C*
     C                   MOVEL     'ORDINÆR '    ORDTUR           14
     C                   MOVE      'TUR: '       ORDTUR
      * SYSTEMTILDELT HOVEDBOKSBILAGSNR KUN NÅR VIA K.REK OG SAMMENSL.
      * 69 ON = SYSTEMTILDEL BILAGSNR HOVEDBOK....
      * 69 OFF= RESKONTRO-BILAGSNR = BILAGSNR HOVEDBOK....
     C     FISADK        IFNE      'N'
     C     FAVREG        ANDNE     'L'
     C                   SETON                                        69
     C                   ELSE
     C                   SETOFF                                       69
     C                   END
     C                   ENDSR
     C***********************************************
     C     STEBAR        BEGSR
     C***********************************************
     C     KODIKE        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KBARER
     C     KODJKE        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KSTED
     C                   MOVE      *BLANKS       GRKOPO
     C                   MOVE      *BLANKS       GRKODE
     C     KBARER        IFNE      *ZEROS
     C     KODIKE        CHAIN     KODIL01                            22
     C  N22              MOVE      GRKOTI        GRKOPO
     C                   END
     C     KSTED         IFNE      *ZEROS
     C     KODJKE        CHAIN     KODJL01                            22
     C                   END
     C                   ENDSR
     C******************************************
     C     STALIN        BEGSR
     C******************************************
     C                   MOVE      ' '           XXTVST
     C                   MOVE      'N'           DIFFJN            1
     C                   MOVE      *ZEROS        TOTBEL           13 2
     C                   MOVE      *ZEROS        TOTVKT           13 2
     C                   MOVE      *ZEROS        TOTOPD           13 2
     C                   MOVE      *ZEROS        OPDBEL           13 2
N04  C                   MOVE      *ZEROS        OPDFINS           9 0
N05  C                   MOVE      'J'           FORBEL            1
      * FLAGG OM AT BELØP ER AVTALT PÅ TUR (DIREKTE BELASTN. ER DA
      * IKKE HELT DIREKTE LIKEVEL, MEN "SLIK DET VILLE BLITT VED AVR.")
     C                   MOVE      ' '           TURAVT            1
     C     THTOT         IFEQ      THAVT
     C                   MOVE      'A'           TURAVT
     C                   END
      * LES DETALJ-LINJER, LEGG UT BRUDD PR. AVD/OPPD
     C     TRAV5K        SETLL     TRAVV5
     C     TRAV5E        READE     TRAVV5                                 94
     C                   MOVE      TVOAVD        GMOAVD
     C                   MOVE      TVOPD         GMOPD
     C     *IN94         DOWEQ     '0'
      * VED BRUDD. LEGG UT LINJE
     C     TVOAVD        IFNE      GMOAVD
     C     TVOPD         ORNE      GMOPD
     C                   EXSR      DETLIN
     C                   END
     C                   ADD       TVBLL         OPDBEL
     C                   MOVE      TVOAVD        GMOAVD
     C                   MOVE      TVOPD         GMOPD
N04  C                   ADD       1             OPDFINS
     C     TRAV5E        READE     TRAVV5                                 94
     C                   END
      * VED AVSLUTTET LESELOOP, LEGG UT SISTE BRUDD-SUM
S04  C*    OPDBEL        IFNE      *ZEROS
N04  C     OPDFINS       IFNE      *ZEROS
     C                   EXSR      DETLIN
     C                   END
      *
      * LEGG UT EVT DIFF MELLOM FORDELT, THTOT
     C                   EXSR      DETLI2
      *
     C                   ENDSR
      *
     C***********************************************
     C     DETLIN        BEGSR
     C***********************************************
      * HENT KUNDE SOM STATISTISK SKAL BELASTES (PRIMÆRT FAKT-LIN 00)
     C     HEAKEY        CHAIN     HEADF                              33
N09  C  N33              EXSR      EUSR
     C     FAKKEY        CHAIN     FAKT                               33
     C     *IN33         IFEQ      '0'
     C     FAKUNR        ANDNE     *zeros
     C     FASK          ANDNE     'I'
     C                   MOVE      FASK          XXSK              1
     C                   MOVE      FAKUNR        XXKUND           80
     C                   ELSE
     C     HEKNSF        IFNE      *ZEROS
     C                   MOVE      'S'           XXSK              1
     C                   MOVE      HEKNSF        XXKUND           80
     C                   ELSE
     C                   MOVE      'K'           XXSK              1
     C                   MOVE      HEKNKF        XXKUND           80
     C                   END
     C                   END
      *
     C                   CLEAR                   FAKTR
     C                   MOVE      GMOAVD        FAAVD
     C                   MOVE      GMOPD         FAOPD
     C                   MOVE      *ZEROS        FALI
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   MOVE      'FRA'         FAVK
     C* HENTER KONTONUMMER
N01  C                   Z-ADD     FAKTKONT      FAACCN
S03  C*    THTAVD        IFEQ      *ZEROS                                       GODSKR..
N03  C     THRTNR        IFEQ      99999998                                     GODSKR..
     C     DIFFJN        IFEQ      'N'
     C                   MOVE      AVRTGD        FAVT
     C                   ELSE
     C                   MOVE      AVRTGF        FAVT
     C                   END
     C                   ELSE                                                   HOVEDTUR..
     C     DIFFJN        IFEQ      'N'
     C                   MOVE      AVRTXD        FAVT
     C                   ELSE
     C                   MOVE      AVRTXF        FAVT
     C                   END
     C                   MOVE      TURAVT        FAVT
     C                   END
     C                   MOVE      XXSK          FASK
     C                   MOVE      XXKUND        FAKUNR
     C                   MOVE      'K'           FAKDA
     C                   MOVE      'NOK'         FAVAL
     C                   Z-SUB     OPDBEL        FABELN
     C                   Z-SUB     OPDBEL        FABELV
     C                   MOVE      THKDM         FAKDM
     C                   Z-ADD     THFAKT        FAFAKT
     C                   Z-ADD     THDATO        FADATO
     C                   Z-ADD     THDAFF        FADAFF
     C                   MOVE      'T'           FAOPKO
     C                   Z-ADD     THCC          FACC
     C                   Z-ADD     THÅR          FAÅR
     C                   Z-ADD     THMND         FAMND
     C                   Z-ADD     THJOUR        FAJOUR
     C     FAVREG        IFEQ      'L'
     C                   MOVE      VMTRLE        FALEVN
     C                   END
      *
N04  C     OPDBEL        IFNE      *ZEROS
     C                   WRITE     FAKTR
N04  C                   ENDIF
     C     FAINK         KLIST
     C                   KFLD                    FIFN
     C                   KFLD                    FIAVD
     C                   KFLD                    FIOPD
     C                   MOVE      FAAVD         FIAVD
     C                   MOVE      FAOPD         FIOPD
     C                   MOVE      FAFAKT        FIFN
     C     FAINK         CHAIN     FAI3                               38
     C   38              WRITE     FAINR
     C*
     C*
      *
      * LEGG UT FAKTOR (ANDEL AV TOTAL I W-FILE...)
      * (MEN KUN VED "HOVED-KJØRING" FOR TUREN, IKKE VED DIFF...)
     C     DIFFJN        IFEQ      'N'
     C     THTAVD        ANDNE     *ZEROS
     C                   MOVE      FAAVD         WFAVD
     C                   MOVE      FAOPD         WFOPD
     C                   Z-ADD     OPDBEL        WFBELØ
     C                   WRITE     WFAKTORR
     C                   ADD       OPDBEL        TOTBEL           13 2
N05  C     OPDBEL        IFLT      0
N05  C                   MOVE      'N'           FORBEL
N05  C                   END
      * AKKUM. OGSÅ TOTVKT/ANT OPPDR.(ALT. FORD.NØKKEL VED 0 I TOTBEL)
     C                   ADD       HEFBV         TOTVKT           13 2
     C                   ADD       1             TOTOPD           13 2
     C                   END
      *
     C                   MOVE      *ZEROS        OPDBEL           13 2
      *
     C                   ENDSR
     C*
     C***********************************************
     C     DETLI2        BEGSR
     C***********************************************
      *
     C* OPDBEL (KUN JUSTERINGEN) LEGGES UT SOM KOSTN.LINJE I FAKT2
     C* WBELØP JUSTERES/LAGRES - BENYTTES VED AVDELINGSFORDELING.
     C* DIFF MELLOM FORDELT/THTOT - REFORDEL ETTER SAMME FAKTOR...
      *
     C                   MOVE      'J'           DIFFJN            1
     C     THTOT         SUB       TOTBEL        DIFF             13 2
     C     THTOT         SUB       TOTBEL        DIFF2            13 2
     C     DIFF          IFNE      *ZEROS
      *
     C     WFUNIK        SETLL     WFAKTOR
     C     WFUNIK        READE     WFAKTOR                                33
     C     *IN33         DOWEQ     '0'
      *
N07  C     TOTBEL        IFEQ      0
N07  C                   MOVE      'N'           FORBEL
N07  C                   END
     C                   SELECT                                                 PRIOR. ETTER
      *
      * FORDELING BELØP
      *
S05  C*    TOTBEL        WHENNE    *ZEROS                                       ANDEL AV...
N05  C     FORBEL        WHENEQ    'J'                                          ANDEL AV...
     C     WFBELØ        DIV       TOTBEL        FAKTOR           19 9          1:"BIDRAG"
      *
      * FORDELING VEKT
      *
     C     TOTVKT        WHENNE    *ZEROS
     C     HEAKEW        CHAIN     HEADF                              33
N09  c  N33              exsr      eusr
     C     HEFBV         DIV       TOTVKT        FAKTOR           19 9          2:VEKT
      *
      * FORDELING OPPPDRAG
      *
     C     TOTOPD        WHENNE    *ZEROS
     C     1             DIV       TOTOPD        FAKTOR           19 9          3:ANT OPPDR.
      *
      * OTHER
      *
     C                   OTHER
      * INGEN OPPDRAG PÅ TUREN - KAN IKKE OPPDATERE STAT.
     C                   GOTO      NEXTW
     C                   ENDSL
      *
      * SLUTT SELECT
      *
     C     DIFF          MULT      FAKTOR        OPDBEL
     C                   ADD       OPDBEL        WFBELØ
     C                   SUB       OPDBEL        DIFF2
     C     DIFF2         IFLE      1
     C     DIFF2         ANDGE     -1
     C                   ADD       DIFF2         OPDBEL
     C                   ADD       DIFF2         WFBELØ
     C                   MOVE      *ZEROS        DIFF2
     C                   END
     C                   MOVE      WFAVD         GMOAVD
     C                   MOVE      WFOPD         GMOPD
     C                   EXSR      DETLIN
     C                   UPDATE    WFAKTORR
     C     DIFF2         CABEQ     *ZEROS        UTDIFF
     C     NEXTW         TAG
     C     WFUNIK        READE     WFAKTOR                                33
     C                   END
     C                   END
     C*
     C     UTDIFF        TAG
     C                   MOVE      'J'           FINNAVD
     C                   EXSR      DELWF
     C                   ENDSR
     C***********************************************
     C     DELWF         BEGSR
     C***********************************************
      * RUTINEN GJØR 2 TING...
      * A: TØMMER WORKFILE WFAKTOR
      * B: HVIS PARAM "FINNAVD" = J SÅ LEGGES "FREMMEDE AVDELINGER"
      *    (ULIK TURENS)/BELASTET BELØP I ARRAYS (MAX 90 POSTER)
      *    DETTE BENYTTES TIL Å SPLITTE BELASTNING PR AVD/KSTED I ØKØ.
     C                   CLEAR                   AVA
     C                   CLEAR                   AVB
     C                   MOVE      *ZEROS        AN
     C                   Z-SUB     1             GMAVD             4 0
     C                   MOVE      *ZEROS        SUMBEL           13 2
     C     WFUNIK        SETLL     WFAKTOR                                            **
     C     WFUNIK        READE     WFAKTOR                                33          **
     C     *IN33         DOWEQ     '0'                                                **
     C     FINNAVD       IFEQ      'J'
     C     GMAVD         IFEQ      -1
     C                   MOVE      WFAVD         GMAVD
     C                   END
     C     WFAVD         IFNE      GMAVD
     C     GMAVD         IFNE      THTAVD
     C     SUMBEL        ANDNE     *ZEROS
     C                   ADD       1             AN
     C                   MOVE      GMAVD         AVA(AN)
     C                   MOVE      SUMBEL        AVB(AN)
     C                   END
     C                   MOVE      *ZEROS        SUMBEL
     C                   END
     C                   MOVE      WFAVD         GMAVD
     C                   ADD       WFBELØ        SUMBEL
     C                   END
     C                   DELETE    WFAKTORR                                           **
     C     WFUNIK        READE     WFAKTOR                                33          **
     C                   END                                                          **
      *
      * LEGG UT "BRUDD" VED EOF.
     C     GMAVD         IFNE      THTAVD
     C     SUMBEL        ANDNE     *ZEROS
     C                   ADD       1             AN
     C                   MOVE      GMAVD         AVA(AN)
     C                   MOVE      SUMBEL        AVB(AN)
     C                   END
     C                   ENDSR
     C***********************************************
     C     TURLIN        BEGSR
     C***********************************************
      * SIMULERER EN DIFF ETTER NORMALFORDELING....
      * BRUK WFIL "WFAKTOR" FOR Å FORDELE ETTER F.VEKT.
     C                   MOVE      *ZEROS        TOTBEL
N06  C                   MOVE      *ZEROS        TOTVKT
     C     BUTUNR        SETLL     HEAD11
     C     BUTUNR        READE     HEAD11                                 33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      HEAVD         WFAVD
     C                   MOVE      HEOPD         WFOPD
     C                   Z-ADD     HEFBV         WFBELØ
s08  C*                  Z-ADD     HEFBV         TOTVKT
     C                   WRITE     WFAKTORR
     C                   ADD       HEFBV         TOTBEL
N08  C                   ADD       HEFBV         TOTVKT
     C     BUTUNR        READE     HEAD11                                 33
     C                   END
      *
      * "TOTBEL" ER NÅ SAMLET VEKT.
      * VED Å TRIKSE INN AKTUELT BELØP PÅ TOPPEN VIL SR "DETLI2" KUNNE
      * KJØRES UFORANDRET (DENNE FINNER DIFF VED THTOT MINUS TOTBEL!)
     C     TOTBEL        IFNE      *ZEROS
     C     TOTBEL        ADD       TVBLL         THTOT
     C                   EXSR      DETLI2
     C                   ELSE
     C                   MOVE      'N'           FINNAVD
     C                   EXSR      DELWF
     C                   END
      *
     C                   ENDSR
     C*
N09  C***********************************************
N09  C     EUSR          BEGSR
N09  C***********************************************
N09  C                   MOVE      *BLANKS       EUKODE            1
N09   * SPESIALKONTERING EU
N09  C     KODTS2K       KLIST
N09  C                   KFLD                    KS2UNI
N09  C                   KFLD                    KS2LK
N09  C                   MOVEL     'S2'          KS2UNI
N09  C
N09   *
N09   * unntak for eu (KS2PRE=B)
N09   *
N09  C                   MOVE      FILAND        KS2LK
N09  C     KODTS2K       CHAIN     KODTS2                             33
N09   *
N09   * EGET LAND ER EU
N09   *
N09  C  N33KS2PRE        IFEQ      'B'
N09   *
N09   *   IMPORT
N09   *
N09  C     HEUR          IFEQ      'A'
N09  C     HEUR          OREQ      'C'
N09  C     HEUR          OREQ      'F'
N09  C                   MOVE      HELKA         FIRLKF
N09  C     KOAPOS        IFEQ      'J'
N09  C                   MOVE      FILAND        FIRLKT
N09  C                   ELSE
N09  C     HESDT         CHAIN     STED2                              33
N09  C                   MOVE      ST2LK         FIRLKT
N09  C                   END
N09  C                   END
N09   *
N09   *   EKSPORT
N09   *
N09  C     HEUR          IFEQ      'B'
N09  C     HEUR          OREQ      'D'
N09  C     HEUR          OREQ      'G'
N09  C                   MOVE      HELKA         FIRLKT
N09  C     KOAPOS        IFEQ      'J'
N09  C                   MOVE      FILAND        FIRLKF
N09  C                   ELSE
N09  C     HESDT         CHAIN     STED2                              33
N09  C                   MOVE      ST2LK         FIRLKF
N09  C                   END
N09  C                   END
N09   *
N09   * TRANSPORTDISPONERING
N09   *
N09  C     HEUR          IFEQ      'H'
N09  C                   MOVE      HELKA         FIRLKF            2
N09  C                   MOVE      HETRI         FIRLKT            2
N09  C                   END
N09   *
N09   * DOMESTIC
N09   *
N09  C     FILAND        IFEQ      FIRLKF
N09  C     FILAND        ANDEQ     FIRLKT
N09  C                   MOVE      'D'           EUKODE
N09  C                   END
N09   *
N09   *
N09   * TRANSIT  BEGGE LAND I EU
N09   *
N09  C     FIRLKF        IFNE      FIRLKT
N09  C                   MOVE      FIRLKF        KS2LK
N09  C     KODTS2K       CHAIN     KODTS2                             33
N09  C  N33KS2PRE        IFEQ      'B'
N09  C                   MOVE      FIRLKT        KS2LK
N09  C     KODTS2K       CHAIN     KODTS2                             33
N09  C  N33KS2PRE        IFEQ      'B'
N09  C                   MOVE      'T'           EUKODE
N09  C                   END
N09  C                   END
N09  C                   END
N09   *
N09  C                   END
N09  C                   SETOFF                                       33
N09  C                   ENDSR
     O*********
     O* TIL IBM REGNSKAP    RESKONTO
     O***********'
     OKOSBR     EADD         IBMNY
     O                       AKTKOD
     O                       FIRMA
     O                       BILNR
     O                       POSNR
     O                       BILAAR
     O                       BILMND
     O                       BILDAG
     O                       PERAAR
     O                       PERNR
     O                       TYPE
     O                       RESNR
     O                       BILTXT
     O                       KONTO
     O                       KSTED
     O                       KBARER
     O                       MOMSK
     O                       BBELOP
     O                       VALKOX
     O                       BELOPV
     O                       REFNR
     O                       KRNR
     O                       KRDAAR
     O                       KRDMND
     O                       KRDDAG
     O                       FFDAAR
     O                       FFDMND
     O                       FFDDAG
     O                       OPDKOD
     O                       POSLNR
     O*********
     O* TIL IBM REGNSKAP    HOVEDBOK  INNTEKTSPOST
     O* (EVT TILLEGG VED SAMMENSLÅING SKJER VIA UPDAT I PROGRAMMET)
     O***********'
     OKOSBR     EADD         IBMNY1
     O                       AKTKOD
     O                       FIRMA
     O                       BILNR
     O                       POSNR
     O                       BILAAR
     O                       BILMND
     O                       BILDAG
     O                       PERAAR
     O                       PERNR
     O                       TYPE
     O                       BILTXT
     O                       KONTO
     O                       KSTED
     O                       KBARER
     O                       MOMSK
     O                       BBELOP
     O                       PROSNR
     O                       OPDKOD
     O                       NOKKEL
     O                       POSLNR
     O                       GRKODE
     O                       GRKOPO
     O*
     OSYKR35P   E            PHEAD            01
     O                       FIFIRM               2
     O                       FIFT                48
     O                                           90 'JOURNALNR:'
     O                       FIJOU4        Z     97
     O                                          105 'SIDE:'
     O                       PAGE          Z    111
     OSYKR35P   E            PHEAD            02
     O                                           24 'T R A N S P O R T Ø R  A'
     O                                           48 ' V R.   J O U R N A L   '
     O                                           60 'DATO:'
     O                       UDATE         Y     68
     OSYKR35P   E            PHEAD            03
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     O          E            PHEAD            04
     O               69                           7 'FAKTNR'
     O              N69                           7 'BIL.NR'
     O                                           16 'FAKTDATO'
     O                                           25 'STE/BÆR'
     O                                           32 'KONTO'
     O                                           35 'MK'
     O                                           49 'D  E  B  E  T'
     O                                           63 'K R E D I T'
     O                                           89 'T E K S T...............'
     O                                          102 'BILNUMMER'
     O                                          115 'RUNDTUR '
     O                                          117 'PR'
     O              N68                         126 'KUNDENR'
     O               68                         126 'LEVER.NR'
     O                                          131 'PER'
     OSYKR35P   E            PHEAD          1 05
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OSYKR35P   E            DET            1
     O                       IBMFAK        Z      7
     O                       IBMDAT              15
     O                       FAVD          Z     21
     O                       SBÆR          Z     26
     O                       KG2MOT              32
     O                       IBMMVA              34
     O               66      IBMNOK        J     49
     O              N66      IBMNOK        J     64
     O              N67      ORDTUR              79
     O              N67      KOVAVD        Z     83
     O              N67                          84 '/'
     O              N67      THTUNR        Z     92
     O               67      BILT27              92
     O                       TUBILN             103
     O                       THRTNR        Z    114
     O                       XXPROS             116
     O                       VMTRKU        Z    126
     O                       SMND               129
     O                                          130 '/'
     O                       SÅR2               132
     O          E            DETNUL         1
     O                       IBMFAK        Z      7
     O                                           31 '   * NULL AVREGNING *   '
     OSYKR35P   E            PNYBIH      2  2
     O                                           24 '** BILAGSNUMMER BENYTTET'
     OSYKR35P   E            PNYBI       0  1
     O                       PERAAR              13
     O                       PERNR               10
     O                                           11 '/'
     O                                           15 '='
     O                       BILNR         Z     24
     OSYKR35P   E            DEBKRE      2
     O                                           24 '**DEB/KRED POSTTOTAL(TIL'
     O                                           31 ' ØKO)**'
     O                       DEBTOT        JB    49
     O                       KRETOT        JB    64
