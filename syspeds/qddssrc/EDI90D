*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9XXXA* 01 YBC PTF9 OPSJON FOR Å KOBLE KRYSSEREF
P9XXXA* 02 YBC PTF10 F10 = VEDLIKEHOLD XREEF
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/HAGF)
     A                                      PRINT
     A                                      ALTHELP
     A                                      ALTPAGEUP
     A                                      ALTPAGEDWN
     A                                      HELP
      *
     A          R EDI90B20
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Exit')
     A                                      CF03(03 'Exit')
N02  A                                      CF10(10 'XREF')
     A                                      SLNO(23)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 23MSGCON(018 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N02  A                                  1 42MSGCON(030 JOV1559 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R EDI90H30                  SFL
     A                                      SFLNXTCHG
     A            RECNUM        10S 0H
N01  A            XOPT           1   B  7  2
N01  A  45                                  DSPATR(PR)
N01  A  45                                  DSPATR(ND)
N01  A            N3039         17   H
     A            SFFELT        75   O  7  4
      *
     A          R EDI90K40                  SFLCTL(EDI90H30)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Exit')
N02  A                                      CF10(10 'XREF')
N01  A                                      CF12(12 'Exit')
     A                                      OVERLAY
     A                                      PUTOVR
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A                                  1  3'EDI90'
     A                                  1 26MSGCON(026 EDI0305 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61MSGCON(005 EDI0306 *LIBL/SYMSGF)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 61MSGCON(005 EDI0307 *LIBL/SYMSGF)
     A                                  2 70TIME
N01  A                                  3  3'Opsjon X=Koble ref.'
N01  A                                      COLOR(BLU)
     A                                  4  3MSGCON(023 EDI0308 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  4MSGCON(042 EDI0309 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
N01   *
N01  A          R EDI90R50
N01  A                                      WINDOW(*DFT 6 45 *NOMSGLIN)
N01  A                                      OVERLAY
N01  A                                      CF04(04 'Søking')
N01  A                                      CF06(06 'Koble')
N01  A                                      CF12(12 'Avslutt program')
N01  A                                      CSRLOC(LINNBR     POSNBR)
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
N01  A                                  1  2'EDI90R'
N01  A                                      COLOR(BLU)
N01  A                                  1 10'Koble krysseref.'
N01  A                                      DSPATR(HI)
N01  A                                  2  2'Kundenr .......:'
N01  A            WSKN           8Y 0B  2 19DSPATR(UL)
N01  A  50                                  DSPATR(PR)
N01  A                                      EDTCDE(4)
N01  A  90                                  DSPATR(RI)
N01  A                                  3  2'Kundes kundenr :'
N01  A            N3039         17A  O  3 19DSPATR(UL)
N01  A                                  4  2'Kundenr/vadr.nr:'
N01  A            WSKN2          8Y 0B  4 19DSPATR(UL)
N01  A                                      EDTCDE(4)
N01  A  89                                  DSPATR(RI)
N01  A                                  4 28'/'
N01  A            WSADRN         5Y 0B  4 30DSPATR(UL)
N01  A                                      EDTCDE(4)
N01  A  88                                  DSPATR(RI)
N01  A                                  5  2'F4=Søk   F6=Koble   F12=Avbryt'
N01  A                                      COLOR(BLU)
N01  A  30        MSGTXT        40A  O  6  2MSGID(&MSGNR SYMSGF)
N01  A                                      COLOR(WHT)
N01  A            MSGNR          7A  P
