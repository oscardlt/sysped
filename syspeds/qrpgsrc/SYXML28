
     ‚***********************************************************
     ‚*** PROGRAM SKAL KONVERTER XML-FIL TIL HEADF OG ZADV    ***
     ‚*** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
     ‚*** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
     ‚*** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
     ‚***********************************************************

     ffirm      if   e           k disk
     fxmlrcvf   if   e           k disk
     fxmlrcvf01 if   e           k disk    rename(xmlrcvfr:xmlrcvfr01)
     fxmlrcaf   if   e           k disk
     fheadf     uf a e           k disk
     fsadh      if   e           k disk
     fzadv      uf a e           k disk
     fsaeh      if   e           k disk
     fzaev      uf a e           k disk
     fdisp      o  a e           k disk
     ffrisok    uf a e           k disk
     ffrisol01  if   e           k disk    rename(frisokr:frisokr1)
     fenadsf    uf a e           k disk
     fedic      uf   e             disk
     fedim      o  a e             disk
     ftrackf    o  a e             disk
     ftell      uf   e           k disk
     fztell     uf   e           k disk

     d                 ds
     d  xtxt                   1     15
     d  xt                     1     15    dim(15)
     d                 ds
     d xtal                          15  3
     d  xtalh                        12  0 overlay(xtal)
     d  xtald1                        1    overlay(xtal:13)
     d  xtald2                        1    overlay(xtal:14)
     d  xtald3                        1    overlay(xtal:15)
     d                 ds
     d x14s                          14
     d  heavd                         4  0 overlay(x14s)
     d   diavd                        4  0 overlay(heavd)
     d    fsavd                       4  0 overlay(diavd)
     d     navd                       4  0 overlay(fsavd)
     d      ttavd                     4  0 overlay(navd)
     d  heopd                         7  0 overlay(x14s:5)
     d   diopd                        7  0 overlay(heopd)
     d    fsopd                       7  0 overlay(diopd)
     d     nopd                       7  0 overlay(fsopd)
     d      ttopd                     7  0 overlay(nopd)
     d  hesg                          3    overlay(x14s:12)
     d   disign                       3    overlay(hesg)
     d                 ds
     d hedtop                         8  0
     d  fsdtop                        8  0 overlay(hedtop)
     d                 ds
     d xxdt                           8  0
     d   xxdta                        4  0 overlay(xxdt)
     d   xxdtm                        2  0 overlay(xxdt:5)
     d   xxdtd                        2  0 overlay(xxdt:7)
     d                 ds
     d hevs1                         25
     d  hevs122                      22    overlay(hevs1:4)
     d                 ds
     d xhedtmo                        8
     d  xhedtmoÅ                      4    overlay(xhedtmo)
     d  xhedtmom                      2    overlay(xhedtmo:5)
     d  xhedtmod                      2    overlay(xhedtmo:7)
     d                sds
     d zuser                 254    263
     ‚*  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
     ‚*  Prototype for syxml28
     d syxml28         pr
     d usn_                                like(usn)
     ‚*  *ENTRY Interface for Main Procedure
     d syxml28         pi
     d usn                            9
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d wdt             s              8
     d wdt08           s              8  0
     d wtm             s              6
     d wtm06           s              6  0
     d xavsmot         s              8
     d xhegnn          s                   like(hegnn)
     d xknr            s              8  0
     d xn              s              3  0
     d xan02           s              2
     d xan04           s              4
     d xslutt          s              1
     d xikkeopd        s              1
     d xnyopd          s              1
     d xpos            s              5  0
     d xxavd           s              4  0
     d xxli            s                   like(svli)
     d xxreff          s                   like(svreff)
     d xxunik          s                   like(svunik)
     d xximp           s              1
     d xx_fskode       s              3
     d xx_fssok        s             35
     d x001_xrnv       s                   like(xrnv)
     d x001_xrlev1     s                   like(xrlev1)
     d x001_xrlev2     s                   like(xrlev2)
     d x001_xrlev3     s                   like(xrlev3)
     d x001_xrlev4     s                   like(xrlev4)
     d x002_xrlev1     s                   like(xrlev1)
     d x002_xrlev2     s                   like(xrlev2)
     d x002_xrlev3     s                   like(xrlev3)
     d x002_xrlev4     s                   like(xrlev4)
     d x003_xrlev1     s                   like(xrlev1)
     d x003_xrlev2     s                   like(xrlev2)
     d x003_xrlev3     s                   like(xrlev3)
     d x003_xrlev4     s                   like(xrlev4)
     d x003_xrlev5     s                   like(xrlev5)
     d xx_svvt01       s                   like(svvt01)
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------

      /free
       exsr init;

       exsr srxml1;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml1;
         //**********************************************
         exsr srfinninf;

         // HENT MELDINGS REFERANSE
         x001_xrlev1 = 1;
         x001_xrlev2 = 1;
         x001_xrlev3 = 1;
         x001_xrlev4 = 0;
         xslutt = 'N';
         dow xslutt = 'N';
           x001_xrlev1 = x001_xrlev1 + 1;
           setll ( xrsn : x001_xrlev1 : x001_xrlev2 : x001_xrlev3 :
           x001_xrlev4 ) xmlrcaf;
           reade ( xrsn : x001_xrlev1 : x001_xrlev2 : x001_xrlev3 :
           x001_xrlev4 ) xmlrcaf;
           if %eof;
             xslutt = 'J';
           else;
             dow not %eof;
               if xratn = 'consignmentId';
                 fssok  = xratv;
                 exsr srxml2;
                 leave;
               endif;
               reade ( xrsn : x001_xrlev1 : x001_xrlev2 : x001_xrlev3 :
               x001_xrlev4 ) xmlrcaf;
             enddo;
           endif;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
         //**********************************************

         xikkeopd = 'N';
         xan04 = %subst(fssok:1:4);
         xxavd = %int(xan04);
         if xxavd = 0;
           xxavd = 9999;
         endif;
         xxavd = 1;
         x002_xrlev1 = xrlev1;
         x002_xrlev2 = xrlev2;
         x002_xrlev3 = 1;
         x002_xrlev4 = 0;

         chain ( xrsn : x002_xrlev1 : x002_xrlev2 : x002_xrlev3 : x002_xrlev4
         ) xmlrcvf;
         dow %found;                                                            // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'Consignment';                                           //  2<-B
             // HENT INFOR. FRA <Consignment>
             exsr srxml301;
             if xikkeopd = 'J';
               leave;
             endif;
           when xrnv = 'CustomsClearance';                                      //  2<-B
             exsr srxml302;
           when xrnv = 'ConsignmentGoodsItemSet';                               // 2
             // HENT INFOR. FRA <ConsignmentGoodsItemSet>
             exsr srxml303;
           when xrnv = 'Party';                                                 // 2
             // HENT INFOR. FRA <Party>
             exsr srxml304;
           when xrnv = 'TransportLeg';                                          // 2
             // HENT INFOR. FRA <TransportLeg>
             exsr srxml305;
           when xrnv = 'TermsOfDelivery';                                       // 2
             // HENT INFOR. FRA <TermsOfDelivery>
             exsr srxml306;
           when xrnv = 'DateAndTimes';                                          // 2
             // HENT INFOR. FRA <DateAndTimes>
             exsr srxml307;
           endsl;                                                               //  2<-E
           x002_xrlev3 = x002_xrlev3 + 1;
           chain ( xrsn : x002_xrlev1 : x002_xrlev2 : x002_xrlev3 : x002_xrlev4
           ) xmlrcvf;
         enddo;                                                                 // 1<-E

         if xikkeopd <> 'J';
           exsr sropdhea;
         endif;

       endsr;

       //**********************************************
       begsr srxml301;
         //**********************************************

         chain ( fskode : fssok ) frisol01;
         if not %found;
           xx_fskode = fskode;
           xx_fssok  = fssok;
           clear frisokr;
           fskode = xx_fskode;
           fssok  = xx_fssok;
           clear headfr;
           clear dispr;
           heavd = xxavd;
           chain heavd tell;
           heopd = teopdn;
           teopdn = teopdn + 1;
           update tellr;
           hedtr = wdt08;
           hedtop = wdt08;
           trknfa = 20000000;
           hekna  = trknfa;
           xnyopd = 'J';
           write frisokr;
           if xximp = ' ';
             fskode = '*CE';
             write frisokr;
             fskode = '*CR';
           endif;
         else;
           select;
           when xximp = 'I';
             chain ( fsavd : fsopd ) sadh;
           when xximp = 'E';
             chain ( fsavd : fsopd ) saeh;
           other;
             chain ( fsavd : fsopd ) sadh;
             if not %found;
               chain ( fsavd : fsopd ) saeh;
             endif;
           endsl;
           if %found;
        //  ikke oppdater pga. at fortolling allerede er laget.
             xikkeopd = 'J';
           else;
             setll ( fsavd : fsopd ) frisok;
             reade ( fsavd : fsopd ) frisok;
             dow not %eof;
               if fskode = '*CR' or fskode = '*CE';
                 update frisokr;
               else;
                 delete frisokr;
               endif;
               reade ( fsavd : fsopd ) frisok;
             enddo;
             setll ( fsavd : fsopd ) enadsf;
             reade ( fsavd : fsopd ) enadsf;
             dow not %eof;
               delete nadsr;
               reade ( fsavd : fsopd ) enadsf;
             enddo;
             chain ( fsavd : fsopd ) headf;
             if not %found;
               hedtr  = wdt08;
               trknfa = 20000000;
               hekna  = trknfa;
               xnyopd = 'J';
             else;
               xnyopd = 'N';
             endif;
           endif;
         endif;

         if xikkeopd <> 'J';
           xxreff = fssok;
           if xximp = 'I' or xximp = ' ';
             setll xxreff zadv;
             reade xxreff zadv;
             dow not %eof;
               if svavd = 0;
                 delete sadvr;
               else;
                 update sadvr;
               endif;
               reade xxreff zadv;
             enddo;
           endif;
           if xximp = 'E' or xximp = ' ';
             setll xxreff zaev;
             reade xxreff zaev;
             dow not %eof;
               if svavd = 0;
                 delete saevr;
               else;
                 update saevr;
               endif;
               reade xxreff zaev;
             enddo;
           endif;
           chain fifirm ztell;
           xxunik = zunik;
           zunik  = zunik + 1;
           update ztellr;
           xxli   = 0;

           x003_xrlev1 = xrlev1;
           x003_xrlev2 = xrlev2;
           x003_xrlev3 = xrlev3;
           x003_xrlev4 = 1;
           x003_xrlev5 = 0;

           chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcvf;
           dow %found;                                                            // 1<-B
             select;                                                              //  2<-B
             when xrnv = 'TotalGrossWeight';                                      //  2
               hevkt = %dec(xrverd:9:0);
             when xrnv = 'TotalVolume';                                           //  2
          //   xpos = %scan ('E+' : xrverd : 1);
               if xpos > 0;
                 xtxt = %subst(xrverd:1:xpos-1);
                 hem3 = %dec(xtxt:7:3);
                 xtxt = %subst(xrverd:xpos+2:68-xpos);
                 xtal = %dec(xtxt:2:0);
                 select;
                 when xtal > 6;
                   hem3 = 9999,999;
                 when xtal = 6;
                   hem3 = hem3 * 1000;
                 when xtal = 5;
                   hem3 = hem3 * 100;
                 when xtal = 4;
                   hem3 = hem3 * 10;
                 when xtal = 2;
                   hem3 = hem3 / 10;
                 when xtal = 1;
                   hem3 = hem3 / 100;
                 when xtal = 0;
                   hem3 = hem3 / 1000;
                 endsl;
               else;
                 hem3 = %dec(xrverd:7:3);
               endif;
             when xrnv = 'LoadingMetres';                                          //  2
               helm = %dec(xrverd:7:3);
             when xrnv = 'NumberOfPackages';                                      //  2
               hent = %dec(xrverd:7:0);
             when xrnv = 'Reference';                                             //  2
               exsr srxml301a;
             endsl;                                                               //  2<-E

             x003_xrlev4 = x003_xrlev4 + 1;
             chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 :
             x003_xrlev4 : x003_xrlev5 ) xmlrcvf;
           enddo;                                                                 // 1<-E

         endif;    // end på if xikkeopd <> 'J';

       endsr;

       //**********************************************
       begsr srxml301a;
         //**********************************************

         x003_xrlev1 = xrlev1;
         x003_xrlev2 = xrlev2;
         x003_xrlev3 = xrlev3;
         x003_xrlev4 = xrlev4;

         fssok  = *blanks;
         fskode = *blanks;

         setll ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         dow not %eof;                                                          // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'ReferenceNo';                                           //  2
             fssok  = xrverd;
           when xrnv = 'ReferenceType';                                         //  2
             fskode = xrverd;
           endsl;                                                               //  2<-E
           if fssok <> *blanks and fskode <> *blanks;                           //  2<-B
             if fskode = 'C' or fskode = 'CU';                                  //   3<-B
               herfa  = fssok;
             endif;                                                             //   3<-E
             write frisokr;
             fssok  = *blanks;
             fskode = *blanks;
           endif;                                                               //  2<-E
           reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           ) xmlrcvf01;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml302;
         //**********************************************
         // INFOR. FRA <CustomsClearance>

         setll ( xrsn : xrlev1 : xrlev2 : xrlev3 : xrlev4 ) xmlrcaf;
         reade ( xrsn : xrlev1 : xrlev2 : xrlev3 : xrlev4 ) xmlrcaf;
         dow not %eof;
           select;
           when xratn = 'status';
           when xrnv = 'function';
           when xrnv = 'lineMerge';
           endsl;

           reade ( xrsn : xrlev1 : xrlev2 : xrlev3 : xrlev4 ) xmlrcaf;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml303;
         //**********************************************
         // INFOR. FRA <ConsignmentGoodsItemSet>

         x003_xrlev1 = xrlev1;
         x003_xrlev2 = xrlev2;
         x003_xrlev3 = xrlev3;
         x003_xrlev4 = 1;
         x003_xrlev5 = 0;
         xx_svvt01 = *blanks;

         chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcvf;
         dow %found;                                                            // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'GoodsItem';                                             //  2
             exsr srxml303a;
           when xrnv = 'CustomsDetails';                                        //  2
             exsr srxml303b;
           endsl;                                                               //  2<-E

           x003_xrlev4 = x003_xrlev4 + 1;
           x003_xrlev5 = 0;
           chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcvf;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml303a;
         //**********************************************
         hevs1  = *blanks;
         hevs2  = *blanks;

         setll ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         dow not %eof;                                                          // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'GoodsDescription';                                      //  2
             select;                                                            //   3<-B
             when hevs1  = *blanks;                                             //   3
               if xx_svvt01 = *blanks;
                 xx_svvt01 = xrverd;
               endif;
               hevs1  = xrverd;
             when hevs122 = *blanks;                                            //   3
               hevs1 = %trimr(hevs1) + ' ' + xrverd;
             when hevs2  = *blanks;                                             //   3
               hevs2  = xrverd;
             endsl;                                                             //   3<-E
           when xrnv = 'PackageTypeCode';                                       //  2
             if hevs1  = *blanks;                                               //   3<-B
               hevs1  = xrverd;
             else;                                                              //   3
               hevs1  = %trim(xrverd) + ' ' + %trim(hevs1);
             endif;                                                             //   3<-E
           when xrnv = 'GrossWeight';                                           //  2
           when xrnv = 'Volume';                                                //  2
           when xrnv = 'LoadingMetres';                                         //  2
           endsl;                                                               //  2<-E

           reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           ) xmlrcvf01;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml303b;
         //**********************************************

         // INFOR. FRA <CustomsDetails>

         if xximp = 'I' or xximp = ' ';
           clear sadvr;
         endif;
         if xximp = 'E' or xximp = ' ';
           clear saevr;
         endif;

         setll ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         dow not %eof;                                                          // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'PlaceOfOrigin';                                         //  2
             if xximp = 'I' or xximp = ' ';
               svlk   = xrverd;
             endif;
             if xximp = 'E' or xximp = ' ';
               svfyl  = xrverd;
             endif;
           when xrnv = 'TarriffLineAmount';                                     //  2
             svbelt = %dec(xrverd:13:2);
           when xrnv = 'NetWeight';                                            //   2
             svvktn = %dec(xrverd:12:3);
           when xrnv = 'GrossWeight';                                          //   2
             svvktb = %dec(xrverd:12:3);
           when xrnv = 'QuantityOtherUnit';                                    //   2
             svnt01 = %dec(xrverd:6:0);
           when xrnv = 'DocumentReference';                                    //   2
             svtoa = xrverd;
           endsl;                                                               //  2<-E

         reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         enddo;                                                                 // 1<-E

         svreff = xxreff;
         svunik = xxunik;
         xxli   = xxli + 1;
         svli   = xxli;
         svvt01 = xx_svvt01;
         if xximp = 'I' or xximp = ' ';
           svvf   = '1';
           write sadvr;
         endif;
         if xximp = 'E' or xximp = ' ';
           write saevr;
         endif;

       endsr;

       //**********************************************
       begsr srxml304;
         //**********************************************
         n3035  = *blanks;
         n3039  = *blanks;
         n1131  = *blanks;
         n3055  = *blanks;
         n3124a = *blanks;
         n3124b = *blanks;
         n3124c = *blanks;
         n3124d = *blanks;
         n3124e = *blanks;
         n3036a = *blanks;
         n3036b = *blanks;
         n3036c = *blanks;
         n3042a = *blanks;
         n3042b = *blanks;
         n3042c = *blanks;
         n3164  = *blanks;
         n3229  = *blanks;
         n3251  = *blanks;
         n3207  = *blanks;
         nkn    = trknfa;
         // INFOR. FRA <Party>

         x003_xrlev1 = xrlev1;
         x003_xrlev2 = xrlev2;
         x003_xrlev3 = xrlev3;
         x003_xrlev4 = 0;
         x003_xrlev5 = 0;

         // HENT PARAMETER FRA <Party>
         xavsmot = *blanks;
         xknr = *zeros;
         setll ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcaf;
         reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcaf;
         dow not %eof;
           select;
           when xratn='subClass' and xratv='Importer';
             xavsmot = 'IMPORTØR';
             n3035  = 'IM';
           when xratn='subClass' and xratv='DespatchParty';
             xavsmot = 'HENTEADR';
             n3035  = 'PW';
           when xratn='subClass' and xratv='Exporter';
             xavsmot = 'EXPORTØR';
             n3035  = 'EX';
           when xratn='subClass' and xratv='DeliveryParty';
             xavsmot = 'LEVERADR';
             n3035  = 'DP';
           when xratn='subClass' and xratv='Consignee';
             xavsmot = 'MOTTAKER';
             n3035  = 'CN';
           when xratn='subClass' and xratv='Consignor';
             xavsmot = 'AVSENDER';
             n3035  = 'CZ';
           when xratn='subClass' and xratv='CustomsBroker';
             xavsmot = 'CUST.BR.';
             n3035  = 'CB';
           when xratn='subClass' and xratv='TransportCompany';
             xavsmot = 'TR.PORTØ';
             n3035  = 'AF';
           when xratn='subClass' and xratv='FreightPayer';
             xavsmot = 'FAKTMOTT';
             n3035  = 'PI';
           when xratn = 'partyId';
             n3039  = xratv;
        //   if xratv <> *blanks and xavsmot <> 'TR.PORTØ';
          //   xtxt = xratv;
          //   exsr knvtal;
          //   xknr = xtal;
        //     xknr = %int(xratv);
        //   endif;
           when xratn = 'idType';
             n1131  = xratv;
           endsl;
           reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcaf;
         enddo;

         select;
         when xavsmot = 'AVSENDER';
           hekns = xknr;
         when xavsmot = 'MOTTAKER';
           heknk = xknr;
         when xavsmot = 'FAKTMOTT';
           heknkf = xknr;
         endsl;

         x003_xrlev4 = 1;
         x003_xrlev5 = 0;
         chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcvf;
         dow %found;                                                            // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'Name';                                                  // 2
             select;                                                            //   3<-B
             when xavsmot = 'AVSENDER';                                         //   3
               henas   = xrverd;
             when xavsmot = 'MOTTAKER';                                         //   3
               henak   = xrverd;
             endsl;                                                             //   3<-E
             n3036a  = xrverd;
           when xrnv = 'Address';                                               //  2
             exsr srxml304a;
           endsl;                                                               //  2<-E

           x003_xrlev4 = x003_xrlev4 + 1;
           chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcvf;
         enddo;                                                                 // 1<-E

         if xavsmot <> *blanks;
           write nadsr;
         endif;

       endsr;

       //**********************************************
       begsr srxml304a;
         //**********************************************

         x003_xrlev4 = xrlev4;

         setll ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         ) xmlrcvf01;
         dow not %eof;                                                          // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'Street';                                                // 2
             select;                                                            //   3<-B
             when xavsmot = 'AVSENDER';                                         //   3
               heads1  = xrverd;
             when xavsmot = 'MOTTAKER';                                         //   3
               headk1  = xrverd;
             endsl;                                                             //   3<-E
             n3042a  = xrverd;
           when xrnv = 'PostalCode';                                            //  2
             select;                                                            //   3<-B
             when xavsmot = 'AVSENDER';                                         //   3
               heads3  = xrverd;
               hesdf   = xrverd;
             when xavsmot = 'MOTTAKER';                                         //   3
               headk3  = xrverd;
               hesdt   = xrverd;
             endsl;                                                             //   3<-E
             n3251   = xrverd;
           when xrnv = 'City';                                                  //  2
             select;                                                            //   3<-B
             when xavsmot = 'AVSENDER';                                         //   3
               heads3 = %trimr(heads3) + ' ' + xrverd;
             when xavsmot = 'MOTTAKER';                                         //   3
               headk3 = %trimr(headk3) + ' ' + xrverd;
             endsl;                                                             //   3<-E
             n3164   = xrverd;
           when xrnv = 'CountryCode';                                           //  2
             n3207   = xrverd;
             select;                                                            //   3<-B
             when xavsmot = 'AVSENDER';                                         //   3
               helka   = xrverd;
             when xavsmot = 'MOTTAKER';                                         //   3
               hetri   = xrverd;
             endsl;                                                             //   3<-E
           endsl;                                                               //  2<-E

           reade ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           ) xmlrcvf01;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml305;
         //**********************************************
         // INFOR. FRA <TransportLeg>

         x003_xrlev1 = xrlev1;
         x003_xrlev2 = xrlev2;
         x003_xrlev3 = xrlev3;
         x003_xrlev4 = 1;
         x003_xrlev5 = 0;

         chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcvf;
         dow %found;                                                            // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'TransportLegType';                                      //  2
           when xrnv = 'TransportLegId';                                        //  2
           when xrnv = 'Location';                                              //  2
           when xrnv = 'ModeOfTransport';                                       //  2
             xan02 = %subst(xrverd:1:2);
             hetrm = %dec(xan02:2:0);
           when xrnv = 'MeansOfTransport';                                      //  2
             hehawb = xrverd;
           endsl;

           x003_xrlev4 = x003_xrlev4 + 1;
           chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcvf;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml306;
         //**********************************************
         // INFOR. FRA <TermsOfDelivery>

         x003_xrlev1 = xrlev1;
         x003_xrlev2 = xrlev2;
         x003_xrlev3 = xrlev3;
         x003_xrlev4 = 1;
         x003_xrlev5 = 0;

         chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcvf;
         dow %found;                                                            // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'TODConditionCode';                                      //  2
             hefr   = xrverd;
            chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
             : xrlev5 ) xmlrcaf;
             if %found and xratn = 'codeListAgencyIdentifier';
               hefr   = xratv;
             endif;                                                             //   3<-E
           when xrnv = 'TODConditionCodeList';                                  //  2
           endsl;                                                               //  2<-E

           x003_xrlev4 = x003_xrlev4 + 1;
           chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcvf;
         enddo;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr srxml307;
         //**********************************************
         // INFOR. FRA <DateAndTimes>

         x003_xrlev1 = xrlev1;
         x003_xrlev2 = xrlev2;
         x003_xrlev3 = xrlev3;
         x003_xrlev4 = 1;
         x003_xrlev5 = 0;

         chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
         : x003_xrlev5 ) xmlrcvf;
         dow %found;                                                            // 1<-B
           select;                                                              //  2<-B
           when xrnv = 'Year';                                                  //  2
             xxdta = %dec(xrverd:4:0);
           when xrnv = 'Month';                                                 //  2
             xxdtm = %dec(xrverd:2:0);
           when xrnv = 'Day';                                                   //  2
             xxdtd = %dec(xrverd:2:0);
           endsl;                                                               // 2<-E

           x003_xrlev4 = x003_xrlev4 + 1;
           chain ( xrsn : x003_xrlev1 : x003_xrlev2 : x003_xrlev3 : x003_xrlev4
           : x003_xrlev5 ) xmlrcvf;
         enddo;                                                                 // 1<-E

         chain ( xrsn : xrlev1 : xrlev2 : xrlev3 ) xmlrcaf;
         select;
         when xratv = 'DepartureDateEstimated';
           hedtop = xxdt;
           trsdfd = xxdt;
         when xratv = 'ArrivalDateEstimated';
           trsdtd = xxdt;
         when xratv = 'DocumentDate';
         endsl;

       endsr;

       //**********************************************
       begsr sropdhea;
         //**********************************************

         if xnyopd = 'J';

           hesg = 'XML';
           if hedtop = 0;
             hedtop = wdt08;
           endif;
           hest = 'G';
           select;
           when xximp = 'I';
             heur = 'H';
           when xximp = 'E';
             heur = 'D';
           other;
             heur = 'H';
           endsl;
           hekdfk = 'X';
           hekdfs = 'X';
           trmva = 'N';
           hepk1 = 'N';
           hepk2 = 'N';
           hepk3 = 'N';
           hepk4 = 'N';
           hepk5 = 'N';
           hepk6 = 'N';
           hepk7 = 'N';
           heot = 'BC';
           write headfr;
           if xximp = 'I' or xximp = ' ';
             write dispr;
           endif;
           ttacti = 'STR';
           tttext = 'Order entrered by XML-CustomsClearance';
           tttexl = 'Order entrered by XML-CustomsClearance';
           exsr sropdtt;

         else;

           update headfr;

           ttacti = 'CHG';
           tttext = 'Changed by XML-CustomsClearance';
           tttexl = 'Changed by XML-CustomsClearance';
           exsr sropdtt;

         endif;

         chain 1 edic;
         cmn = cmn + 1;
         update edicr;
         mmn    = cmn;
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mavd   = heavd;
         mtdn   = heopd;
         mdt    = wdt08;
         mtm    = wtm06;
         m0065  = 'INSTRU';
         m0068  = 'CustomsClearance';
         m1225  = 'INS';
         m1001  = 'INS';
         m1004  = 'Instruction';
         select;
         when xnyopd = 'J';
           m1n07  = 'ORI';
         other;
           m1n07  = 'CHG';
         endsl;
         write edimr;

       endsr;

       //**********************************************
       begsr srfinninf;
         //**********************************************

         x002_xrlev1 = 1;

         setll ( xrsn : x002_xrlev1 ) xmlrcvf;
         reade ( xrsn : x002_xrlev1 ) xmlrcvf;
         dow not %eof;                                                          // 1<-B
           select;                                                              //  2<-B
             // HENT INFOR. FRA <MessageSender>
           when xrnv = 'MessageSender';                                         //  2
             x002_xrlev2 = xrlev2;
             setll ( xrsn : x002_xrlev1 : x002_xrlev2 ) xmlrcaf;
             reade ( xrsn : x002_xrlev1 : x002_xrlev2 ) xmlrcaf;
             dow not %eof;                                                      //   3<-B
               if xratn = 'partyId';                                            //    4<-B
                 m0004 = xratv;
                 leave;
               endif;                                                           //    4<-E
               reade ( xrsn : x002_xrlev1 : x002_xrlev2 ) xmlrcaf;
             enddo;                                                             //   3<-E
             // HENT INFOR. FRA <MessageMessageRecipient>
           when xrnv = 'MessageRecipient';                                      //  2
             x002_xrlev2 = xrlev2;
             setll ( xrsn : x002_xrlev1 : x002_xrlev2 ) xmlrcaf;
             reade ( xrsn : x002_xrlev1 : x002_xrlev2 ) xmlrcaf;
             dow not %eof;                                                      //   3<-B
               if xratn = 'partyId';                                            //    4<-B
                 m0010 = xratv;
                 leave;
               endif;                                                           //    4<-E
               reade ( xrsn : x002_xrlev1 : x002_xrlev2 ) xmlrcaf;
             enddo;                                                             //   3<-E
             // HENT INFOR. FRA <MessageDate>
           when xrnv = 'MessageDate';                                           //  2
             m0062 = %subst(xrverd:1:4)
             + %subst(xrverd:6:2)
             + %subst(xrverd:9:2)
             + %subst(xrverd:12:2)
             + %subst(xrverd:15:2)
             + %subst(xrverd:18:2);
           endsl;                                                               //  2<-E
           reade ( xrsn : x002_xrlev1 ) xmlrcvf;
         enddo;                                                                 // 1<-E

       // Finn import eller eksport. Default import.
         xximp = ' ';
         x002_xrlev1 = 2;

         setll ( xrsn : x002_xrlev1 ) xmlrcvf;
         reade ( xrsn : x002_xrlev1 ) xmlrcvf;
         dow not %eof;
           if xrnv = 'CustomsClearance';
             setll ( xrsn : xrlev1 : xrlev2 : xrlev3 : xrlev4) xmlrcaf;
             reade ( xrsn : xrlev1 : xrlev2 : xrlev3 : xrlev4) xmlrcaf;
             dow not %eof;
               select;
               when xratn = 'status';
             // Import eller eksport fra status
                 select;
                 when xratv = 'Import';
                   xximp = 'I';
                   fskode = '*CR';
                 when xratv = 'Export';
                   xximp = 'E';
                   fskode = '*CE';
                 endsl;
                 leave;
               when xratn = 'function';
               endsl;
               reade ( xrsn : xrlev1 : xrlev2 : xrlev3 : xrlev4) xmlrcaf;
             enddo;
           endif;
           reade ( xrsn : x002_xrlev1 ) xmlrcvf;
         enddo;

         if xximp = ' ';
         //  xximp = 'J';
           fskode = '*CR';
         endif;

       endsr;

       //**********************************************
       begsr sropdtt;
         //**********************************************
         ttdate = wdt08;
         tttime = wtm06;
         ttuser = zuser;
         ttdatl = ttdate;
         tttiml = tttime;
         ttname = 'SYXML28';
         write trackfr;

       endsr;

       //**********************************************
       begsr knvtal;
         //**********************************************
         xtal = *zeros;
         for xn = 1 to 15;                                                      // 1<-B
           if ((xt(xn) = *blanks) or (xt(xn) = '.'));                           //  2<-B
             leave;
           endif;                                                               //  2<-E
      /end-free
     c                   mult      10            xtalh
     c                   move      xt(xn)        xtalh
      /free
         endfor;                                                                // 1<-E

         if xt(xn) = '.';                                                       // 1<-B
           xn = xn + 1;
           xtald1 = xt(xn);
           xn = xn + 1;
           if xt(xn) <> ' ';                                                    //  2<-B
             xtald2 = xt(xn);
             xn = xn + 1;
             if xt(xn) <> ' ';                                                  //   3<-B
               xtald3 = xt(xn);
             endif;                                                             //   3<-E
           endif;                                                               //  2<-E
         endif;                                                                 // 1<-E

       endsr;

       //**********************************************
       begsr init;
         //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );
         wdt08 = %dec(wdt:8:0);
         wtm06 = %dec(wtm:6:0);
         setll *loval firm;
         read firm;

       endsr;

      /end-free
