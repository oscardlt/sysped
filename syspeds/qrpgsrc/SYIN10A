      *
      ***************************************************************
      *** PROGRAM SKAL SETTE PÅ SAMLAST       (KALLES FRA SYIN10) ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER        ***
      *** LEDIGE INDIKATORER 01-04 06 07 09-11 13-24              ***
      *** LEDIGE INDIKATORER 27-32 35-90 95-97                    ***
      *** INDIKATOR 99 PÅ = FLY EKSPORT                           ***
      *** INDIKATOR 99 AV = INNENLANDS FLY                        ***
      ***************************************************************
      *
     FSAMLA     UF   E           K DISK
     FHEAD7     IF   E           K DISK
     FTELL      UF   E           K DISK
     FTELLDE    UF A E           K DISK
     FTELLAWB   UF   E           K DISK
     FSYIN10FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
     D                 DS
     D  XDTD                   1      2  0
     D  XDTM                   3      4  0
     D  XDTÅ                   5      6  0
     D  XDT                    1      6  0
     D                 DS
     D  YDTÅ                   1      4  0
     D  YDTM                   5      6  0
     D  YDTD                   7      8  0
     D  YDT                    1      8  0
     D                 DS
     D  SMDTÅ                  1      4  0
     D  SMDTM                  5      6  0
     D  SMDTD                  7      8  0
     D  SMDT                   1      8  0
     D                 DS
     D  W2DTD                  1      2  0
     D  W2DTM                  3      4  0
     D  W2DTÅ                  5      6  0
     D  W2DT                   1      6  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D* XXXKE                  1     15
     D  XXXKE                  1     35
     D  SMSD                   1      3
     D  SMLNR                  4      6  0
     D                UDS
     D  UFIRM                  1      2
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     W2DT          IFEQ      0
     C                   MOVE      *ZEROS        YDT
     C                   ELSE
     C     W2DTÅ         IFGT      50
     C     W2DTÅ         ADD       1900          YDTÅ
     C                   ELSE
     C     W2DTÅ         ADD       2000          YDTÅ
     C                   END
     C                   MOVE      W2DTM         YDTM              2 0
     C                   MOVE      W2DTD         YDTD              2 0
     C                   END
     C                   MOVE      YDT           W2DT2             8 0
     C                   MOVE      W2KL          YKL               4 0
     C                   MOVE      W2KNF         YKNF              3 0
     C                   MOVE      W2KNS         YKNS              8 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SAMLA1        SETLL     SAMLA
     C     START1        TAG
     C     USDT          READE(N)  SAMLA                                  94
     C  N94SMST          IFEQ      'U'
     C                   GOTO      START1
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        5
     C                   ADD       1             ZRECNO
     C                   EXSR      SUMVKT
     C                   EXSR      KDTFM1
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     USDT          READE(N)  SAMLA                                  94
     C  N94              DO
     C     SMST          IFEQ      'U'
     C                   GOTO      ST21
     C                   END
     C                   MOVE      SMDT          YDT
     C                   MOVE      SMKL          YKL
     C                   MOVE      SMKNF         YKNF
     C                   MOVE      SMKNS         YKNS
     C                   END
     C  N94              END
     C                   END
     C*
     C                   MOVE      ZRECNO        YRECNO            5 0
     C     START3        TAG
     C     *IN04         IFEQ      '0'
     C                   MOVE      *ZEROS        W2DT
     C                   MOVE      *ZEROS        W2KL
     C                   MOVE      *ZEROS        W2KNF
     C                   MOVE      *ZEROS        W2KNS
     C                   SELECT
     C     USDT          WHENEQ    *BLANKS
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     40            POSNBR
     C     ZRECNO        WHENEQ    0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   OTHER
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   ENDSL
     C                   END
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START3
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   MOVE      '0'           PARASH            1
     C                   CALL      'SYEX01'
     C                   PARM                    PARASH
     C                   GOTO      START
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   MOVE      YRECNO        ZRECNO
     C  N94SAMLA3        SETLL     SAMLA
     C  N94USDT          READE(N)  SAMLA                                  94
     C                   GOTO      START2
     C                   END
      *
     C     W2DT          IFNE      0
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C     USDT          ORNE      XSDTGM
     C                   MOVE      USDT          XSDTGM
     C                   GOTO      START
     C                   END
     C                   EXSR      HENTSR
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C     SAMLA2        CHAIN     SAMLA                              98
     C  N98              DO
     C                   EXSR      SAMOVE
     C                   MOVE      UMAWB         SMMAWB
     C                   ADD       1             SMNT
     C                   UPDATE    SAMLAR
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C                   END
     C  N98WSNR          IFEQ      '5'
     C                   CALL      'SYEF53'
     C                   PARM                    SMSD
     C                   PARM      SMLNR         USMLNR            3 0
     C                   PARM                    USMAVD            4 0
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C******************************************
     C     SAMOVE        BEGSR
     C******************************************
     C                   MOVE      SMDTD         XDTD
     C                   MOVE      SMDTM         XDTM
     C                   MOVE      SMDTÅ         XDTÅ
     C                   MOVE      XDT           UDT
     C                   MOVE      SMKNF         UKNF
     C                   MOVE      SMKNS         UKNA
     C*
     C* HENTER PROSJEKTNUMMER HVIS IKKE FINNES
     C*
     C     SMPRO         IFEQ      0
     C     UPRO          IFEQ      0
     C     UAVD          CHAIN     TELL                               33
     C   33AVDZER        CHAIN     TELL                               33
     C  N33              Z-ADD     TETURN        UPRO
     C  N33              ADD       1             TETURN
     C  N33              UPDATE    TELLR
     C                   END
     C                   MOVE      UPRO          SMPRO
     C                   ELSE
     C                   MOVE      SMPRO         UPRO
     C                   END
     C*
     C* FINNER NESTE LØPENUMMER TIL DESTINASJON
     C*
     C     SMLNR         IFEQ      0
     C     USDT          CHAIN     TELLDE                             34
     C   34              DO
     C                   MOVE      USDT          TEDSD
     C                   Z-ADD     1             TEDNR
     C                   WRITE     TELLDER
     C                   END
     C  N34              ADD       1             TEDNR
     C  N34              UPDATE    TELLDER
     C                   Z-ADD     TEDNR         SMLNR
     C                   END
     C*
     C                   MOVEL     USDT          XAREF             6
     C                   MOVE      SMLNR         XAREF
     C                   MOVEL     XAREF         URFA
     C                   MOVE      SMKNS         UKNKF
     C*
     C* HENTER AWBNUMMER HVIS DET IKKE ER TILDELT FRA FØR
     C*
     C     SMMAWB        COMP      0                                  3434
     C   34              MOVE      SMMAWB        UMAWB
     C  N34              EXSR      AWBSR
     C*
     C* HENTER HAWBNUMMER
     C*
     C     UHAWB         IFEQ      *BLANKS
     C     UHAWB         OREQ      *ZEROS
     C     TELKEY        CHAIN     TELLAWB                            33
     C  N33              MOVE      TEHAWB        UHAWB
     C  N33              ADD       1             TEHAWB
     C  N33              UPDATE    TELLAWBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     AWBSR         BEGSR
     C***********************************************
     C                   Z-ADD     XFLAGG        UFLAGG
     C                   MOVE      'E'           UPROD             1
     C                   CALL      'SYIN10C'
     C                   PARM                    UKNF
     C                   PARM                    UMAWB
     C                   PARM                    UFLAGG
     C                   PARM                    UTAB
     C                   PARM                    UPROD
     C*
     C                   Z-ADD     0             UFLAGG
     C*
     C                   ENDSR
     C***********************************************
     C     SUMVKT        BEGSR
     C***********************************************
     C* CALCULATES TOTALS
     C     HEAD7K        SETLL     HEAD7
     C                   SETOFF                                       34
     C                   MOVE      *ZEROS        SMVKT2
     C     STSUM         TAG
     C     HEAD7K        READE     HEAD7                                  34
     C  N34HEST          IFNE      ' '
     C                   GOTO      STSUM
     C                   END
     C  N34              ADD       HEFBV         SMVKT2
     C  N34              GOTO      STSUM
     C     SMVKT1        SUB       SMVKT2        SMVKT3
     C                   ENDSR
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      2
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     42
     C                   MOVEL     USDT          USD               5
     C                   MOVE      *BLANKS       UNVN             30
     C                   MOVE      *BLANKS       ULK               2
     C                   CALL      'SYFI10F'
     C                   PARM                    USD
     C                   PARM                    UNVN
     C                   PARM                    ULK
     C                   MOVEL     USD           USDT
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     21
     C     POSNBR        ANDLE     23
     C                   CALL      'SYFI10K'
     C                   PARM                    W2KNF
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     32
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2KNS
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     HEAD7K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    XXXKE
     C*
     C     SAMLA1        KLIST
     C                   KFLD                    USDT
     C                   KFLD                    W2DT2
     C                   KFLD                    W2KL
     C                   KFLD                    W2KNF
     C                   KFLD                    W2KNS
     C     SAMLA2        KLIST
     C                   KFLD                    SMSD
     C                   KFLD                    SMDT
     C                   KFLD                    SMKL
     C                   KFLD                    SMKNF
     C                   KFLD                    SMKNS
     C     SAMLA3        KLIST
     C                   KFLD                    USDT
     C                   KFLD                    YDT
     C                   KFLD                    YKL
     C                   KFLD                    YKNF
     C                   KFLD                    YKNS
     C     TELKEY        KLIST
     C                   KFLD                    TELK1
     C                   KFLD                    UTAB
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    USG
     C                   PARM                    USDT
     C                   PARM                    UDT               6 0
     C                   PARM                    UKNF              3 0
     C                   PARM                    UKNA              8 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    URFA              6
     C                   PARM                    UKNKF             8 0
     C                   PARM                    UHAWB             9
     C                   PARM                    UMAWB            11 0
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UTAB              1
     C*
     C                   MOVE      UFLAGG        XFLAGG            1 0
     C*** UFLAGG = 0  --->  VANLIG FLY INNENLAND / IMPORT
     C*** UFLAGG = 1  --->  JETPAKKE FLY INNENLAND / IMPORT
     C*** UFLAGG = 2  --->  VANLIG FLY EKSPORT
     C*** UFLAGG = 3  --->  ??? JETPAKKE FLY EKSPORT
     C     UFLAGG        COMP      2                                      99
     C  N99              MOVE      'I'           TELK1             1
     C   99              MOVE      'E'           TELK1             1
     C* N99      UFLAGG    COMP 3                        99
     C                   Z-ADD     0             UFLAGG
     C*
     C     *LIKE         DEFINE    USDT          XSDTGM
     C                   MOVE      USDT          XSDTGM
     C                   MOVE      *ZEROS        W2DT
     C     *LIKE         DEFINE    UAVD          AVDZER
     C                   WRITE     WINDOWSZ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      SMDTD         UDT1              2 0
     C                   MOVE      SMDTM         UDT2              2 0
     C                   MOVE      SMDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      W2DTD         UDT1              2 0
     C                   MOVE      W2DTM         UDT2              2 0
     C                   MOVE      W2DTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          W2DTD
     C                   MOVE      UDT2          W2DTM
     C                   MOVE      UDT3          W2DTÅ
     C*
     C                   ENDSR
     C*
