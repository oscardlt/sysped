      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SPRÅK                     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-97    ***
      ***********************************************************
      *
     FLLVBF     UF A E           K DISK
     FTRKODL01  IF   E           K DISK
     FLL001DB   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  ZUSER                254    263
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LVKEY1        SETLL     LLVBF
     C     LVKEY1        READE(N)  LLVBF                                  94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LVKEY1        READE(N)  LLVBF                                  94
     C  N94              ENDDO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C                   MOVEL     WSSK          YKODE
     C     TRK01K        CHAIN     TRKODL01                           90
     C   90              MOVE      'YBC0810'     MSGNR
     C   90              SETON                                        30
     C  N90LVKEY2        CHAIN(N)  LLVBF                              33
     C  N90
     CANN33              DO
     C                   MOVE      'YBC1836'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     4             LINNBR
     C   90              Z-ADD     13            POSNBR
     C                   END
      *
     C     WSVB          COMP      *BLANKS                                89
     C  N30*IN89         IFEQ      '1'
     C                   MOVE      'YBC0583'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     LVKEY2        CHAIN     LLVBF                              33
     C  N33
     CANN40              GOTO      SLTB1
      *
     C                   EXSR      MOVWLV
     C  N33              UPDATE    LLVBFR
     C   33              WRITE     LLVBFR
      *
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGVB
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGVB         BEGSR
     C***********************************************
     C                   EXSR      MOVLVW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWLV        BEGSR
     C***********************************************
     C                   MOVE      UGRUP         VBGRUP
     C                   MOVE      UPNR          VBPNR
     C                   MOVE      UANR          VBANR
     C                   MOVE      WSSK          VBSK
     C                   MOVE      WSVB          VBVB
     C                   MOVE      WSHB1         VBHB1
     C                   MOVE      WSHB2         VBHB2
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      ZUSER         VBUSER
     C                   MOVE      WDT           VBDT
     C                   MOVE      WTM           VBTD
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVLVW        BEGSR
     C***********************************************
     C                   MOVE      VBSK          WSSK
     C                   MOVE      VBVB          WSVB
     C                   MOVE      VBHB1         WSHB1
     C                   MOVE      VBHB2         WSHB2
     C                   SETON                                        40
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     13            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSSK
     C                   MOVE      *BLANKS       WSVB
     C                   MOVE      *BLANKS       WSHB1
     C                   MOVE      *BLANKS       WSHB2
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     13            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     14
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSSK          UKODE
     C                   CALL      'TR002R'
     C                   PARM                    YUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSSK
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LVKEY1        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UPNR
     C                   KFLD                    UANR
     C     LVKEY2        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UPNR
     C                   KFLD                    UANR
     C                   KFLD                    WSSK
     C     TRK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRUP             8 0
     C                   PARM                    UPNR              8 0
     C                   PARM                    UANR             15
      *
     C                   MOVE      '012'         YUNIK
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
