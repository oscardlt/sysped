     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '1' '2' '4')
     A  27                                  DSPATR(PR)
     A            TGGFT1        24A  O  4  5
     A            TGGADK         4A  O  4 30
     A            TGGBGI         1Y 0O  4 35
     A            TGGBGU         2A  O  4 38
     A            TGGBL         11Y 0O  4 41EDTCDE(4)
     A            TGGVK          3A  O  4 53
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX016R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll garantinr '
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny 2=Ændra 4=Radera'
     A                                      COLOR(BLU)
     A                                  3  2'Op Garantinr                AdKd E-
     A                                      U LK Garantibel. Val'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N27                             11  2'F3/12=Avbryt F5=Uppdatera F6=Skapa-
     A                                       ny'
     A                                      COLOR(BLU)
     A  27                             11  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(10)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2'   Garantinr                AdKd E-
     A                                      U LK Garantibel. Val'
     A                                      COLOR(WHT)
     A            WSGFT1        24A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90
     AON30                                  DSPATR(PC)
     A            WSGADK         4A  B  2 30DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A            WSGBGI         1Y 0B  2 35TEXT('Begrensing innen EF')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A            WSGBGU         2A  B  2 38TEXT('Begrensing utenfor')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A  88                                  DSPATR(PC)
     A            WSGBL         11Y 0B  2 41TEXT('Garantibeløp')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A  87                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSGVK          3A  B  2 53TEXT('Valutakode')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A  86                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A N30                              3  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX025R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll garantinr '
     A                                      COLOR(WHT)
     A N30N27                          11  2'F3/12=Avbryt F5=Uppdatera F6=Skapa-
     A                                       ny'
     A                                      COLOR(BLU)
     A N30 27                          11  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
