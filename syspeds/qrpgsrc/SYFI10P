********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
********************************************************************
      *
      *****************************************************************
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04-11 13-24                ***
      *** LEDIGE INDIKATORER 27-29 34-79 81-87 89 90 95-97    ***
      ***********************************************************
      *
     FDOKUFE    UF   E           K DISK
     FDOK29     UF   E           K DISK
     FDOKUDGS   UF   E           K DISK
     FDOKUFV    UF   E           K DISK
     FDOKUFM    UF   E           K DISK
     FDOKUF     UF   E           K DISK
     FSYFI10PA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      * DS FOR VISNING AV NORSK/UTENLANDS POSTNR/STED I SUBFIL
     D                 DS
     D  NOSTED                 1     31
     D  NOPNR                  1      4
     D  NOSTE                  6     31
     D                 DS
     D  UTSTED                 1     31
     D  UTLK                   1      2
     D  UTPNR                  4     12
     D  UTSTE                 14     31
      * UTENLANDSK LANDKODE/POSTNR ER LAGRET I DS I DFADU
     D                 DS
     D  DFADUL                 1     30
     D  DSADUL                 1     25
     C*
     C     DOKUFK        KLIST
     C                   KFLD                    XDFRI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C*
     C     DOKUF2        KLIST
     C                   KFLD                    XDFRI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    XXXNR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    DFFBNR
     C                   PARM                    TEST              1
     C                   PARM                    KOAKNR            8 0
     C                   PARM                    XTRNV            30
     C                   PARM                    W2KDME            1
     C                   PARM                    W2KDE             1
S01  C*                  PARM                    W2TOLL            2 0
N01  C                   PARM                    W2TOLL            4 0
     C                   PARM                    FILFB             1
     C                   PARM                    FILTB             1
     C                   PARM                    WSMSG            62
     C*
     C                   MOVE      ' '           SJK               1
     C                   MOVE      'F'           XDFRI             1
     C                   Z-ADD     1             XXXNR             3 0
     C*
     C     DOKUFK        SETLL     DOKUF
     C     DOKUFK        READE(N)  DOKUF                                  30
     C*
     C                   EXSR      FRASR
     C     TELLER        IFEQ      0
     C                   Z-ADD     1             DFFBNR
     C                   END
     C     SJK           IFNE      '4'
     C     TELLER        IFEQ      0
     C     TELLER        OREQ      1
     C     UFLAGG        IFEQ      7
     C                   Z-ADD     1             UFLAGG
     C                   ELSE
     C                   MOVE      '2'           TEST
     C                   END
     C                   END
     C                   END
     C     TEST          IFEQ      '2'
     C                   Z-ADD     0             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C   03              Z-ADD     3             UFLAGG
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     FRASR         BEGSR
     C*
     C     STTUR         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C****************
     C* Fyll Subfile *
     C****************
     C     DOKUFK        SETLL     DOKUF
     C     STTUR1        TAG
     C     DOKUFK        READE(N)  DOKUF                                  94
     C                   Z-ADD     0             TELLER            5 0
     C*
     C     STTUR2        TAG
     C*
     C  N94              DO                                                     1<-B
     C                   DO        8                                             2<-B
     C                   ADD       1             TELLER
     C                   ADD       1             ZRECNO
     C                   MOVEL     DF1004        WSSNDN
     C     DFST          IFNE      'U'
     C     DFST          ANDNE     'V'
     C                   MOVE      DFPOUL        NOPNR
     C                   MOVE      DFADUL        NOSTE
     C                   MOVEL     NOSTED        DSSTED
     C                   ELSE
     C                   MOVE      DFLKM         UTLK
     C                   MOVE      DFPNUM        UTPNR
     C                   MOVEL     DFAD3M        UTSTE
     C                   MOVEL     UTSTED        DSSTED
     C                   END
     C                   WRITE     SUBFIL
     C                   MOVE      DFFBNR        XXXNR
     C     ST21B         TAG
     C     DOKUFK        READE(N)  DOKUF                                  94
     C  N94              MOVE      DFFBNR        XXXNR
     C  N94              END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C     NYTXX         TAG
     C                   MOVE      ZRECNO        XRECNO            4 0
     C     STTUR3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0                                            1<-B
     C                   SETON                                        80
     C                   ELSE                                                   1
     C                   WRITE     SUBCTL
     C                   END                                                    1<-E
     C*
     C     TELLER        IFEQ      1                                            1<-B
     C                   GOTO      UT
     C                   END                                                    1<-E
     C*
     C*** AVSLUTT
     C     ST41          TAG
     C     ZRECNO        IFGT      0                                            1<-B
     C                   EXFMT     BUNN
     C                   END                                                    1<-E
     C*
     C*** BLA VIDERE
     C   25              DO                                                     1<-B
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C     DOKUF2        SETLL     DOKUF
     C                   READ(N)   DOKUF                                  94
     C                   GOTO      STTUR2
     C                   END                                                    1<-E
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      UT
     C   03              GOTO      UT
     C*
     C     LESNT         TAG
     C*
     C     ZRECNO        IFEQ      0                                            1<-B
     C                   GOTO      UT
     C                   END                                                    1<-E
     C                   EXSR      HHENT
     C     ZRECNO        IFEQ      0                                            1<-B
     C                   GOTO      UT
     C                   END                                                    1<-E
     C   31
     CANN88              GOTO      STTUR3
     C   31              SETOFF                                       88
     C   31              GOTO      STTUR
     C                   Z-ADD     5             UFLAGG
     C*
     C     UT            ENDSR
     C*
     C***********************************************
     C     HHENT         BEGSR
     C*
     C     KEYFB         KLIST
     C                   KFLD                    XDFRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   MOVEA     '00'          *IN(91)
     C                   SETOFF                                       983031
     C     *IN98         DOWEQ     '0'
     C     *IN80         ANDEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   ENDIF
     C  N98WSNR          IFEQ      '5'
     C                   MOVE      '5'           SJK
     C     KEYFB         CHAIN(N)  DOKUF                              33
     C     DFKDME        IFEQ      'B'
     C                   SETON                                        50
     C                   ELSE
     C                   SETOFF                                       50
     C                   ENDIF
     C                   MOVEL     DF1004        WSSNDN
     C                   EXFMT     BILDE01
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      '1'           *IN31
      *
      * GÅ TIL VARELINJE VEDLIKEHOLD
      *
     C   10              DO
     C                   MOVE      'J'           UVIS              1
     C                   CALL      'SYFA10A'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UFBNR
     C                   PARM                    DFNT
     C                   PARM                    DFVKT
     C                   PARM                    DFLM
     C                   PARM                    DFM3
     C                   PARM                    DFGM
     C                   PARM                    DFGM2
     C                   PARM                    DFCMN
     C                   PARM                    DFVS
     C                   PARM                    UVIS
     C                   END
     C                   END
     C*
     C  N98WSNR          IFEQ      '4'
     C                   MOVE      '4'           SJK
     C     KEYFB         CHAIN     DOKUF                              33
     C                   MOVEL     DF1004        WSSNDN
     C                   SETON                                        8831
     C                   EXFMT     BILDE01
     C   23              DELETE    DOKUFR
     C   23              exsr      slett
     C  N23              UPDATE    DOKUFR
     C   12              Z-ADD     7             UFLAGG
     C                   SUB       1             ZRECNO
     C*
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C  N98WSNR          IFEQ      '2'
     C                   MOVE      '2'           TEST
     C                   CALL      'SYFI10A'
     C                   PARM                    UFIRM
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    DFFBNR
     C                   PARM                    KOAKNR
     C                   PARM                    XTRNV
     C                   PARM                    W2KDME
     C                   PARM                    W2KDE
     C                   PARM                    W2TOLL
     C                   PARM                    FILFB
     C                   PARM                    FILTB
     C                   PARM                    WSMSG
     C                   PARM                    UFLAGG
     C                   PARM                    TEST
     C                   MOVE      *BLANKS       TEST
     C                   MOVE      ' '           WSNR
     C                   SETON                                        8831
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '3'
     C                   MOVE      '3'           TEST
     C                   Z-ADD     7             UFLAGG
     C                   CALL      'SYFI10A'
     C                   PARM                    UFIRM
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM      DFFBNR        UFBNR             3 0
     C                   PARM                    KOAKNR
     C                   PARM                    XTRNV
     C                   PARM                    W2KDME
     C                   PARM                    W2KDE
     C                   PARM                    W2TOLL
     C                   PARM                    FILFB
     C                   PARM                    FILTB
     C                   PARM                    WSMSG
     C                   PARM                    UFLAGG
     C                   PARM                    TEST
     C                   MOVE      *BLANKS       TEST
     C                   SETON                                        8831
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C     UTHENT        ENDSR
     C*
     c**************************************************************************
     c     slett         begsr
     c**************************************************************************
     C*
     C     DOKUFKB       KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C     keyfb         SETLL     DOKUFM                                 32
     C                   IF        *IN(32)
     C     keyfb         READE     DOKUFM                                 33
     C                   DOW       *IN(33) = *OFF
     C                   DELETE    DOKUFMR
     C     keyfb         READE     DOKUFM                                 33
     C                   ENDDO
     C                   ENDIF
      *
     C     keyfb         SETLL     DOKUFV                                 32
     C                   IF        *IN(32)
     C     keyfb         READE     DOKUFV                                 33
     C                   DOW       *IN(33) = *OFF
     C                   DELETE    DOKUFVR
     C     keyfb         READE     DOKUFV                                 33
     C                   ENDDO
     C                   ENDIF
      *
      * farlig gods
      *
      *
     C     DOKUFKB       SETLL     DOKUDGS                                32
     C                   IF        *IN(32)
     C     DOKUFKB       READE     DOKUDGS                                33
     C                   DOW       *IN(33) = *OFF
     C                   DELETE    DOKUDGSR
     C     DOKUFKB       READE     DOKUDGS                                33
     C                   ENDDO
     C                   ENDIF
      *
      * FRITEKST
      *
      *
     C     DOKUFKB       SETLL     DOK29                                  32
     C                   IF        *IN(32)
     C     DOKUFKB       READE     DOK29                                  33
     C                   DOW       *IN(33) = *OFF
     C                   DELETE    DOK29r
     C     DOKUFKB       READE     DOK29                                  33
     C                   ENDDO
     C                   ENDIF
      *
      * ekstra adresseopplysninger
      *
      *
     C     DOKUFKB       SETLL     DOKUFE                                 32
     C                   IF        *IN(32)
     C     DOKUFKB       READE     DOKUFE                                 33
     C                   DOW       *IN(33) = *OFF
     C                   DELETE    DOKUFER
     C     DOKUFKB       READE     DOKUFE                                 33
     C                   ENDDO
     C                   ENDIF
     C                   ENDSR
