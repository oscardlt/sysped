     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP(CA01)
     A                                      HELP
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  1VALUES(' ' '1')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            PONR           4A  O  5  3
     A            GATE          30A  O  5  8
     A            STED          30A  O  5 39
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  *DS3                                SFLSIZ(0016)
     A  *DS4                                SFLSIZ(0016)
     A  *DS3                                SFLPAG(0015)
     A  *DS4                                SFLPAG(0015)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  1'SYGE45 '
     A                                      COLOR(BLU)
     A                                  1 12'Søking postnr via gateadresse'
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  3'Pnr'
     A                                      COLOR(WHT)
     A                                  3  8'Gate/vei'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3 39'Sted'
     A                                      COLOR(WHT)
     A            WPONR          4A  I  4  3DSPATR(UL)
     A            WGATE         30A  I  4  8DSPATR(UL)
     A                                      CHECK(LC)
     A                                  2 28'Søk gate kan kombineres m/Pnr(hele-
     A                                      /deler)'
     A                                      COLOR(RED)
     A          R BUNN
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      SLNO(20)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3=Avslutt  F5=Forny  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  1'SYGE45 '
     A                                      COLOR(BLU)
     A                                  1 12'Søking postnr via gateadresse'
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  3'Nr'
     A                                      COLOR(WHT)
     A                                  3  8'Gate/vei'
     A                                      COLOR(WHT)
     A                                  3 39'Sted'
     A                                      COLOR(WHT)
     A                                 10 20MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
