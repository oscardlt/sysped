********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10318 * 01 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
      *              MYE ENDRING. INGEN MERKING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 02 YBC PTF11 OMBEREGNING
11176 * 03 YBC PTF11 TILLEGGSLINJER MED SAMME POST
11186 * 04 YBC PTF11 VIS TOTAL VEKT OG SUM AV LINJEVEKT
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD052APNL)
     A                                      HLPTITLE('SAD EKSPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'BEKREFTELSE')
     A                                      CF09(09 'Kalkulator av/på')
     A                                      CF11(11 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A                                      CF12(12 'Forrige')
     A                                      CF13(13 'Oppdater søketekst på tari-
     A                                      ff-register')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A  60                                  CF21(21 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A  60                                  CF22(22 'MERK B PÅ STATUS')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SADE009A SAD052APNL)
     A                                      HLPARA(05 002 05 003)
     A          H                           HLPPNLGRP(SADE009 SAD052APNL)
     A                                      HLPARA(05 005 05 012)
     A          H                           HLPPNLGRP(SADE010 SAD052APNL)
     A                                      HLPARA(05 014 05 025)
     A          H                           HLPPNLGRP(SADE011 SAD052APNL)
     A                                      HLPARA(05 028 05 048)
     A          H                           HLPPNLGRP(SADE011B SAD052APNL)
     A                                      HLPARA(05 050 05 056)
     A          H                           HLPPNLGRP(SADE011C SAD052APNL)
     A                                      HLPARA(05 058 05 070)
     A          H                           HLPPNLGRP(SADE012 SAD052APNL)
     A                                      HLPARA(07 002 13 050)
     A          H                           HLPPNLGRP(SADE013 SAD052APNL)
     A                                      HLPARA(15 002 19 032)
     A          H                           HLPPNLGRP(SADE014 SAD052APNL)
     A                                      HLPARA(23 002 23 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD052U'
     A                                      COLOR(BLU)
     A                                  1 21'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 39'Tdeknr.'
     A            UAVD           4Y 0O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 52'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 54COLOR(WHT)
S02  A*                                     EDTCDE(4)
N02  A                                      EDTCDE(L)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            SVLI           5Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVLN           5Y 0O  1 71COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1110DATE
     A                                      EDTCDE(Y)
     A                                  1120TIME
     A  39                              2 52'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              2 66'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  4  2'FK Varenr.. Tollverdi.... Brutto v-
     A                                      ekt... Netto vekt.... Mengde.... FF-
     A                                       Sats.... Sekv.'
     A                                      COLOR(WHT)
     A            W2FYL          2A  B  5  2TEXT('OPPRINGELSESFYLKE')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2VNTI         8A  B  5  5TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
N02  A  28                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            W2VNT          8S 0H
     A            W2BELT        11Y 2B  5 14TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2VKTB        12Y 3B  5 28TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2VKTN        12Y 3B  5 43TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2NTM          9Y 0B  5 58TEXT('Mengde')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2AVT          1A  B  5 69TEXT('AVG. KONJUNKTURFOND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'N' 'X')
     A                                      DUP
     A            W2AVTP         7Y 3B  5 72TEXT('PROSENTSATS AV SVAVT')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AVTS         3A  B  5 81DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'V.B'
     A            W2FT01        28A  B  7  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT01         6Y 0B  7 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH01         4A  B  7 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2VT01        30A  B  7 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2FT02        28A  B  8  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT02         6Y 0B  8 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH02         4A  B  8 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2VT02        30A  B  8 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2FT03        28A  B  9  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT03         6Y 0B  9 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH03         4A  B  9 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2VT03        30A  B  9 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2FT04        28A  B 10  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT04         6Y 0B 10 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH04         4A  B 10 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2VT04        30A  B 10 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2FT05        28A  B 11  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT05         6Y 0B 11 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH05         4A  B 11 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2VT05        30A  B 11 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2FT06        28A  B 12  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT06         6Y 0B 12 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH06         4A  B 12 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A*           W2VT06        30A  B 12 47TEXT('Varetekst')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A            W2FT07        28A  B 13  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT07         6Y 0B 13 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH07         4A  B 13 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A*           W2VT07        30A  B 13 47TEXT('Varetekst')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A                                 15  2'T.O'
     A            W2TOP1        17A  B 15  6DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15 25'Ref.'
     A            W2CRE1         3A  B 15 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15 42'T.O'
     A            W2TOP2        17A  B 15 46DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15 65'Ref.'
     A            W2CRE2         3A  B 15 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP3        17A  B 16  6DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE3         3A  B 16 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP4        17A  B 16 46DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE4         3A  B 16 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP5        17A  B 17  6DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE5         3A  B 17 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP6        17A  B 17 46DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE6         3A  B 17 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP7        17A  B 18  6DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE7         3A  B 18 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP8        17A  B 18 46DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE8         3A  B 18 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP9        17A  B 19  6DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE9         3A  B 19 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP10       17A  B 19 46DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE10        3A  B 19 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 24  2'Fsum->'
     A            UBEL1         13Y 2O 24 12TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 24 32'Vsum->'
     A            W2VSUM        13Y 2O 24 42TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 24 62'Diff->'
     A            W2DIFF        13Y 2O 24 72TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
N04  A                                 25  2'Tvkt->'
N04  A            UVKB           9Y 0O 25 14TEXT('Beløp Faktsum')
N04  A                                      COLOR(WHT)
N04  A                                      EDTCDE(4)
N04  A                                 25 32'Lvkt->'
N04  A            W2VKB         13Y 3O 25 42TEXT('Beløp Faktsum')
N04  A                                      COLOR(WHT)
N04  A                                      EDTCDE(4)
N04  A                                 25 62'Diff->'
N04  A            W2VDIF        13Y 3O 25 72TEXT('Beløp Faktsum')
N04  A                                      COLOR(WHT)
N04  A                                      EDTCDE(L)
     A                                 26  2'F4=Søk  F9=Kalkulator  F11=Autovek-
     A                                      t  F12=Forrige  F15=Autokopi T.O.'
     A                                      COLOR(BLU)
     A N27                             26 71'F5=Lagre'
     A                                      COLOR(BLU)
     A N30        MSGTX2        78A  O 27  2COLOR(RED)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
