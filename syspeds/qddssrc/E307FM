     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/VALUF)
     A                                      PRINT
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '4')
     A            DFAVD          4Y 0O  5  4EDTCDE(4)
     A            DFOPD          7Y 0O  5  9EDTCDE(4)
     A            DFFBNR         3Y 0O  5 17EDTCDE(4)
     A            SF1004        22A  O  5 21
     A            DFVKT          9Y 0O  5 44EDTCDE(4)
     A            DFM3           7Y 3O  5 54EDTCDE(4)
     A            DFLM           4Y 2O  5 63EDTCDE(4)
     A            DFNT           7Y 0O  5 69EDTCDE(4)
     A            DFCMN          1A  O  5 77
     A            DF1004        35A  H
     A            DFVEN          1A  O  5 79
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'FORNY')
     A                                      CF10(10 'Send EDI')
     A                                      CF12(12 'Tilbake')
     A                                  1  2'SYSPED/E307R'
     A                                      COLOR(BLU)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Valg'
     A                                      COLOR(BLU)
     A                                  2 11'Transportør:'
     A            WSTRAN         8Y 0O  2 24DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSTRNN        25A  O  2 33
     A                                  2 66TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  1' 4=Slett  '
     A                                      COLOR(BLU)
     A                                  4  1'Op  Avd   Opd   Fbn Sendingsnr.   -
     A                                                     Vkt       M3    LM  -
     A                                         Kll E V'
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 23 34'F10=FERDIG SEND EDI'
     A                                      COLOR(BLU)
     A                                 23 58'F12=Tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      CF05(05 'FORNY')
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Tilbake')
     A                                  1  2'SYSPED/E307R'
     A                                      COLOR(BLU)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  3  2'Valg   2=Endre fraktbrev          -
     A                                      6=Registrer LxBxH på kolli'
     A                                      COLOR(BLU)
     A                                  4  1'Op Pos Frb  Transportør           -
     A                                      Vkt       M3    LM      Kll   Mål'
     A                                      DSPATR(HI)
     A                                  1 18'Turnr:'
     A            WSPRO          8S 0O  1 25DSPATR(HI)
     A                                 23 58'F12=Tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Avslutt')
     A                                  1  2'SYSPED/E307R'
     A                                      COLOR(BLU)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  9  8'Transportørnummer....: '
     A            WSTRAN         8Y 0B  9 32EDTCDE(4)
     A  71                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A                                 23  4'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 23 50'Enter=videre'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
