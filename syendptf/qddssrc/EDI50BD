     A*%%TS  SD  20180522  101024  JOVO        REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 24 80 *NOMSGLIN)
     A                                      OVERLAY
     A
     A          R SUBFIL                    SFL
     A*%%TS  SD  20180522  085041  JOVO        REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  8  2VALUES(' ' '2' '4')
     A            IN0065    R        O  8  4REFFLD(EDIIAMR/IN0065 *LIBL/EDIIAM)
     A            INTCRD    R        O  8 12REFFLD(EDIIAMR/INTCRD *LIBL/EDIIAM)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A*%%TS  SD  20180522  101024  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/EDI50B'
     A                                      COLOR(BLU)
     A                                  1 16'Vedlikehold avvikende katalog pr E-
     A                                      DItype'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  6 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Standard:'
     A                                      COLOR(WHT)
     A            STD_RD    R        O  3 12REFFLD(EDIIAMR/INTCRD *LIBL/EDIIAM)
     A                                  4  2'Temp.Kat:'
     A                                      COLOR(WHT)
     A            STD_RDT   R        O  4 12REFFLD(EDIIAMR/INTCRDT *LIBL/EDIIAM)
     A                                  5  2'Filnavn.:'
     A                                      COLOR(WHT)
     A            STD_RF    R        O  5 12REFFLD(EDIIAMR/INTCRF *LIBL/EDIIAM)
     A                                  5 45'(Hvis fast fjerntl. filnavn)'
     A                                  6  2'Velg opsjon:   2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  7  1'Op EDItype Fjerntliggende katalog'
     A                                      DSPATR(HI)
     A                                  2  2'EDI-part:'
     A                                      COLOR(WHT)
     A            INID      R        O  2 12REFFLD(EDIIAMR/INID *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A            INNA          30A  O  2 48COLOR(WHT)
     A*
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A*%%TS  SD  20180522  090335  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      SLNO(18)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  5  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A*%%TS  SD  20180522  090335  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(18)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'  EDItype Fjerntliggende katalog  -
     A                                                                '
     A                                      DSPATR(HI)
     A            WS0065         6A  B  2  4COLOR(WHT)
     A N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            WSTCRD    R        B  2 12REFFLD(EDIIAMR/INTCRD *LIBL/EDIIAM)
     A                                      CHECK(LC)
     A                                  3  2'Temp.Kat.'
     A                                      COLOR(WHT)
     A            WSTCRDT   R        B  3 12REFFLD(EDIIAMR/INTCRDT *LIBL/EDIIAM)
     A                                      CHECK(LC)
     A                                  4  2'Filnavn..'
     A                                      COLOR(WHT)
     A            WSTCRF    R        B  4 12REFFLD(EDIIAMR/INTCRF *LIBL/EDIIAM)
     A                                      CHECK(LC)
     A                                  4 45'(Hvis fast fjerntl. filnavn)'
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A*%%TS  SD  20180522  091216  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(18)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'  EDItype Fjerntliggende katalog  -
     A                                                                '
     A                                      DSPATR(HI)
     A            WS0065         6A  O  2  4COLOR(WHT)
     A N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            WSTCRD    R        O  2 12REFFLD(EDIIAMR/INTCRD *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A                                  3  2'Temp.Kat.'
     A                                      COLOR(WHT)
     A            WSTCRDT   R        O  3 12REFFLD(EDIIAMR/INTCRDT *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A                                  4  2'Filnavn..'
     A                                      COLOR(WHT)
     A            WSTCRF    R        O  4 12REFFLD(EDIIAMR/INTCRF *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A                                  4 45'(Hvis fast fjerntl. filnavn)'
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  5 19'Enter=Bekreft sletting'
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/EDI50B'
     A                                      COLOR(BLU)
     A                                  1 16'Vedlikehold avvikende katalog pr E-
     A                                      DItype'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2'EDI-part:'
     A                                      COLOR(WHT)
     A            INID      R        O  2 12REFFLD(EDIIAMR/INID *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A            INNA          30A  O  2 48COLOR(WHT)
     A                                  3  2'Standard='
     A                                      COLOR(WHT)
     A            STD_RD    R        O  3 12REFFLD(EDIIAMR/INTCRD *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A                                  4  2'Temp.Kat.'
     A                                      COLOR(WHT)
     A            STD_RDT   R        O  4 12REFFLD(EDIIAMR/INTCRDT *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A                                  5  2'Filnavn..'
     A                                      COLOR(WHT)
     A            STD_RF    R        O  5 12REFFLD(EDIIAMR/INTCRF *LIBL/EDIIAM)
     A                                      COLOR(WHT)
     A                                  5 45'(Hvis fast fjerntl. filnavn)'
     A                                  6  2'Velg opsjon:   2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  7  1'Op EDItype Fjerntliggende katalog'
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
