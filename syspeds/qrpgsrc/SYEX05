********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 engelsk/tysk
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE LANDKODE                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-67 71-90    ***
      ***                    95-97 99                         ***
      ***********************************************************
      *
     FKODTLK    UF A E           K DISK
     FSYEX05FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  KLKNVN                 1     30
     D  KLKNV4                 1      4
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2LK          YLK               2
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODLK1        SETLL     KODTLK
     C     START1        TAG
     C     YLKUNI        READE     KODTLK                                 94
     C   94              MOVE      *BLANKS       KLKNVN
     C   94              MOVE      *BLANKS       WSNR
     C  N94              UPDATE    KODTLKR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     YLKUNI        READE     KODTLK                                 94
     C  N94              UPDATE    KODTLKR
     C  N94              MOVE      KLKLK         YLK
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   WRITE     BUNN
     C                   END
     C                   SETOFF                                       98
     C                   EXFMT     SUBCTL
     C*
     C*** AVSLUTT
     C     ST41          TAG
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY LANDKODE
     C   06              GOTO      START5
     C*** SØK FRA LANDKODE
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODLK3        SETLL     KODTLK
     C  N94YLKUNI        READE     KODTLK                                 94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   EXSR      HENT
     C     *IN27         IFEQ      *ON
     C     WSNR          ANDEQ     '1'
     C                   GOTO      SLUTT
     C                   ENDIF
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT LANDKODE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODLK2        CHAIN     KODTLK                             33
     C  N33              DELETE    KODTLKR
     C                   EXSR      MOVB1
     C                   MOVEL     '*DLT'        KLKNVN
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE LANDKODE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(68)
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA DESTINASJON
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSLK          COMP      *BLANKS                                70
     C   70              MOVE      'SPL0001'     MSGNR
     C   70              SETON                                        30
     C   70              GOTO      SLTB
     C     KODLKK        CHAIN(N)  KODTLK                             33
     C  N33              SETON                                        7030
     C   70              MOVE      'SPL0032'     MSGNR
     C   70              GOTO      SLTB
     C                   END
     C*
     C     WSNVN         COMP      *BLANKS                                69
n01  C  N69WSNVNE        COMP      *BLANKS                                69
n01  C  N69WSNVNT        COMP      *BLANKS                                69
s01  C*  69              MOVE      'SPN0013'     MSGNR
n01  C   69              MOVE      'SHU1395'     MSGNR
     C   69              SETON                                        30
     C   69              GOTO      SLTB
     C*
     C     WSLK2         IFEQ      *BLANKS
     C                   MOVE      WSLK          WSLK2
     C                   ELSE
     C     WSLK2         IFNE      WSLK
     C     KODLK4        CHAIN(N)  KODTLK                             68
     C   68              MOVE      'SPR0010'     MSGNR
     C   68              SETON                                        30
     C   68              GOTO      SLTB
     C                   END
     C                   END
     C     WSALF         IFEQ      *BLANKS
     C                   MOVE      WSLK          WSALF
     C                   END
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODLKK        CHAIN     KODTLK                             33
     C* CORRECTED 12.02.90 BY J.O.V - MOVE WORKFIELDS EVEN IF UPDATE
     C*    N40             EXSR MOVWLK
     C                   EXSR      MOVWLK
     C  N33              UPDATE    KODTLKR
     C   33              WRITE     KODTLKR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   MOVEL     KLKLK         ULAKO
     C                   GOTO      SLHENT
     C                   ENDIF
     C  N98WSNR          IFEQ      '2'
     C     KLKNV4        COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVLKW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     KLKNV4        COMP      '*DLT'                                 98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KLKLK
     C                   MOVE      *BLANKS       KLKLK2
     C                   MOVE      *BLANKS       KLKNVN
     C                   MOVE      *BLANKS       KLKALF
     C                   MOVE      *BLANKS       KLKPRO
     C                   MOVE      *BLANKS       KLKMIN
     C                   MOVE      *BLANKS       KLKDIS
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSLK
     C                   MOVE      *BLANKS       WSLK2
     C                   MOVE      *BLANKS       WSNVN
n01  C                   MOVE      *BLANKS       WSNVNE
n01  C                   MOVE      *BLANKS       WSNVNT
     C                   MOVE      *BLANKS       WSALF
     C                   MOVE      *BLANKS       WSPRO
     C                   MOVE      *BLANKS       WSMIN
     C                   MOVE      *BLANKS       WSDIS
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVLKW        BEGSR
     C***********************************************
     C                   MOVE      KLKLK         WSLK
     C                   MOVE      KLKLK2        WSLK2
     C                   MOVE      KLKNVN        WSNVN
N01  C                   MOVE      KLKNVE        WSNVNE
n01  C                   MOVE      KLKNVT        WSNVNT
     C                   MOVE      KLKALF        WSALF
     C                   MOVE      KLKPRO        WSPRO
     C                   MOVE      KLKMIN        WSMIN
     C                   MOVE      KLKDIS        WSDIS
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWLK        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      YLKUNI        KLKUNI
     C                   MOVE      WSLK          KLKLK
     C                   MOVE      *BLANKS       KLKXX1
     C                   MOVE      *BLANKS       KLKXX2
     C                   END
     C*
     C                   MOVE      WSLK2         KLKLK2
     C                   MOVE      WSNVN         KLKNVN
n01  C                   MOVE      WSNVNE        KLKNVE
n01  C                   MOVE      WSNVNT        KLKNVT
     C                   MOVE      WSALF         KLKALF
     C                   MOVE      WSPRO         KLKPRO
     C                   MOVE      WSMIN         KLKMIN
     C                   MOVE      WSDIS         KLKDIS
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    KLKLK         ULAKO
      *
     C     *ENTRY        PLIST
     C     ULAKO         PARM      ULAKO         ULAKO
     C     UPLUKK        PARM      UPLUKK        UPLUKK            1
      *
     C     UPLUKK        IFEQ      'J'
     C                   MOVE      *ON           *IN27
     C                   ENDIF
      *
     C     KODLKK        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    WSLK
     C     KODLK1        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    W2LK
     C     KODLK2        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    KLKLK
     C     KODLK3        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    YLK
     C     KODLK4        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    WSLK2
     C*
     C                   MOVE      'LK '         YLKUNI            3
     C*
     C                   ENDSR
     C*
