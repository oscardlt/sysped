      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STS154 MODULE(*LIBL/STS154 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FGSSTRF    UF   E           K DISK    usropn
     FGSSOLL01  UF   E           K DISK    usropn
     FGSSORL21  UF   E           K DISK    usropn
     FGSSFGF    IF   E           K DISK    usropn
     FHEADF     UF   E           K DISK    usropn
     FFREETXT   IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D XXUSER          S             10
     D XXTURNR         S             11
     D FEILMELDING     S             70
     D FEILKODE        s              1
     D lstbrd          s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D i               s             10i 0
     D                 DS
     D  XXTNO                  1     11  0
     D  XXAVD                  1      4  0
     D  XXOPD                  5     11  0
     D                 DS
     D  BOXDS                  1     70
     D  BOXTXT1                1      5
     D  ZOLBOX                 7      9
     D  BOXTXT2               11     12
     D  ZTOTBOX               14     16
     D  BOXTXT3               19     22
     D  ZVKTH                 23     28
     D  BOXTXT4               29     29
     D  ZVKTD                 30     30
     D  BOXTXT5               32     37
     D  ZWSMÅLL               38     41
     D  BOXTXT6               42     42
     D  ZWSMÅLB               43     46
     D  BOXTXT7               47     47
     D  ZWSMÅLH               48     51
     D  ZM3H                  54     56
     D  BOXTXT8               57     57
     D  ZM3D                  58     60
     D  BOXTXT9               62     63
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      *IN60
     C                   MOVEL     '.'           FEILMELDING
     C                   exsr      SetSkl51B
     C                   WHEN      BIINN = ' '
     C                   MOVEL     '.'           FEILMELDING
     C                   exsr      SetSkl51B
      * Blank, ikke gjør noe..(vis bilde på ny)
     C                   WHEN      XXTURNR = *blanks
     C                   MOVE      '2'           FEILKODE
     C                   EXSR      SetSkl152
     C                   OTHER
      * Sjekk om alle kolli er skannet.
     C     XXTNO         SETLL     GSSOLL01
     C     XXTNO         READE(N)  GSSOLL01                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        OLST <> 'M'
     C                   MOVE      '6'           FEILKODE
     C                   EXSR      SetSkl152
     C                   LEAVE
     C                   END
     C     XXTNO         READE(N)  GSSOLL01                               33
     C  N33              ENDDO
      *
     C                   IF        *IN33
      * Oppdater filer
      * Oppdater status på trip
     C     XXTNO         CHAIN     GSSTRF                             33
     C                   IF        *IN33 = *OFF
     C                   IF        ((TRBCD = *BLANKS) OR (TRLOCF <> *BLANKS))
     C                   MOVE      '6'           TRST
     C                   ELSE
     C                   MOVE      '4'           TRST
     C                   END
     C                   UPDATE    GSSTRFR
     C                   END
      * Oppdater blank på status i GSSOLF
     C     XXTNO         SETLL     GSSOLL01
     C     XXTNO         READE     GSSOLL01                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      ' '           OLST
     C                   IF        ((TRBCD = *BLANKS) OR (TRLOCF <> *BLANKS))
     C                   MOVE      TRLOCT        OLSTD
     C                   CAT       'L':0         OLSTD
     C                   END
     C                   UPDATE    GSSOLFR
     C     XXTNO         READE     GSSOLL01                               33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       FGFA11
     C                   MOVE      *BLANKS       FGFA12
     C                   MOVE      *ZEROS        FGFLP1
     C                   MOVE      *BLANKS       FGFA21
     C                   MOVE      *BLANKS       FGFA22
     C                   MOVE      *ZEROS        FGFLP2
      * Oppdater status på GSSORF
     C     XXTNO         SETLL     GSSORL21
     C     XXTNO         READE     GSSORL21                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        FGFA11 = *BLANKS
     C     ORONO         CHAIN     GSSFGF                             33
     C                   END
     C                   IF        ((TRBCD = *BLANKS) OR (TRLOCF <> *BLANKS))
     C                   MOVE      '0'           ORST
     C                   MOVE      TRLOCT        ORLC
     C                   MOVE      TRLOCT        ORPOS1
     C                   CAT       'L':0         ORPOS1
     C                   MOVE      ORTNO2        ORTNO3
     C                   MOVE      ORTNO         ORTNO2
     C                   MOVE      *ZEROS        ORTNO
     C                   ELSE
     C                   MOVE      '4'           ORST
     C                   END
     C                   UPDATE    GSSORFR
     C     XXTNO         READE     GSSORL21                               33
     C  N33              ENDDO
      *
      * OPPDATER OPPDRAG
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        FGFA11 <> *BLANKS
     C                   MOVE      FGFA11        TRFA11
     C                   MOVE      FGFA12        TRFA12
     C                   MOVE      FGFLP1        TRFLP1
     C                   MOVE      FGFA21        TRFA21
     C                   MOVE      FGFA22        TRFA22
     C                   MOVE      FGFLP2        TRFLP2
     C                   END
      * FINN TOTAL KOLLI, VEKT OG KUBIKK
     C                   MOVE      *ZEROS        ZNT               3 0
     C                   MOVE      *ZEROS        ZVKT
     C                   MOVE      *ZEROS        ZM3
     C     HEADFK        SETLL     FREETXT
     C     HEADFK        READE     FREETXT                                33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      FRTTXT        BOXDS
     C     BOXTXT1       IFEQ      '*BOX*'
     C     BOXTXT2       ANDEQ     'AV'
     C                   ADD       1             ZNT
     C                   MOVEL     ZVKTH         ZVKTX             7 1
     C                   MOVE      ZVKTD         ZVKTX
     C                   ADD       ZVKTX         ZVKT              7 1
     C                   MOVEL     ZM3H          ZM3X              6 3
     C                   MOVE      ZM3D          ZM3X
     C                   ADD       ZM3X          ZM3               6 3
     C                   END
     C     HEADFK        READE     FREETXT                                33
     C                   END
     C                   Z-ADD     ZNT           HENT
     C                   EVAL      HEVKT = ZVKT + 0.5
     C                   Z-ADD     ZM3           HEM3
     C                   UPDATE    HEADFR
      *
     C                   MOVE      '1'           *IN60
     C                   MOVEL     '.'           FEILMELDING
     C                   exsr      SetSkl51B
     C                   END
      *
     C                   ENDSL
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   SELECT
     C                   WHEN      *IN60
     C                   callp     wrtsection('all')
     C                   WHEN      BIINN = ' '
     C                   callp     wrtsection('all')
     C                   OTHER
     C                   callp     wrtsection('top')
     C                   ENDSL
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     GSSTRF
     C                   close     GSSOLL01
     C                   close     GSSORL21
     C                   close     GSSFGF
     C                   close     HEADF
     C                   close     FREETXT
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
      *
     C                   eval      XXUSER = zhbgetvar('USER')
     C                   eval      XXTURNR= zhbgetvar('TURNR')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl51B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS51B'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl152     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS152'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updhtmlvar('TURNR':XXTURNR)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     C                   callp     updHTMLvar('FEILTXT':FEILMELDING)
      *
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C                   open      BRIDF
     C     XXUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSTRF
     C                   open      GSSOLL01
     C                   open      GSSORL21
     C                   open      GSSFGF
     C                   open      HEADF
     C                   open      FREETXT
      *
     C     HEADFK        KLIST
     C                   KFLD                    XXAVD
     C                   KFLD                    XXOPD
      *
     C                   MOVE      XXTURNR       XXTNO
     C                   END
     C                   MOVE      *BLANKS       FEILKODE
      *
     C                   ENDSR
