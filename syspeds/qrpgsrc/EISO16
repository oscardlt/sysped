      *********************************************************************
      * BEKREFTE PÅ SLETTING AV OPPLASTET DOKUMENT
      * PROGRAM BEHANDLER INPUT FRA EISO12
      *
CRTPG+*  CRTPGM SYSPED/EISO16 MODULE(*LIBL/EISO16
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FBRIDF     UF   E           K DISK    USROPN
     FARKIVL09  UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSLINK          S             70
     D XLINK           S             40
     D WSFILN          S             35
     D XSLETT          S              1
     D XAVSLUTT        S              1
     D XCMD            S            200
      *Array for fri-tekster
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * HENT LINK UTEN KATALOG
     C     '/'           SCAN      WSLINK        XPOS              3 0    33
     C                   DOW       *IN33
     C                   ADD       1             XPOS
     C                   MOVE      XPOS          XPOS2             3 0
     C     '/'           SCAN      WSLINK:XPOS   XPOS              3 0    33
     C                   ENDDO
     C*                  ADD       19            XPOS2
     C                   IF        XPOS2 < 36
     C                   EVAL      XPOS = 35
     C                   IF        XPOS2 = 0
     C                   EVAL      XPOS2 = 1
     C                   END
     C                   ELSE
     C                   EVAL      XPOS = 71 - XPOS2
     C                   END
     C                   EVAL      XLINK = %subst(WSLINK:XPOS2:XPOS)
      *
     C                   SELECT
     C                   WHEN      XSLETT = 'Y'
      * SLETT DOKUMENT I IFS.
     C                   Z-ADD     200           XCMDL            15 5
     C                   EVAL      XCMD = 'RMVLNK OBJLNK('
     C                             + %TRIM(XAPSTR) + '/pdf/ISOTMP/'
     C                             + %TRIM(WSFILN)
     C                             + %TRIM(XAPSTR) + ')'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    XCMD
     C                   PARM                    XCMDL
     C                   EVAL      XCMD = 'MOV OBJ('
     C                             + %TRIM(XAPSTR) + %TRIM(WSLINK)
     C                             + %TRIM(XAPSTR) + ')'
     C                             + ' TOOBJ('
     C                             + %TRIM(XAPSTR) + '/pdf/ISOTMP/'
     C                             + %TRIM(WSFILN)
     C                             + %TRIM(XAPSTR) + ')'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    XCMD
     C                   PARM                    XCMDL
      * SLETT POST I ZRISOK
     C     ARKIVL09K     CHAIN     ARKIVL09                           33
     C  N33              DELETE    ARKIVR
      *
     C                   MOVE      'Y'           XAVSLUTT
     C                   OTHER
     C                   MOVE      'N'           XAVSLUTT
     C                   ENDSL
      *
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C                   exsr      SetEnd
     C                   callp     wrtsection('end')
      *
      * Send output buffer and quit
     C                   exsr      Exit
      *
      *
      *
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSLINK  = zhbgetvar('WSLINK')
     C                   EVAL      WSFILN  = zhbgetvar('WSFILN')
     C                   EVAL      XSLETT  = zhbgetvar('XSLETT')

     C                   endsr
      *=====================================================================
      * Set html output skeleton VED FEIL
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EISO16'
     C*                  CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   callp     updHtmlVar('WSLINK':WSLINK)
     C                   callp     updHtmlVar('WSFILN':WSFILN)
     C                   callp     updHtmlVar('XAVSLUTT':XAVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$end
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   open      BRIDF                                50
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   open      ARKIVL09                             50
     C                   open      BRPARF                               50
      * Innen Z-filer skapt fra WEB forventes unik kund/reff
     C     ARKIVL09K     klist
     C                   kfld                    WSFILN
     C                   kfld                    XLINK
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   BITON     '123457'      XAPSTR            1
     C                   BITOFF    '06'          XAPSTR
      *
      *
     C                   endsr
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     ARKIVL09
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
