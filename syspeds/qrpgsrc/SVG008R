     FEDIS      O  A E             DISK
     FSVTFI     IF   E             DISK
     FEDIC      UF   E             DISK
     FEDISE     O  A E             DISK
     D                 DS
     D  WMBR                   1     14
     D  WMBRA                  1      1
     D  WMBRB                  2     10  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      UNZ
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WUCIREF          14
      *__________________
      * Hente firmarecord
      *"""""""""""""""""""
     C                   READ      SVTFI                                  33
      *____________________________
      * Hente neste sendningsnummer
      *""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *_____________
      * Init av felt
      *"""""""""""""
      * Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT6              6
     C                   MOVEL     WTM           WTM4              4
     C                   MOVE      'S'           WMBRA
     C                   Z-ADD     CSN           WMBRB
      * Workfields for separation character  '
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________
      * Init av EDIS
      *"""""""""""""
     C                   CLEAR                   EDISR
     C                   EVAL      S0004=SVTF_0004
     C                   EVAL      S0010=SVTF_0010
     C                   MOVE      WMBR          S0020
     C                   EVAL      S0026='CONTRLI-IEFEL'
     C                   EVAL      S0035=SVTF_0035
     C                   EVAL      S0036=1
     C                   EVAL      SSN=CSN
     C                   EVAL      SSR='S'
     C                   EVAL      SST='D'
     C                   MOVEL     WDT           SDT
     C                   MOVEL     WTM           STM
     C                   EVAL      SLIB='*LIBL'
     C                   EVAL      SFILE='EDISE'
     C                   MOVEL     WMBR          SMBR
     C                   EVAL      SMBRS=200
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slutt på file                                        UNZ /
      *////////////////////////////////////////////////////////////
     C     UNZ           BEGSR
      *_____________________________
      * Legg in UNZ (Slutt sendning)
      *"""""""""""""""""""""""""""""
     C                   EXSR      UNZ0
      *________________________
      * Skriv til sendningslogg
      *""""""""""""""""""""""""
     C                   WRITE     EDISR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sendningsslutt                                      UNZ0 /
      *////////////////////////////////////////////////////////////
     C     UNZ0          BEGSR
      * UNA
     C                   EVAL      WSDATA='UNA:+.?'
     C                   CAT       WAPSTR:1      WSDATA          240
      * UNB
     C                   CAT       'UNB+UNOC:3':0WSDATA
     C                   CAT       '+':0         WSDATA
     C                   CAT       SVTF_0004:0   WSDATA
N    C     SVTF_0008     IFNE      *BLANKS
     C                   CAT       '::':0        WSDATA
     C                   CAT       SVTF_0008:0   WSDATA
N    C                   ENDIF
     C                   CAT       '+':0         WSDATA
     C                   CAT       SVTF_0010:0   WSDATA
     C                   CAT       '+':0         WSDATA
     C                   CAT       WDT6:0        WSDATA
     C                   CAT       ':':0         WSDATA
     C                   CAT       WTM4:0        WSDATA
     C                   CAT       '+':0         WSDATA
     C                   CAT       SMBR:0        WSDATA
     C                   CAT       '+':0         WSDATA
     C                   CAT       SVTF_0022:0   WSDATA
     C                   CAT       '+CONTRLI':0  WSDATA
     C                   CAT       '-IEFEL++1':0 WSDATA
N    C                   IF        SVTF_0035 = '1'
N    C                   CAT       '++1':0       WSDATA
N    C                   ENDIF
     C                   CAT       WAPSTR:0      WSDATA
      * UNH
     C                   CAT       'UNH+0+CONT':0WSDATA
     C                   CAT       'RL:D:3:UN:':0WSDATA
     C                   CAT       '145050':0    WSDATA
     C                   CAT       WAPSTR:0      WSDATA
      * UCI
     C                   CAT       'UCI+':0      WSDATA
     C                   CAT       WUCIREF:0     WSDATA
     C                   CAT       '+':0         WSDATA
     C                   CAT       SVTF_0010:0   WSDATA
     C                   CAT       '+':0         WSDATA
     C                   CAT       SVTF_0004:0   WSDATA
N    C     SVTF_0008     IFNE      *BLANKS
     C                   CAT       ':':0         WSDATA
     C                   CAT       ':':0         WSDATA
     C                   CAT       SVTF_0008:0   WSDATA
N    C                   ENDIF
     C                   CAT       '+4':0        WSDATA
     C                   CAT       '+Z81':0      WSDATA
     C                   CAT       WAPSTR:0      WSDATA

      * UNT
     C                   CAT       'UNT+3+0':0   WSDATA
     C                   CAT       WAPSTR:0      WSDATA
     C                   CAT       'UNZ+1+':0    WSDATA
     C                   CAT       SMBR:0        WSDATA
     C                   CAT       WAPSTR:0      WSDATA
      * RECORD-1
     C                   CLEAR                   EDISER
     C                   EVAL      SEMBR=SMBR
     C     80            SUBST     WSDATA:1      SDATA
     C                   WRITE     EDISER
      * RECORD-2
     C                   CLEAR                   EDISER
     C                   EVAL      SEMBR=SMBR
     C     80            SUBST     WSDATA:81     SDATA
     C                   WRITE     EDISER
      * RECORD-3
     C                   CLEAR                   EDISER
     C                   EVAL      SEMBR=SMBR
     C     80            SUBST     WSDATA:161    SDATA
     C                   WRITE     EDISER
     C                   ENDSR
