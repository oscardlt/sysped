      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSF01R MODULE(*LIBL/STSF01R *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D SEMI            s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
      ***
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
      *
     C                   callp     wrtsection('top')
      *
     C                   EXSR      DSPFK
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     KUFAST
     C     SLUTT         TAG
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
     C                   eval      WSUSER   = uppify(WSUSER)
     C                   eval      SEMI     = zhbgetvar('SEMI')
      *
     C                   endsr
      *
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('User':WSUSER)
      *
     C                   endsr
      *
      *=====================================================================*
      *  Display end Customer Adress to select from
      *=====================================================================*
     C     DSPFK         begsr
      * Top inneholder WSKNS eller WSKNK, derfor 2 ulike format...
     c     SEMI          ifne      'Y'
     C                   callp     wrtsection('FKtop')
     c                   end
      *
     C     KUFASTK       SETLL     KUFAST
     C     KUFASTK       READE     KUFAST                                 40
     C                   IF        *IN40
     C                   callp     updhtmlvar('KFKOD':'')
     C                   callp     updhtmlvar('KFTXT':'--- VELG FELT ---')
     C                   ELSE
     C                   DOW       *IN40 = *OFF
     C                   IF        KFKOD = ' DEFN'
     C                   callp     updhtmlvar('KFKOD':'')
     C                   callp     updhtmlvar('KFTXT':'--- VELG FELT ---')
     C                   ELSE
     C                   callp     updhtmlvar('KFKOD':KFKOD)
     C                   callp     updhtmlvar('KFTXT':KFTXT)
     C                   END
     C                   callp     wrtsection('FKrow')
     C     KUFASTK       READE     KUFAST                                 40
     C                   ENDDO
     C                   END
      *
     C                   callp     wrtsection('FKBot')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              30
      *
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
      * (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      KUFAST
      *
     C     KUFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   EVAL      KFTYP = 'FELTKONTR'
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSF01'
     c     SEMI          ifeq      'Y'
     C                   eval      Mbr = 'STSF01T'
     c                   end
      *------------------
     C                   endsr
      *
