      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSL03 MODULE(*LIBL/STSL03
CRTPGM*        *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FLLWW161   UF   E           K DISK    usropn
     FLLWW162   UF A E           K DISK    usropn
     FLLHEAD    IF   E           K DISK    usropn
     FLLLINE    IF   E           K DISK    usropn
     FLLAGJL01  IF   E           K DISK    usropn
     FLLVARE01  IF   E           K DISK    usropn
     FLLSERL01  UF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     Dwsmn             s              7
     DBwsmn            s              7  0
     Dwsvn             s              3
     DBwsvn            s              3  0
     Dwssn             s             35
     Dwssninn          s             35
     Dwsanr            s             15
     DUser             s             10
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Feiltxt         s             50
     D FeilKode        s              1
     D MsgId           s              7
     D State           ds                  based(StateP)
     D  XMR                 1001   1200  0 DIM(40)
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D XTXT01          C                   CONST('DU HAR PLUKKET ALLE PÅ DENNE -
     D                                     VAREN.')
     D XTXT02          C                   CONST('SKANNET SERIENR FINNES IKKE I-
     D                                      SERIENR-REGISTER.')
     D XTXT03          C                   CONST('SKANNET SERIENR ER TILHØRER -
     D                                     EN ANNEN PLUKKLISTE.')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      ***
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * User changed?? - Set libl ..
     C                   exsr      Init
      *
     C                   SELECT
     C                   WHEN      WSSN <> *BLANKS
     C                   EXSR      SLETT
     C                   WHEN      WSSNINN <> *BLANKS
     C                   EXSR      OPPDAT
     C                   ENDSL
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      *
     C     LLWW162K      SETLL     LLWW162
     C     LLWW162K      READE     LLWW162                                50
     C                   DOW       *IN50 = '0'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C     LLWW162K      READE     LLWW162                                50
     C  N50              ENDDO                                                  *hival
      *
     C                   callp     wrtsection('tableend')
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   CLOSE     LLWW161
     C                   CLOSE     LLWW162
     C                   CLOSE     LLHEAD
     C                   CLOSE     LLLINE
     C                   CLOSE     LLAGJL01
     C                   CLOSE     LLVARE01
     C                   CLOSE     LLSERL01
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      USER     = zhbgetvar('USER')
     C                   eval      WSMN     = zhbgetvar('LW1MNU')
     C                   eval      BWSMN    = c2n2(WSMN)
     C                   eval      WSVN     = zhbgetvar('LW1VNU')
     C                   eval      BWSVN    = c2n2(WSVN)
     C                   eval      WSSN     = zhbgetvar('LW2SN')
     C                   eval      WSSNINN  = zhbgetvar('WSSNINN')
     C                   MOVE      WSSNINN       WSSN34           34
     C                   EVAL      WSSNINN  = WSSN34
     C                   eval      WSANR    = zhbgetvar('LW1ANR')
     C                   IF        WSANR    = *BLANKS
     C                   eval      WSANR    = zhbgetvar('LLANR')
     C                   END
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      StateP = RtvUsrSpcPtr(UsrSpcName:
     C                             UsrSpcLib)
      *
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
      * Repeat UserId for the next invocation
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('User':user)
     C                   EVAL      LVNTSN = LVNTSN + 1
     C                   callp     updhtmlvar('xlengde':
     C                             %trim(%editc(LVNTSN:'Z')))
     C                   callp     updhtmlvar('WSALN':
     C                             %trim(%editc(LHALN:'Z')))
     C                   callp     updhtmlvar('lw1mnu':WSMN)
     C                   callp     updhtmlvar('lw1vnu':WSVN)
     C                   callp     updhtmlvar('ANTSN':
     C                             %trim(%editc(XANTSN:'Z')))
     C                   callp     updhtmlvar('LLANR':LVANR)
     C                   callp     updhtmlvar('LLVB':LLVB)
     C                   callp     updHTMLvar('FEILTXT':FEILTXT)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     C                   callp     updhtmlvar('UsrSpcName':UsrSpcName)
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     User          CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      LLWW161
     C                   open      LLWW162
     C                   open      LLHEAD
     C                   open      LLLINE
     C                   open      LLAGJL01
     C                   open      LLVARE01
     C                   open      LLSERL01
      *
     C     LLLINEK       KLIST
     C                   KFLD                    BWSMN
     C                   KFLD                    BWSVN
     C     LLWW161K      KLIST
     C                   KFLD                    BWSMN
     C                   KFLD                    BWSVN
     C     LLWW162K      KLIST
     C                   KFLD                    BWSMN
     C                   KFLD                    BWSVN
     C     LLAGJL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    SNMNI
     C     LLVARE01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C     LLSERL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C                   KFLD                    WSSN
     C     LLSERL01K2    KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C                   KFLD                    WSSNINN
      *
     C     BWSMN         CHAIN     LLHEAD                             33
     C     LLLINEK       CHAIN     LLLINE                             33
     C                   IF        *IN33
     C                   MOVE      WSANR         LLANR
     C                   END
     C     LLVARE01K     CHAIN     LLVARE01                           33
     C                   IF        LVNTSN = 0
     C                   Z-ADD     17            LVNTSN
     C                   END
      *
     C     LLWW161K      CHAIN(N)  LLWW161                            33
     C                   Z-ADD     0             XANTSN            7 0
     C                   DOW       *IN33 = *OFF
     C                   ADD       LW1WNT        XANTSN
     C     LLWW161K      READE(N)  LLWW161                                33
     C                   ENDDO
      *
     C                   endsr
      *
      *=====================================================================******
      *  FJERN SKANNET SERIENR
      *=====================================================================******
     C     SLETT         BEGSR
      *
     C     LLWW162K      SETLL     LLWW162
     C     LLWW162K      READE     LLWW162                                33
     C                   DOW       *IN33 = *OFF
     C                   IF        LW2SN = WSSN
     C                   DELETE    LLWW162R
     C                   LEAVE
     C                   ELSE
     C                   UPDATE    LLWW162R
     C                   END
     C     LLWW162K      READE     LLWW162                                33
     C                   ENDDO
      *
     C     LLWW161K      SETGT     LLWW161
     C     LLWW161K      READPE    LLWW161                                33
     C                   DOW       *IN33 = *OFF
     C                   IF        ((LW1LC = LW2LC) AND (LW1PNT > 0))
     C                   LEAVE
     C                   END
     C     LLWW161K      READPE    LLWW161                                33
     C                   ENDDO
     C                   SUB       1             LW1PNT
     C                   UPDATE    LLWW161R
      *
     C     LLSERL01K     CHAIN     LLSERL01                           33
     C                   MOVE      *ZEROS        SNMNU
     C                   MOVE      *ZEROS        SNVNU
     C                   MOVE      *ZEROS        SNVNI
     C                   MOVE      'I'           SNST
     C                   UPDATE    LLSERR
      *
     C                   endsr
      *
      *=====================================================================******
      *  OPPDATER NY SKANNET SERIENR
      *=====================================================================******
     C     OPPDAT        BEGSR
      *
     C     LLWW161K      CHAIN(N)  LLWW161                            33
     C                   Z-ADD     0             XANTSN2           7 0
     C                   DOW       *IN33 = *OFF
     C                   ADD       LW1PNT        XANTSN2           7 0
     C     LLWW161K      READE(N)  LLWW161                                33
     C                   ENDDO
     C                   IF        XANTSN = XANTSN2
     C                   EVAL      FEILKODE = '1'
     C                   EVAL      FEILTXT  = XTXT01
     C                   GOTO      SLOPPDAT
     C                   END
      *
     C     LLSERL01K2    CHAIN     LLSERL01                           33
     C                   SELECT
     C                   WHEN      *IN33
     C                   EVAL      FEILKODE = '2'
     C                   EVAL      FEILTXT  = XTXT02
     C                   GOTO      SLOPPDAT
     C                   WHEN      (((SNST <> ' ') AND (SNST <> 'I')) OR
     C                               (SNMNU <> 0))
     C                   EVAL      FEILKODE = '3'
     C                   EVAL      FEILTXT  = XTXT03
     C                   UPDATE    LLSERR
     C                   GOTO      SLOPPDAT
     C                   ENDSL
      *
     C     LLAGJL01K     CHAIN(N)  LLAGJL01                           33
     C                   MOVE      'J'           XSAMMELK          1
     C     STOPPDAT1     TAG
     C     LLWW161K      SETLL     LLWW161
     C     LLWW161K      READE     LLWW161                                33
     C                   DOW       *IN33 = *OFF
     C                   IF        LW1WNT = LW1PNT
     C                   UPDATE    LLWW161R
     C                   ELSE
     C                   SELECT
     C                   WHEN      XSAMMELK = 'N'
     C                   LEAVE
     C                   WHEN      LW1LC = VALLOK
     C                   LEAVE
     C                   OTHER
     C                   UPDATE    LLWW161R
     C                   ENDSL
     C                   END
     C     LLWW161K      READE     LLWW161                                33
     C                   ENDDO
     C   33              MOVE      'N'           XSAMMELK
      *
     C                   MOVE      LW1MNU        SNMNU
     C                   MOVE      LW1VNU        SNVNU
     C                   UPDATE    LLSERR
      *
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   EVAL      LW2MNU = LW1MNU
     C                   EVAL      LW2VNU = LW1VNU
     C                   EVAL      LW2MNI = SNMNI
     C                   EVAL      LW2LC  = VALLOK
     C                   EVAL      LW2SN  = WSSNINN
     C                   WRITE     LLWW162R
      *
     C*    LLWW161K      CHAIN     LLWW161                            33
     C                   ADD       1             LW1PNT
     C                   UPDATE    LLWW161R
      *
     C     SLOPPDAT      ENDSR
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSL04'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
      *
     C                   callp     updhtmlvar('FEILKOD':FeilKode)
     C                   callp     updhtmlvar('FEILTXT':Feiltxt)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updhtmlvar('USER':USER)
     C                   callp     updhtmlvar('LW1MNU':
     C                             %trim(%editc(BWSMN:'Z')))
     C                   callp     updhtmlvar('LW1VNU':
     C                             %trim(%editc(BWSVN:'Z')))
     C                   callp     updhtmlvar('LW2SN':LW2SN)
      *
     C                   endsr
