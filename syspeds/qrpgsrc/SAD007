********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 UDS ER ENDRET
********************************************************************
      *
      *************************************************************
      *** PROGRAM SKAL SKRIVE UT STATISTIKK AV AVSENDER/MOTTAKER***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER      ***
      *** LEDIGE INDIKATORER 01 02 04-26                        ***
      *** LEDIGE INDIKATORER 27-39 42-86 89-96                  ***
      *************************************************************
      *
     Fsysthlp   IF   E             DISK
N01  FKODTA     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSADH2     IF   E           K DISK
     FSADH3     IF   E           K DISK
     F                                     RENAME(SADHR:SADH3R)
     FSADHD     IF   E           K DISK
     F                                     RENAME(SADHR:SADHDR)
     FSAD007FM  CF   E             WORKSTN
     FDATALIST  O    F  132        PRINTER OFLIND(*INOF)
     F                                     FORMLEN(48)
     F                                     FORMOFL(42)
     D                 DS
     D  XÅRF2                  1      4  0
     D  XMNDF2                 5      6  0
     D  XDAGF2                 7      8  0
     D  XDATOF                 1      8  0
     D                 DS
     D  XÅRT2                  1      4  0
     D  XMNDT2                 5      6  0
     D  XDAGT2                 7      8  0
     D  XDATOT                 1      8  0
     D                 DS
     D  XDAGF                  1      2  0
     D  XMNDF                  3      4  0
     D  XÅRF                   5      6  0
     D  WSDTF                  1      6  0
     D                 DS
     D  XDAGT                  1      2  0
     D  XMNDT                  3      4  0
     D  XÅRT                   5      6  0
     D  WSDTT                  1      6  0
     D                UDS
     D  UAVD                  15     18  0
     C*
N01  C     KODTAK        KLIST
N01  C                   KFLD                    KOAUNI
N01  C                   KFLD                    UAVD
N01  C                   MOVE      'A'           KOAUNI
     C     SADH2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    WSKNF
     C                   KFLD                    XDATOF
     C     SADH3K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    WSKNF
     C                   KFLD                    XDATOF
     C     SADHDK        KLIST
     C                   KFLD                    XDATOF
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C     COPY          PARM      COPY          COPY              2 0
     C*
N01  C     KODTAK        CHAIN     KODTA                              33
N01  C  N33              EVAL      UAVDNV = KOANVN
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
     C                   MOVE      *ZEROS        UIN01
     C                   MOVE      *ZEROS        WSDTF
     C                   MOVE      *ZEROS        WSDTT
     C*
     C                   EXSR      GETMSG
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     c   01              do
     c                   exsr      helpsr
     C                   GOTO      START
     c                   end
     C   03              GOTO      SLUTT
     C   12              MOVE      '1'           UIN01
     C   12              MOVE      *ZEROS        UAVD
     C   12              GOTO      SLUTT
     C                   SETOFF                                       30
     C                   SETOFF                                       979899
     C*
     C     WSDTF         IFEQ      0
     C                   Z-ADD     010151        WSDTF
     C                   END
     C                   MOVE      XDAGF         XDAGF2
     C                   MOVE      XMNDF         XMNDF2
     C     XÅRF          IFGT      50
     C     XÅRF          ADD       1900          XÅRF2
     C                   ELSE
     C     XÅRF          ADD       2000          XÅRF2
     C                   END
     C                   MOVE      XDAGT         XDAGT2
     C                   MOVE      XMNDT         XMNDT2
     C     XÅRT          IFGT      50
     C     XÅRT          ADD       1900          XÅRT2
     C                   ELSE
     C     XÅRT          ADD       2000          XÅRT2
     C                   END
     C     XDATOF        COMP      XDATOT                             99
     C   99              MOVE      'SPD0007'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START
     C     WSKL          COMP      ' '                                    98
     C   98              MOVE      'SPA0023'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      START
     C     WSKNF         COMP      WSKNT                              97
     C   97              MOVE      'SPK0032'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      START
     C*
     C                   EXSR      PRINT
     C                   MOVE      'SHU0731'     MSGNR
     C                   SETON                                        30
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        XTANN             9 0
     C                   MOVE      *ZEROS        XTALT             9 0
     C                   MOVE      *ZEROS        XTKG              9 0
     C                   MOVE      *ZEROS        XTTOL             9 0
     C                   MOVE      *ZEROS        XTANT             6 0
     C                   MOVE      *ZEROS        XTMVA             9 0
     C                   MOVE      *ZEROS        XTANNT            9 0
     C                   MOVE      *ZEROS        XTALTT            9 0
     C                   MOVE      *ZEROS        XTKGT             9 0
     C                   MOVE      *ZEROS        XTTOLT            9 0
     C                   MOVE      *ZEROS        XTANTT            6 0
     C                   MOVE      *ZEROS        XTMVAT            9 0
     C                   MOVE      *ZEROS        XKNK              8 0
     C                   MOVE      *ZEROS        XKNS              8 0
     C*
     C     WSKL          COMP      'A'                                    40
     C     WSDETJ        COMP      'J'                                8888
     C     WSALLE        COMP      'J'                                    87
     C   40
     CANN87SADH3K        SETLL     SADH3
     C  N40
     CANN87SADH2K        SETLL     SADH2
     C   87SADHDK        SETLL     SADHD
     C                   EXCEPT    DLHEAD
     C*
     C     STPRI         TAG
     C   40
     CANN87UAVD          READE     SADH3                                  60
     C  N40
     CANN87UAVD          READE     SADH2                                  60
     C   87              READ      SADHD                                  60
     C   40
     CANN60
     CANN87SIKNS         COMP      WSKNT                              60
     C  N40
     CANN60
     CANN87SIKNK         COMP      WSKNT                              60
      *
      * TEST VED ALLE AVDELINGER
      *
     C   87
     CANN60              DO
      *
     C     SIDT          COMP      XDATOT                             60
      *
     C   40
     CANN60SIKNS         IFLT      WSKNF
     C     SIKNS         ORGT      WSKNT
     C                   GOTO      STPRI
     C                   END
      *
     C  N40
     CANN60SIKNK         IFLT      WSKNF
     C     SIKNK         ORGT      WSKNT
     C                   GOTO      STPRI
     C                   END
     C                   END
      *
     C*
     C  N60              DO
     C     SIDT          COMP      XDATOF                             31  31
     C   31SIDT          COMP      XDATOT                               3131
     C*
     C   31              DO
     C   40XKNS          IFNE      0
     C     XKNS          COMP      SIKNS                              4141
     C   41              EXSR      BRUDD
     C                   END
     C  N40XKNK          IFNE      0
     C     XKNK          COMP      SIKNK                              4141
     C   41              EXSR      BRUDD
     C                   END
     C   40              MOVE      SIKNS         XKNS
     C  N40              MOVE      SIKNK         XKNK
     C   40              MOVE      SINAS         XNAS             30
     C  N40              MOVE      SINAK         XNAK             30
     C                   ADD       1             XTANT
     C                   ADD       SIBEL5        XTTOL
     C                   ADD       SIBEL6        XTMVA
     C     SIBEL5        ADD       SIBEL6        XWORK             9 0
     C     SIBEL7        SUB       XWORK         XANN              9 0
     C                   ADD       XANN          XTANN
     C                   SUB       SIBEL7        XTALT
     C                   ADD       SIVKB         XTKG
     C*
     C   OF
     CAN 88              EXCEPT    DLHEAD
     C   88              EXCEPT    DLDET
     C                   END
     C*** END PÅ <31 DO>
     C*
     C                   GOTO      STPRI
     C*
     C                   END
     C*** END PÅ <N60 DO>
     C*
     C                   EXSR      BRUDD
     C                   EXCEPT    DLTOT
     C                   MOVE      *ZEROS        WSKNF
     C                   MOVE      *ZEROS        WSKNT
     C                   MOVE      ' '           WSKL
     C                   MOVE      ' '           WSDETJ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     BRUDD         BEGSR
     C***********************************************
     C                   ADD       XTANT         XTANTT
     C                   ADD       XTTOL         XTTOLT
     C                   ADD       XTMVA         XTMVAT
     C                   ADD       XTANN         XTANNT
     C                   ADD       XTALT         XTALTT
     C                   ADD       XTKG          XTKGT
     C   OF              EXCEPT    DLHEAD
     C                   EXCEPT    DLBRU
     C                   MOVE      *ZEROS        XTANN
     C                   MOVE      *ZEROS        XTALT
     C                   MOVE      *ZEROS        XTKG
     C                   MOVE      *ZEROS        XTTOL
     C                   MOVE      *ZEROS        XTANT
     C                   MOVE      *ZEROS        XTMVA
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETMSG        BEGSR
     C***********************************************
     C                   MOVE      'DIV1786'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     34            XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT1            34
     C                   MOVE      'DIV1787'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     8             XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT2             8
     C                   MOVE      'DIV1788'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     15            XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT3            15
     C                   MOVE      'DIV1789'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     15            XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT4            15
     C                   MOVE      'DIV1790'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     7             XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT5             7
     C                   MOVE      'DIV1791'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     7             XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT6             7
     C                   MOVE      'DIV1792'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     110           XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT7           110
     C                   MOVE      'DIV1793'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     40            XMSGL
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT8            40
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETTXT        BEGSR
     C***********************************************
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG            120
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     helpsr        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       'sad007.htm':0pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
     O*
     ODATALIST  E            DLHEAD           01
     O                       KFMT1               75
     ODATALIST  E            DLHEAD           02
     O                       KFMT2               75
     O               88                          90 '¤&l'
     O               88      COPY                92
     O               88                          93 'X'
     O               88                         100 '¤(s12H'
     O               88                         106 '¤&l1O'
     ODATALIST  E            DLHEAD           03
     O               88                          10 '¤&a-3R'
     O               88                          35 '¤(12U¤(s0p12h0s3b4099T'
     ODATALIST  E            DLHEAD           03
     O               88                          24 '¤*c3350A¤*c190B¤*c11G¤*c'
     O               88                          26 '2P'
     ODATALIST  E            DLHEAD           05
     O                       XTXT1               34
     O                       WSDTF         Y     42
     O                       XTXT2               52
     O                       WSDTT         Y     61
     O               40      XTXT3               81
     O              N40      XTXT4               81
     O                       XTXT5               90
     O                       WSKNF         Z    100
     O                       XTXT6              110
     O                       WSKNT         Z    120
     O          E            DLHEAD           06
     O                       XTXT7              110
     O          E            DLHEAD           07
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            DLHEAD         1 07
     O               88                          25 '¤(12U¤(s0p12h0s0b4099T'
     O          E            DLDET          2
     O               40      XKNS          Z      8
     O              N40      XKNK          Z      8
     O               40      XNAS                40
     O              N40      XNAK                40
     O                       SIAVD               45
     O                                           46 '/'
     O                       SITDN         Z     53
     O                       SIBEL5        Z     62
     O                       SIBEL6        Z     72
     O                       XANN          Z     82
     O                       SIBEL7        Z     92
     O                       SIVKB         Z    102
     O          E            DLBRU       0  0
     O               40      XKNS          Z      8
     O              N40      XKNK          Z      8
     O               40      XNAS                40
     O              N40      XNAK                40
     O                       XTANT         Z     50
     O                       XTTOL         Z     60
     O                       XTMVA         Z     70
     O                       XTANN         Z     80
     O                       XTALT         Z     90
     O                       XTKG          Z    100
     O          E            DLBRU          2
     O               40      XKNS          Z      8
     O              N40      XKNK          Z      8
     O               40      XNAS                40
     O              N40      XNAK                40
     O                       XTANT         ZB    50
     O                       XTTOL         ZB    60
     O                       XTMVA         ZB    70
     O                       XTANN         ZB    80
     O                       XTALT         ZB    90
     O                       XTKG          ZB   100
     O          E            DLTOT       2
     O                       XTXT8               41
     O                       XTANTT        Z     50
     O                       XTTOLT        Z     60
     O                       XTMVAT        Z     70
     O                       XTANNT        Z     80
     O                       XTALTT        Z     90
     O                       XTKGT         Z    100
