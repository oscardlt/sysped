      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS11 MODULE(*LIBL/EPOMS11
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
     FHEAD14    IF   E           K DISK    USROPN
     FBILLOF    UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D WSPO            S             10
     D WSART           S             35
     D WSTUR           s              8
     D WBTUR           s              8  0
     D WSVISPO         s              1
     D BSAVD           s              4  0
     D BSOPD           s              7  0
     D Frames          s              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D OddEven         s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
     D                 DS
     D  HEPOS1                 1      7
     D  POS                    1      7    DIM(7)
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
      * Frames = Y = init / første runde
     C     FRAMES        IFeq      'Y'
     C                   callp     wrtsection('all')
     c                   goto      slutt
     C                   END
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      *
     C                   EXSR      DSPKN
      * HOVEDRUTINE
      * Vis ordre på trip.
     C     WBTUR         SETLL     HEAD14
     C     WBTUR         READE     HEAD14                                 35
     C                   DOW       *IN35 = *OFF
     C*                  IF        BSKN = TRKNFA
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C*                  END
     C     WBTUR         READE     HEAD14                                 35
     C                   ENDDO
      *
     C                   callp     wrtsection('bot')
      *
      * Quit
     c     Slutt         tag
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSPO    = zhbgetvar('WSPO')
     C                   eval      WSART    = zhbgetvar('WSART')
     C                   eval      WSTUR    = zhbgetvar('WSTUR')
     C                   EVAL      WBTUR    = c2n2(WSTUR)
     C                   eval      FRAMES   = zhbgetvar('FRAMES')
     C                   eval      WSVISPO  = zhbgetvar('WSVISPO')
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     TURER14
     C                   CLOSE     HEAD14
     C                   CLOSE     BILLOF
     C                   CLOSE     CUNDL01
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   IF        FRAMES = 'Y'
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS11F.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   ELSE
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS11.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   END
     C                   endsr
      *=====================================================================******
      *  FILTRERE POSTER
      *=====================================================================******
     C     SRFILTER      begsr
     C                   MOVE      'N'           XVIS              1
     C                   MOVE      'J'           XVIS
      * hele SR bør skrives om..
      *   .. er startet som noen få IFsetninger som senere er blitt mer og mer uoversiktlige
      *   MYE ryddigere om en gjør test for test og dersom oppd. eksluderes hopp til slutten
      *
     C     SLFILTER      TAG
     C                   ENDSR
      *
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('WSPO':WSPO)
     C                   callp     updHTMLvar('WSART':WSART)
     C                   callp     updHTMLvar('FRAMES':FRAMES)
     C                   callp     updHtmlVar('WSTUR':
     C                             %trim(%editc(WBTUR:'L')))
     C                   callp     updHTMLvar('TUSTEF':TUSTEF)
     C                   callp     updHTMLvar('TUSTET':TUSTET)
     C                   callp     updHtmlVar('TUETD':
     C                             %trim(%editw(TUETD:'    .  .  ')))
     C                   callp     updHTMLvar('FRAMES':FRAMES)
      *
     C                   callp     updHTMLvar('TUTS':
     C                             %trim(%editc(TUTS:'Z')))
     C                   callp     updHTMLvar('TUTVKT':
     C                             %trim(%editc(TUTVKT:'3')))
     C                   callp     updHTMLvar('TUTM3':
     C                             %trim(%editc(TUTM3:'3')))
     C                   callp     updHTMLvar('TUTLM':
     C                             %trim(%editc(TUTLM:'3')))
      *
     C     WBTUR         SETLL     HEAD14
     C     WBTUR         READE     HEAD14                                 35
     C                   DOW       *IN35 = *OFF
     C*                  IF        BSKN = TRKNFA
     C                   SELECT
     C                   WHEN      WSVISPO = ' '
     C                   EVAL      WSVISPO = 'J'
     C                   EVAL      BSAVD   = HEAVD
     C                   EVAL      BSOPD   = HEOPD
     C                   WHEN      WSVISPO = 'J'
     C                   EVAL      WSVISPO = 'N'
     C                   LEAVE
     C                   ENDSL
     C*                  END
     C     WBTUR         READE     HEAD14                                 35
     C                   ENDDO
     C                   callp     updHTMLvar('WSVISPO':WSVISPO)
     C                   callp     updHTMLvar('WSAVD':
     C                             %trim(%editc(BSAVD:'Z')))
     C                   callp     updHTMLvar('WSOPD':
     C                             %trim(%editc(BSOPD:'Z')))
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   callp     updHTMLvar('HEAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('HEOPD':
     C                             %trim(%editc(HEOPD:'Z')))
     C                   callp     updHTMLvar('HERFA':HERFA)
     C                   callp     updHTMLvar('HENAS':HENAS)
     C                   IF        TRKNFA = BSKN
      *LYS GRØNN FARGE
     C                   callp     updHTMLvar('WSFARGE':'BGCOLOR="#00FF7F"')
     C                   ELSE
     C                   callp     updHTMLvar('WSFARGE':'')
     C                   END
     C                   callp     updHTMLvar('HENAK':HENAK)
     C                   callp     updHTMLvar('HEVS1':HEVS1)
     C                   Z-ADD     0             XPOSNR            3 0
     C  N331             DO        3             XN                3 0
     C                   IF        ((POS(XN) >= '0') AND (POS(XN) <= '9'))
     C                   MULT      10            XPOSNR
     C                   MOVE      POS(XN)       XPOSNR
     C                   END
     C                   ENDDO
     C                   callp     updHTMLvar('WSPOS':
     C                             %trim(%editc(XPOSNR:'Z')))
     C     BILLOFK       CHAIN     BILLOF                             33
     C                   callp     updHTMLvar('WSBLORR':BLCH)
     C                   callp     updHTMLvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHTMLvar('HEM3':
     C                             %trim(%editc(HEM3:'3')))
     C                   callp     updHTMLvar('HELM':
     C                             %trim(%editc(HELM:'3')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Display Customer Number to select from
      *=====================================================================******
     C     DSPKN         begsr
      *
     C                   callp     wrtsection('DspClTop')
      * BRUKER-NIVÅ
     C                   EVAL      PABRID = BIBRID
     C                   EVAL      PAKUNR = *ZEROS
     C                   EVAL      PAID   = 'POMCL'
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 33
      * FIRMA-NIVÅ
     C   33              EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C   33BrParKey      SETLL     BRPARF
     C   33BrParKey      READE     BRPARF                                 33
      *
     C                   DOW       *IN33 = *OFF
     C                   IF        BSKN = PAVRDN
     C                   callp     updhtmlvar('KNSEL':'SELECTED')
     C                   ELSE
     C                   callp     updhtmlvar('KNSEL':'')
     C                   END
     C                   Z-ADD     PAVRDN        KUNDNR
     C                   callp     updHTMLvar('SLTKN1':
     C                             %trim(%editc(KUNDNR:'Z')))
     C                   IF        PAVRDA = *BLANKS
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('SLTKN2':KNAVN)
     C                   ELSE
     C                   callp     updhtmlvar('SLTKN2':PAVRDA)
     C                   END
     C                   callp     wrtsection('DspClRow')
     C     BrParKey      READE     BRPARF                                 33
     C                   ENDDO
      *
     C                   callp     wrtsection('DspClBot')
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      TURER14
     C                   OPEN      HEAD14
     C                   OPEN      BILLOF
     C                   OPEN      CUNDL01
      *
     C     BILLOFK       klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    KUNDNR
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     WBTUR         CHAIN     TURER14                            33
      *
     C                   endsr
      *
