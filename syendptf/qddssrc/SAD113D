      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            SIAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            SITDN          7Y 0O  6 10TEXT('Oppdrag')
     A                                      EDTCDE(4)
     A            SIST           1A  O  6 18TEXT('Status')
     A            SINAK         30A  O  6 21TEXT('Consignee')
     A            SINAS         30A  O  6 52TEXT('Consignor')
     A            X_REF         17A  O  6 83TEXT('Transport Ref.nr')
     A            SIVKB          9Y 0O  6101TEXT('Total vekt')
     A                                      EDTCDE(4)
     A            SINTK          7Y 0O  6111TEXT('Total colli')
     A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Søk')
     A                                      CF20(20 'Plukk alle')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD113R'
     A                                      COLOR(BLU)
     A                                  1 20'Plukk oppdrag til kurermanifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            UAVD           4Y 0O  2 12TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 18'Turnr:'
     A            UPRO           8Y 0O  2 25TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 35'UUID:'
     A            UUUID         36A  O  2 41TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Plukk  5=Se oppdrag'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag St Mottaker       -
     A                                                      Avsender           -
     A                                                  Ref.             Tot. v-
     A                                      ekt Tot.kl.'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 26  2'F5=Frisk   F8=Søk   F12=Avbryt   F-
     A                                      20=Plukk alle'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Plukk')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                      CF20(20 'Plukk alle')
     A                                  1  2'SYSPED/SAD113R'
     A                                      COLOR(BLU)
     A                                  1 20'Plukk oppdrag til kurermanifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            UAVD           4Y 0O  2 12TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 18'Turnr:'
     A            UPRO           8Y 0O  2 25TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 35'UUID:'
     A            UUUID         36A  O  2 41TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Plukk  5=Se oppdrag'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag St Mottaker       -
     A                                                      Avsender           -
     A                                                  Ref.             Tot. v-
     A                                      ekt Tot.kl.'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 26  2'F5=Frisk   F8=Søk   F12=Avbryt   F-
     A                                      20=Plukk alle'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(23)
     A                                      CLRL(05)
     A                                  1  2'Tr.middel:'
     A            W3TRID        17A  B  1 14DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  1 32'Status:'
     A            W3ST           1A  B  1 40DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A                                  1 42'(F eller blank)'
     A                                  2  2'Avsender:'
     A            W3KNS          8Y 0B  2 14DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3  2'Mottaker:'
     A            W3KNK          8Y 0B  3 14DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4  2'Avdeling:'
     A            W3AVD          4Y 0I  4 14DSPATR(UL)
     A                                  4 22'Oppdragsnr:'
     A            W3TDN          7Y 0I  4 34DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
