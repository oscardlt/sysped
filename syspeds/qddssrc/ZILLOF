      *__________________________
      * SYSPED / REGISTER FOR BILL OF LADING
      *""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R BILLOFR
     A            BLST      R               REFFLD(ST)
     A            BLAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            BLOPD     R               REFFLD(OPD)
     A                                      COLHDG('Oppdragsnr')
     A            BLUR      R               REFFLD(UR)
     A                                      COLHDG('Oppr.kode')
     A            BLPRO     R               REFFLD(PRO)
     A                                      COLHDG('Prosjektnr')
     A            BLSG      R               REFFLD(SG)
     A                                      COLHDG('Signatur')
     A            BLDTR     R               REFFLD(DT)
     A                                      COLHDG('REGISTRERINGSDATO')
     A            BLKNR          8P 0       COLHDG('Kundenr REDERI')
     A            BLTR           4          COLHDG('TRAFIKKNR.')
     A            BLOB           5          COLHDG('OVERSJØBÅT')
     A            BLREIS        14          COLHDG('REISENR.')
     A            BLBLNR        16          COLHDG('B/L NR.')
     A            BLACC          2  0       COLHDG('ÅRHUNDRE')
     A            BLAAR          2  0       COLHDG('AARSTALL')
     A            BLUKE          2  0       COLHDG('UKENR.')
     A            BLKNA          8P 0       COLHDG('AVSENDERSNR.')
     A            BLDKA          1          COLHDG('DOKUMENT')
     A            BLAD0A    R               REFFLD(AD)
     A                                      COLHDG('AVSENDERSADR.-0')
     A            BLNAA     R               REFFLD(NA)
     A                                      COLHDG('AVSENDERSNAVN')
     A            BLAD1A    R               REFFLD(AD)
     A                                      COLHDG('AVSENDERSADR.-1')
     A            BLAD2A    R               REFFLD(AD)
     A                                      COLHDG('AVSENDERSADR.-2')
     A            BLAD3A    R               REFFLD(AD)
     A                                      COLHDG('AVSENDERSADR.-3')
     A            BLAD4A    R               REFFLD(AD)
     A                                      COLHDG('AVSENDERSADR.-4')
     A            BLKNM          8P 0       COLHDG('MOTTAGERSNR.')
     A            BLDKM          1          COLHDG('DOKUMENT')
     A            BLAD0M    R               REFFLD(AD)
     A                                      COLHDG('MOTTAGERSADR.-0')
     A            BLNAM     R               REFFLD(NA)
     A                                      COLHDG('MOTTAGERSNAVN')
     A            BLAD1M    R               REFFLD(AD)
     A                                      COLHDG('MOTTAGERSADR.-1')
     A            BLAD2M    R               REFFLD(AD)
     A                                      COLHDG('MOTTAGERSADR.-2')
     A            BLAD3M    R               REFFLD(AD)
     A                                      COLHDG('MOTTAGERSADR.-3')
     A            BLAD4M    R               REFFLD(AD)
     A                                      COLHDG('MOTTAGERSADR.-4')
     A            BLKNN          8P 0       COLHDG('NFY.NR.')
     A            BLDKN          1          COLHDG('DOKUMENT')
     A            BLAD0N    R               REFFLD(AD)
     A                                      COLHDG('NOTIFY ADRESSE-0')
     A            BLNAN     R               REFFLD(NA)
     A                                      COLHDG('NOTIFY NAVN')
     A            BLAD1N    R               REFFLD(AD)
     A                                      COLHDG('NOTIFY ADRESSE-1')
     A            BLAD2N    R               REFFLD(AD)
     A                                      COLHDG('NOTIFY ADRESSE-2')
     A            BLAD3N    R               REFFLD(AD)
     A                                      COLHDG('NOTIFY ADRESSE-3')
     A            BLAD4N    R               REFFLD(AD)
     A                                      COLHDG('NOTIFY ADRESSE-4')
     A            BLKNSA         8P 0       COLHDG('SELGERS AGENT')
     A            BLDKSA         1          COLHDG('DOKUMENT')
     A            BLKNK          8P 0       COLHDG('KJØPER')
     A            BLDKK          1          COLHDG('DOKUMENT')
     A            BLKNKA         8P 0       COLHDG('KJØPERS AGENT')
     A            BLDKKA         1          COLHDG('DOKUMENT')
     A            BLKNRA         8P 0       COLHDG('REDERI AGENT')
     A            BLDKRA         1          COLHDG('DOKUMENT')
     A            BLLSS     R               REFFLD(SD)
     A                                      COLHDG('LASTESTED')
     A            BLLSS2         4          COLHDG('LASTESTED 2')
     A            BLLSH     R               REFFLD(SD)
     A                                      COLHDG('LASTEHAVN')
     A            BLLB           5          COLHDG('LOKALBÅT')
     A            BLVH      R               REFFLD(SD)
     A                                      COLHDG('VIAHAVN')
     A            BLDTVH         8P 0       COLHDG('DATO VIA HAVN')
     A            BLLOH     R               REFFLD(SD)
     A                                      COLHDG('LOSSEHAVN')
     A            BLLOS     R               REFFLD(SD)
     A                                      COLHDG('LOSSESTED')
     A            BLLOS2         4          COLHDG('LOSSESTED 2')
     A            BLDTA          8P 0       COLHDG('AVGANG/ANKOMST')
     A            BLKDP          5          COLHDG('KODE FOR LAYOUT')
     A            BLCH           1          COLHDG('C.H.')
     A            BLMH           1          COLHDG('M.H.')
     A            BLMM          15          COLHDG('MOVEMENT')
     A            BLMN      R               REFFLD(FT17)
     A                                      COLHDG('SAMLAST CONT.NR')
     A            BLLSTY         3          COLHDG('LASTETYPE')
     A            BLOLP          1          COLHDG('PRE. ORIG. LANDFR.')
     A            BLOTP          1          COLHDG('PRE. ORIG. TOC')
     A            BLSFP          1          COLHDG('PRE. SEAFREIGHT')
     A            BLDTP          1          COLHDG('PRE. DESTINATION TOC')
     A            BLDLP          1          COLHDG('PRE. DEST. LANDFR.')
     A            BLMFP          1          COLHDG('PRE. MULTIMODAL FR.')
     A            BLOLC          1          COLHDG('COL. ORIG. LANDFR.')
     A            BLOTC          1          COLHDG('COL. ORIG. TOC')
     A            BLSFC          1          COLHDG('COL. SEAFREIGHT')
     A            BLDTC          1          COLHDG('COL. DESTINATION TOC')
     A            BLDLC          1          COLHDG('COL. DEST. LANDFR.')
     A            BLMFC          1          COLHDG('COL. MULTIMODAL FR.')
     A            BLCP           1          COLHDG('COLL/PPD')
     A            BLNTBS        15          COLHDG('ANTALL BS/L')
     A            BLSD      R               REFFLD(SD)
     A                                      COLHDG('STED')
     A            BLKNG          8P 0       COLHDG('AGENTNR.')
     A            BLAD0G    R               REFFLD(AD)
     A                                      COLHDG('AGENTADR.-0')
     A            BLNAG     R               REFFLD(NA)
     A                                      COLHDG('AGENTNAVN')
     A            BLAD1G    R               REFFLD(AD)
     A                                      COLHDG('AGENTADR.-1')
     A            BLAD2G    R               REFFLD(AD)
     A                                      COLHDG('AGENTADR.-2')
     A            BLAD3G    R               REFFLD(AD)
     A                                      COLHDG('AGENTADR.-3')
     A            BLAD4G    R               REFFLD(AD)
     A                                      COLHDG('AGENTADR.-4')
     A            BLBBK          5          COLHDG('BÅT BEKREFTELSE')
     A            BLBBNA        30          COLHDG('BÅTNAVN')
     A            BLBREI        14          COLHDG('REISENR.')
     A            BLBBLN        16          COLHDG('B/L NR.')
     A            BLBOMH    R               REFFLD(SD)
     A                                      COLHDG('OMLASTEHAVN')
     A            BLBDT     R               REFFLD(DT)
     A                                      COLHDG('BEKREFTLSES DATO')
     A            BLVH2     R               REFFLD(SD)
     A                                      COLHDG('VIA HAVN 2')
     A            BLDVH2         8P 0       COLHDG('DATO VIA HAVN 2')
     A            BLDLOS         8P 0       COLHDG('DATO LOSSEHAVN')
     A* Tillegg til BILLOF
     A            BLLIB         10          COLHDG('BIBLIOTEK')
     A            BLFILE        10          COLHDG('FIL')
     A            BLMBR         10          COLHDG('MBR')
     A            BLTYPE         6          COLHDG('TYPE MELDING')
     A            BLLVL          3          COLHDG('MELDINGSNIVÅ')
     A            BLSNN          7  0       COLHDG('MELDINGSNR')
     A            BLUNIK         9  0       COLHDG('RECORDNR')
     A            BLREFF        17          COLHDG('REFERANSE')
     A          K BLUNIK
