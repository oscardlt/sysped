     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 30 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '4')
     A            AIRFNR         6A  O  5 11
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYEF64R'
     A                                      COLOR(BLU)
     A                                  1 10'Vedl. freightnr.'
     A                                      COLOR(WHT)
     A                                  2  2'1=Lage   4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Opsjon   Freightnr.'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2FNR          6A  B  4 11DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Avslutt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYEF64R'
     A                                      COLOR(BLU)
     A                                  1 10'Vedl. freightnr.'
     A                                      COLOR(WHT)
     A                                  2  2'1=Lage   4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Opsjon   Freightnr.'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2FNR          6A  B  4 11DSPATR(UL)
     A N30                             10  2'F3/F12=Avslutt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
