     FEDISE     IF   E           K DISK
     FEOFTPSE   O  A E             DISK
     D XEDB            DS
     D  XEDB1                  1      1    INZ(X'44')
     D  XEDB2                  2      2    INZ(X'BF')
     D  XEDB3                  3     65
     D  XEDB4                 66     66    INZ(X'3E')
     D  XEDB5                 67    128
     D                 DS
     D  FE                     1     64
     D                                     DIM(64)
     D  FELT                   1     64
     D                 DS
     D  AA                     1     80
     D                                     DIM(80)
     D  AADS                   1     80
     D                 DS
     D  AB                     1     80
     D                                     DIM(80)
     D  ABDS                   1     80
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C  NLR              EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     WMBR          SETLL     EDISE
     C     WMBR          READE     EDISE                                  LR
     C   LR              GOTO      ENDINI
     C                   MOVE      SDATA         ABDS

     C                   Z-ADD     81            TN                2 0
     C                   MOVE      'NO '         EOF               3             * FLAGG FOR EOF
     C                   MOVE      ' '           TEGN              1
     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRB                                                  SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     1             DOWEQ     1
     C                   MOVE      *BLANKS       XEDB3
     C                   MOVE      *BLANKS       XEDB5
     C                   MOVE      *BLANKS       FELT
     C                   EXSR      LES63
     C                   MOVEL     FELT          XEDB3
     C                   MOVE      *BLANKS       FELT
     C                   EXSR      LES62
     C                   MOVEL     FELT          XEDB5
      * Konverter til ASCII
     C                   EXSR      CVTTOA
      * Init av kontrolltegn for Exchange Data Buffer
     C                   MOVEL     X'44'         XEDB1
     C                   MOVEL     X'BF'         XEDB2
     C                   MOVEL     X'3E'         XEDB4
      * Skriv til fil
     C                   MOVEL     XEDB          EEDB
     C                   WRITE     EOFTPSER
     C   LR              GOTO      SRBE

     C  NLR              ENDDO

     C     SRBE          ENDSR
      *////////////////////////////////////////////////////////////
      * CVT characters EBCDIC-ASCII                        CVTTOA /
      *////////////////////////////////////////////////////////////
     C     CVTTOA        BEGSR
     C                   MOVEL     'QTCPASCN'    XTABLE
     C                   MOVE      'O '          XTABLE
     C                   CALL      'QDCXLATE'
     C                   PARM      128           XLEN              5 0
     C                   PARM                    XEDB
     C                   PARM                    XTABLE           10
     C                   PARM      '*LIBL'       XLIB             10
     C                   ENDSR
     C*__________________________________________________________
     C* SR for å hente neste tegn i datastroem             GETCH
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETCH         BEGSR
     C                   SETOFF                                           98
     C     STACH         TAG

     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            LR
     C                   GOTO      ENDCH
     C                   ENDIF
     C                   MOVE      ABDS          AADS
     C  N99WMBR          READE     EDISE                                  99     *hent ny til AB
     C  N99              MOVE      SDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF                             * FLAGG FOR EOF
     C                   Z-ADD     1             TN                              * Tegn-index-nr
     C                   ENDIF
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN

     C     ENDCH         ENDSR
     C*__________________________________________________________
     C* LES62                                             LES62 *
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     LES62         BEGSR
     C     1             DO        62            NR                2 0
     C                   EXSR      GETCH
     C                   MOVE      TEGN          FE(NR)
     C   LR              GOTO      LES62E
     C                   ENDDO
     C     LES62E        TAG
     C                   ENDSR
     C*__________________________________________________________
     C* LES63                                             LES63 *
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     LES63         BEGSR
     C     1             DO        63            NR
     C                   EXSR      GETCH
     C                   MOVE      TEGN          FE(NR)
     C   LR              GOTO      LES63E
     C                   ENDDO
     C     LES63E        TAG
     C                   ENDSR
