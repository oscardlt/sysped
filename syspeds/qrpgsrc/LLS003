      *********************************************************************
      * Vis bevegelse pr artikkel pr type
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/LLS003 MODULE(*LIBL/LLS003
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FLLWW01    if   e           k disk    USROPN
     FLLMRF     if   e           k disk    USROPN
     FLLVARE01  if   e           k disk    USROPN
     FLLAGJL01  if   e           k disk    USROPN
     FLLAGJD01  if   e           k disk    USROPN
     FLLHEAD    if   e           k disk    USROPN
     FLLGRPK05  if   e           k disk    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D WSUSER          S             10
     D UsrSpcName      s             10
     D lvanr           s             15
     D Datofr          s             10
     D DatofrN         s              8  0
     D Datoti          s             10
     D DatotiN         s              8  0
     D TYPE            s              1
     D XXTXT           s             50
     D l1              s              3s 0
     D l2              s              3s 0
     D zeros           s             15a   inz('000000000000000')
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D  VAMRNR                 1      5  0
     D  VAMRNRA                1      5
     D                 DS
     D  LHALN                  1      7  0
     D  LHALNA                 1      7
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      LoadHtml
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C                   MOVE      'N'           XALC              1
     c     LLWW01K       SETGT     LLWW01
     C     LLWW01K1      READPE    LLWW01                                 50
     C  N50LWWDAT        COMP      DatofrN                              50
     C   50              IF        TYPE = 'S'
     C                   EVAL      DATOTIN = 99999999
     C                   EVAL      XALC = 'J'
     C     LLWW01K       SETLL     LLWW01
     C     LLWW01K       READE     LLWW01                                 50
     C                   END
      *
     C                   DOW       *IN50 = *OFF
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTART
      *
     C                   EXSR      RecordSlt
     C                   IF        selected = 'Y'
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTART
     C                   END
      *
     C                   IF        XALC = 'N'
     C     LLWW01K1      READPE    LLWW01                                 50
     C  N50LWWDAT        COMP      DatofrN                              50
     C   50              IF        TYPE = 'S'
     C                   EVAL      DATOTIN = 99999999
     C                   EVAL      XALC = 'J'
     C     LLWW01K       SETLL     LLWW01
     C     LLWW01K       READE     LLWW01                                 50
     C                   END
     C                   ELSE
     C     LLWW01K       READE     LLWW01                                 50
     C                   END
     C  N50              ENDDO                                                  *hival
      *
     C     SLHENTART     TAG
     C                   callp     wrtsection('tableend')
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
     C                   callp     wrtsection('*fini')
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      LVANR   = zhbgetvar('LVANR')
     C                   eval      DATOFR  = zhbgetvar('DATOFR')
     C                   EVAL      DATOFRN = c2n2(DATOFR)
     C                   eval      DATOTI  = zhbgetvar('DATOTI')
     C                   EVAL      DATOTIN = c2n2(DATOTI)
     C                   eval      TYPE    = zhbgetvar('TYPE')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * KUNDE
     C                   callp     updhtmlvar('TITLE':'Saldo utvikling')
     C                   callp     updhtmlvar('WSNA':BIBESK)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   MOVE      BIKUNR        XXKUNR            8
     C                   callp     updhtmlvar('KUNR':XXKUNR)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
     C                   SELECT
     C                   WHEN      TYPE = 'I'
     C                   EVAL      XXTXT = 'Mottaksrapport'
     C                   WHEN      TYPE = 'S'
     C                   EVAL      XXTXT = 'Plukkliste'
     C                   WHEN      TYPE = 'J'
     C                   EVAL      XXTXT = 'Justering'
     C                   ENDSL
     C     LLVARE01K     CHAIN     LLVARE01                           33
     C                   callp     updhtmlvar('LVANR':LVANR)
     C                   callp     updhtmlvar('LVVB':LVVB)
     C                   callp     updhtmlvar('DATOFR':DATOFR)
     C                   callp     updhtmlvar('DATOTI':DATOTI)
     C                   callp     updhtmlvar('TYPE':TYPE)
     C                   callp     updhtmlvar('XXTXT':XXTXT)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetRow        begsr
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   SELECT
     C                   WHEN      TYPE = 'I'
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C     LLMRFK        CHAIN     LLMRF                              33
     C                   EVAL      XXTXT = %TRIM(VAMRNRA) + ':' + %TRIM(MRLNA)
     C                   MOVE      MRDT          XXDT              8 0
     C                   WHEN      TYPE = 'S'
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C     LLHEADK       CHAIN     LLHEAD                             33
     C                   IF        *IN33
     C                   EVAL      XXTXT = *BLANKS
     C                   ELSE
     C                   EVAL      XXTXT = %TRIM(LHALNA) + ':' + %TRIM(LHBNA)
     C                   END
     C                   MOVE      VDLDAT        XXDT
     C                   WHEN      TYPE = 'J'
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C                   EVAL      XXTXT = VDTEXT
     C                   MOVE      VDLDAT        XXDT
     C                   ENDSL
      *
     C                   callp     updhtmlvar('XXTXT':XXTXT)
     C                   callp     updHTMLvar('XXDT':
     C                             %trim(%editw(XXDT:'    .  .  ')))
     C                   callp     updHTMLvar('LWWANT':
     C                             %trim(%editc(LWWANT:'L')))
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/LLS004.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     BEGSR
     C                   move      'N'           selected          1
      *  Recd is selected
     C                   IF        LWWTYP = TYPE
     C                   move      'Y'           selected
     C                   END
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   open      LLWW01                               50
     C                   open      LLMRF                                50
     C                   open      LLVARE01                             50
     C                   open      LLAGJD01                             50
     C                   open      LLAGJL01                             50
     C                   open      LLHEAD                               50
     C                   open      LLGRPK05                             50
     C     BIKUNR        chain     LLGRPK05                           33
      *
     c     LLWW01K       klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LVANR
     c                   kfld                    DATOTIN
     c     LLWW01K1      klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LVANR
     c     LLMRFK        klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    VAMRNR
     c     LLVARE01K     klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LVANR
     c     LLAGJL01K     klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LWWMN
     c     LLAGJD01K     klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LWWMN
     c                   kfld                    LWWVN
     c     LLHEADK       klist
     c                   kfld                    VDMNL
      *
     c                   z-add     GKGRP         LWWGRU
     c                   z-add     BIKUNR        LWWKN
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     LLWW01
     C                   close     LLMRF
     C                   close     LLVARE01
     C                   close     LLAGJL01
     C                   close     LLAGJD01
     C                   close     LLHEAD
     C                   close     LLGRPK05
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
