      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYPALL1 MODULE(*LIBL/SYPALL1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FPallDL04  IF   E           K DISK    USROPN
     FPapa      IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D FRA             s              8
     D FRADT           s              8  0
     D TIL             s              8
     D TILDT           s              8  0
     D PP              s              8
     D PPN             s              8  0
     D user            s             10
      * Other variables
     D Header          s              1
     D Inntxt          s             15
     D Uttxt           s             15
     D Bevtxt          s             15
     D Tottxt          s             15
     D Bevegelse       s              7  0
     D SaldoInn        s              7  0
     D SaldoUt         s              7  0
     D TotAvgi         s              7  0
     D TotMott         s              7  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C     FRADT         Ifne      *zeros
     C     TILDT         orne      *zeros
     C                   exsr      LINES
     c                   end
      * Send section bot
     C                   exsr      BOTTOM
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      FRA     = zhbgetvar('FRA')
     C                   eval      FRADT   = c2n2(FRA)
     C                   eval      TIL     = zhbgetvar('TIL')
     C                   eval      TILDT   = c2n2(TIL)
     C                   eval      PP      = zhbgetvar('PP')
     C                   eval      PPN     = c2n2(PP)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     PallDL04
     C                   CLOSE     Papa
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYPALL1':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C* klargjøre HTMLvariable
     C                   callp     updHTMLvar('FRA':FRA)
     C                   callp     updHTMLvar('TIL':TIL)
     C                   callp     updHTMLvar('PP':PP)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('BESK':BIBESK)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     C                   MOVE      *ZEROS        SaldoInn
     C                   MOVE      *ZEROS        SaldoUT
     C                   MOVE      *ZEROS        Bevegelse
     C                   MOVE      *ZEROS        TotMott
     C                   MOVE      *ZEROS        TotAvgi
     C                   MOVE      'J'           Header
     C     PPN           setll     PallDL04
     C     PPN           reade     PallDL04                               33
     C     *in33         DOWEQ     *OFF
      * Ignorer slettede
     C     PASTAT        IFNE      'D'
      * Kommet forbi oppgitt tildato - hopp ut
     C     PAKVDT        IFGT      TILDT
     C     TILDT         ANDNE     *ZEROS
     C                   GOTO      UTLINE
     C                   END
      * Lavere enn f.o.m. data - ikke vis, men akkumuler saldo..
     C     PAKVDT        IFLT      FRADT
     C                   add       PAANT         SaldoInn
     C                   add       PAANT         SaldoUT
     C                   else
      * Innenfor valgt periode, hvis FØRSTE skriv liste-header .
     c     Header        IFEQ      'J'
     C                   MOVE      'N'           Header
     c     SaldoInn      IFGT      0
     c                   movel     'VÅR favør'   Inntxt
     c                   else
     C                   mult      -1            SaldoInn
     c                   movel     'DERES favør' Inntxt
     c                   end
     C                   callp     updHTMLvar('INNTXT':Inntxt)
     C                   callp     updHTMLvar('SALINN':
     C                             %trim(%editc(SaldoInn:'3')))
     C                   callp     wrtsection('rowHead')
     c                   end
      *
     C                   add       PAANT         SaldoUT
     C                   add       PAANT         Bevegelse
      *
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(PAKVDT:'    .  .  ')))
     C                   callp     updHTMLvar('TXT':PATXT)
     C                   callp     updHTMLvar('KVNR':
     C                             %trim(%editc(PAKVNR:'3')))
     c     Paant         ifgt      0
     C                   add       PAANT         TOTMOTT
     C                   callp     updHTMLvar('ANTMOTT':
     C                             %trim(%editc(PAANT:'3')))
     C                   callp     updHTMLvar('ANTAVGI':spaces)
     c                   else
     c                   mult      -1            PAANT
     C                   add       PAANT         TOTAVGI
     C                   callp     updHTMLvar('ANTAVGI':
     C                             %trim(%editc(PAANT:'3')))
     C                   callp     updHTMLvar('ANTMOTT':spaces)
     c                   end
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
      *
     C                   END
     C                   END
     C     PPN           reade     PallDL04                               33
     C                   end
      *
     c     UTLINE        TAG
     C* klargjøre HTMLvariabel for totalt antall for bunn...
     c     SaldoUt       IFGT      0
     c                   movel     'VÅR favør'   Uttxt
     c                   else
     C                   mult      -1            SaldoUt
     c                   movel     'DERES favør' Uttxt
     c                   end
      * bevegelse
     c     Bevegelse     IFGT      0
     c                   movel     'VÅR favør'   Bevtxt
     c                   else
     C                   mult      -1            Bevegelse
     c                   movel     'DERES favør' Bevtxt
     c                   end
     C                   callp     updHTMLvar('BEVTXT':Bevtxt)
     C                   callp     updHTMLvar('BEVEG':
     C                             %trim(%editc(Bevegelse:'3')))
     C                   callp     updHTMLvar('TOTMOTT':
     C                             %trim(%editc(TOTMOTT:'3')))
     C                   callp     updHTMLvar('TOTAVGI':
     C                             %trim(%editc(TOTAVGI:'3')))
      * totalt
     C                   callp     updHTMLvar('UTTXT':Uttxt)
     C                   callp     updHTMLvar('SALUT':
     C                             %trim(%editc(SaldoUt:'3')))
      *
     C                   callp     wrtsection('rowbot')
      *
     C                   endsr
      *=====================================================================******
      *  Klargjør / send bot-record
      *=====================================================================******
     C     BOTTOM        begsr
     C                   Move      *zeros        ppsalt
     C     PPN           CHAIN     PAPA
     c     PPSALT        IFGT      0
     c                   movel     'VÅR favør'   Tottxt
     c                   else
     C                   mult      -1            PPSALT
     c                   movel     'DERES favør' Tottxt
     c                   end
     C                   callp     updHTMLvar('TOTTXT':Tottxt)
     C                   callp     updHTMLvar('SALDO':
     C                             %trim(%editc(PPSALT:'3')))
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
      *
     C                   callp     wrtsection('bot')
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      PallDL04
     C                   OPEN      Papa
      *
     C                   endsr
