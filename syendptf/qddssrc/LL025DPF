     A*****************************************************************
     A* UTSKRIFT AV TOLLAGERLISTE                                     *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132 OVERFLOW=45       *
     A*****************************************************************
     A* HEADING
     A          R HEAD
     A            KFMT1         75      1  1
     A  88                              1 76'¤&l'
     A  88        COPY           2  0   1 79
     A  88                              1 81'X¤(s15H¤&l1O¤&a-3R'
     A            KFMT2         75      2  1
     A  88                              2 76'¤(12U¤(s0p15h0s3b4099T'
     A*
     A                                  3 58MSGCON(30 JOV1253 SYMSGF)
     A                                      HIGHLIGHT
     A                                  3101MSGCON(14 YBC1334 SYMSGF)
     A                                    +7PAGNBR EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                  4101MSGCON(14 YBC1337 SYMSGF)
     A            XIDAG          8S 0   4116EDTWRD('    .  .  ')
     A                                      HIGHLIGHT
S01  A*                                 6  2MSGCON(08 JOV1254 SYMSGF)
N01  A N51N52                           6  2MSGCON(08 JOV1254 SYMSGF)
N01  A  51                              6  2MSGCON(08 JOV1282 SYMSGF)
N01  A  52                              6  2MSGCON(08 JOV1254 SYMSGF)
     A            WSGNF         15A     6 11
     A                                      HIGHLIGHT
     A                                  6 26'->'
     A            WSGNT         15A     6 29
     A                                      HIGHLIGHT
     A                                  6 47MSGCON(08 YBC1341 SYMSGF)
     A            WSLAGF        25A     6 56
     A                                      HIGHLIGHT
     A                                  6 77'->'
     A            WSLAGT        25A     6 80
     A                                      HIGHLIGHT
     A                                  6101MSGCON(17 YBC1342 SYMSGF)
     A            WSDET          1A     6119
     A                                      HIGHLIGHT
     A                                  7  2MSGCON(08 YBC1340 SYMSGF)
     A            WSARTF        15A     7 11
     A                                      HIGHLIGHT
     A                                  7 26'->'
     A            WSARTT        15A     7 29
     A                                      HIGHLIGHT
     A                                  7 60MSGCON(24 YBC1344 SYMSGF)
     A            WSSALD         1A     7 85
     A                                      HIGHLIGHT
     A                                  7101MSGCON(15 YBC1345 SYMSGF)
     A            WSBRUD         1A     7119
     A                                      HIGHLIGHT
     A                                  8  2'L.kunde:'
S01  A*           KNAVN         30A     8 11HIGHLIGHT
N01  A            KUNKNR         8S 0   8 11EDTCDE(Z)
N01  A            KUNKNAV       30A     8 20HIGHLIGHT
      * Kolonneoverskrift
S01  A*                                 9  2MSGCON(130 JOV1256 SYMSGF)
      * 51 = DK / 52 = SE
N01  A*N51N52                           9  2MSGCON(130 JOV1256 SYMSGF)
N01  A N51N52                           9  2MSGCON(130 JOV1768 SYMSGF)
N01  A  51                              9  2MSGCON(130 JOV1683 SYMSGF)
N01  A  52                              9  2MSGCON(130 JOV1684 SYMSGF)
     A                                      HIGHLIGHT
     A* DELESTREK ETTER HEAD (NÅR SUB-DETALJER IKKE SKRIVES)
     A          R HEASTR                    SPACEB(1)
     A                                     1'-------------------------'
     A                                    26'-------------------------'
     A                                    51'-------------------------'
     A                                    76'-------------------------'
     A                                   101'-------------------------------'
     A* DELESTREK FØR DET-POST (NÅR SUB-DETALJER SKRIVES)
     A          R DETSTR                    SPACEB(1)
     A                                     1'========================='
     A                                    26'========================='
     A                                    51'========================='
     A                                    76'========================='
     A                                   101'==============================='
     A*DETALJER (Dette gods/artikkelnr)
     A          R DETALJ                    SPACEB(1)
     A            MRGN          15A        2
     A            XXMOTT        10A       18
     A            MRTRNR        18A       29
N01  A*           ORILK          2A       44
     A*           VAANR         15A       48
N01  A*           VAFAR         10A       64
N01  A*           VAVERD         9S 2     67EDTCDE(3)
N01  A            ORILK          2A       48
     A            VAANR         15A       51
N01  A            VAFAR         10A       67
N01  A            VAVERD         9S 2     70EDTCDE(3)
     A            VADATO         8S 0     81EDTWRD('    .  .  ')
N01  A            KJFAKT        25A       92
     A            VAOANT         7S 0    113EDTCDE(L)
     A            VASALD         7S 0    123EDTCDE(L)
N01  A          R SERLIN                    SPACEB(1)
N01  A            SERTXT       120A        6
     A*TOTALER (DETTE BRUDDNIVÅ/GODSNR)
     A          R TOTAL                     SPACEB(1)
S01  A*                                    2MSGCON(25 JOV1255 SYMSGF)
N01  A N51N52                              2MSGCON(25 JOV1255 SYMSGF)
N01  A  51                                 2MSGCON(25 JOV1585 SYMSGF)
N01  A  52                                 2MSGCON(25 JOV1255 SYMSGF)
N05  A                                    40'Ant art:'
     A            TOTPOS         5S 0     48EDTCDE(L)
N05  A                                    56'Utt.:'
N05  A            TOTUTA         9S 0     61EDTCDE(L)
N05  A                                   101'Ant opplagt:'
     A            TOTUPL         9S 0    113EDTCDE(L)
     A            TOTSAL         9S 0    123EDTCDE(L)
     A                                      SPACEA(1)
     A*TOTALER (GRANDTOTAL)
     A          R GTOTAL                    SPACEB(1)
     A                                     2MSGCON(20 YBC1362 SYMSGF)
     A*           GTOPOS         9S 0     44EDTCDE(L)
N05  A            GTOPOS         9S 0     42EDTCDE(L)
N05  A            GTOUTA         9S 0     59EDTCDE(L)
     A            GTOUPL         9S 0    111EDTCDE(L)
     A            GTOSAL         9S 0    121EDTCDE(L)
      *
     A*SUB-DETALJ-HEADING (motposter på ett gn/art.nr)
     A          R HEADET                    SPACEB(1)
     A                                     1MSGCON(100 YBC1363 SYMSGF)
     A*SUB-DETALJER (motposter på ett art.nr)
     A          R DETDET                    SPACEB(1)
N01  A            SNSN20        20A        6
     A            VDAVD          4S 0     27EDTCDE(Z)
     A            VDOPD          7S 0     33EDTCDE(Z)
     A            VDFBNR         3S 0     42EDTCDE(Z)
     A*
     A            VDLDAT         8S 0     52EDTWRD('    -  -  ')
     A            VDLANT         7S 0     63EDTCDE(Z)
     A            VDTEXT        30A       71
     A            VDBUSE        10A      103
     A            VDBDAT         8S 0    113EDTWRD('    -  -  ')
     A            VDBKL          6S 0    124EDTWRD('  :  :  ')
