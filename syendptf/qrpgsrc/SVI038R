********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 Avrunda vikter på varuposter
********************************************************************
     FSVIFZ     IF   E           K DISK
     FSVIFZL01  UF   E           K DISK
     F                                     RENAME(SVIFR:SVIFR2)
     FSVIF      UF A E           K DISK
     F                                     RENAME(SVIFR:SVIFR1)
     FSVTVK     IF   E           K DISK
     FSVIVZL01  UF   E           K DISK
     F                                     RENAME(SVIVR:SVIVR1)
     FSVIV      IF A E           K DISK
     FSVI038D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
N01  D W110            S             11  0
      *____
      * Inz
      *""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIF_SYAV
     C                   PARM                    UVIF_SYOP

     C     *LIKE         DEFINE    SVIF_SYAV     UVIF_SYAV
     C     *LIKE         DEFINE    SVIF_SYOP     UVIF_SYOP
     C     *LIKE         DEFINE    SVIV_SYLI     ZVIV_SYLI
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS

     C                   MOVEL     *BLANKS       WVIF_REFF
     C                   EVAL      ZVVK_DTS=99999999

     C     SVIFZKEY      KLIST
     C                   KFLD                    WVIF_REFF
     C     SVTVKKEY      KLIST
     C                   KFLD                    SVIF_VAKD
     C                   KFLD                    ZVVK_DTS
     C     SVIFZKEY1     KLIST
     C                   KFLD                    SVIF_UNIK
     C                   KFLD                    SVIF_REFF
     C     SVIVKEY       KLIST
     C                   KFLD                    UVIF_SYAV
     C                   KFLD                    UVIF_SYOP
      *______________
      * Clear Subfile
      *""""""""""""""
     C     START1        TAG

     C                   MOVE      '1'           *IN93
     C                   MOVE      *BLANKS       SFNR
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     WVIF_REFF     SETLL     SVIFZ
     C                   READ      SVIFZ                                  94

     C     *IN94         DOWEQ     '0'
     C     SVIF_SYAV     IFEQ      *ZEROS
     C     SVIF_SYOP     ANDEQ     *ZEROS
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   ENDIF
     C                   READ      SVIFZ                                  94
     C  N94              ENDDO
      *________________
      * Display Subfile
      *""""""""""""""""
     C     START2        TAG

     C                   MOVE      *OFF          *IN51
     C                   MOVEA     '11'          *IN(91)
      * KOMMER HIT VED TOM FIL
     C     ZRECNO        IFEQ      0
     C                   CLEAR                   SUBFIL
     C                   EVAL      SVIF_FATX='Inga externa fakturor att visa'
     C                   MOVE      *OFF          *IN94
     C                   ADD       1             ZRECNO
     C                   MOVE      *ON           *IN51
     C                   WRITE     SUBFIL
     C                   SETON                                        94
     C                   ENDIF
     C                   WRITE     WIND01
     C                   WRITE     BUNN
     C     STBUNN        TAG

     C                   MOVEL     *BLANKS       WVIF_REFF
     C                   EXFMT     SUBCTL

     C                   SETOFF                                       999830
      * FORRIGE
     C   12              GOTO      SLUTT

     C     ZRECNO        IFNE      0
     C                   MOVE      *OFF          *IN98
     C                   READC     SUBFIL                                 98
     C     *IN98         DOWEQ     '0'
     C     SFNR          IFEQ      '1'
     C                   EXSR      SRGET
     C                   ENDIF
     C     SFNR          IFEQ      '4'
     C                   EXSR      SRDEL
     C                   ENDIF
     C                   READC     SUBFIL                                 98
     C  N98              ENDDO

     C                   ENDIF
     C     WVIF_REFF     CABNE     *BLANKS       START1
     C                   GOTO      START2

     C     SLUTT         TAG

     C                   EVAL      *INLR=*ON
      *__________________________________________
      * Hent faktura                        SRGET
      *""""""""""""""""""""""""""""""""""""""""""
     C     SRGET         BEGSR
      * Oppdatere SUBFIL
     C                   EVAL      WVIF_REFF=*BLANKS
     C                   EVAL      WVIF_REFF='A'
      * Skriv till fakturafil
     C                   EVAL      SVIF_SYAV=UVIF_SYAV
     C                   EVAL      SVIF_SYOP=UVIF_SYOP
     C                   EVAL      *IN54=*OFF
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  54
     C     *IN54         IFEQ      *OFF
     C                   EVAL      SVIF_VAKU=SVVK_KRS
     C                   ENDIF
     C                   WRITE     SVIFR1
      * Oppdatere externfil med avd/opd
     C     SVIFZKEY1     CHAIN     SVIFZL01                           54
     C                   IF        *IN54=*OFF
     C                   EVAL      SVIF_SYAV=UVIF_SYAV
     C                   EVAL      SVIF_SYOP=UVIF_SYOP
     C                   EVAL      SVIF_VAKU=SVVK_KRS
     C                   UPDATE    SVIFR2
     C                   ENDIF
      * Hitta høgsta varupostnummret
     C                   EVAL      *IN55=*OFF
     C     SVIVKEY       SETLL     SVIV
     C     SVIVKEY       READE     SVIV                                   55
     C     *IN55         DOWEQ     *OFF
     C                   EVAL      ZVIV_SYLI=SVIV_SYLI
     C     SVIVKEY       READE     SVIV                                   55
     C                   ENDDO
      * Les varuposter externe og add dem til produksjon
     C                   EVAL      *IN54=*OFF
     C     SVIFZKEY1     SETLL     SVIVZL01
     C     SVIFZKEY1     READE     SVIVZL01                               54
     C     *IN54         DOWEQ     *OFF

     C                   EVAL      SVIV_SYAV=UVIF_SYAV
     C                   EVAL      SVIV_SYOP=UVIF_SYOP
     C                   ADD       1             ZVIV_SYLI
     C                   EVAL      SVIV_SYLI=ZVIV_SYLI
     C                   EVAL      SVIV_VANO=ZVIV_SYLI
     C                   UPDATE    SVIVR1
     C                   EVAL      SVIV_CALL='00'
     C                   EVAL      SVIV_ERR='X'
N01  C                   IF        SVIV_BRUT > 1
N01  C                   EVAL(H)   W110=SVIV_BRUT
N01  C                   EVAL      SVIV_BRUT=W110
N01  C                   ENDIF
N01  C                   IF        SVIV_NETO > 1
N01  C                   EVAL(H)   W110=SVIV_NETO
N01  C                   EVAL      SVIV_NETO=W110
N01  C                   ENDIF
     C                   WRITE     SVIVR

     C     SVIFZKEY1     READE     SVIVZL01                               54
     C                   ENDDO
     C                   ENDSR
      *__________________________________________
      * Radera faktura                      SRDEL
      *""""""""""""""""""""""""""""""""""""""""""
     C     SRDEL         BEGSR
      * Oppdatere SUBFIL
     C                   EVAL      WVIF_REFF=*BLANKS
     C                   EVAL      WVIF_REFF='A'
      * Radera externfil
     C     SVIFZKEY1     CHAIN     SVIFZL01                           54
     C                   IF        *IN54=*OFF
     C                   DELETE    SVIFR2
     C                   ENDIF
      * Radera varuposter
     C                   EVAL      *IN54=*OFF
     C     SVIFZKEY1     SETLL     SVIVZL01
     C     SVIFZKEY1     READE     SVIVZL01                               54
     C     *IN54         DOWEQ     *OFF

     C                   DELETE    SVIVR1

     C     SVIFZKEY1     READE     SVIVZL01                               54
     C                   ENDDO
     C                   ENDSR
