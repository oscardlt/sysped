      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE000R MODULE(*LIBL/TNOE000R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FSAEH      IF   E           K DISK    USROPN
     F                                     PREFIX(O2_)
     FTRUV07    IF   E           K DISK    USROPN
     FTRUH      IF   E           K DISK    USROPN
     FARKLOGH4  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     ‚*  Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunk_                              like(ufunk)
     d udat1_                              like(udat1)
     d udat2_                              like(udat2)
     d udgr_                               like(udgr)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D SG              S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D REFNR           S             18
     D DATUM           S              8
     D DATUMN          S              8S 0
     D DATUMT          S              8
     D DATUMTN         S              8S 0
     D STATUS          S              1
     D AVSNAVN         S             35
     D MOTNAVN         S             35
     D LOPENR          S             10
     D AART            S              2
     D GODSNR          S             15
     D FAKNR           S             17
     D XREF            S             35
     D DOKREF          S             18
     D INNSTIKK        S              1
     D EPJN            S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d hopp            S              1
     d wspll           S              1
     d STOPP5          S             35
     d pos             S              5U 0
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d wsetdn          s                   like(o2_setdn)
     d MyPoDS          ds                  Qualified  dim(500)
     d  SEAVD                         4S 0
     d  SETDN                         7S 0
     d  SE0035                        1
     d  SETLL                        10
     d  SETLE                         6
     d  SEDTY                         2
     d  SESG                          3
     d  SEDT                          8S 0
     d  SEVKB                         9  0
     d  SEST                          1
     d  SENAS                        30
     d  SENAK                        30
     d  SEGN                         15
     d  SEMI                          1
     d  SEFIF                        17
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sg¬ : ¬'+%trim(SG)+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬refnr¬ : ¬'+%trim(REFNR)+'¬, '
        +'¬datum¬ : ¬'+%trim(%editc(DATUMN:'Z'))+'¬, '
        +'¬datumt¬ : ¬'+%trim(%editc(DATUMTN:'Z'))+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬avsNavn¬ : ¬'+%trim(AVSNAVN)+'¬, '
        +'¬motNavn¬ : ¬'+%trim(MOTNAVN)+'¬, '
        +'¬lopenr¬ : ¬'+%trim(LOPENR)+'¬, '
        +'¬aart¬ : ¬'+%trim(AART)+'¬, '
        +'¬godsnr¬ : ¬'+%trim(GODSNR)+'¬, '
        +'¬xref¬ : ¬'+%trim(XREF)+'¬, '
        +'¬innstikk¬ : ¬'+%trim(INNSTIKK)+'¬, '
        +'¬faknr¬ : ¬'+%trim(FAKNR)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' SEAVD  = ' + AVD;
       EndIf;

       If    SG  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SESG = ' + cQuote + SG + cQuote +' ';
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SETDN  = ' + OPD;
       Else;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SETDN  > 0';
       EndIf;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SETDN  > 0';

       If    DATUMN > 0;
         DATUM = %editc(DATUMN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEDT  >= ' + DATUM;
       EndIf;

       If    DATUMTN > 0;
         DATUMT = %editc(DATUMTN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEDT  <= ' + DATUMT;
       EndIf;

       If    STATUS  <> *blanks and STATUS <> 'b';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEST = ' + cQuote + STATUS + cQuote;
        EndIf;

       If    AVSNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(SENAK) LIKE UPPER(' + cQuote + '%' +
         %trim(AVSNAVN) + '%' + cQuote + ')';
        EndIf;

       If    MOTNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(SENAS) LIKE UPPER(' + cQuote + '%' +
         %trim(MOTNAVN) + '%' + cQuote + ')';
        EndIf;

       If    LOPENR  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SETLL LIKE ' + cQuote + '%' + %trim(LOPENR) + '%' + cQuote;
        EndIf;


       If    GODSNR  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEGN LIKE ' + cQuote + '%' + %trim(GODSNR) + '%' + cQuote;
        EndIf;

       If    AART  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEDTY = ' + cQuote + AART + cQuote +' ';
       EndIf;

       If    INNSTIKK  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEMI = ' + cQuote + INNSTIKK + cQuote +' ';
       EndIf;

       If    FAKNR  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SEFIF LIKE ' + cQuote + '%' + %trim(FAKNR) + '%' + cQuote;
        EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select SEAVD, SETDN, SE0035, SETLL, ' +
                             'SETLE, SEDTY, SESG, SEDT, SEVKB, ' +
                             'SEST, SENAS, SENAK, SEGN, SEMI, SEFIF ' +
                             'from SAEH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'SEDT desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avd     = zhbgetvar('AVD');
         avdn    = c2n2(avd);
         sg      = zhbgetvar('SG');
         opd     = zhbgetvar('OPD');
         opdn    = c2n2(opd);
         refnr   = zhbgetvar('REFNR');
         datum   = zhbgetvar('DATUM');
         datumn  = c2n2(datum);
         datumt  = zhbgetvar('DATUMT');
         datumtn = c2n2(datumt);
         status  = zhbgetvar('STATUS');
         avsnavn = zhbgetvar('AVSNAVN');
         motnavn = zhbgetvar('MOTNAVN');
         lopenr  = zhbgetvar('LOPENR');
         aart    = zhbgetvar('AART');
         godsnr  = zhbgetvar('GODSNR');
         xref    = zhbgetvar('XREF');
         innstikk= zhbgetvar('INNSTIKK');
         faknr   = zhbgetvar('FAKNR');

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).sesg <> *blanks;

           chain (mypods(x).SEAVD: mypods(x).SETDN: '*CE') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;
        // Hent dokumenref (MRN-nr i export transitering)
           chain (mypods(x).SEAVD: mypods(x).SETDN) TRUV07;
           if not %found;
              dokref = *blanks;
           endif;
           if %found;
           chain (tvavd: tvtdn) TRUH;
             if not %found;
                dokref = *blanks;
             endif;
             if %found;
                dokref = thtrnr;
             endif;
           endif;
N01

        // Valider extern ref
           error=' ';
           if xref <> *blanks;
             pos = %scan (%trim(XREF) : FSSOK);
               if pos = 0;
                 error='X';
               endif;
           endif;

        // Finnes omberegning ?

           wsetdn = mypods(x).SETDN * -1;
           chain (mypods(x).SEAVD: wsetdn) SAEH;
           if not %found;
              O2_SEST = *BLANKS;
              O2_SEMF = *BLANKS;
              O2_SETLL = *BLANKS;
              O2_SEDT = 0;
           else;
              if O2_SETLL = mypods(x).setll;
                O2_SETLL = *BLANKS;
              endif;
           endif;

        // Er e-post sendt ?
           chain (mypods(x).SEAVD: mypods(x).SETDN) ARKLOGH4;
           if %found;
              epjn = 'E';
           else;
              epjn = *BLANKS;
           endif;

        // Videre hvis ikke noen feil !


           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).SEAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).SETDN:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(mypods(x).SE0035)+'¬, '
          +'¬setll¬ : ¬'+%trim(mypods(x).SETLL)+'¬, '
          +'¬setle¬ : ¬'+%trim(mypods(x).SETLE)+'¬, '
          +'¬aart¬ : ¬'+%trim(mypods(x).SEDTY)+'¬, '
          +'¬sg¬ : ¬'+%trim(mypods(x).SESG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(mypods(x).SEDT:'Z'))+'¬, '
          +'¬sevkb¬ : ¬'+%trim(%editc(mypods(x).SEVKB:'4'))+'¬, '
          +'¬status¬ : ¬'+%trim(mypods(x).SEST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(mypods(x).SENAK)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(mypods(x).SENAS)+'¬, '
          +'¬segn¬ : ¬'+%trim(mypods(x).SEGN)+'¬, '
          +'¬semi¬ : ¬'+%trim(mypods(x).SEMI)+'¬, '
          +'¬sefif¬ : ¬'+%trim(mypods(x).SEFIF)+'¬, '
          +'¬h_xref¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬dokref¬ : ¬'+%trim(DOKREF)+'¬, '
          +'¬epjn¬ : ¬'+%trim(EPJN)+'¬, '
          +'¬o2_sest¬ : ¬'+%trim(o2_sest)+'¬, '
          +'¬o2_semf¬ : ¬'+%trim(o2_semf)+'¬, '
          +'¬o2_setll¬ : ¬'+%trim(o2_setll)+'¬, '
          +'¬o2_sedt¬ : ¬'+%trim(%editc(o2_sedt:'Z'))+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;
        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open frisok;
         open brparf;
         open saeh;
         open truv07;
         open truh;
         open arklogh4;

         // Er dato= blank eller 0, sett  da dato 7 dager tilbake
         if opdn=*zeros and refnr=*blanks and datumn=*zeros and
            Lopenr=*blanks and Godsnr=*blanks and Xref=*blanks and
            Faknr=*blanks;

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              pavrdn=7;
           endif;

           ufunk = 'SUB';
           udat1 = datumn;
           udgr = pavrdn;
           syge20 ( ufunk : udat1 : udat2 : udgr );
           datumn=udat2;
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close frisok;
         close brparf;
         close saeh;
         close truv07;
         close truh;
         close arklogh4;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
