     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2')
     A            VBSK           2A  O  5  5
     A            VBVB          35A  O  5  8
     A            VBHB1         35A  H
     A            VBHB2         35A  H
     A            VBUSER        10A  H
     A            VBDT           8  0H
     A            VBTD           6  0H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL001RB'
     A                                      COLOR(BLU)
     A                                  1 10'Aribed med språk'
     A                                      COLOR(WHT)
     A            UGRUP          8  0H
     A            UPNR           8  0H
     A                                  2  2'Art:'
     A            UANR          15A     2  7DSPATR(UL)
     A                                  3  2'Velg option:  2=Endre'
     A                                      COLOR(BLU)
     A                                  4  2'Op SK Varebeskrivelse'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Avsl F5=Frisk F6=Lage ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        33A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL001RA'
     A                                      COLOR(BLU)
     A                                  1 10'Aribed med språk'
     A                                      COLOR(WHT)
     A            UGRUP          8  0H
     A            UPNR           8  0H
     A                                  2  2'Art:'
     A            UANR          15A     2  7DSPATR(UL)
     A                                  3  2'Velg option:  2=Endre'
     A                                      COLOR(BLU)
     A                                  4  2'Op SK Varebeskrivelse'
     A                                      COLOR(WHT)
     A                                 10  2'F3/F12=Avsl F5=Frisk F6=Lage ny'
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL001RA'
     A                                      COLOR(BLU)
     A                                  1 10'Vedlikehold tariffnr'
     A                                      COLOR(WHT)
     A                                  2  2'Art:'
     A            UANR          15A  O  2  7DSPATR(UL)
     A                                  4  2'Språkkod:'
     A            WSSK           2A  B  4 13DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  40                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A                                  5  2'Varebesk:'
     A            WSVB          35A  B  5 13DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                  6  2'Hovedbes:'
     A            WSHB1         35A  B  6 13DSPATR(UL)
     A            WSHB2         35A  B  7 13DSPATR(UL)
     A                                  9  2'F4=Søk F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        33A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
