     H DFTACTGRP(*NO)
      *
      ***********************************************************
      *** PROGRAM SKAL SENDE KRYSSEREFERANSE                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-99                            ***
      ***********************************************************
     FEDIC      UF A E             DISK
     FEDIS      O  A E             DISK
      /copy ifsio_h
      *
     D FILE_o          s             10I 0
     D String          s            512a
     D rc              s             10i 0
     D Idx             s              5u 0
     D FILNAM          s            100A
     D XCHGAUT         s            200A
     D LF              c                   x'25'
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
     D                 DS
     D  WDT                    1      8
     D  WDTA                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRSNDST
      *
     C                   EXSR      SRHEADER
      *
     C                   EXSR      SRXREF
      *
     C                   EXSR      SRSNDSL
     C                   EXSR      SRCLOSE
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRSNDST       BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       SDATA          1024
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0004 = 'C-3248_OWN'                         Avsender
     C                   EVAL      S0010 = 'C-3248_OTHER'                       Mootaker
     C                   EVAL      S0020 = 'C' + %TRIM(USYSKN)
     C                   MOVE      CSN           XAN09             9
     C                   EVAL      S0026 = 'F' + %TRIM(XAN09)
     C                   Z-ADD     1             S0036
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = *BLANKS
     C                   EVAL      SFILE = '*IFS'
     C                   EVAL      SMBR  = *BLANKS
     C                   EVAL      SMBRS  = 0
      *
     C                   MOVE      *BLANKS       TIMESTP         100
     C                   EVAL      TIMESTP = %CHAR(%TIMESTAMP():*iso0)
     C                   EVAL      FILNAM = '/' + %TRIM(ULIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + 'UpdateXRef_' + %TRIM(UBRIKN)
     C                             + '_SYSPED_' + %TRIM(TIMESTP) + '.xml'
      * 1208 = UTF-8
     C                   eval      FILE_o = open( %TrimR( FilNam )
     C                               : O_CREAT+O_TRUNC+O_WRONLY
     C                                + O_TEXT_CREAT+O_CCSID+O_TEXTDATA
     C                               : S_IRUSR + S_IWUSR
     C                               : 1208
     C                               : 0)
      *
     C                   EVAL      SIFS   = FILNAM
     C                   WRITE     EDISR
      *
     C                   EVAL      SDATA = '<?xml version="1.0" '
     C                             + 'encoding="UTF-8"?>'
     C                   EXSR      OPDFIL
      * <upd1:UpdateXRefCustomerAccount> start
     C                   EVAL      SDATA = '<upd1:UpdateXRefCustomerAccount '
     C                             + 'xmlns:v1="urn:posten.no/eConnect/Common/H'
     C                             + 'eader/v1" '
     C                             + 'xmlns:upd="urn:posten.no/eConnect/EBO/Cus'
     C                             + 'tomerParty/v1/UpdateXRefCustomerRequest" '
     C                             + 'xmlns:upd1="urn:posten.no/eConnect/EBO/Cu'
     C                             + 'stomerParty/v1/UpdateXRefCustomerAccount"'
     C                             + '>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRHEADER      BEGSR
      ***********************************************
      * <v1:Header> Start
     C                   EVAL      SDATA = '<v1:Header>'
     C                   EXSR      OPDFIL
      * <v1:MessageId>
     C                   EVAL      SDATA = '<v1:MessageId>T50565_'
     C                             + %TRIM(S0026)
     C                             + '</v1:MessageId>'
     C                   EXSR      OPDFIL
      * <v1:ServiceName>
     C                   EVAL      SDATA = '<v1:ServiceName>'
     C                             + 'T50565_UpdateAccountXrefSysped'
     C                             + '</v1:ServiceName>'
     C                   EXSR      OPDFIL
      * <v1:MessageType>
     C                   EVAL      SDATA = '<v1:MessageType>'
     C                             + 'T50565_UpdateAccountXrefSysped'
     C                             + '</v1:MessageType>'
     C                   EXSR      OPDFIL
      * <v1:MessageMode>
     C                   EVAL      SDATA = '<v1:MessageMode>'
     C                             + 'REQUEST'
     C                             + '</v1:MessageMode>'
     C                   EXSR      OPDFIL
      * <v1:FirstProcessedTimestamp>
     C                   EVAL      SDATA = '<v1:FirstProcessedTimestamp>'
     C                             + %TRIM(WDTA) + '-'
     C                             + %TRIM(WDTM) + '-'
     C                             + %TRIM(WDTD) + 'T'
     C                             + %TRIM(WTMH) + ':'
     C                             + %TRIM(WTMM) + ':'
     C                             + %TRIM(WTMS)
     C                             + '</v1:FirstProcessedTimestamp>'
     C                   EXSR      OPDFIL
      * <v1:ProcessedTimestamp>
     C                   EVAL      SDATA = '<v1:ProcessedTimestamp>'
     C                             + %TRIM(WDTA) + '-'
     C                             + %TRIM(WDTM) + '-'
     C                             + %TRIM(WDTD) + 'T'
     C                             + %TRIM(WTMH) + ':'
     C                             + %TRIM(WTMM) + ':'
     C                             + %TRIM(WTMS)
     C                             + '</v1:ProcessedTimestamp>'
     C                   EXSR      OPDFIL
      * <v1:ProcessedTimestamp>
     C                   EVAL      SDATA = '<v1:SourceSystemTimestamp>'
     C                             + %TRIM(WDTA) + '-'
     C                             + %TRIM(WDTM) + '-'
     C                             + %TRIM(WDTD) + 'T'
     C                             + %TRIM(WTMH) + ':'
     C                             + %TRIM(WTMM) + ':'
     C                             + %TRIM(WTMS)
     C                             + '</v1:SourceSystemTimestamp>'
     C                   EXSR      OPDFIL
      * <v1:SourceCompany>
     C                   EVAL      SDATA = '<v1:SourceCompany>'
     C                             + 'Posten'
     C                             + '</v1:SourceCompany>'
     C                   EXSR      OPDFIL
      * <v1:SourceSystem>
     C                   EVAL      SDATA = '<v1:SourceSystem>'
     C                             + 'SYSPED'
     C                             + '</v1:SourceSystem>'
     C                   EXSR      OPDFIL
      * <v1:SourceSystemUser>
     C                   EVAL      SDATA = '<v1:SourceSystemUser>'
     C                             + '</v1:SourceSystemUser>'
     C                   EXSR      OPDFIL
      * <v1:SourceSystemRef>
     C                   EVAL      SDATA = '<v1:SourceSystemRef>'
     C                             + '</v1:SourceSystemRef>'
     C                   EXSR      OPDFIL
      * <v1:OperationName>
     C                   EVAL      SDATA = '<v1:OperationName>'
     C                             + 'UpdateXref'
     C                             + '</v1:OperationName>'
     C                   EXSR      OPDFIL
      * <v1:IntergationURI>
     C                   EVAL      SDATA = '<v1:IntergationURI>'
     C                             + 'T50565_UpdateAccountXrefSysped/'
     C                             + 'ProxyServices/'
     C                             + 'T50565_UpdateAccountXrefSysped_PS'
     C                             + '</v1:IntergationURI>'
     C                   EXSR      OPDFIL
      * <v1:ResubmissionPoint>
     C                   EVAL      SDATA = '<v1:ResubmissionPoint>'
     C                             + 'PROXY'
     C                             + '</v1:ResubmissionPoint>'
     C                   EXSR      OPDFIL
      * </v1:Header> Slutt
     C                   EVAL      SDATA = '</v1:Header>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXREF        BEGSR
      ***********************************************
      * <upd:UpdateXRefCustomerRequest> start
     C                   EVAL      SDATA = '<upd:UpdateXRefCustomerRequest>'
     C                   EXSR      OPDFIL
      * <upd:OrigSystem>
     C                   EVAL      SDATA = '<upd:OrigSystem>'
     C                             + 'SYSPED'
     C                             + '</upd:OrigSystem>'
     C                   EXSR      OPDFIL
      * <upd:OrigSystemReference>
     C                   EVAL      SDATA = '<upd:OrigSystemReference>'
     C                             + %TRIM(USYSKN)
     C                             + '</upd:OrigSystemReference>'
     C                   EXSR      OPDFIL
      * <upd:EntityName>
     C                   EVAL      SDATA = '<upd:EntityName>'
     C                             + 'ACCOUNT'
     C                             + '</upd:EntityName>'
     C                   EXSR      OPDFIL
      * <upd:EntityId>
     C                   EVAL      SDATA = '<upd:EntityId>'
     C                             + %TRIM(UBRIKN)
     C                             + '</upd:EntityId>'
     C                   EXSR      OPDFIL
      * <upd:UpdateXRefCustomerRequest> slutt
     C                   EVAL      SDATA = '</upd:UpdateXRefCustomerRequest>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSNDSL       BEGSR
      ***********************************************
      * </upd:UpdateXRefCustomerRequest> slutt
     C                   EVAL      SDATA = '</upd1:UpdateXRefCustomerAccount>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
      *
     C                   callp     close( FILE_o )
     C                   EVAL      XCHGAUT = 'CHGAUT OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS
     C                             + ') USER(*PUBLIC) DTAAUT(*RWX) OBJAUT(*ALL)'
     C                   CALLP     RUNCMD(XCHGAUT:200)
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDFIL        BEGSR
      ***********************************************
     C                   EVAL      SDATA=(%trimr(SDATA))+ LF
     c                   callp     write(File_o : %addr(Sdata)
     c                                : %len(%trimr(Sdata)) )
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    ULIBF            10
     C                   PARM                    USYSKN            8
     C                   PARM                    UBRIKN           15
      *
     C                   MOVE      *BLANKS       WDT
     C                   MOVE      *BLANKS       WTM
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
      *
     C                   ENDSR
      *
