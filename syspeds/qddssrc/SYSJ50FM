     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYSJ50PNL)
     A                                      HLPTITLE('BOOKING SJØ/EKSPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt MED UTSKRIFT')
     A                                      CF04(04 'SØK')
     A                                      CF09(09 'Bytt signatur')
     A                                  1  2'SYSPED/SYSJ50'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3225 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            UFIRM          2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  6 15MSGCON(18 DIV0599 SYMSGF)
     A            WSSG           3A  B  6 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                  8 15MSGCON(18 DIV0600 SYMSGF)
     A            WSAVD          4Y 0B  8 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                 10 15MSGCON(18 DIV0601 SYMSGF)
     A            WSOPD          7Y 0B 10 35TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N30
     AO 97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10 50MSGCON(20 DIV0604 SYMSGF)
     A                                 14 15MSGCON(18 DIV0602 SYMSGF)
     A            BOAVD          4Y 0B 14 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            BOOPD          7Y 0B 14 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 14 50MSGCON(20 DIV0036 SYMSGF)
     A                                 17 15MSGCON(18 DIV0603 SYMSGF)
     A            WSOPDG         7Y 0O 17 35COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23 15MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 35MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 55MSGCON(18 DIV0606 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF02(02 'ENDRE MODUS')
     A                                      CF03(03 'Avslutt registrering av BO-
     A                                      OKING')
     A                                      CF04(04 'Søke')
     A                                      CF05(05 'VIDERE')
     A                                      CF07(07 'TRANSPORTØR/RATE')
     A                                      CF09(09 'Ny tur')
     A                                      CF10(10 'B/L VARELINJE TYPE 1')
     A                                      CF11(11 'B/L VARELINJE TYPE 2')
     A                                      CF12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning til kjøper-
     A                                       / SELGER')
     A                                      CF15(15 'FrI SØK')
     A                                      CF18(18 'Freetext')
     A          H                           HLPPNLGRP(SYSJ50010 SYSJ50PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SYSJ50020 SYSJ50PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SYSJ50025 SYSJ50PNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(SYSJ50030 SYSJ50PNL)
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPPNLGRP(SYSJ50030 SYSJ50PNL)
     A                                      HLPARA(05 001 05 080)
     A          H                           HLPPNLGRP(SYSJ50030 SYSJ50PNL)
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPPNLGRP(SYSJ50030 SYSJ50PNL)
     A                                      HLPARA(07 001 07 080)
     A          H                           HLPPNLGRP(SYSJ50040 SYSJ50PNL)
     A                                      HLPARA(08 001 10 080)
     A          H                           HLPPNLGRP(SYSJ50050 SYSJ50PNL)
     A                                      HLPARA(11 001 12 080)
     A          H                           HLPPNLGRP(SYSJ50060 SYSJ50PNL)
     A                                      HLPARA(13 001 17 080)
     A          H                           HLPPNLGRP(SYSJ50070 SYSJ50PNL)
     A                                      HLPARA(18 001 21 080)
     A          H                           HLPPNLGRP(SYSJ50999 SYSJ50PNL)
     A                                      HLPARA(22 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSAVDK         4Y 0H
     A            WSOPDK         7Y 0H
     A            MSGNR          7A  P
     A                                  1  2'Opd'
     A            WSOPD          7Y 0O  1  6
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 14'Sg'
     A            WSSG           3A  B  1 17TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  1 21'Booking Eksp.'
     A                                      COLOR(WHT)
     A                                  1 36'Pr'
     A            WSPRO          8Y 0B  1 39DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 48'Uke/År'
     A            WSUKE          2Y 0B  1 55DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSAAR          2Y 0B  1 58DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 61MSGCON(003 DIV3190 *LIBL/SYMSGF)
     A            WSKNR          8Y 0B  1 65DSPATR(UL)
     A  98                                  DSPATR(RI)
     A N98 51                               COLOR(RED)
     A N51                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 74MSGCON(002 DIV3191 *LIBL/SYMSGF)
     A            WSTR           4A  B  1 77DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2  2MSGCON(005 DIV3233 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  2  8DSPATR(UL)
     A  96                                  DSPATR(RI)
     A N96 52                               COLOR(RED)
     A N52                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAA         30A  B  2 17DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 48MSGCON(007 DIV3231 *LIBL/SYMSGF)
     A            WSKNAA        25A  B  2 56DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(005 DIV3232 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  3  8DSPATR(UL)
     A  95                                  DSPATR(RI)
     A N95 53                               COLOR(RED)
     A N53                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAS         30A  B  3 17DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 48MSGCON(007 DIV3231 *LIBL/SYMSGF)
     A            WSKNAS        25A  B  3 56DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(005 DIV3200 *LIBL/SYMSGF)
     A            WSLB           5A  B  4  8DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLBN         20A  B  4 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 35MSGCON(005 DIV3199 *LIBL/SYMSGF)
     A            WSLSH          5A  B  4 41DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLSHN        20A  B  4 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 69MSGCON(005 DIV3235 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            WSDT1          6Y 0B  4 75DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(009 YBC0742 *LIBL/SYMSGF)
     A            WSBKNL        14A  B  5 12DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 27MSGCON(003 DIV3248 *LIBL/SYMSGF)
     A            WSCPL          1A  B  5 31DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'C' 'P')
     A                                  5 33MSGCON(004 DIV3246 *LIBL/SYMSGF)
     A            WSVALL         3A  B  5 38DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBLL          7Y 0B  5 42DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 50MSGCON(005 YBC0743 *LIBL/SYMSGF)
     A            WSBLCP         7Y 0B  5 56DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 64MSGCON(005 YBC0744 *LIBL/SYMSGF)
     A            WSBLG          7Y 0B  5 70DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  35                              6  2MSGCON(008 DIV3236 *LIBL/SYMSGF)
     A  35        WSANM         15A  B  6 11DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                              6 28MSGCON(004 DIV3237 *LIBL/SYMSGF)
     A  35        WSREF1        10A  B  6 33DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                              6 46MSGCON(004 DIV3238 *LIBL/SYMSGF)
     A  35        WSANMN        15A  B  6 51DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                              6 69MSGCON(005 DIV3202 *LIBL/SYMSGF)
     A  35        WSDT2          6Y 0B  6 75DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  35                              7  2MSGCON(008 DIV3239 *LIBL/SYMSGF)
     A  35        WSINS1        55A  B  7 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 69MSGCON(005 DIV3209 *LIBL/SYMSGF)
     A            WSLSTY         3A  B  7 75DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8  2MSGCON(016 YBC0695 *LIBL/SYMSGF)
     A            WSVHL          5A  B  8 19DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8 25MSGCON(005 YBC0694 *LIBL/SYMSGF)
     A            WSDT7          6Y 0B  8 31DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8 48MSGCON(005 DIV3240 *LIBL/SYMSGF)
     A            WSOB           5A  B  8 54DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSOBN         20A  B  8 60DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2MSGCON(005 DIV3201 *LIBL/SYMSGF)
     A            WSVH           5A  B  9  8DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVHN         20A  B  9 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 35MSGCON(005 DIV3235 *LIBL/SYMSGF)
     A            WSDT3          6Y 0B  9 41DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 48MSGCON(008 YBC0696 *LIBL/SYMSGF)
     A            WSVH2          5A  B  9 57DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 63MSGCON(005 YBC0694 *LIBL/SYMSGF)
     A            WSDT8          6Y 0B  9 69DSPATR(UL)
     A  67                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(005 DIV3203 *LIBL/SYMSGF)
     A            WSLOH          5A  B 10  8DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLOHN        20A  B 10 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 35MSGCON(005 YBC0694 *LIBL/SYMSGF)
     A            WSDT9          6Y 0B 10 41DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 48MSGCON(005 DIV3204 *LIBL/SYMSGF)
     A            WSLOS          5A  B 10 54DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLOSN        20A  B 10 60DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(005 DIV3192 *LIBL/SYMSGF)
     A            WSREIS        14A  B 11  8DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                             11 27MSGCON(003 DIV3207 *LIBL/SYMSGF)
     A  35        WSCH           1A  B 11 31DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                             11 35MSGCON(006 DIV3896 *LIBL/SYMSGF)
     A  35        WSKNRP         8Y 0B 11 42DSPATR(UL)
     A  93                                  DSPATR(RI)
     A N93 54                               COLOR(RED)
     A N54                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  35        WSNARP        30A  B 11 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2MSGCON(005 DIV3241 *LIBL/SYMSGF)
     A            WSMM          15A  B 12  8DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                             12 27MSGCON(003 DIV3210 *LIBL/SYMSGF)
     A  35        WSMH           1A  B 12 31DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 35MSGCON(006 DIV3897 *LIBL/SYMSGF)
     A            WSKNDA         8Y 0B 12 42DSPATR(UL)
     A  75                                  DSPATR(RI)
     A N75 56                               COLOR(RED)
     A N56                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNADA        30A  B 12 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13  2MSGCON(078 JOV1053 *LIBL/SYMSGF)
     A            WSNT1          7Y 0B 14  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKDC1         2A  B 14 11DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSMN1         20A  B 14 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS1         25A  B 14 35DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVKT1         9Y 0B 14 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM31          7Y 3B 14 73DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNT2          7Y 0B 15  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKDC2         2A  B 15 11DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSMN2         20A  B 15 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 15 35DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVKT2         9Y 0B 15 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM32          7Y 3B 15 73DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNT3          7Y 0B 16  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKDC3         2A  B 16 11DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSMN3         20A  B 16 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS3         25A  B 16 35DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVKT3         9Y 0B 16 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM33          7Y 3B 16 73DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNT4          7Y 0B 17  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKDC4         2A  B 17 11DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSMN4         20A  B 17 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS4         25A  B 17 35DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVKT4         9Y 0B 17 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM34          7Y 3B 17 73DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(005 DIV3198 *LIBL/SYMSGF)
     A            WSLSS          5A  B 18  8DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLSSN        20A  B 18 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 42MSGCON(005 DIV3202 *LIBL/SYMSGF)
     A            WSDT4          6Y 0B 18 48DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 56MSGCON(004 DIV3237 *LIBL/SYMSGF)
     A            WSREF2        20A  B 18 61DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                             19  2MSGCON(008 DIV3239 *LIBL/SYMSGF)
     A  35        WSINS2        55A  B 19 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19 67MSGCON(012 JOV1042 *LIBL/SYMSGF)
     A            WSBLLI         1   B 19 80COLOR(YLW)
     A  35                             20  2MSGCON(005 DIV3244 *LIBL/SYMSGF)
     A  35        WSDP          20A  B 20  8DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                             20 42MSGCON(005 DIV3202 *LIBL/SYMSGF)
     A  35        WSDT5          6Y 0B 20 48DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  35                             20 56MSGCON(004 DIV3237 *LIBL/SYMSGF)
     A  35        WSREF3        20A  B 20 61DSPATR(UL)
     A                                      COLOR(YLW)
     A  35                             21  2MSGCON(008 DIV3239 *LIBL/SYMSGF)
     A  35        WSINS3        55A  B 21 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 67'Reg.dat'
     A            WSDTR          6S 0O 21 75COLOR(WHT)
     A                                 22  2MSGCON(018 YBC0751 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 YBC0697 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 SHU0127 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(078 YBC0184 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE03
     A                                      WINDOW(8 5 7 68 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt registrering av BO-
     A                                      OKING')
     A                                      CF04(04 'Søke')
     A                                      CF05(05 'VIDERE')
     A                                      CF10(10 'B/L VARELINJE TYPE 1')
     A                                      CF11(11 'B/L VARELINJE TYPE 2')
     A                                      CF15(15 'FrI SØK')
     A                                      CF18(18 'Freetext')
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYSJ50080 SYSJ50PNL)
     A                                      HLPARA(01 001 06 065)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ50'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 DIV3225 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(005 DIV3245 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  2 11DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAT         20A  B  2 20DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(005 DIV3202 *LIBL/SYMSGF)
     A            WSDT6          6Y 0B  3 11DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 19MSGCON(004 DIV3237 *LIBL/SYMSGF)
     A            WSREF4        20A  B  3 24DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(008 DIV3239 *LIBL/SYMSGF)
     A            WSINS4        55A  B  4 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2MSGCON(004 DIV3246 *LIBL/SYMSGF)
     A            WSRATE        50A  B  5 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(009 YBC0742 *LIBL/SYMSGF)
     A            WSREFN        14A  B  6 12DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 27MSGCON(003 DIV3248 *LIBL/SYMSGF)
     A            WSCP           1A  B  6 31DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'C' 'P')
     A                                  6 33MSGCON(004 DIV3247 *LIBL/SYMSGF)
     A            WSVAL          3A  B  6 38DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBL           7Y 0B  6 42DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A N30                              7  2MSGCON(60 YBC0698 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
