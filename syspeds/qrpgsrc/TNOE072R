      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI072R MODULE(*LIBL/TNOI072R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FZAEFL01   UF A E           K DISK    USROPN
     F                                     RENAME(SADFR:ZADFR)
     FZAEVL01   UF A E           K DISK    USROPN
     F                                     RENAME(SAEVR:ZAEVR)
     FSAEF      O  A E             DISK    USROPN
     FSAEV      IF A E           K DISK    USROPN
     FKODTVAL2  IF   E           K DISK    USROPN
     FTARI      IF   E           K DISK    USROPN
     FSADL      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D LEVNR           S              8
     D WMODE           S              1
     D UNIKA           S              9
     D REFF            S             17
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬unik¬ : ¬'+%trim(UNIKA)+'¬, '
        +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬avd¬ : ¬'+%trim(AVD)+'¬, '
        +'¬opd¬ : ¬'+%trim(OPD)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errmsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"updexternfakt" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     ZAEFL01
     C  N50              CLOSE     ZAEVL01
     C  N50              CLOSE     SAEF
     C  N50              CLOSE     SAEV
     C  N50              CLOSE     KODTVAL2
     C  N50              CLOSE     TARI
     C  N50              CLOSE     SADL

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD = zhbgetvar('AVD')
     C                   EVAL      AVDN  = c2n2(AVD)
     C                   EVAL      OPD = zhbgetvar('OPD')
     C                   EVAL      OPDN  = c2n2(OPD)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      REFF  = zhbgetvar('REFF')
     C                   EVAL      UNIKA   = zhbgetvar('UNIK')
     C                   EVAL      UNIK  = c2n2(UNIKA)
     C                   EVAL      LEVNR   = zhbgetvar('LEVNR')
     C                   EVAL      LEVNRN= c2n2(LEVNR)
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C     *LIKE         DEFINE    SFUNIK        UNIK
     C     *LIKE         DEFINE    KVADT         ZVADT
     C     *LIKE         DEFINE    SVLI          ZVLI
     C     *LIKE         DEFINE    SLKNR         LEVNRN
     C     *LIKE         DEFINE    SLALFA        WSLALFA
     C                   EVAL      ZVADT=99999999
     C     SAEFKEY       KLIST
     C                   KFLD                    UNIK
     C                   KFLD                    REFF
     C     VALKEY        KLIST
     C                   KFLD                    SFVK28
     C                   KFLD                    ZVADT
     C     SAEVKEY       KLIST
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPDN
A24  C     SADLK         KLIST
A24  C                   KFLD                    LEVNRN
A24  C                   KFLD                    WSLALFA

      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      ZAEFL01
     C  N50              OPEN      ZAEVL01
     C  N50              OPEN      SAEF
     C  N50              OPEN      SAEV
     C  N50              OPEN      KODTVAL2
     C  N50              OPEN      TARI
     C  N50              OPEN      SADL

     c                   endsr
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta extern faktura
     C     SAEFKEY       CHAIN     ZAEFL01                            55
      * MODE D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    ZADFR

     C     SAEFKEY       SETLL     ZAEVL01
     C     SAEFKEY       READE     ZAEVL01                                56
     C     *IN56         DOWEQ     *OFF
     C                   DELETE    ZAEVR
     C     SAEFKEY       READE     ZAEVL01                                56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      SFAVD=AVDN
     C                   EVAL      SFOPDN=OPDN
     C     VALKEY        SETGT     KODTVAL2
     C                   READP     KODTVAL2                               57
     C     *IN57         IFEQ      *OFF
     C                   EVAL      SFKR28=KVAKRS
     C                   EVAL      SFOM28=KVAOMR
     C                   ENDIF
     C                   UPDATE    ZADFR
     C                   WRITE     SADFR
      * Hitta høgsta varupostnummret
     C                   EVAL      *IN57=*OFF
     C     SAEVKEY       SETLL     SAEV
     C     SAEVKEY       READE     SAEV                                   57
     C     *IN57         DOWEQ     *OFF
     C                   EVAL      ZVLI=SVLI
     C     SAEVKEY       READE     SAEV                                   57
     C                   ENDDO
      * Skriv til produktionsfil

     C     SAEFKEY       SETLL     ZAEVL01
     C     SAEFKEY       READE     ZAEVL01                                56
     C     *IN56         DOWEQ     *OFF
     C                   IF        SVAVD = *ZEROS
     C                   EVAL      SVAVD=AVDN
     C                   EVAL      SVTDN=OPDN
     C                   ADD       1             ZVLI
     C                   EVAL      SVLI=ZVLI
     C                   EVAL      SVLN=ZVLI
     C                   UPDATE    ZAEVR
     C                   EXSR      SRUP
     C                   ENDIF
     C     SAEFKEY       READE     ZAEVL01                                56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update, skriv till produktionsfiler              SRUP
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUP          BEGSR
      * Hent fra kundes vareregister
     C                   MOVEL     SVEXR02       WSLALFA
     C     SADLK         CHAIN     SADL                               61
     C                   IF        *IN61=*OFF
     C                   EVAL      SVLK=SLOPPL
     C                   MOVEL     SLTANR        SVVNT
     C                   ENDIF
      * Kontroll av ghyldig tariffnr + extra mengde
     C                   IF        SVNTM > 0
     C     SVVNT         CHAIN     TARI                               61
     C                   IF        *IN61=*OFF
     C                   IF        TASTK <> 'J'
     C                   EVAL      SVNTM=0
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF

     C                   WRITE     SAEVR
     C                   ENDSR
