             PGM
             DCLF       TR200D
             DCL        &CMD2 *CHAR LEN(512)
             DCL        &OPT *CHAR LEN(2)
             DCL        &PMTCMD *CHAR LEN(512)
             DCL        &CMDSTACK *CHAR LEN(400)
             DCL        &SX *DEC LEN(5 0) VALUE(-3)
             DCL        &MSGCMD *CHAR LEN(512)
             DCL        &LONGCMD *CHAR LEN(1)
             DCL        &ACTION *CHAR LEN(8)
             DCL        &RCDNBR *DEC LEN(4 0)
             DCL        &MSGCNT *DEC LEN(3 0)
             OVRDSPF    TR200D SHARE(*YES)
           /*_____________________________________________*/
 PROMPT:   /* Send bilde til skjerm og behandle F3/F12    */
           /*"""""""""""""""""""""""""""""""""""""""""""""*/
             SNDRCVF    RCDFMT(PROMPT)
/*_________*/
/* Avslutt */
/*"""""""""*/
      IF         (&IN93 *EQ '1') GOTO ENDPGM /* F3 = Exit */
      IF         (&IN92 *EQ '1') GOTO ENDPGM /* F12= Cancel */
      CHGVAR     &IN90 '0' /* Error indicator used for cursor */
/*__________*/
/* Menyvalg */
/*""""""""""*/
      IF         COND((%SST(&CMD 1 2) *GE '01') *AND (%SST(&CMD +
                     1 2) *LE '90'))  THEN(DO)
                 CHGVAR     VAR(&IN94) VALUE('1')
      IF         COND(%SST(&CMD 1 2) *EQ '1') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR251C'))
      IF         COND(%SST(&CMD 1 2) *EQ '01') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR251C'))
      IF         COND(%SST(&CMD 1 2) *EQ '2') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR252C'))
      IF         COND(%SST(&CMD 1 2) *EQ '02') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR252C'))
      IF         COND(%SST(&CMD 1 2) *EQ '3') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR253C'))
      IF         COND(%SST(&CMD 1 2) *EQ '03') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR253C'))
      IF         COND(%SST(&CMD 1 2) *EQ '4') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR257C'))
      IF         COND(%SST(&CMD 1 2) *EQ '04') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR257C'))
      IF         COND(%SST(&CMD 1 2) *EQ '5') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR255C'))
      IF         COND(%SST(&CMD 1 2) *EQ '05') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR255C'))
      IF         COND(%SST(&CMD 1 2) *EQ '6') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR256C'))
      IF         COND(%SST(&CMD 1 2) *EQ '06') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR256C'))
      IF         COND(%SST(&CMD 1 2) *EQ '7') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR258C'))
      IF         COND(%SST(&CMD 1 2) *EQ '07') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR258C'))
      IF         COND(%SST(&CMD 1 2) *EQ '8') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR254C'))
      IF         COND(%SST(&CMD 1 2) *EQ '08') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR254C'))
      IF         COND(%SST(&CMD 1 2) *EQ '9') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR259C'))
      IF         COND(%SST(&CMD 1 2) *EQ '09') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR259C'))
      IF         COND(%SST(&CMD 1 2) *EQ '11') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR201C'))
      IF         COND(%SST(&CMD 1 2) *EQ '12') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR202C'))
      IF         COND(%SST(&CMD 1 2) *EQ '13') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR203C'))
      IF         COND(%SST(&CMD 1 2) *EQ '14') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR207C'))
      IF         COND(%SST(&CMD 1 2) *EQ '15') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR205C'))
      IF         COND(%SST(&CMD 1 2) *EQ '16') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR206C'))
      IF         COND(%SST(&CMD 1 2) *EQ '17') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR208C'))
      IF         COND(%SST(&CMD 1 2) *EQ '18') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR204C'))
      IF         COND(%SST(&CMD 1 2) *EQ '19') THEN(CHGVAR +
                          VAR(&CMD) VALUE('TR209C'))
      IF         COND(%SST(&CMD 1 2) *EQ '90') THEN(CHGVAR +
                          VAR(&CMD) VALUE('SIGNOFF *LIST'))
         ENDDO
/*__________________________________*/
/* Hnet tidligere kommandoer via F9 */
/*""""""""""""""""""""""""""""""""""*/
             IF         (&IN99 *EQ '1') DO /* Retrieve for F9 */
                        /* On each successful cmd, SX is set to -3 */
             CHGVAR     &SX (&SX + 4) /* Bump to prev Msg key */
             IF         (&SX *GT 400) CHGVAR &SX 1 /* Loop back */
             CHGVAR     &KEYVAR %SST(&CMDSTACK &SX 4) /* Prv msg key */
                        /* If no cmds entered, just return to prompt */
             IF         ((&SX *EQ 1) *AND +
                          (&KEYVAR *EQ ' ')) GOTO PROMPT
                        /* If no more cmds, loop back */
             IF         (&KEYVAR *EQ ' ') DO /* Reset to first */
             CHGVAR     &KEYVAR &CMDSTACK /* First msg key */
             CHGVAR     &SX 1
             ENDDO      /* Reset to first */
                        /* Receive previous command from pgm msgq */
             RCVMSG     MSGKEY(&KEYVAR) RMV(*NO) MSG(&MSGCMD)
             CHGVAR     &CMD2 &MSGCMD
             CHGVAR     &CMD &CMD2 /* Prompted portion */
             IF         (%SST(&CMD2 154 350) *NE ' ') DO /* Long cmd */
             CHGVAR     %SST(&CMD 151 3) '...' /* Show ... */
             CHGVAR     &LONGCMD 'X' /* Set switch */
             CHGVAR     &PMTCMD &CMD /* Save prompted command */
             ENDDO      /* Long cmd */
             GOTO       PROMPT /* Prompt with retreived command */
             ENDDO      /* Retrieve for F9 */
 /*_______________________*/
 /* Bearbeide Forespørsel */
 /*"""""""""""""""""""""""*/
             IF         (&IN94 *EQ '1') DO /* F4 for prompting */
             CHGVAR     &CMD ('?' *CAT &CMD)
             ENDDO      /* F4 for prompting */
             CHGVAR     &CMD2 &CMD /* Make 512 byte command */
                        /* If prompting by F4 or ? use QCMDCHK */
             IF         (%SST(&CMD 1 1) *EQ '?') DO /* Use QCMDCHK */
             CALL       QCMDCHK PARM(&CMD2 512)
             MONMSG     MSGID(CPF6801) EXEC(DO) /* Cmd cancelled */
             RCVMSG     /* Remove 'Cmd cancelled' msg */
             CHGVAR     &CMD ' ' /* Blank cmd line */
             GOTO       PROMPT /* Prompt with blank line */
             ENDDO      /* Cmd cancelled */
             MONMSG     MSGID(CPF0000) EXEC(GOTO DSPMSGS) /* Any err */
             ENDDO      /* Use QCMDCHK */
 /*_______________________________________*/
 /* If long cmd occurred on F9 retrieve   */
 /*    and execution requested without    */
 /*    making any changes, use msgq cmd   */
 /*"""""""""""""""""""""""""""""""""""""""*/
             IF         ((&LONGCMD *EQ 'X') *AND +
                          (&CMD *EQ &PMTCMD)) DO /* Use msgq cmd */
             CHGVAR     &CMD2 &MSGCMD
             ENDDO      /* Use msgq cmd */
             CHGVAR     &LONGCMD ' '
                        /*                                         */
                        /*   Place command in pgm msgq and RCV to  */
                        /*      prevent execution                  */
                        /*                                         */
             SNDPGMMSG  MSG(&CMD2) TOPGMQ(*SAME) MSGTYPE(*RQS) +
                          KEYVAR(&KEYVAR)
             RCVMSG     PGMQ(*SAME) MSGKEY(&KEYVAR) RMV(*NO)
                        /*                                         */
                        /*     Put cmd in stack for F9 usage       */
                        /*                                         */
             CHGVAR     &CMDSTACK (&KEYVAR *CAT &CMDSTACK)
             CHGVAR     &SX -3 /* Inlz stack count */
                        /*                                         */
                        /*    Execute command using QCMDEXC        */
                        /*                                         */
             CALL       QCMDEXC PARM(&CMD2 512)
                        /*                                         */
                        /*    Handle command error                 */
                        /*                                         */
             MONMSG     MSGID(CPF0000) EXEC(DO) /* Cmd error */
             CHGVAR     &IN90 '0' /* Error indicator for cursor pos */
                        /*                                         */
                        /* Command string may be longer due to      */
                        /*    prompting.  Move back to cmd line and */
                        /*    show '...' if greater than cmd line   */
                        /*                                         */
             CHGVAR     &CMD &CMD2
             IF         (%SST(&CMD2 154 350) *NE ' ') DO /* Show ... */
             CHGVAR     %SST(&CMD 151 3) '...'
             ENDDO      /* Show ... */
             GOTO       DSPMSGS
             ENDDO      /* Cmd error */
                        /*                                         */
                        /*    Blank cmd line on good execution     */
                        /*                                         */
             CHGVAR     &CMD ' ' /* Blank cmd line */
                        /*                                         */
 DSPMSGS:               /*      Display messages                   */
                        /*                                         */
             CHGVAR     &IN88 '0' /* Clear indicator */
             SNDF       RCDFMT(MSGCTL)   /* Clear subfile of prev  */
             CHGVAR     &RCDNBR 0 /* Inlz SFL record number */
             CHGVAR     &MSGCNT 0 /* Inlz msg cnt for this cmd */
                        /*                                         */
                        /*    Receive msg and use RPG for SFL wrt  */
                        /*                                         */
 RCVMSG:     RCVMSG     RMV(*NO) KEYVAR(&KEYVAR)
             IF         (&KEYVAR *NE '  ') DO /* Msg exists */
                        /* Inlz values and call RPG to write to SFL */
             CHGVAR     &ACTION 'WRTSFL'
             CHGVAR     &RCDNBR (&RCDNBR + 1) /* Subfile rel rcd nbr */
             CHGVAR     &MSGCNT (&MSGCNT + 1)
             CALL       TR200R PARM(&ACTION &RCDNBR &KEYVAR)
             GOTO       RCVMSG /* Loop back for more messages */
             ENDDO      /* Msg exists */
                        /*                                         */
                        /*  If some msgs exist, write SFL ctl rcd  */
                        /*                                         */
             IF         (&MSGCNT *GT 0) DO /* Some msgs exist */
             CHGVAR     &IN88 '1' /* Subfile display indicator */
             SNDF       RCDFMT(MSGCTL) /* Write subfile ctl rcd */
             ENDDO      /* Some msgs exist */
             GOTO       PROMPT /* Prompt again */
                        /*                                         */
                        /*      End of program routine             */
                        /*                                         */
 ENDPGM:     CHGVAR     &ACTION 'ENDPGM' /* Close up RPG pgm */
             CALL       TR200R PARM(&ACTION &RCDNBR &KEYVAR)
             ENDPGM
