      *=====================================================================
      *  RPG ILE MODULE SYSPED/ESIO15
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/SS203 MODULE(*LIBL/SS203 *LIBL/CHECKUSR
CRTPGM*         *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 hvis path IKKE inneholder \ sette start til 1.
10XXX * 02 YBC PTF10 USEROPEN PÅ ARKTXT OG ARKEXT
10XXX * 03 YBC PTF10 KOMMANDO VARIABLE ØKES TIL 256
10XXX * 04 YBC PTF10 Parameterstyrt midletidig mappe for /pdf/tmp/
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 05 YBC PTF11 BEHANDLE AXXNN SAMME SOM NN
10XXX * 06 SH  PTF12 eventuelt sende SS-ordre vedlegg til kunden
10XXX * 07 SH  PTF12 HISTORISK ARKIV
10XXX * 08 SH  PTF12 GOOGLE
********************************************************************
      *  Option switches:
      *
      *=====================================================================
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
N04  FBRPARF    if   e           k disk    usropn
     FFIRM      IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    USROPN
n08  FFIRMGOOG  IF   E           K DISK    USROPN
S02  F*ARKTXT    IF   E           K DISK
S02  F*ARKEXT    IF   E           K DISK
N02  FARKTXT    IF   E           K DISK    USROPN
N02  FARKEXT    IF   E           K DISK    USROPN
     FARKIVP    O  A E             DISK    USROPN
N06  FARKIVL11  UF   E           K DISK    USROPN
N06  F                                     RENAME(arkivr:arkivr11)
N06  FCUNDC03   IF   E           K DISK    USROPN
N97  FGSSORF    IF   E           K DISK    USROPN
N97  FCDBOAT    IF   E           K DISK    USROPN
N97  FGSSPOF    IF   E           K DISK    USROPN
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *=================================
     D UsrSpcName      s             10
     D WSUSER          S             10
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D lng             s              2
     D FTYPE           S             30A
     D WSONO           S              7A
     D BSONO           S              7  0
     D WSDOKN          S             40A
     D KEY11           S             44A
     D KEY12           S             45A
     D KEY13           S             43A
     D KEY14           S             49A
     D NBRVARS         S             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     Dsavedquerystring...
     D                 s          32767    varying
     D                 DS
     D TXT                     1    512    DIM(512)
     D TX512                   1    512
     D                 DS
     D FT                      1     70    DIM(70)
     D FT70                    1     70
      *=================================
      * VARIABLES TO CONTROL INPUT FILE
     D InDta           s          32767    based(InDtaP)
     D InDtaLn         s             10i 0
     D RspLn           s             10i 0
     D SaveP           s               *
     D StrOfDtaP       s               *
     D CvtDtaArr       s              1a   dim(1000)
     D CvtDta          s           1000a
     D CvtDtaLn        s             10i 0
     D PartSepLn       s             10i 0
     D FileSize        s             10i 0
     D FileName        s            512a
     D ServerFile      s            500a
     D ServerDir       s            500a
     D FileType        s            100a
     D FileStrPos      s             10i 0
      *=================================
      * VARIABLES FOR IFS "STAT" FUNCTION
      * Buffer for IFS stat function
     D StatusBuffer    ds                  align
     D  StsPermissions...
     D                               10u 0
     D  StsFileID                    10u 0
     D  StsLinkCount                  5u 0
     D  StsUserIDNbr                 10u 0
     D  StsGroupIdNbr                10u 0
     D  StsBytesInFile...
     D                               10i 0
     D  StsTimeLastAcc...
     D                               10i 0
     D  StsTimeLastChg...
     D                               10i 0
     D  StsTimeStsLastChg...
     D                               10i 0
     D  StsFileSysID                 10u 0
     D  StsBlockSize                 10u 0
     D  StsAllocBytes                10u 0
     D  StsObjectType                11
     D  StsCodePage                   5u 0
     D                               62
     D  StsGenerationID...
     D                               10u 0
     D  CodePage       s             10u 0 inz(819)
     D FileHandle      s             10i 0
     D ErrNoRet        s             10i 0
      *=================================
      * VARIABLES TO CHECK AN IFS FILE
     D rc              s             10i 0 inz(0)
     D ThisSubProc     c                   'FUPLOAD: '
      *=================================
      * VARIABLES TO PROVIDE USER RESPONSE
     D DoneSW          s              1a
      *=================================
      * VARIABLES TO HANDLE MESSAGES
     D ErrorType       s             10i 0
     D  msg1           s           1600a
     D  msg2           s           1600a
     D  msg3           s           1600a
     D  msg4           s           1600a
     D  msg5           s           1600a
     D                 DS
     D msg                     1   1600
     D msgid                   1      7
     D msgsev                  8      9
     D msgtxt1                10    521
     D msgtxt2               522   1600
      *=================================
      * MISCELLANEOUS VARIABLES
     D EOR             s              2a   inz(x'0D25')
     D key1            s             58a
     D key2            s             13a   inz('Content-Type:')
     D UserName        s            512a
     D Cmd             s           1024a
     D DlyNbr          s             10i 0
     D i               s             10i 0
     D j               s             10i 0
     D k               s             10i 0
     D l               s             10i 0
     D m               s             10i 0
     D r               s             10i 0
     D s               s             10i 0
     D t               s             10i 0
     D x               s             10i 0
     D y               s             10i 0
      *=================================
      * VARIABLES FOR PROGRAM STATUS SUBROUTINE
     D psds           sds
     D  psdsdata                    429
     D pssrswitch      s              1    inz(*off)
     D wrotetop        s              1    inz(*off)
      *===========================================
      * PART 0- PROLOG
      *===========================================
      * Initialize search keys
     C                   eval      key1 = 'Content-Disposition: form-data;' +
     C                             ' name="filesnt"; filename="'
     C                   EVAL      KEY11 = 'Content-Disposition: form-data;' +
     C                             ' name="WSONO"'
     C                   EVAL      KEY12 = 'Content-Disposition: form-data;' +
     C                             ' name="WSDOKN"'
     C                   EVAL      KEY13 = 'Content-Disposition: form-data;' +
     C                             ' name="USER"'
     C                   EVAL      KEY14 = 'Content-Disposition: form-data;' +
     C                             ' name="UsrSpcName"'
      * Get externally described HTML, start response
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS203'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     wrtsection('top')
     C                   eval      wrotetop = *on
      * Retrieve logged-in user name (if protection active)
     C                   eval      UserName = getenv('REMOTE_USER':qusec)
      *====================================================================
      * PART 1 - ASK USER INPUT
      *====================================================================
      * Get content length
     C                   eval      InDtaLn = contlen
     C                   if        InDtaLn = 0
     C                   eval      nbrVars =
     C                             zhbgetinput(SAVEDQUERYSTRING:qusec)
     C                   eval      WSUSER = zhbgetvar('USER')
     C                   eval      USRSPCNAME= zhbgetvar('USRSPCNAME')
     C                   eval      WSDOKN = zhbgetvar('WSDOKN')
     C                   eval      WSONO  = zhbgetvar('WSONO')
     C                   eval      BSONO  = c2n2(WSONO)
     C                   EVAL      RC = DOCMD('OVRDBF FILE(BRIDF) +
     C                             TOFILE(*LIBL/BRIDF) SECURE(*YES)')
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              30
     C                   CLOSE     BRIDF                                50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     UpdHTMLVar('USER':WSUSER)
     C                   callp     UpdHTMLVar('UsrSpcName':USRSPCNAME)
     C                   callp     UpdHTMLVar('WSONO':WSONO)
     C                   callp     UpdHTMLVar('WSDOKN':WSDOKN)
     C                   callp     wrtsection('step1')
     C                   exsr      Exit
     C                   endif
      *====================================================================
      * PART 2- ALLOCATE STORAGE TO RECEIVE INPUT
      *         RECEIVE INPUT
      *====================================================================
      * Allocate storage for content-length bytes.  Save pointer.
     C                   alloc(e)  InDtaLn       InDtaP
     C                   if        InDtaP = *null
     C                   exsr      AllocErr
     C                   endif
     C                   eval      SaveP = InDtaP
      * Read standard input using API instead of any of FUPLOAD's
      * getinput because length can easily be larger than getinput's
      * maximum buffer size of 32767.
     C                   callb     'QtmhRdStin'
     C                   parm                    InDta
     C                   parm                    InDtaLn
     C                   parm                    RspLn
     C                   parm                    qusec
      *====================================================================
      * PART 3 -  DETAIL ANALYSIS OF INPUT DATA
      *====================================================================
      * Convert initial input data to EBCDIC
      *   - establish the length of the data to be copied
     C                   eval      CvtDtaLn = %len(CvtDta)
     C                   if        InDtaLn < CvtDtaLn
     C                   eval      CvtDtaLn = InDtaLn
     C                   endif
     C                   if        CvtDtaLn > 1000
     C                   eval      CvtDtaLn = 1000
     C                   endif
      *   - copy "InDta" to "CvtDta" one byte at a time
      *     (need to move the pointer "InDtaP")
     C                   eval      i = 1
     C                   dow       i <= CvtDtaLn
     C                   eval      CvtDtaArr(i) = %subst(InDta:1:1)
     C                   eval      InDtaP = InDtaP + 1
     C                   eval      i = i +1
     C                   enddo
     C                   movea     CvtDtaArr     CvtDta
      *   - restore the pointer "InDtaP" to its initial value
     C                   eval      InDtaP = SaveP
      *   - convert "CvtDta" contents from ASCII to EBCDIC
     C                   eval      CvtDta = xlatwCCSIDS(*on:CvtDta)
      * Retrieve control data:
      *  1-path and name of the local file to be uploaded
     C                   eval      r = %scan(key1:CvtDta)
     C                   if        r>0
     C                   eval      s = %scan(EOR:CvtDta:r)
     C                   eval      FileName = %subst(CvtDta:
     C                             r + %len(key1):
     C                             s - (r + %len(key1))-1)
     C                   endif
      *  2-type of the local file to be uploaded
     C                   eval      r = %scan(key2:CvtDta)
     C                   if        r>0
     C                   eval      s = %scan(EOR:CvtDta:r)
     C                   eval      FileType = %subst(CvtDta:
     C                             r + %len(key2):
     C                             s - (r + %len(key2)))
     C                   endif
      *  3-start position (in the buffer) of the file to be uploaded
     C                   eval      FileStrPos = s +4
      *
      * TEST BRUKER-ID OG USERSPACENAME
     C                   eval      r = %scan(KEY13:CvtDta)
     C                   if        r>0
     C*                  eval      s = %scan(EOR:CvtDta:r+50)
     C                   eval      s = %scan(EOR:CvtDta:r+48)
     C                   eval      WSUSER = %subst(CvtDta:
     C                             r + %len(KEY13) + 4:
     C                             s - (r + %len(KEY13))-4)
     C                   endif
     C                   eval      r = %scan(KEY14:CvtDta)
     C                   if        r>0
     C                   eval      s = %scan(EOR:CvtDta:r+60)
     C                   eval      UsrSpcName = %subst(CvtDta:
     C                             r + %len(KEY14) + 4:
     C                             s - (r + %len(KEY14))-4)
     C                   endif
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              30
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
N05  C                   SELECT
S05  C*                  IF        ((WSUSER='NN') AND (UsrSpcName='InT5WXz4BJ'))
N05  C                   WHEN      ((WSUSER='NN') AND (UsrSpcName='InT5WXz4BJ'))
     C     WSUSER        CHAIN     BRIDF                              30
N05  C                   WHEN      %SUBST(WSUSER:1:1) = 'A' AND
N05  C                             %SUBST(WSUSER:4:2) = 'NN' AND
N05  C                             UsrSpcName = 'InT5WXz4BJ'
N05  C     WSUSER        CHAIN     BRIDF                              30
N05  C                   WHEN      %SUBST(WSUSER:1:3) = 'NNA' AND
N05  C                             UsrSpcName = 'InT5WXz4BJ'
N05  C     WSUSER        CHAIN     BRIDF                              30
S05  C*                  ELSE
N05  C                   OTHER
     C                   EXSR      TSTUSER
     C                   END
     C                   CLOSE     BRIDF                                50
      *
      * HENT REFERANSE
     C                   eval      r = %scan(KEY11:CvtDta)
     C                   if        r>0
     C                   eval      s = %scan(EOR:CvtDta:r+49)
     C                   eval      WSONO = %subst(CvtDta:
     C                             r + %len(KEY11) + 4:
     C                             s - (r + %len(KEY11))-4)
     C                   endif
     C                   eval      BSONO  = c2n2(WSONO)
      *
      * HENT DOKUMENTNAVN
     C                   eval      r = %scan(KEY12:CvtDta)
     C                   if        r>0
     C                   eval      s = %scan(EOR:CvtDta:r+49)
     C                   eval      WSDOKN = %subst(CvtDta:
     C                             r + %len(KEY12) + 4:
     C                             s - (r + %len(KEY12))-4)
     C                   endif
      *
      *  4a-length of part separator "------------------..."
     C                   eval      PartSepLn = %scan(EOR:CvtDta)
      *  4b-size of the file to be uploaded
     C                   eval      FileSize = InDtaLn - FileStrPos -
     C                             PartSepLn -4
      *  5-path and name of the server IFS file to be created
     C                   IF        FILENAME <> *BLANKS
      *
      * FINN FILNAVN
     C     '\'           SCAN      FILENAME      WPOS              3 0    51
     C                   DOW       *IN51
     C                   ADD       1             WPOS
     C                   Z-ADD     WPOS          XN                3 0
     C     '\'           SCAN      FILENAME:WPOS WPOS              3 0    51
     C                   ENDDO
N01  c                   IF        XN = 0
N01  C                   Z-ADD     1             XN
N01  c                   END
      * FJERNE ALLE BLANKE PÅ FILNAVN
     C                   MOVE      *ZEROS        XN3               3 0
     C                   EVAL      FT70  = *BLANKS
     C                   EVAL      TX512 = FILENAME
     C     XN            DO        512           XN2               3 0
     C                   IF        TXT(XN2) <> ' '
     C                   ADD       1             XN3
     C                   MOVE      TXT(XN2)      FT(XN3)
     C                   IF        XN3 = 70
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
N04  C                   open      BRPARF                               50
     C                   open      FIRM                                 50
     C                   open      FIRMARC                              50
N08  C                   open      FIRMGOOG                             50
     C                   open      ARKTXT                               50
     C                   open      ARKEXT                               50
     C     ARKLAGK       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ARKLAG
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   50
n08  C     FIFIRM        CHAIN     FIRMGOOG                           33
N08  C  N33              MOVE      'J'           GOOGLE            1
     C     'ZO'          CHAIN     ARKTXT                             33
     C                   IF        ARKLAG = *BLANKS
     C     FIFIRM        READE     FIRMARC                                50
     C                   ELSE
     C     ARKLAGK       CHAIN     ARKEXT                             33
     C                   END
N07  c     arbhis        ifne      *blanks
N07  c                   eval      arcane=arbhis
N07  c                   end
     C*                  eval      ServerFile = '/' + %TRIM(ARCANE) + '/'
N04   * HENT MIDLETIDIG MAPPE
N04  C     BrParKey      klist
N04  C                   kfld                    PABRID
N04  C                   kfld                    PAKUNR
N04  C                   kfld                    PAID
N04  C                   EVAL      PABRID = '*KUNDFT*' + %TRIM(BIFIRM)
N04  C                   MOVE      'OLTMP'       PAID
N04  C     BrParKey      CHAIN     BRPARF                             33
N04  C                   IF        *IN33
     C                   eval      ServerFile = '/pdf/tmp/'
N04  C                   ELSE
N04  C                   EVAL      ServerFile = PAVRDA
N04  C                   END
N04   *
N04  C                   CLOSE     BRPARF                               50
     C                   CLOSE     FIRM                                 50
     C                   CLOSE     FIRMARC                              50
N08  C                   CLOSE     FIRMGOOG                             50
     C                   CLOSE     ARKTXT                               50
     C                   CLOSE     ARKEXT                               50
      *
     C                   OPEN      ARKIVP
N06  C                   OPEN      ARKIVL11
N06  C                   OPEN      CUNDC03
N06  C                   OPEN      GSSORF
N06  C                   OPEN      CDBOAT
N06  C                   OPEN      GSSPOF
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN           10
     C                   EVAL      ARTYPE = 'ZO'
     C                   EVAL      ARFIRM = BIFIRM
     C                   MOVE      BSONO         XAN07             7
     C                   EVAL      ARUNDE = 'SS:'
     C                   CAT       XAN07:0       ARUNDE
     C                   EVAL      ARUSER = WSUSER
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           ARDATE
     C                   MOVE      WTM           ARTIME
     C                   EVAL      ARLINK = 'ZO'
     C                   CAT       XAN07:0       ARLINK
     C                   CAT       'SHIPS':0     ARLINK
     C                   CAT       NEWRAN:0      ARLINK
     C                   IF        WSDOKN = *BLANKS
     C                   EVAL      ARRFK = FT70
     C                   ELSE
     C                   EVAL      ARRFK = WSDOKN
     C                   END
     C                   CAT       ARLINK:0      SERVERFILE
      *
     C                   EVAL      TX512 = FILENAME
     C                   Z-ADD     512           XN                3 0
     C                   DOW       XN > 0
     C                   IF        TXT(XN) <> ' '
     C                   LEAVE
     C                   END
     C                   SUB       1             XN
     C                   ENDDO
     C                   EVAL      FT70 = *BLANKS
     C                   Z-ADD     70            XN2               3 0
     C                   DOW       XN > 0
     C                   MOVE      TXT(XN)       FT(XN2)
     C                   IF        TXT(XN) = '.'
     C                   LEAVE
     C                   END
     C                   SUB       1             XN2
     C                   SUB       1             XN
     C                   ENDDO
     C                   EVAL      XN = 71 - XN2
     C                   eval      FTYPE = %subst(FT70:XN2:XN)
     C                   CAT       FTYPE:0       SERVERFILE
     C                   CAT       FTYPE:0       ARLINK
      * OPPDATER ARKIV
     C*                  WRITE     ARKIVR
     C*                  CLOSE     ARKIVP
      *
      *=====================================================================
      * PART 5 - COPY INPUT FILE TO SREAM FILE
      *=====================================================================
     C                   exsr      WrtDta
      * OPPDATER ARKIV
     C                   IF        doneSW = 'Y'
     C                   MOVE      *BLANKS       WSDOKN
     C                   MOVE      'A'           ARSTAT
     C                   WRITE     ARKIVR
N06   * skal dokument sendes
s08  c*                  exsr      sendss
s08  C*                  CLOSE     ARKIVP
s08  C*                  CLOSE     ARKIVL11
s08  C*                  CLOSE     CUNDC03
s08  C*                  CLOSE     GSSORF
s08  C*                  CLOSE     CDBOAT
s08  C*                  CLOSE     GSSPOF
      * FLYTT OVER TIL ENDELIG MAPPE PGA. NAS
S03  C*                  Z-ADD     128           UCMDL            15 5
N03  C                   Z-ADD     256           UCMDL            15 5
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
     C                   EVAL      UCMD = 'MOV OBJ(' + XAPS
     C                             + %TRIM(SERVERFILE) + XAPS
     C                             + ') TODIR(' + XAPS
     C                             + '/' + %TRIM(ARCANE) + XAPS + ')'
     C                             + ' FROMCCSID(*JOBCCSID) TOCCSID(*JOBCCSID)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
n08  c     GOOGLE        IFEQ      'J'
N08  C                   EXSR      GOOGLESR
N08  C                   END
n08  c                   exsr      sendss
n08  C                   CLOSE     ARKIVP
N08  C                   CLOSE     ARKIVL11
N08  C                   CLOSE     CUNDC03
N08  C                   CLOSE     GSSORF
n08  C                   CLOSE     CDBOAT
n08  C                   CLOSE     GSSPOF
     C                   END
      *
     C                   END
      *=====================================================================
      * PART 6 - PROVIDE USER RESPONSE
      *=====================================================================
      * Write out the input file information
     C                   callp     UpdHTMLVar('USER':WSUSER)
     C                   callp     UpdHTMLVar('UsrSpcName':USRSPCNAME)
     C                   callp     UpdHTMLVar('WSONO':WSONO)
     C                   callp     UpdHTMLVar('WSDOKN':WSDOKN)
     C                   callp     wrtsection('step1')
     C                   callp     UpdHTMLVar('filename':FileName)
     C                   callp     UpdHTMLVar('filetype':FileType)
     C                   callp     UpdHTMLVar('filesize':
     C                             %trim(%editc(FileSize:'J')))
     C                   callp     wrtsection('strresult')
      * Display result of operation
     C                   callp     updhtmlvar('serverfile':ServerFile)
     C                   if        doneSW = 'Y'
     C                   callp     wrtsection('done')
     C                   else
     C                   callp     wrtsection('notdone')
     C                   exsr      DspErrMsg
     C                   endif
     C                   callp     wrtsection('donee')
      *
     C                   exsr      Exit
      *=====================================================================
      * Back to caller
      *=====================================================================
     C     Exit          begsr
      * Close html and send buffer
     C                   callp     wrtsection('endhtml *fini')
      * Back to caller
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Create and write the IFS output file
      *=====================================================================
     C     WrtDta        begsr
      * Assume it fails
     C                   eval      DoneSW = 'N'
     C                   eval      ErrorType = 0
      * Sjekk file extention for gyldighet
     c                   call      'FUPLOEXT'
     c                   parm                    FileName
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    ValidExt        512
     c                   parm                    UFlagg            1
     c     Uflagg        ifeq      '1'
     C                   eval      ErrNoRet = 0
     C                   eval      msg1 = ThisSubProc + 'Upload of file ' +
     C                             %trim(FileName) +
     C                             ' failed.  Message text is ' +
     C                             'file extention not valid.<BR>'+
     C                             'Valid extentions: ' + %trim(ValidExt)
     C                   callp     wrtdebug(msg1:*on)
     C                   eval      ErrorType = 1
     C                   goto      WrtDtaX
     c                   end
      * Unlink (delete) the file if it already exists.
     C                   eval      dlynbr = 0
     C                   dow       stat(%trim(ServerFile):
     C                             %addr(StatusBuffer)) = 0
     C                   eval      rc = unlink(%trim(ServerFile))
     C                   if        rc <> 0
     C                   eval      dlynbr = dlynbr +1
     C                   if        dlynbr > 5
     C                   leave
     C                   endif
     C                   eval      rc = docmd('dlyjob 10')
     C                   endif
     C                   enddo
      * Open stream file
     C                   eval      FileHandle = open(%trim(ServerFile):
     C                             O_CREAT + O_WRONLY +
     C                             O_TRUNC + O_TEXTDATA + O_CODEPAGE:
     C                             S_IRWXU + S_IROTH + S_IXOTH:CodePage)
     C                   if        FileHandle = -1
     C                   eval      ErrNoRet = errno
     C                   eval      msg1 = ThisSubProc + 'Open of IFS file ' +
     C                             %trim(ServerFile) +
     C                             ' failed.  Message text is ' +
     C                             errnotxt(ErrNoRet)
     C                   callp     wrtdebug(msg1:*on)
     C                   eval      ErrorType = 1
     C                   goto      WrtDtaX
     C                   endif
      * Write buffer to the streamfile
     C                   eval      StrOfDtaP = InDtaP + FileStrPos -1
     C                   eval      rc = write(FileHandle:
     C                             StrOfDtaP:FileSize)
     C                   if        rc = -1
     C                   eval      ErrNoRet = errno
     C                   eval      msg1 = ThisSubProc + 'Write into IFS -
     C                             file ' + %trim(ServerFile) +
     C                             ' failed.  Message text is ' +
     C                             errnotxt(ErrNoRet)
     C                   callp     wrtdebug(msg1:*on)
     C                   eval      ErrorType = 3
     C                   goto      WrtDtaX
     C                   endif
      * Close streamfile
     C                   eval      rc = close(FileHandle)
     C                   if        rc = -1
     C                   eval      ErrNoRet = errno
     C                   eval      msg1 = ThisSubProc + 'Close of IFS file '+
     C                             %trim(ServerFile) +
     C                             ' failed.  Message text is ' +
     C                             errnotxt(ErrNoRet)
     C                   callp     wrtdebug(msg1:*on)
     C                   eval      ErrorType = 2
     C                   goto      WrtDtaX
     C                   endif
      * File was correctly created and written
     C                   eval      DoneSW = 'Y'
      *
     C     '\pdf\tmp\'   SCAN      FILENAME      XPOS              3 0    33
     C                   IF        *IN33
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
S03  C*                  Z-ADD     128           UCMDL            15 5
S03  C*                  MOVE      *BLANKS       UCMD            128
N03  C                   Z-ADD     256           UCMDL            15 5
N03  C                   MOVE      *BLANKS       UCMD            256
     C                   ADD       12            XPOS
     C                   MOVE      *BLANKS       XLNK             50
     C                   EVAL      XLNK = %SUBST(FILENAME:XPOS:50)
     C                   EVAL      UCMD = 'RMVLNK OBJLNK('
     C                             + XAPS + '/pdf/tmp/'
     C                             + %TRIM(XLNK)
     C                             + XAPS + ')'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   END
      *
     C     WrtDtaX       tag
     C                   endsr
      *====================================================================
      * Memory allocation error
      *====================================================================
     C     AllocErr      begsr
     C                   callp     updhtmlvar('indtaln':
     C                             %editc(InDtaLn:'J'))
     C                   callp     wrtsection('allocerr')
     C                   exsr      Exit
     C                   endsr
      *====================================================================
      * Display error messages
      *====================================================================
     C     DspErrMsg     begsr
     C                   eval      msg = msg1
     C                   exsr      MsgRow
     C                   eval      msg = msg2
     C                   exsr      MsgRow
     C                   eval      msg = msg3
     C                   exsr      MsgRow
     C                   eval      msg = msg4
     C                   exsr      MsgRow
     C                   eval      msg = msg5
     C                   exsr      MsgRow
     C                   endsr
      *=====================================================================
      * Issue a message row
      *=====================================================================
     C     MsgRow        begsr
     C                   IF        msgid <> ' '
     C                   callp     updhtmlvar('msgid':msgid)
     C                   callp     updhtmlvar('msgsev': msgsev)
     C                   callp     updhtmlvar('msgtxt1':msgtxt1)
     C                   eval      l = %size(msgtxt2)
      *
     C                   eval      r = %scan('&N':msgtxt2)
     C                   dow       r > 0
     C                   if        r = 1
     C                   eval      msgtxt2 = %subst(msgtxt2:3:l-2)
     C                   else
     C                   eval      msgtxt2 = %subst(msgtxt2:1:r-1) +
     C                             '<br>' + %subst(msgtxt2:r+2:l-r-1)
     C                   endif
     C                   eval      r = %scan('&N':msgtxt2)
     C                   enddo
      *
     C                   eval      r = %scan('&P':msgtxt2)
     C                   dow       r > 0
     C                   if        r = 1
     C                   eval      msgtxt2 = %subst(msgtxt2:3:l-2)
     C                   else
     C                   eval      msgtxt2 = %subst(msgtxt2:1:r-1) +
     C                             '<br>' + %subst(msgtxt2:r+2:l-r-1)
     C                   endif
     C                   eval      r = %scan('&P':msgtxt2)
     C                   enddo
      *
     C                   eval      r = %scan('&B':msgtxt2)
     C                   dow       r > 0
     C                   if        r = 1
     C                   eval      msgtxt2 = %subst(msgtxt2:3:l-2)
     C                   else
     C                   eval      msgtxt2 = %subst(msgtxt2:1:r-1) +
     C                             '<li>' + %subst(msgtxt2:r+2:l-r-1)
     C                   endif
     C                   eval      r = %scan('&B':msgtxt2)
     C                   enddo
      *
     C                   callp     updhtmlvar('msgtxt2':msgtxt2)
     C                   callp     wrtsection('msgrow1')
     C                   if        msgtxt2 <> ' '
     C                   callp     wrtsection('msgrow2')
     C                   endif
     C                   ENDIF
     C                   endsr
      *=====================================================================
      * PROGRAM STATUS SUBROUTINE
      *=====================================================================
     C     *pssr         begsr
     C                   if        pssrswitch=*on
     C                   eval      *inlr = *on
     C                   return
     C                   endif
     C                   eval      pssrswitch=*on
     C                   if        wrotetop=*off
     C                   callp     wrtsection('top')
     C                   endif
     C                   callp     wrtsection('pssr *fini')
     C                   callp     wrtpsds(psds)
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
N06   ********************************************************
N06  C     sendss        begsr
N06   ********************************************************
N06  c     arkiv11k      klist
N06  c                   kfld                    ARSTAT
N06  c                   kfld                    ARUNDE
N06  c                   kfld                    ARLINK
N06  c     mailk         klist
N06  c                   kfld                    fifirm
N06  c                   kfld                    mail30
N06  c                   kfld                    orcno
N06  c     ARKSND        ifeq      'J'
N06  c                   move      arunde        ssordre           7 0
N06  c                   move      ssordre       oronoa            7
N06  c     ssordre       chain     gssorf                             33
N06  c  N33orbcd         chain     cdboat                             33
N06  c  N33ssordre       chain     gsspof                             33
N06  C                   MOVE      ARTXT         MAIL16           16
N06  C                   MOVEL     '*'           MAIL16
N06  c                   movel     MAIL16        mail30           30
N06  c     mailk         chain     cundc03                            33
N06  c     *in33         ifeq      '0'
N06  c     arkiv11k      chain     arkivl11                           33
N06  c     *in33         ifeq      '0'
N06  c                   move      ' '           arstat
N06  c                   move      orcno         arkund
N06  c                   update    arkivr11
N06  c                   move      'N'           fins
N06  c                   call      'ARC002CL'
N06  c                   parm                    artype
N06  c                   parm                    aruser
N06  c                   parm                    fins              1
N06  c                   end
N06  c                   end
N06  c                   end
N06  c                   endsr
N08   ********************************************************************
N08  C     GOOGLESR      BEGSR
N08   ********************************************************************
N08   *Hvis google cloud benyttes - send det i skyen med en gang
N08  c                   eval      Companyid = GOBUID
N08  c                   eval      UlocalPath= '/' + %trim(ARCANE)+'/'
N08  c                   eval      USrcFile  = ARLINK
N08  c                   eval      UNewFile  = *blanks
N08  c                   eval      Udelete   = 'J'
N08  c                   call      'ARC99WSU1'
N08  c                   parm                    Companyid        10
N08  c                   parm                    UlocalPath      100
N08  c                   parm                    uSrcFile        120
N08  c                   parm                    udelete           1
N08  c                   parm                    uNewFile         60
N08  c                   parm                    Uok               1
N08  C                   ENDSR
