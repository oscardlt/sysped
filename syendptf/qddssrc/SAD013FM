     A*%%TS  SD  20240125  132720  JOVO        REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXXA* 02 YBC PTF11 KUN ENDRE STATUS FRA 'E' ELLER BLANK
11XXXA* 03 YBC PTF11 TILLATE NEGATIV OPPDRAGSNR
12281A* 04 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
     A*
     A          R BILDE01
     A*%%TS  SD  20240125  132720  JOVO        REL-V7R4M0  5770-WDS
     A                                      CF03(03 'Avslutt program.')
     A                                  2  7'SAD013'
     A                                      COLOR(BLU)
     A                                  4 23MSGCON(035 DIV1765 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 23MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 23MSGCON(012 DIV1766 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 12 23MSGCON(012 DIV1767 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 12 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                 15 23MSGCON(012 DIV1768 *LIBL/SYMSGF)
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 23 23MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 12 50'(Ved Omberegn. sett minus bak)'
      *
     A          R BILDE02
S02  A*                                     CF05(05 'Bekrefte å utføre endring.-
N02  A N28                                  CF05(05 'Bekrefte å utføre endring.-
     A                                      ')
     A                                      CF12(12 'Avbryt')
S03  A*                                     CF07(07 'Fjern innstikksmerke')
S03  A*                                     CF09(09 'Sett inn innstikksmerke')
N03  A N29                                  CF07(07 'Fjern innstikksmerke')
N03  A N29                                  CF09(09 'Sett inn innstikksmerke')
     A                                  1  2MSGCON(018 DIV1725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 41MSGCON(008 DIV1726 *LIBL/SYMSGF)
     A            SIDTY          2A  O  1 50TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIDP           2Y 0O  1 53TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(9)
N04  A            SIDP2          2Y 0O  1 56TEXT('Dekl.prosedyre 2')
N04  A                                      DSPATR(UL)
N04  A                                      COLOR(WHT)
N04  A                                      EDTCDE(9)
     A                                  2  2MSGCON(010 DIV1728 *LIBL/SYMSGF)
     A            SIKNS          8Y 0O  2 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SINAS         30A  O  3  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 41MSGCON(008 DIV1729 *LIBL/SYMSGF)
     A            SINTK          7Y 0O  3 50TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A                                  3 58MSGCON(004 DIV1730 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  3 63COLOR(WHT)
     A                                      EDTCDE(4)
     A            SITDN          7Y 0O  3 68COLOR(WHT)
S03  A*                                     EDTCDE(4)
N03  A                                      EDTCDE(L)
     A            SISG           3A  O  3 76COLOR(WHT)
     A            SIADS1        30A  O  4  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4 41MSGCON(015 DIV1731 *LIBL/SYMSGF)
     A                                  4 60'D/K'
     A                                  4 68MSGCON(012 DIV1732 *LIBL/SYMSGF)
     A            SIADS2        30A  O  5  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SISKI          1A  O  5 48TEXT('Selg,Kjøper,Ingen')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIKDDK         1A  O  5 61TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIVKB          9Y 0O  5 69TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(1)
     A                                      COLOR(WHT)
     A            SIADS3        30A  O  6  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(010 DIV1733 *LIBL/SYMSGF)
     A            SIKNK          8Y 0O  7 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7 22MSGCON(005 DIV1734 *LIBL/SYMSGF)
     A            SIRG          11A  O  7 28TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 40MSGCON(016 DIV1735 *LIBL/SYMSGF)
     A                                  7 58MSGCON(016 DIV1736 *LIBL/SYMSGF)
     A                                  7 77'+/-'
     A            SINAK         30A  O  8  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIVAL1         3A  O  8 40TEXT('Valutakode Frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIBEL1        11Y 2O  8 44TEXT('Beløp tollb.frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SIVAL2         3A  O  8 59TEXT('Val.kode A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIBEL2        11Y 2O  8 63TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            SIFTG2         1A  O  8 78TEXT('Fortegn +/- a.kost')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIADK1        30A  O  9  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9 40MSGCON(016 DIV1737 *LIBL/SYMSGF)
     A            SILKA          2A  O  9 57TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9 62MSGCON(007 DIV1738 *LIBL/SYMSGF)
     A            WSRAB          4Y 2O  9 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SIADK2        30A  O 10  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIADK3        30A  O 11  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 40MSGCON(009 DIV1739 *LIBL/SYMSGF)
     A            SIKDC          1A  O 11 50TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  1MSGCON(012 DIV1740 *LIBL/SYMSGF)
     A            SITLF         12A  O 12 14TEXT('Telefon Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 40MSGCON(014 DIV1741 *LIBL/SYMSGF)
     A            SILV           3A  O 12 55TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SILVT         17A  O 12 59TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SINAD         30A  O 13  2TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 40MSGCON(018 DIV1742 *LIBL/SYMSGF)
     A                                 13 59MSGCON(010 DIV1743 *LIBL/SYMSGF)
     A                                 13 70MSGCON(010 DIV1744 *LIBL/SYMSGF)
     A                                 14  1MSGCON(010 DIV1745 *LIBL/SYMSGF)
     A            SITRID        17A  O 14 12TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SILKT          2A  O 14 30TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIVAL3         3A  O 14 40TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIBEL3        13Y 2O 14 44TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SIVKU          7Y 3O 14 60TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
S04  A*           SITST          1Y 0O 14 71TEXT('Transaksjonstype')
N04  A            SITST          2Y 0O 14 71TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  1MSGCON(018 DIV1746 *LIBL/SYMSGF)
     A            SITRM          2Y 0O 15 20TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 38MSGCON(010 DIV1747 *LIBL/SYMSGF)
     A            SIFIF         17A  O 15 49TEXT('Finans oppl.faknr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15 68MSGCON(005 DIV1748 *LIBL/SYMSGF)
EV5  A            SIFID          8Y 0H
NV5  A            WSFID          6Y 0O 15 74TEXT('Finans oppl.fakdat')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  1MSGCON(010 DIV1749 *LIBL/SYMSGF)
     A            SILS          16A  O 16 12TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIKDLS         2A  O 16 29TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 32'48.KT.'
     A            SIKTC          1A  O 16 39DSPATR(UL)
     A                                      COLOR(YLW)
     A            SIKTA          5S 0O 16 41TEXT('Tollkontonr-1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIKTB          2S 0O 16 47TEXT('Tollkontonr-2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 55'49.Godsn'
     A            SIGN          15A  O 16 65TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIPOS          9A  O 17 71TEXT('Posisjon')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  4MSGCON(035 DIV1752 *LIBL/SYMSGF)
S01  A*                                17 45MSGCON(005 DIV1753 *LIBL/SYMSGF)
S01  A*           SIKDH          2Y 0O 17 51TEXT('Havnekode')
N01  A                                 17 43MSGCON(005 DIV1753 *LIBL/SYMSGF)
N01  A            SIKDH          4Y 0O 17 49TEXT('Havnekode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(9)
     A                                 17 54MSGCON(007 DIV1754 *LIBL/SYMSGF)
     A            SIKDTR         1A  O 17 62TEXT('Transportavgift kode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIAS           5Y 1O 17 64TEXT('Avgiftssats transp')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  1'31.'
     A            SIFT1         45A  O 18  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18 53MSGCON(016 DIV1755 *LIBL/SYMSGF)
     A            SIFT2         45A  O 19  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SITARF        16A  O 19 53TEXT('TARIFFØRSNAVN')
     A                                      COLOR(WHT)
     A                                 20  1'44.'
     A            SIFT3         45A  O 20  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 53MSGCON(005 DIV1748 *LIBL/SYMSGF)
     A                                 20 66MSGCON(007 DIV1756 *LIBL/SYMSGF)
     A                                 20 75MSGCON(005 DIV1757 *LIBL/SYMSGF)
     A            SIFT4         45A  O 21  5TEXT('Fritekst-4')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SIDTG         10A  O 21 53TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SITLL         10A  O 21 66TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SITLE          6A  O 22 66TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(015 DIV1769 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            SIST           1A  O 22 18TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 20MSGCON(012 DIV1768 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            WSST           1A  O 22 33TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 22 35MSGCON(008 JOV0019 *LIBL/SYMSGF)
     A            SIMI      R        O 22 44REFFLD(SADHR/SIMI *LIBL/SADH)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
EV5  A            SIDT           8Y 0H
NV5  A            WSDT           6Y 0O 22 56TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
S02  A*                                23  1MSGCON(035 DIV1770 *LIBL/SYMSGF)
N02  A N28                             23  1MSGCON(035 DIV1770 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N02  A  28                             23  1MSGCON(035 JOV1708 *LIBL/SYMSGF)
     A                                      COLOR(RED)
S03  A*                                23 37MSGCON(020 JOV0017 *LIBL/SYMSGF)
N03  A N29                             23 37MSGCON(020 JOV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S03  A*                                23 58MSGCON(020 JOV0018 *LIBL/SYMSGF)
N03  A N29                             23 58MSGCON(020 JOV0018 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 46'reg.dato:'
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
