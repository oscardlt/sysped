      *****************************************************************
      * GENERER DE FRAKTBREV SOM IKKE FINNES PÅ TUR...                *
      * 'Y' i edi dersom "PROD-KODE" finne på tur(henger) mm          *
      * Selve KALLET av dette programmet kjøres som spesial           *
      * (fra kjøring av godsliste/lasteliste...)                      *
      *****************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10218 * 01 JOV PTF10 DERSOM DER SKREVET BREV FØR PÅ ANNEN TRANSP. ÅPNE
********************************************************************
     FTURER14   IF   E           K DISK
     FHEAD14    IF   E           K DISK
     FDOKUF     UF A E           K DISK
     FKODTA     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FKODTTR    IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  HXSNDN                 1     20
     D  HXSEAN                 1      3
     D  HXSN10                 1     10
     D  HXSN17                 4     20
      * Egentlig en Euro-spesial, men dersom fri søkevei E_E finnes
      *  (EDI-mottatt EtterkravsInfo) - fyll ut etterkr.rubr...
     D                 DS
     D  FSSOK                  1     35
     D  XKONTO                 1     15
     D  XTLF                  16     35
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
      *
     C     PROKOD        IFNE      *BLANKS
     C                   EXSR      HOVED
     C                   Else
     C     UFUNK         ifeq      'J'
     C                   EXSR      LAGJ
     C                   END
     C                   END
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
     C* FOR ALLE OPPD PÅ TUREN
     C     WUPRO         SETLL     HEAD14
     C     WUPRO         READE     HEAD14                                 35
     C     *IN35         DOWEQ     '0'
     C                   MOVE      HEAVD         DFAVD
     C                   MOVE      HEOPD         DFOPD
     C                   Z-ADD     1             DFFBNR
     C     DOKUFK        CHAIN     DOKUF                              33
      * MIDLERTIDIG FOR Å RETTE(V/OMKJØRING) TIDLIGERE MISS
     C     *IN33         IFEQ      '0'
N01   * åpne status dersom det er printet brev mot andre TURENS transp..
N01   * (MEN hvis rett Transp OG status P så skal den IKKE åpnes - KUN NYE brev skal da sendes)
N01  C     DFTRAN        IFne      TUKNT2
N01  c                   MOVE      ' '           DFST
N01  c                   endif
     C     DFST          IFEQ      ' '
S01  C*    DFKDME        andeq     ' '
     C                   EXSR      MOVHEDF
     C                   END
     C                   UPDATE    DOKUFR
     C                   ELSE
     C                   EXSR      SKAPFB
     C                   END
      *
     C     WUPRO         READE     HEAD14                                 35
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     LAGJ          BEGSR
      *////////////////////////////////////////////////////////////
     C* FOR ALLE OPPD PÅ TUREN
     C     WUPRO         SETLL     HEAD14
     C     WUPRO         READE     HEAD14                                 35
     C     *IN35         DOWEQ     '0'
      *
     C     HEPK1         IFEQ      'J'
     C                   MOVE      HEAVD         DFAVD
     C                   MOVE      HEOPD         DFOPD
     C                   Z-ADD     1             DFFBNR
     C     DOKUFK        CHAIN     DOKUF                              33
      * J på oppdraget men Fbrev finnes ikke - SKAP det
     C     *IN33         IFEQ      '1'
     C                   EXSR      SKAPFB
     C                   END
     C                   END
      *
     C     WUPRO         READE     HEAD14                                 35
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     SKAPFB        BEGSR
      *////////////////////////////////////////////////////////////
      * FRAKTBREV 1 FINNES IKKE - SKAP DET.
     C                   CLEAR                   DOKUFR
     C                   EXSR      MOVHEDF
     C                   MOVE      'F'           DFRI
     C                   MOVE      HEAVD         DFAVD
     C                   MOVE      HEOPD         DFOPD
     C                   Z-ADD     1             DFFBNR
     C                   WRITE     DOKUFR
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     MOVHEDF       BEGSR
      *////////////////////////////////////////////////////////////
      *
     C     HXSEAN        IFEQ      '401'
     C                   MOVEL     HXSN17        DF1004
     C                   ELSE
     C                   MOVEL     HXSN10        DF1004
     C                   END
      * KUN hvis det er snakk om EDI.fraktbrewv på ALLE....
      * settes transp/edikode/merkelapp mm
     C     PROKOD        IFNE      *BLANKS
     C                   MOVE      TUKNT2        DFTRAN
     C                   MOVEL     VMNAVN        DFNAT
     C                   MOVE      ' '           DFST
     C                   MOVE      'S'           DFKDME
     C                   MOVEL     PROKOD        DFPROK
     C                   MOVE      'Y'           DFCMN
     C                   END
N01  C                   MOVEL     'S'           DFBELA                         DEFAULT
     C     HEFR          IFEQ      'S'
     C     HEFR          OREQ      'M'
     C                   MOVEL     HEFR          DFBELA
     C                   END
     C                   MOVE      HEKNS         DFKNSS
     C                   MOVE      HENAS         DFNAVS
     C                   MOVE      HEADS1        DFAD1S
     C                   MOVE      *BLANKS       DFAD2S
     C                   MOVEL     HEADS3        TMPPUL            4
     C                   TESTN                   TMPPUL               77
     C     *IN77         IFEQ      '1'
     C                   MOVE      TMPPUL        DFPNLS                          LASTESTED
     C                   MOVE      HEADS3        TMPAD3           25
     C                   MOVEL     TMPAD3        DFAD3S
     C                   ELSE
     C                   MOVEL     HEADS3        DFAD3S
     C                   END
     C                   MOVE      HENT          DFNTLA
     C                   MOVE      HENAK         DFNAVL
     C                   MOVE      HEADK1        DFAD1L
     C                   MOVE      *BLANKS       DFAD2L
     C                   MOVEL     HEADK3        TMPPUL            4
     C                   TESTN                   TMPPUL               77
     C     *IN77         IFEQ      '1'
     C                   MOVE      TMPPUL        DFPOUL                          UTL.POSTNR
     C                   MOVE      HEADK3        TMPAD3           25
     C                   MOVEL     TMPAD3        DFADUL                          UTL.STED
     C                   ELSE
     C                   MOVEL     HEADK3        DFADUL
     C                   END
     C                   MOVE      HEKNK         DFKNSM
     C                   MOVE      HENAK         DFNAVM
     C                   MOVE      HEADK1        DFAD1M
     C                   MOVE      HEADK3        DFAD3M
     C     DFBELA        IFEQ      'M'
     C                   MOVE      DFKNSM        XXKNSY
     C     KOTRKE        CHAIN     KODTTR                             33
     C   33              MOVE      KOAKNR        XXKNSY
     C   33KOTRKE        CHAIN     KODTTR                             33
     C  N33              MOVEL     KTRAK3        DFKNM
     C                   ELSE
     C                   MOVE      DFKNSS        XXKNSY
     C     KOTRKE        CHAIN     KODTTR                             33
     C   33              MOVE      KOAKNR        XXKNSY
     C   33KOTRKE        CHAIN     KODTTR                             33
     C  N33              MOVEL     KTRAK3        DFKNS
     C                   END
     C     HEGM1         IFEQ      *BLANKS
     C                   MOVEL     HEGM1         DFGM
     C                   ELSE
     C     HEGM1         CAT       HERFA:1       DFGM
     C                   END
     C                   MOVE      HENT          DFNT
     C                   MOVE      HEVS1         DFVS
     C                   MOVE      HEVKT         DFVKT
     C                   MOVE      HELM          DFLM
     C                   MOVE      HEM3          DFM3
     C                   MOVEL     HEGM2         DFGM2
     C                   MOVEL     HEVS2         DFVS2
     C                   MOVE      HEFBV         DFVKTF
     C                   MOVE      HESG          DFSG
      * Etterkrav (MEN OM DET ALT ER!! VEDLIKEHOLDT PÅ FBR. IKKE
      * (MEN OM DET ALT ER!! VEDLIKEHOLDT PÅ FBR..IKKE OVERSKRIV...)
     C     DFBELE        IFEQ      *ZEROS
     C     TRETTB        ANDNE     *ZEROS
     C     FRISOKKEY     Chain     FRISOK                             33
     C     *in33         IFEQ      '0'
      * Også kto/tlf må finnes for å sende POD Etterkravsbeløp
     C                   MOVEL     XKONTO        DFPRO2
     C                   MOVEL     XTLF          DFLEVI
     C                   Z-add     TRETTB        DFBELE
     C                   END
     C                   END
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *////////////////////////////////////////////////////////////
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WPRO              8
     C                   PARM                    UFUNK             1
      * Ufunk Inn = J : Generer fbr/DOKUF også på  "IKKE EDI-TUR"..
      *       (Dersom spesialer gjør at saksbehandler på gitte oppdrag
      *        IKKE trenger å gå innom for å "godkj.beløp" da kan en ha
      *        EDI-Mottatte oppdr. hvor det er satt J i fbr men ikke skapt..)
      * På "EDI-TUR" genreres (eller oppdateres) alltid DOKUF, CMN->Y
      * Ufunk UT  = L : Print labels om de ikke er printet før...
      *
     C                   MOVE      WPRO          WUPRO             8 0
      *
     c     *loval        setll     Firm
     c                   Read      Firm
      *
     C     TRANKEY       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNT2
      *
     C     KOTRKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XXKNSY            8 0
     C                   KFLD                    TUKNT2
      *
     C     KODTAKEY      KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    TUAVD
     C                   MOVE      'A'           KOAUNI
      *
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   MOVE      'F'           DFRI
     C     FRISOKKEY     KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FSKODE
     C                   MOVE      'E_E'         FSKODE
      *
     C     PROKEY        KLIST
     C                   KFLD                    PROTYP
     C                   KFLD                    WSPROK            5
     C                   MOVEL     'FRBREVPR'    PROTYP           10
     C                   MOVE      'O '          PROTYP
      * EDI - PRODUKTKODE
     C                   MOVE      *BLANK        PROKOD            5
      *
     C     WUPRO         CHAIN     TURER14                            33
     C     *IN33         CABEQ     '1'           SRINI9
      *
     C     TRANKEY       CHAIN     TRAN                               33
     C     *IN33         CABEQ     '1'           SRINI9
     C     VMLAYO        CABEQ     *BLANKS       SRINI9
     C     VM0004        CABEQ     *BLANKS       SRINI9
     C     VM0010        CABEQ     *BLANKS       SRINI9
     C     TUHENG        CABEQ     *BLANKS       SRINI9
      * AVD'S knr for senders nr hos transportør
     C     KODTAKEY      CHAIN     KODTA                              33
     C     *IN33         CABEQ     '1'           SRINI9
     C                   MOVEL     TUHENG        WSPROK
     C     PROKEY        CHAIN     KOFAST                             33
     C     *IN33         CABEQ     '1'           SRINI9
      * ALT OK
     C                   MOVEL     WSPROK        PROKOD
     C                   MOVEL     KFTXT         PROTXT           30
      *
     C     SRINI9        ENDSR
