      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED OPPDRAG MED UTL.FORBEHOLD  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09 11 13-24
      *** LEDIGE INDIKATORER 31 32 34-39 41-90 97
      ***********************************************************
      *
     FUTLLOGF   O  A E             DISK
     FHEADF     UF   E           K DISK
     FFORDEL5   UF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR11)
     FKODTU     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYFO03D   CF   E             WORKSTN
     F                                     SFILE(SYFO03DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D UT              S              2    DIM(10)                              UTL-TYPE
     D AN              S              3  0 DIM(10)                              ANTALL
     D FBAREA          DS
     D*                                   B 370 3710WSCLOC
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  HEPOS1                 1      7
     D  WSPOS1                 1      2
     D                 DS
     D  HEFT1                  1     51
     D  HEFT1A                 1     30
     D  HEFT1B                31     45
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  AU                     1     45
     D                                     DIM(9)                               ANT/UTL
     D  WSUT1A                 1      3
     D  WSUT1T                 4      5
     D  WSUT2A                 6      8
     D  WSUT2T                 9     10
     D  WSUT3A                11     13
     D  WSUT3T                14     15
     D  WSUT4A                16     18
     D  WSUT4T                19     20
     D  WSUT5A                21     23
     D  WSUT5T                24     25
     D  WSUT6A                26     28
     D  WSUT6T                29     30
     D  WSUT7A                31     33
     D  WSUT7T                34     35
     D  WSUT8A                36     38
     D  WSUT8T                39     40
     D  WSUT9A                41     43
     D  WSUT9T                44     45
     D  WSU10A                46     48
     D  WSU10T                49     50
     D                UDS
     D  UUSR                 301    310
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SYFO03DB
     C                   MOVEA     '00'          *IN(93)
     C                   MOVE      '0'           *IN27
     C                   MOVE      *ZEROS        ZRECNO
     C                   MOVEA     *ZEROS        AN
     C                   MOVEA     *BLANKS       UT
     C                   MOVEA     *BLANKS       AU
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO          SETLL     HEAD11                             94
     C     START1        TAG
     C     UPRO          READE     HEAD11                                 94
      * Hopp uansett over oppdrag uten utlev-forbehold
     C  N94HEKF          IFEQ      *BLANKS
     C                   GOTO      START1
     C                   END
      * Hopp over "avkvitterte" hvis modus er VIS KUN ÅPNE
     C  N94HEKFT1        IFNE      *BLANKS
     C     *IN50         ANDEQ     '0'
     C                   GOTO      START1
     C                   END
      *
     C     START2        TAG
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVE      HEKF          WSKF
     C                   MOVE      HEKFT1        WSKFT1
     C                   MOVE      HEFT1A        WSFT1A
     C                   MOVE      HEFT1B        WSFT1B
     C                   MOVE      HEDTD         WSDTD
     C                   MOVE      HEDTM         WSDTM
     C                   MOVE      HEDTÅ         WSDTÅ
     C                   MOVEL     HENAS         WSSELG
     C                   MOVEL     HENAK         WSKJØP
     C     HENT          IFGT      99999
     C                   MOVE      '99999'       WSNT
     C                   ELSE
     C                   MOVE      HENT          WSNT
     C                   END
     C     HEVKT         IFGT      9999999
     C                   MOVE      '9999999'     WSVKT
     C                   ELSE
     C                   MOVE      HEVKT         WSVKT
     C                   END
     C                   EXSR      TELLTY
     C                   WRITE     SYFO03DA
     C     ST21          TAG
     C     UPRO          READE     HEAD11                                 94
      * Hopp uansett over oppdrag uten utlev-forbehold
     C  N94HEKF          IFEQ      *BLANKS
     C                   GOTO      ST21
     C                   END
      * Hopp over "avkvitterte" hvis modus er VIS KUN ÅPNE
     C  N94HEKFT1        IFNE      *BLANKS
     C     *IN50         ANDEQ     '0'
     C                   GOTO      ST21
     C                   END
     C  N94              END
      *
      * Sorter/EDITERER  array med antall pr. utl-kode
     C                   EXSR      SORTER
      *
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     ZRECNO        IFNE      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     SYFO03DG
     C                   ELSE
     C                   WRITE     SYFO03DB
     C                   EXFMT     SYFO03DC
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C                   MOVE      *ZEROS        UFLAGG
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** ENDRE MODUS
     C     *IN07         IFEQ      '1'
     C     *IN50         IFEQ      '0'
     C                   MOVE      '1'           *IN50                             *ALLE
     C                   ELSE
     C                   MOVE      '0'           *IN50                             *ÅPNE
     C                   END
     C                   GOTO      START
     C                   END
      *** SØK
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START3
     C                   END
      *** SETT NY UTL.FORBEHOLDSKODE
     C   10              DO
     C                   EXSR      NYUFB
     C   12              GOTO      START3
     C                   GOTO      START
     C                   END
      *** AVSLUTT
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C   27              GOTO      START
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C***********************************************
     C     TELLTY        BEGSR
     C***********************************************
      * Legg antall forkomster / UTL.kode i arrays.
     C     1             DO        10            XN                2 0
     C     UT(XN)        IFEQ      '  '
     C     UT(XN)        OREQ      HEKF
     C                   GOTO      UTLOOP
     C                   END
     C                   END
     C     UTLOOP        TAG
      *
     C                   MOVE      HEKF          UT(XN)
     C                   ADD       1             AN(XN)
      *
     C                   ENDSR
     C*
     C***********************************************
     C     SORTER        BEGSR
     C***********************************************
      * INNTIL 9 TYPER SORT. PÅ STIGENDE FOREKOMST(PÅ SKJERM SYNKENDE)
      *       10. GRUPPE = SAMLING AV "ANNET" (= '**')
     C     1             DO        10            XN                2 0
     C     UT(XN)        IFNE      *BLANKS
     C     XN            IFLT      10
     C                   MOVEL     AN(XN)        AU(XN)
     C                   MOVE      UT(XN)        AU(XN)
     C                   ELSE
     C                   MOVEL     AN(XN)        WSU10A
     C                   MOVE      '**'          WSU10T
     C                   END
     C                   END
     C                   END
      *
     C                   SORTA     AU
      *
     C                   ENDSR
     C*
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C                   MOVE      '0'           *IN27                           NOE ENDRET?
     C     *IN98         DOWEQ     '0'
     C                   READC     SYFO03DA                               98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       5             LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   ADD       5             LINNBR
     C                   END
     C                   SELECT
     C     WSNR          WHENEQ    '5'
     C                   EXSR      DSPORD
     C     WSNR          WHENEQ    '9'
     C                   EXSR      DSPLOG
     C     WSKF          WHENNE    HEKF
     C     WSKFT1        ORNE      HEKFT1
     C     WSFT1A        ORNE      HEFT1A
     C     WSFT1B        ORNE      HEFT1B
     C                   EXSR      CHGORD
     C                   ENDSL
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGORD        BEGSR
     C***********************************************
     C                   MOVE      ' '           WSNR
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '00'          *IN(61)
     C                   MOVE      '0'           *IN30
      *
     C     WSKF          COMP      '  '                                   61
     C  N61WSKF          CHAIN     KODTU                              61
     C     *IN61         IFEQ      '1'
     C                   MOVE      'YBC0487'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   ELSE
     C     WSFT1A        IFEQ      *BLANKS
     C                   MOVE      KUTX          WSFT1A
     C                   END
     C                   END
      *
     C                   MOVEL     WSKFT1        XKFKOD
     C     KOFASK        CHAIN     KOFAST                             62
     C   62
     CANN30              MOVE      'YBC0488'     MSGNR
     C   62              MOVE      '1'           *IN30
      *
     C  N30              EXSR      LDOPPD
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SYFO03DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPORD        BEGSR
     C***********************************************
     C                   MOVE      ' '           WSNR
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM      HEAVD         UAVD2             4 0
     C                   PARM      HEOPD         UOPD2             7 0
     C                   PARM                    UFLAGG
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SYFO03DA
      *
     C                   ENDSR
     C***********************************************
     C     DSPLOG        BEGSR
     C***********************************************
     C                   MOVE      ' '           WSNR
     C                   MOVE      '1'           *IN99
     C                   CALL      'SYFA19R'
     C                   PARM      HEAVD         UAVD2             4 0
     C                   PARM      HEOPD         UOPD2             7 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SYFO03DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     LDOPPD        BEGSR
     C***********************************************
     C     HEADFK        CHAIN     HEADF                              33
     C                   MOVE      WSKF          HEKF
     C                   MOVE      WSKFT1        HEKFT1
     C                   MOVE      WSFT1A        HEFT1A
     C                   MOVE      WSFT1B        HEFT1B
     C                   UPDATE    HEADFR
     C                   EXSR      OPDLOG
     C* N27      HEKFT1    COMP *BLANKS              2727
      *
     C     HEADFK        CHAIN     FORDEL5                            33
     C     *IN33         IFEQ      '0'
     C     FOAVDV        IFEQ      0
     C     FOOPDV        ANDEQ     0
     C                   MOVE      HEKDPL        UKDPL
     C                   MOVE      *ZEROS        UAVDV
     C                   MOVE      *ZEROS        UOPDV
     C                   MOVE      '0'           UIN10
     C                   MOVE      *ZEROS        XXXFBN            3 0
     C                   CALL      'SYFO02'
     C                   PARM      HEAVD         UAVD              4 0
     C                   PARM      HEOPD         UOPD              7 0
     C                   PARM                    UAVDV             4 0
     C                   PARM                    UOPDV             7 0
     C                   PARM                    UIN10             1
     C                   PARM                    UKDPL             1
     C                   PARM                    FICURR
     C                   PARM                    XXXFBN
      *
     C                   MOVE      'G'           FOSTAT
     C                   MOVE      UAVDV         FOAVDV
     C                   MOVE      UOPDV         FOOPDV
     C                   END
     C                   UPDATE    FORDR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYUFB         BEGSR
     C***********************************************
     C     STUFB         TAG
     C                   EXFMT     SYFO03DX
     C                   MOVEA     '00'          *IN(71)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     26
     C                   CALL      'SYFA71A'
     C                   PARM                    WSKDGM
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      STUFB
     C                   END
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     26
     C                   CALL      'SYFA71A'
     C                   PARM                    WSKDNY
     C                   GOTO      STUFB
     C                   END
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C                   END
     C   12              GOTO      SLUFB
      *
      * TEST UTLEVERINGSFORBEHOLD
     C     WSKDGM        COMP      '  '                                   71
     C  N71WSKDGM        CHAIN     KODTU                              71
     C     *IN71         IFEQ      '1'
     C                   MOVE      'YBC0487'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STUFB
     C                   END
     C     WSKDNY        IFNE      '  '
     C     WSKDNY        CHAIN     KODTU                              72
     C     *IN71         IFEQ      '1'
     C                   MOVE      'YBC0487'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STUFB
     C                   END
     C                   END
      *
      * OPPDATERE NY UTLEVERINGSFORBEHOLD
     C     UPRO          SETLL     HEAD11
     C     UPRO          READE     HEAD11                                 33
     C     *IN33         DOWEQ     '0'
     C     HEKF          IFEQ      WSKDGM
     C     HEKFT1        ANDEQ     *BLANKS
     C     HEADFK        CHAIN     HEADF                              30
     C     WSKDNY        IFEQ      *BLANKS
     C     HEADFK        CHAIN     FORDEL5                            30
     C     *IN30         IFEQ      '0'
     C     FOAVDV        ANDEQ     0
     C     FOOPDV        ANDEQ     0
     C                   MOVE      'X'           HEKFT1
     C                   MOVE      HEKDPL        UKDPL
     C                   MOVE      *ZEROS        UAVDV
     C                   MOVE      *ZEROS        UOPDV
     C                   MOVE      '0'           UIN10
     C                   MOVE      *ZEROS        XXXFBN            3 0
     C                   CALL      'SYFO02'
     C                   PARM      HEAVD         UAVD              4 0
     C                   PARM      HEOPD         UOPD              7 0
     C                   PARM                    UAVDV             4 0
     C                   PARM                    UOPDV             7 0
     C                   PARM                    UIN10             1
     C                   PARM                    UKDPL             1
     C                   PARM                    FICURR
     C                   PARM                    XXXFBN
     C*          HEADFK    CHAINFORDEL               33
     C                   MOVE      'G'           FOSTAT
     C                   MOVE      UAVDV         FOAVDV
     C                   MOVE      UOPDV         FOOPDV
     C                   UPDATE    FORDR
     C                   END
     C                   ELSE
     C                   MOVE      WSKDNY        HEKF
     C                   MOVEL     KUTX          HEFT1A
     C                   END
     C                   UPDATE    HEADFR
     C                   EXSR      OPDLOG
     C                   END
     C     UPRO          READE     HEAD11                                 33
     C  N33              END
      *
     C     SLUFB         ENDSR
      *
     C***********************************************
     C     OPDLOG        BEGSR
     C***********************************************
     C                   MOVE      *ON           *IN27
     C                   MOVE      HEAVD         ULAVD
     C                   MOVE      HEOPD         ULOPD
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      XULTM         ULTID
     C                   MOVE      UUSR          ULUSR
     C                   MOVE      HEKF          ULKF
     C                   MOVE      HEKFT1        ULKFT1
     C                   MOVE      HEFT1A        ULFT1A
     C                   MOVE      HEFT1B        ULFT1B
     C                   WRITE     UTLLOGR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SØKDTA        BEGSR
      ***********************************************
     C     LINNBR        SUB       4             XRCNR             4 0
     C                   DIV       2             XRCNR
     C                   MVR                     XLINRS            1 0
     C     XRCNR         CABLE     0             SLSØK2
     C     XRCNR         CABGT     6             SLSØK2
     C     XLINRS        CABEQ     1             SLSØK2
      *
     C     POSNBR        IFGE      22
     C     POSNBR        ANDLE     23
     C                   ADD       XRECNO        XRCNR
     C     XRCNR         CHAIN     SYFO03DA                           30
     C     *IN30         CABEQ     '1'           SLSØK2
     C                   CALL      'SYFA71A'
     C                   PARM                    WSKF
     C                   UPDATE    SYFO03DA
     C                   GOTO      SLSØK
     C                   END
      *
     C     SLSØK2        TAG
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KOFASK        KLIST
     C                   KFLD                    XKFTYP
     C                   KFLD                    XKFKOD
     C                   MOVEL     'UTLKOD'      XKFTYP
     C     *LIKE         DEFINE    KFTYP         XKFTYP
     C     *LIKE         DEFINE    KFKOD         XKFKOD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8 0
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
