      *
      **************************************************************
      *** PROGRAM SKAL SPØRRE PÅ BOOKING                         ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 02 04 06-11 13-24                   ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-40 52-90 95-97 99    ***
      **************************************************************
      *
     FBOOKING   IF   E           K DISK
     FBOOKLF01  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN1R)
     FBOOKLF02  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN2R)
     FBOOKLF03  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN3R)
     FBOOKLF04  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN4R)
     FBOOKLF05  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN5R)
     FBOOKLF06  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN6R)
     FBOOKLF07  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN7R)
     FBOOKLF08  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN8R)
     FBOOKLF11  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKINBR)
     FBOOKLF12  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKINCR)
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSYSJ52FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  XÅR4                   1      4  0
     D  BOACC                  1      2  0
     D  BOAAR                  3      4  0
     D                 DS
     D  BODT1Å                 1      4  0
     D  BODT1M                 5      6  0
     D  BODT1D                 7      8  0
     D  BODT1                  1      8  0
     D                 DS
     D  XDT1D                  1      2  0
     D  XDT1M                  3      4  0
     D  XDT1Å                  5      6  0
     D  XDT1                   1      6  0
     D                 DS
     D  BONAA                  1     30
     D  XNAA                   1     18
     D                 DS
     D  BONAS                  1     30
     D  XNAS                   1     20
     D                UDS
     D  UFIRM                  1      2
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        8
     C                   ADD       1             ZRECNO
     C                   EXSR      KDTFMT
     C                   WRITE     SUBFIL
     C                   EXSR      LESREC
     C  N94              END
     C                   END
     C*
     C                   MOVE      ZRECNO        XRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   END
     C                   EXFMT     BUNN
     C*** AVSLUTT
     C   01
     COR 03
     COR 12              GOTO      SLUTT
     C*
     C                   SETOFF                                       9830
     C   05              GOTO      START
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      BHENT
     C     WSNR          IFEQ      '2'
     C                   MOVE      BOOPD         UOPD
     C                   Z-SUB     3             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C   98              GOTO      START3
     C                   END
     C*
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C*
     C                   EXSR      UTBOOK
     C  N01
     CANN03              GOTO      START3
     C*
     C     SLUTT         TAG
     C   01              MOVE      *ZEROS        UAVD
     C   01              Z-ADD     1             UFLAGG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     9             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     UTBOOK        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYSJ52B'
     C                   PARM                    UAVD
     C                   PARM                    BOOPD
     C                   PARM                    UFLAGG
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     BHENT         BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     WSNR          OREQ      '5'
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41              MOVEL     UÅR4          YCC               2 0
     C   41              MOVE      UÅR4          YÅR               2 0
     C   41              MOVE      UUKE          YUKE              2 0
     C   42              MOVE      UKNR          YKNR              8 0
     C   43              MOVE      UTR           YTR               4
     C   44              MOVE      UKNA          YKNA              8 0
     C   45              MOVE      ULB           YLB               5
     C   46              MOVE      ULSH          YLSH              5
     C   47              MOVE      UKNS          YKNS              8 0
     C   48              MOVE      UOB           YOB               5
     C   50              MOVE      UREFN         YREFN            14
     C   51              MOVE      ULOH          YLOH              5
     C   41BOOK1K        SETLL     BOOKLF01
     C   42BOOK2K        SETLL     BOOKLF02
     C   43BOOK3K        SETLL     BOOKLF03
     C   44BOOK4K        SETLL     BOOKLF04
     C   45BOOK5K        SETLL     BOOKLF05
     C   46BOOK6K        SETLL     BOOKLF06
     C   47BOOK7K        SETLL     BOOKLF07
     C   48BOOK8K        SETLL     BOOKLF08
     C   49BOOKK         SETLL     BOOKING
     C   50BOOKBK        SETLL     BOOKLF11
     C   51BOOKCK        SETLL     BOOKLF12
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C     STLES         TAG
     C   41UAVD          READE     BOOKLF01                               94
     C   42UAVD          READE     BOOKLF02                               94
     C   43UAVD          READE     BOOKLF03                               94
     C   44UAVD          READE     BOOKLF04                               94
     C   45UAVD          READE     BOOKLF05                               94
     C   46UAVD          READE     BOOKLF06                               94
     C   47UAVD          READE     BOOKLF07                               94
     C   48UAVD          READE     BOOKLF08                               94
     C   49UAVD          READE     BOOKING                                94
     C   50UAVD          READE     BOOKLF11                               94
     C   51UAVD          READE     BOOKLF12                               94
     C*
     C  N49
     CANN94UDT8          IFGT      BODT1
     C   41              MOVE      BOACC         YCC               2 0
     C   41              MOVE      BOAAR         YÅR               2 0
     C   41              MOVE      BOUKE         YUKE              2 0
     C   42              MOVE      BOKNR         YKNR              8 0
     C   43              MOVE      BOTR          YTR               4
     C   44              MOVE      BOKNA         YKNA              8 0
     C   45              MOVE      BOLB          YLB               5
     C   46              MOVE      BOLSH         YLSH              5
     C   47              MOVE      BOKNS         YKNS              8 0
     C   48              MOVE      BOOB          YOB               5
     C   50              MOVE      BOREFN        YREFN            14
     C   51              MOVE      BOLOH         YLOH              5
     C                   GOTO      STLES
     C                   END
     C   41
     CANN94UÅR4          COMP      XÅR4                               9494
     C   41
     CANN94UUKET         COMP      BOUKE                                94
     C*
     C  N41
     CANN49
     CANN94UUKE          IFNE      0
     C     UÅR4          IFNE      XÅR4
     C                   GOTO      STLES
     C                   END
     C     UUKE          IFNE      BOUKE
     C                   GOTO      STLES
     C                   END
     C                   END
     C  N42
     CANN49
     CANN94UKNR          IFNE      0
     C     UKNR          ANDNE     BOKNR
     C                   GOTO      STLES
     C                   END
     C  N43
     CANN49
     CANN94UTR           IFNE      *BLANKS
     C     UTR           ANDNE     BOTR
     C                   GOTO      STLES
     C                   END
     C  N44
     CANN49
     CANN94UKNA          IFNE      0
     C     UKNA          ANDNE     BOKNA
     C                   GOTO      STLES
     C                   END
     C  N45
     CANN49
     CANN94ULB           IFNE      *BLANKS
     C     ULB           ANDNE     BOLB
     C                   GOTO      STLES
     C                   END
     C  N46
     CANN49
     CANN94ULSH          IFNE      *BLANKS
     C     ULSH          ANDNE     BOLSH
     C                   GOTO      STLES
     C                   END
     C  N47
     CANN49
     CANN94UKNS          IFNE      0
     C     UKNS          ANDNE     BOKNS
     C                   GOTO      STLES
     C                   END
     C  N48
     CANN49
     CANN94UOB           IFNE      *BLANKS
     C     UOB           ANDNE     BOOB
     C                   GOTO      STLES
     C                   END
     C  N50
     CANN49
     CANN94UREFN         IFNE      *BLANKS
     C     UREFN         ANDNE     BOREFN
     C                   GOTO      STLES
     C                   END
     C  N51
     CANN49
     CANN94ULOH          IFNE      *BLANKS
     C     ULOH          ANDNE     BOLOH
     C                   GOTO      STLES
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BOOKK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BOOK1K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YCC
     C                   KFLD                    YÅR
     C                   KFLD                    YUKE
     C     BOOK2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YKNR
     C                   KFLD                    UDT8
     C     BOOK3K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YTR
     C                   KFLD                    UDT8
     C     BOOK4K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YKNA
     C                   KFLD                    UDT8
     C     BOOK5K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YLB
     C                   KFLD                    UDT8
     C     BOOK6K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YLSH
     C                   KFLD                    UDT8
     C     BOOK7K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YKNS
     C                   KFLD                    UDT8
     C     BOOK8K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YOB
     C                   KFLD                    UDT8
     C     BOOKBK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YREFN
     C                   KFLD                    UDT8
     C     BOOKCK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YLOH
     C                   KFLD                    UDT8
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UDT               6 0
     C                   PARM                    UNR               2 0
     C                   PARM                    UÅR               2 0
     C                   PARM                    UUKE              2 0
     C                   PARM                    UUKET             2 0
     C                   PARM                    UKNR              8 0
     C                   PARM                    UTR               4
     C                   PARM                    UKNA              8 0
     C                   PARM                    ULB               5
     C                   PARM                    ULSH              5
     C                   PARM                    UKNS              8 0
     C                   PARM                    UOB               5
     C                   PARM                    UOPD              7 0
     C                   PARM                    UREFN            14
     C                   PARM                    ULOH              5
     C                   PARM                    UFLAGG            1 0
     C*
     C     UDT           IFEQ      0
     C                   MOVE      *ZEROS        UDT8              8 0
     C                   ELSE
     C     UDT           IFGT      501231
     C     UDT           ADD       19000000      UDT8
     C                   ELSE
     C     UDT           ADD       20000000      UDT8
     C                   END
     C                   END
     C     UÅR           IFGT      50
     C     UÅR           ADD       1900          UÅR4              4 0
     C                   ELSE
     C     UÅR           ADD       2000          UÅR4
     C                   END
     C                   MOVE      *ZEROS        BODT1
     C                   MOVE      *ZEROS        XDT1
     C*
     C     UNR           ADD       40            XN                2 0
     C                   MOVE      '1'           *IN(XN)
     C   42              MOVE      UKNR          KUNDNR
     C   44              MOVE      UKNA          KUNDNR
     C   47              MOVE      UKNS          KUNDNR
     C   42
     COR 44
     COR 47KUND1K        CHAIN     CUNDL01                            33
     C   45ULB           CHAIN     CDBOAT                             33
     C   46ULSH          CHAIN     STED2                              33
     C   48UOB           CHAIN     CDBOAT                             33
     C   51ULOH          CHAIN     STED2                              33
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      BODT1D        UDT1              2 0
     C                   MOVE      BODT1M        UDT2              2 0
     C                   MOVE      BODT1Å        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XDT1D
     C                   MOVE      UDT2          XDT1M
     C                   MOVE      UDT3          XDT1Å
     C*
     C                   ENDSR
     C*
