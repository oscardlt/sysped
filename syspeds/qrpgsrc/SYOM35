     FSYOM35FM  CF   E             WORKSTN
     FFAK3      UF   E           K DISK
      *
     C     *ENTRY        PLIST
     C                   PARM                    IN01              1
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK3
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        XXOPKO
     C     *LIKE         DEFINE    FAOPKO        YYOPKO
     C     *LIKE         DEFINE    FAOPKO        ZZOPKO
     C     *LIKE         DEFINE    FAFAKT        XXFAKT
     C     *LIKE         DEFINE    FAKUNR        XXKUNR
     C     *LIKE         DEFINE    FAAVD         XXAVD
     C     *LIKE         DEFINE    FAOPD         XXOPD
     C     KEYFRA        KLIST
     C                   KFLD                    XXOPKO
     C                   KFLD                    WSFRA
     C                   KFLD                    XXKUNR
     C                   KFLD                    XXAVD
     C                   KFLD                    XXOPD
     C     KEYTST        KLIST
     C                   KFLD                    YYOPKO
     C     KEYTSZ        KLIST
     C                   KFLD                    ZZOPKO
     C*--------------------------------------------------------------
     C                   MOVE      'O'           XXOPKO
     C                   MOVE      'G'           YYOPKO
     C                   MOVE      'T'           ZZOPKO
     C* NOEN ORDINÆRE MED STATUS G - AVBRYT MELDING:KJØR OVERFØRING
     C     KEYTST        SETLL     FAK3
     C     KEYTST        READE     FAK3                                   30
     C     *IN30         DOWEQ     '0'
     C     FASK          IFNE      'I'
     C     FAKDA         ANDNE     'K'
     C                   MOVE      '1'           IN01
     C                   GOTO      SLUTT
     C                   END
     C     KEYTST        READE     FAK3                                   30
     C                   END
     C* NOEN MED STATUS T - KJØR STAT-OPPDATERING (SYFA39)
     C     KEYTSZ        SETLL     FAK3
     C     STSTZ         TAG
     C     KEYTSZ        READE     FAK3                                   30
     C  N30FAKDA         IFEQ      'K'
     C                   GOTO      STSTZ
     C                   END
     C  N30              MOVE      '2'           IN01
     C  N30              GOTO      SLUTT
     C     START         TAG
     C                   EXFMT     BILDE01
     C* AVBRUDD ?? GI MELDING
     C   03              MOVE      '3'           IN01
     C   03              GOTO      SLUTT
     C* DERSOM MER ENN 1000 MERKET MÅ DET KVITTERES FOR MED F23
     C     WSTIL         SUB       WSFRA         ANTALL            7 0
     C     ANTALL        COMP      1000                               99
     C   99
     CANN23              GOTO      START
     C*
     C* OMKJØRING GJENNOMFØRES - MERK ALLE FRA/TIL NR
     C*
     C     KEYFRA        SETLL     FAK3
     C     STLES         TAG
     C                   READ      FAK3                                   30
     C  N30FAOPKO        IFEQ      'O'
     C     FAFAKT        IFLE      WSTIL
     C     FAKDA         IFNE      'K'
     C                   MOVE      'G'           FAOPKO
     C                   UPDATE    FAKTR
     C                   END
     C                   GOTO      STLES
     C                   END
     C                   END
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
