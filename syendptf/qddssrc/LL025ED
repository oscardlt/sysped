     A*%%TS  SD  20180926  105106  SVEIN       REL-V7R1M0  5770-WDS
KINGSA*        20  2'Skriv til csv-fil ...........:'
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXXA* 01 YBC PTF5 NYTT FELT FOR TELLINGSDATO
PXXXXA* 02 YBC PTF7 VAREGRUPPE OG VAREERIER
P8XXXA* 03 YBC PTF8 VELG KUNDENR
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A*
     A          R BILDE1
     A*%%TS  SD  20180926  105106  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'Spørr')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL025D'
     A                                      COLOR(BLU)
     A                                  1 21'Utskrift av tollagerliste'
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  5'Fra og med Godsnummer.....:'
     A            WSGNF         15A  B  3 34
     A                                  4  5'Til og med Godsnummer.....:'
     A            WSGNT         15A  B  4 34
     A                                  4 52MSGCON(021 YBC1318 *LIBL/SYMSGF)
     A                                  6  2'Evt ekstra filtrering...'
     A                                  7  2MSGCON(030 YBC1315 *LIBL/SYMSGF)
     A            WSARTF        15A  B  7 34
     A                                  8  2MSGCON(030 YBC1317 *LIBL/SYMSGF)
     A            WSARTT        15A  B  8 34
     A                                  8 52MSGCON(021 YBC1318 *LIBL/SYMSGF)
     A                                  9  2MSGCON(030 YBC1319 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(030 YBC1320 *LIBL/SYMSGF)
     A            WSLF1          5A  B 10 34
     A            WSLF2         20A  B 10 40
     A                                 10 60MSGCON(015 YBC1316 *LIBL/SYMSGF)
     A                                 11  2MSGCON(030 YBC1321 *LIBL/SYMSGF)
     A            WSLT1          5A  B 11 34
     A            WSLT2         20A  B 11 40
     A                                 11 61'(Blank=Fra lok.)'
     A                                 13  2'KUN for kunde ............:'
     A            WSPNR          8Y 0B 13 34EDTCDE(4)
     A                                 13 52'(0=Alle)'
     A                                 15  2MSGCON(030 YBC1323 *LIBL/SYMSGF)
     A            WSDET          1A  B 15 34VALUES('J' 'N' ' ')
     A                                 15 38'(J/N)'
     A                                 16  2MSGCON(030 JOV1257 *LIBL/SYMSGF)
     A            WSBRUD         1A  B 16 34VALUES('J' 'N' ' ')
     A                                 16 38'(J/N)'
     A                                 17  2MSGCON(030 YBC1325 *LIBL/SYMSGF)
     A            WSSALD         1A  B 17 34VALUES(' ' '+' '0')
     A                                 17 38MSGCON(042 YBC1326 *LIBL/SYMSGF)
     A                                 18 38MSGCON(042 YBC1327 *LIBL/SYMSGF)
     A            WSTYPE         3A  B 19 34DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(030 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 20  2'Skriv til csv-fil ...........:'
     A            WSFILE         1A  B 20 34VALUES('J' 'N' ' ' 'P')
     A  51                             20 38'(Blank=Kun print, J=Fil+Print, F=K-
     A                                      un fil)'
     A                                 22  2'Epost:'
     A            WSEPOST       70A  B 22  9CHECK(LC)
     A N51                             20 38'(Blank=Nei J=Fil og Print, F=Kun f-
     A                                      il)    '
     A                                 21 38MSGCON(040 SHU1895 *LIBL/SYMSGF)
