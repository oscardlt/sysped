********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11109 * 01 YBC PTF11 TAKLE OMBEREGNING. MYE ENDRING I POSISJONER.
********************************************************************
S01  A*                                     DSPSIZ(24 80 *DS3 27 132 *DS4)
N01  A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CA03(03 'Gå til inngangsbilde')
     A                                      CA12(12 'Gå til inngangsbilde')
     A  27                                  CF15(15 'Mer rubr.28')
     A                                  1  2'SYSPED/SAD017'
     A                                      COLOR(BLU)
     A                                  1 20'HOVED-OPPLYSNINGER'
     A                                      COLOR(WHT)
     A                                  1 57'1.Dekl.'
     A            SEDTY          2A  O  1 65TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEDP           2Y 0O  1 68TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                  2  2'2.Avsender'
     A            SEKNK          8Y 0O  2 14TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
     A            SERG          11A  O  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 57'Meldingstype:'
     A            SE1225         3A  O  2 71TEXT('Meldingstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SE1225T       12A  O  2 75TEXT('Meldingstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAK         30A  O  3  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 51'6.Kolli'
     A            SENTK          7Y 0O  3 60TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 89'Ref'
     A            SEAVD          4Y 0O  3 93COLOR(WHT)
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  3 98COLOR(WHT)
     A                                      EDTCDE(4)
     A            SESG           3A  O  3106COLOR(WHT)
     A            SEADK1        30A  O  4  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 51'D/K'
     A            SEKDDK         1A  O  4 55TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 89'Tariffør'
     A            SETARF        16A  O  4 98TEXT('TARIFFØRSNAVN')
     A                                      COLOR(WHT)
     A            SEADK2        30A  O  5  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK3        30A  O  6  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            SEKNS          8Y 0O  7 14TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 51'Andre kostnad'
     A                                  7 65'+/-(P/M)'
     A            SENAS         30A  O  8  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEVAL2         3A  O  8 51TEXT('Valutakode Frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEBEL2        11Y 2O  8 55TEXT('Beløp tollb.frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SEFTG2         1A  O  8 68TEXT('Fortegn +/- a.kost (el. P/M --
     A                                       uten add til rub.22)')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS1        30A  O  9  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 51'15.Avs/utf.land'
     A            SELKA          2A  O  9 67TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 73'17.Best.land'
     A            SELKB          2A  O  9 87TEXT('Bestemmelseland')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS2        30A  O 10  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 51'29.Utpassering'
     A            SEKDH          4Y 0O 10 67TEXT('Utpasseringssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            KTSNAV        15A  O 10 73TEXT('FRITEKST TOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS3        30A  O 11  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 51'19.Cont.'
     A            SEKDC          1A  O 11 60TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'14.Deklarant'
     A            SETLF         12A  O 12 15TEXT('Telefon Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 51'20.Lev.vilk.'
     A            SELV           3A  O 12 64TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELVT         17A  O 12 68TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAD         30A  O 13  2TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 51'22.Fakturasum'
     A                                 13 70'23.Omregn '
     A                                 13 81'24.Tr.type'
     A                                 14  2'21.Ident.'
     A            SETRID        17A  O 14 12TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKT          2A  O 14 30TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEVAL1         3A  O 14 51TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEBEL1        13Y 2O 14 55TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SEVKU          7Y 3O 14 71TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SETST          1Y 0O 14 84TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15  2'25.Transportmåte'
     A            SETRM          2Y 0O 15 19TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 15 51'28.Dato'
     A            SEFID          8Y 0O 15 60DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 76'Finans'
     A            SEFIF         17A  O 15 83TEXT('Finans oppl.faknr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'30.Lag.st.'
     A            SELS          16A  O 16 13TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEKDLS         2A  O 16 30TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 51'48.KT.'
     A            SEKT           8A  O 16 58DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 69'49.Godsn'
     A            SEGN          15A  O 16 78TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*           SEPOS          8A  O 17 60TEXT('Posisjon')
N01  A            SEPOS          8A  O 16 94TEXT('Posisjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*                                18  1'Total toll/avg'
S01  A*           SETOTA        11Y 0O 18 18TEXT('TOTAL TOLL/AVG')
N01  A                                 17  2'Total toll/avg'
N01  A            SETOTA        11Y 0O 17 19TEXT('TOTAL TOLL/AVG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*                                19  1'Avgift:'
S01  A*           SETKD1         2A  O 19 10TEXT('AVGIFTSKODE')
N01  A                                 17 32'Avgift:'
N01  A            SETKD1         2A  O 17 41TEXT('AVGIFTSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*           SETAV1        11Y 0O 19 13TEXT('AVGIFTS BELØP')
N01  A            SETAV1        11Y 0O 17 44TEXT('AVGIFTS BELØP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*           SETKD2         2A  O 19 27TEXT('AVGIFTSKODE')
N01  A            SETKD2         2A  O 17 58TEXT('AVGIFTSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*           SETAV2        11Y 0O 19 30TEXT('AVGIFTS BELØP')
N01  A            SETAV2        11Y 0O 17 61TEXT('AVGIFTS BELØP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*           SETKD3         2A  O 19 44TEXT('AVGIFTSKODE')
N01  A            SETKD3         2A  O 17 75TEXT('AVGIFTSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*           SETAV3        11Y 0O 19 47TEXT('AVGIFTS BELØP')
N01  A            SETAV3        11Y 0O 17 78TEXT('AVGIFTS BELØP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*           SETKD4         2A  O 19 61TEXT('AVGIFTSKODE')
N01  A            SETKD4         2A  O 17 92TEXT('AVGIFTSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*           SETAV4        11Y 0O 19 64TEXT('AVGIFTS BELØP')
N01  A            SETAV4        11Y 0O 17 95TEXT('AVGIFTS BELØP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*                                23  2'F3/F12=Avbryt'
S01  A*                                     COLOR(BLU)
S01  A* 27                             23 18'F15=Rubr. 28'
S01  A*                                     COLOR(BLU)
N01  A  39                             19  2'Omberegning:'
N01  A  39                             19 22'Typetilfelle:'
N01  A  39        SETYPE         4A  O 19 36TEXT('Typetilfelle')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A  39                             19 42'Original fortollings løpenr:'
N01  A  39        SETLL         10A  O 19 71TEXT('Tollnr Løpenr')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A  39                             20 22'Melding til TVINN:'
N01  A  39        SEFT01        70A  O 20 42TEXT('Melding til TVINN')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A  39        SEFT02        70A  O 21 42TEXT('Melding til TVINN')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A  39        SEFT03        70A  O 22 42TEXT('Melding til TVINN')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A  39        SEFT04        70A  O 23 42TEXT('Melding til TVINN')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A  39        SEFT05        70A  O 24 42TEXT('Melding til TVINN')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                 26  2'F3/F12=Avbryt'
N01  A                                      COLOR(BLU)
N01  A  27                             26 18'F15=Rubr. 28'
N01  A                                      COLOR(BLU)
