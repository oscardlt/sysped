      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED FRITEKST TIL FAKTURALINJE  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FFAKTXT    UF A E           K DISK
     FSYFI12FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     FAKTXTK       SETLL     FAKTXT
     C     FAKTXTK       READE(N)  FAKTXT                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     FAKTXTK       READE(N)  FAKTXT                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C                   EXSR      CRTFTX
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     FAKTXTK       SETLL     FAKTXT
     C     FAKTXTK       READE     FAKTXT                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    FAKTXTR
     C     FAKTXTK       READE     FAKTXT                                 33
     C  N33              ENDDO
      *
     C     YRECNO        IFGT      0
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     FXVT          IFNE      '*DELETE'
     C                   WRITE     FAKTXTR
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     FXVT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGFTX
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     FXVT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTFTX
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTFTX        BEGSR
     C***********************************************
     C     W2VT          COMP      *BLANKS                                90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SPF0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
      *
     C                   MOVE      W2VT          FXVT
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2VT
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGFTX        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      FXVT          WSVT
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVE      '0'           *IN90
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      SLCHG
      *
     C     WSVT          COMP      *BLANKS                                90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SPF0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
      *
     C                   MOVE      WSVT          FXVT
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTFTX        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EVAL      FXVT = '*DELETE'
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     FAKTXTK       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C     *IN27         PARM      *IN27         UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UAVD          FXAVD
     C                   MOVE      UTDN          FXOPD
     C                   MOVE      ULI           FXLI
      *
     C                   ENDSR
      *
