      * L1-SYEDI For EILAG INVOIC D.93A
      *
      * 1. L1 Fakturering legger ut poster i EDISF hvis styringsfilen
      *    ESTYF inneholder en samsvarende post på kundenivå.
      *
      * 2. Dette programmet leser fra EDISF
      *    OBS ! Kundenummer må opprettes på EKUNDF
      *
      * 3. Dette programmet legger ut til EDI-filer i SYEDI.
      *
      * 4. SYEDI lager automatisk EDIFACT utifra EDI-filene.
      *
     FEDISF     UF   E             DISK
     FSSTAL02   IF   E           K DISK    PREFIX(Z_)
     FKUNDL01   IF   E           K DISK    PREFIX(X_)
     FVADRL01   IF   E           K DISK    PREFIX(Å_)
     FADREL01   IF   E           K DISK    PREFIX(Å_)
     FEKVNL01   IF   E           K DISK    PREFIX(K_)
     FHORDL06   IF   E           K DISK    PREFIX(Y_)
     FDORDL01   IF   E           K DISK    PREFIX(D_)
     FKODAL01   IF   E           K DISK    PREFIX(Æ_)
     FKHEAL01   IF   E           K DISK    PREFIX(Ø_)
     FVAREL01   IF   E           K DISK    PREFIX(Q_)
     FKODML01   IF   E           K DISK    PREFIX(Q_)
     FKODNL01   IF   E           K DISK    PREFIX(Q_)
     FKOMML01   IF   E           K DISK    PREFIX(V_)
     FKHEGL01   IF   E           K DISK    PREFIX(G_)
     FECMSG     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FEKUNDF    IF   E           K DISK
     FEALC93A   O  A E             DISK
     FECNT93A   O  A E             DISK
     FECTA93A   O  A E             DISK
     FECUX93A   O  A E             DISK
     FEDTM93A   O  A E             DISK
     FEFII93A   O  A E             DISK
     FEFTX93A   O  A E             DISK
     FEIMD93A   O  A E             DISK
     FELIN93A   O  A E             DISK
     FELOC93A   O  A E             DISK
     FEMOA93A   O  A E             DISK
     FENAD93A   O  A E             DISK
     FEPAT93A   O  A E             DISK
     FEPCD93A   O  A E             DISK
     FEPIA93A   O  A E             DISK
     FEPRI93A   O  A E             DISK
     FEQTY93A   O  A E             DISK
     FEQVA93A   O  A E             DISK
     FERFF93A   O  A E             DISK
     FETAX93A   O  A E             DISK
     FETDT93A   O  A E             DISK
     FETOD93A   O  A E             DISK
     FEUNS      O  A E             DISK
     D TABA1           S              1    DIM(29) CTDATA PERRCD(1)
     D TABA2           S              1    DIM(29) ALT(TABA1)
     D TABE1           S              3    DIM(15) CTDATA PERRCD(1)
     D TABE2           S              3    DIM(15) ALT(TABE1)
     D TABE3           S              3    DIM(15) CTDATA PERRCD(1)
     D TABE4           S              3    DIM(15) ALT(TABE3)
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  G_MVADATO              1      8
     D  G_MVAAAR               1      4  0
     D  G_MVAMND               5      6  0
     D  G_MVADAG               7      8  0
     D                 DS
     D  FAKDAT                 1      8
     D  FAKAAR                 1      4  0
     D  FAKMND                 5      6  0
     D  FAKDAG                 7      8  0
     D                 DS
     D  FFDDAT                 1      8
     D  FFDAAR                 1      4  0
     D  FFDMND                 5      6  0
     D  FFDDAG                 7      8  0
     D                 DS
     D  RENDAT                 1      8
     D  KAARFS                 1      4  0
     D  KMNDFS                 5      6  0
     D  KDAGFS                 7      8  0
     D                 DS
     D  ZRDDAT                 1      8
     D  Z_ORDAAR               1      4  0
     D  Z_ORDMND               5      6  0
     D  Z_ORDDAG               7      8  0
     D                 DS
     D  ZAKDAT                 1      8
     D  Z_PAKAAR               1      4  0
     D  Z_PAKMND               5      6  0
     D  Z_PAKDAG               7      8  0
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
     D                 DS
     D  Q_EANNR                1     15  0
     D  Q_EANNR13              3     15
     D                 DS
     D  Q_ENHET                1      5
     D  Q_ENHET3A              1      3
     D  ENH                    1      5
     D                                     DIM(5)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR                                                  ALC14 /
      *////////////////////////////////////////////////////////////
     C     ALC14         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
      *____________
      * Ordrerabatt
      *""""""""""""
     C     ORBEL         IFNE      0
     C                   CLEAR                   ALCR
     C                   Z-ADD     WMN           AMN
     C                   Z-ADD     14            AGN
     C                   Z-ADD     WREP0         AREP0
     C                   MOVEL     'A'           A5463
     C                   MOVEL     'OTE'         A7161
     C                   MOVEL     'ORDRERAB'    A7160
     C     A7160         CAT       'ATT':0       A7160
     C                   WRITE     ALCR
     C                   EXSR      MOA18
     C                   EXSR      TAX20
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  ALC35 /
      *////////////////////////////////////////////////////////////
     C     ALC35         BEGSR
     C                   Z-ADD     0             WREP1
      *____________
      * Ordrerabatt
      *""""""""""""
     C     D_RABBEL      IFNE      0
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   ALCR
     C                   Z-ADD     WMN           AMN
     C                   Z-ADD     35            AGN
     C                   Z-ADD     WREP0         AREP0
     C                   Z-ADD     WREP1         AREP1
     C                   MOVEL     'A'           A5463
     C                   MOVEL     'OTE'         A7161
     C                   MOVEL     'RABATT'      A7160
     C                   WRITE     ALCR
     C                   EXSR      PCD37
     C*                  EXSR      MOA38
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '2'           C6069
     C                   Z-ADD     XANTLIN       ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 CTA05A /
      *////////////////////////////////////////////////////////////
     C     CTA05A        BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
      *___________________________________
      * "Deres referanse fra bestillingen"
      *"""""""""""""""""""""""""""""""""""
     C                   MOVEL     'PD'          C3139
     C                   MOVEL     Y_KUNREF      C3412
     C                   WRITE     CTAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 CTA05B /
      *////////////////////////////////////////////////////////////
     C     CTA05B        BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
      *_________________________________
      * "Vår referanse fra bestillingen"
      *"""""""""""""""""""""""""""""""""
     C                   MOVEL     'AD'          C3139
     C                   MOVEL     Y_SELGER      C3412
     C                   WRITE     CTAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CUX07 /
      *////////////////////////////////////////////////////////////
     C     CUX07         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   CUXR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     7             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '2'           C6347A
     C                   MOVEL     'NOK'         C6345A
     C                   MOVEL     '4'           C6343A
     C                   WRITE     CUXR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
      *____________
      * Meldingsdato
      *""""""""""""
     C                   MOVEL     '137'         D2005
     C                   MOVEL(P)  WDATO         D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
      *____________
      * Leveringsdato
      *""""""""""""
     C                   IF        ZAKDAT <> *ZEROS
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         DREP0
     C                   MOVEL     '35 '         D2005
     C                   MOVEL(P)  ZAKDAT        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 DTM08A /
      *////////////////////////////////////////////////////////////
     C     DTM08A        BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_____________
      * Fakturadato
      *"""""""""""""
     C                   MOVEL     '7'           D2005
     C                   MOVEL(P)  FAKDAT        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 DTM08B /
      *////////////////////////////////////////////////////////////
     C     DTM08B        BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *____________________________
      * Rentedato ved kontantrabatt
      *""""""""""""""""""""""""""""
     C                   MOVEL     '12'          D2005
     C                   MOVEL(P)  RENDAT        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 DTM08C /
      *////////////////////////////////////////////////////////////
     C     DTM08C        BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_____________
      * Forfallsdato
      *"""""""""""""
     C                   MOVEL     '13'          D2005
     C                   MOVEL(P)  FFDDAT        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * EDIM0                                                EDIM0/
      *////////////////////////////////////////////////////////////
     C     EDIM0         BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVE      EK0035        M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   WRITE     EDIMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FII02 /
      *////////////////////////////////////////////////////////////
     C*    FII02         BEGSR
     C*                  Z-ADD     1             WREP1
     C*                  Z-ADD     0             WREP2
     C*                  CLEAR                   FIIR
     C*                  Z-ADD     WMN           FMN
     C*                  Z-ADD     2             FGN
     C*                  Z-ADD     WREP0         FREP0
     C*                  Z-ADD     WREP1         FREP1
      *____________________
      * Kontonummer
      *""""""""""""""""""""
     C*                  MOVEL     'RH'          F3035
     C*                  MOVEL     '60650501910' F3194
     C*                  WRITE     FIIR
     C*                  ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FTX00 /
      *////////////////////////////////////////////////////////////
     C*    FTX00         BEGSR
     C*                  Z-ADD     1             WREP0
     C*                  Z-ADD     0             WREP1
     C*                  CLEAR                   FTXR
     C*                  Z-ADD     WMN           FMN
     C*                  Z-ADD     0             FGN
     C*                  Z-ADD     WREP0         FREP0
     C*                  MOVEL     'INV'         F4451
     C*                  MOVEL     ?FAKT.OPPL?   F4440A
     C*                  WRITE     FTXR
     C*                  ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  IMD22 /
      *////////////////////////////////////////////////////////////
     C     IMD22         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   IMDR
     C                   Z-ADD     WMN           IMN
     C                   Z-ADD     22            IGN
     C                   Z-ADD     WREP0         IREP0
     C                   Z-ADD     WREP1         IREP1
     C                   MOVEL     'FL'          I7077

     C     KOMMKEY       CHAIN     KOMML01                            52
     C                   IF        *IN52=*OFF AND V_UTSKDF='J'
     C                   MOVEL     V_OTBKOM      I7008A
     C                   ELSE

     C     VARKEY        CHAIN     VAREL01                            52
     C                   IF        *IN52
     C                   EVAL      I7008A = 'DIVERSEG'
     C                   ELSE
     C                   MOVEL     Q_VARTE1      I7008A
     C                   MOVEL     Q_VARTE2      I7008B
     C                   ENDIF
     C                   ENDIF
     C                   WRITE     IMDR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LIN22 /
      *////////////////////////////////////////////////////////////
     C     LIN22         BEGSR
     C                   Z-ADD     0             WREP0
      * Første record fra SSTAL02 er allerede lest i init av dette pgm.
     C     *IN53         DOWEQ     *OFF
     C                   MOVE      'O'           D_OTBKOD
     C     DORKEY        CHAIN     DORDL01                            33
     C  N33              DOW       FAKTNR <> D_FAKTNR
     C     DORKEY        READE     DORDL01                                33
     C                   ENDDO
     C                   IF        *IN33
     C                   MOVE      'A'           D_OTBKOD
     C     DORKEY        CHAIN     DORDL01                            33
     C  N33              DOW       FAKTNR <> D_FAKTNR
     C     DORKEY        READE     DORDL01                                33
     C                   ENDDO
     C                   END
      *
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LINR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     22            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP0         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082
     C     KVNKEY        CHAIN     EKVNL01                            52
     C   52              MOVE      *BLANKS       K_EKSVNR
     C     VARKEY        CHAIN     VAREL01                            52
     C   52              MOVE      *ZEROS        Q_EANNR
     C                   IF        Q_EANNR <> 0
     C                   MOVEL     Q_EANNR13     L7140
     C                   END
     C                   MOVEL     'EN'          L7143
     C                   WRITE     LINR
     C                   IF        *IN52 = *OFF
     C                   EXSR      PIA22
     C                   EXSR      IMD22
     C                   END
     C                   EXSR      QTY22
     C                   EXSR      MOA23
     C                   EXSR      PRI25
     C                   EXSR      RFF26
     C                   EXSR      TAX30
     C                   EXSR      ALC35

     C     SSKEY         READE     SSTAL02                                53
     C                   ENDDO
     C                   Z-ADD     WREP0         XANTLIN           5 0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LOC11 /
      *////////////////////////////////////////////////////////////
     C     LOC11         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     11            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   MOVEL     '1'           L3227
     C     Y_VADRNR      IFEQ      0
     C                   MOVE      'O'           Å_OTBKOD
     C     ADRKEY        CHAIN     ADREL01                            52
     C                   ELSE
     C     VADKEY        CHAIN     VADRL01                            52
     C                   ENDIF
     C     *IN52         IFEQ      '1'
     C                   Z-ADD     KUNDNR        WUNDN2
     C     KUNKEY        CHAIN     KUNDL01                            52
     C                   MOVEL     X_ADR3        L3224
     C                   ELSE
     C                   MOVEL     Å_VADRN3      L3224
     C                   END
     C                   WRITE     LOCR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA18 /
      *////////////////////////////////////////////////////////////
     C     MOA18         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     18            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '8'           M5025
     C                   Z-ADD     ORBEL         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA23 /
      *////////////////////////////////////////////////////////////
     C     MOA23         BEGSR
     C                   Z-ADD     0             WREP1
      *______________________
      * Linjebeløp etter rabatter og gebyrer
      *""""""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     23            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '203'         M5025
     C*                  IF        D_BELOP <> 0
     C*                  EVAL      ØN153=D_BELOP
     C                   IF        D_NTOBEL <> 0
     C                   EVAL      ØN153=D_NTOBEL
     C                   ELSE
     C                   EVAL      ØN153=(D_LEVERT*D_PRIS)-D_RABBEL+0,005
     C                   MOVE      '0'           ØN153
     C*                  IF        Z_PRKOSS = 'C'
     C*                  EVAL      ØN153=ØN153 * 0.01
     C*                  ENDIF
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *___________________________________________
      * Linjebeløp før rabatter og gebyrer
      *"""""""""""""""""""""""""""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     23            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '66'          M5025
     C                   IF        D_PRIS = 0
     C                   EVAL      ØN153=D_BVBEL
     C                   ELSE
     C                   EVAL      ØN153=(D_LEVERT*D_PRIS)
     C                   MOVE      '0'           ØN153
     C                   ENDIF
     C*                  IF        Z_PRKOSS = 'C'
     C*                  EVAL      ØN153=ØN153 * 0.01
     C*                  ENDIF

     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA30 /
      *////////////////////////////////////////////////////////////
     C*    MOA30         BEGSR
     C*                  Z-ADD     1             WREP2
     C*                  CLEAR                   MOAR
     C*                  Z-ADD     WMN           MMN
     C*                  Z-ADD     30            MGN
     C*                  Z-ADD     WREP0         MREP0
     C*                  Z-ADD     WREP1         MREP1
     C*                  Z-ADD     WREP2         MREP2
     C*                  MOVEL     '124'         M5025
     C*                  Z-ADD     Z_RABBEL      ØN153
     C*                  EXSR      SRNE
     C*                  MOVEL     ØAN           M5004
     C*                  WRITE     MOAR
     C*                  ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA38 /
      *////////////////////////////////////////////////////////////
     C*    MOA38         BEGSR
     C*                  Z-ADD     1             WREP2
     C*                  CLEAR                   MOAR
     C*                  Z-ADD     WMN           MMN
     C*                  Z-ADD     38            MGN
     C*                  Z-ADD     WREP0         MREP0
     C*                  Z-ADD     WREP1         MREP1
     C*                  Z-ADD     WREP2         MREP2
     C*                  MOVEL     '8'           M5025
     C*                  Z-ADD     Z_RABBEL      ØN153
     C*                  EXSR      SRNE
     C*                  MOVEL     ØAN           M5004
     C*                  WRITE     MOAR
     C*                  ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA45 /
      *////////////////////////////////////////////////////////////
     C     MOA45         BEGSR
      *_____________
      * Fakturabeløp
      *"""""""""""""
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '86'          M5025
     C                   Z-ADD     FTOTN         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *______________________________________________________
      * Sum MVA-grunnlag
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   IF        MVABEL <> 0
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '125'         M5025
     C                   IF        FAKTYP = 'F'
     C                   Z-ADD     MVAGFS        ØN153
     C                   ELSE
     C                   Z-SUB     MVAGFS        ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *______________________________________________________
      * Sum MVA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '150'         M5025
     C                   IF        FAKTYP = 'F'
     C                   Z-ADD     MVABEL        ØN153
     C                   ELSE
     C                   Z-SUB     MVABEL        ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   endif
      *______________________________________________________
      * Sum totalt  varelinjebeløp, inklusive rab/geb
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '203'         M5025
     C                   EVAL      ØN153 = SVBEL - SVRAB + SDBEL
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *______________________________________________________
      * Avrunding
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     AVRUFS        IFNE      0
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '165'         M5025
     C     FAKTYP        IFEQ      'F'
     C     *LIKE         DEFINE    AVRUFS        XAVRUFS
     C                   Z-ADD     AVRUFS        XAVRUFS
     C                   ELSE
     C                   Z-SUB     AVRUFS        XAVRUFS
     C                   END
     C     XAVRUFS       IFGT      0
     C                   Z-ADD     XAVRUFS       ØN153
     C                   ELSE
     C                   Z-SUB     XAVRUFS       ØN153
     C                   END
     C                   EVAL      ØN153=XAVRUFS
     C                   EXSR      SRNE
     C     XAVRUFS       IFGT      0
     C                   MOVEL     ØAN           M5004
     C                   ELSE
     C                   MOVEL     '-'           M5004
     C                   CAT       ØAN:0         M5004
     C                   END
     C                   WRITE     MOAR
     C                   END
      *______________________________________________________
      * Sum totalbeløp eks. MVA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     'NET'         M5025
     C                   EVAL      ØN153=FTOTN-MVABEL-AVRUFS
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *______________________________________________________
      * Sum total rabatt
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      ØN153=SVRAB+ORBEL
     C                   IF        ØN153<>0
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '260'         M5025
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA47 /
      *////////////////////////////////////////////////////////////
     C     MOA47         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     47            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '176'         M5025
     C                   IF        FAKTYP = 'F'
     C                   Z-ADD     MVABEL        ØN153
     C                   ELSE
     C                   Z-SUB     MVABEL        ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD02 /
      *////////////////////////////////////////////////////////////
     C     NAD02         BEGSR
     C                   Z-ADD     0             WREP0
      *________________
      * Fakturamottaker
      *""""""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'IV'          N3035
     C                   Z-ADD     WUNDNR        WUNDN2
     C     KUNKEY        CHAIN     KUNDL01                            52
     C                   IF        X_EANNKU = 0
     C                   MOVEL     WUNDNR        N3039
     C                   MOVEL     '92'          N3055
     C                   ELSE
     C                   MOVEL     X_EANNKU      N3039
     C                   MOVEL     '9'           N3055
     C                   END
     C                   MOVEL     X_KNAVN       N3036A
     C                   MOVEL     X_ADR1        N3042A
     C                   MOVEL     X_ADR3        N3164
     C                   MOVEL     X_POSTNR      N3251
     C                   MOVEL     'NO'          N3207
     C                   WRITE     NADR
      *_______
      * Kjøper
      *"""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'BY'          N3035
     C                   Z-ADD     KUNDNR        WUNDN2
     C     KUNKEY        CHAIN     KUNDL01                            52
     C                   IF        X_EANNKU = 0
     C                   MOVEL     KUNDNR        N3039
     C                   MOVEL     '92'          N3055
     C                   ELSE
     C                   MOVEL     X_EANNKU      N3039
     C                   MOVEL     '9'           N3055
     C                   END
     C                   MOVEL     X_KNAVN       N3036A
     C                   MOVEL     X_ADR1        N3042A
     C                   MOVEL     X_ADR3        N3164
     C                   MOVEL     X_POSTNR      N3251
     C                   MOVEL     'NO'          N3207
     C                   WRITE     NADR
     C     Y_KUNREF      IFNE      *BLANKS
     C                   EXSR      CTA05A
     C                   ENDIF
      *___________________________________
      * Mottaker (hvis forskjellig fra BY)
      *"""""""""""""""""""""""""""""""""""
      * Hent vareadresse
     C     Y_VADRNR      IFEQ      0
     C                   MOVE      'O'           Å_OTBKOD
     C     ADRKEY        CHAIN     ADREL01                            52
     C                   ELSE
     C     VADKEY        CHAIN     VADRL01                            52
     C                   ENDIF
     C     *IN52         IFEQ      *OFF
     C     Å_VADRNA      IFNE      X_KNAVN
     C     Å_VADRN1      ORNE      X_ADR1
     C     Å_VADRN3      ORNE      X_ADR3
     C     Å_VPOSTN      ORNE      X_POSTNR

     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     KUNDNR        N3039
     C                   MOVEL     '92'          N3055
     C                   MOVEL     'DP'          N3035
     C                   MOVEL     Å_VADRNA      N3036A
     C                   MOVEL     Å_VADRN1      N3042A
     C                   MOVEL     Å_VADRN3      N3164
     C                   MOVEL     Å_VPOSTN      N3251
     C                   MOVEL     'NO'          N3207
     C                   WRITE     NADR

     C                   ENDIF
     C                   ENDIF

      *_______
      * Selger
      *"""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     EKKNL         N3039
     C                   MOVEL     '9'           N3055
     C                   MOVEL     'SU'          N3035
     C                   MOVEL     EKNAVN        N3036A
     C                   MOVEL     EKAD1         N3042A
     C                   MOVEL     EKAD2         N3042B
     C                   MOVEL     EKPS          N3164
     C                   MOVEL     EKPN          N3251
     C                   MOVEL     'NO'          N3207
     C                   WRITE     NADR
     C*                  EXSR      FII02
     C                   EXSR      RFF03
     C     Y_SELGER      IFNE      *BLANKS
     C                   EXSR      CTA05B
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PAT08 /
      *////////////////////////////////////////////////////////////
     C     PAT08         BEGSR
      *_____________
      * Fakturadato
      *"""""""""""""
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '1'           P4279
     C                   WRITE     PATR
     C                   EXSR      DTM08A
      *____________________________
      * Renetdato ved kontantrabatt
      *""""""""""""""""""""""""""""
     C     KAARFS        IFGT      0
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '22'          P4279
     C                   WRITE     PATR
     C                   EXSR      DTM08B
     C                   EXSR      PCD08
     C                   ENDIF
      *_____________
      * Forfallsdato
      *"""""""""""""
     C     FFDDAT        IFNE      *ZEROS
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '3'           P4279
     C                   WRITE     PATR
     C                   EXSR      DTM08C
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PCD08 /
      *////////////////////////////////////////////////////////////
     C     PCD08         BEGSR
     C     BETKEY        CHAIN     KODAL01                            52
     C     *IN52         IFEQ      *OFF
     C     Æ_KONRAB      ANDNE     0

     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PCDR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '15'          P5245
     C                   EVAL      ØN153 = Æ_KONRAB
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           P5482
     C                   WRITE     PCDR

     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PCD37 /
      *////////////////////////////////////////////////////////////
     C     PCD37         BEGSR

     C                   Z-ADD     1             WREP2
     C                   CLEAR                   PCDR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     37            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   Z-ADD     WREP2         PREP2
     C                   MOVEL     '3'           P5245
     C                   IF        D_PRIS = 0
     C                   EVAL      ØN153=D_RABBEL/D_BVBEL*100+0,005
     C                   ELSE
     C                   EVAL      ØN153=D_RABBEL/(D_LEVERT*D_PRIS)*100+0,005
     C                   ENDIF
     C                   MOVE      '0'           ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           P5482
     C                   WRITE     PCDR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PIA22 /
      *////////////////////////////////////////////////////////////
     C     PIA22         BEGSR
     C                   Z-ADD     1             WREP1
      *______________________________________
      * Elektroforeningens Artikkel
      *""""""""""""""""""""""""""""""""""""""
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PIAR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     22            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   IF        Q_EANNR = 0
     C                   MOVEL     '5'           P4347
     C                   ELSE
     C                   MOVEL     '1'           P4347
     C                   END
     C                   IF        K_EKSVNR = *BLANKS
     C                   EVAL      P7140A = %TRIML(Q_VARENR)
     C                   MOVEL     'SA '         P7143A
     C                   ELSE
     C                   EVAL      P7140A = %TRIML(K_EKSVNR)
     C                   MOVEL     'NO1'         P7143A
     C                   END
     C                   WRITE     PIAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PRI25 /
      *////////////////////////////////////////////////////////////
     C     PRI25         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PRIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     25            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     'AAB'         P5125
     C                   Z-ADD     D_PRIS        ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           P5118
     C*                  MOVEL     '1'           P5284
     C                   MOVEL     XENHET        P6411
     C                   WRITE     PRIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  QTY22 /
      *////////////////////////////////////////////////////////////
     C     QTY22         BEGSR
     C     1             DO        5             XN                3 0
     C     ENH(XN)       LOOKUP    TABA1         TABA2                    52
     C   52              MOVE      TABA2         ENH(XN)
     C                   ENDDO
     C                   IF        K_EKSVNR = *BLANKS
     C     Q_ENHET3A     LOOKUP    TABE3         TABE4                    52
     C                   IF        *IN52
     C                   MOVE      TABE4         XENHET            3
     C                   ELSE
     C                   MOVE      'PCE'         XENHET
     C                   END
     C                   ELSE
     C     Q_ENHET3A     LOOKUP    TABE1         TABE2                    52
     C                   IF        *IN52
     C                   MOVE      TABE2         XENHET            3
     C                   ELSE
     C                   MOVE      'PCE'         XENHET
     C                   END
     C                   END

     C                   Z-ADD     0             WREP1
      *______________
      * Levert mengde
      *""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     22            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   MOVEL     '46'          Q6063
     C                   Z-ADD     D_LEVERT      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   MOVEL     XENHET        Q6411
     C                   WRITE     QTYR
      *________________
      * Fakturert mengde
      *""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     22            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   MOVEL     '47'          Q6063
     C                   Z-ADD     D_LEVERT      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   MOVEL     XENHET        Q6411
     C                   WRITE     QTYR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   Z-ADD     WREP0         RREP0
      *____________________
      * Selgers Ordrenummer
      *""""""""""""""""""""
     C                   MOVEL     'VN'          R1153
     C                   Z-ADD     Z_ORDNR       ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1154
     C                   WRITE     RFFR
      *_________________
      * Pakkseddelnummer
      *"""""""""""""""""
     C                   IF        Z_PAKKSN <> 0
     C                   ADD       1             RREP0
     C                   MOVEL     'AAK'         R1153
     C                   Z-ADD     Z_PAKKSN      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1154
     C                   WRITE     RFFR
     C                   END
      *_________________
      * Kjøpers bestillingsnr
      *"""""""""""""""""
     C                   ADD       1             RREP0
     C                   MOVEL     'ON '         R1153
     C                   MOVEL     *BLANKS       R1154
     C                   IF        Y_KUNREF = *BLANKS
     C                   MOVEL     '.'           R1154
     C                   ELSE
     C                   MOVEL     Y_KUNREF      R1154
     C                   END
     C                   WRITE     RFFR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
      *____________________
      * Organisasjonsnummer
      *""""""""""""""""""""
     C                   MOVEL     'VA'          R1153
     C                   MOVEL     EKMVNR        R1154
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF26 /
      *////////////////////////////////////////////////////////////
     C     RFF26         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     26            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
      *____________________
      * Kjøpersordrenummer
      *""""""""""""""""""""
     C*    DORKEY        CHAIN     DORDL01                            33
     C*                  MOVEL     'ON'          R1153
     C*                  IF        ((*IN33 = *ON) OR (D_MERK = *BLANKS))
     C*                  MOVEL     '.'           R1154
     C*                  ELSE
     C*                  MOVEL     D_MERK        R1154
     C*                  END
     C                   MOVEL     'ON '         R1153
     C                   IF        Y_KUNREF = *BLANKS
     C                   MOVEL     '.'           R1154
     C                   ELSE
     C                   MOVEL     Y_KUNREF      R1154
     C                   END
     C                   Z-ADD     WREP0         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1156
     C                   WRITE     RFFR
     C                   MOVE      *BLANKS       R1154
      *___________________
      * Selgersordrenummer
      *"""""""""""""""""""
     C                   ADD       1             RREP1
     C                   MOVEL     'VN '         R1153
     C                   Z-ADD     Z_ORDNR       ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1154
     C                   WRITE     RFFR
      *_________________
      * Pakkseddelnummer
      *"""""""""""""""""
     C                   IF        Z_PAKKSN <> 0
     C                   MOVE      *BLANKS       R1154
     C                   ADD       1             RREP1
     C                   MOVEL     'AAK'         R1153
     C                   Z-ADD     Z_PAKKSN      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1154
     C                   WRITE     RFFR
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UFIRMA            2
     C                   PARM                    UKUNDNR           8
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C     WÅÅ           IFGT      31
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   ELSE
     C                   Z-ADD     WDD           WÅ
     C                   Z-ADD     WÅÅ           WD
     C                   ENDIF
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    ECSR          WCSR
     C     *LIKE         DEFINE    EC0065        WC0065
     C     *LIKE         DEFINE    KUNDNR        WUNDNR
     C     *LIKE         DEFINE    KUNDNR        WUNDN2
     C     *LIKE         DEFINE    KUNDNR        W_KUNDNR
     C     *LIKE         DEFINE    Ø_AVG         WAVG
     C     *LIKE         DEFINE    AVBEL1        WAVBEL
     C     *LIKE         DEFINE    UMN           WMN
     C     *LIKE         DEFINE    V_OTBKOD      W_OTBKOD
     C     *LIKE         DEFINE    V_LNRKOM      W_LNRKOM
     C                   MOVE      'S'           WCSR
     C                   MOVEL     'INVOIC'      WC0065
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   MOVE      UKUNDNR       W_KUNDNR
     C                   MOVE      'O'           W_OTBKOD
     C                   Z-ADD     0             W_LNRKOM
     C                   Z-ADD     0             W_ØN153          15 3
      * DEF AV KEYS
     C     KVNKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    K_KODEEV
     C                   KFLD                    Z_VARENR
     C                   MOVE      'NO'          K_KODEEV
     C     EKUNDFK       KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    W_KUNDNR
     C     ECMKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    WUNDNR
     C                   KFLD                    WCSR
     C                   KFLD                    WC0065
     C     SSKEY         KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    FAKAAR
     C                   KFLD                    FAKTNR
     C     KUNKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    WUNDN2
     C     VADKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    Y_VADRNR
     C     ADRKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    D_OTBKOD
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDNR
     C     HORKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDMND
     C                   KFLD                    Z_ORDDAG
     C                   KFLD                    Z_ORDNR
     C     DORKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    D_OTBKOD
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDNR
     C                   KFLD                    Z_LNRORD
     C                   MOVE      'O'           D_OTBKOD
     C     BETKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    BETBET
     C     AVGKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    WAVG
     C     VARKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    Z_VARENR
     C     KOMKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    Y_LEVBET
     C     KONKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    Y_LEVMAT
     C     KOMMKEY       KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    W_OTBKOD
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDNR
     C                   KFLD                    Z_LNRORD
     C                   KFLD                    W_LNRKOM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Les faktura fra L1
      *"""""""""""""""""""
     C                   READ      EDISF                                  51
     C  N51EKUNDFK       CHAIN     EKUNDF                             51
     C     *IN51         DOWEQ     '0'
     C     AKTKOD        IFEQ      'A'
     C     FIRMA         ANDEQ     UFIRMA
      *_________________________
      * Hente riktig kundenummer
      *"""""""""""""""""""""""""
     C                   Z-ADD     KUNDNR        WUNDNR
     C     FMOT          IFGT      0
     C                   Z-ADD     FMOT          WUNDNR
     C                   ENDIF
     C     WUNDNR        IFEQ      W_KUNDNR
     C     FTOTN         ANDNE     0
      *_______________________
      * Hente ordreinformasjon
      *"""""""""""""""""""""""
     C     SSKEY         CHAIN     SSTAL02                            53
     C     HORKEY        CHAIN     HORDL06                            52
      *________________________
      * Hente partID og release
      *""""""""""""""""""""""""
     C     ECMKEY        CHAIN     ECMSG                              52
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   MOVE      '1'           *IN41
      *______________
      * Hent momssats
      *""""""""""""""
     C     FIRMA         CHAIN     KHEGL01                            52
      *_________________
      * EDIFACT Detaljer
      *"""""""""""""""""
     C                   EXSR      SRB
      *____________________________
      * Oppdater status på fakturan
      *""""""""""""""""""""""""""""
     C                   MOVE      'D'           AKTKOD
     C                   ENDIF
     C                   ENDIF
     C                   UPDATE    EDISR
      *__________________
      * Les neste faktura
      *""""""""""""""""""
     C                   READ      EDISF                                  51
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      DTM00
     C*                  EXSR      FTX00
     C                   EXSR      RFF01
     C                   EXSR      NAD02
     C                   EXSR      CUX07
     C                   EXSR      PAT08
     C                   EXSR      TOD11
     C                   EXSR      ALC14
     C                   EXSR      LIN22
     C                   EXSR      UNS00
     C                   EXSR      CNT00
     C                   EXSR      MOA45
     C                   EXSR      TAX47
      *_____________
      * WRITE TO UNH
      *"""""""""""""
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      EDIM0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Remove neg sign.
     C                   IF        ØN153 < 0
     C                   Z-SUB     ØN153         ØN153
     C                   ENDIF
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'
     C                   MOVE      ØA153H        ØA12
     C                   ELSE
     C                   MOVEL     ØA153X        ØA12
     C                   MOVE      ',   '        ØA12
     C                   MOVE      ØA153D        ØA12
     C                   END
      * search from left and find first sign over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'
     C     ØA153X        IFEQ      '00000000'
     C                   SUB       1             ØX
     C                   ENDIF
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX20 /
      *////////////////////////////////////////////////////////////
     C     TAX20         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     20            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   Z-ADD     WREP1         TREP1
     C                   MOVEL     '7'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   MOVEL     '0'           T5278
     C                   MOVEL     'E'           T5305
     C                   WRITE     TAXR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX30 /
      *////////////////////////////////////////////////////////////
     C     TAX30         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     30            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   Z-ADD     WREP1         TREP1
     C                   MOVEL     '7'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   IF        D_VMVA = '3'
     C     G_MVADATO     IFLE      FAKDAT
     C                   Z-ADD     G_MVA         ØN153
     C                   ELSE
     C                   Z-ADD     G_MVAGML      ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           T5278
     C                   MOVEL     'S'           T5305
     C                   ELSE
     C                   MOVEL     '0'           T5278
     C                   MOVEL     'E'           T5305
     C                   END
     C                   WRITE     TAXR
     C*                  IF        D_VMVA = ' '
     C*                  EXSR      MOA30
     C*                  ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX47 /
      *////////////////////////////////////////////////////////////
     C     TAX47         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     47            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     '7'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   IF        FAKTYP = 'F'
     C                   Z-ADD     MVAGFS        ØN153
     C                   ELSE
     C                   Z-SUB     MVAGFS        ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           T5286
     C     G_MVADATO     IFLE      FAKDAT
     C                   Z-ADD     G_MVA         ØN153
     C                   ELSE
     C                   Z-ADD     G_MVAGML      ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           T5278
     C                   MOVEL     'S'           T5305
     C                   WRITE     TAXR
     C                   EXSR      MOA47

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TOD11 /
      *////////////////////////////////////////////////////////////
     C     TOD11         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   TODR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     11            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     '3'           T4055
     C                   MOVEL     'DDP'         T4053
     C                   MOVEL     '106'         T1131
     C                   WRITE     TODR
     C                   EXSR      LOC11
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     EC0065        U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '93A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'TBF10'       U0057
     C                   Z-ADD     FAKTNR        ØN153
     C                   EXSR      SRNE
     C     FAKTYP        IFEQ      'F'
     C                   MOVEL     '380'         U1001
     C                   MOVEL     ØAN           U1004
     C                   ELSE
     C                   MOVEL     '381'         U1001
     C                   MOVEL     ØAN           U1004
     C                   ENDIF
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  UNS00 /
      *////////////////////////////////////////////////////////////
     C     UNS00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   UNSR
     C                   Z-ADD     WMN           UMN
     C                   Z-ADD     50            UGN
     C                   Z-ADD     WREP0         UREP0
     C                   MOVEL     'S'           U0081
     C                   WRITE     UNSR
     C                   ENDSR
**
aA
bB
cC
dD
eE
fF
gG
hH
iI
jJ
kK
lL
mM
nN
oO
pP
qQ
rR
sS
tT
uU
vV
wW
xX
yY
zZ
æÆ
øØ
åÅ
**
STKSTK
SETSET
METM
M  M
PARPAR
M3 M3
M2 M2
LTRLTR
KG KG
DIVSTK
PALPAL
KARKRT
PAKPAK
ESKESK
BUNBNT
**
STKPCE
SETSET
METMTR
M  MTR
PARPR
M3 MTQ
M2 MTK
LTRLTR
KG KGM
DIVPCE
PALPF
KARCT
PAKPA
ESKESK
BUNBE
