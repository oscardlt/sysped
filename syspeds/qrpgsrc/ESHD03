      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/ESHD03 MODULE(*LIBL/ESHD03 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF10 Title sette i RPG
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    USROPN
     FMSGKL01   IF   E           K DISK    USROPN
     FARKIVL11  IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    usropn
     FARKTXT    IF   E           K DISK    usropn
     FARKEXT    IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D KUNDE           S             30
     D EPOSTADR        S             70
     D TELEFON         S             21
     D KPERSON         S             30
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
N01  D Title           S            100
     D MSGREFA         s              7
     D                 DS
     D  MSGMTXB                4     30
     D    MSGMTX               1     30
     D                 DS
     D  MSGDTXB                5     30
     D    MSGDTX               1     30
     D                 DS
      *
     D  updf1                  1    400
     D  updf2                401    800
     D  updf3                801   1200
     D  updf4               1201   1600
     D  updf5               1601   2000
     D  updf6               2001   2400
     D  updf7               2401   2800
     D  updf8               2801   3200
     D  updf9               3201   3600
     D  updf10              3601   4000
     D  updf11              4001   4400
     D  updf12              4401   4800
     D  updf13              4801   5200
     D  updf14              5201   5600
     D  updf15              5601   6000
     D  updf16              6001   6400
     D  updf17              6401   6800
     D  updf18              6801   7200
     D  updf19              7201   7600
     D  updf20              7601   8000
     D  updf                   1   8000    DIM(20)
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'
      * Definere  hex appostrof
     D Ap              c                   x'7D'
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
      ***
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     MSGKL01
     C                   close     ARKIVL11
     C                   CLOSE     FIRMARC
     C                   CLOSE     ARKTXT
     C                   CLOSE     ARKEXT
      *
     C     SLUTT         TAG
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *
      *
      *************************************
     C     HentArk       begsr
      *************************************
      * (MSGREF er 9 lang - kun siste 7 ligger i arkiv/ARUNDE=HD:1234567 )
      *
     C                   MOVE      *BLANKS       UPDF
     C                   MOVE      *ZEROS        XN                3 0
     c                   move      MSGREF        XXUNDE
     c                   movel     'HD:'         XXUNDE
     c     ArkKey        setll     ARKIVL11
     c     ArkKey        reade     ARKIVL11                               33
     c     *in33         doweq     '0'
      *
     C     ARTYPE        CHAIN     ARKTXT                             33
     C                   MOVE      ARKLAG        ARCEXT
     C     ARKEXTK       CHAIN     ARKEXT                             33
     C   33BIFIRM        CHAIN     FIRMARC                            33
     C*
     C                   ADD       1             XN
      *
     C                   EVAL      UPDF(XN) = '<a href="'
     C                             + '/'  + %trim(ARCANE)
     C                             + '/'  + %trim(ARLINK)
     C                             + '" target="SyPDFWindN">'
     C                             + %trim(ARRFK)
     C                             + '<img src="/syspeddev/Binders01.jpg"'
     C                             + ' ALT="'
     C                             + %trim(ARRFK)
     C                             + '"></a>'
     c     ArkKey        reade     ARKIVL11                               33
     c                   enddo
     c     Updf1         ifeq      *blanks
     c                   eval      Updf1 = 'Ingen.'
     c                   endif
      *
     C                   callp     updHTMLvar('UPDF1':UPDF1)
     C                   callp     updHTMLvar('UPDF2':UPDF2)
     C                   callp     updHTMLvar('UPDF3':UPDF3)
     C                   callp     updHTMLvar('UPDF4':UPDF4)
     C                   callp     updHTMLvar('UPDF5':UPDF5)
     C                   callp     updHTMLvar('UPDF6':UPDF6)
     C                   callp     updHTMLvar('UPDF7':UPDF7)
     C                   callp     updHTMLvar('UPDF8':UPDF8)
     C                   callp     updHTMLvar('UPDF9':UPDF9)
     C                   callp     updHTMLvar('UPDF10':UPDF10)
     C                   callp     updHTMLvar('UPDF11':UPDF11)
     C                   callp     updHTMLvar('UPDF12':UPDF12)
     C                   callp     updHTMLvar('UPDF13':UPDF13)
     C                   callp     updHTMLvar('UPDF14':UPDF14)
     C                   callp     updHTMLvar('UPDF15':UPDF15)
     C                   callp     updHTMLvar('UPDF16':UPDF16)
     C                   callp     updHTMLvar('UPDF17':UPDF17)
     C                   callp     updHTMLvar('UPDF18':UPDF18)
     C                   callp     updHTMLvar('UPDF19':UPDF19)
     C                   callp     updHTMLvar('UPDF20':UPDF20)
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
     C                   eval      WSUSER   = uppify(WSUSER)
     C                   eval      MSGREFA  = zhbgetvar('MSGREF')
     C                   eval      MSGREF  = c2n2(MSGREFA)
      *
     C                   endsr
      *
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
N01  c                   eval      Title='SYSTEMA HELPDESK'
N01  C                   callp     updhtmlvar('TITLE':Title)
     c
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('User':WSUSER)
     c     MSGREF        chain     MSGKL01                            33
      * erstatt Èè med CRLF i problembeskrivlesen
     c     'Î|':CrLf     xlate     MSGPRO        MSGPRO
      * meld-dato/tid
     c                   movel     20            MSGDAT8           8 0
     c                   move      MSGDAT        MSGDAT8
     c                   movel     MSGTID        MSGTID4           4 0
      * ferd-dato/tid
     c                   move      *zeros        MSGFDA8           8 0
     c                   move      *blanks       FDTXT            12
     c                   move      *blanks       FTTXT            12
     c     MSGFDA        ifne      *zeros
     c                   movel     20            MSGFDA8           8 0
     c                   move      MSGFDA        MSGFDA8
     c                   eval      FDTXT='Ferd.dato:'
     c                   eval      FTTXT='Kl:'
     C                   endif
     c                   movel     MSGFTI        MSGFTI4           4 0
      * Status med full tekst
     c                   move      *blanks       Status           50
     c                   select
     c     MSGSTA        wheneq    'A  '
     c                   eval      Status = 'Ny'
     c     MSGEAV        ifne      *blanks                                      Endret av/eier
     c                   eval      Status = 'Påbegynt'
     c                   endif
     c     MSGTBR        ifne      *blanks                                      Endret av/eier
     c                   eval      Status = 'Videresendt'
     c                   endif
     c     MSGSTA        wheneq    'AB '                                        Åpnet av  konsulent
     c     MSGSTA        oreq      'AC '                                        Tilbake til trond
     c                   eval      Status = 'Påbegynt'
     c     MSGSTA        wheneq    'FER'
     c                   eval      Status = 'Avsluttet'
     c     MSGSTA        wheneq    '999'
     c                   eval      Status = 'Slettet'
     c                   other
     c                   eval      Status = 'UKJ: '+MSGSTA
     c                   endsl
     C                   callp     updHTMLvar('STATUS':STATUS)
     c                   callp     UpdHTMLvar('HDREF':
     c                             %trim(%editc(MSGREF:'Z')))
     c                   eval      Kunde = %trim(%editc(MSGKNR:'Z'))
     c                             + ' '  + MSGNAM
     C                   callp     updhtmlvar('KUNDE':KUNDE)
     C                   callp     updhtmlvar('KPERS':MSGKPE)
     C                   callp     updhtmlvar('EPOSTADR':MSGEPO)
     C                   callp     updhtmlvar('TELEFON':MSGTLF)
     C                   callp     updhtmlvar('KPERS':MSGKPE)
     C                   callp     updhtmlvar('MSGMTX':MSGMTXB)
     C                   callp     updhtmlvar('MSGDTX':MSGDTXB)
     C                   callp     updhtmlvar('MSGPRO':MSGPRO)
     C                   callp     updhtmlvar('MSGLØS':MSGLØS)
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(MSGDAT8:'    .  .  ')))
     C                   callp     updHTMLvar('TID':
     C                             %trim(%editw(MSGTID4:'  :  ')))
     C                   callp     updhtmlvar('FDTXT':FDTXT)
     C                   callp     updHTMLvar('FDATO':
     C                             %trim(%editw(MSGFDA8:'    .  .  ')))
     C                   callp     updhtmlvar('FTTXT':FTTXT)
     C                   callp     updHTMLvar('FTID':
     C                             %trim(%editw(MSGFTI4:'  :  ')))
      *
     C                   exsr      HentArk
      *
     C                   endsr
      *
      *=====================================================================******
     C     Init          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              30
      *
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
      * (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   eval      rc = docmd('OVRDBF FILE(MSGKL01) +
     C                             TOFILE(SYHD/MSGKL01)')
     C                   open      MSGKL01
     c                   OPEN      ARKIVL11
     C                   OPEN      FIRMARC
     C                   OPEN      ARKTXT
     C                   OPEN      ARKEXT
      *
      *
     C     ARKKey        klist
     C                   kfld                    ARSTAT
     C                   kfld                    XXUNDE           10
     C                   MOVE      'A'           ARSTAT
     C     ARKEXTK       KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    ARCEXT
      *
     c                   movel     Hex0D         CrLf              2
     c                   move      Hex25         CrLf
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'ESHD03 '
      *------------------
     C                   endsr
      *
