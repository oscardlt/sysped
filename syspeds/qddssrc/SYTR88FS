     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(*DFT 18 75 *NOMSGLIN)
     A*
     A          R SUBFIL                    SFL
     A            STFR           3A  H
     A            STKDPL         1A  H
     A            STAMNA        30A  H
     A            STAMA1        30A  H
     A            STAMA2        30A  H
     A            STAMA3        30A  H
     A            STLKFB         2A  H
     A            STSDFB         5A  H
     A            STLKTB         2A  H
     A            STSDTB         5A  H
     A            STVKT          9S 0H
     A            STFBV          9S 0H
     A            STLM           4S 2H
     A            STM3           7S 3H
     A            STNT           7S 0H
     A            STFRB          3A  H
     A            STKDPB         1A  H
     A            STKNRB         8S 0H
     A            STVKTS         9S 0H
     A            STFBVS         9S 0H
     A            STLMS          4S 2H
     A            STM3S          7S 3H
     A            STNTS          7S 0H
     A            STOPFS        11S 2H
     A            STSAFS        11S 2H
     A            STBEFS        11S 2H
     A            STBEFD        11S 2H
     A            XXFRA         11S 2H
     A            STOAND         9S 6H
     A            KUAVTA         8S 0H
     A            STAVD          4S 0H
     A            STOPD          7S 0H
     A            STOPKO         1A  H
     A            WSNR           1A  I  6  2VALUES(' ' '1' '5' '6' '7' 'X')
     A            STKNR          8  0O  6  4EDTCDE(4)
     A  52N53                               COLOR(WHT)
     A  52 53                               COLOR(PNK)
     A     55                               DSPATR(RI)
     A            STLKF          2A  O  6 13
     A  52N53                               COLOR(WHT)
     A  52 53                               COLOR(PNK)
     A            STSDF          5A  O  6 16
     A  52N53                               COLOR(WHT)
     A  52 53                               COLOR(PNK)
     A            STLKT          2A  O  6 22
     A  52N53                               COLOR(WHT)
     A  52 53                               COLOR(PNK)
     A            STSDT          5A  O  6 25
     A  52N53                               COLOR(WHT)
     A  52 53                               COLOR(PNK)
     A            STAVMO        10A  O  6 31
     A            STNR           2Y 0O  6 42EDTCDE(4)
     A            STOPFR        11Y 2O  6 45EDTCDE(J)
     A  54                                  DSPATR(RI)
     A            XXBEL         11Y 2O  6 61EDTCDE(J)
     A  51                                  DSPATR(HI)
     A  56                                  COLOR(RED)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED400/SYTR88S'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med samtaksering av tur   -
     A                                              '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 YBC0639 *LIBL/SYMSGF)
     A            UPRO2          8Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21'Bilnr.'
     A            TUBILN        10A  O  2 28COLOR(WHT)
     A                                  2 39'Avg:'
     A            WSDT           6Y 0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A            TUTM           4Y 0O  2 51EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  2 57'Ank:'
     A            WSDTT          6Y 0O  2 62EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUTMT          4Y 0O  2 69EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  3  2'Velg: 1=Samtaksering  5=Se oppdrag-
     A                                        6=Se fakt.  7=Arb. m. oppdr./fakt-
     A                                      ura'
     A                                      COLOR(BLU)
     A                                  4  8'X=Opphev.'
     A                                      COLOR(BLU)
     A                                  4 47'Opprinnelig   Fraktpris etter'
     A                                      COLOR(WHT)
     A                                  5  2'Op Kundenr Fra sted Til sted Avs./-
     A                                      Mott. Snr  fraktpris'
     A                                      COLOR(WHT)
     A N50                              5 60'samtaks./juster.'
     A                                      COLOR(WHT)
     A  50                              5 61'REN samtakser.'
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA07(07 'MODUS')
     A                                      CA12(12 'AVSLUTT')
     A                                      SLNO(17)
     A                                      CLRL(02)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        73A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A N50                              1 41'F7=Se KUN samtaksert'
     A                                      COLOR(BLU)
     A  50                              1 41'F7=Se med evt. justering'
     A                                      COLOR(BLU)
     A*
     A          R VARELI
     A                                      WINDOW(*DFT 10 65 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF10(10 'SAMTAKSER')
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED400/SYTR88S'
     A                                      COLOR(BLU)
     A                                  1 20'Utfør samtaksering'
     A                                      COLOR(WHT)
     A                                  2  1'Knr:'
     A            WSKNRB         8Y 0O  2  6COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 16'Fra:'
     A            WSLKF          2A  O  2 21COLOR(WHT)
     A            WSSDF          5A  O  2 24COLOR(WHT)
     A                                  2 30'Til:'
     A            WSLKT          2A  O  2 35COLOR(WHT)
     A            WSSDT          5A  O  2 38COLOR(WHT)
     A                                  1 46'Ant. opd:'
     A            WSANTO         3Y 0O  1 57EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSANTF         3Y 0O  1 63EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  1'Benyttede verdier.:'
     A            WSLKFB         2A  B  3 21COLOR(WHT)
     A            WSSDFB         5A  B  3 24COLOR(WHT)
     A            WSLKTB         2A  B  3 35COLOR(WHT)
     A            WSSDTB         5A  B  3 38COLOR(WHT)
     A                                  3 46'Frank:'
     A            WSFRB          3A  O  3 53COLOR(WHT)
     A                                  3 57'Pr.kod:'
     A            WSKDPB         1A  O  3 65COLOR(WHT)
     A                                  4  1'Vkt:'
     A            WSVKTS         9Y 0O  4  6COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 16'Volum:'
     A            WSM3S          7Y 3O  4 23EDTWRD('    .   ')
     A                                      COLOR(WHT)
     A                                  4 32'Lastem:'
     A            WSLMS          4Y 2O  4 40EDTWRD('  .  ')
     A                                      COLOR(WHT)
     A                                  4 46'F.vkt:'
     A            WSFBVS         9Y 0O  4 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  6'Sum original oppdragspris.........-
     A                                      ..:'
     A            WSOPFS        11Y 2O  6 48EDTCDE(J)
     A                                      COLOR(WHT)
     A                                  7  6'Avtaleberegnet samtakseringspris..-
     A                                      ..:'
     A            WSSAFS        11Y 2O  7 48EDTCDE(J)
     A                                      COLOR(WHT)
     A                                  8  6'Fraktpris fordelt/til fordeling...-
     A                                      ..:'
     A            WSBEFS        11Y 2B  8 48EDTCDE(J)
     A                                      DSPATR(UL)
     A N30                             10 46MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 24'F10=Fordel frakt '
     A                                      COLOR(BLU)
     A                                  1 61'/'
     A                                  2 46'Ant'
     A                                  2 50'kll.:'
     A            WSNTS          7Y 0O  2 57COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  9  6'Ford.nøkk (1=Vkt 2=M3 3=LM 4=Oppr -
     A                                      pris).:'
     A            NØKKEL         1A  B  9 48
     A*
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED400/SYTR88S'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med samtaksering av tur   -
     A                                              '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 YBC0639 *LIBL/SYMSGF)
     A            UPRO2          8Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21'Bilnr.'
     A            TUBILN        10A  O  2 28COLOR(WHT)
     A                                  2 39'Avg:'
     A            WSDT           6Y 0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 50DSPATR(ND)
     A            TUTM           4Y 0O  2 51EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  2 57'Ank:'
     A            WSDTT          6Y 0O  2 62EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUTMT          4Y 0O  2 69EDTWRD('  :  ')
     A                                      COLOR(WHT)
     A                                  3  2'Velg: 1=Samtaksering  5=Se oppdrag-
     A                                        6=Se fakt.  7=Arb. m. oppdr./fakt-
     A                                      ura'
     A                                      COLOR(BLU)
     A                                  4  8'X=Opphev.'
     A                                      COLOR(BLU)
     A                                  4 47'Opprinnelig   Fraktpris etter'
     A                                      COLOR(WHT)
     A                                  5  2'Op Kundenr Fra sted Til sted Avs./-
     A                                      Mott. Snr  fraktpris    samtaks./ju-
     A                                      ster.'
     A                                      COLOR(WHT)
     A                                 17  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             18  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
