     FFIRM      IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FEDISE     IF   E           K DISK
     FEDISE1K   IF   E           K DISK    RENAME(EDISER:EDISER1K)
     FEDIRE     IF   E           K DISK
     FEDIRE1K   IF   E           K DISK    RENAME(EDIRER:EDIRER1K)
     FEDI41PXM  O  A F 1024        DISK    USROPN
     FEDI41DXM  CF   E             WORKSTN
     D EDIDTA          DS           256
     D  UELIBF               171    180
     IEDIRER1K
     I              RDATA                       RDATA1K
     IEDISER1K
     I              SDATA                       SDATA1K
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *_______________
      * Create Workfile
      *"""""""""""""""
     C                   CALL      'EDI41CXM1'
      *______________
      * Fill Workfile
      *""""""""""""""
     C                   OPEN      EDI41PXM
     C                   EXSR      RTVDB
     C                   CLOSE     EDI41PXM
      *_______________
      * Copy to Workfile
      *"""""""""""""""
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN           10
n02  C                   EVAL      XLINK = '/SYSPEDDEV/TEMP/' + %TRIM(UELIBF)
n02  C                             + %TRIM(WMBR) + %TRIM(NEWRAN) + '.XML'
     C                   CALL      'EDI41CXM2'
     C                   PARM                    XLINK
n02  C                   EVAL      XLINK = 'http://' + %TRIM(ARCPAD)
n02  C                             + '/SYSPEDDEV/TEMP/' + %TRIM(UELIBF)
n02  C                             + %TRIM(WMBR) + %TRIM(NEWRAN) + '.XML'
      *_________________
      * Display WORKFILE
      *"""""""""""""""""
     C                   WRITE     WINDOWSZ
     C                   EXFMT     BILDE01
      *_______________
      * Delete Workfile
      *"""""""""""""""
n02  C                   EVAL      XLINK = '/SYSPEDDEV/TEMP/' + %TRIM(UELIBF)
n02  C                             + %TRIM(WMBR) + %TRIM(NEWRAN) + '.XML'
     C                   CALL      'EDI41CXM3'
     C                   PARM                    XLINK
      *
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WLIB             10
     C                   PARM                    WFILE            10
     C                   PARM                    WMBR             10
      *
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
     C                   READ      FIRM                                   33
     C     FIFIRM        CHAIN     FIRMARC                            33
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SFL          RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *
     C                   SELECT
     C                   WHEN      WFILE = 'EDIRE'
     C     WMBR          SETLL     EDIRE
     C     WMBR          READE     EDIRE                                  51
     C                   DOW       *IN51 = *OFF
     C                   EVAL      ZDATA = RDATA
     C                   EXCEPT    EDIADD
     C     WMBR          READE     EDIRE                                  51
     C                   ENDDO
     C                   WHEN      WFILE = 'EDISE'
     C     WMBR          SETLL     EDISE
     C     WMBR          READE     EDISE                                  51
     C                   DOW       *IN51 = *OFF
     C                   EVAL      ZDATA = SDATA
     C                   EXCEPT    EDIADD
     C     WMBR          READE     EDISE                                  51
     C                   ENDDO
     C                   WHEN      WFILE = 'EDISE1K'
     C     WMBR          SETLL     EDISE1K
     C     WMBR          READE     EDISE1K                                51
     C                   DOW       *IN51 = *OFF
     C                   MOVE      SDATA1K       ZDATA          1024
     C                   EXCEPT    EDIADD
     C     WMBR          READE     EDISE1K                                51
     C                   ENDDO
     C                   OTHER
     C     WMBR          SETLL     EDIRE1K
     C     WMBR          READE     EDIRE1K                                51
     C                   DOW       *IN51 = *OFF
     C                   EVAL      ZDATA = RDATA1K
     C                   EXCEPT    EDIADD
     C     WMBR          READE     EDIRE1K                                51
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     OEDI41PXM  EADD         EDIADD
     O                       ZDATA
