********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 endring hanling info
********************************************************************
     FDOLAL01   IF   E           K DISK
     FSYLA03P   O    F   36        PRINTER OFLIND(*INOF)
     D F1              S             36    DIM(67) CTDATA PERRCD(1)             FORM for AWB
     D F2              S             36    DIM(72) CTDATA PERRCD(1)             FORM for HWB
     D A4              S              1    DIM(4)                               Input string
     D B4              S              1    DIM(4)                               Output string
     D Æ               S              1    DIM(20)                              Input string
     D Å               S              1    DIM(20)                              Output string
     D                 DS
      * Make "No. of Pieces" Zoned numeric
     D  LANUMA                 1      4  0
     D                 DS
     D  ØN151                  1     15  1
     D  ØA151H                 1     14
     D  ØA151X                 5     14
     D  ØA151D                15     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)                              Num edit
     D                 DS
     D  DATA                   1     36
     D  DATA1                  2     36
     D                 DS
     D  WRK                    1      4  0
     D                 DS            10
     D MINNE           C                   CONST('M180,20,20')
     D                 DS            10
     D  WCOPN                  1      4  0
     D  WCOPA                  1      4
      * Input Parameter
     C     *ENTRY        PLIST
     C                   PARM                    AVD               4 0                       ID
     C                   PARM                    OPD               7 0                       ID
      *
     C                   EXSR      SRINIT
     C     *IN90         IFEQ      *ON
     C                   GOTO      ENDPGM
     C                   ENDIF
      * LOOP FOR NO OF COPIES
     C                   DO        LANUMC        WCOPN
      * Send and set Correct form
     C                   EXSR      SR0001
      * Send field data to printer
     C                   EXSR      SR100
      * Set number of copies
     C                   EXSR      SR200
      * END LOOP
     C                   ENDDO
      * End Program
     C     ENDPGM        TAG
     C                   SETON                                        LR
      *_____________________________________________________
      * Execute Label Init
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR0001        BEGSR
     C                   Z-ADD     0             ØX
     C     LAHCDE        IFNE      'H'
      * Send AWB FORM from tabel
     C                   DO        67
     C                   ADD       1             ØX
     C                   MOVEA     F1(ØX)        DATA
     C                   EXCEPT    SYLA01
     C                   ENDDO
     C                   ELSE
      * Send HWB FORM from tabel
     C                   DO        72
     C                   ADD       1             ØX
     C                   MOVEA     F2(ØX)        DATA
     C                   EXCEPT    SYLA01
     C                   ENDDO
     C                   ENDIF
     C                   Z-ADD     0             ØX
      * Send Default Instructions, blank to start command structure
     C                   MOVE      *BLANKS       DATA
     C                   EXCEPT    SYLA01
      * Retreive FORM
     C                   MOVEL     *BLANKS       DATA
     C     LAHCDE        IFNE      'H'
     C                   MOVEL     'FR"AWB"'     DATA
     C                   ELSE
     C                   MOVEL     'FR"HWB"'     DATA
     C                   ENDIF
     C                   EXCEPT    SYLA01
      * Make printer ready to receive paramaters
     C                   MOVEL     *BLANKS       DATA
     C                   MOVEL     '?'           DATA
     C                   EXCEPT    SYLA01
      *
     C                   ENDSR
      *_____________________________________________________
      * Send all data fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR100         BEGSR
      * Logo
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LAISTX        DATA
     C                   EXCEPT    SYLA01
      * Service Code
     C                   MOVEA     *BLANKS       Æ
     C                   MOVEA     LASERC        Æ
     C                   Z-ADD     18            ÆX
     C                   EXSR      SRCENT
     C                   MOVE      *BLANKS       DATA
     C                   MOVEA     Å             DATA
     C                   EXCEPT    SYLA01
      * AWB number with Origin City
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LAAWBC        LAAWBX            3
     C                   MOVE      LAAWBC        LAAWBY            8
     C                   MOVEL     LAAWBX        DATA
     C     DATA          CAT       '-':0         DATA
     C     DATA          CAT       LAAWBY:0      DATA
     C                   EXCEPT    SYLA01
      * Destination City
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LADCTY        DATA
     C                   EXCEPT    SYLA01
      * Number of Pieces
     C                   MOVE      *BLANKS       DATA
     C                   Z-ADD     LANUMP        LANUMA
     C                   MOVE      LANUMA        CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEA     B4            DATA
     C                   EXCEPT    SYLA01
      * Weight
     C                   MOVE      *BLANKS       DATA
     C                   Z-ADD     LAWGHT        ØN151
     C                   EXSR      SRNE1
     C                   MOVEL     ØAN           DATA
     C                   EXCEPT    SYLA01
      * Handling Info
     C                   MOVE      *BLANKS       DATA
s01  C*                  MOVEL     LAHAIN        DATA
n01  C     LAHCDE        IFEQ      'H'
n01  C                   MOVEL     'HWB:'        DATA
n01  C     DATA          CAT       LAHAIN:1      DATA
n01  c                   else
n01  C                   MOVEL     LAHAIN        DATA
n01  c                   end
     C                   EXCEPT    SYLA01
      * SHP 1
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LASHP1        DATA
     C                   EXCEPT    SYLA01
      * SHP 2
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LASHP2        DATA
     C                   EXCEPT    SYLA01
      * SHP 3
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LASHP3        DATA
     C                   EXCEPT    SYLA01
      * SHP 4
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LASHP4        DATA
     C                   EXCEPT    SYLA01
      * CNE 1
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LACNE1        DATA
     C                   EXCEPT    SYLA01
      * CNE 2
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LACNE2        DATA
     C                   EXCEPT    SYLA01
      * CNE 3
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LACNE3        DATA
     C                   EXCEPT    SYLA01
      * CNE 4
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LACNE4        DATA
     C                   EXCEPT    SYLA01
      * RTN 1
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LARTN1        DATA
     C                   EXCEPT    SYLA01
      * RTN 2
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LARTN2        DATA
     C                   EXCEPT    SYLA01
      * RTN 3
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LARTN3        DATA
     C                   EXCEPT    SYLA01
      * RTN 4
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LARTN4        DATA
     C                   EXCEPT    SYLA01
      * RTN 5
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LARTN5        DATA
     C                   EXCEPT    SYLA01
      * AWB number as bar code
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LAAWBN        DATA
     C     DATA          CAT       '0':0         DATA
     C     DATA          CAT       WCOPA:0       DATA
     C                   EXCEPT    SYLA01
     C     LAHCDE        IFEQ      'H'
      * Number of Pieces (HWB)
     C                   MOVE      *BLANKS       DATA
     C                   Z-ADD     LAHNUM        LANUMA
     C                   MOVE      LANUMA        CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEA     B4            DATA
     C                   EXCEPT    SYLA01
      * Weight
     C                   MOVE      *BLANKS       DATA
     C                   Z-ADD     LAHWGT        ØN151
     C                   EXSR      SRNE1
     C                   MOVEL     ØAN           DATA
     C                   EXCEPT    SYLA01
     C                   ENDIF
      * Origin
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     LAAWBC        LAAWBZ            6
     C                   MOVE      LAAWBZ        LAAWBX
     C                   MOVEL     LAAWBX        DATA
     C                   EXCEPT    SYLA01
      *
     C                   ENDSR
      *_____________________________________________________
      * Leftjustify num.fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRNE1         BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA151D        IFEQ      '0'                                          <-------------I
     C     ØADEC         ANDEQ     'N'                                          <-------------I
     C                   MOVE      ØA151H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA151X        ØA12                                         I
     C                   MOVE      '. '          ØA12                                         I
     C                   MOVE      ØA151D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   LEAVE                                                               II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
      * Leftjustify to a new field
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      * Reset decimal indicator
     C                   MOVE      'N'           ØADEC
      *
     C                   ENDSR
      *_____________________________________________________
      * Center text
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRCENT        BEGSR
     C                   MOVEA     *BLANKS       Å
      * Find rightmost character of input
     C     SRB1          TAG
     C     ÆX            CABEQ     0             CENT99
     C     Æ(ÆX)         IFEQ      *BLANKS
     C                   SUB       1             ÆX
     C                   GOTO      SRB1
     C                   ENDIF
      * Sub from the rightmost counter
      * This is only valid if formtype A0 us used because blank
      * caracters uses less space in grafic mode.
     C                   SELECT
     C     ÆX            WHENLT    19
     C     ÆX            ANDGE     15
     C                   SUB       2             ÆX
     C     ÆX            WHENLT    14
     C     ÆX            ANDGE     10
     C                   SUB       4             ÆX
     C     ÆX            WHENLT    9
     C     ÆX            ANDGE     5
     C                   SUB       6             ÆX
     C     ÆX            WHENLT    4
     C                   SUB       8             ÆX
     C                   ENDSL
      * Calculate left positions of output
     C     18            SUB       ÆX            ÆX
     C     ÆX            IFGE      2
     C     ÆX            DIV       2             ÆX
     C                   ADD       1             ÆX
     C                   ELSE
     C                   Z-ADD     1             ÆX
     C                   ENDIF
      * Move input to output arrary
     C                   MOVEA     Æ             Å(ÆX)
      *
     C     CENT99        ENDSR
      *_____________________________________________________
      * Initialise subroutine, set character set
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C                   MOVE      *ON           *IN50
      *
     C                   Z-ADD     0             ØX                3 0          Index for Ø
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   MOVE      'N'           ØADEC             1            Decimal Indicator
     C                   Z-ADD     0             ÆX                3 0
     C                   Z-ADD     0             X                 1 0
     C                   Z-ADD     0             Y                 1 0
     C                   MOVE      *BLANKS       MSGVAR            7            Message Variabel
     C     OPDKEY        KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C     OPDKEY        CHAIN     DOLAL01                            90
     C                   MOVE      *BLANKS       DATA
      * Send minneinitiering og språkkode
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     MINNE         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     'I8,5'        DATA
     C                   EXCEPT    SYLA01
      *
     C                   ENDSR
      *_____________________________________________________
      * Subroutine to set number of copies
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR200         BEGSR
     C                   MOVEL     *BLANKS       DATA
     C                   Z-ADD     LANUMC        LANUMA
     C                   MOVE      LANUMA        CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEL     'P'           DATA
      *                    MOVEAB4        DATA1
     C                   MOVEL     '1'           DATA1
     C                   EXCEPT    SYLA01
     C                   ENDSR
      *
     OSYLA03P   E            SYLA01
     O                       DATA
**    * FORM AWB, SKAL STARTE MED BLANK LINJE

FK"AWB"
FS"AWB"
V00,10,N,"LOGO"
V01,18,N,"PRODUKTNAVN"
V02,14,N,"AWB NO + ORG. CITY"
V03,03,N,"DEST. CITY"
V04,04,N,"PIECES"
V05,09,N,"WEIGHT"
V06,20,N,"HANDLING INFO"
V07,29,N,"SHP 1"
V08,29,N,"    2"
V09,29,N,"    3"
V10,29,N,"    4"
V11,29,N,"CNE 1"
V12,29,N,"    2"
V13,29,N,"    3"
V14,29,N,"    4"
V15,15,N,"RTG 1"
V16,15,N,"    2"
V17,15,N,"    3"
V18,15,N,"    4"
V19,15,N,"    5"
V20,16,N,"BAR CODE"
V21,03,N,"ORIGIN"
Q1536,24
ZB
R54,0
S2
D7
X1,1,120,700,120
LE12,368,666,4
A12,536,0,2,1,1,N,"Destination"
LE12,504,666,4
LE351,512,4,122
A360,536,0,2,1,1,N,"Total No of Pcs"
A18,844,0,2,1,1,N,"ORIGIN:"
A360,844,0,2,1,1,N,"TTL WEIGHT:"
A18,906,0,2,1,1,N,"HANDLING INFO:"
LE12,632,666,4
A18,656,0,2,1,1,N,"FROM:"
A360,656,0,2,1,1,N,"TO:"
LE12,1008,666,4
A18,1028,0,2,1,1,N,"ROUTING:"
A95,40,0,4,3,3,R,V00
A12,800,0,3,2,2,N,V01
A110,432,0,5,1,1,N,V02
A160,568,0,3,3,3,N,V03
A415,568,0,5,1,1,N,V04
A500,846,0,5,1,1,N,V05
A18,930,0,3,2,2,N,V06
A18,683,0,1,1,2,N,V07
A18,710,0,1,1,2,N,V08
A18,737,0,1,1,2,N,V09
A18,764,0,1,1,2,N,V10
A360,683,0,1,1,2,N,V11
A360,710,0,1,1,2,N,V12
A360,737,0,1,1,2,N,V13
A360,764,0,1,1,2,N,V14
A138,1028,0,2,3,2,N,V15
A138,1073,0,2,3,2,N,V16
A138,1118,0,2,3,2,N,V17
A138,1163,0,2,3,2,N,V18
A138,1208,0,2,3,2,N,V19
B18,140,0,1,5,20,187,B,V20
A160,846,0,3,3,3,N,V21
FE
**    * FORM HWB, SKAL STARTE MED BLANK LINJE

FK"HWB"
FS"HWB"
V00,10,N,"LOGO"
V01,18,N,"PRODUKTNAVN"
V02,14,N,"AWB NO + ORG. CITY"
V03,03,N,"DEST. CITY"
V04,04,N,"PIECES"
V05,09,N,"WEIGHT"
V06,20,N,"HWB NUMMER"
V07,29,N,"SHP 1"
V08,29,N,"    2"
V09,29,N,"    3"
V10,29,N,"    4"
V11,29,N,"CNE 1"
V12,29,N,"    2"
V13,29,N,"    3"
V14,29,N,"    4"
V15,15,N,"RTG 1"
V16,15,N,"    2"
V17,15,N,"    3"
V18,15,N,"    4"
V19,15,N,"    5"
V20,16,N,"BAR CODE"
V21,04,N,"HWB PIECES"
V22,08,N,"HWB WEIGHT"
V23,03,N,"ORIGIN"
Q1536,24
ZB
R54,0
S2
D7
X1,1,120,700,120
LE12,368,666,4
A12,536,0,2,1,1,N,"Destination"
LE12,504,666,4
LE351,512,4,122
A360,536,0,2,1,1,N,"Total No of Pcs"
A18,844,0,2,1,1,N,"ORIGIN:"
A360,844,0,2,1,1,N,"TTL WEIGHT:"
A18,985,0,2,1,1,N,"HWB PIECES:"
A360,985,0,2,1,1,N,"HWB WEIGHT:"
LE12,632,666,4
A18,656,0,2,1,1,N,"FROM:"
A360,656,0,2,1,1,N,"TO:"
LE12,1008,666,4
A18,1028,0,2,1,1,N,"ROUTING:"
A95,40,0,4,3,3,R,V00
A12,800,0,3,2,2,N,V01
A110,432,0,5,1,1,N,V02
A160,568,0,3,3,3,N,V03
A415,568,0,5,1,1,N,V04
A500,846,0,5,1,1,N,V05
A18,930,0,3,2,2,N,V06
A18,683,0,1,1,2,N,V07
A18,710,0,1,1,2,N,V08
A18,737,0,1,1,2,N,V09
A18,764,0,1,1,2,N,V10
A360,683,0,1,1,2,N,V11
A360,710,0,1,1,2,N,V12
A360,737,0,1,1,2,N,V13
A360,764,0,1,1,2,N,V14
A138,1028,0,2,3,2,N,V15
A138,1073,0,2,3,2,N,V16
A138,1118,0,2,3,2,N,V17
A138,1163,0,2,3,2,N,V18
A138,1208,0,2,3,2,N,V19
B18,140,0,1,5,20,187,B,V20
A220,970,0,2,1,2,N,V21
A560,970,0,2,1,2,N,V22
A160,846,0,3,3,3,N,V23
FE
