      *
      ***********************************************************
      *** PROGRAM SKAL HENTE OPPLYSNINGER FOR Å SKRIVE        ***
      ***           MANIFEST                                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 05-26                      ***
      *** LEDIGE INDIKATORER 31 34-78
      ***********************************************************
      *
     FMULTKODT  IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FCUNDC     IF   E             DISK
     FFAXLK     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYSJ27FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D F4              S             75    DIM(12)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D  WSCVP1                 1     40
     D  WSCVP2                41     80
     D  WSCVP3                81    120
     D  WSCVP4               121    160
     D  WSCVP5               161    200
     D  WSCVP6               201    240
     D  WSCVP7               241    280
     D  WSCVP8               281    320
     D  UCVP1                  1    160
     D  UCVP2                161    320
     D  WSCNV                 41     70
     D  WSCAD1                81    110
     D  WSCAD2               121    150
     D  WSCAD3               161    190
     D  WSCATT               201    230
     D                 DS
     D  WSCF01                 1     75
     D  WSCF02                76    150
     D  WSCF03               151    225
     D  WSCF04               226    300
     D  WSCF05               301    375
     D  WSCF06               376    450
     D  WSCF07               451    525
     D  WSCF08               526    600
     D  WSCF09               601    675
     D  WSCF10               676    750
     D  WSCF11               751    825
     D  WSCF12               826    900
     D  F5                     1    900
     D                                     DIM(12)
     D                 DS
     D  WSLSH1                 1      5
     D  WSLSH2                 6     10
     D  WSLSH3                11     15
     D  WSLSH4                16     20
     D  WSLSH5                21     25
     D  LSH                    1     25
     D                                     DIM(5)
     D  LSH2                   1     25
     D                 DS
     D  WSLOH1                 1      5
     D  WSLOH2                 6     10
     D  WSLOH3                11     15
     D  WSLOH4                16     20
     D  WSLOH5                21     25
     D  WSLOH6                26     30
     D  WSLOH7                31     35
     D  WSLOH8                36     40
     D  WSLOH9                41     45
     D  WSLOH0                46     50
     D  LOH                    1     50
     D                                     DIM(10)
     D  LOH2                   1     50
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  FAXNR                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D                 DS
     D  P1                     1    256
     D                                     DIM(256)
     D  UPAR1                  1    256
     D                 DS
     D  P2                     1    256
     D                                     DIM(256)
     D  UPAR2                  1    256
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C  N04
     CANN30              Z-ADD     8             LINNBR
     C  N04
     CANN30              Z-ADD     24            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVEA     '00000000'    *IN(84)
     C                   MOVEA     '00000'       *IN(79)
     C                   SETOFF                                       30
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
      *
      * HENT LASTEHAVN OG LOSSEHAVN
     C     WSAVD         IFGT      0
     C     WSBK          ANDNE     *BLANKS
     C     LSH2          IFEQ      *BLANKS
     C     LOH2          OREQ      *BLANKS
     C                   CALL      'SYSJ27D'
     C                   PARM                    WSAVD
     C                   PARM                    WSBK
     C                   PARM                    WSREIS
     C                   PARM      WSDT          UDTX              6 0
     C                   PARM                    LSH2
     C                   PARM                    LOH2
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C* TESTER DATA PÅ BILDE
     C*
     C                   EXSR      TESTB
     C   30              GOTO      START
     C*
     C     WSFAX         IFEQ      'J'
     C                   EXSR      FAXSR
     C   12              GOTO      START
     C                   END
     C                   Z-ADD     0             UFLAGG            1 0
     C                   MOVE      WSDT          UDT               6
     C                   MOVEL     WSDTÅ         UDT
     C                   MOVE      WSDTD         UDT
     C                   MOVE      WSAVD         XAVD              4
     C                   MOVE      *BLANKS       UPAR1
     C                   MOVEA     XAVD          P1(1)
     C                   MOVEA     WSBK          P1(5)
     C                   MOVEA     WSREIS        P1(11)
     C                   MOVEA     UDT           P1(25)
     C                   MOVEA     LSH2          P1(31)
     C                   MOVEA     LOH2          P1(56)
     C                   MOVEA     KNAVN         P1(106)
     C                   MOVE      WSFAX         P1(136)
     C                   MOVE      *BLANKS       UPAR2
     C                   MOVEA     FAXNRP        P2(1)
     C                   MOVEA     FILEN         P2(22)
     C                   MOVEA     PTIME         P2(34)
     C                   MOVEA     PDATE         P2(42)
     C                   MOVE      UFAXSY        P2(50)
     C                   MOVE      WSFAX         P2(51)
     C                   MOVE      WSCVP         P2(52)
     C                   MOVE      EPOSTJ        P2(53)
     C                   MOVEA     EPOST         P2(54)
     C                   MOVE      '.'           F5(12)
     C                   MOVEA     F5            F4
     C                   CALL      'SYSJ27A'
     C                   PARM                    UPAR1
     C                   PARM                    UPAR2
     C                   PARM                    UCVP1
     C                   PARM                    UCVP2
     C                   PARM                    UPRT33           10
     C                   PARM                    UPT33            10
     C                   PARM                    UPRT34           10
     C                   PARM                    UPT34            10
     C                   PARM                    F4
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      1
     C                   GOTO      START
     C                   END
     C                   MOVE      WSAVD         UAVD
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     TESTB         BEGSR
     C******************************************
     C     KODTVK        CHAIN     KOVR                               99
     C  N99KODTWK        CHAIN     KOWR                               99
     C  N99              Z-ADD     33            YPLNR             3 0
     C  N99KODTPK        CHAIN     KOPR                               99
     C  N99              MOVE      KOPNVN        UPRT33
     C  N99              MOVE      KOPTY         UPT33
     C  N99              Z-ADD     34            YPLNR             3 0
     C  N99KODTPK        CHAIN     KOPR                               99
     C  N99              MOVE      KOPNVN        UPRT34
     C  N99              MOVE      KOPTY         UPT34
     C   99              DO
     C                   SETON                                        30
     C                   MOVE      'SPA0001'     MSGNR
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     24            POSNBR
     C                   END
     C*
     C     WSBK          CHAIN     CDBOAT                             98
     C   98
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPB0104'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     24            POSNBR
     C                   END
     C*
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   EXSR      TESTDT
     C     UFLAGG        COMP      1                                      95
     C   95
     CANN30              DO
     C                   MOVE      'SPD0001'     MSGNR
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   SETON                                        30
     C                   END
     C*
     C     1             DO        5             XN                2 0
     C     LSH(XN)       IFNE      *BLANKS
     C                   GOTO      STTB1
     C                   END
     C                   END
     C  N30              DO
     C                   MOVE      'SPS0110'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   END
     C                   SETON                                        9430
     C                   GOTO      STTB2
     C*
     C     STTB1         TAG
     C     1             DO        5             XN
     C     LSH(XN)       IFNE      *BLANKS
     C                   MOVE      LSH(XN)       ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C   3395            SUB       XN            X2                2 0
     C   33              MOVE      '1'           *IN(X2)
     C   33
     CANN30              DO
     C                   MOVE      'SPS0102'     MSGNR
     C     XN            ADD       13            LINNBR
     C     XN            MULT      8             POSNBR
     C                   ADD       16            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
     C                   END
     C*
     C     STTB2         TAG
     C     1             DO        10            XN                2 0
     C     LOH(XN)       IFNE      *BLANKS
     C                   GOTO      STTB3
     C                   END
     C                   END
     C  N30              DO
     C                   MOVE      'SPS0111'     MSGNR
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   END
     C                   SETON                                        8930
     C                   GOTO      SLTB
     C*
     C     STTB3         TAG
     C     1             DO        10            XN
     C     LOH(XN)       IFNE      *BLANKS
     C                   MOVE      LOH(XN)       ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C   3390            SUB       XN            X2                2 0
     C   33              MOVE      '1'           *IN(X2)
     C   33
     CANN30              DO
     C                   MOVE      'SPS0104'     MSGNR
     C     XN            IFLE      5
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     XN            X2
     C                   ELSE
     C                   Z-ADD     17            LINNBR
     C     XN            SUB       5             X2
     C                   END
     C     X2            MULT      8             POSNBR
     C                   ADD       16            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
     C                   END
      *
     C     KUNDNR        IFEQ      0
     C                   MOVE      *BLANKS       KNAVN
     C                   ELSE
     C     KUKEY         CHAIN     CUNDL01                            79
     C  N30
     CAN 79              MOVE      'SPK0001'     MSGNR
     C  N30
     CAN 79              Z-ADD     19            LINNBR
     C  N30
     CAN 79              Z-ADD     24            POSNBR
     C                   END
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     24
     C     POSNBR        ANDLE     28
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSBK
     C                   Z-ADD     57            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      14                                           1<-B
     C     POSNBR        ANDGE     24
     C     POSNBR        ANDLE     60
     C     POSNBR        SUB       16            XN
     C     XN            DIV(H)    8             XN
     C                   MOVE      LSH(XN)       ULSH              5
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    ULSH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   MOVE      ULSH          LSH(XN)
     C     POSNBR        IFLT      52                                            2<-B
     C                   ADD       8             POSNBR
     C                   ELSE                                                    2
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   END                                                     2<-E
     C                   GOTO      SLSØK
     C                   END                                                    1<-E
     C*
     C     LINNBR        IFGE      16                                           1<-B
     C     LINNBR        ANDLE     17
     C     POSNBR        ANDGE     24
     C     POSNBR        ANDLE     60
     C     POSNBR        SUB       16            XN
     C     XN            DIV(H)    8             XN
     C     LINNBR        IFEQ      17                                            2<-B
     C                   ADD       5             XN
     C                   END                                                     2<-E
     C                   MOVE      LOH(XN)       ULOH              5
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    ULOH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   MOVE      ULOH          LOH(XN)
     C     POSNBR        IFLT      52                                            2<-B
     C                   ADD       8             POSNBR
     C                   ELSE                                                    2
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   END                                                     2<-E
     C                   GOTO      SLSØK
     C                   END                                                    1<-E
      *
     C     LINNBR        IFEQ      19
     C     POSNBR        ANDGE     24
     C     POSNBR        ANDLE     31
     C                   EXSR      SØKKUN
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     37            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   CALL      'SOEKKUN'
     C                   PARM                    KUNDNR
     C                   PARM                    UFIRM
     C                   PARM                    USØKNV           30
     C                   Z-ADD     0             UFLAGG
      *
     C                   ENDSR
      *
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     KUKEY         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C     KODTPK        KLIST
     C                   KFLD                    YPUNI
     C                   KFLD                    WSAVD
     C                   KFLD                    YPLNR
     C     KODTWK        KLIST
     C                   KFLD                    YWUNI
     C                   KFLD                    WSAVD
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    WSAVD
     C*
     C*===============================================
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
     C                   PARM                    UFAXSY            1
     C                   PARM                    UCVP6            50
     C*===============================================
     C*
     C                   MOVE      'P'           YPUNI             1
     C                   MOVE      'W'           YWUNI             1
     C                   MOVE      'V'           YVUNI             1
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      *BLANKS       LSH
     C                   MOVE      *BLANKS       LOH
      *
     C                   READ      FIRM                                   33
     C     UFAXSY        COMP      *BLANKS                                29
     C     UFAXSY        COMP      'F'                                2828
     C                   MOVE      *BLANKS       UCVP1
     C                   MOVE      *BLANKS       UCVP2
     C                   MOVEL     FIFT          WSCVP5
     C                   MOVEL     UCVP6         WSCVP6
      *
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TESTDT        BEGSR
     C***********************************************
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     FAXSR         BEGSR
     C***********************************************
     C                   MOVE      'N'           EPOSTJ            1
     C                   MOVE      *BLANKS       EPOST            70
     C     KUNDNR        IFEQ      0
     C                   MOVE      CBKN          KUNDNR
     C                   END
     C     WSCVP         COMP      'J'                                    27
     C   28              MOVE      *BLANKS       WSCVP5
     C   28              MOVE      *BLANKS       WSCVP6
     C                   EXSR      TESTF
     C*
     C   28              MOVE      *BLANKS       F5
     C     ST2           TAG
     C  N28              EXFMT     BILDE2
     C   28              EXFMT     BILDE3
     C                   SETOFF                                       309998
      *
     C   04              DO
     C                   MOVE      *BLANKS       EPOFAX            1
     C     LO:UP         XLATE     EPOST         USOK
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     C                   PARM                    PARMAX            1
     C                   MOVE      URRN2         RRN              10 0
     C     RRN           CHAIN     CUNDC                              33
     C  N33              DO
     C     EPOFAX        IFNE      'E'
     C                   MOVEL     CFAX          FAXNR
     C                   ELSE
     C                   MOVE      *BLANKS       FAXNR
     C                   END
     C     EPOFAX        IFNE      'F'
     C                   MOVEL     CEMAIL        EPOST
     C                   ELSE
     C                   MOVE      *BLANKS       EPOST
     C                   END
     C                   MOVE      CCOMPN        KUNDNR
     C                   END
     C                   END
     C   12              GOTO      SL2
     C                   EXSR      TESTF
     C     FUNK          CABEQ     'A'           ST2
     C   30              GOTO      ST2
     C   05              EXSR      SNDFAX
     C  N05              GOTO      ST2
     C*
     C     SL2           ENDSR
     C***********************************************
     C     TESTF         BEGSR
     C***********************************************
     C     KUNDNR        IFNE      0
     C     KUKEY         CHAIN(N)  CUNDL01                            99
     C     *IN99         IFEQ      '0'
     C     WSCVP2        IFEQ      *BLANKS
     C                   MOVEL     KNAVN         WSCVP2
     C                   MOVEL     ADR1          WSCVP3
     C   28              MOVEL     ADR2          WSCAD2
     C   28              MOVEL     POSTNR        WSCAD3
     C   28              MOVE      ADR3          WSCAD3
     C                   END
     C     WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C     FAXNR         IFEQ      *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     C                   END
     C                   ELSE
     C                   SETON                                        30
     C   30              MOVE      'SPK0001'     MSGNR
     C   30              GOTO      SLTBFA
     C                   END
     C                   END
      *
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   SETON                                        3098
     C   30              MOVE      'SHU0169'     MSGNR
     C   30              GOTO      SLTBFA
     C                   END
     C*
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
      *
     C     EPOST         IFEQ      *BLANKS
     C                   MOVE      'N'           EPOSTJ
     C                   ELSE
     C                   MOVE      'J'           EPOSTJ
     C                   MOVE      *BLANKS       FAXNR
     C     *IN28         IFEQ      '1'
     C                   CALL      'EPOCVP'
     C                   PARM                    WSCVP1
     C                   PARM                    WSCVP2
     C                   PARM                    WSCVP3
     C                   PARM                    WSCVP4
     C                   PARM                    WSCVP5
     C                   PARM                    WSCVP6
     C                   PARM                    WSCVP7
     C                   PARM                    WSCVP8
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           SLTBFA
     C                   END
     C                   END
     C*
     C     SLTBFA        ENDSR
     C*
     C*
     C***********************************************
     C     SNDFAX        BEGSR
     C***********************************************
     C     EPOST         IFEQ      *BLANKS
     C     KUKEY         CHAIN     CUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD
     C     KUKEY         CHAIN     KUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD2
     C                   END
      *
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
     C                   MOVEL     FAXNR         FAXNRP
     C                   END
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END
     C                   END
     C                   MOVEL     'SYSJ27PF'    FILEN            12
      *
     C                   ENDSR
      *
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
      *
     OKUNDR     E            UPD
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
