BHR   * EGET PROG BASERT PÅ SYFA62I
     F*****************************************************************
BHR   * MERKER I-LINJER,
BHR   * MEN KUN DE SOM ER PRISET, PÅ TUR(IKKE ÅPEN), GODSLISTE OK
BHR   * VED DUPOPPDRAG MÅ OGSÅ OVERORDNET OPPDRAG VÆRE "LISTET"
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Linjer som er spesifikt styrt mot oppdrag slipper gjennom uferdig dup/snu
10XXX * 02 SH  PTF10 tester turstatus
     F*****************************************************************
     FFAK5      UF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR5)
     FHEADF     IF   E           K DISK
     FDISP      IF   E           K DISK
     FTURER     IF   E           K DISK
N01  D                 DS
N01  D  FADOCN                 1     11
N01  D  FADOCN5_11             5     11
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK5
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        YYOPKO
     C     *LIKE         DEFINE    FASK          YYSK
      *
     C     KEYFA5        KLIST
     C                   KFLD                    YYOPKO
     C                   KFLD                    YYSK
      *
     C     KEYHEA        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C     DUPKEY        KLIST
     C                   KFLD                    DUAVD             4 0
     C                   KFLD                    DUOPD             7 0
     C     TURKEY        KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
      *
      * MERKER INTERNDEBITERINGER
      *
     C                   MOVE      ' '           YYOPKO
     C                   MOVE      'I'           YYSK
     C     KEYFA5        SETLL     FAK5
     C     STARTI        TAG
     C     KEYFA5        READE     FAK5                                   33
     C   33              GOTO      SLUTT
      *
      * Straks de har beløp ulik 0 regnes de som klare
     C     FABELN        IFEQ      *ZEROS
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     C                   END
      * OPPDRAG MÅ FINNES.
     C     KEYHEA        CHAIN     HEADF                              44
     C   44              UPDATE    FAKTR5
     C   44              GOTO      STARTI
      * Kombinasjon under betyr sendt via oppdr.interface men ennå ikke pl.inn
      * (Men hvis fadocn5_11 ulik blank så er den direkte styrt til et oppdrag - OK)
     c     Travd2        IFne      *zeros
     c     Tropd2        andeq     *zeros
     c     Tropd0        andeq     *zeros
N01  c     FADOCN5_11    andeq     *blanks
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     c                   end
      * ER DET DUP-OPPDRAG?
      *   OVERORDNET SENDING MÅ VÆRE PÅ AVSLUTTET/"LISTET"(GODSLIS) TUR
     C     TROPD0        IFNE      *ZEROS
     C                   MOVE      TRAVD0        DUAVD
     C                   MOVE      TROPD0        DUOPD
     C     DUPKEY        CHAIN     HEADF                              44
      * Hvis bestilt fra TR.Modul må også overordnet oppdrag være på tur...
     C  N44HEUR          IFEQ      'H'
     C  N44HEPRO         COMP      *ZEROS                                 44
     C  N44DUPKEY        CHAIN     DISP                               44
     C  N44TURKEY        CHAIN     TURER                              44
n02  C  N44TUST          COMP      ' '                                    44
n02  C  N44TUTST1        COMP      ' '                                    44
     C                   end
     C   44              UPDATE    FAKTR5
     C   44              GOTO      STARTI
      * HENT TILBAKE OPPDRAGET SOM I-LINJA HENGER PÅ
     C     KEYHEA        CHAIN     HEADF                              44
     C                   END
      *
      * OPPDRAGET (SOM I-LINJA LIGGER PÅ) MÅ VÆRE PÅ AVSL. TUR.
      * DERSOM I-LINJE TILHØRER HOV.OPPDRAG MÅ DEN VÆRE LISTET.
      * (MENS "DUP-TURENE" (AVD 100/8100)KUN ER AVSLUTTET, IKKE LISTET)
     C     HEPRO         COMP      *ZEROS                                 44
     C  N44KEYHEA        CHAIN     DISP                               44
     C  N44TURKEY        CHAIN     TURER                              44
N02  C  N44TUST          COMP      ' '                                    44
N02  C  N44TROPD0        IFEQ      *ZEROS
N02  C     TUTST1        COMP      ' '                                    44
N02  C                   END
     C   44              UPDATE    FAKTR5
     C   44              GOTO      STARTI
      *
      * KOMET GJENNOM HIT.... ALT OK
      * bruk TURENS periode
     C                   MOVE      TURACC        FACC
     C                   MOVE      TURAAR        FAÅR
     C                   MOVE      TURMND        FAMND
     C                   MOVE      'G'           FAOPKO
     C                   UPDATE    FAKTR5
      * NESTE
     C                   GOTO      STARTI
      *
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
