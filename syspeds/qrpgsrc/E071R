     FENITCMNF  CF   F   80        WORKSTN USROPN
     F                                     INFDS(FEEDBK)
     FENITF     UF A F   80        DISK    USROPN
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1     70
     D                                     DIM(70)                              Input string
     D  ADS                    1     70
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1     80
     D                                     DIM(80)                              Output string
     D  LDATA                  1     80
     D FEEDBK          DS
     D  MAJMIN               401    404
     D  MAJCOD               401    402
     IENITCMNF  AA  09
     I                                  1   80  RDATA
     I                                  2    9  RMSG
     IENITF     AA  09
     I                                  1   80  FDATA
      *//////////////
      * MAIN LOGIC //
      *//////////////
      *_____
      * INIT
      *"""""
     C                   EXSR      SRA
      *____________
      * Open TVI78F
      *""""""""""""
     C                   OPEN      ENITF                                98
     C     *IN98         IFEQ      '1'
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C                   END
      *__________
      * Open ICFF
      *""""""""""
     C                   OPEN      ENITCMNF                             98
     C                   MOVE      '1'           *IN13
     C     MAJCOD        IFGT      '03'
     C     *IN98         OREQ      '1'
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C                   ENDIF
      *_________________________
      * Evoke Signon Transaction
      *"""""""""""""""""""""""""
      * Lage signostreng
     C                   MOVEL(P)  'CESN USE'    LDATA
     C                   CAT       'RID=':0      LDATA
     C                   CAT       Æ003:0        LDATA
     C                   CAT       ',PS=':0      LDATA
     C                   CAT       Æ004:0        LDATA
      *
     C                   EXCEPT    SIGNON
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
      * RECEIVE "Signon Complete"
     C  N98MAJMIN        DOUEQ     '0308'
     C     MAJMIN        OREQ      '0028'
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C                   MOVE      RDATA         Æ011
     C                   SELECT
     C     RMSG          WHENEQ    'FHCE3503'                                   PW MISSING
     C     RMSG          OREQ      'TSS7102E'                                   PW MISSING
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C     RMSG          WHENEQ    'FHCE3532'                                   PW INVALID
     C     RMSG          OREQ      'TSS7101E'                                   PW INVALID
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C     RMSG          WHENEQ    'FHCE3525'                                   PW EXPIRED
     C     RMSG          OREQ      'TSS7110E'                                   PW EXPIRED
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C                   ENDSL
     C                   END
      *_______________________
      * Evoke Appl Transaction
      *"""""""""""""""""""""""
     C                   EXCEPT    STRPGM
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
      * RECEIVE "VELKOMMEN TIL NIT-EXPRESS"
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
      *_____________________
      * LOGON TO NIT-EXPRESS
      *"""""""""""""""""""""
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       LDATA
      * Lage påloggingsstreng
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     'IELOGON'     ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     7             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ' ACCOUNT'    ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     '('           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     Æ002          ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ')'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ' USERID('    ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     Æ003          ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ');'          ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     2             AXH
     C                   EXSR      SRÅ
      *
     C                   EXCEPT    IELOGN
      * RECEIVE "DATA SET"
     C  N98MAJMIN        DOUEQ     '0000'
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C     MAJMIN        IFEQ      '0310'                                       TIMER
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C                   ENDIF
     C                   END
      *_____________
      * SND/RCV FILE
      *"""""""""""""
     C     Æ001          CASEQ     'S'           SNDSR
     C     Æ001          CASEQ     'R'           RCVSR
     C                   END
      *________________________
      * LOGOFF FROM NIT-EXPRESS
      *""""""""""""""""""""""""
     C     Æ001          IFEQ      'S'
     C                   EXCEPT    IELOGF
     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "DATA SET"
     C  N98MAJMIN        DOUEQ     '0008'
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
     C                   END
     C                   END
      *_________________
      * SIGNOFF FROM NIT
      *"""""""""""""""""
     C                   EXCEPT    SIGNOF
     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "SIGNOFF IS COMPLETE"
     C  N98MAJMIN        DOUEQ     '0308'
     C     MAJMIN        OREQ      '0028'
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C                   END
      *____________
      * END PROGRAM
      *""""""""""""
     C     END           TAG
     C                   CLOSE     ENITCMNF                             98
     C                   MOVE      '1'           *INLR
      *////////////
      * SEND MSG //
      *////////////
     C     SNDSR         BEGSR
      *_____
      * SEND
      *"""""
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       LDATA
      * SEND "SEND" COMMAND
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     'SEND'        ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     4             AXH
     C                   EXSR      SRÅ
      *
     C     Æ005          IFNE      *BLANKS
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ' ACCOUNT'    ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     '('           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     Æ005          ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ')'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
     C                   ENDIF
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ';'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
      *
     C                   EXCEPT    IESNDD
     C     MAJCOD        CABGT     '03'          END
     C                   READ      ENITCMNF                             9898
      *__________
      * SEND FILE
      *""""""""""
     C                   READ      ENITF                                  97
     C     *IN97         DOWEQ     '0'
      *
     C                   MOVE      FDATA         LDATA
     C                   EXCEPT    IESNDD
     C                   READ      ENITCMNF                             9898
      *
     C                   READ      ENITF                                  97
     C  N97              END
      *___________________
      * SEND "LAST RECORD"
      *"""""""""""""""""""
     C                   Z-ADD     1             BX                                          II
     C                   MOVE      *BLANKS       LDATA                                       II
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     ';'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
     C                   EXCEPT    IESNDD
      * RECEIVE "DATA SET"
     C                   READ      ENITCMNF                             9898
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_____________
      * Init av felt
      *"""""""""""""
      * Workfields for Array's
     C                   Z-ADD     0             AX                2 0          Index for A
     C                   Z-ADD     1             BX                2 0          Index for B
     C                   Z-ADD     0             AXV               2 0          Leftindx A
     C                   Z-ADD     0             AXH               2 0          Rightindx A
     C                   MOVE      'R'           WDIR              1            Search direction
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   MOVE      'Y'           WCHART            1            Test character
     C                   MOVE      *BLANKS       WCONT             5            Continues
      * Workfields for separation character  '
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
      * Other workfields
      * Input fields
     C     *ENTRY        PLIST
     C                   PARM                    Æ001              1            SND/RCV
     C                   PARM                    Æ002              8            Account SND
     C                   PARM                    Æ003              8            Userid SND
     C                   PARM                    Æ004              8            Password SND
     C                   PARM                    Æ005              8            Account RCV
     C                   PARM                    Æ006              8            Userid RCV
     C                   PARM                    Æ007              1            Status
      *                                                    0=OK
      *                                                    1=OK, pw exp soon
      *                                                    2=NOK, pw exp
      *                                                    3=NOK, Other
     C                   PARM                    Æ011             80            Msgtxt
      * Init workfields
     C                   MOVE      '0'           Æ007
      * DUMMY UPDATE
     C                   MOVE      *BLANKS       DUMMY             1
     C     DUMMY         IFNE      *BLANKS
     C                   EXCEPT    UENITF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_______________
      * Find positions
      *""""""""""""""""
      * Find first non-0 position
     C     WDIR          IFEQ      'L'                                          <-------------I
     C     AXV           DO        AXH           AXV                             <-----------II
     C     A(AXV)        COMP      '0'                                  8383                 II
     C   83              END                                                     <-----------II
      * Find last non-blank position                                     I
     C                   ELSE                                                   <-------------I
      *                                                                  I
     C     SRÅ01         TAG                                                                  I
      *                                                                  I
     C     AXH           IFGT      AXV                                           <-----------II
     C     A(AXH)        IFEQ      *BLANKS                                        <---------III
     C                   SUB       1             AXH                                        III
     C                   GOTO      SRÅ01                                                    III
     C                   END                                                      <---------III
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C                   MOVE      'R'           WDIR
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV                            <-------------I
     C                   MOVE      A(AXV)        WCHAR                                        I
      * If invalid character (' + : ?) then replace with 'Ø'             I
     C     WCHART        IFEQ      'Y'                                           <-----------II
     C     WDIR          IFEQ      'R'                                            <---------III
     C     WCHAR         COMP      WAPSTR                                 83                III
     C  N83WCHAR         COMP      '+'                                    83                III
     C  N83WCHAR         COMP      ':'                                    83                III
     C  N83WCHAR         COMP      '?'                                    83                III
     C   83              EXSR      SRÅA                                                     III
     C                   END                                                      <---------III
     C                   END                                                     <-----------II
      * Move to output string                                            I
     C                   MOVE      WCHAR         B(BX)                                        I
     C                   ADD       1             BX                                           I
      * If arrayindex for output array is over 80 Then add a record      I
     C     BX            IFGT      80                                            <-----------II
     C                   EXCEPT    IELOGN                                                    II
     C                   Z-ADD     1             BX                                          II
     C                   MOVE      *BLANKS       LDATA                                       II
     C                   END                                                     <-----------II
      *                                                                  I
     C                   END                                                    <-------------I
      *
     C                   MOVE      'Y'           WCHART
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for replace control characters                      SRÅA
      *////////////////////////////////////////////////////////////
     C     SRÅA          BEGSR
      * Move '?' before the character
     C                   MOVE      '?'           B(BX)
     C                   ADD       1             BX
     C     BX            IFGT      80                                           <-------------I
     C                   EXCEPT    IELOGN                                                     I
     C                   Z-ADD     1             BX                                           I
     C                   MOVE      *BLANKS       LDATA                                        I
     C                   END                                                    <-------------I
      *
     C                   ENDSR
      *///////////////
      * RECEIVE MSG //
      *///////////////
     C     RCVSR         BEGSR
      *_________________
      * SEND RCV COMMAND
      *"""""""""""""""""
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       LDATA
      * SEND RECEIVE COMMAND
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     'RECEIVE;'    ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     8             AXH
     C                   EXSR      SRÅ
     C                   EXCEPT    IESNDD
     C     MAJCOD        CABGT     '03'          END
     C                   READ      ENITCMNF                             9898
     C                   READ      ENITCMNF                             9898
     C     MAJMIN        IFEQ      '0021'
     C                   READ      ENITCMNF                             9898
     C                   ENDIF
      *________
      * RECEIVE
      *""""""""
     C     MAJMIN        DOWEQ     '0003'
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
      *
     C                   MOVE      RDATA         FDATA
     C                   EXCEPT    AENITF
      *
     C                   READ      ENITCMNF                             9898
     C                   END
     C                   ENDSR
     OENITCMNF  E            IELOGN
     O                                           K8 'SNDDATA '
     O                       LDATA               80
     O          E            IELOGF
     O                                           K8 'SNDDATA '
     O                                              '/*LOGOFF'
     O          E            IESNDD
     O                                           K8 'SNDDATA '
     O                       LDATA               80
     O          E            SIGNON
     O                                           K8 'SNDDATA '
     O                       LDATA               80
     O          E            SIGNOF
     O                                           K8 'SIGNOF  '
     O          E            STRPGM
     O                                           K8 'STRPGM  '
     OENITF     EADD         AENITF
     O                       FDATA               80
     O          E            UENITF
