      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TRAFIKK                   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 90 95-97 ***
      ***********************************************************
      *
     FCDTRAF    UF A E           K DISK
     FCDTRAFLF  IF   E           K DISK
     F                                     RENAME(CDTRAFR:CDTRAFRL)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYSJ17FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KNAVN                  1     30
     D  WSNA                   1     20
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     CDTRKL        SETLL     CDTRAFLF
     C     START1        TAG
     C                   READ      CDTRAFLF                               94
     C  N94FIFIRM        COMP      CTFIRM                             9494
     C  N94
     CAN 27W2KN          IFNE      0
     C     W2KN          COMP      CTKN                               9494
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C     KUND2K        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   WRITE     SUBFIL
     C                   READ      CDTRAFLF                               94
     C  N94FIFIRM        COMP      CTFIRM                             9494
     C  N94
     CAN 27W2KN          IFNE      0
     C     W2KN          COMP      CTKN                               9494
     C                   END
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY AVDELING
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
     C     WSKN          IFEQ      0
     C                   MOVE      UKN           WSKN
     C                   END
     C                   MOVE      *BLANKS       WSNA
     C                   GOTO      START5
     C                   END
     C*** SØK FRA AVDELING
     C   08              GOTO      START6
     C*** AVSLUTT
     C   12              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE AVDELING
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
     C   04              EXSR      SØKKUN
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA BÅTNAVN
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   04              EXSR      SØKKU2
     C   04              GOTO      START6
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKN          COMP      *ZEROS                                 90
     C  N90KUND1K        CHAIN     CUNDL01                            90
     C   90              MOVE      'SPK0115'     MSGNR
     C   90              SETON                                        30
     C     WSKD          COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPK0116'     MSGNR
     C   89              SETON                                        30
     C  N30CDTRK         CHAIN     CDTRAF                             33
     C  N30
     CANN33              DO
     C                   UPDATE    CDTRAFR
     C                   MOVE      'SPK0117'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   30              Z-ADD     22            LINNBR
     C   89              Z-ADD     42            POSNBR
     C   90              Z-ADD     10            POSNBR
     C                   END
     C*
     C     WSFT          IFEQ      *BLANKS
     C  N30              MOVE      'SPF0105'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     48            POSNBR
     C                   SETON                                        8830
     C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     CDTRK         CHAIN     CDTRAF                             33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWCT
     C  N33              UPDATE    CDTRAFR
     C   33              WRITE     CDTRAFR
     C     KUND2K        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   MOVE      CTKN          UKN
     C                   MOVE      CTKD          UKD
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C  N98CTAKT         COMP      'I'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCT
     C   12              GOTO      SLHENT
     C                   END
     C  N27WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCT
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '7'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     CDTRK2        CHAIN     CDTRAF                             33
     C  N33              MOVE      'I'           CTAKT
     C  N33              UPDATE    CDTRAFR
     C                   UPDATE    SUBFIL
     C                   MOVE      '1'           *IN99
     C                   END
     C     WSNR          IFEQ      '8'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     CDTRK2        CHAIN     CDTRAF                             33
     C  N33              MOVE      ' '           CTAKT
     C  N33              UPDATE    CDTRAFR
     C                   UPDATE    SUBFIL
     C                   MOVE      '1'           *IN99
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGCT         BEGSR
     C***********************************************
     C                   EXSR      MOVCTW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTCT         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     CDTRK2        CHAIN     CDTRAF                             33
     C  N33              DELETE    CDTRAFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWCT        BEGSR
     C***********************************************
     C                   MOVE      FIFIRM        CTFIRM
     C                   MOVE      WSKN          CTKN
     C                   MOVE      WSKD          CTKD
     C                   MOVE      WSFT          CTFT
     C                   MOVE      ' '           CTAKT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVCTW        BEGSR
     C***********************************************
     C                   MOVE      CTKN          WSKN
     C                   MOVE      CTKD          WSKD
     C                   MOVE      CTFT          WSFT
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     48            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       CTKN
     C                   MOVE      *BLANKS       CTKD
     C                   MOVE      *BLANKS       WSNA
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *BLANKS       CTFT
     C                   MOVE      *BLANKS       CTAKT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C*                    MOVE *BLANKS   WSKN
     C                   MOVE      *BLANKS       WSKD
     C                   MOVE      *BLANKS       WSNA
     C                   MOVE      *BLANKS       WSFT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKU2        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2KN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CDTRK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C                   KFLD                    WSKD
     C     CDTRK2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    CTKN
     C                   KFLD                    CTKD
     C     CDTRKL        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    W2KN
     C                   KFLD                    W2FT
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     KUND2K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    CTKN
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UKN               8 0
     C                   PARM                    UKD               4
     C* UFLAGG = 1 ---> CALL SØKING FRA ET ANNET PROGRAM
     C     UFLAGG        COMP      1                                      27
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C   27UKN           IFEQ      0
     C                   MOVE      *ZEROS        W2KN
     C                   WRITE     NYVARE
     C                   EXFMT     VAREL2
     C                   ELSE
     C                   MOVE      UKN           W2KN
     C                   END
     C*
     C                   ENDSR
     C*
