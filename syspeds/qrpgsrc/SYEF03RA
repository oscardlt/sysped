      *
      *
      ***********************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV FLYFRAKTPRISER (BRUTTO)  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
     FAIRLD     UF A E           K DISK
     FSYEF03DA  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       YIRCOD            1
     C                   MOVE      *BLANKS       YIRCCP            1
     C                   MOVE      *ZEROS        YIRWEI            5 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIKU         SETLL     AIRLD
     C     START1        TAG
     C     PRIKU         READE(N)  AIRLD                                  94
      *
     C     START2        TAG
     C                   MOVE      *BLANKS       XDLT
      *
     C     *IN94         IFEQ      '0'
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  AIRLD                                  94
     C     *IN94         IFEQ      '0'
     C                   MOVE      AIRCOD        YIRCOD
     C                   MOVE      AIRCCP        YIRCCP            1
     C                   MOVE      AIRWEI        YIRWEI
     C                   ENDIF
     C  N94              ENDDO
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
      *
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C  N94PRISK3        CHAIN(N)  AIRLD                              94
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
     C   12              EXSR      MOVB2
     C   12              GOTO      START3
      * Søk på gyldige koder
     C     *IN04         IFEQ      *ON
     C                   EXSR      SOEK
     C                   GOTO      START5
     C                   ENDIF
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
      * TEST KODE FOR RATETYPE
     C     WSRCOD        IFNE      'Q'
     C     WSRCOD        ANDNE     'N'
     C     WSRCOD        ANDNE     'K'
     C                   MOVE      'SHU0635'     MSGNR
     C                   MOVE      *ON           *IN90
     C                   MOVE      *ON           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   ENDIF
      * TEST VEKTGRUPPE
     C  N30WSRWEI        IFEQ      0
     C                   MOVE      'SHU0160'     MSGNR
     C                   SETON                                        3089
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     11            POSNBR
     C                   END
      *
     C  N30PRISK         CHAIN     AIRLD                              33
     C  N30
     CANN33              MOVE      'SHU0132'     MSGNR
     C  N30
     CANN33              SETON                                        3090
     C   30              GOTO      SLTB1
     C                   END
      *
     C     PRISK         CHAIN     AIRLD                              33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    AIRLDR
     C   33              WRITE     AIRLDR
     C                   MOVE      *BLANKS       XDLT
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB1
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     1             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              3 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     XDLT          ANDNE     '*DELETE'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGPR
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C     XDLT          ANDNE     '*DELETE'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CPYPR
     C   03
     COR 12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C     XDLT          ANDNE     '*DELETE'
     C                   MOVE      ' '           WSNR
     C                   EXSR      DLTPR
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGPR         BEGSR
     C***********************************************
     C                   EXSR      MOVPRW
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     21            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYPR         BEGSR
     C***********************************************
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVPRW
     C                   MOVE      '1'           *IN99
     C                   MOVE      '0'           *IN40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C     *IN04         IFEQ      *ON
     C                   EXSR      SOEK
     C                   GOTO      STCPY
     C                   ENDIF
     C     *IN03         IFEQ      *ON
     C     *IN12         OREQ      *ON
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCPY
     C                   END
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTPR         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISK2        CHAIN     AIRLD                              33
     C  N33              DELETE    AIRLDR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      ULIN          AIRLIN
     C                   MOVE      UPFR          AIRPFR
     C                   MOVE      UPTO          AIRPTO
     C                   MOVE      WSRCOD        AIRCOD
     C                   MOVE      WSRCCP        AIRCCP
     C*                    MOVE WSRTRC    AIRTRC
     C                   MOVE      WSRWEI        AIRWEI
     C                   MOVE      WSRGRN        AIRGRN
     C                   MOVE      WSRAWB        AIRAWB
     C                   MOVE      WSRHAW        AIRHAW
     C                   MOVE      WSRHAM        AIRHAM
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN40
     C                   MOVE      AIRCOD        WSRCOD
     C                   MOVE      AIRCCP        WSRCCP
     C*                    MOVE AIRTRC    WSRTRC
     C                   MOVE      AIRWEI        WSRWEI
     C                   MOVE      AIRGRN        WSRGRN
     C                   MOVE      AIRAWB        WSRAWB
     C                   MOVE      AIRHAW        WSRHAW
     C                   MOVE      AIRHAM        WSRHAM
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       AIRCOD                                     AIRLD)
     C                   MOVE      *BLANKS       AIRCCP
     C*                    MOVE *BLANKS   AIRTRC
     C                   MOVE      *ZEROS        AIRWEI                                     AIRLD)
     C                   MOVE      *ZEROS        AIRGRN                                     AIRLD)
     C                   MOVE      *ZEROS        AIRAWB                                     AIRLD)
     C                   MOVE      *ZEROS        AIRHAW
     C                   MOVE      *ZEROS        AIRHAM
     C                   MOVE      '*DELETE'     XDLT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSRCOD
     C                   MOVE      *BLANKS       WSRCCP
     C*                    MOVE *BLANKS   WSRTRC
     C                   MOVE      *ZEROS        WSRWEI
     C                   MOVE      *ZEROS        WSRGRN
     C                   MOVE      *ZEROS        WSRAWB
     C                   MOVE      *ZEROS        WSRHAW
     C                   MOVE      *ZEROS        WSRHAM
      *
     C                   ENDSR
      *
     C***********************************************
     C     SOEK          BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C  N40LINNBR        IFEQ      22
     C     POSNBR        ANDEQ     5
     C                   MOVEL     'AIRCOD'      WFTYP1           10
     C                   MOVE      *BLANKS       WFKOD1            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    WFTYP1
     C                   PARM                    UUPDAT            1
     C                   PARM                    WFKOD1
     C                   MOVEL     WFKOD1        WSRCOD
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     11            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK2        ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     PRIKU         KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C     PRISK         KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C                   KFLD                    WSRCCP            1
     C                   KFLD                    WSRCOD
     C                   KFLD                    WSRWEI
     C     PRISK2        KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C                   KFLD                    AIRCCP
     C                   KFLD                    AIRCOD
     C                   KFLD                    AIRWEI
     C     PRISK3        KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C                   KFLD                    YIRCCP            1
     C                   KFLD                    YIRCOD
     C                   KFLD                    YIRWEI
      *
     C     *ENTRY        PLIST
     C                   PARM                    ULIN              3 0
     C                   PARM                    UPFR              3
     C                   PARM                    UPTO              3
     C                   PARM                    USDT             30
     C                   PARM                    UFLAGG            1 0
      *
     C                   ENDSR
      *
