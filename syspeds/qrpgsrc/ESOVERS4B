      *********************************************************************
CRTPG+* CRTPGM SYSPED/ESOVERS4B MODULE(*LIBL/ESOVERS4B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10
10019 * 02 YBC PTF10 NYE DATOFELTER
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FTRACKT    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FHEAD11    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEAD11R)
     FTRACKL02  UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSAVD           S              4
     D WSOPD           S              7
     D AVSLUTT         S              1
     D ONEALL          S              3
     D ChgStat         S              4
     D PGM             C                   CONST('*LIBL/CHECKUSR')
     D MELD1           S             80
     D MELD2           S             80
     D MELD3           S             80
     D THISBUTTON      S            100
     D ALLBUTTON       S            100
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D  ParmOS1                1   1024
     D  OSCHG                  1      1
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   EVAL      HEAVD = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   EVAL      HEOPD = c2n2(WSOPD)
     C                   eval      ChgStat  = zhbgetvar('ChgStat')
     C                   eval      ONEALL= zhbgetvar('ONEALL')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
     C     TRACKTK       CHAIN     TRACKT                             33
     C     HEADFK        CHAIN     HEADF                              33
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('ChgStat':ChgStat)                Chgd / Read
      *
     C                   callp     updHtmlVar('WSAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHtmlVar('WSOPD':
     C                             %trim(%editc(HEOPD:'Z')))
     C                   callp     updHtmlVar('HEPRO':
     C                             %trim(%editc(HEPRO:'Z')))
     C                   callp     updHtmlVar('WSETD':
     C                             %trim(%editw(TXETDD:'    .  .  ')))
     C                   callp     updHtmlVar('WSETA':
     C                             %trim(%editw(TXETAD:'    .  .  ')))
     C                   callp     updHtmlVar('WSATD':
     C                             %trim(%editw(TXATDD:'    .  .  ')))
     C                   callp     updHtmlVar('WSATA':
     C                             %trim(%editw(TXATAD:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS01':
N02  C                             %trim(%editw(TXOS01:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS02':
N02  C                             %trim(%editw(TXOS02:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS03':
N02  C                             %trim(%editw(TXOS03:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS04':
N02  C                             %trim(%editw(TXOS04:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS05':
N02  C                             %trim(%editw(TXOS05:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS06':
N02  C                             %trim(%editw(TXOS06:'    .  .  ')))
N02  C                   callp     updHtmlVar('WSOS07':
N02  C                             %trim(%editw(TXOS07:'    .  .  ')))
      * ikke på tur - kvitter ubetinget. (foreløpig ikke åpne for "UNMARK"=blanke )
     C     Hepro         ifeq      *zeros
     c                   move      'Y'           AVSLUTT
     c                   exsr      MarkOne
     c                   else
     c     ONEALL        ifeq      *blanks
     c                   eval      THISButton= '<INPUT TYPE="button" '
     c                             + 'VALUE="ONLY this order" '
     c                             + 'NAME="ONEButton" '
     c                             + 'onClick="MarkOne()">'
     c                   eval      ALLButton= '<INPUT TYPE="button" '
     c                             + 'VALUE="ALL on trip" '
     c                             + 'NAME="ALLButton" '
     c                             + 'onClick="MarkAll()">'
     c                   eval      Meld1='The order is part of a trip.'
     c                   eval      Meld2='To mark date changes as READ push:'
     c                   eval      Meld3='"ONLY this order" or "ALL on trip"'
     c                   else
      * bruker-dialog er ferdig - har valgt enten "One" eller "All"
     c                   move      'Y'           AVSLUTT
     c     ONEALL        ifeq      'One'
     c                   exsr      MarkOne
     c                   else
     c                   exsr      MarkAll
     c                   endif
     c                   endif
     c                   endif
      *
     C                   callp     updHTMLvar('AVSLUTT':AVSLUTT)
     C                   callp     updhtmlvar('Meld1':Meld1)
     C                   callp     updhtmlvar('Meld2':Meld2)
     C                   callp     updhtmlvar('Meld3':Meld3)
     C                   callp     updhtmlvar('THISBUTTON':THISBUTTON)
     C                   callp     updhtmlvar('ALLBUTTON':ALLBUTTON)
      *
     C                   endsr
      *
      *****************************************************
     C     MarkAll       begsr
      *****************************************************
     c     HEPRO         setll     HEAD11
     c     HEPRO         reade     HEAD11                                 33
     c     *in33         doweq     *off
     c                   exsr      MarkOne
     c     HEPRO         reade     HEAD11                                 33
     c                   enddo
     C                   endsr
      *****************************************************
     C     MarkOne       begsr
      *****************************************************
     c     CHDKey        setgt     Trackl02
     c     CHDKeyE       readpe    trackl02                               33
     c     *in33         ifeq      *off
     c                   eval      TTEDEV=WSUSER
     c                   update    trackfr
     c                   endif
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'ESOVERS4B'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     TRACKTK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TXFBNR
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     c     CHDKey        Klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c                   kfld                    TTFBNR
     c                   kfld                    CHDACT
     c                   kfld                    MAXDAT
     c                   move      'CHD'         CHDACT            3
     c                   move      99999999      Maxdat            8 0
     c     CHDKeyE       Klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c                   kfld                    TTFBNR
     c                   kfld                    CHDACT
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      TRACKT
     C                   OPEN      headf
     C                   OPEN      head11
     C                   OPEN      TRACKL02
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('end')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     trackt
     C                   close     headf
     C                   close     head11
     C                   close     TRACKL02
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
