     FE002D     CF   E             WORKSTN
     F                                     SFILE(E002DB:ZRECNO)
     FEQTA902   UF A E           K DISK
     FEQTA9021  IF   E           K DISK
     F                                     RENAME(QTAR:QTAR1)
      *_________________
      * DS for constants
      *"""""""""""""""""
     D                 DS
     D  WTXT01                 1     40    INZ('(No codes match the -
     D                                     subsetting criteria)')
      *//////////////
      *  Main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Move blanks to PF                                  MOVEA /
      *////////////////////////////////////////////////////////////
     C     MOVEA         BEGSR
     C                   MOVE      *BLANKS       QTAGTX
     C                   MOVE      *BLANKS       QTAGD1
     C                   MOVE      *BLANKS       QTAGD2
     C                   MOVE      *BLANKS       QTAGD3
     C                   MOVE      *BLANKS       QTAGD4
     C                   MOVE      *BLANKS       QTAGD5
     C                   MOVE      *BLANKS       QTAGN1
     C                   MOVE      *BLANKS       QTAGN2
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move format to PF                                  MOVEB /
      *////////////////////////////////////////////////////////////
     C     MOVEB         BEGSR
     C                   MOVE      ÅTAGTX        QTAGTX
     C                   MOVE      ÅTAGD1        QTAGD1
     C                   MOVE      ÅTAGD2        QTAGD2
     C                   MOVE      ÅTAGD3        QTAGD3
     C                   MOVE      ÅTAGD4        QTAGD4
     C                   MOVE      ÅTAGD5        QTAGD5
     C                   MOVE      ÅTAGN1        QTAGN1
     C                   MOVE      ÅTAGN2        QTAGN2
     C                   MOVE      'P'           QTAGTY
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move PF to format                                  MOVEC /
      *////////////////////////////////////////////////////////////
     C     MOVEC         BEGSR
     C                   MOVE      QTAGTX        ÅTAGTX
     C                   MOVE      QTAGD1        ÅTAGD1
     C                   MOVE      QTAGD2        ÅTAGD2
     C                   MOVE      QTAGD3        ÅTAGD3
     C                   MOVE      QTAGD4        ÅTAGD4
     C                   MOVE      QTAGD5        ÅTAGD5
     C                   MOVE      QTAGN1        ÅTAGN1
     C                   MOVE      QTAGN2        ÅTAGN2
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move blanks to format                              MOVED /
      *////////////////////////////////////////////////////////////
     C     MOVED         BEGSR
     C                   MOVE      *BLANKS       ÅTAGTX
     C                   MOVE      *BLANKS       ÅTAGD1
     C                   MOVE      *BLANKS       ÅTAGD2
     C                   MOVE      *BLANKS       ÅTAGD3
     C                   MOVE      *BLANKS       ÅTAGD4
     C                   MOVE      *BLANKS       ÅTAGD5
     C                   MOVE      *BLANKS       ÅTAGN1
     C                   MOVE      *BLANKS       ÅTAGN2
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  INZ                                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    QTAGNO        WTAGNO
     C     *LIKE         DEFINE    QTAGTX        WTAGTX
      *_____________
      * Define key's
      *"""""""""""""
     C     KEYA          KLIST                                                  QTAGF
     C                   KFLD                    WTAGNO
     C     KEYB          KLIST                                                  QTAGL1
     C                   KFLD                    WTAGTX
      *________________________
      * Input/output parameters
      *""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    QTAGNO                         Output
      *________________
      * Init workfields
      *""""""""""""""""
     C                   MOVE      *BLANKS       WTAGNO
     C                   MOVE      '0'           *IN03
     C                   MOVE      '0'           *IN12
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Calculate formats                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Select tag
      *"""""""""""
     C     SRB0          TAG
      *
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
      *
     C     SRB1          TAG
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     E002DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C     XTAGNO        IFEQ      '*ALL'
     C                   MOVE      *BLANKS       WTAGNO
     C                   ELSE
     C                   MOVE      XTAGNO        WTAGNO
     C                   END
     C     XTAGTX        IFEQ      '*ALL'
     C                   MOVE      *BLANKS       WTAGTX
     C                   MOVE      '0'           *IN82                          SORT BY TAG
     C                   ELSE
     C                   MOVE      '1'           *IN82                          SORT BY ALPHA
     C                   MOVEL     XTAGTX        WTAGTX
     C                   END
      *_____________
      * Fill Subfile
      *"""""""""""""
     C  N82KEYA          SETLL     EQTA902
     C   82KEYB          SETLL     EQTA9021
     C  N82              READ(N)   EQTA902                                94
     C   82              READ      EQTA9021                               94
     C     *IN94         IFEQ      '1'
     C                   MOVE      *BLANKS       QTAGNO
     C                   MOVEL     WTXT01        QTAGTX
     C                   MOVE      '1'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E002DB
     C                   GOTO      SRB3
     C                   ENDIF
      *
     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO
      *
     C  N94              DO        12
     C                   MOVE      '0'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E002DB
     C  N82              READ(N)   EQTA902                                94
     C   82              READ      EQTA9021                               94
     C  N94              END
      *______
      * Enter
      *""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E002DØ
     C                   WRITE     E002DD
     C                   EXFMT     E002DC
      *
      *________
      * F3=Exit
      *""""""""
     C     *IN03         CABEQ     '1'           SRB9
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRB0
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *___________
      * F5=Refresh
      *"""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *__________
      * F6=Create
      *""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBF
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_________
      * Option ?
      *"""""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN05
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     E002DB                                 52
     C     *IN52         IFEQ      '0'
     C     ZOP           CABEQ     '1'           SRB9                           Select
     C     ZOP           CASEQ     '2'           SRBB                           Change
     C     ZOP           CASEQ     '3'           SRBC                           Copy
     C     ZOP           CASEQ     '4'           SRBD                           Delete
     C     ZOP           CASEQ     '5'           SRBE                           Display
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9                           End
     C     *IN05         CABEQ     '1'           SRB1                           Refresh
     C     *IN05         CABEQ     '0'           SRB3                           Subfile
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Select tag                                          SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C     SRBA1         TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E002DØ
     C                   EXFMT     E002DA
     C                   MOVEA     '00'          *IN(30)
      *________
      * F3=Exit
      *""""""""
     C     *IN03         CABEQ     '1'           SRBA9
      *__________
      * F4=Prompt
      *""""""""""
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRBA9
      *______
      * Enter
      *""""""
     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Change                                              SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Move fields
      *""""""""""""
     C                   MOVE      QTAGNO        WTAGNO
     C     KEYA          CHAIN(N)  EQTA902                            53
     C                   MOVE      QTAGNO        ÅTAGNO
     C                   MOVE      '1'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
      *
     C     SRBB1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E002DØ
     C                   EXFMT     E002DE
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *___________
      * Test input
      *"""""""""""
     C   30              GOTO      SRBB1
      *__________
      * Update PF
      *""""""""""
     C     KEYA          CHAIN     EQTA902                            53
     C                   EXSR      MOVEB
     C                   UPDATE    QTAR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E002DB
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Copy                                                SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *____________
      * Copy fields
      *""""""""""""
     C                   MOVE      QTAGNO        WTAGNO
     C     KEYA          CHAIN(N)  EQTA902                            53
     C                   MOVE      QTAGNO        ÅTAGNO
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   EXSR      MOVEA
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E002DØ
     C                   EXFMT     E002DE
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅTAGNO        QTAGNO
     C                   EXSR      MOVEB
     C                   WRITE     QTAR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     E002DB
      *
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Delete                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Delete record
      *""""""""""""""
     C                   MOVE      QTAGNO        WTAGNO
     C     KEYA          CHAIN     EQTA902                            53
     C                   DELETE    QTAR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E002DB
     C                   MOVE      '1'           *IN05
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Display                                             SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      QTAGNO        WTAGNO
     C     KEYA          CHAIN(N)  EQTA902                            53
     C                   MOVE      QTAGNO        ÅTAGNO
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E002DØ
     C                   EXFMT     E002DE
     C                   MOVEA     '000'         *IN(30)
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Create                                              SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEA
     C                   EXSR      MOVED
      *
     C     *IN03         DOWEQ     '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVE      *BLANKS       ÅTAGNO
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E002DØ
     C                   EXFMT     E002DE
     C                   MOVEA     '0000'        *IN(30)
      *______________
      * End or Cancel
      *""""""""""""""
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅTAGNO        QTAGNO
     C                   EXSR      MOVEB
     C                   WRITE     QTAR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     E002DB
      *
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDSR
