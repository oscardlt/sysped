/* TDVIS MÅ VÆRE INSTALLERT           */

             PGM
             /*_____________________*/
             /* Deklarere Variabler */
             /*"""""""""""""""""""""*/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OK) TYPE(*LGL) LEN(1)
             DCL        VAR(&SSN) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&NETW) TYPE(*CHAR) LEN(1) VALUE('7')
             DCL        VAR(&IIDS) TYPE(*CHAR) LEN(35)
             DCL        VAR(&IIDR) TYPE(*CHAR) LEN(35)
             DCL        VAR(&STRTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ENDTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&QTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0)

             ADDLIBLE   LIB(SMALIB)
             MONMSG     MSGID(CPF2100)
             ADDLIBLE   LIB(SMALIBF)
             MONMSG     MSGID(CPF2100)
             /*__________________________________________________*/
             /* Denne Jobben kan bli startet *IMMED eller        */
             /* venter til et klokkeslett som finnes i firmans   */
             /* dataarea.                                        */
             /*""""""""""""""""""""""""""""""""""""""""""""""""""*/
             RTVDTAARA  DTAARA(EDIDTA (154 6)) RTNVAR(&STRTIME)
             IF         COND(&STRTIME *EQ *IMMED) THEN(GOTO +
                          CMDLBL(START))

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&QTIME)
             IF         COND(&STRTIME *LE &QTIME) THEN(GOTO +
                          CMDLBL(START))

             DLYJOB     RSMTIME(&STRTIME)
 /*__________________________*/
 /* Skal polling avsluttes ? */
 /*""""""""""""""""""""""""""*/
 START:      RTVDTAARA  DTAARA(EDIDTA (160 6)) RTNVAR(&ENDTIME)
             IF         COND(&ENDTIME *NE *NONE) THEN(DO)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&QTIME)
             IF         COND(&ENDTIME *LT &QTIME) THEN(GOTO +
                          CMDLBL(END))
             ENDDO
             /*_______________*/
             /* Sjekk om noen poster skal oppdateres til status 'C' */
             /*"""""""""""""""*/
             CALL       PGM(EDI12R)
             /*_______________*/
             /* Mottak av MSG */
             /*"""""""""""""""*/
/* TDV IS LEGGER DATA RETT I EDI79F                            */
             /*_______________*/
             /* Finn status A */
             /*"""""""""""""""*/
 START2:     CALL       PGM(EDI07R) PARM(&OK &LIB &FIL &MBR &SSN +
                          &NETW &IIDS &IIDR)
             IF         COND(&OK *EQ '1') THEN(DO)
             DLYJOB     DLY(30)
             GOTO       CMDLBL(START)
             ENDDO
             /*______*/
             /* Send */
             /*""""""*/
             CALL       PGM(EDI11R) PARM(&SSN &NBRCURRCD)

             GOTO       CMDLBL(START2)

END:         ENDPGM
