      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA ELOGI01 OG VISER LISTE
      *
CRTPG+*  CRTPGM SYSPED/ELOGI03 MODULE(*LIBL/ELOGI03
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FLLGRPK05  IF   E           K DISK    USROPN
     FLLAGJL01  IF   E           K DISK    USROPN
     FLLAGJD01  IF   E           K DISK    USROPN
     FLLMRFL01  IF   E           K DISK    USROPN
     FLLMRFL02  IF   E           K DISK    USROPN
     F                                     RENAME(LLMRFR:LLMRFR02)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *--------------------------------------------------------------------
     D OddEven         s             10
      * Variables received from the input string
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSPNR           S              8
     D BSPNR           S              8  0
     D WSMN            S              7
     D BSMN            S              7  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
      * and converted to numeric
     D zeros           s             15a   inz('000000000000000')
     D NIER            s             35A   INZ('9999999999999999999999999999999-
     D                                     9999')
     D l1              s              3s 0
     D l2              s              3s 0
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C  N50              UPDATE    BRIDFR
      * Set/READ output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('tablestr')
      * hent/skriv uttaks linjer
     C                   exsr      HentUttak
      * tableend
     C                   callp     wrtsection('tableend')
     C                   callp     wrtsection('endhtml')
      *
     C                   callp     wrtsection('*fini')
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('KNAVN')
      *
     C                   EVAL      WSPNR   = zhbgetvar('WSPNR')
     C                   EVAL      BSPNR   = c2n2(WSPNR)
     C                   EVAL      WSMN    = zhbgetvar('WSMN')
     C                   EVAL      BSMN    = c2n2(WSMN)
      *
     C                   endsr
      *
      ******************************************************
     C     SetTop        begsr
      ******************************************************
     C                   callp     updHTMLvar('ROWHEAD':RowHead:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updhtmlvar('KNAVN':KNAVN)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updhtmlvar('WSPNR':WSPNR)
     C                   callp     updhtmlvar('WSMN':WSMN)
     C                   callp     updhtmlvar('MRGN':MRGN)
     C                   callp     updhtmlvar('MRTRNR':MRTRNR)
     C                   callp     updhtmlvar('VAANR':VAANR)
     C                   callp     updHTMLvar('VADATO':
     C                             %trim(%editw(VADATO:'    .  .  ')))
     C                   callp     updhtmlvar('VAOANT':
     C                             %trim(%editc(VAOANT:'Z')))
     C                   callp     updhtmlvar('VASALD':
     C                             %trim(%editc(VASALD:'Z')))
     C                   callp     updhtmlvar('MRLNA':MRLNA)
      *
     C                   endsr
      *
      *=====================================================================******
      * HENT VARER
      *=====================================================================******
     C     HENTUTTAK     BEGSR
      *
     C     LLAGJD01K     SETLL     LLAGJD01
     C     LLAGJD01K     READE     LLAGJD01                               33
      *
     C                   DOW       *IN33 = *OFF
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C     LLAGJD01K     READE     LLAGJD01                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updhtmlvar('WSPNR':WSPNR)
     C                   callp     updhtmlvar('WSMN':WSMN)
     C                   callp     updhtmlvar('VDVN':
     C                             %trim(%editc(VDVN:'Z')))
     C                   callp     updhtmlvar('VDMNL':
     C                             %trim(%editc(VDMNL:'Z')))
     C                   callp     updhtmlvar('VDVNL':
     C                             %trim(%editc(VDVNL:'Z')))
     C                   callp     updhtmlvar('VDLANT':
     C                             %trim(%editc(VDLANT:'Z')))
     C                   callp     updhtmlvar('VDLDAT':
     C                             %trim(%editw(VDLDAT:'    .  .  ')))
     C                   callp     updhtmlvar('VDAVD':
     C                             %trim(%editc(VDAVD:'Z')))
     C                   callp     updhtmlvar('VDOPD':
     C                             %trim(%editc(VDOPD:'Z')))
     C                   callp     updhtmlvar('VDTEXT':VDTEXT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member/read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
     C*    XSPRÅK        ifeq      'EN'
     C*                  eval      IfsMultIndicators = gethtmlifsmult(
     C*                            '/syspeddev/html/ELOGI03EN.html':
     C*                            '/CGITAG/')
     C*                  else
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/ELOGI03.html':
     C                             '/CGITAG/')
     C*                  endif
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   open      LLGRPK05                             50
     C                   open      LLAGJL01                             50
     C                   open      LLAGJD01                             50
     C                   open      LLMRFL01                             50
     C                   open      LLMRFL02                             50
     C                   open      BRPARF                               50
     C                   open      CUNDL01                              50
      *
     C     KunKey        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     LLAGJL01K     klist
     C                   kfld                    GKGRP
     C                   kfld                    GKPNR
     C                   kfld                    BSMN
     C     LLAGJD01K     klist
     C                   kfld                    VAGRUP
     C                   kfld                    VAKJED
     C                   kfld                    BSMN
     C     LLMRFK        klist
     C                   kfld                    VAGRUP
     C                   kfld                    VAKJED
     C                   kfld                    VAMRNR
      *
     C                   MOVE      '<CITE>'      XCITESTR          6
     C                   MOVE      '</CITE>'     XCITEEND          7
      *
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C     KunKey        chain     cundl01                            33
     C     BSPNR         CHAIN     LLGRPK05                           33
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C     LLMRFK        CHAIN     LLMRFL01                           33
     C   33LLMRFK        CHAIN     LLMRFL02                           33
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   close     BRIDF
     C                   close     LLGRPK05
     C                   close     LLAGJL01
     C                   close     LLAGJD01
     C                   close     LLMRFL01
     C                   close     LLMRFL02
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
