      *********************************************************************
      * Dette program benyttes for å kalkulere lastemeter via spes.regler
      * kalles først ved on leave (lxBx)H  - hent lastyem.
      * MERK - litt rar ERR-håndtering - bygd for å kunne takle flere errors på en linje
      *        samt returnere retur-values / infomelding
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE23RV2 MODULE(*LIBL/TJGE23RV2 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
11XXX * 01 JOV PTF11 UNnr
11XXX * 02 JOV PTF11 UNnr (hent emb-gruppe) kan UTGÅ (se TJINQUNNR)
12060 * 03 SH  PTF12 test volom mottak
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
s02  F*DRUNNRF  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      *
     D Len             S              4S 0
     D Brd             S              4S 0
     D Ant             S              7S 0
     D Pakn            S              7
     D LM              S              4S 2
      *  Prototype for 'SYOXLM'
     d syoxlm          pr                  extpgm('SYOXLM')
     d fvlen_                              like(len)
     d fvbrd_                              like(brd)
     d fvant_                              like(ant)
     d fvpakn_                             like(pakn)
     d fvlm_                               like(lm)
     D SumBrd          S             15S 0
      * Varible for
     D WSUSER          S             10
     D JSONstring      S          32000
     D NyErr           S            150                                         en errormelding
     D NyInf           S            150                                         en ny value i svar
     D NyVal           S            150                                         en ny value i svar
     D Error           S           5000                                         flere feilmeldinger
     D InfoMsg         S           5000                                         flere feilmeldinger
     D Values          S           5000                                         Flere nye verdier
     D WA              S             15
     D AVD             S              4S 0
     D OPDTYP          S              2
     D FMMRK1          S             35
     D FVANT           S              7P 0
     D FVPAKN          S              7
     D FVVT            S             35
     D FVVKT           S              9P 0
     D FVVOL           S              7P 3
n03  D FVVOLW          S             10P 3
     D FVLM            S              4P 2
     D FVLM2           S              4P 2
     D FVLEN           S              4P 0
     D FVBRD           S              4P 0
     D FVHOY           S              4P 0
S02   *
S02  D*FFUNNR          S              4
S02  D*FFEMBG          S              3
S02  D*FFKLAS          S              4
S02  D*FFSEDD          S             10
S02  D*FFTRES          S             10
S02  D*FFFAKT          S              4S 0
S02  D*FFANTK          S              5S 0
S02  D*FFANTE          S              7S 0
S02  D*FFENH           S              3
S02  D*FFPOEN          S              5S 0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST                                         50 ON = Ukjent user

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVD:'Z'))+'¬, '
        +'¬opdtyp¬ : ¬'+%trim(OPDTYP)+'¬, '
        +'¬infoMsg¬ : ¬'+%trim(InfoMsg)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"awblineValidate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * RETURNER KUN DE VERDIER SOM ER ENDRET
     c                   if        Values = *blanks
     c                   eval      JSONstring = *blanks
     c                   else
     C                   eval      JSONstring = '{' + %trim(values) + '}'
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     c                   endif
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
S02  C* N50              CLOSE     ADRUNNRF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      AVD        = c2n2(WA)
     C                   EVAL      OPDTYP  = zhbgetvar('OPDTYP')
     C                   EVAL      FMMRK1  = zhbgetvar('FMMRK1')
     C                   EVAL      WA      = zhbgetvar('FVANT')
     C                   EVAL      FVANT   = c2n2(WA)
     C                   EVAL      FVPAKN     = zhbgetvar('FVPAKN')
     C                   EVAL      FVVT       = zhbgetvar('FVVT')
     C                   EVAL      WA      = zhbgetvar('FVVKT')
     C                   EVAL      FVVKT   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVVOL')
N03  C                   eval      FVVOLW  = c2n2(WA)
N03  C     fvvolw        ifgt      9999,999
N03  c                   z-add     9999,999      fvvolw
N03  c                   end
s03  C*                  EVAL      FVVOL   = c2n2(WA)
n03  C                   EVAL      FVVOL   = FVVOLW
     C                   EVAL      WA      = zhbgetvar('FVLM')
     C                   EVAL      FVLM    = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLM2')
     C                   EVAL      FVLM2   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLEN')
     C                   EVAL      FVLEN   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVBRD')
     C                   EVAL      FVBRD   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVHOY')
     C                   EVAL      FVHOY   = c2n2(WA)
S02  C*                  EVAL      FFUNNR  = zhbgetvar('FFUNNR')
S02  C*                  EVAL      FFEMBG  = zhbgetvar('FFEMBG')
S02  C*                  EVAL      FFKLAS  = zhbgetvar('FFKLAS')
S02  C*                  EVAL      FFSEDD  = zhbgetvar('FFSEDD')
S02  C*                  EVAL      FFTRES  = zhbgetvar('FFTRES')
S02  C*                  EVAL      WA      = zhbgetvar('FFFAKT')
S02  C*                  EVAL      FFFAKT  = c2n2(WA)
S02  C*                  EVAL      WA      = zhbgetvar('FFANTK')
S02  C*                  EVAL      FFANTK  = c2n2(WA)
S02  C*                  EVAL      WA      = zhbgetvar('FFANTE')
S02  C*                  EVAL      FFANTE  = c2n2(WA)
S02  C*                  EVAL      FFENH   = zhbgetvar('FFENH')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
S02  C*    UnNrKey       KLIST
S02  C*                  KFLD                    FFUNNR
S02  C*                  KFLD                    FFEMBG
      *
     C   50              eval      Error = 'Invalid userID'
S02  C* N50              OPEN      ADRUNNRF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      *
      * Input av - ant / vareslag / vekt valideres i eSpedsg
     C                   eval      Error = *blanks
     C                   eval      InfoMsg = *blanks
      *
      * benytter free for enklere strengbehandling
      /free
         //ant/vekt
         if FVANT=0 or FVVKT=0;
           NyErr = 'Ant stykk/kolli samt vekt er obligatorisk';
           exsr AddErr;
         endif;

       //BRING - L X B X H ER OBLIGATORISK
       //Beregning av LM ut fra Formel er ren Bring / olgeexpress-spesial
        if OPDTYP='OX' or OPDTYP='LO' or OPDTYP='OB';
          if FVLEN=0 or FVBRD=0 or FVHOY=0;
              NyErr = 'L x B x H er obligatorisk på oppdragstype '+OPDTYP;
              exsr AddErr;
          else;
          // LxBxH ER angitt - hvis LM IKKE er angitt bereg den (egentlig ren Bring-spesial)
            if FVLM =0;
              len = fvlen;
              brd = fvbrd;
              ant = fvant;
              pakn= fvpakn;
              lm  = fvlm;
              SYOXLM ( len : brd : ant  : pakn : lm );
            // FVLM var 0 - nå satt  - gi melding/returner verdi
              fvlm = lm;
              if  FVLM <> 0;                               // hvis nå LM er definert
                NyInf = 'Lastemeter(Fa) er autoberegnet basert på LxBxH';
                exsr AddInfo;
                NyVal='¬fvlm¬ : ¬'+%trim(%editc(FVLM:'4'))+'¬';
                exsr AddVal;
                //også LM2 blank før??
                if  FVLM2 = 0;
                  FVLM2 = FVLM;
                  NyVal='¬fvlm2¬ : ¬'+%trim(%editc(FVLM2:'4'))+'¬';
                  exsr AddVal;
                endif;
              endif;
             endif; // end FVLM=0
          endif;   // end lbh ulik 0
        endif;    // end OX-oppdr-typer
      /end-free
      *
S02  C*                  IF        FFUNNR <> *blanks
S02  c*                  move      *blanks       NyErr
S02   * Sjekk kombinasjon UnNr/Emb.grp (om det på gitt UNnr fins kun en emb-grp, autovelg )
S02  C*    UnNrKey       chain     ADRUNNRF                           33
S02  C*    *in33         ifeq      '1'
S02  C*    FFUNNR        chain     ADRUNNRF                           33
S02  C*    *in33         ifeq      '1'
S02  C*                  eval      NyErr = 'Ugyldig UnNo'
S02  c*                  exsr      AddErr
S02  c*                  goto      UtUnnr
S02  c*                  endif
S02   * kommer hit kun når gitt UNNR+EMBG IKKE finnes men det finnes minst en på gitt UNNR
S02  C*    FFUNNR        reade     ADRUNNRF                               33
S02  C*    *in33         ifeq      *on
S02  C*                  eval      FFEMBG = ADEMBG
S02  C*                  else
S02  C*                  eval      NyErr = 'Ugyldig Emb.Grp for dette UnNo'
S02  c*                  exsr      AddErr
S02  c*                  goto      UtUnnr
S02  c*                  endif
S02  c*                  endif
S02   * OK UNnr/emb  - returner div verdier
S02  C*                  EVAL      FFKLAS=ADKLAS
S02  C*                  EVAL      FFSEDD=ADSEDD
S02  C*                  EVAL      FFTRES=ADTRES
S02  C*                  EVAL      FFFAKT=ADFAKT
S02   *
S02  c*    UtUnnr        tag
S02  C*                  if        FFANTK = 0
S02  C*                  eval      NyErr = 'Antall kolli (som inneholder ADR) '
S02  c*                            +'kan ikke være 0'
S02  c*                  exsr      AddErr
S02  c*                  endif
S02  C*                  if        FFANTE = 0
S02  C*                  eval      NyErr = 'Antall enheter (LTR/KG/GR) '
S02  c*                            +'kan ikke være 0'
S02  c*                  exsr      AddErr
S02  c*                  endif
S02  C*                  if        FFENH<>'KG' and FFENH<>'LTR' and FFENH<>'GR'
S02  C*                  eval      NyErr = 'Enhetskode må være KG, LTR eller GR'
S02  c*                  exsr      AddErr
S02  c*                  endif
S02  C* Returverdi for Poeng ??
S02  c*                  if        NyErr = *blanks
S02  C*                  EVAL      FFPOEN=FFFAKT * FFANTE
S02  c* clientside??     eval      NYval = ......
S02  c*                  exsr      AddVAL
S02  c*                  endif
S02  C*                  ENDIF
     C                   ENDSR
      *******************************************************
     C     AddErr        BEGSR
      *******************************************************
     c                   if        Error = *blanks
     c                   eval      Error = NyErr
     C                   else
     c                   eval      Error = %trim(Error) +'§'+NyErr
     C                   endif
     C                   ENDSR
      *
      *******************************************************
     C     AddInfo       BEGSR
      *******************************************************
     c                   if        InfoMsg = *blanks
     c                   eval      InfoMsg = NyInf
     C                   else
     c                   eval      InfoMsg = %trim(InfoMsg) +'§'+NyInf
     C                   endif
     C                   ENDSR
      *
      *******************************************************
     C     AddVal        BEGSR
      *******************************************************
     c                   if        Values = *blanks
     c                   eval      Values = NyVal
     C                   else
     c                   eval      Values = %trim(Values) +','+NyVal
     C                   endif
     C                   ENDSR
