     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '5')
     A            BVMN          17   H
     A            BVMN15        15   O  6  4
     A            BOPRO          8  0O  6 20EDTCDE(4)
     A            XDT1           6  0O  6 29EDTCDE(4)
     A            BVAVD          4  0O  6 36EDTCDE(4)
     A            BVOPD          7  0O  6 41EDTCDE(4)
     A            BOSG           3   O  6 49
     A            BOKNR          8  0O  6 53EDTCDE(4)
     A            BOTR           4   O  6 62
     A            BOLB           5   O  6 67
     A            BOOB           5   O  6 73
     A            BOUKE          2  0O  6 79EDTCDE(4)
     A            BOST           1   O  7  7DSPATR(UL)
     A            XNAA          20   O  7 12DSPATR(UL)
     A            XNAS          20   O  7 33DSPATR(UL)
     A            BOLSHN        20   O  7 54DSPATR(UL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94 95 96                            SFLEND
     A  91N93N94
     AO 91N93N95
     AO 91N93N96                            ROLLUP(25)
     A  91N93N94
     AO 91N93N95
     AO 91N93N96                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ53'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3673 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(26 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(22 DIV3676 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(80 DIV3674 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  5MSGCON(74 DIV3675 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2MSGCON(27 DIV3659 SYMSGF)
     A            W2MN          17A  I  1 30DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  1 49MSGCON(9 DIV3660 SYMSGF)
     A            W2AVD          4  0I  1 59DSPATR(UL)
     A                                  1 64MSGCON(8 DIV3661 SYMSGF)
     A            W2OPD          7  0I  1 73DSPATR(UL)
     A                                  2  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYSJ53'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3673 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(26 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(22 DIV3676 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(80 DIV3674 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  5MSGCON(74 DIV3675 SYMSGF)
     A                                      COLOR(WHT)
