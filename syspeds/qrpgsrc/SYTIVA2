********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 BEHOLDE AVGIFTER I 3 ÅR PGA. OMBEREGNING
********************************************************************
      *
      * LES NYE VALUTAREGISTER OG SAMMELIGNE KURS. OPPDATERE DRIFTS-
      * FIL HVIS KURS ER FORSKJELLIGE ELLER IKKE FINNES FRA FØR.
      *
     H DATEDIT(*DMY)
     FKODTVAL2  UF A E           K DISK
     FKODTVANY  IP   E             DISK
     F                                     RENAME(KOVAR:KOVARNY)
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅ                   1      4  0
     D  XDTM                   5      6  0
     D  XDTD                   7      8  0
     IKOVARNY
     I              KVASTA                      XVASTA
     I              KVAUNI                      XVAUNI
     I              KVAKOD                      XVAKOD
     I              KVAKRS                      XVAKRS
     I              KVAOMR                      XVAOMR
     I              KVADT                       XVADT
     I              KVAGKR                      XVAGKR
     I              KVAXXX                      XVAXXX
     I              KVAGV                       XVAGV
      *
     C     *IN99         IFEQ      *OFF
     C                   Z-ADD     1             SIDE              4 0
     C                   TIME                    TID               6 0
     C                   EXCEPT    PHEAD                                         1 PAGE
     C                   SETON                                        99
     C                   END
     C   OF              ADD       1             SIDE              4 0
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF         OVERFLOW
      *
     C     KEY           KLIST
     C                   KFLD                    XVAKOD
     C                   KFLD                    XVADT
      *
     C     KEY           CHAIN     KODTVAL2                           33
     C     *IN33         IFEQ      '0'
     C     KVAKRS        IFNE      XVAKRS
     C                   MOVE      XVAKRS        KVAKRS
     C                   EXCEPT    DET
     C                   END
     C                   UPDATE    KOVAR
     C                   ELSE
     C     KEY           SETGT     KODTVAL2
     C     XVAKOD        READPE(N) KODTVAL2                               33
     C     *IN33         IFNE      '0'
     C     KVAKRS        ORNE      XVAKRS
     C                   MOVE      XVASTA        KVASTA
     C                   MOVE      XVAUNI        KVAUNI
     C                   MOVE      XVAKOD        KVAKOD
     C                   MOVE      XVAKRS        KVAKRS
     C                   MOVE      XVAOMR        KVAOMR
     C                   MOVE      XVADT         KVADT
     C                   MOVE      XVAGKR        KVAGKR
     C                   MOVE      XVAXXX        KVAXXX
     C                   MOVE      XVAGV         KVAGV
     C                   WRITE     KOVAR
     C                   MOVE      '1'           *IN33
     C                   EXCEPT    DET
     C                   END
     C                   END
      *
     CLR                 EXSR      SLTGK
      *
      ***********************************************
     C     SLTGK         BEGSR
      ***********************************************
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XDT
S01  C*                  IF        XDTM < 3
S01  C*                  EVAL      XDT = XDT - 9000
S01  C*                  ELSE
S01  C*                  EVAL      XDT = XDT - 200
S01  C*                  END
N01  C                   EVAL      XDT = XDT - 30000
      *
     C     *LOVAL        SETLL     KODTVAL2
     C                   READ      KODTVAL2                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        ((KVAKOD = XGMKD) AND (KVADT < XDT))
     C                   DELETE    KOVAR
     C                   ELSE
     C                   UPDATE    KOVAR
     C                   END
     C                   MOVE      KVAKOD        XGMKD             3
     C                   READ      KODTVAL2                               33
     C                   ENDDO
      *
     C                   ENDSR
     OLISTE     E            PHEAD            02
     O                                           24 '** ENDREDE OG NYE TOLL-'
     O                                           40 'KURSER  ** '
     O                                           86 'KJØRT DATO:'
     O                       UDATE         Y     96
     O                                          101 'TID:'
     O                       TID                110 '0  :  :  '
     O                                          120 'SIDE:'
     O                       SIDE          Z    125
     OLISTE     E            PHEAD            04
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            PHEAD            05
     O                                           24 ' ENDR.KODE  VALUTA  TEK'
     O                                              'ST....                  '
     O                                              '  OMR.FAKT    NY KURS   '
     O                                              ' GML KURS'
     OLISTE     E            PHEAD            06
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            DET         2
     O              N33                           9 'ENDRET: '
     O               33                           9 'NY:     '
     O                       KVAKOD              18
     O                       KVAXXX              48
     O                       KVAOMR        3     56
     O                       KVAKRS        3     69
     O              N33      KVAGKR        3     81
