     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLSAK' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 4 20 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' 'S' 'A' '4' '5' '6' '8' -
S05  A*                                     '9' 'P' 'R' 'H')
N05  A                                      '9' 'P' 'R' 'H' 'B')
     A            FPSTAT    R        O  6  5REFFLD(FPXREFR/FPSTAT *LIBL/FPXREF)
     A            FPAVD     R        O  6  7REFFLD(FPXREFR/FPAVD *LIBL/FPXREF)
     A            FPOPD     R        O  6 12REFFLD(FPXREFR/FPOPD *LIBL/FPXREF)
     A            SINAK         30A  H
     A            XSINAK        28A  O  6 20
     A            FPAVDH    R        O  6 49REFFLD(FPXREFR/FPAVDH *LIBL/FPXREF)
     A  61                                  DSPATR(RI)
     A            FPOPDH    R        O  6 54REFFLD(FPXREFR/FPOPDH *LIBL/FPXREF)
     A  61                                  DSPATR(RI)
     A            FPKURE        17A  O  6 62
     A  60                                  DSPATR(RI)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            MSGNRH         7A  P
     A            ZRECNO         4S 0O  1 59SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/FP0002'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(040 SHU1054 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 68TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(024 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU1058 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 SHU1055 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 17'STATUS:'
     A            STA            1A  O  2 25
     A                                  2 27'='
     A            MSGTXZ        17A  O  2 29MSGID(&MSGNRH SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 28'R=Reverser ALLE  H=Hekt av en  B=B-
     A                                      eregn på nytt'
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'LAG TOLLDEK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  30        MSGTXY        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2005  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 SHU1060 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 15MSGCON(060 SHU1059 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            STA            1A  B  2 17DSPATR(UL)
     A                                      DSPATR(PC)
     A            AVD            4Y 0B  2 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            OPD            7Y 0B  2 27DSPATR(UL)
     A                                      EDTCDE(4)
     A            KURE          17A  B  2 36
     A            TLL           10Y 0B  2 55EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2005  '
     A                                      COLOR(WHT)
     A            MSGNRS         7A  P
     A  30        MSGTXS        78A  O  4  2MSGID(&MSGNRS SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A            MSGNRH         7A  P
     A                                  1  2'SYSPED/FP0002'
     A                                      COLOR(BLU)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 17'STATUS:'
     A            STA            1   O  2 25
     A                                  2 27'='
     A            MSGTXZ        17A  O  2 29MSGID(&MSGNRH SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 68TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N70                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  70                              4  2MSGCON(030 SHU1032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(040 SHU1054 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  1MSGCON(078 SHU1055 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  2005  '
     A                                      COLOR(WHT)
      *
     A          R PRINTW
     A                                      WINDOW(WINDOWSZ)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(02)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'TAST PRINTERNAVN'
     A                                      DSPATR(HI)
     A            PRT           10A  B  2  2DSPATR(UL)
     A                                  3  2MSGCON(015 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
