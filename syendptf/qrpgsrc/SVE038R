********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 Avrunda vikter på varuposter
********************************************************************
     FSVEFZ     IF   E           K DISK
     FSVEFZL01  UF   E           K DISK
     F                                     RENAME(SVEFR:SVEFR2)
     FSVEF      UF A E           K DISK
     F                                     RENAME(SVEFR:SVEFR1)
     FSVTVK     IF   E           K DISK
     FSVEVZL01  UF   E           K DISK
     F                                     RENAME(SVEVR:SVEVR1)
     FSVEV      IF A E           K DISK
     FSVE038D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
N01  D W110            S             11  0
      *____
      * Inz
      *""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEF_SYAV
     C                   PARM                    UVEF_SYOP

     C     *LIKE         DEFINE    SVEF_SYAV     UVEF_SYAV
     C     *LIKE         DEFINE    SVEF_SYOP     UVEF_SYOP
     C     *LIKE         DEFINE    SVEV_SYLI     ZVEV_SYLI
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS

     C                   MOVEL     *BLANKS       WVEF_REFF
     C                   EVAL      ZVVK_DTS=99999999

     C     SVEFZKEY      KLIST
     C                   KFLD                    WVEF_REFF
     C     SVTVKKEY      KLIST
     C                   KFLD                    SVEF_VAKD
     C                   KFLD                    ZVVK_DTS
     C     SVEFZKEY1     KLIST
     C                   KFLD                    SVEF_UNIK
     C                   KFLD                    SVEF_REFF
     C     SVEVKEY       KLIST
     C                   KFLD                    UVEF_SYAV
     C                   KFLD                    UVEF_SYOP
      *______________
      * Clear Subfile
      *""""""""""""""
     C     START1        TAG

     C                   MOVE      '1'           *IN93
     C                   MOVE      *BLANKS       SFNR
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     WVEF_REFF     SETLL     SVEFZ
     C                   READ      SVEFZ                                  94

     C     *IN94         DOWEQ     '0'
     C     SVEF_SYAV     IFEQ      *ZEROS
     C     SVEF_SYOP     ANDEQ     *ZEROS
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   ENDIF
     C                   READ      SVEFZ                                  94
     C  N94              ENDDO
      *________________
      * Display Subfile
      *""""""""""""""""
     C     START2        TAG

     C                   MOVE      *OFF          *IN51
     C                   MOVEA     '11'          *IN(91)
      * KOMMER HIT VED TOM FIL
     C     ZRECNO        IFEQ      0
     C                   CLEAR                   SUBFIL
     C                   EVAL      SVEF_FATX='Inga externa fakturor att visa'
     C                   MOVE      *OFF          *IN94
     C                   ADD       1             ZRECNO
     C                   MOVE      *ON           *IN51
     C                   WRITE     SUBFIL
     C                   SETON                                        94
     C                   ENDIF
     C                   WRITE     WIND01
     C                   WRITE     BUNN
     C     STBUNN        TAG

     C                   MOVEL     *BLANKS       WVEF_REFF
     C                   EXFMT     SUBCTL

     C                   SETOFF                                       999830
      * FORRIGE
     C   12              GOTO      SLUTT

     C     ZRECNO        IFNE      0
     C                   MOVE      *OFF          *IN98
     C                   READC     SUBFIL                                 98
     C     *IN98         DOWEQ     '0'
     C     SFNR          IFEQ      '1'
     C                   EXSR      SRGET
     C                   ENDIF
     C     SFNR          IFEQ      '4'
     C                   EXSR      SRDEL
     C                   ENDIF
     C                   READC     SUBFIL                                 98
     C  N98              ENDDO

     C                   ENDIF
     C     WVEF_REFF     CABNE     *BLANKS       START1
     C                   GOTO      START2

     C     SLUTT         TAG

     C                   EVAL      *INLR=*ON
      *__________________________________________
      * Hent faktura                        SRGET
      *""""""""""""""""""""""""""""""""""""""""""
     C     SRGET         BEGSR
      * Oppdatere SUBFIL
     C                   EVAL      WVEF_REFF=*BLANKS
     C                   EVAL      WVEF_REFF='A'
      * Skriv till fakturafil
     C                   EVAL      SVEF_SYAV=UVEF_SYAV
     C                   EVAL      SVEF_SYOP=UVEF_SYOP
     C                   EVAL      *IN54=*OFF
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  54
     C     *IN54         IFEQ      *OFF
     C                   EVAL      SVEF_VAKU=SVVK_KRS
     C                   ENDIF
     C                   WRITE     SVEFR1
      * Oppdatere externfil med avd/opd
     C     SVEFZKEY1     CHAIN     SVEFZL01                           54
     C                   IF        *IN54=*OFF
     C                   EVAL      SVEF_SYAV=UVEF_SYAV
     C                   EVAL      SVEF_SYOP=UVEF_SYOP
     C                   EVAL      SVEF_VAKU=SVVK_KRS
     C                   UPDATE    SVEFR2
     C                   ENDIF
      * Hitta høgsta varupostnummret
     C                   EVAL      *IN55=*OFF
     C     SVEVKEY       SETLL     SVEV
     C     SVEVKEY       READE     SVEV                                   55
     C     *IN55         DOWEQ     *OFF
     C                   EVAL      ZVEV_SYLI=SVEV_SYLI
     C     SVEVKEY       READE     SVEV                                   55
     C                   ENDDO
      * Les varuposter externe og add dem til produksjon
     C                   EVAL      *IN54=*OFF
     C     SVEFZKEY1     SETLL     SVEVZL01
     C     SVEFZKEY1     READE     SVEVZL01                               54
     C     *IN54         DOWEQ     *OFF

     C                   EVAL      SVEV_SYAV=UVEF_SYAV
     C                   EVAL      SVEV_SYOP=UVEF_SYOP
     C                   ADD       1             ZVEV_SYLI
     C                   EVAL      SVEV_SYLI=ZVEV_SYLI
     C                   EVAL      SVEV_VANO=ZVEV_SYLI
     C                   UPDATE    SVEVR1
     C                   EVAL      SVEV_CALL='00'
     C                   EVAL      SVEV_ERR='X'
N01  C                   IF        SVEV_BRUT > 1
N01  C                   EVAL(H)   W110=SVEV_BRUT
N01  C                   EVAL      SVEV_BRUT=W110
N01  C                   ENDIF
N01  C                   IF        SVEV_NETO > 1
N01  C                   EVAL(H)   W110=SVEV_NETO
N01  C                   EVAL      SVEV_NETO=W110
N01  C                   ENDIF
     C                   WRITE     SVEVR

     C     SVEFZKEY1     READE     SVEVZL01                               54
     C                   ENDDO
     C                   ENDSR
      *__________________________________________
      * Radera faktura                      SRDEL
      *""""""""""""""""""""""""""""""""""""""""""
     C     SRDEL         BEGSR
      * Oppdatere SUBFIL
     C                   EVAL      WVEF_REFF=*BLANKS
     C                   EVAL      WVEF_REFF='A'
      * Radera externfil
     C     SVEFZKEY1     CHAIN     SVEFZL01                           54
     C                   IF        *IN54=*OFF
     C                   DELETE    SVEFR2
     C                   ENDIF
      * Radera varuposter
     C                   EVAL      *IN54=*OFF
     C     SVEFZKEY1     SETLL     SVEVZL01
     C     SVEFZKEY1     READE     SVEVZL01                               54
     C     *IN54         DOWEQ     *OFF

     C                   DELETE    SVEVR1

     C     SVEFZKEY1     READE     SVEVZL01                               54
     C                   ENDDO
     C                   ENDSR
