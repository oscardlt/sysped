     ‚* V11TMP - Obs ved inst. hos kunder-DE FLESTE HAR 70 lang 'ubane' i kall på BHRURLCL
     ‚*********************************************************************
     ‚*  Sørring oversjø - basert på TJOPI1/ - interaktivt motstykke = ESOVERS2
     ‚*********************************************************************

CRTP‚+*  CRTPGM SYSPED/TJOVERS2 MODULE(*LIBL/TJOVERS2 *LIBL/INCGI)
CRTP‚m*         ACTGRP(*NEW) AUT(*USE)

****‚***************************************************************
     ‚* RETTINGER I DETTE PROGRAM:
PTFn‚:*Sek Sig Vers  Beskrivelse...........................
""""‚"*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
****‚***************************************************************
     ‚*********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     ‚*********************************************************************
     fbridf     if   e           k disk    usropn
     fheadf     if   e           k disk    usropn
     ftrackt    if   e           k disk    usropn
     fbillof    if   e           k disk    usropn
     fblv1      if   e           k disk    usropn
     ftrackl01  if   e           k disk    usropn
     fkofast    if   e           k disk    usropn
     ffrisok    if   e           k disk    usropn
     ffrisol01  if   e           k disk    usropn
     f                                     rename(frisokr:frisokr2)
     fkodfri    if   e           k disk    usropn
     fdokuf7    if   e           k disk    usropn
     ftran      if   e           k disk    usropn
     fturer14   if   e           k disk    usropn
     FARKIVL02  IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    USROPN
     FARKTXT    IF   E           K DISK    USROPN
     FARKEXT    IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    USROPN
     FGSSCGL02  IF   E           K DISK    USROPN
     ‚*********************************************************************
     ‚*--------------------------------------------------------------------
     ‚* Variables common to all CGIs
     ‚*--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3

     ‚* Client input variables
     d user            s             10
     d wsavd           s              4
     d wsopd           s              7
     d jsonstring      s          32000
     d error           s            150
     d kvurl           s            140
     d uttext          s            500
     d wssok           s             80
     d wssokurl        s            200
     d WSCONTNR        s            500
     D xcontnr         s              1
     D OKdok           s              1
     D ANTLEST         s              9  0
     D DOCLNK          S            200
     D DOCTXT          S             35

     d kvdato          s              8  0
     d spaces          s             12a   inz('&nbsp;&nbsp;')

     D KN              S              8  0 DIM(30)
     D                 DS
     D  ATString               1    192
     D  ATString1              1     48
     D  ATString2             49     96
     D  ATString3             97    144
     D  ATString4            145    192
     D  AT                     1    192    DIM(64)
     D                 DS
     D  A2                     1    128    DIM(64)
     d                 ds
     d dsavop                        12
     d  dsavd                         4    overlay(dsavop)
     d  strek                         1    overlay(dsavop:5)
     d  dsopd                         7    overlay(dsavop:6)
     d                 ds
     d kftxt                         50
     d  kftxtb                       45    overlay(kftxt:5)
     d  visweb                        1    overlay(kftxt:50)

     ‚* Prototype
     d jsonescape      pr                  extpgm('JSONESC')
     d   jsonstr                  32000a
     ‚*  Prototype for 'BHRKVFB'
     d bhrkvfb         pr                  extpgm('BHRKVFB')
     d heavd_                              like(heavd)
     d heopd_                              like(heopd)
     d uttext_                             like(uttext)
     d ttdate_                             like(ttdate)
     d tttime_                             like(tttime)
     ‚*  Prototype for 'BHRURLCL'
     d bhrurlcl        pr                  extpgm('BHRURLCL')
     d ubane_                              like(ubane)
     d gifok_                              like(gifok)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for bilibl
     d bilibl_001      pr                  extpgm(bilibl)
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d firstfri        s              1
     d firsttt         s              1
     d gifok           s              1
     d snr             s             17
     d snrgm           s             17
     d tttim4          s              4  0
     d ubane           s            256
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------

      * Prolog3=GetInput
      /copy syspeds/qrpgsrcpy,prolog3

      /free
       // Get input /parse input string
       exsr parse;

       // File open / keys
       exsr init;


       exsr setall;

       // File close/exit
       exsr exit;

       *inlr = *on;
       return;


       //**************************
       begsr parse;
       //**************************
         // Parse variables from QUERY_STRING environment variable
         wsavd   = zhbgetvar('heavd');
         HEAVD   = c2n2(WSAVD);
         wsopd   = zhbgetvar('heopd');
         HEOPD   = c2n2(WSOPD);
         // Userid.....
         user = zhbgetvar('user');
       endsr;


       //**************************
       begsr exit;
       //**************************
         // Do not delete the call to wrtsection with section name *fini.  It is needed
         // to ensure that all output html that has been buffered gets output.
         wrtsection('*fini');

         close bridf;
         close headf;
         close trackt;
         close trackl01;
         close billof;
         close blv1;
         close kofast;
         close frisok;
         close frisol01;
         close kodfri;
         close dokuf7;
         close tran;
         close turer14;
         close ARKIVL02;
         close FIRMARC;
         close ARKTXT;
         close ARKEXT;
         close BRPARF;
         close GSSCGL02;
       endsr;


       //**************************
       begsr setall;
       //**************************

         jsonstring='{'
         +'¬inp_user¬ : ¬'+%trim(user)+'¬, '
         +'¬inp_heavd¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬,'
         +'¬inp_heopd¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬ ';
         exsr skrivjson;

         exsr tstinput;

         if error=*blanks;
    ‚      // gyldig input er gitt - vis info om ett oppdrag
           // hent ekstra data
           chain ( heavd : heopd ) trackt;
           chain ( heavd : heopd ) billof;
           setll ( HEAVD : HEOPD ) BLV1;
           reade ( HEAVD : HEOPD ) BLV1;
           dow not %eof;
              if BVMN <> *BLANKS;
                 XCONTNR = 'J';
                 if WSCONTNR = *BLANKS;
                    WSCONTNR = %TRIM(BVMN);
                 else;
                    WSCONTNR = %TRIM(WSCONTNR) +','+ %TRIM(BVMN);
                 endif;
              endif;
              reade ( HEAVD : HEOPD ) BLV1;
           enddo;
           // fiks gitte data
           monitor;
             kvdato = %int(hedtmo);
           on-Error;
             kvdato = 0;
           endmon;
           jsonstring=', '
           +'¬poNumber¬ : ¬'+%trim(herfa)+'¬, '
           +'¬avdeling¬ : ¬'+%trim(%editc(heavd:'Z'))+'¬,'
           +'¬oppdrag¬ : ¬'+%trim(%editc(heopd:'Z'))+'¬,'
           +'¬saksbeh¬ : ¬'+%trim(hesg)+'¬, '
           +'¬ordredato¬ : ¬'+%trim(%editw(hedtop:'    .  .  '))+'¬,'
           +'¬etd¬ : ¬'+%trim(%editw(txetdd:'    .  .  '))+'¬,'
           +'¬atd¬ : ¬'+%trim(%editw(txatdd:'    .  .  '))+'¬,'
           +'¬eta¬ : ¬'+%trim(%editw(txetad:'    .  .  '))+'¬,'
           +'¬ata¬ : ¬'+%trim(%editw(txatad:'    .  .  '))+'¬,'
           +'¬avsender¬ : ¬'+%trim(henas)+'¬, '
           +'¬avseAdr1¬ : ¬'+%trim(heads1)+'¬, '
           +'¬avseAdr2¬ : ¬'+%trim(heads2)+'¬, '
           +'¬avseAdr3¬ : ¬'+%trim(heads3)+'¬, '
           +'¬mottaker¬ : ¬'+%trim(henak)+'¬, '
           +'¬mottAdr1¬ : ¬'+%trim(headk1)+'¬, '
           +'¬mottAdr2¬ : ¬'+%trim(headk2)+'¬, '
           +'¬mottAdr3¬ : ¬'+%trim(headk3)+'¬, '
           +'¬fromPlace¬ : ¬'+%trim(bllss)+'¬, '
           +'¬fromPort¬ : ¬'+%trim(bllsh)+'¬, '
           +'¬via1Port¬ : ¬'+%trim(blvh)+'¬, '
           +'¬via2Port¬ : ¬'+%trim(blvh2)+'¬, '
           +'¬toPort¬ : ¬'+%trim(blloh)+'¬, '
           +'¬toPlace¬ : ¬'+%trim(bllos)+'¬, '
           +'¬contNummer¬ : ¬'+%trim(WSCONTNR)+'¬, '
           +'¬antall¬ : ¬'+%trim(%editc(hent:'3'))+'¬,'
           +'¬varebesk¬ : ¬'+%trim(hevs1)+'¬, '
           +'¬varebesk2¬ : ¬'+%trim(hevs2)+'¬, '
           +'¬vekt¬ : ¬'+%trim(%editc(hevkt:'3'))+'¬,'
           +'¬volM3¬ : ¬'+%trim(%editc(hem3:'3'))+'¬,'
           +'¬lastem¬ : ¬'+%trim(%editc(helm:'3'))+'¬,'
           +'¬fvekt¬ : ¬'+%trim(%editc(hefbv:'3'))+'¬,'
           +'¬godsmerke¬ : ¬'+%trim(hegm1)+'¬, '
           +'¬godsmerke2¬ : ¬'+%trim(hegm2)+'¬, '
           +'¬oppdrType¬ : ¬'+%trim(heot)+'¬, '
           +'¬frankatur¬ : ¬'+%trim(hefr)+'¬, '
           +'¬kvittert¬ : ¬'+%trim(hesgm)+'¬, '
           +'¬kvDato¬ : ¬'+%trim(%editw(kvdato:'    .  .  '))+'¬,'
           +'¬kvTid¬ : ¬'+%trim(%editw(heklmo:'  :  '))+'¬';
           exsr skrivjson;
    ‚      //arkivposter
             exsr dsparkiv;
    ‚      //diverse tabellbasert info referanser / T&T
           exsr dspfrisok;
           exsr trackinfo;
         endif;

    ‚    //avslutning
         jsonstring=', ¬errMsg¬ : ¬'+%trim(error)+'¬ }';
         exsr skrivjson;

       endsr;


       //**************************
       begsr tstinput;
       //**************************
         chain ( heavd : heopd ) headf;
         if not %found;
           error = 'Shipment ' +%trim(%editc(heavd:'Z')) +'/'
                 +%trim(%editc(heopd:'Z'))+ ' not found.';
         endif;

       endsr;

       //**************************
       begsr dsparkiv;
       //**************************
         exsr initDok;
         // liste-start ubetinget
         jsonstring=', ¬akivDoc¬ : [';
         exsr skrivjson;
         if urlex <> *blanks;
         // VIS TRACKBAR SØKEVEI
         firstfri = 'Y';
         // frie søkeveier som er trackbare.. (herunder IFB fraktbrev..)
         setll ( 'A' : heavd : heopd ) arkivl02;
         reade ( 'A' : heavd : heopd ) arkivl02;
         dow not %eof(arkivl02);
           // visse typer dokument må "eies" av webkunden
               okDok = 'J';
           if artype='go' or
              artype='am' or
              artype='fa' or
              artype='fs' or
              artype='fx' or
              artype='dg' or
              artype='ta';
             if xintern <> 'J';
               okDok = 'N';
               xn2 = 1;
               dow xn2 <= 30;
                 if KN(XN2) = ARKUND;
                   okDok = 'J';
                   leave;
                 endif;
                 if KN(XN2) = *zeros;
                   leave;
                 endif;
                 xn2 = xn2 + 1;
               enddo;
             endif;
           endif;
      /end-free
      * Brukeren må være autorisert for å se aktuell type
N01  C                   IF            XINTERN <> 'J'                           Intern:ikke typ-test
     C     artype        lookup    A2                                     33
     C     *IN33         CABEQ     '0'           NextDoc
N01  C                   ENDIF
      * OK
     C                   IF        OKDok = 'J'
     C     artype        IFEQ      'go'
     C     artype        oreq      'am'
     C     artype        oreq      'fx'
     C     artype        oreq      'fs'
     C                   MOVE      *ZEROS        aravd
     C                   MOVE      *ZEROS        aropd
     C                   END
     C     artype        CHAIN     arktxt                             95
     C  n95              EXSR      genbane
     C                   ENDIF
     C     NextDoc       TAG
      /free
           reade ( 'A' : heavd : heopd ) arkivl02;
         enddo;
         endif;
         // avslutt liste ubetinget
         jsonstring=']';
         exsr skrivjson;

      /end-free
      /free
       endsr;

       //**************************
       begsr dspfrisok;
       //**************************
         // liste-start ubetinget
         jsonstring=', ¬trackbarRef¬ : [';
         exsr skrivjson;

         // VIS TRACKBAR SØKEVEI
         firstfri = 'Y';
         // Innland sendt med transportør som er trackbar..
         if hxsndn <> *blanks and hepro <> *zeros;
           exsr innltransp;
         endif;
         // frie søkeveier som er trackbare.. (herunder IFB fraktbrev..)
         setll ( heavd : heopd ) frisok;
         reade ( heavd : heopd ) frisok;
         dow not %eof(frisok);
           kfsttu = *blanks;
           chain fskode kodfri;
           if %found;
             if fskode = 'IFB';
               exsr tstfbr;
             endif;
             exsr tstlaglink;
           endif;
           reade ( heavd : heopd ) frisok;
         enddo;
         // avslutt liste ubetinget
         jsonstring=']';
         exsr skrivjson;


         // VIS IKKE-TRACKBAR SØKEVEI

         // liste-start ubetinget
         jsonstring=', ¬andreRef¬ : [';
         exsr skrivjson;

         firstfri = 'Y';
         setll ( heavd : heopd ) frisok;
         reade ( heavd : heopd ) frisok;
         dow not %eof(frisok);
           chain fskode kodfri;
           if %found and %scan('A':kfsodk)>0;             // dokkode=A = vis på web
             if fskode = 'IFB';
               exsr tstfbr;
             endif;
             // Har denne frisok-kode peker til Track&Trace-URL-definisjon?
             kfkod = kfsttu;
             chain ( 'TRACKPREFI' : kfkod ) kofast;
             if not %found;
               if firstfri='Y';
                 jsonstring=*blanks;
                 firstfri = 'N';
               else;
                 jsonstring=', ';
               endif;
               jsonstring=%trim(jsonstring)+'{'
                  +'¬type¬ : ¬'+%trim(kfsotx)+'¬, '
                  +'¬verdi¬ : ¬'+%trim(fssok)+'¬} ';
               exsr skrivjson;
             endif;
           endif;
           reade ( heavd : heopd ) frisok;
           enddo;
         // avslutt liste ubetinget
         jsonstring=']';
         exsr skrivjson;
       endsr;


       //**************************
       begsr tstfbr;
       //**************************
       // Test om transportør på fraktbrev overstyrer T&T-URL-kode
       // fri søkevei er fraktbrevsnr innland - da er det turens transporør som gjelder
         if fssok = snr;
           leavesr;
         endif;
         // samme fr.brevsnr på flere oppdrag ?  finn det rette
         setll fssok dokuf7;
         reade fssok dokuf7;
         dow not %eof(dokuf7);
           if dfavd = heavd and dfopd = heopd;
             leave;
           endif;
           reade fssok dokuf7;
         enddo;
         if %eof(dokuf7) or dftran = 0;
           leavesr;
         endif;
         chain ( bifirm : dftran ) tran;
         if not %found or vmintp = *blanks;
           leavesr;
         endif;
         kfsttu  = vmintp;
         kfsotx  = 'Fr.brevsnr ' + dfnat;
       endsr;



       //**************************
       begsr innltransp;
       //**************************
         // Sendingsnr i tr.modul innland - transportør med T&T-URL-kode ??

         chain hepro turer14;
         if not %found;
           leavesr;
         endif;

         dftran = tuknt2;
         chain ( bifirm : dftran ) tran;
         if not %found or vmintp = *blanks;
           leavesr;
         endif;

         // OK fant tur med tansportør som har peker til tracking - lag lenke
         kfsttu  = vmintp;
         kfsotx  = 'Fr.brevsnr ' + vmnavn;
         fssok   = snr;
         exsr tstlaglink;
       endsr;


       //**************************
       begsr tstlaglink;
       //**************************
         // Har denne frisok-kode peker til Track&Trace-URL-definisjon?
         kfkod = kfsttu;
         chain ( 'TRACKPREFI' : kfkod ) kofast;
         if %found;
           // Verdi som vises i link..
           wssok = fssok;
           // URL som skal utføres
           wssokurl = kftxt;
           // append linje 2 (engelsk tekst når siste tegn er A)
           if %subst(kftxte:50:1) = 'A';
             %subst(kftxte:50:1) = ' ';
             wssokurl = %trimr(wssokurl) + kftxte;
           endif;
           wssokurl = %trimr(wssokurl) + fssok;
           // Suffix i søkestrengen..
           chain ( 'TRACKSUFFI' : kfkod ) kofast;
           if %found;                                                           //   3<-B
             wssokurl = %trimr(wssokurl) + kftxt;
           endif;                                                               //   3<-E

           if firstfri='Y';
             jsonstring=*blanks;
             firstfri = 'N';
           else;
             jsonstring=', ';
           endif;
           jsonstring=%trim(jsonstring)+'{'
              +'¬type¬ : ¬'+%trim(kfsotx)+'¬, '
              +'¬verdi¬ : ¬'+%trim(wssok)+'¬, '
              +'¬url¬ : ¬'+%trim(wssokurl)+'¬} ';
           exsr skrivjson;
         endif;                                                                 //  2<-E
       endsr;

       //**************************
       begsr trackinfo;
       //**************************
       // INFO FRA T&T -LOGG
         //tabellstart ubetinget
         jsonstring=', ¬trackTraceEvents¬ : [';
         exsr skrivjson;

         firsttt = 'Y';
         kfkod = *blanks;

         // HENDELSER I T&T
         setll ( heavd : heopd ) trackl01;
         reade ( heavd : heopd ) trackl01;
         dow not %eof(trackl01);
           visweb = ' ';
           %subst(kfkod:1:3) = ttacti;
           chain ( 'TRACKT    ' : kfkod ) kofast;
           if ttacti = 'POD' or ttacti = 'AVV';
             if ttfbnr <> 0;
               visweb = ' ';
             endif;
           endif;
           if visweb = 'J';
             if ttacti = 'POD';
               uttext = kftxtb + tttext;
             else;
               uttext = tttext;
             endif;
             tttim4 = tttime/100;
             if firsttt='Y';
               jsonstring=*blanks;
               firsttt  = 'N';
             else;
               jsonstring=', ';
             endif;
             jsonstring=%trim(jsonstring)+'{'
                +'¬dato¬ : ¬'+%trim(%editw(ttdate:'    .  .  '))+'¬,'
                +'¬tid¬ : ¬'+%trim(%editw(tttim4:'  :  '))+'¬,'
                +'¬tekst¬ : ¬'+%trim(uttext)+'¬} ';
             exsr skrivjson;
             if ttacti = 'POD';
               // normalt er evt signatur sendingsnr.gif
               snr = hxsndn;
               if %subst(hxsndn:20:1)<>' ';
                 snr = %subst(hxsndn:4:17);
               endif;
               exsr getgif;
             if gifok <> 'J';
               snrgm = snr;
               dsavd = %editc(heavd:'X');
               strek = '_';
               dsopd = %editc(heopd:'X');
               snr    = dsavop;
               exsr getgif;
               snr = snrgm;
             endif;
             endif;

             // vis evt gif ved COL/TEI/TEU/TE2
             if ttacti = 'COL'
               or ttacti = 'TEI'
               or ttacti = 'TEU'
               or ttacti = 'TE2';
               exsr getgif2;
               if gifok <> 'J';
                 snrgm = snr;
                 dsavd = %editc(heavd:'X');
                 strek = '_';
                 dsopd = %editc(heopd:'X');
                 snr    = dsavop;
                 exsr getgif2;
                 snr = snrgm;
               endif;
             endif;

           endif;
           reade ( heavd : heopd ) trackl01;
         enddo;

         // finnes det kvittert fraktbrev??
         uttext = *blanks;
         bhrkvfb ( heavd : heopd : uttext : ttdate : tttime );
         if uttext <> *blanks;
           if firsttt='Y';
             jsonstring=*blanks;
           else;
             jsonstring=', ';
           endif;
           firsttt  = 'N';
           jsonstring=%trim(jsonstring)+'{'
              +'¬dato¬ : ¬¬,'
              +'¬tid¬ : ¬¬,'
              +'¬tekst¬ : ¬'+%trim(uttext)+'¬} ';
           exsr skrivjson;
           uttext = *blanks;
         endif;

         // avslutt liste - ubetinget
         jsonstring=']';
         exsr skrivjson;
       endsr;


       //**************************
       begsr getgif;
       //**************************

         gifok = 'N';
         ubane = %trim(urlex)+'/syspeddev/signatur/'+%trim(snr)+ '.gif';
         bhrurlcl ( ubane : gifok );
         // gif fantes ikke, prøv med bmp.
         if gifok <> 'J';
           ubane = %trim(urlex)+'/syspeddev/signatur/'+%trim(snr)+ '.bmp';
           bhrurlcl ( ubane : gifok );
         endif;
         if gifok <> 'J';
           ubane = %trim(urlex)+'/syspeddev/signatur/'+%trim(snr)+ '.jpg';
           bhrurlcl ( ubane : gifok );
         endif;
         if gifok <> 'J';
           ubane = %trim(urlex)+ '/syspeddev/signatur/'+%trim(snr)+ '.png';
           bhrurlcl ( ubane : gifok );
         endif;
         if gifok = 'J';
           uttext = '<p><img border="0" src="'+%trim(ubane)+'"></p>';
           if firsttt='Y';
             jsonstring=*blanks;
             firsttt  = 'N';
           else;
             jsonstring=', ';
           endif;
           jsonstring=%trim(jsonstring)+'{'
              +'¬dato¬ : ¬'+%trim(%editw(ttdate:'    .  .  '))+'¬,'
              +'¬tid¬ : ¬'+%trim(%editw(tttim4:'  :  '))+'¬,'
              +'¬tekst¬ : ¬'+%trim(uttext)+'¬} ';
           exsr skrivjson;
           uttext = *blanks;
         endif;
       endsr;


       //**************************
       begsr getgif2;
       //**************************

         gifok = 'N';
         ubane = %trim(urlex)+ '/syspeddev/signatur/'
                 +ttacti+'_'+%trim(snr)+ '.gif';
         bhrurlcl ( ubane : gifok );
         if gifok <> 'J';
           ubane = %trim(urlex)+ '/syspeddev/signatur/'
                   +ttacti+'_'+%trim(snr)+ '.bmp';
           bhrurlcl ( ubane : gifok );
         endif;
         if gifok <> 'J';
           ubane = %trim(urlex)+  '/syspeddev/signatur/'
                   +ttacti+'_'+%trim(snr)+ '.jpg';
           bhrurlcl ( ubane : gifok );
         endif;
         if gifok <> 'J';
           ubane = %trim(urlex)+ '/syspeddev/signatur/'
                   +ttacti+'_'+%trim(snr)+ '.png';
           bhrurlcl ( ubane : gifok );
         endif;
         if gifok = 'J';
           uttext = '<p><img border="0" src="'+%trim(ubane)+'"></p>';
           if firsttt='Y';
             jsonstring=*blanks;
             firsttt  = 'N';
           else;
             jsonstring=', ';
           endif;
           jsonstring=%trim(jsonstring)+'{'
              +'¬dato¬ : ¬'+%trim(%editw(ttdate:'    .  .  '))+'¬,'
              +'¬tid¬ : ¬'+%trim(%editw(tttim4:'  :  '))+'¬,'
              +'¬tekst¬ : ¬'+%trim(uttext)+'¬} ';
           exsr skrivjson;
           uttext = *blanks;
         endif;
       endsr;



       //**************************
       begsr init;
       //**************************
       // prepare HTMLbuffer - write top section
         gethtmlifs('/syspeddev/html/JSON.html':'/CGITAG/');
         wrtsection('top');

         open bridf;
         chain user bridf;
         if not %found;
         // kall på SkrivJSON/jsonescape (extpg JSONESC) kan feile om ikke libl er satt..
         //         - alternativt må vi ALLTID sørge for at JSONESC ligger i SYSPED
           jsonstring='{"errMsg" : "Ugyldig userid"}';
           updhtmlvar2('JSONstring':%addr(jsonstring):%len(jsonstring));
           wrtsection('JSONlin');
           // send htmlbuffer + avslutt
           wrtsection('*fini');
           close bridf;
           *inlr = *on;
           return;
         else;
           if bilibl = *blanks;
             incgi ( bistdl );              // standardliste via nr
           else;
             bilibl_001();                  // helt egen libl
           endif;
           open headf;
           open trackt;
           open trackl01;
           open billof;
           open blv1;
           open kofast;
           open frisok;
           open frisol01;
           open kodfri;
           open dokuf7;
           open tran;
           open turer14;
           open ARKIVL02;
           open FIRMARC;
           open ARKTXT;
           open ARKEXT;
           open BRPARF;
           open GSSCGL02;
         endif;

       endsr;

       //**************************
       begsr skrivjson;
       //**************************
         callp jsonescape(jsonstring);
         updhtmlvar2('JSONstring':%addr(jsonstring):%len(jsonstring));
         wrtsection('JSONlin');
       endsr;

      /end-free
      ***********************************************
     C     genbane       BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       DOCTXT
     C                   MOVE      *BLANKS       DOCLNK
     C                   MOVE      ARKLAG        ARCEXT
     C     ARKEXTK       CHAIN     ARKEXT                             33
     C   33ARFIRM        CHAIN     FIRMARC                            33
      * genererer bane for lagring
     C                   MOVE      ARAVD         AAVD              4
     C                   MOVE      AROPD         AOPD              7
     C                   EVAL      DOCLNK = URLEX
     C     DOCLNK        CAT       '/':0         DOCLNK
     C     DOCLNK        CAT       arcane:0      DOCLNK
     C     DOCLNK        CAT       '/':0         DOCLNK
     C     ARLINK        IFNE      *BLANKS
     C     DOCLNK        CAT       ArLink:0      DOCLNK
     C                   ELSE
     C     DOCLNK        CAT       aavd:0        DOCLNK
      * TEST LINK
     C                   EVAL      UXLINK = %trim(URLEX)+ '/' + %TRIM(ARCANE)
     C                             + '/' + AAVD + AOPD
     C                             + %TRIM(ARTYPE)
     C                             + %TRIM(ARUNDE)
     C                             + '.pdf'
     C                   MOVE      'J'           UXOK
     C                   CALL      'CHECKLNK'
     C                   PARM                    UXLINK           50
     C                   PARM                    UXOK              1

     C                   IF        UXOK = 'N'
      * GAMMEL DOKUMENT MED 5 SIFRE OPPDRAGSNR
     C                   MOVE      aopd          AOPD5             5
     C                   CAT       aopd5:0       DOCLNK
     C                   ELSE
     C     DOCLNK        CAT       aopd:0        DOCLNK
     C                   END
     C     DOCLNK        CAT       artype:0      DOCLNK
     C     DOCLNK        CAT       arunde:0      DOCLNK
     C                   END
     C     '.pdf'        SCAN      DOCLNK                                 33
     C  N33'.PDF'        SCAN      DOCLNK                                 33
     C  N33'.'           SCAN      arlink                                 33
     C  N33              IF        ARKLAG = *BLANKS
     C                   CAT       '.pdf':0      DOCLNK
     C                   END
     C                   IF        ARRFK = *BLANKS
     C                   EVAL      DOCTXT = ARTXT
     C                   ELSE
     C                   EVAL      DOCTXT = ARRFK
     C                   ENDIF

     C                   ADD       1             ANTLEST
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
        +'¬doctxt¬ : ¬'+%trim(DOCTXT)+'¬, '
        +'¬doclnk¬ : ¬'+%trim(DOCLNK)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      ****************************************
     C     InitDok       BEGSR
      ****************************************
     C     arkivkey      KLIST
     C                   KFLD                    UNKODE
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'A'           unkode            1
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     ARKEXTK       KLIST
     C                   KFLD                    ARFIRM
     C                   KFLD                    ARCEXT
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
      * hent extern ipadr/start på url..
     C                   MOVEL     '*KUNDFT*'    PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVEL     'URLEX'       PAID
N01  C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      '0'
     C                   movel     Pavrda        URLEX            50
     C                   END
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
N01   * kontroll mot kunde/kundegruppe/arktype benyttes iKKE ved intern
N01  c     XINTERN       IFNE      'J'
     C                   MOVE      *ZEROS        KN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   MOVEL     'KUNGR'       PAID
N01  C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA = *BLANKS))
     C                   MOVE      BIKUNR        KN(1)
     C                   ELSE
     C                   MOVEL     PAVRDA        CGCG
     C     CGCG          SETLL     GSSCGL02
     C     CGCG          READE     GSSCGL02                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN2
     C                   MOVE      CGCNO         KN(XN2)
     C     CGCG          READE     GSSCGL02                               33
     C                   ENDDO
     C                   END
      * LÅNER ARKTY-PARAMETEREN FOR Å AVGJØRE OM BRUKER KAN SE FAKTURALINK
      * sjekk om arkivtypene fa,fs,fx finnes(på førstelinjer) Hvis ikke, N i se fakturalink
      * (sjekk av arkivtyper for å se pdf-dok, sker generelt annet sted)
     C                   MOVE      'N'           OKFaktLink        1
     C                   MOVE      'ARKTY'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      '0'
     C     'fa'          scan      Pavrda                                 33
     C  N33'fs'          scan      Pavrda                                 33
     C  N33'fx'          scan      Pavrda                                 33
     C   33              MOVE      'J'           OKFaktLink
     C                   END

s01  C*    BIFIRM        CHAIN     FIRM                               33

      * Vis kun dokumeter bruker er autorisert for å se...
     C                   MOVE      *BLANKS       ATString
     C                   MOVE      'ARKTY'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         CABEQ     '1'           ENDINI
     C                   MOVEL     Pavrda        ATstring1
     C     BrParKey      READE     BRPARF                                 33
     C  N33              MOVEL     Pavrda        ATstring2
     C  N33BrParKey      READE     BRPARF                                 33
     C  N33              MOVEL     Pavrda        ATstring3
     C  N33BrParKey      READE     BRPARF                                 33
     C  N33              MOVEL     Pavrda        ATstring4
     C     1             DO        64            ln                2 0
     C                   MOVEa     AT(ln)        A2(ln)
     C                   END
N01  c                   ENDIF

     C     ENDINI        ENDSR
