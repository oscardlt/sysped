********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10103 * 01 CB  PTF10 2 nye seleksjonskriterier + retting av gamle feil
     FEDI61D    CF   E             WORKSTN
     F                                     SFILE(EDI61DB:ZRECNO)
     F                                     INFDS(FBAREA)
     FEUNHJ1    IF   E           K DISK
     FEDII      IF   E           K DISK
     FEDIS      IF   E           K DISK
     D                 DS
     D  ZA                     1     14
     D                                     DIM(14)
     D  W0062                  1     14
     D                 DS
     D  ZDTTM                  1     14  0
     D  ZDTT                   1      8  0
     D  ZTMT                   9     14  0
     D                 DS
     D  MDTTM                  1     14  0
     D  MDT                    1      8  0
     D  MTM                    9     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  WTID                   1      6
     D  WTIDH                  1      2  0
     D  WTIDM                  3      4  0
     D  WTIDS                  5      6  0
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C                   Z-ADD     0             ZAX               2 0
     C                   Z-ADD     0             WAX               2 0
     C                   MOVE      *BLANKS       X0062            14
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZDTF
     C                   KFLD                    ZTMF
     C     KEYB          KLIST
     C                   KFLD                    MSN
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM      MMN           MMN
     C                   PARM                    WUPD
     C     *IN03         PARM                    WIN03
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           ZDTF
     C                   MOVEL     WDT           ZDTT
     C                   Z-ADD     000000        ZTMF
     C                   Z-ADD     235959        ZTMT
     C                   MOVE      'J'           ZJNS
     C                   MOVE      'J'           ZJNR
     C                   MOVE      '*'           ZST
     C                   MOVE      '*'           ZVEN
     C                   MOVEL     '*'           Z0065
     C                   MOVEL     '*'           Z1001
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     33            POSNBR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI61DØ
     C                   EXFMT     EDI61DA
     C                   MOVEA     '0000000'     *IN(30)
     C     *IN03         CABEQ     '1'           SRB9
      * Teste dato fra:
     C                   MOVEL     ZDTF          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1100000'     *IN(30)
     C   30              Z-ADD     6             LINNBR
     C   30              GOTO      SRB1
      * Teste dato til:
     C                   MOVEL     ZDTT          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1010000'     *IN(30)
     C   30              Z-ADD     9             LINNBR
     C   30              GOTO      SRB1
      * Fra dato større en til dato.
     C     ZDTF          IFGT      ZDTT
     C                   MOVE      'SPD0002'     ZMSGNO
     C                   MOVEA     '1110000'     *IN(30)
     C                   Z-ADD     6             LINNBR
     C                   GOTO      SRB1
     C                   END
      * Teste tid fra:
     C                   MOVEL     ZTMF          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1001000'     *IN(30)
     C   30              Z-ADD     7             LINNBR
     C   30              GOTO      SRB1
      * Teste tid til:
     C                   MOVEL     ZTMT          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000100'     *IN(30)
     C   30              Z-ADD     10            LINNBR
     C   30              GOTO      SRB1
      * Klokkeslett fra får ikke vare større en klokkeslett til.
     C     ZDTF          IFEQ      ZDTT
     C     ZTMF          ANDGT     ZTMT
     C                   MOVE      'SPK0007'     ZMSGNO
     C                   MOVEA     '1010100'     *IN(30)
     C                   Z-ADD     7             LINNBR
     C                   GOTO      SRB1
     C                   END
      * En må sette J på minimum 1 av sendn/mottak.
     C     ZJNS          IFEQ      'N'
     C     ZJNR          ANDEQ     'N'
     C                   MOVE      'SPS0005'     ZMSGNO
     C                   MOVEA     '1000011'     *IN(30)
     C                   Z-ADD     12            LINNBR
     C                   GOTO      SRB1
     C                   END
      * Ordne stopper for søkebegrepp.
     C                   MOVE      Z0062         W0062
     C                   MOVEL     Z0062         W0062B            4
     C     W0062B        IFEQ      '*ALL'
     C     W0062B        OREQ      '*all'
     C                   MOVE      *BLANKS       Z0062
     C                   MOVE      *BLANKS       W0062
     C                   ENDIF
     C     1             DO        14            ZAX
     C     15            SUB       ZAX           WAX
     C     ZA(WAX)       CABNE     *BLANKS       SRB2
     C                   MOVE      '9'           ZA(WAX)
     C                   END
      *
     C     SRB2          TAG
      * Forespørsel Utvekslingsid ?
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     *IN04         IFEQ      '1'
     C     LINNBR        ANDEQ     15
     C                   CALL      'EDI52R'
     C                   PARM                    ZIID
     C                   GOTO      SRB1
     C                   ENDIF
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     EDI61DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EUNHJ1
     C                   READ      EUNHJ1                                 94
     C  N94MDTTM         COMP      ZDTTM                              94
      *
     C     *IN94         DOWEQ     '0'
      * Check Status
     C     ZST           IFNE      '*'
     C     MST           ANDNE     ZST
     C                   GOTO      SRB3
     C                   ENDIF
      * Wait code
     C     ZVEN          IFNE      '*'
     C     MVEN          ANDNE     ZVEN
     C                   GOTO      SRB3
     C                   ENDIF
      * Check Send/Receive
     C                   MOVE      '0'           *IN21
     C     ZJNS          COMP      'N'                                    20
     C   20MSR           COMP      'S'                                    20
     C  N20ZJNR          COMP      'N'                                    21
     C  N20
     CAN 21MSR           COMP      'R'                                    21
     C     *IN20         IFEQ      '1'
     C     *IN21         OREQ      '1'
     C                   GOTO      SRB3
     C                   ENDIF
      * Check Interchange ID
     C     ZIID          IFNE      *BLANKS
     C     M0004         COMP      ZIID                                   20
     C  N20M0010         COMP      ZIID                                   20
     C     *IN20         IFEQ      '0'
     C                   GOTO      SRB3
     C                   ENDIF
     C                   ENDIF
      * Check Message reference
     C     Z0062         IFNE      *BLANKS
     C     M0062         COMP      Z0062                              20  20
     C   20M0062         COMP      W0062                                2020
     C     *IN20         IFEQ      '0'
     C                   GOTO      SRB3
     C                   ENDIF
     C                   ENDIF
      * Check Meldingstype
     C     Z0065         IFNE      *BLANKS
     C     Z0065         ANDNE     '*'
     C     M0065         COMP      Z0065                              2020
     C                   IF        *IN20=*ON
     C                   GOTO      SRB3
     C                   ENDIF
     C                   ENDIF
      * Check Meldingsnavn
     C     Z1001         IFNE      *BLANKS
     C     Z1001         ANDNE     '*'
     C     M1001         COMP      Z1001                              2020
     C                   IF        *IN20=*ON
     C                   GOTO      SRB3
     C                   ENDIF
     C                   ENDIF
      *
     C                   ADD       1             ZRECNO
     C     MSR           IFEQ      'S'
     C                   MOVEL     M0010         INID
     C                   ELSE
     C                   MOVEL     M0004         INID
     C                   END
     C     INID          CHAIN     EDII                               25
     C  N25              MOVEL     INNA          WSMOTP
     C   25              MOVE      *BLANKS       WSMOTP
     C   25              MOVEL     M0004         WSMOTP
     C   25              MOVE      M0010         WSMOTP
     C                   WRITE     EDI61DB
      *
     C     SRB3          TAG
      *
     C                   READ      EUNHJ1                                 94
     C  N94MDTTM         COMP      ZDTTM                              94
     C  N94              ENDDO
     C     ZRECNO        IFEQ      0
     C                   MOVE      'SPR0002'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRB1
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB4          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI61DØ
     C                   WRITE     EDI61DD
     C                   EXFMT     EDI61DC
      *
     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB2
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     EDI61DB                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '7'           SRBG
     C     ZOP           CASEQ     '9'           SRBI
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB2
     C     *IN95         CABEQ     '0'           SRB4
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Change message                                      SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVE      'Y'           WUPD              1
     C                   CALL      'EDI67R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI61DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVE      'N'           WUPD
     C                   CALL      'EDI67R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI61DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk filkomponent                            SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
      * Hent utvekslingen
     C     MSN           CHAIN     EDIS                               20
     C   20              EVAL      SMBR='REMOVED               '
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   ELSE
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   MOVEL(P)  'DSPPFM FILE('QCMDA           512
     C     QCMDA         CAT       SLIB:0        QCMDA
     C     QCMDA         CAT       '/':0         QCMDA
     C     QCMDA         CAT       SFILE:0       QCMDA
     C     QCMDA         CAT       '_) MBR(':0   QCMDA
     C     QCMDA         CAT       SMBR:0        QCMDA
     C     QCMDA         CAT       ')':0         QCMDA
     C                   Z-ADD     50            QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI44R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI44R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI44S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI44S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ENDSL
     C                   END
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C  N30              MOVE      *BLANKS       ZOP
     C  N30              UPDATE    EDI61DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk komponent MED LINEFEED                  SRBI /
      *////////////////////////////////////////////////////////////
     C     SRBI          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBI9
     C                   END
      * TEST OM XML-FILER
     C                   MOVEL     S0026         XAN04             4
     C                   IF        XAN04 = 'XML-'
     C                   CALL      'EDI41RXM'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ELSE
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   CALL      'EDI41RX'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI41R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI41R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI41S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI41S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ENDSL
     C                   END
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI61DB
      *
     C     SRBI9         ENDSR
