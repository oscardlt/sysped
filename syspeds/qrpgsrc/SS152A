      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS152A MODULE(*LIBL/SS152A *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D UsrSpcName      s             10
     D IFSFIL          S             30
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D WKEY1                   1    256
     D WINT                  147    152
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   eval      IFSFil = 'ListActiveOrders'
     C                   cat       WSUSER:0      IFSFIL
     C                   cat       '.pdf':0      IFSFIL
     C                   call      'SS152C'
     c                   parm                    IFSFIL
     c                   parm                    WINT
      * Set output skeleton html member name
      *-dummy html for å få kjørt prog                                      ******
      * "bytter ut seg selv" med Html-fil i IFS umiddelbart                 ******
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     updhtmlvar('PDF':'N')
     C                   callp     wrtsection('top')
      *------------------                                                   ******
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C*
     C                   close     BRIDF
     C                   eval      *inlr = *on
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WKEY1   = zhbgetvar('WKEY')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   endsr
