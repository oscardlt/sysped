      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR) SAD  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97       ***
      **************************************************************
      *
     FLETT      UF A E           K DISK
     FA4ARK     O    F   90        PRINTER
     FSYAP01FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  FETEXT                 1     40
     D  XDLT                   1      5
     D                 DS
     D  XARKB                  5      6  0
     D  XARKH                  8      9  0
     D  XARK                   1     10
     D                 DS
     D  XPAR1H                 1      2  0
     D  XPAR1D                 4      5  0
     D  XPAR2H                 7      8  0
     D  XPAR2D                10     11  0
     D  XPAR                   1     11
     C*
     C     TAGB1         TAG
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2LINE        YSLINE
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSI1        SETLL     LETT
     C     START1        TAG
     C     WSLETT        READE     LETT                                   94
     C  N94              UPDATE    LETTR
     C  N94              MOVE      WSLINE        YSLINE
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     WSLETT        READE     LETT                                   94
     C  N94              UPDATE    LETTR
     C  N94              MOVE      WSLINE        YSLINE
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      TAGB1
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSI3        SETLL     LETT
     C  N94WSLETT        READE     LETT                                   94
     C  N94              UPDATE    LETTR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C*
     C  N90WSLINE        COMP      *ZEROS                                 90
     C   90              MOVE      'SPL0107'     MSGNR
     C  N90KODSIK        CHAIN     LETT                               33
     C  N90
     CANN33              DO
     C                   UPDATE    LETTR
     C                   SETON                                        90
     C                   MOVE      'SPL0108'     MSGNR
     C                   END
     C   90              Z-ADD     21            POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     LETT                               33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    LETTR
     C   33              WRITE     LETTR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSXX        CHAIN     LETT                               33
     C  N33              DELETE    LETTR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        FILINE
     C                   MOVE      *ZEROS        FI0001
     C                   MOVE      *ZEROS        FI0002
     C                   MOVE      *ZEROS        FI0003
     C                   MOVE      *ZEROS        FI0004
     C                   MOVE      *BLANKS       FITEXT
     C*
     C                   MOVEL     '*DEL*'       XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSLINE
     C                   MOVE      *ZEROS        WS0001
     C                   MOVE      *ZEROS        WS0002
     C                   MOVE      *ZEROS        WS0003
     C                   MOVE      *ZEROS        WS0004
     C                   MOVE      *BLANKS       WSTEXT
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      FILINE        WSLINE
     C                   MOVE      FI0001        WS0001
     C                   MOVE      FI0002        WS0002
     C                   MOVE      FI0003        WS0003
     C                   MOVE      FI0004        WS0004
     C                   MOVE      FITEXT        WSTEXT
     C*
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     31            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C* FIX KEY IF NOT FOUND
     C   33              DO
     C                   MOVE      WSLINE        FILINE
     C                   END
     C*
     C* FIXES VARIABLES
     C                   MOVE      WS0001        FI0001
     C                   MOVE      WS0002        FI0002
     C                   MOVE      WS0003        FI0003
     C                   MOVE      WS0004        FI0004
     C                   MOVE      WSTEXT        FITEXT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C* KALLES KUN EN GANG
     C*
     C                   MOVEL     '0,0,  , '    XARK
     C                   MOVE      ','           XARK
     C* ZARKB              =    ARKBREDDE I TOMMER     (2,0)
     C* ZARKH              =    ARKHØYDE  I TOMMER     (2,0)
     C                   Z-ADD     12            XARKH
     C                   Z-ADD     9             XARKB
     C                   MOVEL     'A4ARK   '    XFNAME
     C                   MOVEL     '  .  ,  '    XPAR
     C                   MOVE      '.  '         XPAR
     C*
     C     *LIKE         DEFINE    FILINE        YSLINE
     C     KEYPRI        KLIST
     C                   KFLD                    FILETT
     C     KODSIK        KLIST
     C                   KFLD                    WSLETT
     C                   KFLD                    WSLINE
     C     KODSXX        KLIST
     C                   KFLD                    WSLETT
     C                   KFLD                    FILINE
     C     KODSI1        KLIST
     C                   KFLD                    WSLETT
     C                   KFLD                    W2LINE
     C     KODSI3        KLIST
     C                   KFLD                    WSLETT
     C                   KFLD                    YSLINE
     C*
     C*
     C     STI           TAG
     C                   MOVE      *ZEROS        WSFRA
     C                   EXFMT     BILDE1
     C   16WSLETT        IFNE      *ZEROS
     C                   MOVE      WSLETT        FILETT
     C                   EXSR      PRINT
     C                   GOTO      STI
     C                   END
     C     WSFRA         IFNE      *ZEROS
     C     WSFRA         SETLL     LETT
     C     *IN33         DOWEQ     '0'
     C     WSFRA         READE     LETT                                   33
     C  N33              MOVE      WSLETT        FILETT
     C  N33              WRITE     LETTR
     C                   END
     C                   END
     C   03              GOTO      UTI
     C*
     C                   MOVE      WSLETT        FILETT
     C     UTI           ENDSR
     C*
     C******************************************
     C     PRINT         BEGSR
     C******************************************
     C*
     C                   EXSR      ZSTART
     C*
     C* TEGNE FIRKANT RUNDT PÅ ARK
     C                   EXSR      ZLINE
     C* TEGNE RASTER
     C                   EXSR      ZRAST
     C*
     C                   Z-ADD     .3            XPAR1             4 2
     C                   Z-ADD     .5            XPAR2             4 2
     C                   EXSR      ZBOK
     C                   Z-ADD     5             XPAR1
     C                   Z-ADD     11.5          XPAR2
     C                   EXSR      ZPOS
     C                   MOVEL     'SYSTEMA '    XTEKS1           16
     C                   MOVE      'A/S     '    XTEKS1
     C                   MOVE      *BLANKS       XTEKST           80
     C                   MOVEL     XTEKS1        XTEKST           80
     C                   EXSR      ZTEXT
     C                   Z-ADD     .15           XPAR1             4 2
     C                   Z-ADD     .3            XPAR2             4 2
     C                   EXSR      ZBOK
     C                   Z-ADD     5             XPAR1
     C                   Z-ADD     11.0          XPAR2
     C                   EXSR      ZPOS
     C                   MOVEL     'Skipperg'    XTEKS1           16
     C                   MOVE      'ata 8-10'    XTEKS1
     C                   MOVEL     XTEKS1        XTEKS2           24
     C                   MOVE      '        '    XTEKS2
     C                   MOVE      *BLANKS       XTEKST           80
     C                   MOVEL     XTEKS2        XTEKST           80
     C                   EXSR      ZTEXT
     C                   Z-ADD     .15           XPAR1             4 2
     C                   Z-ADD     .3            XPAR2             4 2
     C                   EXSR      ZBOK
     C                   Z-ADD     5             XPAR1
     C                   Z-ADD     10.7          XPAR2
     C                   EXSR      ZPOS
     C                   MOVEL     '0152 OSL'    XTEKS1           16
     C                   MOVE      'O       '    XTEKS1
     C                   MOVE      *BLANKS       XTEKST           80
     C                   MOVEL     XTEKS1        XTEKST           80
     C                   EXSR      ZTEXT
     C                   Z-ADD     .15           XPAR1             4 2
     C                   Z-ADD     .3            XPAR2             4 2
     C                   EXSR      ZBOK
     C                   Z-ADD     5             XPAR1
     C                   Z-ADD     10.4          XPAR2
     C                   EXSR      ZPOS
     C                   MOVEL     'Tlf: 02-'    XTEKS1           16
     C                   MOVE      '331086  '    XTEKS1
     C                   MOVE      *BLANKS       XTEKST           80
     C                   MOVEL     XTEKS1        XTEKST           80
     C                   EXSR      ZTEXT
     C*
     C*
     C     KEYPRI        SETLL     LETT
     C     STPRI         TAG
     C     KEYPRI        READE     LETT                                   30
     C  N30              DO
     C                   Z-ADD     FI0001        XPAR1             4 2
     C                   Z-ADD     FI0002        XPAR2             4 2
     C                   EXSR      ZBOK
     C                   Z-ADD     FI0003        XPAR1
     C     12            SUB       FI0004        XPAR2
     C                   EXSR      ZPOS
     C                   MOVEL     FITEXT        XTEKST           80
     C                   EXSR      ZTEXT
     C*
     C                   GOTO      STPRI
     C                   END
     C                   EXSR      ZSLUTT
     C*
     C                   ENDSR
     C*
     C********************************************************************D260
     C     ZSTART        BEGSR
     C********************************************************************
     C                   EXCEPT    PR
     C*
     C*  ZSTART - DEFINERER ARKET UTSEENDE
     C*
     C                   MOVEL     'BEGSEG  '    XOPTIO            8
     C                   MOVEL     XARK          XPARM            80
     C*
     C                   EXSR      ZROUT
     C*
     C                   ENDSR
     C*
     C******************************************
     C     ZBOK          BEGSR
     C******************************************
     C*
     C* ZBOK   DEFINERER BOKSTAVSTØRRELSE
     C*
     C* PARAMETER 1  = BREDDE I TOMMER
     C* PARAMETER 2  = HØYDE  I TOMMER
     C*
     C                   MOVEL     'CHARSIZE'    XOPTIO
     C                   EXSR      ZMOVE
     C                   EXSR      ZROUT
     C*
     C                   ENDSR
     C*
     C******************************************
     C     ZMOVE         BEGSR
     C******************************************
     C*
     C* FLYTTER PARAMETRE TIL DATASTRUKTUR
     C*
     C                   MOVEL     XPAR1         XPAR1H
     C                   MOVE      XPAR1         XPAR1D
     C                   MOVEL     XPAR2         XPAR2H
     C                   MOVE      XPAR2         XPAR2D
     C                   MOVEL     XPAR          XPARM
     C*
     C                   ENDSR
     C*
     C******************************************
     C     ZPOS          BEGSR
     C******************************************
     C*
     C* PARAMETER 1 = TOMMER FRA VENSTRE
     C* PARAMETER 2 = TOMMER UNDENIFRA
     C*
     C                   MOVEL     'POSITION'    XOPTIO
     C                   EXSR      ZMOVE
     C                   EXSR      ZROUT
     C*
     C                   ENDSR
     C*
     C******************************************
     C     ZTEXT         BEGSR
     C******************************************
     C*
     C* FORTELLER HVILKEN TEKST SOM SKAL SKRIVES
     C*
     C                   MOVEL     'TEXT    '    XOPTIO
     C                   MOVEL     XTEKST        XPARM
     C                   EXSR      ZROUT
     C                   MOVE      *BLANKS       XTEKST
     C*
     C                   ENDSR
     C*
     C******************************************
     C     ZSLUTT        BEGSR
     C*
     C* AVSLUTTER UTSKRIFT PÅ SIDEN
     C*
     C******************************************
     C*
     C* FORTELLER AT DET ER SLUTT PÅ IPDF
     C*
     C                   MOVEL     'ENDSEG  '    XOPTIO
     C                   MOVEL     *BLANKS       XPARM
     C                   EXSR      ZROUT
     C                   EXCEPT    DETAPF
     C                   ENDSR
     C*
     C********************************************************************
     C     ZROUT         BEGSR
     C********************************************************************
     C*
     C* KALLER ASSEMBLERRUTINE FOR IPDF
     C*
     C                   CALL      'SUBR51'
     C                   PARM                    XFNAME            8
     C                   PARM                    XOPTIO
     C                   PARM                    XPARM
     C                   PARM                    XRTCDE            2
     C                   MOVE      *BLANKS       XPARM
     C*
     C                   ENDSR
     C*
     C*
     C********************************************************************
     C     ZLINE         BEGSR
     C********************************************************************
     C*
     C                   EXSR      ZTHICK
     C                   Z-ADD     1.5           XPAR1
     C                   Z-ADD     2             XPAR2
     C                   EXSR      ZPOS
     C                   Z-ADD     7             XPAR1
     C                   Z-ADD     2             XPAR2
     C                   EXSR      ZLINE2
     C                   Z-ADD     7             XPAR1
     C                   Z-ADD     8             XPAR2
     C                   EXSR      ZLINE2
     C                   Z-ADD     1.5           XPAR1
     C                   Z-ADD     8             XPAR2
     C                   EXSR      ZLINE2
     C                   Z-ADD     1.5           XPAR1
     C                   Z-ADD     2             XPAR2
     C                   EXSR      ZLINE2
     C                   ENDSR
     C******************************************
     C     ZLINE2        BEGSR
     C******************************************
     C*
     C* ZLINE2 TEGNER LINJER
     C*
     C* PARAMETER 1  = POSISJON  TIL
     C* PARAMETER 2  = POSISSJON TIL
     C*
     C                   MOVEL     'LINE    '    XOPTIO
     C                   EXSR      ZMOVE
     C                   EXSR      ZROUT
     C*
     C                   ENDSR
     C*
     C******************************************
     C     ZTHICK        BEGSR
     C*
     C*
     C******************************************
     C*
     C*
     C                   MOVEL     'LINEWDTH'    XOPTIO
     C                   MOVE      *BLANKS       XPARM
     C                   MOVEL     'THICK'       XPARM
     C                   EXSR      ZROUT
     C                   ENDSR
     C*
     C*
     C********************************************************************
     C     ZRAST         BEGSR
     C********************************************************************
     C*
     C*
     C                   MOVEL     'PATTYPE '    XOPTIO
     C                   MOVE      *BLANKS       XPARM
     C                   MOVEL     '7'           XPARM
     C                   EXSR      ZROUT
     C                   MOVEL     'BEGAREA '    XOPTIO
     C                   MOVE      *BLANKS       XPARM
     C                   EXSR      ZROUT
     C                   Z-ADD     1.5           XPAR1
     C                   Z-ADD     2             XPAR2
     C                   EXSR      ZPOS
     C                   Z-ADD     7             XPAR1
     C                   Z-ADD     2             XPAR2
     C                   EXSR      ZLINE2
     C                   Z-ADD     7             XPAR1
     C                   Z-ADD     8             XPAR2
     C                   EXSR      ZLINE2
     C                   Z-ADD     1.5           XPAR1
     C                   Z-ADD     8             XPAR2
     C                   EXSR      ZLINE2
     C                   Z-ADD     1.5           XPAR1
     C                   Z-ADD     2             XPAR2
     C                   EXSR      ZLINE2
     C                   MOVEL     'ENDAREA '    XOPTIO
     C                   MOVE      *BLANKS       XPARM
     C                   EXSR      ZROUT
     C                   ENDSR
     O*
     O* UTLISTING FOR 4224-PRINTER
     O*
     OA4ARK     E            DETAPF
     OA4ARK     E            PR
     O                                            1 '.'
