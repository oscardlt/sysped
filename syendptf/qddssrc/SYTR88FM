     A*%%TS  SD  20181122  145756  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 19 TH PTF8 NY HELP - FUNKSJON - KUN DDS
P8068A* 04 SH PTF10 RETTELSE TIL SUBFIL ATA
     A*        BRINGTMS-PROSJEKT
     A* 21 JOV PTF12 FARGE TURKIS ER FJERNET FRA PDA (VIA FIX-TUTST4=3 )
*****A**************************************************************
01   A* 78 REVERSE TURNUMMER 79 RED I TURNUMMER
02   A* 80 DSPATR RI I AVD
12   A* TURNR RØD = FARLIG, GUL = AVSLUTTET - HIGHLIGHT = MOTTATT GODSLISTE
XX   A* BILNUMMER 10 LANG
25   A* ÅPNE FOR 130 LANG OPSJONS LINJE + 6 X CMD PR LINJE/SWITCH
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYTR88PNL)
     A                                      HLPTITLE('ARBEIDE MED DISPONERING T-
     A                                      URER (TRANSPORTMODULEN)')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20181122  145756  SVEIN       REL-V7R1M0  5770-WDS
     A            TUETD          8Y 0H
     A            TUETA          8Y 0H
     A            TUATD          8Y 0H
     A            TUATA          8Y 0H
     A            TUDT           8Y 0H
     A            TUSJA1         8Y 0H
     A            TUSJN1        30A  H
     A            TUTST4         1A  H
SXX  A*           TUBILN        10A  H
     A            TUDTT          8Y 0H
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A  77                                  DSPATR(ND)
     A  77                                  DSPATR(PR)
     A            ISTAT1         1A  B  6  5
     A  98N77                               DSPATR(RI)
     A  77                                  DSPATR(ND)
     A  77                                  DSPATR(PR)
     A            ISTAT2         1A  B  6  7
     A  98N77                               DSPATR(RI)
     A  77                                  DSPATR(ND)
     A  77                                  DSPATR(PR)
     A            TUAVD          4Y 0O  6  9EDTCDE(4)
     A N80                                  DSPATR(RI)
     A N74 75
     AO 74                                  COLOR(RED)
     A  75                                  DSPATR(BL)
     A            TUPRO          8Y 0O  6 14EDTCDE(4)
     A N79                                  COLOR(YLW)
     A N81                                  COLOR(RED)
     A N78                                  DSPATR(RI)
SXX  A*           XXBILN         9A  O  6 23
     A            TUBILN        10A  O  6 23
     A  76N72                               COLOR(BLU)
     A  71                                  COLOR(PNK)
     A  72N76                               COLOR(YLW)
     A  72 76                               COLOR(TRQ)
     A  73                                  DSPATR(RI)
     A            XXSDF          5A  O  6 34COLOR(WHT)
     A            WSDATF         6Y 0O  6 40COLOR(WHT)
     A                                      EDTCDE(Z)
     A            TUTM      R        O  6 47REFFLD(TURERR/TUTM *LIBL/TURER)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A            XXSDT          5A  O  6 52COLOR(WHT)
     A            WSDATT         6Y 0O  6 58COLOR(WHT)
     A                                      EDTCDE(Z)
     A            TUTMT     R        O  6 65REFFLD(TURERR/TUTMT *LIBL/TURER)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A            TURUND         8Y 0O  6 70EDTCDE(4)
S12  A* 27                                  COLOR(YLW)
     A            TUTST1         1A  O  6 79
     A  77                                  COLOR(RED)
     A            TUST           1A  O  6 81
     A  77                                  COLOR(RED)
     A            TUTVKT    R        O  6 83REFFLD(TURERR/TUTVKT *LIBL/TURER)
     A                                      EDTCDE(3)
     A            TUTM3     R        O  6 93REFFLD(TURERR/TUTM3 *LIBL/TURER)
     A                                      EDTCDE(3)
     A            TUTLM2    R        O  6102REFFLD(TURERR/TUTLM2 *LIBL/TURER)
     A                                      EDTCDE(3)
     A            TUPOEN    R        O  6109REFFLD(TURERR/TUPOEN *LIBL/TURER)
     A                                      EDTCDE(Z)
     A            TURETH         3Y 0O  6115EDTCDE(L)
     A  82                                  COLOR(RED)
     A  83                                  COLOR(YLW)
     A            TUREEH         3Y 0O  6120EDTCDE(L)
     A  84                                  COLOR(RED)
     A  85                                  COLOR(YLW)
     A            TUANT1         5Y 0O  6125EDTCDE(Z)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20181122  145756  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(24)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A N35N38                               CF06(06 'LAGE NY')
     A                                      CF04(04 'SØKE BILNR')
     A                                      CF10(10 'TUR I ARBEID')
     A                                      CF12(12 'TIL SYEX10')
     A                                      CF11(11 'BEKREFTE ENDRING')
     A                                      CF23(23 'FLERE OPSJONER')
     A                                      CF24(24 'FLERE F-TASTER')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYTR880020 SYTR88PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SYTR880030 SYTR88PNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(SYTR880030 SYTR88PNL)
     A                                      HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(SYTR880030 SYTR88PNL)
     A                                      HLPARA(06 001 06 003)
     A          H                           HLPPNLGRP(SYTR880040 SYTR88PNL)
     A                                      HLPARA(04 005 04 007)
     A          H                           HLPPNLGRP(SYTR880040 SYTR88PNL)
     A                                      HLPARA(06 005 06 007)
     A          H                           HLPPNLGRP(SYTR880050 SYTR88PNL)
     A                                      HLPARA(04 009 04 074)
     A          H                           HLPPNLGRP(SYTR880050 SYTR88PNL)
     A                                      HLPARA(06 009 06 074)
     A          H                           HLPPNLGRP(SYTR880060 SYTR88PNL)
     A                                      HLPARA(04 075 04 080)
     A          H                           HLPPNLGRP(SYTR880060 SYTR88PNL)
     A                                      HLPARA(06 075 06 080)
     A          H                           HLPPNLGRP(SYTR880100 SYTR88PNL)
     A                                      HLPARA(05 001 05 080)
     A          H                           HLPPNLGRP(SYTR880600 SYTR88PNL)
     A                                      HLPARA(23 001 23 080)
     A*
     A*
     A            MSGC1V         7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR88'
     A                                      COLOR(BLU)
     A  36                              1 22MSGCON(030 DIV4033 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  35                              1 22MSGCON(030 DIV0712 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  38                              1 22MSGCON(030 JOV0259 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  37                              1 22MSGCON(030 DIV4034 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  35
     AO 38                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A N35N38                           2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  35
     AO 38                                  DSPATR(PR)
     A  96                                  DSPATR(HI)
     A N35N38                               DSPATR(UL)
     A  35
     AO 38                                  COLOR(WHT)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
S25  A*           MSGC1T        78A  O  3  2MSGID(&MSGC1V SYMSGF)
N25  A            MSGC1T       130A  O  3  2MSGID(&MSGC1V SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(131 YBC0091 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2ST2          1A  B  5  7DSPATR(UL)
     A            W2AVD          4Y 0B  5  9EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2PRO          8Y 0B  5 14EDTCDE(4)
     A                                      DSPATR(UL)
SXX  A*           W2BILN         9A  B  5 23DSPATR(UL)
     A            W2BILN        10A  B  5 23DSPATR(UL)
     A            W2STEF    R        B  5 34REFFLD(TURERR/TUSTEF *LIBL/TURER)
     A                                      DSPATR(UL)
     A            W2DATF         6Y 0B  5 40EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2STET    R        B  5 52REFFLD(TURERR/TUSTET *LIBL/TURER)
     A                                      DSPATR(UL)
     A                                  5 60MSGCON(009 YBC0096 *LIBL/SYMSGF)
     A            W2SONF         5A  B  5 70DSPATR(UL)
     A            W2SONT         5A  B  5 76DSPATR(UL)
      *
     A          R BUNN
     A                                      OVERLAY
     A            MSGB1T        18A  O 23  2MSGID(&MSGB1V SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB2T        18A  O 23 22MSGID(&MSGB2V SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB3T        18A  O 23 42MSGID(&MSGB3V SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB4T        18A  O 23 62MSGID(&MSGB4V SYMSGF)
     A                                      COLOR(BLU)
N25  A            MSGB5T        18A  O 23 82MSGID(&MSGB5V SYMSGF)
N25  A                                      COLOR(BLU)
N25  A            MSGB6T        18A  O 23102MSGID(&MSGB6V SYMSGF)
N25  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGB1V         7A  P
     A            MSGB2V         7A  P
     A            MSGB3V         7A  P
     A            MSGB4V         7A  P
N25  A            MSGB5V         7A  P
N25  A            MSGB6V         7A  P
      *
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A N35N38                               CF06(06 'LAGE NY')
     A                                      CF08(08 'SØKE TUR')
N26  A                                      CF10(10 'TUR I ARBEID')
     A                                      CF12(12 'TIL SYEX10')
     A                                      CF23(23 'FLERE OPSJONER')
     A                                      CF24(24 'FLERE F-TASTER')
     A                                      CSRLOC(LINNBR     POSNBR)
     A*
S19  A*         H                           HLPDOC('SYTR880020' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880020 SYTR88PNL)
     A                                      HLPARA(02 001 02 080)
     A*OPTION...
S19  A*         H                           HLPDOC('SYTR880030' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880030 SYTR88PNL)
     A                                      HLPARA(03 001 03 080)
     A*
S19  A*         H                           HLPDOC('SYTR880030' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880030 SYTR88PNL)
     A                                      HLPARA(04 001 04 003)
     A*
S19  A*         H                           HLPDOC('SYTR880030' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880030 SYTR88PNL)
     A                                      HLPARA(06 001 06 003)
     A*KONV STAT/DISPSTST
S19  A*         H                           HLPDOC('SYTR880040' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880040 SYTR88PNL)
     A                                      HLPARA(04 005 04 007)
     A*
S19  A*         H                           HLPDOC('SYTR880040' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880040 SYTR88PNL)
     A                                      HLPARA(06 005 06 007)
     A* AVD/TURNR....
S19  A*         H                           HLPDOC('SYTR880050' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880050 SYTR88PNL)
     A                                      HLPARA(04 009 04 074)
     A*
S19  A*         H                           HLPDOC('SYTR880050' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880050 SYTR88PNL)
     A                                      HLPARA(06 009 06 074)
     A* GJELDENDE KONV/DISPSTATSU
S19  A*         H                           HLPDOC('SYTR880060' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880060 SYTR88PNL)
     A                                      HLPARA(04 075 04 080)
     A*
S19  A*         H                           HLPDOC('SYTR880060' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880060 SYTR88PNL)
     A                                      HLPARA(06 075 06 080)
     A* SØKELINJE
S19  A*         H                           HLPDOC('SYTR880100' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880100 SYTR88PNL)
     A                                      HLPARA(05 001 05 080)
     A* HELP-TASTER
S19  A*         H                           HLPDOC('SYTR880600' 'SYTR88' 'SY400-
S19  A*                                     LO/TRANS')
N19  A          H                           HLPPNLGRP(SYTR880600 SYTR88PNL)
     A                                      HLPARA(23 001 23 080)
     A            MSGC1V         7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR88'
     A                                      COLOR(BLU)
     A  36                              1 22MSGCON(030 DIV4033 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  38                              1 22MSGCON(030 JOV0259 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  35                              1 22MSGCON(030 DIV0712 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  37                              1 22MSGCON(030 DIV4034 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  35
     AO 38                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A N35N38                           2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  35
     AO 38                                  DSPATR(PR)
     A  96                                  DSPATR(HI)
     A N35N38                               DSPATR(UL)
     A  35
     AO 38                                  COLOR(WHT)
N25  A*           MSGC1T        78A  O  3  2MSGID(&MSGC1V SYMSGF)
N25  A            MSGC1T       130A  O  3  2MSGID(&MSGC1V SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(131 YBC0091 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2ST2          1A  B  5  7
     A                                      DSPATR(UL)
     A            W2AVD          4Y 0B  5  9EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2PRO          8Y 0B  5 14EDTCDE(4)
     A                                      DSPATR(UL)
SXX  A*           W2BILN         9A  B  5 23DSPATR(UL)
NXX  A            W2BILN        10A  B  5 23DSPATR(UL)
     A            W2STEF    R        B  5 34REFFLD(TURERR/TUSTEF *LIBL/TURER)
     A                                      DSPATR(UL)
     A            W2DATF         6Y 0B  5 40EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2STET    R        B  5 52REFFLD(TURERR/TUSTET *LIBL/TURER)
     A                                      DSPATR(UL)
     A                                  5 60MSGCON(009 YBC0096 *LIBL/SYMSGF)
     A            W2SONF         5A  B  5 70DSPATR(UL)
     A            W2SONT         5A  B  5 76DSPATR(UL)
     A            MSGB1T        18A  O 23  2MSGID(&MSGB1V SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB2T        18A  O 23 22MSGID(&MSGB2V SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB3T        18A  O 23 42MSGID(&MSGB3V SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB4T        18A  O 23 62MSGID(&MSGB4V SYMSGF)
     A                                      COLOR(BLU)
N25  A            MSGB5T        18A  O 23 82MSGID(&MSGB5V SYMSGF)
N25  A                                      COLOR(BLU)
N25  A            MSGB6T        18A  O 23102MSGID(&MSGB6V SYMSGF)
N25  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGB1V         7A  P
     A            MSGB2V         7A  P
     A            MSGB3V         7A  P
     A            MSGB4V         7A  P
N25  A            MSGB5V         7A  P
N25  A            MSGB6V         7A  P
     A*
     A          R ARKTYP
     A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      CF03(12)
     A                                  1  2'SYTR88'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(025 JOV1596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(037 JOV1597 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  3 14EDTCDE(Z)
     A                                  4  2'Type.....:'
     A            SCATYP         2A  B  4 14DSPATR(UL)
     A                                  5  2MSGCON(012 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 19MSGCON(012 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
