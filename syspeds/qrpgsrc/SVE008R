     FSVEF      IF   E           K DISK
     FSVEH      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSVE008P   O    E             PRINTER OFLIND(*IN27) USROPN
     FFIRM      IF   E             DISK
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FARKIVP    O  A E           K DISK
     D                SDS
     D  ZUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEHASYAV         4
     C                   PARM                    UVEHASYOP         7
     C                   PARM                    CALLTYP           1
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVEH_SYAV     UVEH_SYAV
     C     *LIKE         DEFINE    SVEH_SYOP     UVEH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVEH_SYAV
     C                   KFLD                    UVEH_SYOP
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UVEH_SYAV
     C                   EVAL      UOPLNR=253
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
      *______________________________________
      * Hente firmaopplysninger fra spedisjon
      *""""""""""""""""""""""""""""""""""""""
     C                   READ      FIRM
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C     CALLTYP       IFEQ      'A'
     C                   CALL      'SVE008CA'
     C                   ELSE
     C                   EVAL      UCMD='OVRPRTF FILE(SVE008P) OUTQ('
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   ENDIF
     C                   OPEN      SVE008P
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs fakturor                                         SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVEH
     C                   WRITE     SVE008PA

     C     KEYA          SETLL     SVEF
     C     KEYA          READE     SVEF                                   51
     C     *IN51         DOWEQ     *OFF
     C                   EVAL      WVEF_FATX=SVEF_FATY
     C     WVEF_FATX     CAT       SVEF_FATX:1   WVEF_FATX
     C                   EVAL      WVEF_VAKD=SVEF_VAKD
     C                   EVAL      WVEF_VAKU=SVEF_VAKU
     C                   EVAL      WVEF_FABL=SVEF_FABL

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SVE008PA
     C                   ENDIF
     C                   WRITE     SVE008PB

     C     KEYA          READE     SVEF                                   51
     C                   ENDDO

     C                   WRITE     SVE008PZ
     C     CALLTYP       IFEQ      'C'
     C                   EXSR      ARKSR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *    Arkiver                                          ARKSR /
      /////////////////////////////////////////////////////////////
     C     ARKSR         BEGSR
      * SKAL DOKUMENT ARKIVERES ?
     C                   MOVE      'S4'          ARTYPE
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     ARKJN         ANDEQ     'J'

     C                   Z-ADD     SVEH_AVKN     ARKUND
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      UVEH_SYAV     ARAVD
     C                   MOVE      *ZEROS        AROPD
     C                   MOVE      WDT           ARDATE
     C                   MOVE      WTM           ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   MOVE      UVEH_SYOP     ARUNDE
     C                   MOVEL     'S4:'         ARUNDE
     C                   WRITE     ARKIVR
     C                   ENDIF
     C                   ENDSR
