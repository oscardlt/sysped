********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10155 * 01 CB  PTF10 PKI  Lag katalogstruktur
11071 * 02 CB  PTF11 Tlf till handlæggare
********************************************************************
     FSVT056D   CF   E             WORKSTN
     F                                     SFILE(SVT056DA:ZRECNO)
     FSVTHA     UF A E           K DISK
     FSVTHA1    IF   E           K DISK
     F                                     RENAME(SVTHAR:SVTHAR1)
     FKODTSF    IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    KOSFUN        XOSFUN
     C                   EVAL      XOSFUN='SF'
     C     *LIKE         DEFINE    *IN30         WIN30
     C                   MOVEL     '*MSGQ'       WOBJTYP          10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SFKEY         KLIST
     C                   KFLD                    XOSFUN
     C                   KFLD                    ZVTH_SYSG
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVT056DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SVTHA1
     C                   READ      SVTHA1                                 94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVT056DA
     C                   READ      SVTHA1                                 94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVT056DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT056DE
     C                   WRITE     SVT056DØ
     C                   WRITE     SVT056DC
     C                   EXFMT     SVT056DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVT056DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

     C                   CLEAR                   SVT056DD
     C                   MOVEA     '00000000'    *IN(30)
     C                   EVAL      ZVTH_PWD='*SAME     '

     C     SRBA1         TAG

      *____________
      * Blanke felt
      *""""""""""""
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT056DØ
     C                   EXFMT     SVT056DD
     C                   MOVEA     '00000000'    *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *_______
      * F4=Søk
      *"""""""
      *____________
      * Teste input-1
      *""""""""""""
      * signatur må vare utfyllt
     C     ZVTH_SYSG     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0005'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * signatur må finnes i SYSPED
     C     SFKEY         CHAIN     KODTSF                             30
     C                   IF        *IN30
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0005'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Namn må vare utfyllt
     C     ZVTH_NAMN     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0006'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Userid må vare utfyllt
     C     ZVTH_USID     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0007'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Userid må finnes
     C                   CALL      'EDI33C'
     C                   PARM                    ZVTH_USID
     C     *IN30         PARM                    WIN30
     C                   PARM                    WOBJTYP
     C                   IF        *IN30
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0007'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Anstællningsnummer måste vara utfylt
     C     ZVTH_ANN      IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN34=*ON
     C                   MOVE      'SVT0008'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Løsenord saknas
     C     ZVTH_PWD      IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN36=*ON
     C                   MOVE      'SVT0010'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Kontrollera løsenord och anvændarid mot OS
     C     ZVTH_PWD      IFNE      '*SAME     '
     C                   CALL      'SVT063R'
     C                   PARM      *BLANKS       SVTH_PWD
     C                   PARM                    ZVTH_PWD
     C                   PARM                    UERROR            7
     C     UERROR        IFNE      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN36=*ON
     C                   MOVE      UERROR        ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      SVTH_SYSG=ZVTH_SYSG
     C                   EVAL      SVTH_NAMN=ZVTH_NAMN
     C                   EVAL      SVTH_USID=ZVTH_USID
     C                   EVAL      SVTH_ANN=ZVTH_ANN
     C                   EVAL      SVTH_ANNN=ZVTH_ANNN
     C                   EVAL      SVTH_SMS=ZVTH_SMS
N02  C                   EVAL      SVTH_TLF=ZVTH_TLF
     C     ZVTH_PWD      IFNE      '*SAME     '
     C                   EVAL      SVTH_PWD=ZVTH_PWD
     C                   MOVE      WDT           SVTH_PWDD
     C                   ENDIF
     C                   EVAL      SVTH_LOCK=ZVTH_LOCK
     C                   WRITE     SVTHAR
N01   *______________________________________
N01   * Kontroller og ev bygg katalogstruktur
N01   *""""""""""""""""""""""""""""""""""""""
N01  C                   CALL      'SVT060C'
N01  C                   PARM                    SVTH_USID
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     SVT056DA

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVT056DA
      *____________
      * Flytte felt
      *""""""""""""
     C     SVTH_SYSG     CHAIN(N)  SVTHA                              53
     C                   MOVEA     '00000000'    *IN(30)
     C                   EVAL      ZVTH_SYSG=SVTH_SYSG
     C                   EVAL      ZVTH_NAMN=SVTH_NAMN
     C                   EVAL      ZVTH_USID=SVTH_USID
     C                   EVAL      ZVTH_ANN=SVTH_ANN
     C                   EVAL      ZVTH_ANNN=SVTH_ANNN
     C                   EVAL      ZVTH_SMS=SVTH_SMS
N02  C                   EVAL      ZVTH_TLF=SVTH_TLF
     C                   EVAL      ZVTH_PWD='*SAME     '
     C                   EVAL      ZVTH_LOCK=SVTH_LOCK

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT056DØ
     C                   EXFMT     SVT056DD
     C                   MOVEA     '00000000'    *IN(30)
     C     *IN12         IFEQ      '0'
      *_______
      * F4=Søk
      *"""""""
      *____________
      * Teste input
      *""""""""""""
      * signatur må vare utfyllt
     C     ZVTH_SYSG     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0005'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * signatur må finnes i SYSPED
     C     SFKEY         CHAIN     KODTSF                             30
     C                   IF        *IN30
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0005'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Namn må vare utfyllt
     C     ZVTH_NAMN     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0006'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Userid må vare utfyllt
     C     ZVTH_USID     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0007'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Userid må finnes
     C                   CALL      'EDI33C'
     C                   PARM                    ZVTH_USID
     C     *IN30         PARM                    WIN30
     C                   PARM                    WOBJTYP
     C                   IF        *IN30
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0007'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Anstællningsnummer måste vara utfylt
     C     ZVTH_ANN      IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN34=*ON
     C                   MOVE      'SVT0008'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Løsenord saknas
     C     ZVTH_PWD      IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN36=*ON
     C                   MOVE      'SVT0010'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Kontrollera løsenord och anvændarid mot OS
     C     ZVTH_PWD      IFNE      '*SAME     '
     C                   CALL      'SVT063R'
     C                   PARM                    SVTH_PWD
     C                   PARM                    ZVTH_PWD
     C                   PARM                    UERROR            7
     C     UERROR        IFNE      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN36=*ON
     C                   MOVE      UERROR        ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
     C   30              GOTO      SRBB1
     C                   ENDIF
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     ZVTH_SYSG     CHAIN     SVTHA                              53
     C                   EVAL      SVTH_NAMN=ZVTH_NAMN
     C                   EVAL      SVTH_USID=ZVTH_USID
     C                   EVAL      SVTH_ANN=ZVTH_ANN
     C                   EVAL      SVTH_ANNN=ZVTH_ANNN
     C                   EVAL      SVTH_SMS=ZVTH_SMS
N02  C                   EVAL      SVTH_TLF=ZVTH_TLF
     C     ZVTH_PWD      IFNE      '*SAME     '
     C                   EVAL      SVTH_PWD=ZVTH_PWD
     C                   MOVE      WDT           SVTH_PWDD
     C                   ENDIF
     C                   EVAL      SVTH_LOCK=ZVTH_LOCK
     C                   UPDATE    SVTHAR
N01   *______________________________________
N01   * Kontroller og ev bygg katalogstruktur
N01   *""""""""""""""""""""""""""""""""""""""
N01  C                   CALL      'SVT060C'
N01  C                   PARM                    SVTH_USID

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     SVTH_SYSG     CHAIN     SVTHA                              53
     C                   DELETE    SVTHAR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVT056DA
     C                   UPDATE    SVT056DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
