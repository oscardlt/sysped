*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A                                      HLPPNLGRP(HLPSCR SYFA63PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV AVDELINGER-
     A                                      S HODE PÅ FAKTURA')
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A          H                           HLPPNLGRP(SYFA630020 SYFA63PNL)
     A                                      HLPARA(01 001 18 080)
     A                                  1  2'SYSPED/SYFA63'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(055 DIV1472 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 66DATE
     A                                      EDTCDE(Y)
     A                                  3 66TIME
     A                                  9  2MSGCON(012 DIV1473 *LIBL/SYMSGF)
     A            WSNET          1A  I  9 24DSPATR(UL)
     A                                      VALUES(' ' 'E' 'T')
     A                                  9 31MSGCON(040 DIV1475 *LIBL/SYMSGF)
     A                                 11  2MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 24DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 17  2MSGCON(020 DIV1474 *LIBL/SYMSGF)
     A            WSAVDK         4Y 0B 17 24DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 15  2MSGCON(060 SHU1203 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 18  2MSGCON(020 SHU1204 *LIBL/SYMSGF)
     A            WSFRA          4Y 0B 18 24DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A            WSTIL          4Y 0B 18 32DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                 18 29'->'
     A                                 18 38MSGCON(040 SHU1205 *LIBL/SYMSGF)
     A                                 23 21MSGCON(020 SHU1206 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE02
     A                                      CF12(12 'Avbryt vedlikehold')
S01  A*         H                           HLPDOC('SYFA630030' 'SYFA63' 'SY400-
S01  A*                                     LO/MAINT')
N01  A          H                           HLPPNLGRP(SYFA630030 SYFA63PNL)
     A                                      HLPARA(01 001 11 080)
S01  A*         H                           HLPDOC('SYFA630040' 'SYFA63' 'SY400-
S01  A*                                     LO/MAINT')
N01  A          H                           HLPPNLGRP(SYFA630040 SYFA63PNL)
     A                                      HLPARA(12 001 23 080)
     A                                  1  2'SYSPED/SYFA63'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(055 DIV1472 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 66DATE
     A                                      EDTCDE(Y)
     A                                  3 66TIME
     A            WSHT1         79A  B  5  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSHT2         79A  B  6  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSHT3         79A  B  7  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSHT4         79A  B  8  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSHT5         79A  B  9  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSHT6         79A  B 10  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSHT7         79A  B 11  2DSPATR(UL)
     A                                      CHECK(LC)
     A                                 13  2MSGCON(030 JOV0265 *LIBL/SYMSGF)
     A            WSSTFR         2   B 13 34
     A                                 15  2MSGCON(050 DIV1477 *LIBL/SYMSGF)
     A            WSBT1         79A  B 17  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSBT2         79A  B 18  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSBT3         79A  B 19  2DSPATR(UL)
     A                                      CHECK(LC)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
