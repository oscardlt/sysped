*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12309 * 01 CB  PTF12 FEIL KOLONNER FOR BRUTTO OG NETTOVEKTER
     A*%%TS  SD  20200526  102904  SVEIN       REL-V7R3M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20200526  102904  SVEIN       REL-V7R3M0  5770-WDS
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A            KUNDNR         8Y 0O  5 12TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  5 21TEXT('Kundenavn')
     A            FYL            2S 0O  5 53
     A            ART            2S 0O  5 59
     A            BES            2S 0O  5 65
     A            TAN            2S 0O  5 71
S01  A*           NEV            2S 0O  5 77
S01  A*           BRV            2S 0O  5 83
N01  A            BRV            2S 0O  5 77
N01  A            NEV            2S 0O  5 83
     A            VER            2S 0O  5 89
     A            ANT            2S 0O  5 95
     A            STR           25   O  5100
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20200526  102739  SVEIN       REL-V7R3M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYNA14'
     A                                      COLOR(BLU)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 82TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  3MSGCON(129 SHU2055 *LIBL/SYMSGF)
     A                                  1 17MSGCON(050 SHU2056 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A*%%TS  SD  20200526  102904  SVEIN       REL-V7R3M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A*
     A                                  1  2MSGCON(129 SHU2055 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSKUND         8Y 0B  2 12EDTCDE(Z)
     A            KNAVN         30A  O  2 21COLOR(WHT)
     A            WSFYL          2S 0B  2 52
     A            WSART          2S 0B  2 58
     A            WSBES          2S 0B  2 64
     A            WSTAN          2S 0B  2 70
     A            WSBRV          2S 0B  2 76
     A            WSNEV          2S 0B  2 82
     A            WSVER          2S 0B  2 88
     A            WSANT          2S 0B  2 94
     A            WSSTR         25   B  2 99
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A*%%TS  SD  20161201  113811  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            W2KN           8S 0B  2  2DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(030 SHU1699 *LIBL/SYMSGF)
     A          R VAREL3
     A*%%TS  SD  20200526  102904  SVEIN       REL-V7R3M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(129 SHU2055 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KUNDNR         8Y 0O  2 12DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 21COLOR(WHT)
     A            FYL            2S 0O  2 52
     A            ART            2S 0O  2 58
     A            BES            2S 0O  2 64
     A            TAN            2S 0O  2 70
     A            BRV            2S 0O  2 76
     A            NEV            2S 0O  2 82
     A            VER            2S 0O  2 88
     A            ANT            2S 0O  2 94
     A            STR           25   O  2 99
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A*%%TS  SD  20200506  102559  SVEIN       REL-V7R3M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYNA14'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 76TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 17MSGCON(045 SHU2056 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  3MSGCON(129 SHU2055 *LIBL/SYMSGF)
