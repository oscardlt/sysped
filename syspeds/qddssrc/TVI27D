     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '4' '5' '6' '7')
     A            TRAVD          4Y 0O  5  7EDTCDE(4)
     A            TRTDN          7Y 0O  5 12EDTCDE(4)
     A            TRPRO          8Y 0O  5 20EDTCDE(4)
     A            TRMTY          1A  O  5 29
     A            TR068A         8S 0O  5 36
     A            TR068B         6Y 0O  5 46EDTCDE(4)
     A            TR068C         2Y 0O  5 54EDTCDE(4)
     A            TRMN           9Y 0O  5 59EDTCDE(Z)
     A            WSUTSK         9A  O  5 70COLOR(RED)
     A            TRST           1A  H
     A            TR068D         8S 0H
     A            TR068E         6S 0H
     A            TR068F         2S 0H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/TVI27R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20'Plukk meld. til utskrift'
     A                                      DSPATR(HI)
     A                                  2  2'Tariffør:'
     A            W2SG           3A  O  2 12DSPATR(UL)
     A                                      COLOR(WHT)
     A            KSINAV        16A  O  2 17DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 42'Meldingstype:'
     A            W2MTY          1A  O  2 56DSPATR(UL)
     A                                  3  2'Velg opsjon,   4=Slett  5=Se på  6-
     A                                      =Utskrift  7=Tvinn logg'
     A                                      COLOR(BLU)
     A                                  4  2'Opt  Avd. Tdeknr. Turnr.   M.type -
     A                                       M.dato  Sekvnr. V.nr. Meld.nr.'
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Frisk')
     A                                      CA08(08 'SØK')
     A                                      CA23(23 'Fjern alle feilmeld.')
     A                                      CA10(10 'Skriv alle')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2'F10=Skriv alle'
     A                                      COLOR(BLU)
     A                                  2 22'F23=Slett alle feilmeld.'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1993  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Tariffør:'
     A            W2SG           3A  B  1 12DSPATR(UL)
     A                                  1 22'Meldingstype:'
     A            W2MTY          1A  B  1 36DSPATR(UL)
     A                                      VALUES(' ' '1' '2' '3')
     A                                  1 39'1=FeilM.  2=U-attest  3=T-kvit.'
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARESL
     A                                      CA12(12 'Forrige')
     A                                      CA23(23 'Slett alle FM')
     A                                      ALARM
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2'Trykk F23 for å bekrefte sletting -
     A                                      av alle feilmeldinger eller F12 avb-
     A                                      ryt'
     A                                      COLOR(RED)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Frisk')
     A                                      CA08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/TVI27R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20'Plukk meld. til utskrift'
     A                                      DSPATR(HI)
     A                                  2  2'Tariffør:'
     A            W2SG           3A  O  2 12DSPATR(UL)
     A                                      COLOR(WHT)
     A            KSINAV        16A  O  2 17DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 42'Meldingstype:'
     A            W2MTY          1A  O  2 56DSPATR(UL)
     A                                  3  2'Velg opsjon,   4=Slett  5=Se på  6-
     A                                      =Utskrift  7=Tvinnlogg'
     A                                      COLOR(BLU)
     A                                  4  2'Opt  Avd. Tdeknr. Turnr.   M.type -
     A                                       M.dato  Sekvnr. V.nr. Meld.nr.'
     A                                      DSPATR(HI)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1993  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
