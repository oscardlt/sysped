      *
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXXA* 01 YBC PTF5 NYTT FELT FOR TELLINGSDATO
PXXXXA* 02 YBC PTF7 VAREGRUPPE OG VAREERIER
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 SVENSK GODSJOURNAL PÅ MAIL
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'Spørr')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL025R'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 YBC1308 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  3MSGCON(015 YBC1309 *LIBL/SYMSGF)
     A            WSGRUP         8Y 0B  3 20DSPATR(HI)
     A                                      EDTCDE(4)
     A N49        NVNGRU        30A  O  3 29COLOR(WHT)
     A  49                              3 29MSGCON(040 YBC1310 *LIBL/SYMSGF)
     A                                  4  3MSGCON(015 YBC1311 *LIBL/SYMSGF)
     A            WSKJED         8Y 0B  4 20DSPATR(HI)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  4 29COLOR(WHT)
     A                                  5  3MSGCON(015 YBC1312 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  5 20DSPATR(HI)
     A                                      EDTWRD('0      ')
     A                                  5 29MSGCON(015 YBC1313 *LIBL/SYMSGF)
     A                                  5 45MSGCON(035 JOV1250 *LIBL/SYMSGF)
     A                                      COLOR(PNK)
     A                                  6  2MSGCON(040 YBC1314 *LIBL/SYMSGF)
     A                                  6 45MSGCON(035 JOV1394 *LIBL/SYMSGF)
     A                                      COLOR(PNK)
     A                                  7  2MSGCON(030 YBC1315 *LIBL/SYMSGF)
     A            WSARTF        15A  B  7 34
     A                                  7 60MSGCON(015 YBC1316 *LIBL/SYMSGF)
     A                                  8  2MSGCON(030 YBC1317 *LIBL/SYMSGF)
     A            WSARTT        15A  B  8 34
     A                                  8 60MSGCON(021 YBC1318 *LIBL/SYMSGF)
     A                                  9  2MSGCON(030 YBC1319 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(030 YBC1320 *LIBL/SYMSGF)
     A            WSLF1          5A  B 10 34
     A            WSLF2         20A  B 10 40
     A                                 10 60MSGCON(015 YBC1316 *LIBL/SYMSGF)
     A                                 11  2MSGCON(030 YBC1321 *LIBL/SYMSGF)
     A            WSLT1          5A  B 11 34
     A            WSLT2         20A  B 11 40
     A                                 11 60MSGCON(021 YBC1318 *LIBL/SYMSGF)
     A                                 12  2MSGCON(030 YBC1319 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(030 YBC1322 *LIBL/SYMSGF)
     A            WSDATF         6Y 0B 13 34EDTCDE(4)
     A                                 13 41'/'
     A            WSDATT         6Y 0B 13 43EDTCDE(4)
     A                                 13 50MSGCON(021 YBC1318 *LIBL/SYMSGF)
     A                                 14  2MSGCON(030 JOV1395 *LIBL/SYMSGF)
     A            WSFORP         1A  B 14 34VALUES('J' 'N' ' ')
     A                                 14 38'(J/N)'
     A                                 14 50MSGCON(020 YBC1313 *LIBL/SYMSGF)
     A                                 15  2MSGCON(030 YBC1323 *LIBL/SYMSGF)
     A            WSDET          1A  B 15 34VALUES('J' 'N' ' ')
     A                                 15 38'(J/N)'
     A                                 16  2MSGCON(030 YBC1324 *LIBL/SYMSGF)
     A            WSBRUD         1A  B 16 34VALUES('J' 'N' ' ')
     A                                 16 38'(J/N)'
     A                                 17  2MSGCON(030 YBC1325 *LIBL/SYMSGF)
     A            WSSALD         1A  B 17 34VALUES(' ' '+' '0')
     A                                 17 38MSGCON(042 YBC1326 *LIBL/SYMSGF)
     A                                 18 38MSGCON(042 YBC1327 *LIBL/SYMSGF)
     A                                 19  2MSGCON(030 YBC1328 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            WSTYPE         3A  B 19 34DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 19 38MSGCON(030 YBC1329 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A                                 20  2MSGCON(030 YBC1330 *LIBL/SYMSGF)
     A            WSSORT         1A  B 20 34VALUES(' ' '1' '2' '3')
     A                                 20 38MSGCON(042 YBC1331 *LIBL/SYMSGF)
     A                                 21  2MSGCON(015 YBC1806 *LIBL/SYMSGF)
     A            WSAG01         5A  B 21 18
     A            WSAG02         5A  B 21 24
     A            WSAG03         5A  B 21 30
     A            WSAG04         5A  B 21 36
     A            WSAG05         5A  B 21 42
     A                                 21 52MSGCON(015 YBC1807 *LIBL/SYMSGF)
     A            WSEIER         5A  B 21 68
     A                                 22  2MSGCON(030 YBC1332 *LIBL/SYMSGF)
     A            WSBATC         1A  B 22 34VALUES('J' 'N')
     A                                 22 38'(J/N)'
N03  A  27                             22 52MSGCON(015 YBC2014 *LIBL/SYMSGF)
N03  A  27        WSSVGS         1A  B 22 68VALUES('J' 'N')
N03  A  27                             22 70'(J/N)'
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(030 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
