********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 YBC PTF7 FLERE SØKEBEGREPER. MYE ENDRINGER, INGEN MERKING
P8XXX * 02 YBC PTF8 VIS OGSÅ ANTALL EURO PALLER
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R BILDE01
     A                                      CA03(03 'Exit')
     A                                      CF04(04 'Spørr')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL042R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1498 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  5  2'Kun lagersted:'
     A            WSSTD1         5A  B  5 24DSPATR(UL)
     A            WSSTD2         5A  B  5 30DSPATR(UL)
     A            WSSTD3         5A  B  5 36DSPATR(UL)
     A            WSSTD4         5A  B  5 42DSPATR(UL)
     A            WSSTD5         5A  B  5 48DSPATR(UL)
     A                                  7  2'Kun lokasjonstype:'
     A            WSLLT1         3A  B  7 24DSPATR(UL)
     A            WSLLT2         3A  B  7 30DSPATR(UL)
     A            WSLLT3         3A  B  7 36DSPATR(UL)
     A            WSLLT4         3A  B  7 42DSPATR(UL)
     A            WSLLT5         3A  B  7 48DSPATR(UL)
     A                                  9  2'Kun med pallfaktor:'
     A            WSOMRF         1A  B  9 24DSPATR(UL)
     A                                      VALUES('J' 'N' 'B')
     A                                  9 28'(J/N/B)'
     A                                 23  2'F3=Avslutt   F4=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '5')
     A            XGKN           8  0H
     A            XLKN           8  0H
S02  A*           XGRUP         30A  O  5  5
S02  A*           XKUNDE        30A  O  5 38
S02  A*           XANT           5  0O  5 69EDTCDE(4)
N02  A            XGRUP         28A  O  5  5
N02  A            XKUNDE        30A  O  5 34
N02  A            XANT           5  0O  5 65EDTCDE(4)
N02  A            XANTEU         7  2O  5 71EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      OVERLAY
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/LL042R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1498 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  2  2MSGCON(29 YBC1499 SYMSGF)
     A            XANTT          7  0O  2 32EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  3  2'Velg opsjon:   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1500 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CA03(03 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL042R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1498 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  4  2MSGCON(78 YBC1500 SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
