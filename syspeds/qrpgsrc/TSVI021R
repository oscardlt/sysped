      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVI021R MODULE(*LIBL/TSVI021R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
     D WSUSER          S             10
     D ZVIV_ULKD       S              2
     D ZVIV_VATA       S             10
     D FOKD            S              3
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d uwtsat          S             10S 3
     d uwtfti          S              2
     d uwtmti          S              3
     d uwtmuc          S              3

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬vata¬ : ¬'+%trim(ZVIV_VATA)+'¬, '
        +'¬lk¬ : ¬'+%trim(ZVIV_ULKD)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"foreslagenformanskod" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta varukod/varukoder
      *
     C  N50              EXSR      SATS
     c     Error         ifeq      *blanks

     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬fokd¬ : ¬'+%trim(FOKD)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')


     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      ZVIV_ULKD = zhbgetvar('LK')
     C                   EVAL      ZVIV_VATA = zhbgetvar('VATA')
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C   50              eval      Error = 'Invalid userID'
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sats                                                SATS /
      *////////////////////////////////////////////////////////////
     C     SATS          BEGSR
N20  C     ZVIV_ULKD     IFEQ      'CH'
N20  C     ZVIV_ULKD     OREQ      'NO'
N20  C     ZVIV_ULKD     OREQ      'EU'
N20  C                   EVAL      FOKD='300'
     C                   CALL      'SVI303R'
     C                   PARM                    ZVIV_VATA
     C                   PARM                    ZVIV_ULKD
     C                   PARM      *BLANKS       UFOKD             4
     C                   PARM      *ZEROS        uwtsat
     C                   PARM      *BLANKS       uwtfti
     C                   PARM      *BLANKS       uwtmti
     C                   PARM      *BLANKS       uwtmuc

N21  C     uwtsat        IFEQ      *ZEROS
N21  C                   EVAL      FOKD='100'
N21  C                   ENDIF

N20  C                   ELSE

N20  C                   EVAL      FOKD='100'

N20  C                   ENDIF

     C                   ENDSR
