     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            XTIL2         70A  H      TEXT('TIL')
     A            XTIL3         70A  H      TEXT('TIL')
     A            XTIL4         70A  H      TEXT('TIL')
     A            XTIL5         70A  H      TEXT('TIL')
     A            XJOBNA        10A  H      TEXT('JOBNA')
     A            XJOBNO         6A  H      TEXT('JOBNO')
     A            XSNBRA         2A  H      TEXT('SNBRA')
     A            WSNR           1A  B  6  3VALUES(' ' '1' '2' '4' '5')
     A            XUSRDT        10A  O  6  7
     A            XNAVN         30A  O  6 18
     A            XUSER         10A  O  6 49
     A            XFILE         10A  O  6 60
     A            XTIL          70A  O  7  7COLOR(WHT)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/MAIL02'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 SHU1106 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(075 SHU1107 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(013 SHU1108 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 17MSGCON(030 SHU1114 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'søk kunde')
     A                                      SLNO(16)
     A                                      CLRL(09)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(075 SHU1107 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WUSRDT        10A  B  2  7DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      TEXT('LOSSESTEDKODE')
     A            WNAVN         30A  B  2 18DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                      TEXT('LOSSESTED')
     A            WUSER         10A  O  2 49
     A            WFILE         10A  O  2 60
     A                                  3  3MSGCON(006 SHU0976 *LIBL/SYMSGF)
     A            WTIL          70A  B  3 10DSPATR(UL)
     A                                      CHECK(LC)
     A            WTIL2         70A  B  4 10DSPATR(UL)
     A                                      CHECK(LC)
     A            WTIL3         70A  B  5 10DSPATR(UL)
     A                                      CHECK(LC)
     A            WTIL4         70A  B  6 10DSPATR(UL)
     A                                      CHECK(LC)
     A            WTIL5         70A  B  7 10DSPATR(UL)
     A                                      CHECK(LC)
     A  30        MSGTXT        78A  O  9  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              9  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2USER        10A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(075 SHU1107 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A            WUSRDT        10A  O  2  7DSPATR(PC)
     A                                      COLOR(WHT)
     A            WNAVN         30A  O  2 18COLOR(WHT)
     A            WUSER         10   O  2 49COLOR(WHT)
     A            WFILE         10   O  2 60COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/MAIL02'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 SHU1106 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(075 SHU1107 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
