      *********************************************************************
      * SPØRRING PÅ artikkels saldohistorikk
      *
CRTPG+*  CRTPGM SYSPED/LLS001D MODULE(*LIBL/LLS001D
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 02 YBC PTF11 VISE OGSÅ TOTAL AV RESERVERTE PLUKKLISTER
11XXX * 03 YBC PTF11 SORTERING PÅ DATO. SAMME DATO SORTERES MED LØPENR SOM LLWW01
11XXX * 04 JOV PTF11 Ta hensyn til evt overstyrt oppr-LK  (Danmark)
11XXX * 05 YBC PTF11 VIS ALLE RESERVERTE PLL UTEN SUMMERING
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FLLWW01    if   e           k disk    USROPN
     FLLAGJL01  if   e           k disk    USROPN
N04  FLLAGJFEU  if   e           k DISK    USROPN
     FLLAGJD01  if   e           k disk    USROPN
     FLLGRPK05  if   e           k disk    USROPN
     FLLMRF     if   e           k disk    USROPN
N01  FLLLISF    if   e           k disk    USROPN
     FLLHEAD    if   e           k disk    USROPN
     FHEADF     if   e           k disk    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          s             10
     D Artnr1          s             15
     D Saldo           s              7
     D SaldoN          s              7  0
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
N03  D                 DS
N03  D LWWDAT                  1      8  0
N03  D LWWLNR                  9     13  0
N03  D LWWDATLNR               1     13
N04  D                 DS
N04  D MRTRNR                  1     18
N04  D OriTx                  13     16                                         Ori:
N04  D OriLK                  17     18                                             PH
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      LoadHtml
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      *
S02  C*    LLWW01K1      setll     LLWW01
N02  C     LLWW01K1      setgt     LLWW01
      *
     C                   Z-ADD     SALDON        SaldEtt           7 0
N02  C                   Z-ADD     0             XRSVANT           7 0
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C     STHENTART     TAG
     C     LLWW01K2      readpe    LLWW01                                 50
      *
     C                   IF        *IN50 = *OFF
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTART
S05  C*                  IF        lwwdat = 99999999
S05  C*                  eval      SaldEtt = saldett - lwwant
S05  C*                  EVAL      XRSVANT = XRSVANT + lwwant
S05  C*                  ELSE
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTART
     C                   GOTO      STHENTART
     C                   END
S05  C*                  END
N02   *
S05  C*                  IF        XRSVANT <> 0
N05  C                   IF        XRSVANT <> 0 AND XRSVANT = 0
N02  C                   callp     updHTMLvar('LWWDAT':'9999.99.99')
N03  C                   callp     updHTMLvar('LWWDATLNR':'9999999999999')
N02  C                   IF        XSPRÅK = 'EN'
N02  C                   callp     updhtmlvar('LINTXT':
N02  C                             'Total pieces of reserved pickinglists')
N02  C                   ELSE
N02  C                   callp     updhtmlvar('LINTXT':
N02  C                             'Total antall av reserverte plukklister')
N02  C                   END
N02  C                   callp     updHTMLvar('LWWANT':
N02  C                             %trim(%editc(XRSVANT:'M')))
N02  C                   callp     updHTMLvar('SALDETT':
N02  C                             %trim(%editc(SALDON:'M')))
N02  C                   callp     updhtmlvar('XXREF':' ')
N02  C                   callp     updhtmlvar('XXGN':' ')
N02  C                   callp     updhtmlvar('XXLIS':' ')
N02  C                   callp     updhtmlvar('XXUNIPRI':' ')
N02  C                   callp     updhtmlvar('XXBATCH':' ')
N02  C                   callp     wrtsection('row')
N02  C                   END
      *
     C     SLHENTART     TAG
     C                   callp     wrtsection('tableend')
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
     C                   callp     wrtsection('*fini')
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      Artnr1  = zhbgetvar('Artnr1')
     C                   eval      saldo   = zhbgetvar('Saldo')
N01  C                   eval      SALDON  = c2n2(SALDO)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
OddEvC*                  callp     updHTMLvar('LogoImg':LogoImg)
      * KUNDE
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':'Search in warehouse')
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':'Søk i varelager')
     C                   END
     C                   callp     updhtmlvar('WSNA':BIBESK)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('Artnr1':Artnr1)
     C                   callp     updhtmlvar('Saldo':Saldo)
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetRow        begsr
      *
     C                   MOVE      *BLANKS       LINTXT           60
S01  C*                  MOVE      *BLANKS       XXGN             25
N01  C                   MOVE      *BLANKS       XXGN            100
     C                   MOVE      *BLANKS       XXREF            25
     C                   MOVE      *BLANKS       XXBATCH          25
N01  C                   MOVE      *BLANKS       XXUNIPRI         25
N01  C                   MOVE      *BLANKS       XXLIS           200
     C     LWWTYP        IFEQ      'I'
     C     LLJLKey       chain     LLagjL01                           33
     C  N33              movel     'Mot.rapp:'   TXT1             15
     C  N33              move      VAMRNR        TXT1
     C  N33              movel     TXT1          LINTXT
N01  C  N33              if        VAVERD <> 0
N01  C                   eval      XXUNIPRI=%trim(%editc(VAVERD:'M'))
N01  C                             +' '+VAFAR                                   VAFAR=Valuta
N01  C                   endif
     C                   IF        *IN33 = *OFF
     C                   MOVEL     VASENR        XXBATCH
     C     LLMRFK        CHAIN     LLMRF                              33
     C  N33              CAT       'Leverandør':3LINTXT
     C  N33              CAT       ':':0         LINTXT
     C  N33              CAT       MRLNA:0       LINTXT
     C  N33              EVAL      XXREF = MRLREF
     C  N33              EVAL      XXGN = MRGN
N01  C  N33              IF        MRGN<>*BLANKS
N04   * varens opprinnelse overstyrer det på Mrapp
N04  C                   move      *blanks       VAOPLK
n04  C     LLJLKey       chain     LLagjfEU                           33
N04  C                   if        OriTx='Ori:' and VAOPLK<>'  '
N04  c                   move      VAOPLK        OriLK
N04  C                   endif
N01  C                   EVAL      XXGN='<font color=red><b>'+MRGN+'</b></font>'
N01  C                             + ' ' +%trim(MRTRNR)
N01  C                   ENDIF
     C                   END
     C                   IF        XXGN = *BLANKS
     C     HEADFK2       CHAIN     HEADF                              33
     C  N33              EVAL      XXGN = HEGN
     C                   END
N01   * lisenser/mm på innlegget ??
N01  C     LWWMN         SETLL     LLLISF
N01  C     LWWMN         READE     LLLISF                                 33
N01  C     *IN33         DOWEQ     '0'
N01  c                   if        XXLIS=*blanks
N01  c                   eval      XXLIS= LIKD+':'+LITXT
N01  c                   else
N01  c                   eval      XXLIS= %trim(XXLIS)+', '+LIKD+':'+LITXT
N01  c                   endif
N01  C     LWWMN         READE     LLLISF                                 33
N01  C                   END
      * UTTAK ...
     C                   else
     C     LLJDKey       chain     LLagjD01                           33
     C  N33              movel     VDTEXT        LINTXT
     C     VDMNL         CHAIN     LLHEAD                             33
     C                   IF        *IN33 = *OFF
     C                   movel     'Plukksed:'   TXT1             15
     C                   move      LHALN         TXT1
     C                   movel     TXT1          LINTXT
     C                   CAT       VDTEXT:3      LINTXT
     C                   EVAL      XXREF = LHPAK
     C     HEADFK        CHAIN     HEADF                              33
     C     HETLL         IFNE      *blanks
     C  N33              EVAL      XXGN = HETLE+' / '+HETLL
     C                   END
     C                   END
     C                   end
      *
     C                   callp     updHTMLvar('LWWDAT':
     C                             %trim(%editw(Lwwdat:'    .  .  ')))
N03  C                   callp     updhtmlvar('LWWDATLNR':LWWDATLNR)
     C                   callp     updhtmlvar('LINTXT':lintxt)
     C                   callp     updHTMLvar('LWWANT':
     C                             %trim(%editc(Lwwant:'M')))
     C                   callp     updHTMLvar('SALDETT':
     C                             %trim(%editc(SaldEtt:'M')))
     C                   callp     updhtmlvar('XXREF':XXREF)
     C                   callp     updhtmlvar('XXGN':XXGN)
N01  C                   callp     updhtmlvar('XXLIS':XXLIS)
N01  C                   callp     updhtmlvar('XXUNIPRI':XXUNIPRI)
     C                   callp     updhtmlvar('XXBATCH':XXBATCH)
N01  C                   IF        LWWDAT <> 99999999 OR LWWTYP <> 'K'
     c                   sub       Lwwant        SaldEtt
N01  C                   END
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     LoadHtml      begsr
cccccC                   IF        XSPRÅK = 'EN'
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/LLS001DEN.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
cccccC                   ELSE
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/LLS001D.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
cccccC                   END
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   open      BRPARF                               50
     C                   open      LLWW01                               50
     C                   open      LLagjL01                             50
N04  C                   open      LLAGJFEU                             50
     C                   open      LLagjD01                             50
     C                   open      LLGRPK05                             50
     C                   open      LLMRF                                50
N01  C                   open      LLLISF                               50
     C                   open      LLHEAD                               50
     C                   open      HEADF                                50
      *
     c     LLJLKey       klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LWWMN
     c     LLJDKey       klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    LWWMN
     c                   kfld                    LWWVN
     C     LLMRFK        klist
     C                   kfld                    LWWGRU
     C                   kfld                    LWWKN
     C                   kfld                    VAMRNR
     C     LLWW01K1      klist
     C                   kfld                    LWWGRU
     C                   kfld                    LWWKN
     C                   kfld                    Artnr1
     C                   kfld                    XLWWDAT
     C     LLWW01K2      klist
     C                   kfld                    LWWGRU
     C                   kfld                    LWWKN
     C                   kfld                    Artnr1
     C     HEADFK        klist
     C                   kfld                    LHAVD
     C                   kfld                    LHOPD
     C     HEADFK2       klist
     C                   kfld                    VAAVD
     C                   kfld                    VAOPD
      *
     c     ReadKey       klist
     c                   kfld                    LWWGRU
     c                   kfld                    LWWKN
     c                   kfld                    ArtNr1
     c                   z-add     GKGRP         LWWGRU
     c                   z-add     BIKUNR        LWWKN
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     BIKUNR        chain     LLGrpK05                           33
     C     *LIKE         DEFINE    LWWDAT        XLWWDAT
     C                   EVAL      XLWWDAT = 99999999
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
      * Legg ut i arb.fil
     c                   move      LWWGRU        LWWGRUa           8
     c                   move      LWWKN         LWWKNa            8
     c                   MOVE      '1'           XXTØM             1
     C                   call      'LLWW01C'
     C                   PARM                    LWWGRUa
     C                   PARM                    LWWKNa
     C                   PARM                    ARTNR1
     C                   PARM                    XXTØM
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   close     BRIDF
     C                   close     LLWW01
     C                   close     LLagjL01
Nxxx C                   close     LLAGJFEU
     C                   close     LLagjD01
     C                   close     LLGRPK05
     C                   close     LLMRF
N01  C                   close     LLLISF
     C                   close     LLHEAD
     C                   close     HEADF
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
