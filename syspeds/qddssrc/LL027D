********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 OPSJON 2 FOR ENDRING AV MOTTAKSRAPORT
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 's¯king')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL027R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(035 YBC1373 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  5  2MSGCON(027 YBC1374 *LIBL/SYMSGF)
     A            WSGRUP         8Y 0B  5 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  5 41MSGCON(035 YBC1375 *LIBL/SYMSGF)
     A                                  7  2MSGCON(027 YBC1376 *LIBL/SYMSGF)
     A            WSPNR          8Y 0B  7 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  B  6  2VALUES(' ' '5' '6' '9' 'E')
N01  A            WSNR           1A  B  6  2VALUES(' ' '2' '5' '6' '9' 'E')
     A            MRMRNR         5Y 0O  6  5EDTCDE(4)
     A            MRLNA         30A  O  6 11
     A            MRDT           8Y 0H
     A            XMRDT          8Y 0O  6 42EDTWRD('  .  .    ')
     A            MRTM           4Y 0O  6 53EDTWRD('  :  ')
N01  A            VAAVD          4Y 0O  6 59EDTCDE(Z)
N01  A            VAOPD          7Y 0O  6 64EDTCDE(Z)
     A            MRLNR         17A  H
     A            MRLAD1        30A  H
     A            MRLAD2        30A  H
     A            MRLPN          4S 0H
     A            MRLAD3        24A  H
     A            XLAD3          8A  H
     A            MRLREF        25A  H
     A            MRKREF        25A  H
     A            MRFT1         30A  H
     A            MRFT2         30A  H
     A            MRGN          15A  H
     A            MRMOTT        15A  H
     A            MRTRNR        18A  H
     A            MRPTDF         4S 0H
     A            MRPTDT         4S 0H
     A            MRPSG         30A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA07(07 'MODUS')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/LL027R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(035 YBC1373 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 44SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(008 YBC1378 *LIBL/SYMSGF)
     A            WSPNR          8Y 0O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
S01  A*                                 3  2MSGCON(070 YBC1379 *LIBL/SYMSGF)
N01  A                                  3  2MSGCON(050 YBC1379 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N01  A  46                              3 54MSGCON(010 YBC2015 *LIBL/SYMSGF)
N01  A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 YBC1380 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '6')
     A            W2MRNR         5Y 0B  5  5EDTCDE(4)
     A          R BUNN
     A                                      OVERLAY
     A N46                             23  2MSGCON(78 YBC1381 SYMSGF)
     A                                      COLOR(BLU)
     A  46                             23  2MSGCON(78 YBC1382 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA07(07 'Modus')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL027R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(35 YBC1373 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(08 YBC1378 SYMSGF)
     A            WSKJED         8Y 0O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(50 YBC1379 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1380 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '6')
     A            W2MRNR         5Y 0B  5  5EDTCDE(4)
     A N46                             23  2MSGCON(78 YBC1381 SYMSGF)
     A                                      COLOR(BLU)
     A  46                             23  2MSGCON(78 YBC1382 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
      *
     A          R OVROPPD
     A                                      WINDOW(*DFT 9 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL027R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC1383 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(15 YBC1701 SYMSGF)
     A            WSMOD          1A  B  2 18DSPATR(UL)
     A                                      VALUES('A' 'B' 'H')
     A                                  2 20MSGCON(20 YBC1702 SYMSGF)
     A                                  3  2MSGCON(15 YBC1384 SYMSGF)
     A            WSAVD          4  0B  3 18DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(15 YBC1385 SYMSGF)
     A            WSSG           3A  B  4 18DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                  5  2MSGCON(15 YBC1703 SYMSGF)
     A            WSAVD2         4  0B  5 18DSPATR(UL)
     A  88                                  DSPATR(HI)
     A  88                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSOPD2         7  0B  5 23DSPATR(UL)
     A  88                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  6  2'PÂbegynt tid:'
     A            WSPTDF         4Y 0B  6 18DSPATR(UL)
     A  86                                  DSPATR(HI)
     A  86                                  DSPATR(PC)
     A  46                                  DSPATR(PR)
     A  46                                  COLOR(WHT)
     A                                      EDTWRD('0 .  ')
     A                                  7  2'Ferdig tid:'
     A            WSPTDT         4Y 0B  7 18DSPATR(UL)
     A  85                                  DSPATR(HI)
     A  85                                  DSPATR(PC)
     A  46                                  DSPATR(PR)
     A  46                                  COLOR(WHT)
     A                                      EDTWRD('0 .  ')
     A                                  8  2'Signatur:'
     A            WSPSG         30A  B  8 18DSPATR(UL)
     A  84                                  DSPATR(HI)
     A  84                                  DSPATR(PC)
     A  46                                  DSPATR(PR)
     A  46                                  COLOR(WHT)
     A N30                              9  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSG36        36A  O  9  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
      *
     A          R LEVDATO
     A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL027R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC1383 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2'Mottaksdato:'
     A            WSDT           8  0B  2 15DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(PC)
     A                                      EDTWRD('    .  .  ')
     A                                  2 27'(≈≈≈≈.MM.DD)'
     A                                  3  2'Mottakstid:'
     A            WSTM           4  0B  3 15DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        ZMSG36        36A  O  5  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
