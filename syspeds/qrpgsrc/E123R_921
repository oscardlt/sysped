     H DATEDIT(*YMD)
     FEUNH      IF   E           K DISK
     FEDIM      UF   E           K DISK
     FEDTM921   IF   E           K DISK
     FELIN921   IF   E           K DISK
     FEMOA921   IF   E           K DISK
     FEMEA921   IF   E           K DISK
     FEQTY921   IF   E           K DISK
     FENAD921   IF   E           K DISK
     FERFF921   IF   E           K DISK
     FECNT921   IF   E           K DISK
     FEALC921   IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FFRISOL01  IF   E           K DISK
     F                                     RENAME(FRISOKR:FRISOKL)
     FHEADF     IF   E           K DISK
     FKOSTT     UF   E           K DISK
     FKOSTA     O  A E             DISK
     FKOSTB     O  A E             DISK
     FE123P     O    E             PRINTER
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D TABA            S              3    DIM(34) CTDATA PERRCD(1)
     D TABB            S             35    DIM(34) ALT(TABA)
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  3      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  R1154                  1     35
     D  R11540                 1     10
     D                 DS
     D  DSMNN                  1      9  0
     D                 DS
     D  WFSSOK                 1     35
     D  WFSSO0                 1      9
     D  WFSSO1                 1      4
     D  WFSSO2                 5      9
     D                 DS            10
     D DIGITS          C                   CONST('0123456789')
     IEDIMR
     I              MMN                         TVIMMN
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N20              EXSR      RTVD
      *___________
      * UPDATE LOG
      *"""""""""""
     C                   MOVE      'C'           MST
     C                   UPDATE    EDIMR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     KEYMS3        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN3
     C                   KFLD                    ZREPD0
     C                   KFLD                    ZREPD1
     C                   KFLD                    ZREPD2
     C     KEYMS4        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN4
     C                   KFLD                    ZREPE0
     C                   KFLD                    ZREPE1
     C                   KFLD                    ZREPE2
     C                   KFLD                    ZREPE3
     C     KEYMS5        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN5
     C                   KFLD                    ZREPF0
     C                   KFLD                    ZREPF1
     C                   KFLD                    ZREPF2
     C                   KFLD                    ZREPF3
     C                   KFLD                    ZREPF4
     C     FRIKEY        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     FRIKE2        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     HEAKEY        KLIST
     C                   KFLD                    KBAVD
     C                   KFLD                    KBOPD
     C     *LIKE         DEFINE    TVIMMN        ZMMN
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NGN           ZGN3
     C     *LIKE         DEFINE    NGN           ZGN4
     C     *LIKE         DEFINE    NGN           ZGN5
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    NREP0         ZREPD0
     C     *LIKE         DEFINE    NREP1         ZREPD1
     C     *LIKE         DEFINE    NREP2         ZREPD2
     C     *LIKE         DEFINE    NREP0         ZREPE0
     C     *LIKE         DEFINE    NREP1         ZREPE1
     C     *LIKE         DEFINE    NREP2         ZREPE2
     C     *LIKE         DEFINE    NREP3         ZREPE3
     C     *LIKE         DEFINE    NREP0         ZREPF0
     C     *LIKE         DEFINE    NREP1         ZREPF1
     C     *LIKE         DEFINE    NREP2         ZREPF2
     C     *LIKE         DEFINE    NREP3         ZREPF3
     C     *LIKE         DEFINE    NREP4         ZREPF4
     C     *LIKE         DEFINE    NREP0         ZREPG0
     C     *LIKE         DEFINE    NREP1         ZREPG1
     C     *LIKE         DEFINE    NREP2         ZREPG2
     C     *LIKE         DEFINE    NREP3         ZREPG3
     C     *LIKE         DEFINE    NREP4         ZREPG4
     C     *LIKE         DEFINE    NREP5         ZREPG5
     C     *LIKE         DEFINE    NREP0         ZREPH0
     C     *LIKE         DEFINE    NREP1         ZREPH1
     C     *LIKE         DEFINE    NREP2         ZREPH2
     C     *LIKE         DEFINE    NREP3         ZREPH3
     C     *LIKE         DEFINE    NREP4         ZREPH4
     C     *LIKE         DEFINE    NREP5         ZREPH5
     C     *LIKE         DEFINE    NREP6         ZREPH6
     C     *LIKE         DEFINE    NREP0         ZREPI0
     C     *LIKE         DEFINE    NREP1         ZREPI1
     C     *LIKE         DEFINE    NREP2         ZREPI2
     C     *LIKE         DEFINE    NREP3         ZREPI3
     C     *LIKE         DEFINE    NREP4         ZREPI4
     C     *LIKE         DEFINE    NREP5         ZREPI5
     C     *LIKE         DEFINE    NREP6         ZREPI6
     C     *LIKE         DEFINE    NREP7         ZREPI7
     C     *LIKE         DEFINE    NREP0         ZREPJ0
     C     *LIKE         DEFINE    NREP1         ZREPJ1
     C     *LIKE         DEFINE    NREP2         ZREPJ2
     C     *LIKE         DEFINE    NREP3         ZREPJ3
     C     *LIKE         DEFINE    NREP4         ZREPJ4
     C     *LIKE         DEFINE    NREP5         ZREPJ5
     C     *LIKE         DEFINE    NREP6         ZREPJ6
     C     *LIKE         DEFINE    NREP7         ZREPJ7
     C     *LIKE         DEFINE    NREP8         ZREPJ8
     C     *ENTRY        PLIST
     C                   PARM                    DSMNA             9
     C                   MOVE      DSMNA         DSMNN
     C                   Z-ADD     DSMNN         ZMN
     C     ZMN           CHAIN     EUNH                               20
     C  N20              Z-ADD     ZMN           ZMMN
     C  N20ZMMN          CHAIN     EDIM                               20
      *DELAY THIS JOB
     C                   MOVEL(P)  'DLYJOB D'    QCMDR            15
     C                   CAT       'LY(180':0    QCMDR
     C                   CAT       ')':0         QCMDR
     C                   Z-ADD     15            QCMDLE           15 5
     C                   CALL      'QCMDEXC'                            3131
     C                   PARM                    QCMDR
     C                   PARM                    QCMDLE
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *___________________________
      * INIT AV FELT I PRINTERFILE
      *"""""""""""""""""""""""""""
     C                   CLEAR                   RCD000
     C                   MOVEL     U1004         X1004
      *______________
      * INIT AV KOSTA
      *""""""""""""""
     C                   CLEAR                   KOSTAR
     C                   MOVE      'NOK'         KAVAL
     C                   MOVEL     U1004         KAFNR
     C                   MOVE      'EDI'         KASG
     C                   MOVE      'F'           KAMVA
      *___________________
      * HENTE BILAGSNUMMER
      *"""""""""""""""""""
     C     'T'           CHAIN     KOSTT                              20
     C     *IN20         IFEQ      '0'
     C                   Z-ADD     KTNR          KABNR
     C                   ADD       1             KTNR
     C                   UPDATE    KOSTTR
     C                   ENDIF
      *___________________
      * READ MAIN SEGMENTS
      *"""""""""""""""""""
      * CNT00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        CHAIN     ECNT921                            51
     C     *IN51         IFEQ      '0'
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C     ZNUM          DIV       14            XDIV              4 1
     C                   ADD       1             XDIV
     C                   Z-ADD     XDIV          XPAG2
     C                   ENDIF
      * DTM00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     D2380         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     D2005         WHENEQ    '3  '
     C                   Z-ADD     ZNUM          KAFDT
     C     KAFDT         IFGT      501231
     C                   ADD       19000000      KAFDT
     C                   ELSE
     C                   ADD       20000000      KAFDT
     C                   END
     C                   MOVEL     D2380         X2380
     C                   ENDSL
      *
     C     KEYMS0        READE     EDTM921                                51
     C  N51              ENDDO
     C                   ENDIF
      * RFF01
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     1             ZGN0
     C     KEYMS0        SETLL     ERFF921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'SS '
     C                   MOVEL     R1154         KALKID
     C                   ENDSL
      *
     C     KEYMS0        READE     ERFF921                                51
     C  N51              ENDDO
     C                   ENDIF
      * NAD02
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ENAD921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     N3039         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     N3035         WHENEQ    'SE '
     C                   Z-ADD     ZNUM          KALNR
INT  C                   Z-ADD     1758          KALNR
     C                   MOVEL     N3039         Z3039
     C                   MOVEL     N3036A        Z3036
INT  C                   MOVEL(P)  'TOLLPOST'    Z3036
INT  C                   CAT       ' GLOBE':0    Z3036
     C                   MOVEL     N3251         Z3251
INT  C                   MOVEL     N3124A        Z3124             3
INT  C     Z3124         LOOKUP    TABA          TABB                     20
INT  C                   MOVEL     TABB          Z3042
     C                   MOVEL     N3164         Z3164
      *
     C     N3035         WHENEQ    'BY '
     C                   MOVEL     N3039         Å3039
     C                   MOVEL     N3036A        Å3036
     C                   MOVEL     N3251         Å3251
     C                   MOVEL     N3164         Å3164
     C     N3035         WHENEQ    'IV '
     C                   MOVEL     N3039         Å3039
     C                   MOVEL     N3036A        Å3036
     C                   MOVEL     N3251         Å3251
     C                   MOVEL     N3164         Å3164
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD921                                51
     C  N51              ENDDO
     C                   ENDIF
      * DTM08
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     8             ZGN0
     C     KEYMS0        SETLL     EDTM921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     D2380         A
     C                   EXSR      GETNUM
     C     D2005         IFEQ      '13 '
     C                   Z-ADD     ZNUM          KAFFDT
     C     KAFFDT        IFGT      501231
     C                   ADD       19000000      KAFFDT
     C                   ELSE
     C                   ADD       20000000      KAFFDT
     C                   END
     C                   MOVEL     D2380         X2380B
     C                   ENDIF
      *
     C     KEYMS0        READE     EDTM921                                51
     C  N51              ENDDO
     C                   ENDIF
      * MOA45
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     45            ZGN0
     C     KEYMS0        SETLL     EMOA921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMOA921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M5025         WHENEQ    '77 '
     C                   Z-SUB     ZNUM          KABL
     C                   MOVEL     M5004         X5004
     C                   ENDSL
      *
     C     KEYMS0        READE     EMOA921                                51
     C  N51              ENDDO
     C                   ENDIF
      * MOA47
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     47            ZGN0
     C     KEYMS0        SETLL     EMOA921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMOA921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M5025         WHENEQ    '150'
     C                   CLEAR                   KOSTBR
     C                   Z-ADD     KABNR         KBBNR
     C                   MOVE      'MOM'         KBVK
     C                   MOVE      'F'           KBKDPF
     C                   Z-ADD     ZNUM          KBBLHB
     C                   Z-ADD     9             KBKDM
     C                   WRITE     KOSTBR
     C                   MOVEL     M5004         X5004M
     C                   ENDSL
      *
     C     KEYMS0        READE     EMOA921                                51
     C  N51              ENDDO
     C                   ENDIF
      *___________
      * LAGE KOSTA
      *"""""""""""
     C                   WRITE     KOSTAR
      *______________________
      * SKRIV FAKTURA HEADING
      *""""""""""""""""""""""
     C                   Z-ADD     1             XPAG1
     C                   Z-ADD     0             XLIN              2 0
     C                   WRITE     RCD000
      *_________
      * READ LIN
      *"""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     22            ZGN0
     C     KEYMS0        SETLL     ELIN921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELIN921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ELIN921                                51
     C  N51              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________
      * INZ KOSTB
      *""""""""""
     C                   CLEAR                   KOSTBR
     C                   Z-ADD     KABNR         KBBNR
     C                   MOVE      'TGI'         KBVK
     C                   MOVE      'F'           KBKDPF
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     LREP0         ZREPB0
     C                   EXSR      MEA22
     C                   EXSR      QTY22
     C                   EXSR      MOA23
     C                   EXSR      RFF26
     C                   EXSR      NAD31
     C                   EXSR      ALC35
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     KOSTBR
      *____________________
      * SKRIV FAKTURA LINJE
      *""""""""""""""""""""
     C                   ADD       1             XLIN
     C     XLIN          IFGT      35
     C                   ADD       1             XPAG1
     C                   Z-ADD     0             XLIN
     C                   WRITE     RCD000
     C                   ENDIF
     C                   WRITE     RCD001                               99
     C     *IN99         IFEQ      '1'
     C                   ADD       1             XPAG1
     C                   Z-ADD     0             XLIN
     C                   SETOFF                                       99
     C                   WRITE     RCD000
     C                   END
     C                   CLEAR                   RCD001
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA23                                               MOA23 /
      *////////////////////////////////////////////////////////////
     C     MOA23         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     23            ZGN1
     C     KEYMS1        SETLL     EMOA921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMOA921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M5025         WHENEQ    '38 '
     C                   Z-ADD     ZNUM          KBBLHB
     C                   MOVEL     M5004         X5004C
     C     M5025         WHENEQ    '203'
     C                   MOVEL     M5004         X5004A
     C                   ENDSL
      *
     C     KEYMS1        READE     EMOA921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA38                                               MOA38 /
      *////////////////////////////////////////////////////////////
     C     MOA38         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     38            ZGN2
     C     KEYMS2        SETLL     EMOA921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EMOA921                                53
     C     *IN53         DOWEQ     '0'
      *
     C                   SELECT
     C     M5025         WHENEQ    '52 '
     C                   MOVEL     M5004         X5004B
     C                   ENDSL
      *
     C     KEYMS2        READE     EMOA921                                53
     C  N53              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA22                                               MEA22 /
      *////////////////////////////////////////////////////////////
     C     MEA22         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     22            ZGN1
     C     KEYMS1        SETLL     EMEA921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     M6311         WHENEQ    'AAU'
     C                   MOVEL     M6314         X6314A
     C     M6311         WHENEQ    'WT '
     C                   MOVEL     M6314         X6314B
     C                   ENDSL
      *
     C     KEYMS1        READE     EMEA921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * QTY22                                               QTY22 /
      *////////////////////////////////////////////////////////////
     C     QTY22         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     22            ZGN1
     C     KEYMS1        SETLL     EQTY921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EQTY921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     Q6063         WHENEQ    '100'
     C                   MOVEL     Q6060         X6060A
     C                   ENDSL
      *
     C     KEYMS1        READE     EQTY921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF26                                               RFF26 /
      *////////////////////////////////////////////////////////////
     C     RFF26         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     26            ZGN1
     C     KEYMS1        SETLL     ERFF921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'AAM'
     C                   MOVEL     R1154         FSSOK
     C                   MOVE      'IFB'         FSKODE
     C     FRIKEY        CHAIN     FRISOL01                           20
     C     *IN20         IFEQ      '0'
     C                   MOVE      FSAVD         KBAVD
     C                   MOVE      FSOPD         KBOPD
      *CHECK OM DET FINNES X-REF TIL ET ANNET OPPDRAG
     C     Z3124         IFNE      '001'
     C                   MOVE      'GIN'         FSKODE
     C     FRIKE2        SETLL     FRISOK
     C     FRIKE2        READE     FRISOK                                 21
     C     *IN21         IFEQ      '0'
     C                   MOVE      FSSOK         WFSSOK
     C     DIGITS        CHECK     WFSSO0                                 21
     C     *IN21         IFEQ      '0'
     C                   MOVE      WFSSO1        KBAVD
     C                   MOVE      WFSSO2        KBOPD
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     HEAKEY        CHAIN     HEADF                              20
     C     *IN20         IFEQ      '0'
     C                   MOVE      HEKNKF        KBKN
     C                   ELSE
     C                   Z-ADD     0             HEAVD
     C                   Z-ADD     0             HEOPD
     C                   ENDIF
     C                   ENDIF
     C                   MOVEL     R1154         X1154B
     C                   Z-ADD     RREP0         ZREPC0
     C                   Z-ADD     RREP1         ZREPC1
     C                   EXSR      DTM26
     C     R1153         WHENEQ    'CO '
     C                   MOVEL     R1154         X1154
     C                   ENDSL
      *
     C     KEYMS1        READE     ERFF921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD31                                               NAD31 /
      *////////////////////////////////////////////////////////////
     C     NAD31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     ENAD921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'DP '
     C                   MOVEL     N3036A        X3036A
     C                   MOVEL     N3164         X3164
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * ALC35                                               ALC35 /
      *////////////////////////////////////////////////////////////
     C     ALC35         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     35            ZGN1
     C     KEYMS1        SETLL     EALC921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EALC921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   Z-ADD     AREP0         ZREPC0
     C                   Z-ADD     AREP1         ZREPC1
     C                   EXSR      MOA38
      *
     C     KEYMS1        READE     EALC921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * DTM26                                               DTM26 /
      *////////////////////////////////////////////////////////////
     C     DTM26         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     26            ZGN2
     C     KEYMS2        SETLL     EDTM921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EDTM921                                53
     C     *IN53         DOWEQ     '0'
      *
     C*                    MOVELD2380     X2380C
      *
     C     KEYMS2        READE     EDTM921                                53
     C  N53              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
** TABELL FOR AVDELINGER INNOM TOLLPOST GLOBE
001OSLO
002ÅNDALSNES
003DRAMMEN
004STAVANGER
005BERGEN
006ÅLESUND
007TRONDHEIM
008BODØ
009SKIEN
016FREDRIKSTAD
022KONGSVINGER
023MJØSTERMINALEN
024ELVERUM
027OTTA
031SEM
038GOL
046KRISTIANSAND
051FØRDE
055HAUGESUND
057VOSS
058SOGNDAL
059STRYN
064MOLDE
065KRISTIANSUND
077STEINKJER
081STOKMARKNES
086MO I RANA
087MOSJØEN
089TANA BRU
090TROMSØ
093FINNSNES
094HARSTAD
095ALTA
096HAMMERFEST
