      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYMN0J MODULE(*LIBL/SYMN0J *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 02 JOV PTF11 TID/USER inn i USRSPC-DS
11XXX * 03 JOV PTF11 koder for opplasting
11XXX * 04 JOV PTF11 ikke kall rutine for opplastingskoder ved errir (ukjent user)
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPRF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
N01  FFIRM      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D BRUKERID        S             10
     D PASSORD         S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
      * State related variables (DS som legges i UsrSpc )
     D State           ds                  based(StateP)
     D  UBRID                        10
     D  UINNDT                        8
     D  UINNTM                        6
      * User space variables
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
      * Message ID for CrtUsrSpc
     D MsgId           s              7

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      *
     C                   OPEN      BRIDF
      *
     C                   EVAL      BRUKERID = zhbgetvar('USER')
     C                   EVAL      PASSORD = zhbgetvar('PWD')
     C                   eval      BRUKERID = uppify(BRUKERID)
     C                   eval      PASSORD  = uppify(PASSORD)
      *
      * Test innlogging
     C     BRUKERID      CHAIN     BRIDF                              50
N04  C     *IN50         IFEQ      '0'
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
N04  C                   ENDIF
     C     *IN50         IFEQ      '0'
     C                   OPEN      BRPRF
     C                   OPEN      BRPARF
N01  C                   OPEN      FIRM
     C                   ENDIF
     C*
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     c                   if        *in50 or BIPO<>PASSORD
     c                   eval      Error = '01 User or password incorrect'
     c                   endif
     c                   if        Error = *blanks and BIANTF >  4
     c                   eval      Error = '02 User deactivated, '
     c                             + 'too many attempts'
     c                   endif
     c                   if        Error = *blanks and BIAKT <> 'A'
     c                   eval      Error = '03 User deactivated '
     c                             + 'by system administrator'
     c                   endif
N01  c     Error         cabne     *blanks       wrtTop
      *
S01  c*                  if        Error = *blanks
N01  C     BRPARFK       klist
N01  C                   kfld                    BIBRID
N01  C                   kfld                    PAKUNR
N01  C                   kfld                    PAID
N01  c     KundKey       klist
N01  c                   kfld                    DFTUSER          10
N01  c                   kfld                    BIKUNR
N01  c                   kfld                    PAID
N01  c                   Eval      DFTUSER = '*KUNDFT*'+BIFIRM
N01  c     GrupKey       klist
N01  c                   kfld                    DFTUSER
N01  c                   kfld                    BIKNG
N01  c                   kfld                    PAID
N01  c     FirmKey       klist
N01  c                   kfld                    DFTUSER
N01  c                   kfld                    NULLKU            8 0
N01  c                   kfld                    PAID
N03   * FINNES ARKIVKODE PÅ USERNIVÅ / FIRMANIVÅ ??
N03  c     BrArkKey      klist
N03  c                   kfld                    PABRID
N03  c                   kfld                    NULLKU            8 0
N03  c                   kfld                    PAID
      * Hent språk
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   MOVEL     PAVRDA        XSPRÅK            2
     C                   MOVEL     'ASUSR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   MOVEL     PAVRDA        XASUSR           10
      * Hent Intern/Ekstern
     C                   MOVEL     'INTER'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XINTERN           1
      * Hent SYSPED Signatur
     C                   MOVEL     'ASSIG'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XSIGN             3
      * Minst ett menypunkt ?
S01   *
S01  C*    BIKUNR        setll     BRPRF
S01  C*    BIKUNR        READE     BRPRF                                  55
S01  C*    *IN55         IFEQ      '1'
S01  C*                  MOVE      'J'           XGRP              1
S01  C*    BIKNG         setll     BRPRF
S01  C*    BIKNG         READE     BRPRF                                  55
S01  C*                  END
N01   * På kunde-nr-nivå??
N01  C     BIKUNR        setll     BRPRF
N01  C     BIKUNR        READE     BRPRF                                  55
N01  C     *IN55         doweq     '0'
N01  C                   if        BPAKT='T' or BPAKT='B'
N01  C                   leave
N01  C                   endif
N01  C     BIKUNR        READE     BRPRF                                  55
N01  C                   enddo
N01   * Hvis ikke på kunde-nr-nivå - finnes det på gruppenivå??
N01  C                   if        *in55
N01  C     BIKNG         setll     BRPRF
N01  C     BIKNG         READE     BRPRF                                  55
N01  C     *IN55         doweq     '0'
N01  c                   if        BPAKT='T' or BPAKT='B'
N01  c                   leave
N01  C                   endif
N01  C     BIKNG         READE     BRPRF                                  55
N01  C                   enddo
N01  c                   endif
N01   * sjekk om brukeren har minst ett produkt, Test AKTKOD (T=Tomcat, B=Begge, A=kun Esped)
     c                   if        *in55
     c                   eval      Error = '04 User has no menu items'
N01  c                   goto      wrtTop
     c                   endif
S01  c*                  endif
      * tildel userspacename kun ved vellykket pålogging
S01  c*                  if        Error = *blanks
     C                   eval      UsrSpcName= CrtUsrSpc(
     C                               UsrSpcLib : StateP : MsgID : '*ALL')
S01  c*                  endif
N02   * Fyll opp UsrSpsDS med inndato/tid/user
N02   *     (må faktisk skje etter at CrtUsrSpc - hvis ikke så får en NOT ADRESSABLE..)
N02   *     (UsrSpc ser altså ut som å oppføre seg som en åpen UDS som dynemisk endres...)
N02  C                   CALL      'SYGE15C'
N02  C                   PARM                    UINNDT
N02  C                   PARM                    UINNTM
N02  C                   MOVE      BIBRID        UBRID
N01   * firmanavn / logo
N01  c     *loval        setll     FIRM
N01  c                   read      FIRM
N01   * Logo?  kode TLOSG  = TopLogo eSpedsg
N01  C*                  eval      Logo ='/syspeddev/systematop1.jpg'
N01  C                   MOVEL     'TLOSG'       PAID
N01  C     BRPARFK       chain     BRPARF                             33
N01  C   33KundKey       chain     BRPARF                             33
N01  C   33GrupKey       chain     BRPARF                             33
N01  C   33FirmKey       chain     BRPARF                             33
N01  c     *in33         ifeq      *off
N01  c     PAVRDA        andne     *Blanks
N01  c                   movel     PAVRDA        Logo             50
N01  c                   end
      * Banner ??   kode TLOSB  = TopLogo BANNER  eSpedsg
N01  C                   MOVEL     'TLOSB'       PAID
N01  C     BRPARFK       chain     BRPARF                             33
N01  C   33KundKey       chain     BRPARF                             33
N01  C   33GrupKey       chain     BRPARF                             33
N01  C   33FirmKey       chain     BRPARF                             33
N01  c     *in33         ifeq      *off
N01  c     PAVRDA        andne     *Blanks
N01  c                   movel     PAVRDA        Banner           50
N01  c                   end
      * Systemalogo/profilering til høyre ??   kode TLOSS  = TopLogo Systemalogo  eSpedsg
N01  c                   movel     'Y'           SyLogo           50
N01  C                   MOVEL     'TLOSS'       PAID
N01  C     BRPARFK       chain     BRPARF                             33
N01  C   33KundKey       chain     BRPARF                             33
N01  C   33GrupKey       chain     BRPARF                             33
N01  C   33FirmKey       chain     BRPARF                             33
N01  c     *in33         ifeq      *off
N01  c     PAVRDA        andne     *Blanks
N01  c                   movel     PAVRDA        SyLogo           50
N01  c                   end
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
     c     wrtTop        tag
     C                   callp     wrtsection('top')
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(BRUKERID)+'¬, '
N01     +'¬userName¬ : ¬'+%trim(BIBESK)+'¬, '
        +'¬custNr¬ : ¬'+%trim(%editc(BIKUNR:'Z'))+'¬, '
E01     +'¬custName¬ : ¬'+%trim(FIKRTN)+'¬, '
N01     +'¬logo¬ : ¬'+%trim(LOGO)+'¬, '
N01     +'¬banner¬ : ¬'+%trim(BANNER)+'¬, '
N01     +'¬systemaLogo¬ : ¬'+%trim(SYLOGO)+'¬, '
        +'¬usrLang¬ : ¬'+%trim(XSPRÅK)+'¬, '
        +'¬usrAS400¬ : ¬'+%trim(XASUSR)+'¬, '
        +'¬intern¬ : ¬'+%trim(XINTERN)+'¬, '
        +'¬signatur¬ : ¬'+%trim(XSIGN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, '
        +'¬usrSpcName¬ : ¬'+%trim(UsrSpcName)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"menuList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hent menypunkter kun dersom vellykket pålogging
S01  C*                  if        Error = *blanks
N01  c     Error         cabne     *blanks       wrtBot
N01   *
N01  C     BPKUNR        setll     BRPRF
N01  C     BPKUNR        READE     BRPRF                                  55
     C     *IN55         DOWEQ     '0'
N01  C                   if        BPAKT<>'T' and BPAKT<>'B'
N01  C                   goto      nextProd
N01  C                   endif
     C                   Add       1             AntLest
     c                   if        XSPRÅK='EN'
     c                   eval      BPTXT=BPTXTE
     c                   endif
      * Test om prod 1 inneholder http:// i så fall er det en klassisk URL
     C                   MOVE      *BLANKS       XBPPRD          200
     C     *like         DEFINE    BPPRD         BPPRDTmp
     C                   EVAL      BPPRDTmp = uppify(BPPRD)
     C     'HTTP://'     SCAN      BPPRDTmp                               45
     c                   if        *IN45=*on
     C                   eval      XBPPRD=%trim(BPPRD)+%trim(BIBRID)
     c                             +'&UsrSpcName=' + UsrSpcName + %trim(BPPRD2)
     C                   else
     C                   eval      XBPPRD=%trim(BPPRD)
     C                   endif
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬line¬ : ¬'+%trim(%editc(BPLIN:'Z'))+'¬, '
          +'¬col¬ : ¬'+%trim(%editc(BPKOL:'Z'))+'¬, '
          +'¬prog¬ : ¬'+%trim(XBPPRD)+'¬, '
          +'¬prTxt¬ : ¬'+%trim(BPTXT)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
N01  C     nextProd      tag
S01  C*    XGRP          IFEQ      'J'
S01  C*    BIKNG         READE     BRPRF                                  55
S01  C*                  ELSE
S01  C*    BIKUNR        READE     BRPRF                                  55
S01  C*                  END
N01  C     BPKUNR        READE     BRPRF                                  55
     C                   ENDDO
S01  c*                  endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
N01  c     wrtBot        tag
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
N04  C                   if        Error = *blanks
     C                   EXSR      ARKKoD
N04  C                   endif
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Lukk fil
     C                   CLOSE     BRIDF
     C     *IN50         IFEQ      '0'
     C                   CLOSE     BRPRF
     C                   CLOSE     BRPARF
N01  C                   CLOSE     FIRM
     C                   ENDIF
      * Quit
     C                   exsr      Exit
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
N03   **************************************************
N03  C     ARKKOD        BEGSR
N03   **************************************************
N03   * på oppdrag
N03  C                   MOVEL     'SGAKO'       PAID
N03   * listetopp
N03  c                   eval      JSONstring =', "arkivKodOpdList" : ['
N03  C                   callp     updHTMLvar('JSONstring':JSONstring)
N03  C                   callp     wrtsection('JSONlin')
N03   * listeinnhold
N03  c                   EXSR      ARKKOD2
N03   * listebunn
N03  c                   eval      JSONstring =']'
N03  C                   callp     updHTMLvar('JSONstring':JSONstring)
N03  C                   callp     wrtsection('JSONlin')
N03   *
N03   *
N03   * på tur
N03  c                   eval      JSONstring =', "arkivKodTurList" : ['
N03  C                   callp     updHTMLvar('JSONstring':JSONstring)
N03  C                   callp     wrtsection('JSONlin')
N03  C                   MOVEL     'SGAKT'       PAID
N03   * listeinnhold
N03  c                   EXSR      ARKKOD2
N03   * listebunn
N03  c                   eval      JSONstring =']'
N03  C                   callp     updHTMLvar('JSONstring':JSONstring)
N03  C                   callp     wrtsection('JSONlin')
N03   *
N03  C                   ENDSR
N03   **************************************************
N03  C     ARKKOD2       BEGSR
N03   **************************************************
N03  C                   move      *zeros        AntLest
N03  C                   MOVEL     BIBRID        PABRID
N03   * Innhold i arakivkode-list
N03  C     BrArkKey      setll     BRPARF
N03  C     BrArkKey      reade     BRPARF                                 33
N03  C   33              move      DFTUSER       PABRID
N03  C   33BrArkKey      setll     BRPARF
N03  C   33BrArkKey      reade     BRPARF                                 33
N03  C     *in33         doweq     '0'
N03  C                   ADD       1             AntLest
N03   /free
N03    if AntLest=1;
N03       JSONstring=*blanks;
N03       else;
N03       JSONstring=', ';
N03       endif;
N03    JSONstring=%trim(JSONstring)+'{'
N03       +'¬arkKod¬ : ¬'+%trim(%subst(PAVRDA:1:2))+'¬, '
N03       +'¬arkTxt¬ : ¬'+%trim(%subst(PAVRDA:4))+'¬}';
N03   /end-free
N03  C                   call      'JSONFIX'
N03  C                   parm                    JSONstring
N03  C                   callp     updHTMLvar('JSONstring':JSONstring)
N03  C                   callp     wrtsection('JSONlin')
N03  C     BrArkKey      reade     BRPARF                                 33
N03  C                   enddo
N03   * Ingen ? ZO  er hadkodet default ...
N03  C     Antlest       ifeq      0
N03   /free
N03     PAVRDA = 'ZO Opplastet dokument';
N03     JSONstring='{'
N03       +'¬arkKod¬ : ¬'+%trim(%subst(PAVRDA:1:2))+'¬, '
N03       +'¬arkTxt¬ : ¬'+%trim(%subst(PAVRDA:4))+'¬}';
N03   /end-free
N03  C                   call      'JSONFIX'
N03  C                   parm                    JSONstring
N03  C                   callp     updHTMLvar('JSONstring':JSONstring)
N03  C                   callp     wrtsection('JSONlin')
     C                   endif
N03   *
N03  C                   ENDSR
