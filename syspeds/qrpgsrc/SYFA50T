      *
      **************************************************************
      *** PROGRAM SKAL OPPDATERE MVA/TOLL PÅ FAKTURAREGISTER     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01-26                               ***
      *** LEDIGE INDIKATORER 27-30 34-47 49-99                   ***
      **************************************************************
      *
     FFAK2      UF A E           K DISK
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     C*
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C*
     C                   RETURN
     C*
     C******************************************
     C     HOVED         BEGSR
     C******************************************
     C     HEADFK        CHAIN     HEADF                              33
     C* BYGGER OPP FAKTURABILDET
     C     HEKDTM        IFEQ      'S'
     C                   MOVE      HEKNSF        KUNDNR
     C                   ELSE
     C     HEKDTM        IFEQ      'K'
     C                   MOVE      HEKNKF        KUNDNR
     C                   ELSE
     C                   MOVE      UKN           KUNDNR
     C                   END
     C                   END
     C*
     C                   MOVE      *ZEROS        XUTLGR            9 0
     C                   MOVE      *ZEROS        XLN               2 0
     C                   SETOFF                                       3132
     C*
     C     FAK2K         SETLL     FAK2                               33
     C*
     C     STLES         TAG
     C     FAK2K         READE     FAK2                                   33
     C   33              GOTO      UTLES
     C     FAOPKO        IFNE      ' '
     C     FALI          IFGT      XLN
     C                   Z-ADD     FALI          XLN
     C                   END
     C                   GOTO      STLES
     C                   END
     C*
     C     FAVK          IFEQ      'TOL'
     C                   SETON                                        31
     C                   DELETE    FAKTR
     C                   GOTO      STLES
     C                   END
     C     FAVK          IFEQ      'MVA'
     C                   SETON                                        31
     C                   DELETE    FAKTR
     C                   GOTO      STLES
     C                   END
     C     FALI          IFGT      XLN
     C                   Z-ADD     FALI          XLN
     C                   END
     C*
     C*** ---------------------------------------------
     C     HEKDTM        IFEQ      FASK
     C*** FJERNER UTLEGGSPOST
     C     FAVK          IFEQ      'UTL'
     C                   MOVE      FAKDM         XAKDM             1
     C                   DELETE    FAKTR
     C                   ELSE
     C*** ØKER UTLEGGSGRUNNLAG
     C     FAKDUL        IFEQ      'J'
     C                   ADD       FABELN        XUTLGR
     C                   END
     C                   END
     C                   END
     C*** ---------------------------------------------
     C*
     C                   GOTO      STLES
     C*
     C     UTLES         TAG
     C*
     C*** OPPDATERER FIL
     C***----------------------------------------------
     C     HEKDTM        IFNE      ' '
     C     HEKDTM        ANDNE     'I'
     C*** OPPDATERER UTLAGT TOLL
     C   31HEBELT        IFNE      0
     C                   MOVE      'TOL'         FAVK
     C                   Z-ADD     HEBELT        FABELV
     C                   Z-ADD     HEBELT        FABELN
     C                   ADD       HEBELT        XUTLGR
     C                   MOVE      'M'           FAKDA
     C                   MOVE      'N'           FAKDM
     C                   EXSR      OPDFAK
     C                   END
     C* OPPDATERER MOMSBELØP
     C   32HEBELM        IFNE      0
     C                   MOVE      'MVA'         FAVK
     C                   Z-ADD     HEBELM        FABELV
     C                   Z-ADD     HEBELM        FABELN
     C                   ADD       HEBELM        XUTLGR
     C                   MOVE      'M'           FAKDA
     C                   MOVE      'N'           FAKDM
     C                   EXSR      OPDFAK
     C                   END
     C                   END
     C***----------------------------------------------
     C*
     C* OPPDATERER UTLEGGS PROVISJON
     C*
     C     KUND1K        CHAIN     CUNDL01                            48
     C  N48              Z-ADD     SYMINU        XUTLMI
     C  N48SYUTLP        IFEQ      *ZEROS
     C                   MOVE      'B'           FAKDA
     C                   MOVE      FIUPS         SYUTLP            4 2
     C                   Z-ADD     FIUPM         XUTLMI            4 0
     C     AVUTPR        IFNE      *ZEROS
     C                   Z-ADD     AVUTPR        SYUTLP
     C                   END
     C     AVUTMI        IFNE      *ZEROS
     C                   Z-ADD     AVUTMI        XUTLMI
     C                   END
     C                   END
     C     SYUTLP        IFEQ      99.99
     C                   MOVE      'A'           FAKDA
     C                   MOVE      *ZEROS        SYUTLP
     C                   END
     C*
     C  N48XUTLGR        IFNE      *ZEROS
     C     HEKDTM        ANDNE     ' '
     C     SYUTLP        ANDNE     0
     C                   MOVE      'UTL'         FAVK
     C     XUTLGR        MULT      SYUTLP        FABELV
     C     FABELV        DIV(H)    100           FABELN
     C                   Z-ADD     FABELN        FABELV
     C     FABELN        IFLT      XUTLMI
     C                   Z-ADD     XUTLMI        FABELV
     C                   Z-ADD     XUTLMI        FABELN
     C                   END
     C     XAKDM         IFNE      *BLANK
     C                   MOVE      XAKDM         FAKDM
     C                   ELSE
     C                   MOVE      'N'           FAKDM
     C                   END
     C                   EXSR      OPDFAK
     C                   END
     C*
     C     SLLES         ENDSR
     C*
     C******************************************
     C     OPDFAK        BEGSR
     C******************************************
     C                   CLEAR                   FAKTR
     C                   ADD       1             XLN
     C                   MOVE      XLN           FALI
     C                   MOVE      HEAVD         FAAVD
     C                   MOVE      HEOPD         FAOPD
     C                   MOVE      HEKDTM        FASK
     C                   MOVE      FICURR        FAVAL
     C                   MOVE      'J'           FAKDUL
     C                   EXSR      DECI
     C                   WRITE     FAKTR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DECI          BEGSR
     C***********************************************
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  FABELN        FABEL0           11 0
     C                   Z-ADD     FABEL0        FABELN
     C                   Z-ADD(H)  FABELU        FABEL0           11 0
     C                   Z-ADD     FABEL0        FABELU
     C                   ELSE
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  FABELN        FABEL1           12 1
     C                   Z-ADD     FABEL1        FABELN
     C                   Z-ADD(H)  FABELU        FABEL1           12 1
     C                   Z-ADD     FABEL1        FABELU
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     FAK2K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    UAVD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UKN               8 0
     C*
     C                   MOVE      'V'           YVUNI             1
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     KODTVK        CHAIN     KODTV                              33
     C*
     C                   ENDSR
     C*
