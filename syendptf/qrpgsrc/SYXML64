     h dftactgrp(*no)
     fxmlrcvf   if   e           k disk
     ‚* Prototype for syxml64f
     d syxml64         pr
     d usn_                                like(usn)
     ‚* Prototype for 'SYXML64DKE'
     d syxml64dke      pr                  extpgm('SYXML64DKE')
     d usn_                                like(usn)
     ‚* Prototype for 'SYXML64DKI'
     d syxml64dki      pr                  extpgm('SYXML64DKI')
     d usn_                                like(usn)
     ‚* Prototype for 'SYXML64NOE'
     d syxml64noe      pr                  extpgm('SYXML64NOE')
     d usn_                                like(usn)
     ‚* Prototype for 'SYXML64NOI'
     d syxml64noi      pr                  extpgm('SYXML64NOI')
     d usn_                                like(usn)
     ‚* Prototype for 'SYXML64SEE'
     d syxml64see      pr                  extpgm('SYXML64SEE')
     d usn_                                like(usn)
     ‚* Prototype for 'SYXML64SEI'
     d syxml64sei      pr                  extpgm('SYXML64SEI')
     d usn_                                like(usn)
     ‚* *ENTRY Interface for Main Procedure
     d syxml64         pi
     d usn                            9
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d uxrsn           s              9P 0
     d xie             s              3
     d xland           s              2
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------
     c                   move      usn           uxrsn

      /free
       setll uxrsn xmlrcvf;
       reade uxrsn xmlrcvf;
       dow not %eof;
         select;
           // Landkode mottager ?
         when xrnv   = 'CountryCode';
           xland = xrverd;
           // Import eller eksport ?
         when xrnv   = 'Customsdirection';
           xie = xrverd;
           leave;
         endsl;
         reade uxrsn xmlrcvf;
       enddo;

       // Kall på riktigt program NO/DK/SE og I eller E
       select;

       when xland  = 'DK';
         if xie = 'EXP';
           syxml64dke ( usn );
         endif;
         if xie = 'IMP';
           syxml64dki ( usn );
         endif;

       when xland  = 'NO';
         if xie = 'EXP';
           syxml64noe ( usn );
         endif;
         if xie = 'IMP';
           syxml64noi ( usn );
         endif;

       when xland  = 'SE';
         if xie = 'EXP';
           syxml64see ( usn );
         endif;
         if xie = 'IMP';
           syxml64sei ( usn );
         endif;
       endsl;

       *inlr = *on;
      /end-free
