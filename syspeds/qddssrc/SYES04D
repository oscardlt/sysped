     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 75 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  1VALUES(' ' '1')
     A            ESKODE         5A  O  6  3
     A            KFTXT         50A  O  6  9COLOR(WHT)
     A            ESPROD        15A  O  6 60
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A  31                                  CA06(06 'LAGE NY')
     A                                      CA07(07 'SORT-KODE')
     A                                      CA09(09 'SORT-PROD')
     A                                      CA10(10 'VIS URL')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYES04D'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(045 JOV1499 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  1MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 38SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  1MSGCON(025 JOV1500 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N55                              4 27MSGCON(025 JOV1501 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              4 27MSGCON(025 JOV1503 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 60MSGCON(015 JOV1502 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  1VALUES(' ' '1')
     A            W2KODE         5A  I  5  3
     A            W2PROD        15A  I  5 60
     A          R BOTREC
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(14)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30 31                           7 19MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              7  3'F7=Kode-sort'
     A                                      COLOR(BLU)
     A N30N31                           7 19MSGCON(036 YBC0459 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              7 63'F9=Prod-sort'
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NEWREC
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A  31                                  CA06(06 'LAGE NY')
     A                                      CA07(07 'SORT-KODE')
     A                                      CA09(09 'SORT-PROD')
     A                                      CA10(10 'SORT-PROD')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYFAKDA'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(045 JOV0470 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(045 YBC0240 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  1VALUES(' ' '1')
     A            W2KODE         5A  I  5  3
     A            W2PROD        15A  I  5 61
     A N30 31                          20 19MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             20  3'F7=Kode-sort'
     A                                      COLOR(BLU)
     A N30N31                          20 19MSGCON(036 YBC0459 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             20 63'F9=Prod-sort'
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 20  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
