      *  Obs ! Kompiler med tillat error 20  GENLVL(20)    (decedit angitt 2 ganger-siste ignoreres)
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK541 MODULE(*LIBL/ESTK541 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF11 Ved UKJENT USER kan en ikke kalle jsonfix (manglende libl..)
10XXX * 02 JOV PTF11 SPRÅK innført
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 03 JOV PTF12 EDITCODE J er livsfarlig (komme ved over 1000.. "bryter"JSON) bytt til L
********************************************************************
     H decedit('0.')
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FTurer20   IF   E           K DISK    USROPN
     FFREETUR2  IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D EnvString       s          32000
     D JSONstring      s          32000
      * Prototype
     D JSONescape      PR                  ExtPgm('JSONESC')
     D   JSONstr                  32000A
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     D                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
      * Client input variables
     D userId          s             10
     D UsrSpcName      s             10                                         =
     D SJA             s              8
     D SJN             s              8  0
     D VERSJ           s             10
     D KunAnt          s              1
     D AntTur          s             10i 0
N02  D Sprak           s              2
     D ItemCount       s             10i 0
     D i               s             10i 0
     D i2              s             10i 0
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  TUDTA                  1      8
     D  TudtÅ                  1      4
     D  TudtM                  5      6
     D  TudtD                  7      8
      *
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * File open / keys
     C                   EXSR      INIT
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
      * Userid = PD+8 siffer (8 siffer er sjåførid)
     C                   eval      userId = zhbgetvar('userId')
     C                   move      userId        SJA
     C                   eval      SJN     = c2n2(SJA)
      * versjonsnr av klientprogramvare
     C                   eval      VERSJ = zhbgetvar('V')
     C                   eval      KunAnt= zhbgetvar('KunAnt')
     C                   eval      UsrSpcName= zhbgetvar('sessionId')
N02  C                   eval      Sprak = zhbgetvar('S')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     TURER20
     C                   CLOSE     FREETUR2
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      *
      *  Vis turvelger..
      /free
        JSONstring='{'
        +'¬userId¬ : ¬'+%trim(userId)+'¬, '
        +'¬error¬ : ¬¬, '
        +'¬turer¬ : [';
        callp JSONescape(JSONstring);
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');
      /end-free
     c                   move      'Y'           FØrsteTur         1
     C     TURKEY20      setll     TURER20
     C     TURKEY20      Reade     TURER20                                33
     C     *IN33         DOWEQ     '0'
     C                   move      TUPRO         TUPROA            8
     C     TUST          IFEQ      'P'                                          Protect= I arbeid
     C                   move      ' '           TUST                             = åpen
     c                   end
     C     TUST          IFEQ      ' '
     C                   Move      '*BT*'        TUSTA             4            Behold tur
     c                   else
     C                   Move      '*DT*'        TUSTA             4            Delet tur
     c                   end
      /free
       antTur = antTur + 1;
       if KunAnt <> 'J';
       if FØrsteTur = 'N';
         JSONstring=', ';
         updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
         wrtsection('JSONlin');
       endif;
         FØrsteTur='N';

        JSONstring='{'
        +'¬turNr¬ : ¬'     +TUPROA                               +'¬, '
        +'¬dato¬ : ¬'      +%trim(%editW(TUDT:'    .  .  '))     +'¬, '
        +'¬status¬ : ¬'    +%trim(TUSTA)                         +'¬, '
        +'¬antOrd¬ : '     +%trim(%editc(TUAO:'3'))              +', '
        +'¬vekt¬ : '       +%trim(%editc(TUTVKT:'3'))            +', '
E03     +'¬volum¬ : '      +%trim(%editc(TUTM3:'L'))             +', '
        +'¬fra¬ : ¬'       +%trim(TUSDF)                         +'¬, '
        +'¬til¬ : ¬'       +%trim(TUSDT)                         +'¬, ';
        exsr GetTurInfo;
        callp JSONescape(JSONstring);
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');
        endif;
      /end-free
     C     TURKEY20      Reade     TURER20                                33
     c                   end
      *
      *  avslutt turliste OG hele strengenn
      /free
        JSONstring='], '
         + '¬antTurer¬ : '     +%trim(%editc(AntTur:'3'))        +'}';
        callp JSONescape(JSONstring);
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');
      /end-free
      *
     C
     C                   endsr
      *
      *
      *=====================================================================******
      *  TURINFO fra notisblokk
      *=====================================================================******
     C     GetTurInfo    begsr
     C                   eval      JSONstring=%trim(JSONstring)
     C                             + '¬info¬ : ['
      *
     C                   MOVE      'G'           XXSKKO
     c                   move      'Y'           FØrsteFtx         1
     C     FREETURK      SETLL     FREETUR2
     C     FREETURK      READE     FREETUR2                               33
     C     *in33         doweq     '0'
     c                   if        FØrsteFtx = 'N'
     C                   eval      JSONstring=%trim(JSONstring)+', '
     c                   endif
     C                   eval      JSONstring=%trim(JSONstring)
     C                             + '¬'  +%trim(FRTTXT) +'¬'
     c                   move      'N'           FØrsteFtx
     C     FREETURK      READE     FREETUR2                               33
     c                   end
      * slutt fritekst og slutt denne tur
     C                   eval      JSONstring=%trim(JSONstring)+']}'
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     userId        CHAIN     BRIDF                              50
      * Ukjent UserID
     c     *in50         ifeq      '1'
     C                   close     BRIDF
      /free
        wrtsection('top');

        JSONstring='{'
        +'"userId" : "'+%trim(userId)+'", '
        +'"error" : "'+'UnKnown UserID'+'", '
        +'"turer" : [ ]}';
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');

        wrtsection('*fini');
      /end-free
     C                   eval      *inlr = *on
     C                   return
     c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * Kontoll av UsrSpcName (= sessionId)
      * pr juli 2014 - ingen test av korrekt UsrSpcName (lage variant av CHECK-USER ??)
     C*                  CALLB     PGMXXX
     C*                  PARM                    BIBRID
     C*                  PARM                    UsrSpcName
     C*                  PARM                    UINN              1
     C*                  IF        UINN = 'N'
     C* .....
     C*                  eval      *inlr = *on
     C*                  return
     C*                  END
      *
     C                   OPEN      TURER20
     C                   OPEN      FREETUR2
      * Key for TURER
     C     TurKey20      KLIST
     C                   KFLD                    SJN
     C                   KFLD                    xxTST4
     C                   MOVE      ' '           xxTST4            1
     C     FREETURK      KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
     C                   KFLD                    XXSKKO            1
      * Initier CrLF
     c                   movel     Hex0D         CrLf              2
     c                   move      Hex25         CrLf
      *
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
      * HENT NetCCSID (1208 = UTF8)
     c                   move      *blanks       NetCCSID         10
     C                   eval      NetCCSID   =getenv('CGI_ASCII_CCSID':
     C                             qusec)
     C                   eval      EnvString  =getenv('HTTP_COOKIE':
     C                             qusec)
     C                   eval      EnvString  =getenv('QUERY_STRING':
     C                             qusec)
     C                   eval      EnvString  =getenv('AUTH_TYPE':
     C                             qusec)
     C                   eval      EnvString  =getenv('CONTENT_TYPE':
     C                             qusec)
      *
     C     UtInit        endsr
      *
