      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM sysped/ESOVERSF MODULE(*LIBL/ESOVERSF
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
10XXX * 01 JOV PTF10 også interne bruker uiten change-rett kan filtrere på KUNDENR
10014 * 02 YBC PTF10 CURCOR SETTES TIL KUNDENR VED INTERN-BRUKERE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 AVSLUTT PROGRAM NÅR USER ER BLANK
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D UsrSpcName      s             10
     D lng             s              2
     D WSUSER          S             10
     D Dager           s              5
     D DagerN          s              5  0
     D MaxSN           s              5
     D MaxRC           s              5
      *
     D Idag            s              8  0
     D Dato8N          s              8  0
     D AddSub          s              3
     D Dato6A          s              6
     D TurNr           s              8
     D TurNrN          s              8  0
     D WINT            s              6
     D WINTEG          s              6  0
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  Dato8                  1      8  0
     D  Dato8Å                 1      4  0
     D  Dato8M                 5      6  0
     D  Dato8D                 7      8  0
     D                 DS
     D  Dato6                  1      6  0
     D  Dato6D                 1      2  0
     D  Dato6M                 3      4  0
     D  Dato6Å                 5      6  0
     D                 DS
     D  ParmOS1                1   1024
     D  OSCHG                  1      1
     D  OSOT16                 2     49
     D  OT                     2     49    DIM(16)
     D  OSST08               150    197
     D  STD                  150    197    DIM(8)
     D  AV                   198    247    DIM(10)
     D PGM             C                   CONST('*LIBL/CHECKUSR')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
N03  C                   IF        WSUSER = *BLANKS
N03  C                   exsr      SetSkl01B
N03  C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
N03  C                   ELSE
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
N01  C*                  IF        OSCHG = 'J'
N01  C                   IF        ((OSCHG = 'J') or (XINTERN = 'J'))
     C                   callp     wrtsection('intern')
     C                   END
     C                   callp     wrtsection('ekstern')
N03  C                   END
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      WINT    = zhbgetvar('WINT')
     C                   eval      WINTEG  = c2n2(WINT)
     C                   eval      DAGER    = zhbgetvar('DAGER')
     C                   eval      DAGERN   = c2n2(DAGER)
     C                   eval      MaxSn   = zhbgetvar('MaxSn')
     C                   eval      MaxRc   = zhbgetvar('MaxRc')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
      ****************************************************************************
     C     SetTop        begsr
      ****************************************************************************
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updHtmlVar('MaxSn':MaxSn)
     C                   callp     updHtmlVar('MaxRc':MaxRc)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('WSDTF':'20070101')
     C                   callp     updHTMLvar('WSDTT':'')
      * Hent dato fra og med. (legg ut også som hidden imput)
     C                   exsr      GetDato6A
     C                   callp     updHTMLvar('Dato':Dato6A)
     C                   callp     updHTMLvar('DatoH':Dato6A)
     C                   callp     updHtmlVar('wint':WINT)
N02  C                   callp     updHtmlVar('XINTERN':XINTERN)
      *
     C                   endsr
      *=====================================================================******
      * hent dato for x dager tilbake i tid
      *=====================================================================******
     C     GetDato6a     begsr
     C     DAGERN        IFEQ      *ZEROS
     C                   Z-ADD     7             DAGERN
     C                   END
     c                   move      *zeros        Idag
     c                   move      *zeros        Dato8N
     c                   move      'SUB'         AddSub
     C                   CALL      'SYGE20'
     C                   PARM                    AddSub
     C                   PARM                    Idag
     C                   PARM                    Dato8N
     C                   PARM                    DagerN
     c                   move      Dato8N        Dato8
     c                   move      Dato8Å        Dato6Å
     c                   move      Dato8M        Dato6M
     c                   move      Dato8D        Dato6D
     C                   move      Dato6         Dato6A
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'ESOVERSF'
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      * Key for CUNDL01
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     c                   movel     Bibesk        KNAVN            30
      *
     C                   IF        WINTEG   = 0
     C                   eval      WINTEG   = random(1:999999)
     C                   move      WINTEG        WINT
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   eval      *inlr = *on
      *
     C                   endsr
