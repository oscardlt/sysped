      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJOPOS2 MODULE(*LIBL/TJOPOS2 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR0)
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR17)
     FHEAD18    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR18)
     FBILLOF    IF   E           K DISK    usropn
     FBILLOFLG  IF   E           K DISK    usropn
     F                                     RENAME(BILLOFR:BILLOFG)
     FBLV1      IF   E           K DISK    usropn
     FBLJOINLF  IF   E           K DISK    USROPN
     F                                     RENAME(BILLOFR:BLJOIN0)
     F                                     RENAME(BLV1R:BLJOIN1)
     F                                     RENAME(BLV2R:BLJOIN2)
     FDOKUFM1   IF   E           K DISK    USROPN
     FTRACKT    IF   E           K DISK    usropn
     FBRPARF    IF   E           K DISK    usropn
     FFRISOK    IF   E           K DISK    usropn
     FFRISOL01  IF   E           K DISK    usropn
     F                                     RENAME(FRISOKR:FRISOKR1)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D WSUSER          s             10
     D BOOKREF         S             35
     D SENDREF         S             35
     D BLNR            S             16
     D CONTNR          S             17
     D GODSNR          S             15
     D XSTATUS         s             15
     D XFRBREVNR       s             35
     D WSCONTNR        s            500
     D KOLLIID         S             35
     D XXCMD           S            128
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     d jsonstring      s          32000
     d error           s            150
      *
     D xfskode         s              3
     D xcontnr         s              1
     D xnopd           s              3  0
     D xkomma          s              1
     D xvisopd         s              1
     d rowhead         s             10
     d odd             s             10
     d even            s             10
     d logoimg         s             50
     d xsprak          s              2
     d xintern         s              1
     d parmds1         s           1024
     d parmds2         s           1024
     d parmds3         s           1024
      * Message ID for CrtUsrSpc
     D MsgId           s              7
      * State related variables
     D State           ds                  based(StateP)
     D  UBRID                        10
     D  UINNDT                        8
     D  UINNTM                        6
     D* UAVD                1001   1080  0 DIM(20)
     D* UOPD                1081   1220  0 DIM(20)
     D                 DS
     D HEAVD                   1      4  0
     D FSAVD                   1      4  0
     D BLAVD                   1      4  0
     D BVAVD                   1      4  0
     D FMAVD                   1      4  0
     D                 DS
     D HEOPD                   1      7  0
     D FSOPD                   1      7  0
     D BLOPD                   1      7  0
     D BVOPD                   1      7  0
     D FMOPD                   1      7  0
     D                 DS
     D BLMN                    1     17
     D BVMN                    1     17
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     ‚* Prototype
     d jsonescape      pr                  extpgm('JSONESC')
     d   jsonstr                  32000a
     ‚*  Prototype for 'ESGETPAR'
     d esgetpar        pr                  extpgm('ESGETPAR')
     d bibrid_                             like(bibrid)
     d bifirm_                             like(bifirm)
     d bikunr_                             like(bikunr)
     d bikng_                              like(bikng)
     d rowhead_                            like(rowhead)
     d odd_                                like(odd)
     d even_                               like(even)
     d logoimg_                            like(logoimg)
     d xsprak_                             like(xsprak)
     d xintern_                            like(xintern)
     d parmds1_                            like(parmds1)
     d parmds2_                            like(parmds2)
     d parmds3_                            like(parmds3)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for bilibl
     d bilibl_001      pr                  extpgm(bilibl)
      /copy syspeds/qrpgsrcpy,prolog3

      /free
       // Get input /parse input string
       exsr parse;

       // File open / keys
       exsr init;

       exsr setall;

       // File close/exit
       exsr exit;


       *inlr = *on;
       return;

       //**************************
       begsr exit;
       //**************************
         // Do not delete the call to wrtsection with section name *fini.  It is needed
         // to ensure that all output html that has been buffered gets output.
         wrtsection('*fini');

         close bridf;
         close headf;
         close head17;
         close head18;
         close dokufm1;
         close billof;
         close billoflg;
         close blv1;
         close bljoinlf;
         close trackt;
         close brparf;
         close frisok;
         close frisol01;
       endsr;


       //**************************
       begsr setall;
       //**************************

         jsonstring='{'
         +'¬inp_user¬ : ¬'+%trim(WSUSER)+'¬, '
           +'¬inp_bookref¬ : ¬'+%trim(BOOKREF)+'¬, '
           +'¬inp_sendref¬ : ¬'+%trim(SENDREF)+'¬, '
           +'¬inp_blnr¬ : ¬'+%trim(BLNR)+'¬, '
           +'¬inp_contnr¬ : ¬'+%trim(CONTNR)+'¬, '
           +'¬inp_godsnr¬ : ¬'+%trim(GODSNR)+'¬, '
           +'¬inp_kolliid¬ : ¬'+%trim(KOLLIID)+'¬ ';
         exsr skrivjson;

         exsr tstinput;

         if error=*blanks;
         // liste-start ubetinget
         jsonstring=', ¬orderList¬ : [';
         exsr skrivjson;

         // oppdrag
         select;
         when BOOKREF <> *blanks;
            setll ( BOOKREF ) HEAD17;
            reade ( BOOKREF ) HEAD17;
         when SENDREF <> *blanks;
            setll ( XFSKODE : SENDREF ) FRISOL01;
            reade ( XFSKODE : SENDREF ) FRISOL01;
         when BLNR <> *blanks;
            setll ( BLNR ) BILLOFLG;
            reade ( BLNR ) BILLOFLG;
         when CONTNR <> *blanks;
            setll ( CONTNR ) BLJOINLF;
            reade ( CONTNR ) BLJOINLF;
         when GODSNR <> *blanks;
            setll ( GODSNR ) HEAD18;
            reade ( GODSNR ) HEAD18;
         when KOLLIID <> *blanks;
            setll ( KOLLIID ) DOKUFM1;
            reade ( KOLLIID ) DOKUFM1;
         endsl;

         dow not %eof;

            exsr srpost;

            select;
            when BOOKREF <> *blanks;
               reade ( BOOKREF ) HEAD17;
            when SENDREF <> *blanks;
               reade ( XFSKODE : SENDREF ) FRISOL01;
            when BLNR <> *blanks;
               reade ( BLNR ) BILLOFLG;
            when CONTNR <> *blanks;
               reade ( CONTNR ) BLJOINLF;
            when GODSNR <> *blanks;
               reade ( GODSNR ) HEAD18;
            when KOLLIID <> *blanks;
               reade ( KOLLIID ) DOKUFM1;
            endsl;
         enddo;

         // avslutt liste ubetinget
         jsonstring=']';
         exsr skrivjson;

         endif;

    ‚    //avslutning
         jsonstring=', ¬errMsg¬ : ¬'+%trim(error)+'¬ }';
         exsr skrivjson;

       endsr;

       //**************************
       begsr parse;
       //**************************
         // Parse variables from QUERY_STRING environment variable
         WSUSER  = zhbgetvar('USER');
         WSUSER  = %xlate(LC:UC:WSUSER);
         // Div filterparamtere...
         BOOKREF = zhbgetvar('BOOKREF');
         BOOKREF = %xlate(LC:UC:BOOKREF);
         SENDREF = zhbgetvar('SENDREF');
         SENDREF = %xlate(LC:UC:SENDREF);
         BLNR    = zhbgetvar('BLNR');
         BLNR    = %xlate(LC:UC:BLNR);
         CONTNR  = zhbgetvar('CONTNR');
         CONTNR  = %xlate(LC:UC:CONTNR);
         GODSNR  = zhbgetvar('GODSNR');
         GODSNR  = %xlate(LC:UC:GODSNR);
         KOLLIID = zhbgetvar('KOLLIID');
         KOLLIID = %xlate(LC:UC:KOLLIID);
       endsr;

       //**************************
       begsr tstinput;
       //**************************

       endsr;

       //**************************
       begsr srpost;
       //**************************
       BLBLNR = *BLANKS;
       BLCH   = *BLANKS;
       TXETDD = 0;
       TXATDD = 0;
       TXETAD = 0;
       TXATAD = 0;

       chain (HEAVD:HEOPD) HEADF;
       if HEST = 'S';
          XVISOPD = 'N';
       else;
          XVISOPD = 'J';
       endif;

       if XNOPD < 20;
          XNOPD = XNOPD + 1;
        //  UAVD(XNOPD) = HEAVD;
        //  UOPD(XNOPD) = HEOPD;
       endif;

       XFRBREVNR = *BLANKS;
       if  BOOKREF = *BLANKS AND SENDREF <> *BLANKS;
          XFRBREVNR = SENDREF;
       else;
          setll ( HEAVD : HEOPD ) FRISOK;
          reade ( HEAVD : HEOPD ) FRISOK;
          dow not %eof;
             if FSKODE = 'IFB';
                XFRBREVNR = FSSOK;
                LEAVE;
             endif;
             reade ( HEAVD : HEOPD ) FRISOK;
          enddo;
       endif;

       chain ( HEAVD : HEOPD ) BILLOF;
       chain ( HEAVD : HEOPD ) TRACKT;

       XCONTNR = 'N';
       WSCONTNR = BLMN;
       setll ( HEAVD : HEOPD ) BLV1;
       reade ( HEAVD : HEOPD ) BLV1;
       dow not %eof;
          if BVMN <> *BLANKS;
             XCONTNR = 'J';
             if WSCONTNR = *BLANKS;
                WSCONTNR = %TRIM(BVMN);
             else;
                WSCONTNR = %TRIM(WSCONTNR) +','+ %TRIM(BVMN);
             endif;
          endif;
          reade ( HEAVD : HEOPD ) BLV1;
       enddo;

       select;
       when TXATAD <> 0;
          XSTATUS = 'Arrived';
       when TXATDD <> 0;
          XSTATUS = 'Shipped';
       when BLMM = 'New';
          XSTATUS = 'New';
       when BLMM = 'Updated';
          XSTATUS = 'Updated';
       other;
          select;
          when BVMN <> *BLANKS and BVMN <> '?';
             XSTATUS = 'Confirmed';
          when TXETDD <> 0;
             XSTATUS = 'Booked';
          when XCONTNR = 'N';
             XSTATUS = 'Pending';
          other;
             XSTATUS = 'Pending';
          endsl;
       endsl;

       if XVISOPD = 'J';
       if Xkomma  = ' ';
          jsonstring=*blanks;
          Xkomma  = 'J';
       else;
          jsonstring=', ';
       endif;
          jsonstring=%trim(jsonstring)+'{'
           +'¬heavd¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬,'
           +'¬heopd¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬,'
           +'¬hepro¬ : ¬'+%trim(%editc(HEPRO:'Z'))+'¬,'
           +'¬herfa¬ : ¬'+%trim(HERFA)+'¬, '
           +'¬xfrbrevnr¬ : ¬'+%trim(XFRBREVNR)+'¬, '
           +'¬henas¬ : ¬'+%trim(HENAS)+'¬, '
           +'¬henak¬ : ¬'+%trim(HENAK)+'¬, '
           +'¬hesdf¬ : ¬'+%trim(HESDF)+'¬, '
           +'¬hesdt¬ : ¬'+%trim(HESDt)+'¬, '
           +'¬hegn¬ : ¬'+%trim(HEGN)+'¬, '
           +'¬hetll¬ : ¬'+%trim(HETLL)+'¬, '
           +'¬hetle¬ : ¬'+%trim(HETLE)+'¬, '
           +'¬status¬ : ¬'+%trim(XSTATUS)+'¬, '
           +'¬st¬ : ¬'+%trim(XSTATUS)+'¬, '
           +'¬wscontnr¬ : ¬'+%trim(WSCONTNR)+'¬, '
           +'¬con¬ : ¬'+%trim(WSCONTNR)+'¬, '
           +'¬blblnr¬ : ¬'+%trim(BLBLNR)+'¬, '
           +'¬bl¬ : ¬'+%trim(BLBLNR)+'¬, '
           +'¬blorr¬ : ¬'+%trim(BLCH)+'¬, '
           +'¬txetdd¬ : ¬'+%trim(%editw(TXETDD:'    .  .  '))+'¬,'
           +'¬txatdd¬ : ¬'+%trim(%editw(TXATDD:'    .  .  '))+'¬,'
           +'¬txetad¬ : ¬'+%trim(%editw(TXETAD:'    .  .  '))+'¬,'
           +'¬txatad¬ : ¬'+%trim(%editw(TXATAD:'    .  .  '))+'¬,'
           +'¬hent¬ : ¬'+%trim(%editc(HENT:'3'))+'¬,'
           +'¬hevkt¬ : ¬'+%trim(%editc(HEVKT:'3'))+'¬,'
           +'¬hem3¬ : ¬'+%trim(%editc(HEM3:'3'))+'¬,'
           +'¬helm¬ : ¬'+%trim(%editc(HELM:'3'))+'¬} ';
           exsr skrivjson;
       endif;

       endsr;

       //**************************
       begsr init;
       //**************************
       // prepare HTMLbuffer - write top section
         gethtmlifs('/syspeddev/html/JSON.html':'/CGITAG/');
         wrtsection('top');

         open bridf;
         chain WSUSER bridf;
         if not %found;
         // kall på SkrivJSON/jsonescape (extpg JSONESC) kan feile om ikke libl er satt..
         //         - alternativt må vi ALLTID sørge for at JSONESC ligger i SYSPED
           jsonstring='{"errMsg" : "Ugyldig userid"}';
           updhtmlvar2('JSONstring':%addr(jsonstring):%len(jsonstring));
           wrtsection('JSONlin');
           // send htmlbuffer + avslutt
           wrtsection('*fini');
           close bridf;
           *inlr = *on;
           return;
         else;
           if bilibl = *blanks;
             incgi ( bistdl );              // standardliste via nr
           else;
             bilibl_001();                  // helt egen libl
           endif;
             esgetpar ( bibrid : bifirm : bikunr : bikng : rowhead : odd :
             even : logoimg : xsprak : xintern: parmds1 : parmds2 : parmds3);
           open headf;
           open head17;
           open head18;
           open dokufm1;
           open billof;
           open billoflg;
           open blv1;
           open bljoinlf;
           open trackt;
           open brparf;
           open frisok;
           open frisol01;

           xfskode = 'IFB';
         //uavd    = 0;
         //uopd    = 0;
           xnopd   = 0;
         endif;

       endsr;

       //**************************
       begsr skrivjson;
       //**************************
         callp jsonescape(jsonstring);
         updhtmlvar2('JSONstring':%addr(jsonstring):%len(jsonstring));
         wrtsection('JSONlin');
       endsr;

      /end-free
