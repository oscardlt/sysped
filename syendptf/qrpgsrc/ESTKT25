      *********************************************************************
      *  Motta IOD mm  ved utlevering far TKterm
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTKT25 MODULE(*LIBL/ESTKT25 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHeadf     UF   E           K DISK    usropn
     FTRACKF    O  A E           K DISK    Usropn
     FTRACKT    UF   E           K DISK    Usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D                 DS
     D  DUP                    1   1100  0
     D                                     DIM(100)
     D                 DS
     D  AvdOpd                 1     11  0
     D   DsAvd                 1      4  0
     D   DsOpd                 5     11  0
      *
      * Varibles
     D WSUSER          S             10
     D Avd             s              4
     D Opd             s              7
     D SIGN            s             50
     D Sted            s             20
     D AVVIK1          s             50
     D KOMM1           s             50
     D AVVIK2          s             50
     D KOMM2           s             50
     D AVVIK3          s             50
     D KOMM3           s             50
     D AVVIK4          s             50
     D KOMM4           s             50
     D AVVIK5          s             50
     D KOMM5           s             50
     D JSONstring      s          32000
N02  D ErrMsg          S            150
N02  D InfoMsg         S            150
N02  D SuccessMsg      S            150
     D Cmd             S             70
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      ************************************************
     C     Parse         begsr
      ************************************************
     C                   eval      WSUSER = zhbgetvar('user')
     C                   eval      Avd    = zhbgetvar('Avd')
     C                   eval      HEAVD = c2n2(Avd)
     C                   eval      Opd    = zhbgetvar('Opd')
     C                   eval      HEOPD = c2n2(Opd)
     C                   eval      Sted  = zhbgetvar('Sted')
     C                   eval      Sign  = ZhbGetVar('Sign')
     C                   eval      Avvik1= ZhbGetVar('Avvik1')
     C                   eval      Avvik2= ZhbGetVar('Avvik2')
     C                   eval      Avvik3= ZhbGetVar('Avvik3')
     C                   eval      Avvik4= ZhbGetVar('Avvik4')
     C                   eval      Avvik5= ZhbGetVar('Avvik5')
     C                   eval      Komm1   = ZhbGetVar('Komm1')
     C                   eval      Komm2   = ZhbGetVar('Komm2')
     C                   eval      Komm3   = ZhbGetVar('Komm3')
     C                   eval      Komm4   = ZhbGetVar('Komm4')
     C                   eval      Komm5   = ZhbGetVar('Komm5')
      *
     c                   endsr
      *
      *****************************************************
     C     LoadHtml      begsr
      *****************************************************
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   endsr
      *****************************************************
     C     SetAll        begsr
      *****************************************************
     c                   if        ErrMsg= *blanks
     c                   exsr      SRTT
     c                   endif
      * Send section top
     C                   callp     wrtsection('top')
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(ErrMsg)+'¬, '
        +'¬infoMsg¬ : ¬'+%trim(InfoMsg)+'¬, '
        +'¬successMsg¬ : ¬'+%trim(SuccessMsg)+'¬}';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C                   endsr
      ******************************************************
     C     Exit          begsr
      ******************************************************
      * dummy fini-section
     C                   callp     wrtsection('*fini')
      * Quit
     C                   close     BRIDF
     C                   IF        %found ( BRIDF )
     C                   close     HEADF
     C                   close     TRACKF
     C                   CLOSE     TRACKT
     c                   endif
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      ************************************************
     C     INIT          begsr
      ************************************************
      * Sett libr.list etter UserID
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF
      *
     C                   IF        not %found ( BRIDF )
     c                   eval      ErrMsg='Feil user-ID eller ugyldig innlogget'
     C                   else
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     c                   end
      *
      *
     C     HeadKey       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DsHeaKey      KLIST
     C                   KFLD                    DsAVD
     C                   KFLD                    DsOPD
     C     HEAKY0        KLIST
     C                   KFLD                    TRAVD0
     C                   KFLD                    TROPD0
      *
      *
N04  C                   open      HEADF
N04  C                   open      TRACKF
     C                   OPEN      TRACKT
      *
     c                   exsr      AddDup
      *
     c                   endif
      *
      *
     c                   endsr
      *
      *
      **************************************************************************
     C     AddDup        begsr
      **************************************************************************
      *
     C                   move      HEAVD         gmavd             4 0
     C                   move      HEOPD         gmopd             7 0
     C     HeadKey       CHAIN(N)  HEADF                              33
     c                   if        not %found(HEADF)
     c                   eval      ErrMsg='Ugyldig oppdrag'
     c                   goto      UtDup
     c                   endif
      * FINN TOPP-NIVÅET
     C     TROPD0        DOWNE     0
     C     HEAKY0        CHAIN(N)  HEADF                              33
     C                   ENDDO
     C* Legg inn topp-oppdraget som start i array
     c                   clear                   DUP
     c                   z-add     1             Nr                3 0
     c                   movel     heavd         Dup(Nr)
     c                   move      heopd         Dup(Nr)
     c     1             do        100           Cur               3 0
     c     Dup(Cur)      cabeq     *zeros        UtDup
     c                   move      Dup(Cur)      AvdOpd
     C     DSHeaKey      CHAIN     HEADF                              33
     C  N33TrOpd1        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd1        Dup(Nr)
     c                   move      TrOpd1        Dup(Nr)
     C                   ENDIF
     C  N33TrOpd2        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd2        Dup(Nr)
     c                   move      TrOpd2        Dup(Nr)
     C                   ENDIF
     c                   enddo
      *
     C     Utdup         tag
      *
     C                   move      gmAVD         heavd
     C                   move      gmOPD         heopd
     C     HeadKey       CHAIN(N)  HEADF                              33
     C                   ENDSR
      *
     C***********************************************
     C     SRTT          BEGSR
     C***********************************************
      *
     c                   eval      SuccessMsg = 'POD er registrert'
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     c     HeadKey       Chain     Headf                              30
     c     *in30         ifeq      '0'
     c     Hesgm         IFEQ      *blanks
     c     Hesgm         oreq      'X'
     c     Hesgm         oreq      'LEVERT'
     C                   movel     Sign          HESGM
     C                   movel     WDT           HEDTMO
     C                   movel     WTM           HEKLMO
     C                   end
     C                   update    headfr
      * Actual time of ARRIVAL  i trackt
     c     HeadKey       chain     Trackt                             33
     C     *in33         Ifeq      *OFF
     C     TXATAD        andeq     *zeros
     c                   movel     WDT           TXATAD
     c                   movel     WTM           TXATAK
     c                   update    tracktr
     C                   end
     C                   end
      * denne SR logger COL TE2 Hentet på Term - TE2 Levret Term - POD
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   MOVE      'POD'         TTACTI
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      TTDATL        TTDATE
     C                   MOVE      TTTIML        TTTIME
     C                   MOVEL     WSUser        TTUSER
     C                   MOVEL     Sted          TTDEPO
     C                   eval      TTNAME = Sign
     C                   eval      TTTEXT = 'Signed by '+Sign
     C                   eval      TTTEXL = 'Kvittert av '+Sign
      * Tillegs-info
     c     Sted          ifne      *blanks
     C                   eval      TTTEXT = %trim(TTTEXT)+' ('
     c                             + %trim(Sted)+')'
     C                   eval      TTTEXL = %trim(TTTEXL)+' ('
     c                             + %trim(Sted)+')'
     c                   endif
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
      * Oppdatere alle andre oppdrag i evt dup-struktur
     c                   exsr      DupOppd
      *
      * Eventuelle avvik
     c                   exsr      SRTTAvv
      *
N10  c     UtSRTT        tag
     C                   ENDSR
      *
      **************************************************************************
     C     SRTTAVV       begsr
      **************************************************************************
     c                   move      'AVV'         TTACTI
      *
     c     Avvik1        ifne      *blanks
     c     Komm1         orne      *blanks
     c     Avvik1        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik1) +' / '+ Komm1
     c                   eval      TTTEXL = %trim(Avvik1) +' / '+ Komm1
     c                   else
     c                   eval      TTTEXT = Komm1
     c                   eval      TTTEXL = Komm1
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik2        ifne      *blanks
     c     Komm2         orne      *blanks
     c     Avvik2        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik2) +' / '+ Komm2
     c                   eval      TTTEXL = %trim(Avvik2) +' / '+ Komm2
     c                   else
     c                   eval      TTTEXT = Komm2
     c                   eval      TTTEXL = Komm2
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik3        ifne      *blanks
     c     Komm3         orne      *blanks
     c     Avvik3        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik3) +' / '+ Komm3
     c                   eval      TTTEXL = %trim(Avvik3) +' / '+ Komm3
     c                   else
     c                   eval      TTTEXT = Komm3
     c                   eval      TTTEXL = Komm3
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik4        ifne      *blanks
     c     Komm4         orne      *blanks
     c     Avvik4        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik4) +' / '+ Komm4
     c                   eval      TTTEXL = %trim(Avvik4) +' / '+ Komm4
     c                   else
     c                   eval      TTTEXT = Komm4
     c                   eval      TTTEXL = Komm4
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik5        ifne      *blanks
     c     Komm5         orne      *blanks
     c     Avvik5        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik5) +' / '+ Komm5
     c                   eval      TTTEXL = %trim(Avvik5) +' / '+ Komm5
     c                   else
     c                   eval      TTTEXT = Komm5
     c                   eval      TTTEXL = Komm5
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
     C                   ENDSR
      *
      **************************************************************************
     C     DupOppd       begsr
      **************************************************************************
     C                   MOVE      'KOP'         TTEDRE                         KOPI FRA ANNET...
      * Dupliser hendelse til alle oppdrag i "DUP-struktur"
     c     1             do        100           Nr
     c     Dup(Nr)       cabeq     *zeros        UtDupArr
     c                   move      Dup(Nr)       AvdOpd
     C     DsAvd         IFNE      TTAVD
     C     DsOpd         orne      TTOPD
     C                   MOVE      TTAVD         TmpAVD            4 0
     C                   MOVE      TTOPD         TmpOPD            7 0
     C                   MOVE      DsAvd         TTAVD
     C                   MOVE      DsOpd         TTOPD
     C                   WRITE     TRACKFR
     C                   MOVE      TmpAVD        TTAVD
     C                   MOVE      TmpOPD        TTOPD
      * dersom POD oppdater også oppdrag....
     C                   ENDIF
     c                   enddo
     C     UtDupArr      tag
     C                   MOVE      *BLANKS       TTEDRE                         EVENT PÅ DETTE ..
     C                   ENDSR
      *
