********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10010 * 01 YBC PTF10 OPPDATER M VED 907-MELDING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11105 * 02 YBC PTF11 SEND EPOST
********************************************************************
      *
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
N02  FKODTSI    IF   E           K DISK
     FTRUH      UF   E           K DISK
     FTRUH06    IF   E           K DISK
     F                                     RENAME(TRUHR:TRUH06R)
     FEDIM4     IF   E           K DISK
     FTVINF     UF   E           K DISK
     FTVINME    O    E             PRINTER OFLIND(*IN51)
N02  F                                     USROPN
     D                 DS
     D  SISG                   1      3
     D  THSG                   1      3
N02  D XCMD31          C                   CONST('OVRPRTF -
N02  D                                     FILE(TVINME) -
N02  D                                     PAGESIZE(72 80) -
N02  D                                     OVRFLW(66) -
N02  D                                     FORMTYPE(*STD) -
N02  D                                     HOLD(*YES) -
N02  D                                     OUTQ(OUTMAIL)')
N02  D UCMD            DS          1024
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRHOVE
N02  C                   CLOSE     TVINME
     C                   SETON                                        LR
      *
      *
      *////////////////////////////////////////////////////////////
      *  Initiering                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     UAVD          PARM                    UAVD              4 0
     C     UWS           PARM                    UWS              10
N02  C                   PARM                    USLUTT            1
N02  C                   PARM                    UEP              70
N02  C                   PARM                    UEMNE            40
N02  C                   EVAL      USLUTT = 'N'
N02  C                   EVAL      UEP    = *BLANKS
N02  C                   EVAL      UEMNE  = *BLANKS
N02  C                   OPEN      TVINME
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYSH         KLIST
     C                   KFLD                    THAVD
     C                   KFLD                    THTDN
     C     KEYSH6        KLIST
     C                   KFLD                    UWS
     C                   KFLD                    WST               1
     C     KEYTM4        KLIST
     C                   KFLD                    WSR               1
     C                   KFLD                    THAVD
     C                   KFLD                    THTDN
     C                   KFLD                    W0068A            8 0
     C     KEYM42        KLIST
     C                   KFLD                    WSR
     C                   KFLD                    THAVD
     C                   KFLD                    THTDN
     C     KEYTF         KLIST
     C                   KFLD                    MMN
     C     KODTPK        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    KOPLNR
     C                   MOVE      'P'           KOPUNI
     C                   Z-ADD     97            KOPLNR
N02  C     KODTSIK       KLIST
N02  C                   KFLD                    KSIUNI
N02  C                   KFLD                    THSG
N02  C                   EVAL      KSIUNI = 'SI'
      *___________________
      * Initiere faste nøkkelfelt
      *_________________
     C                   MOVE      'R'           WSR
     C                   MOVE      99999999      W0068A
     C                   MOVE      'Å'           WST
     C                   MOVEA     '10'          *IN(38)
     C     KODTPK        CHAIN     KOPR                               33
     C  N33KOPFM         IFNE      *BLANKS
     C     KOPFM         SETLL     KODTFM
     C     KOPFM         READE     KODTFM                                 33
     C                   END
      *_____________
      * Sett nøkkel for å hente alle ME ('EDIRE     Å')
      *"""""""""""""
     C     KEYSH6        SETLL     TRUH06
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Behandl alle Å-oppdrag,                           SRHOVE /
      *////////////////////////////////////////////////////////////
     C     SRHOVE        BEGSR
      *___________________
      * Hent alle SAD- med EDIRE/Å. finn siste Recievemelding
      * via meldingsnr skriv  FRITEKST/ oppdater status i sadh
      *"""""""""""""""""
     C                   SETOFF                                           33
N02  C                   MOVE      'J'           XSNDEP            1
     C     FLERSH        TAG
     C     KEYSH6        READE     TRUH06                                 33
N02  C   33              EVAL      USLUTT = 'J'
     C  N33              DO
N02  C     KODTSIK       CHAIN     KODTSI                             33
N02  C                   IF        *IN33 = *ON OR KSIXXX = *BLANKS
N02  C                   EVAL      XSNDEP = 'N'
N02  C                   ELSE
N02  C                   IF        XSNDEP = 'N'
N02  C                   GOTO      SLHOVE
N02  C                   END
N02  C                   EXSR      OVR2
N02  C                   END
     C     THAVD         CABNE     UAVD          FLERSH
     C                   MOVEA     '11'          *IN(51)
     C     KEYTM4        SETLL     EDIM4
     C     FLEREM        TAG
     C     KEYM42        READPE    EDIM4                                  34
     C  N34M1001         CABNE     'TET'         FLEREM
     C  N34              EXSR      SRSKRI
     C     *IN52         IFEQ      '0'
     C                   WRITE     TVINMEØ
     C                   ELSE
     C     KEYSH         CHAIN     TRUH                               20
     C  N20              MOVE      ' '           THST
     C  N20              UPDATE    TRUHR
     C                   END
N02  C                   IF        XSNDEP = 'J'
N02  C                   EVAL      UEP = KSIXXX
N02  C                   EVAL      UEMNE = 'Melding fra TVINN: '
N02  C                             + %TRIM(%EDITC(THAVD:'Z')) + '/'
N02  C                             + %TRIM(%EDITC(THTDN:'Z'))
N02  C                   GOTO      SLHOVE
N02  C                   END
     C                   GOTO      FLERSH
     C                   END
N02   *
N02  C     SLHOVE        TAG
N02   *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv en melding (en post i TVINF)                SRSKRI /
      *////////////////////////////////////////////////////////////
     C     SRSKRI        BEGSR
      * Oppdatere tolldeklarasjon med ny status
     C     KEYSH         CHAIN     TRUH                               20
     C     THST          IFEQ      'Å'
     C                   SELECT
     C     M1N07         WHENEQ    '009'                                        KANSELLERT
     C     TH0035        IFEQ      '1'
     C                   MOVE      'D'           THST
     C                   ELSE
     C                   MOVE      'P'           THST
     C                   END
     C     M1N07         WHENEQ    '016'                                        AVVIST DEKL.
N01  C     M1N07         OREQ      '907'                                        FEILMELDING
     C     M1N07         OREQ      '917'                                        FEILMELDING
     C     M1N07         OREQ      'CTL'                                        FEIL DESEGMETERING
     C                   MOVE      'M'           THST
     C     M1N07         WHENEQ    '051'                                        INGEN FRIGIVELSE
     C                   MOVE      'F'           THST
     C     M1N07         WHENEQ    '055'                                        FEIL VED GARANTI
     C                   IF        THKDV = 'F'
     C                   MOVE      'F'           THST
     C                   ELSE
     C                   MOVE      'G'           THST
     C                   END
     C                   MOVE      ' '           THKDV
     C     M1N07         WHENEQ    '060'                                        VAREKONTROLL
     C                   MOVE      'V'           THST
     C     M1N07         WHENEQ    '062'                                        AVVIST FRIGIVELSE
     C                   MOVE      'S'           THST
     C                   ENDSL
     C                   END
     C                   UPDATE    TRUHR
      * Les alle på samme meldingsnr
     C     KEYTF         SETLL     TVINF
     C     FLEREF        TAG
     C     KEYTF         READE     TVINF                                  35
     C  N35F4815         IFEQ      'JA'
     C     M1N07         IFEQ      'CTL'                                        FEIL DESEGMETERING
     C                   MOVE      *BLANKS       F0078E
     C                   MOVEL     'KONTAKT'     F0078E
     C                   CAT       'SYSTEM':1    F0078E
     C                   CAT       'A':0         F0078E
     C                   END                                                                TERING
      * Skriv ut melding.
     C  N35
     CAN 51              DO                                                     OVERFLOW
     C                   WRITE     TVINMEA
     C                   SETOFF                                       51
     C                   MOVE      '0'           *IN52
     C                   END
     C     F0078B        COMP      *BLANKS                                41
     C     F0078C        COMP      *BLANKS                                42
     C     F0078D        COMP      *BLANKS                                43
     C     F0078E        COMP      *BLANKS                                44
     C                   WRITE     TVINMEB
     C                   MOVE      'PP'          F4815
     C                   END
     C  N35              UPDATE    TVINFF
     C  N35              GOTO      FLEREF
      *
     C                   ENDSR
N02   **********************************************************************************
N02  c     ovr2          begsr
N02   **********************************************************************************
N02  C                   CLOSE     TVINME
N02  C                   Z-ADD     1024          UCMDL            15 5
N02  C                   EVAL      UCMD = XCMD31
N02  C                   CALL      'QCMDEXC'                            33
N02  C                   PARM                    UCMD
N02  C                   PARM                    UCMDL
N02  C                   OPEN      TVINME
N02  C                   ENDSR
