      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQTNR MODULE(*LIBL/TJINQTNR *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF12 GetVal=Getvalidate= J  EN og kun EN
********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
     FTRANL01   IF   E           K DISK    USROPN
     F                                     RENAME(TRANR:TRANR01)
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D SOKTNR          s              8
     D SOKTNRN         s              8  0
     D SOKNVN          s             10
     D USER            s             10
N01  D GetVal          S              1                                         J=Get/validate
N02  D typKod          s              1
N02  D typTxt          s             20
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
     C                   EXSR      PARSE

     C                   EXSR      INIT
     C     ERROR         CABNE     *BLANKS       TAGERR

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
N01   * sjekker før leseloopen for å kunne gi error i header
N01  c     GetVal        ifeq      'J'
N01  C     TNRKEY        CHAIN     TRAN                               33
N01  C   33              eval      Error = 'Ugyldig transportørnummer'
N01  c                   endif

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬soktnr¬ : ¬'+%trim(SOKTNR)+'¬, '
        +'¬soknvn¬ : ¬'+%trim(SOKNVN)+'¬, '
N01     +'¬getVal¬ : ¬'+%trim(getVal)+'¬, '
        +'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Find/send lines
s01  c**   Error         IFEQ      *blanks
     C                   EXSR      LINES
s01  C**                 ENDIF

     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Quit
     C     TAGERR        TAG
     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         BEGSR
     C                   EVAL      USER = zhbgetvar('USER')
     C                   EVAL      SOKTNR  = zhbgetvar('SOKTNR')
     C                   EVAL      SOKTNRN = c2n2(SOKTNR)
     C                   EVAL      SOKNVN  = zhbgetvar('SOKNVN')
     C                   EVAL      SOKNVN  = uppify(SOKNVN)
     C                   IF        SOKTNR=*BLANKS
     C                   EVAL      SOKTNRN=*ZEROS
     C                   ENDIF
N01  C                   EVAL      GETVAL  = zhbgetvar('GETVAL')
N01  C                   EVAL      GETVAL  = uppify(GETVAL)
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C  N50              CLOSE     TRAN
     C  N50              CLOSE     TRANL01
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      * Fine lins - sEND to browser
      *=====================================================================
     C     Lines         BEGSR
     c                   EVAL      JSONstring ='"translist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C*    SOKTNRN       IFEQ      *ZEROS
     C*    SOKNVN        ANDEQ     *BLANKS
     C*                  GOTO      UTLES
     C*                  END

     C     SOKTNRN       IFNE      *ZEROS
     C     TNRKEY        SETLL     TRAN
     C     BIFIRM        READE     TRAN                                   33
N01  c     GetVal        ifeq      'J'
N01  c     SOKTNRN       andne     VMTRAN
N01  c                   goto      UtLes
N01  c                   endif
     C                   ELSE
     C     NVNKEY        SETLL     TRANL01
     C     BIFIRM        READE     TRANL01                                33
     C*    SOKNVN        SCAN      VMALFA                                 34
     C*    *IN34         CABEQ     *OFF          UtLes
     C                   END
     C     *IN33         DOWEQ     *OFF

     C                   ADD       1             ANTLEST
N02  C                   move      VMINCR        typKod
N02  C                   select
N02  c     VMINCR        wheneq    0
N02  c                   eval      typTxt='Provisjonsavregning '
N02  c     VMINCR        wheneq    1
N02  c                   eval      typTxt='Sender faktura      '
N02  c     VMINCR        wheneq    2
N02  c                   eval      typTxt='Egen bil(ikke avr)  '
N02  c     VMINCR        wheneq    3
N02  c                   eval      typTxt='Sender fakt(iht avr)'
N02  c     VMINCR        wheneq    4
N02  c                   eval      typTxt='Avregn iht frakttab.'
N02  c     VMINCR        wheneq    5
N02  c                   eval      typTxt='Sender fakt(iht tab)'
N02  C                   endsl
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬vmtran¬ : ¬'+%trim(%editc(VMTRAN:'Z'))+'¬, '
S02       //+'¬navn¬ : ¬'+%trim(VMNAVN)+'¬}';
N02         +'¬navn¬ : ¬'+%trim(VMNAVN)+'¬,'
N02         +'¬typKod¬ : ¬'+%trim(typKod)+'¬, '
N02         +'¬typTxt¬ : ¬'+%trim(typTxt)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
N01  c     GetVal        ifeq      'J'
N01  c                   goto      UtLes
N01  c                   endif

     C     ANTLEST       CABGE     100           UTLES
     C     SOKTNRN       IFNE      *ZEROS
     C     BIFIRM        READE     TRAN                                   33
     C                   ELSE
     C     BIFIRM        READE     TRANL01                                33
     C*    SOKNVN        SCAN      VMNAVN                                 34
     C*    *IN34         CABEQ     *OFF          UTLES
     C                   END
     C                   END

     C     UTLES         TAG
     c                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C  N50BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END

OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvC  N50              CALL      'ESGETPAR'
OddEvC                   PARM                    BIBRID
OddEvC                   PARM                    BIFIRM
OddEvC                   PARM                    BIKUNR
OddEvC                   PARM                    BIKNG
OddEvC                   PARM                    RowHead          10
OddEvC                   PARM                    Odd              10
OddEvC                   PARM                    Even             10
OddEvC                   PARM                    LogoImg          50
OddEvC                   PARM                    XSPRÅK            2
OddEvC                   PARM                    XINTERN           1
OddEvC                   PARM                    PARMDS1        1024
OddEvC                   PARM                    PARMDS2        1024
OddEvC                   PARM                    PARMDS3        1024

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      TRAN
     C  N50              OPEN      TRANL01
      * Key for CUNDL01
     C     TNRKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    SOKTNRN
      * Key for CUNDL01
     C     NVNKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    SOKNVN
     C                   ENDSR
