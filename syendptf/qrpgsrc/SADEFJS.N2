     H*debug(*yes)
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
      *
     FSADEFL02  IF   E           K DISK
     FSADEFCL1  IF   E           K DISK
     FSADEFCSL1 IF   E           K DISK
     FSADV      IF   E           K DISK
     FTARI      IF   E           K DISK
     F*STED2     IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E           K DISK
     FEDIS2     IF   E           K DISK    RENAME(EDISR:EDISR2)
     F                                     PREFIX(S2_)
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
      *
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Data            s             80a   Varying
     D codepage        s              5a   Varying
     D CmdLine         S            512
     D CmdLen          S             15  5
     D LF              c                   x'25'
     D*CLSDFT          S             30a
     D*CLSDTT          S             30a
     D CLESERT         S             30a
     D X_1LIN          S              1a
     D XAPS            S              1a
     D XN51            S              3  0
     D XN52            S              3  0
     D XN53            S              3  0
     D XN54            S              3  0
     D XN55            S              3  0
     D XN56            S              3  0
     D XAN80_1         S             80
     D XAN80_2         S             80
      *
      * Status = Pre-alert  / On-Arrival / Cancelled
     D Status          s             20
     D JSONstring      s          32000
     D WFILNAM         S            200
     D WFILNAM1        S            200
     D WFILNAM2        S            200
     D WFILNAM3        S            200
     D WFILNAM4        S            200
     D WMAPPE          S            100
     D WMAPPE2         S            100
     D XLIB            S             11
     D WFilback        S            512
     D WFIL            S            512
     D timeData        S               Z                                        Z=Timestamfield
     D Fldr            S             60
     d wdt             s              8a
     d wtm             s              6a
      *
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )
     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value
     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
      *   EDIDTA data area
     D EDIDTA          DS           256    DTAARA('EDIDTA')
     D  UELIBF               171    180
     D                 DS
     D  TS                     1     26
     D  TS_N                   1     26  0
     D                 DS
     D  EFPRO                  1      8  0
     D  EFPRO_A                1      8
     D                 DS
     D  EFETA                  1      8  0
     D  EFETAA                 1      4
     D  EFETAM                 5      6
     D  EFETAD                 7      8
     D                 DS
     D  EFETM                  1      6  0
     D  EFETMH                 1      2
     D  EFETMM                 3      4
     D  EFETMS                 5      6
     D                 DS
     D  EFSJADT                1      8  0
     D  EFSJADTA               1      4
     D  EFSJADTM               5      6
     D  EFSJADTD               7      8
     D                 DS
     D  CS0068A                1      8  0
     D  CL0068A                1      8  0
     D  CL0068AA               1      4
     D  CL0068AM               5      6
     D  CL0068AD               7      8
     D                 DS
     D  CS0068B                1      6  0
     D  CL0068B                1      6  0
     D  CL0068B_A              1      6
     D                 DS
     D  EFTSD                  1      4  0
     D  EFTSD_A                1      4
     D                 DS
     D  XTXT801                1     80
     D  XT801                  1     80    DIM(80)
     D                 DS
     D  XTXT802                1     80
     D  XT802                  1     80    DIM(80)
     D                 DS
     D  CSRG                   1     11
     D  CLRG                   1     11
     D                 DS
     D  CSNTK                  1      7  0
     D  CLNTK                  1      7  0
     D                 DS
     D  CSVKB                  1      9  0
     D  CLVKB                  1      9  0
     D                 DS
     D  CSVT                   1     30
     D  CLVT                   1     30
     D                 DS
     D  CSPR                   1      2
     D  CLPR                   1      2
     D                 DS
     D  CSPRT                  1     30
     D  CLPRT                  1     30
     D                 DS
     D  CSETYP                 1      2
     D  CLETYP                 1      2
     D                 DS
     D  CSETYPT                1     30
     D  CLETYPT                1     30
     D                 DS
     D  CSEID                  1     18
     D  CLEID                  1     18
     D                 DS
     D  CSESER                 1      1
     D  CLESER                 1      1
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'ARC040C1'
     d ARC040C1        pr                  extpgm('ARC040C1')
     d Fldr_                               like(Fldr)
      *  Prototype for 'SADEFJSONC'
     d SADEFJSONC      pr                  extpgm('SADEFJSONC')
     d wfilnam_                            like(wfilnam)
     d wfilnam1_                           like(wfilnam1)
     d wfilnam2_                           like(wfilnam2)
     d wfilnam3_                           like(wfilnam3)
     d wfilnam4_                           like(wfilnam4)
     d wmappe_                             like(wmappe)
     d wmappe2_                            like(wmappe2)
      *  Prototype for SADEFJSON
     d SADEFJSON       pr
     d upro_                               like(upro)
      *  *ENTRY Interface for Main Procedure
     d SADEFJSON       pi
     d upro                           8
      *
      /free

         exsr init;
         // tEMPORÆRE HARDKODEDE VERDIER

         exsr LagFil;

         exsr OpdEDIS;

         *inlr = *on;
         return;

        //*********************
        Begsr LagFil;
        //*********************
        //time (Timedata);
        //ts = Timedata;
      /end-free
     C                   time                    Timedata
     C                   movel     timedata      TS
      /free

        //Lag fil og set codepage (pc)

        if efst2 = 'D';
          WFil = 'D_' + EFUUID;                // DELETE
        else;
          WFil = EFUUID;                       // 1. GANG SENDING
          S0020 = 'EF' + EFPRO_A;
          setll s0020 edis2;
          reade s0020 edis2;
          dow not %eof;
            if s2_ssr = 'S' and s2_s0026 = 'SADEFJSON' and s2_sst = 'C';
              WFil = 'U_' + EFUUID;            // UPDATE
              leave;
            endif;
            reade s0020 edis2;
          enddo;
        endif;

        XLIB = '/' + UELIBF;
        // ByggFlr kan gjerne flyttes til annet prog. men det er gjort på millisekunder..
        exsr ByggFlr;
        // Fil i backup med stempel
        WFilNam  = %TRIM(XLIB) + '/EXPRESSMANIFEST/json_Ut/Backup/'
                  + %TRIM(TS) + %TRIM(Wfil) + '.json';
        // Fil i temp med stempel
        WFilNam1 = %TRIM(XLIB) + '/EXPRESSMANIFEST/' + 'json_Ut/Temp/'
                  + %TRIM(TS) + %TRIM(Wfil) + '.json';
        // Fil i temp uten stempel
        WFilNam2 = %TRIM(XLIB) + '/EXPRESSMANIFEST/' + 'json_Ut/Temp/'
                  + %TRIM(Wfil) + '.json';
        // Filnavn uten stempel
        WFilNam3 = %TRIM(Wfil) + '.json';
        // Sjekk tidligere versjon
        WFilNam4 = %TRIM(XLIB) + '/EXPRESSMANIFEST/' + 'json_Ut/'
                  + %TRIM(Wfil) + '.json';

        WMAPPE   = %TRIM(XLIB) + '/EXPRESSMANIFEST/' + 'json_Ut/Temp';
        WMAPPE2  = %TRIM(XLIB) + '/EXPRESSMANIFEST/' + 'json_Ut';

        FILE_o = open( %TrimR( WFilNam ) : 'w, codepage=865' );
        rc = close( FILE_o );
        FILE_o = open( %TrimR( WFilNam ) : 'w' );

        exsr LagJson;

        rc = close( FILE_o );

        sadefjsonc (wfilnam: wfilnam1: wfilnam2: wfilnam3: wfilnam4:
                    wmappe: wmappe2);
        // Kopiert til endelig mappe ..

        endsr;

        //*********************
        Begsr LagJson;
        //*********************

        // Start JSON
        jsonstring ='{';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Message / UUID
        if efst2 <> 'R';
          jsonstring ='¬manifestId¬:¬'+%trim(EFUUID)+'¬,';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
        endif;

        // Hodeinforasjon (Mandatory)
        exsr HODEINFO;

        // CargoLines
        exsr CargoLines;

        // Bunninforasjon (Mandatory)
        exsr BunnINFO;

        jsonstring = '}';                                    // slutt Hele meldingen
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr CargoLines;
        //*********************

        // CargoLines start
        jsonstring = '¬cargoLines¬:[';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        x_1lin = 'J';

        setll efpro sadefcl1;
        reade efpro sadefcl1;
        dow not %eof;
          setll (clavd: cltdn) sadefcsl1;
          reade (clavd: cltdn) sadefcsl1;
          if not %eof;
            dow not %eof (sadefcsl1);
              exsr cargoLine;
              reade (clavd: cltdn) sadefcsl1;
            enddo;
          else;
            exsr cargoLine;
          endif;
          reade efpro sadefcl1;
        enddo;

        jsonstring = '],';                                   // slutt et CargoLines
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr CargoLine;
        //*********************

        // Hvis eksport-id er blank, henter id fra hode
        if cleid = *blanks;
          cleid = efeid;
        endif;
        // Hent varebeskrivelse fra første varelinje
        if clvt = *blanks;
          chain (clavd: cltdn) sadv;
          if %found;
            if svvt01 = *blanks;
              chain svvnt tari;
            // Hent varebeskrivelse fra tariff-register
              if %found;
                svvt01 = taalfa;
              endif;
            endif;
          endif;
        else;
          svvt01 = clvt;
        endif;
        xtxt801 = svvt01;
        xn53 = 30;
        exsr knvst;
        svvt01 = xtxt802;
        //chain clsdf sted2;
        //clsdft = st2nvn;
        //chain clsdt sted2;
        //clsdtt = st2nvn;
        // Sett tekst om sertifisert
        if cleser = 'J' or cletyp = '04';
          clesert = 'true';
        else;
          clesert = 'false';
        endif;

        // CargoLine start
        if x_1lin = 'J';
          jsonstring = '{';
          x_1lin = 'N';
        else;
          jsonstring = ', {';
        endif;
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        xtxt801 = CLSDFT;
        xn53 = 30;
        exsr knvst;
        xan80_1 = xtxt802;
        xtxt801 = CLSDTT;
        xn53 = 30;
        exsr knvst;
        xan80_2 = xtxt802;
        jsonstring =
           '¬typeOfImportProcedure¬:¬'+%trim(CLPRT)+'¬,'
           +'¬numberOfPackages¬:¬'+%trim(%editc(CLNTK:'Z'))+'¬,'
           +'¬descriptionOfGoods¬:¬'+%trim(SVVT01)+'¬,'
           +'¬placeOfLoading¬:¬'+%trim(XAN80_1)+'¬,'
           +'¬placeOfUnloading¬:¬'+%trim(XAN80_2)+'¬,'
           +'¬grossMass¬:¬'+%trim(%editc(CLVKB:'Z'))+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        xtxt801 = clnak;
        xn53 = 30;
        exsr knvst;
        xan80_1 = xtxt802;
        xtxt801 = clnas;
        xn53 = 30;
        exsr knvst;
        xan80_2 = xtxt802;

        if clrg = *blanks;
          jsonstring =
             '¬importProcedure¬: { '
             +'¬mrn¬:¬'+%trim(cltrnr)+'¬,'
             +'¬recipientName¬:¬'+%trim(xan80_1)+'¬,'
             +'¬senderName¬:¬'+%trim(xan80_2)+'¬'
             +'}';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
        else;
          jsonstring =
             '¬importProcedure¬: { '
             +'¬sequenceNumber¬:¬'+%trim(CL0068B_A)+'¬,'
             +'¬declarantNumber¬:¬'+%trim(CLRG)+'¬,'
             +'¬declarationDate¬:¬'+%trim(CL0068AD)
             +%trim(CL0068AM)+%trim(CL0068AA)+'¬'
             +'},';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
          jsonstring =
             '¬exports¬: [ { '
             +'¬typeOfExport¬:¬' + %trim(cletypt)+'¬,'
             +'¬exportId¬:¬'+%trim(cleid)+'¬,'
             +'¬exportCertificate¬: '+%trim(clesert)+' } ]';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
        endif;

        jsonstring = '}';                                    // slutt et CargoLine
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr HODEINFO;
        //*********************

        jsonstring =
           '¬transportationCompanyId¬:¬'+%trim(EFRGD)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        select;
        when efst2 = 'S';
          jsonstring =
              '¬status¬:¬'+'SUBMITTED'+'¬,';
        when efst2 = 'R';
          jsonstring =
              '¬status¬:¬'+'REOPENED'+'¬,';
        when efst2 = 'D';
          jsonstring =
              '¬status¬:¬'+'DELETED'+'¬,';
        endsl;
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬placeOfEntryCode¬:¬'+%trim(EFTSD_A)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬estimatedTimeOfArrival¬:¬'
              +%trim(EFETAA)+'-'+%trim(EFETAM)+'-'+%trim(EFETAD)+'T'
              +%trim(EFETMH)+':'+%trim(EFETMM)+':'+%trim(EFETMS)+'.000Z¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬modeOfTransportCode¬:¬'+%trim(EFTM)+'¬,';
              //'¬modeOfTransportCode¬:¬'+%trim(EFTMT)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr BUNNINFO;
        //*********************

        jsonstring = '¬activeMeansOfTransport¬:{';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
           '¬typeOfMeansOfTransport¬:¬'+%trim(EFKTYPT)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬identificationCode¬:¬'+%trim(EFKMRK)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬countryCode¬:¬'+%trim(EFKLK)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬driver¬: { '
              +'¬firstName¬:¬'+%trim(EFSJAF)+'¬,'
              +'¬lastName¬:¬'+%trim(EFSJAE)+'¬,'
              +'¬countryCode¬:¬'+%trim(EFSJALK)+'¬,'
              +'¬dateOfBirth¬:¬'+%trim(EFSJADTA)+'-'
              +%trim(EFSJADTM)+'-'+%trim(EFSJADTD)+'¬,'
              +'¬sendReleasedConfirmation¬:false'+','
              +'¬sendReleasedConfirmationEmail¬:null';
        if efplk <> *blanks and efpmrk <> *blanks;
          jsonstring = %trim(jsonstring)
                +'},';
        else;
          jsonstring = %trim(jsonstring)
                +'}';
        endif;
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        if efplk <> *blanks and efpmrk <> *blanks;
          jsonstring =
                '¬passiveMeansOfTransports¬:[ { '
                +'¬identificationCode¬:¬'+%trim(EFPMRK)+'¬,'
                +'¬countryCode¬:¬'+%trim(EFPLK)+'¬'
                +'} ]';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
        endif;

        jsonstring = '}';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr OpdEDIS;
        //*********************
        chain ('S': 'EXPREM') ecmsg1;
        chain 1 edic;
        csn = csn + 1;
        update edicr;
        s0004 = ECIIDI;
        s0010 = ECIIDC;
        s0020 = 'EF' + %TRIM(UPRO);
        s0026 = 'SADEFJSON';
        ssn   = CSN;
        ssr   = 'S';
        sst   = 'B';
        sdt   = %dec(wdt:8:0);
        stm   = %dec(wtm:6:0);
        slib  = '*LIB';
        sfile = '*IFS';
        smbr  = *BLANKS;
        sifs  = WFilNam;
        write edisr;

        endsr;

        //*********************
        Begsr knvst;
        //*********************

        xn51 = 0;
        xn52 = 0;
        xn54 = xn53 - 4;
        xn55 = xn53 - 5;
        xn56 = xn53 - 6;
        XTXT802 = *blanks;

        dow xn51 < 80;                                                        // 1<b
          xn51 = xn51 + 1;
          if XT801(XN51) = '&' OR XT801(XN51) = '"' OR                        // 2<b
             XT801(XN51) = 'Æ' OR XT801(XN51) = 'Ø' OR XT801(XN51) = 'Å' OR
             XT801(XN51) = 'æ' OR XT801(XN51) = 'ø' OR XT801(XN51) = 'å' OR
             XT801(XN51) = X'5A' OR XT801(XN51) = X'8E' OR                    // , ¤->' '
             XT801(XN51) = X'B0' OR XT801(XN51) = X'B2' OR
             XT801(XN51) = X'71' OR XT801(XN51) = X'54' OR
             XT801(XN51) = X'74' OR XT801(XN51) = X'58' OR
             XT801(XN51) = X'7D' OR XT801(XN51) = X'EC' OR
             XT801(XN51) = X'90' OR XT801(XN51) = X'63' OR                    //      , Ä->AA
             XT801(XN51) = X'FC' OR XT801(XN51) = X'CC' OR                    //      , ö->oe
             XT801(XN51) = X'43' OR XT801(XN51) = X'55' OR                    // ä->aa, í->i
             XT801(XN51) = X'62' OR XT801(XN51) = X'51' OR                    // Â->A,  é->e
             XT801(XN51) = X'42' OR XT801(XN51) = X'CE' OR                    // â->aa, ó->o
             XT801(XN51) = X'63' OR XT801(XN51) = X'64' OR                    // À->A,  À->A
             XT801(XN51) = X'65' OR XT801(XN51) = X'5B' OR                    // Á->A,  Å->A
             XT801(XN51) = X'41' OR XT801(XN51) = XAPS OR                     // '41'->' ',
             XT801(XN51) = '<' OR XT801(XN51) = '>';

            if XN52 < XN53;                                                   // 3<b
              xn52 = xn52 + 1;

              select;                                                         // 4<-b
              when XT801(XN51) = 'Æ' OR XT801(XN51) = X'63';                  // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:2) = 'AA';
                  xn52 = xn52 + 1;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = 'Ø' OR XT801(XN51) = X'8E'                   // 4
                  OR XT801(XN51) = X'B2';
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:2) = 'OE';
                  xn52 = xn52 + 1;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = 'Å';                                         // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:2) = 'AE';
                  xn52 = xn52 + 1;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = 'æ' OR XT801(XN51) = X'43'                   // 4
                  OR XT801(XN51) = X'42';
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:2) = 'aa';
                  xn52 = xn52 + 1;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = 'ø' OR XT801(XN51) = X'B0'                   // 4
                  OR XT801(XN51) = X'7D' OR XT801(XN51) = X'CC';
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:2) = 'oe';
                  xn52 = xn52 + 1;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = 'å';                                         // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:2) = 'ae';
                  xn52 = xn52 + 1;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'5A';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = '.';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'71' OR XT801(XN51) = X'54'                 // 4
                  OR XT801(XN51) = X'74';
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'E';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'58' OR XT801(XN51) = X'55';                // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'i';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'7D';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:5) = '&apos;';
                  xn52 = xn52 + 5;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'EC';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'O';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'90';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = X'5F';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'FC';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'U';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-b
              when XT801(XN51) = X'62' OR XT801(XN51) = X'63'                 // 4
                  OR XT801(XN51) = X'64' OR XT801(XN51) = X'65'
                  OR XT801(XN51) = X'5B';
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'A';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'51';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'e';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'CE';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = 'o';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = X'41';                                       // 4
                if XN51 < XN55;                                               // 5<-b
                  XT802(XN52) = ' ';
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = '&';                                         // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:5) = '&amp;';
                  xn52 = xn52 + 4;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = '"';                                         // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:5) = '&quot;';
                  xn52 = xn52 + 5;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = XAPS;                                        // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:5) = '&apos;';
                  xn52 = xn52 + 5;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = '>';                                         // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:5) = '&gt;';
                  xn52 = xn52 + 3;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              when XT801(XN51) = '<';                                         // 4
                if XN51 < XN55;                                               // 5<-b
                  %SUBST(XTXT802:XN52:5) = '&lt;';
                  xn52 = xn52 + 3;
                else;                                                         // 5
                  xn52 = xn52 - 1;
                endif;                                                        // 5<-e
              other;                                                          // 4
                if xn52 < xn53;                                               // 5<-b
                  xn52 = xn52 + 1;
                  XT802(XN51) = XT801(XN52);
                else;                                                         // 5
                  leave;
                endif;                                                        // 5<-e
              endsl;                                                          // 4<-e
            endif;                                                            // 3<-e
          else;                                                               // 2
            if XN52 < XN53;                                                   // 3<-b
              xn52 = xn52 + 1;
              XT802(XN51) = XT801(XN52);
            else;                                                             // 3
              leave;
            endif;                                                            // 3<-e
          endif;                                                              // 2<-e
        enddo;                                                                // 1<-e

        if XTXT802 = *BLANKS;
          XTXT802 = '.';
        endif;

        endsr;

        //*********************
        Begsr init;
        //*********************

        // hent turheader
        efpro_a = upro;
        chain efpro sadefl02;

        // hent  consingment / xref   - MINST 1 må finnes
        chain efpro sadefcl1;
        if not %found;
          *INLR = *on;
          return;
        endif;
        // hent  sad header-data

        syge15c (wdt: wtm);
        xaps = x'7D';               // Apstroff

        // Åpne dataarea
        IN EDIDTA;

        endsr;

        //*********************
        Begsr ByggFlr;
        //*********************

        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/json_Ut';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/json_Ut/Temp';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/json_Ut/Backup';
        ARC040C1 (Fldr);

        // for Oscar/Java-bruk
        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/json_Ut/error';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/json_Ut/sent';
        ARC040C1 (Fldr);

        // BAT = Batch / for Java sende motoren
        Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/BAT';
        ARC040C1 (Fldr);

        // automatistere også denne (lage egen CL ?)
        // også kopiere  /apache/express-client/upload-engine-expressmanif-client.jar
        //          til  .../BAT/upload-engine-expressmanif-client.jar

        endsr;

      /end-free
