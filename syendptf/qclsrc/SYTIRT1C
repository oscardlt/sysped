             PGM
             DCL        VAR(&INST) TYPE(*LGL) LEN(1) VALUE('1')
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10) VALUE('          ')
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(30) +
                            VALUE('                              ')
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(15) +
                          VALUE('regkobl.txt')
/* INLIBL slik at det kan kjøre fra jobscheduler  */
             CD /
             MONMSG     MSGID(CPF0000)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
             CHGVAR     VAR(&STMF) VALUE(&LIB *TCAT '/TOLL/' *TCAT &FILE)
/* HENT FRA TOLLVESENET    */
             CALL       PGM(SYTIHENTC) PARM(&STMF &FILE)
             DLTF       FILE(QTEMP/SYTIRCV)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/SYTIRCV) RCDLEN(256) SIZE(1000000 +
                          10000 10)
/* Konverter fra PC-tekst-fil, til FLAT tekstfil  */
             QSYS/CPYFRMSTMF FROMSTMF(&STMF) +
                          TOMBR('QSYS.LIB/QTEMP.LIB/SYTIRCV.FILE/SYTI+
                          RCV.MBR') MBROPT(*REPLACE) CVTDTA(*TBL) +
                          TBL('qsys.lib/sysped.lib/QTCPEBCNO.tbl')
/* Filen finnes ikke - hopp ut  */
             MONMSG     MSGID(CPFA095) EXEC(DO)
             SNDPGMMSG  MSG('Aktuell txt-fil finnes ikke i IFS +
                          /home/sytoll/   Konvertering IKKE +
                          kjørt!!!') MSGTYPE(*INFO)
             GOTO       CMDLBL(SLUTT)
             ENDDO
 /* KONVERTER TIL KODTS8NY, SADSDNY */
             CLRPFM     FILE(KODTS8NY)
             CLRPFM     FILE(SADSDNY)

             CALL       PGM(SYTIRT1) PARM(&INST)
             CLRPFM     FILE(QTEMP/SADSDNYE)
             MONMSG     MSGID(CPF0000) EXEC(CRTDUPOBJ OBJ(SADSDNY) +
                          FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(SADSDNYE))
             CPYF       FROMFILE(SADSDNY) TOFILE(SADSDNYE) +
                          MBROPT(*ADD) CRTFILE(*NO)

 /* MODULEN IKKE INSTALLERT?? ELLER 0 POSTER LEST. */
             IF         COND(&INST = '1') THEN(GOTO CMDLBL(SLUTT))

 /* Oppdater GAMLE AVGIFTER, SLETT UTGÅTTE i KODTS8  */
             CALL       PGM(SYTIRT2)
 /* LEGG TIL HELT NYE NR i KODTS8   */
             CALL       PGM(SYTIRT3)

 /* Oppdater GAMLE AVGIFTER, SLETT UTGÅTTE i SADSD   */
             CALL       PGM(SYTIRT4)
 /* LEGG TIL HELT NYE NR i SADSD    */
             CALL       PGM(SYTIRT5)

             SNDPGMMSG  MSG('Konvertering av RT-AVGIFTER ER KJØRT.') +
                          MSGTYPE(*INFO)

SLUTT:       ENDPGM
