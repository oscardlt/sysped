     A*%%TS  SD  20130314  210853  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A                                      PRINT
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 73 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A*%%TS  SD  20121213  102049  CB          REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  4  1VALUES(' ' '1')
     A            WCALL          2A  O  4  4
     A            WTXT          65A  O  4  8
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20121213  102049  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                  1  1'SVT011D'
     A                                      COLOR(BLU)
     A                                  1 19'Sök och plocka Call me kod'
     A                                      COLOR(WHT)
     A                                  2  1'Välj option:'
     A                                      COLOR(BLU)
     A                                  2 16'1=Plocka'
     A                                      COLOR(BLU)
     A                                  3  1'Op Kod Beskrivning'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(12)
     A                                      CLRL(01)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F3/F12=Avbryt   F5=Förnya'
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A*%%TS  SD  20061206  233348  CB          REL-V5R2M0  5722-WDS
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT011D'
     A                                      COLOR(BLU)
     A                                  1 36'Sök och plocka Call me kod'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plocka'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kod Beskrivning'
     A                                      COLOR(WHT)
     A                                 12  2'F3/F12=Avbryt   F5=Förnya'
     A                                      COLOR(BLU)
