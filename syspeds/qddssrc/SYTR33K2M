     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R NOERAS
     A                                      ASSUME
     A                                      OVERLAY
     A                                  1  2' '
     A          R WIND01
     A                                      OVERLAY
     A                                      WINDOW(*DFT 14 47)
     A          R DETAIL
     A                                      WINDOW(WIND01)
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF12(12 'tilbake')
     A                                      CF03(12)
     A                                  1  1'SYTR33K2'
     A                                      COLOR(BLU)
     A                                  1 14'Vise kolliidentifikasjoner'
     A                                      COLOR(WHT)
     A                                  2 22'Detaljer'
     A                                      COLOR(WHT)
     A                                  4  1'EAN Kolliidentifikasjon:'
     A            KOID          20A  O  4 26
     A                                  5  1'Navn på egen terminal  :'
     A            KOTERM        20A  O  5 26
     A                                  6  1'__________________Målt____________-
     A                                      _________'
     A                                  7  5'Vekt'
     A                                  7 13'Volum'
     A                                  7 22'LM'
     A                                  7 25'Lengde'
     A                                  7 32'Bredde'
     A                                  7 39'Høyde'
     A            KOMVKT         7Y 2O  8  1EDTCDE(4)
     A            KOMM3          7Y 5O  8 10EDTCDE(4)
     A            KOMLM          4Y 2O  8 19EDTCDE(4)
     A            KOMLEN         3Y 0O  8 28EDTCDE(4)
     A            KOMBRE         3Y 0O  8 35EDTCDE(4)
     A            KOMHØY         3Y 0O  8 41EDTCDE(4)
     A                                  9  1'Stasjon på terminal. . :'
     A            KOSTNR         5A  O  9 26
     A                                 10  1'Inn terminal dato/tid  :'
     A            KOINDT         8Y 0O 10 26EDTCDE(4)
     A            KOINTI         6Y 0O 10 35EDTCDE(4)
     A                                 11  1'PDA bruker / Godstype. :'
     A            KOUSER        10A  O 11 26
     A                                 12  1'Avd/opd siste henv.  . :'
     A            KOAVD          4Y 0O 12 26EDTCDE(4)
     A            KOOPD          7Y 0O 12 31EDTCDE(4)
     A                                 13  1'Løpenummer HENOP/HENOS :'
     A            KOLNR          7Y 0O 13 26EDTCDE(4)
     A            KOLNR2         3Y 0O 13 34EDTCDE(4)
     A            KOGTYPA       10A  O 11 37
