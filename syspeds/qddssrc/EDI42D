     A*%%TS  SD  20131112  185452  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/EDISS)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      HLPDOC('EDI42R10' 'EDI42R' 'SY400LO-
     A                                      /EDI')
     A                                      HELP
      *
     A          R EDI42DB                   SFL
     A            ZOP            1A  I  7  3
     A                                      VALUES(' ' '5')
     A            SSDT      R        O  7  5
     A            SSTM      R        O  7 14EDTWRD('  :  :  ')
     A            SSST      R        O  7 23
     A            SSTEXT    R        O  8  2COLOR(YLW)
     A*
     A          R EDI42DC                   SFLCTL(EDI42DB)
     A*%%TS  SD  20131112  185452  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPDOC('EDI42R20' 'EDI42R' 'SY400LO-
     A                                      /EDI')
     A                                      HLPARA(01 001 27 132)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 54'Detailed Interchange log'
     A                                      COLOR(WHT)
     A                                  1125'EDI42D B'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 67SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Interchange ID'
     A                                      COLOR(WHT)
     A            SSN       R        O  3 17REFFLD(SSSN)
     A                                  5  2'Type option and press Enter'
     A                                      COLOR(BLU)
     A                                  5 39'5=Display log text'
     A                                      COLOR(BLU)
     A                                  6  5'Date'
     A                                      COLOR(WHT)
     A                                  6 14'Time'
     A                                      COLOR(WHT)
     A                                  6 23'Status'
     A                                      COLOR(WHT)
     A*
     A          R EDI42DD
     A*%%TS  SD  20131112  185452  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                 26  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 26MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI42DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
