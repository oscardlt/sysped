CB    * Endret i din ferie JOV, misstenker at key skal inneholde DRA isteden for DRM.
CB    * det er i hvertfall det som ligger i CARGOF
      * 02 JOV PTF9 Vekt OG eller MÅL (M3) KAN være utfylt også ved PDA..
     FCARGOF    UF   E           K DISK
     FCARGOO    IF A E           K DISK
     FDOKUFM    IF   E           K DISK
     FHENOP     IF   E           K DISK
     FHENOK     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  FMMRK1                 1     35
     D  FMCLID                 1     18
      *
      * Foreløpig behandling kun på oppdragsnivå (i det en setter på tur)
      * Parametermessig også klargjort for å justere hente-oppdragssendinger
     C     *ENTRY        PLIST
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UULNR             7 0
     C                   PARM                    UULNR2            3 0
     C                   PARM                    uukund            8 0
     C                   PARM                    uunt              7 0
     C                   PARM                    uuntp             3 0
     C                   PARM                    uuvkt             9 0
     C                   PARM                    uum3              7 3
     C                   PARM                    uufbv             9 0
     C                   PARM                    uufrab           13 2
     C                   PARM                    uumxavvik         6 1
     C                   PARM                    uugjavvik         6 1
     C                   PARM                    KOINDT
     C                   PARM                    KOINTI
     C                   PARM                    KOTERM
     C                   PARM                    UFLAGGI           1
     C                   PARM                    UFLAGGU           1
      *
     C* Flagg INN
      * Blank = test / hvis NY og diff vis vindu...
     C* Flagg UT/retur
      * CSretur = 0 = IKKE cargoscan
      * CSretur = 1 = Cargoscan, men sjekket evt justert før...
      * CSretur = 2 = Cargoscan, alt ok
      * CSretur = 3 = Cargoscan, Diff / infobilde skal vises
      *
     C     DokKey        KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    UUAVD
     C                   KFLD                    UUOPD
     C                   MOVE      'F'           FMRI
     C     HenokKey      KLIST
     C                   KFLD                    UULNR
     C                   KFLD                    UULNR2
     C     KunKey        KLIST
     C                   KFLD                    FIFIRM
jov  C*                  KFLD                    UUKUNR            8 0
jov  C                   KFLD                    UUKUND
     C     CARGOFKey     KLIST
     C                   KFLD                    KOID
     C                   KFLD                    KOTERM
      *
     C     *loval        setll     FIRM
     C                   read      firm
      *
      *
      * Sjekk om aktuelle kolli   ligger i cargof
      * COGNTA= ant poster i DOKUFM,   COMNT= ant poster i CARGOF
     C                   Move      *zeros        COGNTA
     C                   Move      *zeros        COMNT
     C                   Move      *zeros        COMNTP
     C                   Move      *zeros        COMVKT
     C                   Move      *zeros        COMM3
     C                   Move      *zeros        COMFBV
     C                   MOVE      'N'           IKKEVKT           1
     C                   MOVE      'N'           IKKEM3            1
     C                   eval      KOTERM = 'OSL'
     C     UUopd         ifne      *zeros
     C     DokKey        setll     DOKUFM
     C     DokKey        Reade     DOKUFM                                 33
     C                   else
     C     UULNR         chain     HENOP                              33
     C     HenoKKey      setll     HENOK
     C     HenoKKey      Reade     HENOK                                  33
     C                   end
     C     *IN33         Doweq     '0'
     C     UUopd         ifne      *zeros
     c                   eval      KOID = 'xxxx'
     C     FMST          IFEQ      'I'
     C     FMST          OREQ      'E'
     C                   add       1             COGNTA
     c                   eval      KOID = '00' + FMCLID
     c                   end
     c                   else
     C                   add       1             COGNTA
     c                   eval      KOID = HKID
     c                   end
     C     CARGOFKey     Chain     CARGOF                             34
     C   34              MOVE      'J'           IKKEVKT
     C   34              MOVE      'J'           IKKEM3
     C     *in34         IFEQ      *OFF
     C                   add       1             COMNT
     C     KOSTNR        ifeq      '02'                                         OSL pall
     C     KOSTNR        oreq      '52'                                         DRA pall
     C     KOSTNR        oreq      '98'                                         PDA- på pall
     C                   add       1             COMNTP
     C                   end
     C     KOMVKT        IFEQ      *ZEROS
     C                   MOVE      'J'           IKKEVKT
     C                   end
     C     KOMM3         IFEQ      *ZEROS
     C                   MOVE      'J'           IKKEM3
     C                   end
     C                   add       KOMVKT        COMVKT
     C                   add       KOMM3         COMM3
      * oppdater at kolliet er talt med i oppdraget (eller henteoppdragslin)
     C                   move      UUAVD         KOAVD
     C                   move      UUOPD         KOOPD
     C                   move      UULNR         KOLNR
     C                   move      UULNR2        KOLNR2
     C                   update    CARGOFR
     C                   end
     C     UUopd         ifne      *zeros
     C     DokKey        Reade     DOKUFM                                 33
     C                   else
     C     HenoKKey      Reade     HENOK                                  33
     C                   end
      *
     C                   end
      *
      * COGNTA= ant poster i DOKUFM,   COMNT= ant poster i CARGOF
     C     COMNT         ifeq      *zeros
     C                   move      '0'           UFLAGGU
     C                   goto      UT
     C                   else
      * Det FANTES cargo-scan-kolli.. Skriv til fil / finn diff%
      * cargoscan fantes 2 = alt OK - byttes med 3 om oppdraget endres..
     C                   move      '2'           UFLAGGU
     C                   move      UUAVD         COAVD
     C                   move      UUOPD         COOPD
     C                   move      UULNR         COLNR
     C                   move      UULNR2        COLNR2
     C                   z-add     UUNT          COGNTB
     C                   z-add     UUNTP         COGNTP
     C                   z-add     UUVKT         COGVKT
     C                   z-add     UUM3          COGM3
     C                   z-add     UUFBV         COGFBV
     C                   z-add     UUFRAB        COFRAB
      * minst ett kolli manglet Vekt (pda) - slett akkumulert vekt - kan ikke brukes
     c     IKKEVKT       ifeq      'J'
     C                   move      *zeros        COMVKT
     c                   end
      * minst ett kolli manglet M3   (pda) - slett akkumulert M3   - kan ikke brukes
     c     IKKEM3        ifeq      'J'
     C                   move      *zeros        COMM3
     c                   end
      * Når kolli (minst ett) ikke innehr vkt mm,kan IKKE oppdater-flagg settes
     C     IKKEVKT       IFEQ      'J'
     C     IKKEM3        andeq     'J'
     C                   move      *zeros        xxgjavvik
     C                   z-add     xxgjavvik     COAVVP
     C                   ELSE
      * når alle kolli inneholder vekt og/eller M3 skal sum vekt/m3 oppdateres
     C                   exsr      FinnFvkt
     C                   END
     C                   write     CARGOOR
      *
     C     xxgjavvik     ifgt      uumxavvik
     C                   move      '3'           UFLAGGU
     C                   end
      *
     C                   end
      *
     C     UT            TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     FinnFvkt      BEGSR
     C***********************************************
      * Hvis 0 i vekt (fra PDA) så bruk oppdragets
     C     COMVKT        IFEQ      *ZEROS
     C                   Z-ADD     uuvkt         COMVKT
     C                   END
      * Hvis 0 i M3   (fra PDA) så bruk oppdragets
     C     COMM3         IFEQ      *ZEROS
     C                   Z-ADD     uum3          COMM3
     C                   END
      * Vis bilde kun dersom F-vekt blir påvirket pluss/minus XX %
     C                   Z-add(H)  COMVKT        COMFBV
     C     COMM3         IFne      *zeros
     C                   z-add     286           XXM3IN            3 0
     C     Kunkey        Chain     CundL01                            33
     C     *in33         IFEQ      *OFF
      * SYSTAT ER AVVIKEND M3 Innland (BHR-spesial)
     C                   TESTN                   SYSTAT               33
     C     *IN33         IFEQ      '1'
     C                   MOVE      SYSTAT        YYM3IN            3 0
     C     YYM3IN        IFGE      100
     C                   Z-ADD     YYM3IN        XXM3IN
     C                   END
     C                   END
     C                   END
      * Finn m3-fvekt
     C     COMM3         MULT(H)   XXM3IN        COMM3F            5 0
     C     Comm3F        Ifgt      COMFBV
     C                   Z-add(H)  COMM3F        COMFBV
     C                   END
     C                   END
      * Dersom oppdrag har 0 i f-vekt (EDI-mottatt)  - beregn den nå
     C     UUFBV         ifeq      *zeros
     c                   z-add     uuvkt         UUFBV
     C     uum3          MULT(H)   XXM3IN        uuM3F             5 0
     C     uum3F         Ifgt      UUFBV
     C                   Z-add(H)  uum3F         UUFBV
     C                   END
     c                   end
      *
      * Cargoscan FVEKT funnet  . max xx % avvik fra oppdragets f.vekt
      * Dersom %-avvik større/mindre enn tillatt sett warning/endrings-flagg
     C                   move      *zeros        xxgjavvik         6 1
     C     UUFBV         ifne      *zeros
     C     COMFBV        SUB       UUFBV         DIFFFBV           5 0
     C                   EVAL      xxgjavvik  = DIFFFBV / UUFBV * 100
     C                   end
     C                   z-add     xxgjavvik     uugjavvik
     C                   z-add     xxgjavvik     COAVVP
     C     xxgjavvik     iflt      *zeros
     C                   MULT      -1            xxgjavvik
     C                   end
      * Hindre at oppdragsverdier lagres som målte vedier
      * minst ett kolli manglet Vekt (pda) - kan ikke brukes
     c     IKKEVKT       ifeq      'J'
     C                   move      *zeros        COMVKT
     c                   end
      * minst ett kolli manglet M3   (pda) - kan ikke brukes
     c     IKKEM3        ifeq      'J'
     C                   move      *zeros        COMM3
     c                   end
     C                   ENDSR
      *
