      * HVIS DEN IKKE FINNES I GM, MERK INAKTIV I NY...
      *
     H DATEDIT(*DMY)
     FSADSD     UP   E             DISK
     FSADSDGM   IF   E           K DISK
     F                                     RENAME(SADSDR:XXXX)
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
      *
     C     *IN99         IFEQ      *OFF
     C                   Z-ADD     1             SIDE              4 0
     C                   TIME                    TID               6 0
     C                   EXCEPT    PHEAD                                         1 PAGE
     C                   SETON                                        99
     C                   END
     C   OF              ADD       1             SIDE              4 0
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF         OVERFLOW
      *
     C     KEY           KLIST
     C                   KFLD                    SDTNRF
     C                   KFLD                    SDKDAÆ
     C                   KFLD                    SDKDSÆ
     C                   KFLD                    SDHKOD
     C                   KFLD                    SDVF
     C                   KFLD                    SDTRM
     C* FINNES IKKE I GM - MERK INAKTIV I NY..
     C     KEY           CHAIN     SADSDGM                            33
     C   33              MOVE      'I'           SDAKTK
     C   33              UPDATE    SADSDR
     C   33              EXCEPT    DET
     OLISTE     E            PHEAD            02
     O                                           24 '** SÆRAVG. MERKET INANK.'
     O                                              ' (FANTES IKKE I GM)'
     O                                           86 'KJØRT DATO:'
     O                       UDATE         Y     96
     O                                          101 'TID:'
     O                       TID                110 '0  :  :  '
     O                                          120 'SIDE:'
     O                       SIDE          Z    125
     OLISTE     E            PHEAD            04
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            PHEAD            05
     O                                           24 ' ENDR.KODE  TARIFFNR   '
     O                                              'AVG.KODE  SEKVENS       '
     O                                              '  SATS                  '
     OLISTE     E            PHEAD            06
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            DET         1
     O                                            9 'INAKTIV:'
     O                       SDTNRF              19
     O                       SDKDAÆ              27
     O                       SDKDSÆ              39
     O                       SDBLSÆ        3     56
