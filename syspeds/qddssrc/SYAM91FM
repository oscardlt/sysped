     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            THDTD          2S 0H
     A            WSNRO          1A  B  9  2VALUES(' ' '1' '5' '6' '7')
     A            THST           1A  O  9  4DSPATR(HI)
     A            THPOSN         4A  O  9  6
     A            THUNR          2Y 0O  9 12EDTCDE(4)
     A                                      DSPATR(ND)
     A            THAVD          4Y 0O  9 16EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            THOPD          5Y 0O  9 22EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            THVEKT         5Y 0O  9 29EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            THVOL          7Y 3O  9 36EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            THVTXT         2A  O  9 46
     A  44                                  DSPATR(ND)
     A            THFRAN         3A  O  9 50
     A  44                                  DSPATR(ND)
     A            THTXT         26A  O  9 54
     A            THEINT        13Y 2O 10 46EDTCDE(L)
     A                                      COLOR(YLW)
     A            THEUTG        13Y 2O 10 63EDTCDE(L)
     A                                      COLOR(YLW)
     A            THAINT        13Y 2O 10  6EDTCDE(L)
     A                                      COLOR(YLW)
     A            THAUTG        13Y 2O 10 23EDTCDE(L)
     A                                      COLOR(YLW)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYAM91'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 TRO0454 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(009 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  2 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 23MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2KNA          8Y 0O  2 30EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAVN        30A  O  2 39COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2MSGCON(009 TRO0105 *LIBL/SYMSGF)
     A            W2EREF        15A  O  3 12COLOR(WHT)
     A                                  3 29MSGCON(009 TRO0107 *LIBL/SYMSGF)
     A            W2ADAT         6Y 0O  3 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3 49MSGCON(009 TGI0015 *LIBL/SYMSGF)
     A            W2VALK         3A  O  3 59COLOR(WHT)
     A            W2KURS         7Y 4O  3 63EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(009 TGI0006 *LIBL/SYMSGF)
     A            W2AREF        15A  O  4 12COLOR(WHT)
     A                                  4 29MSGCON(009 TRO0102 *LIBL/SYMSGF)
     A            W2BDAT         6Y 0O  4 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  4 49MSGCON(009 TGI0013 *LIBL/SYMSGF)
     A            W2PERM         2S 0O  4 59COLOR(WHT)
     A            W2PERÅ         2S 0O  4 62COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  62                              6 24MSGCON(025 TRO0123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 50'6=Se oppdrag'
     A                                      COLOR(BLU)
     A                                  6 66'7=Se faktura'
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  6MSGCON(025 TGI0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  8 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Ny linje')
     A N95                                  CF10(10 'FERDIG')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CF13(13 'Se utregning')
     A                                      CF07(07 'HENT ORIGINALE DATA')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Sum'
     A            TOAINT        13Y 2O  1  6EDTCDE(L)
     A                                      COLOR(WHT)
     A            TOAUTG        13Y 2O  1 23EDTCDE(L)
     A                                      COLOR(WHT)
     A            TOEINT        13Y 2O  1 46EDTCDE(L)
     A                                      COLOR(WHT)
     A            TOEUTG        13Y 2O  1 63EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 50MSGCON(012 DIV2523 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              5  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R DLTLIN
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6MSGCON(025 TGI0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            THPOSN         4A  O  3  6
     A            THUNR          2  0O  3 12EDTCDE(4)
     A                                      DSPATR(ND)
     A            THAVD          4Y 0O  3 16EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            THOPD          5Y 0O  3 22EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            THVEKT         5Y 0O  3 29EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            THVOL          7Y 3O  3 36EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            THVTXT         2A  O  3 46
     A  44                                  DSPATR(ND)
     A            THFRAN         3A  O  3 50
     A  44                                  DSPATR(ND)
     A            THTXT         26A  O  3 54
     A            THAINT        13Y 2O  4  6EDTCDE(L)
     A                                      COLOR(YLW)
     A            THAUTG        13Y 2O  4 23EDTCDE(L)
     A                                      COLOR(YLW)
     A            THEINT        13Y 2O  4 46EDTCDE(L)
     A                                      COLOR(YLW)
     A            THEUTG        13Y 2O  4 63EDTCDE(L)
     A                                      COLOR(YLW)
     A                                  5 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R CHGLIN
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6MSGCON(025 TGI0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            THPOSN         4A  O  3  6
     A            THUNR          2Y 0O  3 12EDTCDE(4)
     A                                      DSPATR(ND)
     A                                      DSPATR(PC)
     A            THAVD          4Y 0O  3 16EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            THOPD          5Y 0O  3 22EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            THVEKT         5Y 0B  3 29EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A            THVOL          7Y 3B  3 36EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A            THVTXT         2A  B  3 46
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A                                      VALUES('  ' 'LM' 'M3')
     A            THFRAN         3A  B  3 50
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A            THTXT         26A  B  3 54
     A            THEINT        13Y 2B  4 46EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            THEUTG        13Y 2B  4 63EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            THAINT        13Y 2B  4  6EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            THAUTG        13Y 2B  4 23EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A          R ADDLIN
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6MSGCON(025 TGI0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            THPOSN         4D  B  3  6
     A            THUNR          2Y 0B  3 12EDTCDE(4)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            THAVD          4Y 0B  3 16EDTCDE(4)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            THOPD          5Y 0B  3 22EDTCDE(4)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            THVEKT         5Y 0B  3 29EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            THVOL          7Y 3B  3 36EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            THVTXT         2A  B  3 46VALUES('  ' 'LM' 'M3')
     A            THFRAN         3A  B  3 50
     A            THTXT         26A  B  3 54
     A            THEINT        13Y 2B  4 46EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            THEUTG        13Y 2B  4 63EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            THAINT        13Y 2B  4  6EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            THAUTG        13Y 2B  4 23EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Ny linje')
     A                                      CF07(07 'NY BEREGNING')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM91'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 TRO0454 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(009 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  2 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 23MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2KNA          8Y 0O  2 30EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAVN        30A  O  2 39COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2MSGCON(009 TRO0105 *LIBL/SYMSGF)
     A            W2EREF        15A  O  3 12COLOR(WHT)
     A                                  3 29MSGCON(009 TRO0107 *LIBL/SYMSGF)
     A            W2ADAT         6Y 0O  3 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3 49MSGCON(009 TGI0015 *LIBL/SYMSGF)
     A            W2VALK         3A  O  3 59COLOR(WHT)
     A            W2KURS         7Y 4O  3 63EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(009 TGI0006 *LIBL/SYMSGF)
     A            W2AREF        15A  O  4 12COLOR(WHT)
     A                                  4 29MSGCON(009 TRO0102 *LIBL/SYMSGF)
     A            W2BDAT         6Y 0O  4 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  4 49MSGCON(009 TGI0013 *LIBL/SYMSGF)
     A            W2PERM         2S 0O  4 59COLOR(WHT)
     A            W2PERÅ         2S 0O  4 62COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A  62                              6 24MSGCON(025 TRO0123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  4MSGCON(071 DIV2429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  6MSGCON(025 TGI0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  8 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            XTOTXT        50A  O 21  2COLOR(WHT)
     A            XTOTBL    R        O 21 67REFFLD(TRAVHR/THAVT *LIBL/TRAVH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM91'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 TRO0454 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4 18MSGCON(010 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0B  4 29CHECK(FE)
     A                                      CHECK(RB)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(12)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM91'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 TRO0454 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3MSGCON(010 TRO0103 *LIBL/SYMSGF)
     A            W2TNR          8Y 0O  2 14DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 67TIME
     A                                  3  3MSGCON(010 TGI0020 *LIBL/SYMSGF)
     A            W2TYPT        16A  O  3 14DSPATR(HI)
     A            W2AMT         16A  O  3 32DSPATR(HI)
     A            W2TYPE         1A  O  3 56DSPATR(ND)
     A                                  4  3MSGCON(010 TGI0004 *LIBL/SYMSGF)
     A            W2KNA          8Y 0B  4 14EDTCDE(4)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            W2NAVN        30A  O  4 23DSPATR(HI)
     A                                  6  3MSGCON(010 TRO0105 *LIBL/SYMSGF)
     A            W2EREF        15A  B  6 14DSPATR(PR)
     A                                  7  3MSGCON(010 TGI0006 *LIBL/SYMSGF)
     A            W2AREF        15A  B  7 14DSPATR(PR)
     A                                  8  3MSGCON(010 TGI0008 *LIBL/SYMSGF)
     A            W2ENH         15A  B  8 14DSPATR(PR)
     A                                  9  3MSGCON(010 TGI0019 *LIBL/SYMSGF)
     A            W2BILN        10A  B  9 14DSPATR(PR)
     A                                 10  3MSGCON(010 TRO0107 *LIBL/SYMSGF)
     A            W2ADAT         6Y 0B 10 14EDTCDE(Y)
     A                                      DSPATR(PR)
     A                                 11  3MSGCON(010 TRO0102 *LIBL/SYMSGF)
     A            W2BDAT         6Y 0B 11 14EDTCDE(Y)
     A                                      DSPATR(PR)
     A                                 12  3MSGCON(010 TGI0009 *LIBL/SYMSGF)
     A            W2KNT          8Y 0B 12 14EDTCDE(4)
     A                                      DSPATR(PR)
     A            W2TRAN        30A  B 12 23DSPATR(PR)
     A                                 13  3MSGCON(010 TGI0010 *LIBL/SYMSGF)
     A            W2TRKO        13Y 2B 13 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(PR)
     A            W2TRKP         1A  B 13 32VALUES(' ' 'A' 'E' 'T')
     A                                      DSPATR(PR)
     A                                 14  3MSGCON(010 TGI0011 *LIBL/SYMSGF)
     A            W2FERJ        13Y 2B 14 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(PR)
     A            W2FERP         1A  B 14 32VALUES(' ' 'A' 'E' 'T')
     A                                      DSPATR(PR)
     A                                 14 38MSGCON(016 TRO0244 *LIBL/SYMSGF)
     A                                 15  3MSGCON(010 TGI0012 *LIBL/SYMSGF)
     A            W2TREK        13Y 2B 15 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(PR)
     A            W2TREP         1A  B 15 32VALUES(' ' 'A' 'E' 'T')
     A                                      DSPATR(PR)
     A                                 15 38MSGCON(016 TRO0245 *LIBL/SYMSGF)
     A                                 16  3MSGCON(009 TGI0033 *LIBL/SYMSGF)
     A            W2DIV         13Y 2B 16 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(PR)
     A            W2DIVP         1A  B 16 32VALUES(' ' 'A' 'E' 'T')
     A                                      DSPATR(PR)
     A            W2DIVT        30A  B 16 37DSPATR(PR)
     A            W2DIV2        13Y 2B 17 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(PR)
     A            W2DIVQ         1A  B 17 32VALUES(' ' 'A' 'E' 'T')
     A                                      DSPATR(PR)
     A            W2DIVU        30A  B 17 37
     A                                 18  3MSGCON(010 TGI0013 *LIBL/SYMSGF)
     A            W2PERM         2Y 0B 18 14CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  71                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            W2PERÅ         2Y 0B 18 17CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  71                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                 18 21MSGCON(010 TGI0014 *LIBL/SYMSGF)
     A                                 19  3MSGCON(010 TGI0015 *LIBL/SYMSGF)
     A            W2VALK         3A  B 19 14DSPATR(PR)
     A                                 20  3MSGCON(010 TGI0016 *LIBL/SYMSGF)
     A            W2KURS         7Y 4B 20 14CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(L)
     A                                      DSPATR(PR)
     A                                 21  3MSGCON(010 TGI0017 *LIBL/SYMSGF)
     A            W2AGNT         3Y 0B 21 14CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                      DSPATR(PR)
     A                                 21 19MSGCON(010 TGI0018 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R AVREGN
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM91'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 TRO0454 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(007 DIV2423 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  2 10COLOR(WHT)
     A                                  2 20MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2KNA          8Y 0O  2 27EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAVN        30A  O  2 36COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(016 DIV2425 *LIBL/SYMSGF)
     A            W2VALK         3A  O  3 20COLOR(WHT)
     A            W2KURS         7Y 4O  3 24EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  3 35MSGCON(008 DIV2426 *LIBL/SYMSGF)
     A            W2PERM         2S 0O  3 44COLOR(WHT)
     A            W2PERÅ         2S 0O  3 47COLOR(WHT)
     A                                  3 51MSGCON(014 DIV2427 *LIBL/SYMSGF)
     A            W2AGNT         3Y 0O  3 66COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A                                  6 25MSGCON(007 DIV2459 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 62MSGCON(009 DIV2460 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 15MSGCON(030 DIV2522 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 53MSGCON(026 DIV2522 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(078 DIV2512 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(007 DIV2515 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            SMAINT        13Y 2O  9 10EDTCDE(L)
     A            SMAUTG        13Y 2O  9 27EDTCDE(L)
     A            SMEINT        13Y 2O  9 48EDTCDE(L)
     A            SMEUTG        13Y 2O  9 65EDTCDE(L)
     A                                 10  2MSGCON(078 DIV2524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(007 DIV2516 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51        SLAINT        13Y 2O 11 10EDTCDE(L)
     A  52        SLAUTG        13Y 2O 11 27EDTCDE(L)
     A  53        SLEINT        13Y 2O 11 48EDTCDE(L)
     A  54        SLEUTG        13Y 2O 11 65EDTCDE(L)
     A                                 13  2MSGCON(007 DIV2517 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51        KSAINT        13Y 2O 13 10EDTCDE(L)
     A  52        KSAUTG        13Y 2O 13 27EDTCDE(L)
     A                                 14  2MSGCON(078 DIV2524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N50                             15  2MSGCON(007 DIV2518 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  50                             15  2MSGCON(007 DIV2519 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            GTBEL1        13Y 2O 15 18EDTCDE(L)
     A            PAPRO1         3Y 0O 17  2COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17  6'%'
     A                                      COLOR(WHT)
     A            PABEL1        13Y 2O 17 18EDTCDE(L)
     A                                 18  2MSGCON(078 DIV2524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2VALA         3A  O 20 61COLOR(WHT)
     A  55        RSBEL1        13Y 2O 20 65EDTCDE(L)
     A                                      COLOR(WHT)
     A  56        RSBEL2        13Y 2O 20 65EDTCDE(L)
     A                                      COLOR(WHT)
     A  55                             20 54MSGCON(006 DIV2520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  56                             20 54MSGCON(006 DIV2521 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 21 54MSGCON(026 DIV2514 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
