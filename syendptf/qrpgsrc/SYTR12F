      *
      **************************************************************
      *** PROGRAM SKAL FINNE PROSENTSATS TRANSPORTØRAVREGNING    ***
      ***     INNPARAMETRE - AVD/OPPDRAG/GEBYRKODE               ***
      ***     UTPARAMETRE  - PROV.SATS                           ***
      **************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12057 * 01 JOV PTF12 Avvikende sast 0,00 på transp tillates (skal IKKE trekkes prov, ALT til Transp)
********************************************************************
      *
     FHEADF     IF   E           K DISK
     FDISP      IF   E           K DISK
     FTURER     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FTRAB      IF   E           K DISK
     FFIRM      IF   E           K DISK
     C                   EXSR      INIT
     C     *IN98         IFEQ      '0'
     C* Transportørspesifik sats....
     C                   MOVE      TUKNT2        YYTNR
     C                   EXSR      PROV
N01   * 37 on = leste alle i PROV uten å finne ( tillater avvikende sats er 0 = alt til transp)
S01  C*    TBPRO         IFEQ      *ZEROS
N01  C     *in37         IFEQ      *ON
     C* ... eller generell prov.sats benyttes ...
     C                   MOVE      *ZEROS        YYTNR
     C                   EXSR      PROV
     C                   END
     C                   END
     C*
     C                   MOVE      TBPRO         UPRO
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     PROV          BEGSR
     C***********************************************
     C     TRABK         SETLL     TRAB
     C     YYTNR         READE     TRAB                                   37
     C     *IN37         DOWEQ     '0'
     C     YYTNR         IFEQ      *ZEROS                                       KUN VED GENERELL..
     C     TBTKAT        ANDNE     *BLANKS                                      *KATEGORI
     C     TBTKAT        ANDNE     VMTKAT                                       (MEDLEM/IKKE
     C                   GOTO      NESTE                                         M.M....)
     C                   END
     C     TBUNIT        IFNE      *BLANKS                                      KUN BILNR...
     C     TBUNIT        ANDNE     TUBILN
     C                   GOTO      NESTE
     C                   END
     C     TBAVD         IFNE      *ZEROS                                       KUN AVDELING..
     C     TBAVD         ANDNE     HEAVD
     C                   GOTO      NESTE
     C                   END
     C     TBGEB         IFNE      *BLANKS                                      KUN GEBYR...
     C     TBGEB         ANDNE     FAVK
     C                   GOTO      NESTE
     C                   END
     C     TBLK          IFNE      *BLANKS                                      KUN LANDKODE..
     C* INNLAND - BEGGE KODER MÅ MATCHE
     C     TBLK          IFEQ      'NO'
     C     TBLK          IFNE      HELKA                                            FRA
     C     TBLK          ORNE      HETRI                                            TIL
     C                   GOTO      NESTE
     C                   END
     C                   ELSE
     C* IMP/EKSP- ENTEN FRA ELLER TIL MÅ MATCHE...
     C     TBLK          IFNE      HELKA                                            FRA
     C     TBLK          ANDNE     HETRI                                            TIL
     C                   GOTO      NESTE
     C                   END
     C                   END
     C                   END
     C     TBOPTY        IFNE      *BLANKS                                      KUN BILNR...
     C     TBOPTY        ANDNE     HEOT
     C                   GOTO      NESTE
     C                   END
     C     TBVKTT        IFNE      *ZEROS                                       KUN Vekt T.o.m..
     C     TBVKTT        ANDLT     HEFBV
     C                   GOTO      NESTE
     C                   END
     C     TBDATE        IFLT      TUDT                                         UTLØPT DATO...
     C                   GOTO      NESTE
     C                   END
     C* OK!! - Er gjennom alle testene - gyldig prov.linje er funnet.....
     C                   GOTO      ENDPRO
     C***
     C* Kommer hit når ett av vilkårene ikke ble oppfylt
     C     NESTE         TAG
     C     YYTNR         READE     TRAB                                   37
     C  N37              END
     C* FANT IKKE SATS
     C                   MOVE      *ZEROS        TBPRO
     C*
     C     ENDPRO        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C**
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UVK               3
     C                   PARM                    UPRO              5 2
     C                   MOVE      UAVD          HEAVD
     C                   MOVE      UOPD          HEOPD
     C                   MOVE      UVK           FAVK              3
     C*
     C     *LIKE         DEFINE    TBFIRM        YYTNR
     C     *LIKE         DEFINE    TBLIN         YYLIN
     C*
     C     TRABK         KLIST
     C                   KFLD                    YYTNR
     C                   KFLD                    YYLIN
     C                   MOVE      *ZEROS        YYLIN
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TURERK        KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNT2
     C*
     C                   READ      FIRM                                   33
     C     HEADFK        CHAIN     HEADF                              33
     C     HEADFK        CHAIN     DISP                               33
     C     TURERK        CHAIN     TURER                              98
     C     TRANKE        CHAIN     TRAN                               33
     C*
     C                   ENDSR
     C*
