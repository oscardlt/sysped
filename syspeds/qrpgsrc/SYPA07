     FPALFIR    UF   E             DISK
     FPALLDS04  UF   E           K DISK
     FPALLDS08  UF   E           K DISK
     F                                     RENAME(PALLDR:PALLDR08)
     FPAPA      UF   E           K DISK
     FPAPAL01   IF   E           K DISK
     F                                     RENAME(PAPAR:PAPAR2)
     FPKUSA     IF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FPRIS      IF   E           K DISK
     FSYPA07P   O    E             PRINTER OFLIND(*IN90)
     D                 DS
     D  IDAG                   1      8  0
     D  IDAGÅ                  1      4  0
     D  IDAGM                  5      6  0
     D  IDAGD                  7      8  0
     C                   EXSR      INIT
      *
      * Skriv - Kun for en / flere utfra utvalg???
     C     UUPPNR        IFNE      *ZEROS
     C                   MOVE      UUPPNR        XXPPNR            8 0
     C     XXPPNR        CHAIN(N)  PAPA                               33
     C                   EXSR      ENPART
     C                   ELSE
     C                   EXSR      FLPART
     C                   END
      *
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      ********************************************************
     C     ENPART        BEGSR
      ********************************************************
      * Finn PARTENS SALDO FRA FORIGE KJØRING VIA PAR/BIL/SALD-REG
     C                   MOVE      PPNR          PKPNR
     C                   MOVE      *BLANKS       PKBILN
     C                   MOVE      *BLANKS       XXBILN
      *    STATUS U=Ubehandlede/upurrede/uferdige (MEN BUNT ER KJØRT)
     C                   MOVE      'U'           XXSTAT
     C                   MOVE      *ZEROS        SIDE
     C                   MOVE      *ZEROS        TOTANT            5 0
     C                   MOVE      *ZEROS        TOTMAN
     C                   MOVE      *ZEROS        TOTLAN
      * Hent neste KTO.UTSKR.NR (purrenr)
     C     1             CHAIN     PALFIR                             33
     C                   MOVE      PFPUNR        PURRNR            7 0
     C                   ADD       1             PFPUNR
     C  N33              UPDATE    PALFIRR
      * Hent pallepartens navn
     C     KUNKEY        CHAIN     CUNDL01                            33
     C                   MOVE      PPNR          XXNR              8 0
     C                   MOVE      PPKUNR        XXKUNR            8 0
     C                   MOVE      KNAVN         XKNAVN
     C                   MOVE      ADR1          XADR1
     C                   MOVE      ADR2          XADR2
     C                   MOVE      POSTNR        XPOSTN
     C                   MOVE      ADR3          XADR3
      *
      *
      * SKRIV/Merk detaljlinjer med status/KTO-UTDRAGSNR
     C     PPTRNR        COMP      *ZEROS                             70
      * 70 OFF=KUN PARTSSALDO
     C     *IN70         IFEQ      '0'
      * Hent partens saldo fra forige utskrift
     C     PKUSAK        SETGT     PKUSA
     C     PKUSAK        READPE    PKUSA                                  33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *ZEROS        PKDT
     C                   MOVE      *ZEROS        PKNR
     C                   MOVE      *ZEROS        PKSALE
     C                   END
     C                   EXSR      SKRIV
     C                   ELSE
      * 70 ON=OPPFØLGING PÅ BILNIVÅ, Akkumuler også parens totaler
     C                   MOVE      *ZEROS        XOTANT
     C                   MOVE      *ZEROS        XOTMAN
     C                   MOVE      *ZEROS        XOTLAN
      * Finn første bilnr som har bevegelse (og hent inngående saldo)
     C     NESTEB        TAG
     C                   EXSR      FINBIL
     C                   EXSR      SKRIV
     C     *IN75         IFNE      '1'
     C                   GOTO      NESTEB
     C                   END
     C                   END
      **
      * Minst en er skrevet. (RETURPARAMETER)
     C                   MOVE      'X'           MINST1
     C*                    END
      *
     C                   ENDSR
      *
      ********************************************************
     C     FINBIL        BEGSR
      ********************************************************
     C                   MOVE      *ZEROS        TOTANT
     C                   MOVE      *ZEROS        TOTMAN
     C                   MOVE      *ZEROS        TOTLAN
     C* Finn neste bil for aktuell part..
     C                   MOVE      '9'           XXBILN
     C     PALKBI        SETLL     PALLDS08
     C     PALKEY        READE     PALLDS08                               75
     C     *IN75         DOWEQ     '0'
      * hopp ut straks EN gyldig post er funnet
     C     PAKVDT        IFLE      UUDAT8
     C                   GOTO      UTFINB
     C                   END
     C     PALKEY        READE     PALLDS08                               75
     C                   END
      *
     C     UTFINB        TAG
      *
      * 75 ON - INGEN FLERE BILER,,, KLAGJØR FOR TOTALER..
     C     *IN75         IFEQ      '0'
     C                   MOVE      PABILN        XXBILN
     C                   MOVE      PABILN        PKBILN
     C                   ELSE
     C                   MOVE      *BLANKS       XXBILN
     C                   MOVEL     '*TOTAL*'     XXBILN
     C                   MOVE      *BLANKS       PKBILN
     C                   END
      *
      * Hent bilens (eller total for parten) saldo fra forige utskrift
     C     PKUSAK        SETGT     PKUSA
     C     PKUSAK        READPE    PKUSA                                  33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *ZEROS        PKDT
     C                   MOVE      *ZEROS        PKNR
     C                   MOVE      *ZEROS        PKSALE
     C                   END
      *
     C                   ENDSR
      *
      ********************************************************
     C     SKRIV         BEGSR
      ********************************************************
     C* Skriv dokumentet
      *
     C   70              Z-ADD     1             BISIDE
     C                   ADD       1             SIDE
      * Inngående saldo:60 ON = I DERES FAVØR,   OFF = I DERES DISFAVØR
     C     PKSALE        COMP      *ZEROS                               6060
     C                   WRITE     HEAD
      *
     C     XXBILN        IFNE      '*TOTAL*'
      * 70 OFF=STIGENDE DATO , 70 ON=PÅ STIGENDE BILNR (BILOPPFØLGING)
     C  N70PALKEY        SETLL     PALLDS04
     C  N70PALKEY        READE     PALLDS04                               33
     C   70PALKBI        SETLL     PALLDS08
     C   70PALKBI        READE     PALLDS08                               33
     C     *IN33         DOWEQ     '0'
     C     PAKVDT        IFGT      UUDAT8
     C                   GOTO      UTSKRI
     C                   END
     C                   MOVE      *ZEROS        XXMANT            5 0
     C                   MOVE      *ZEROS        XXLANT            5 0
     C     PAANT         IFGT      0
     C                   Z-ADD     PAANT         XXMANT                           MOTTATT
     C                   ELSE
     C                   Z-SUB     PAANT         XXLANT                           LEVERT
     C                   END
      * KOLONNETALL(ALLTID +)/GRANDTOTAL M/FORTEGN FOR SALDO-JUSTERING
     C                   ADD       XXMANT        TOTMAN                           MOTATT
     C                   ADD       XXLANT        TOTLAN                           LEVERT
     C                   ADD       PAANT         TOTANT                           TOTALT
     C   70              ADD       XXMANT        XOTMAN                           MOTATT
     C   70              ADD       XXLANT        XOTLAN                           LEVERT
     C   70              ADD       PAANT         XOTANT                           TOTALT
     C                   MOVE      'P'           PASTAT
     C                   MOVE      PURRNR        PAPUNR
     C  N70              UPDATE    PALLDR
     C   70              UPDATE    PALLDR08
      * SIDESKIFT
     C     PABEIE        COMP      'X'                                    62     (=BILEIER)
     C     *IN90         IFEQ      '1'
     C                   ADD       1             SIDE
     C   70              ADD       1             BISIDE
     C                   WRITE     HEAD
     C                   MOVE      '0'           *IN90
     C                   END
     C                   WRITE     DET
     C  N70PALKEY        READE     PALLDS04                               33
     C   70PALKBI        READE     PALLDS08                               33
     C                   END
      *
     C     UTSKRI        TAG
      * Oppdater "Saldo purret"/siste purredato  i partsregisteret
     C     XXPPNR        CHAIN     PAPA                               33
     C                   ADD       TOTANT        PPSALP
     C                   MOVE      IDAG          PPSPDT                         KJØREDATO
     C                   MOVE      UUDAT8        PPSTDT                         T.O.M DATO
     C                   UPDATE    PAPAR
      *
      *
     C                   ELSE
      * KUN VED PARTER SOM HAR SPESIFIKASJON PR.BIL
      * SKRIVER SIDE FOR *TOTAL*, LEGG PARTENS GRANDTOTALER I TOT....
      * (Partens saldototal i PAPA er alt oppdat via ADD i hver syklus)
     C                   MOVE      XOTANT        TOTANT
     C                   MOVE      XOTMAN        TOTMAN
     C                   MOVE      XOTLAN        TOTLAN
     C                   WRITE     TOTTXT
     C                   END
      **
      * I register PKUSA (saldohistorikk) er det alltid ubetinget write
     C     PKSALE        ADD       TOTANT        NYSALE            7 0
     C                   MOVE      NYSALE        PKSALE
     C                   MOVE      IDAG          PKDT                           KJØREDATO
     C                   MOVE      UUDAT8        PKDTT                          T.O.M DATO
     C                   MOVE      PURRNR        PKNR
     C                   WRITE     PKUSAR
      * 60 ON = I DERES FAVØR,   OFF = I DERES DISFAVØR
     C     PKSALE        COMP      *ZEROS                               6060
      * HVIS SKYLDIG PALLER OG STANDARD ENH-PRIS LIGGER INNE REGN UT
     C                   MOVE      *ZEROS        TOTBEL            9 0
     C     *IN60         IFEQ      '0'
     C     PFVK          ANDNE     *BLANKS
     C     PFBTAB        ANDNE     *BLANKS
     C                   Z-ADD     PKSALE        PRINT
     C     PRISK1        SETLL     PRIS
     C     PRISK2        READE     PRIS                                   33
     C     *IN33         IFEQ      '0'
     C     PRBELØ        MULT      PKSALE        TOTBEL
     C                   END
     C                   END
     C     TOTBEL        COMP      *ZEROS                             61
     C                   WRITE     TOT
      *
      *
     C                   ENDSR
      *
      *
     C***********************************************
     C     FLPART        BEGSR
     C***********************************************
     C* FLERE PARTER - GÅ GJENNOM ALLE EXKLUDER ETTER UVALG I BILDET
     C* 51 ON ALFA SORTERING/OFF PARTSNR SORTERING
     C     W2ALFA        COMP      'X'                                    51
     C  N51*LOVAL        SETLL     PAPA
     C   51*LOVAL        SETLL     PAPAL01
     C*
     C  N51              READ      PAPA                                   94
     C   51              READ      PAPAL01                                94
      *
     C     *IN94         DOWEQ     '0'
     C     NESTE         TAG
     C                   EXSR      TSTEXC
     C     EXCLUD        IFEQ      'J'
     C  N51              READ      PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94              GOTO      NESTE
     C   94              GOTO      UTFLER
     C                   END
     C* SKAL VÆRE MED...
     C                   MOVE      PPNR          XXPPNR
     C                   EXSR      ENPART
     C  N51              READ      PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94              END
     C*
     C     UTFLER        ENDSR
     C***********************************************
     C     TSTEXC        BEGSR
     C***********************************************
     C                   MOVE      'N'           EXCLUD            1
     C*
     C     W2KUNR        IFNE      *ZEROS
     C     W2KUNR        ANDNE     PPKUNR
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2KATE        IFNE      ' '
     C     W2KATE        ANDNE     PPKATE
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '+'
     C     PPSALP        ANDLE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '-'
     C     PPSALP        ANDGE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '0'
     C     PPSALP        ANDNE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '+'
     C     PPSALT        ANDLE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '-'
     C     PPSALT        ANDGE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '0'
     C     PPSALT        ANDNE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C*
     C     UTEXCL        ENDSR
      *
      ********************************************************
     C     INIT          BEGSR
      ********************************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUPPNR            8 0
     C                   PARM                    UUDATO            6 0
     C                   PARM                    W2KUNR            8 0
     C                   PARM                    W2KATE            1
     C                   PARM                    W2SPPM            1
     C                   PARM                    W2STPM            1
     C                   PARM                    W2ALFA            1
     C                   PARM                    MINST1            1
      *
     C                   MOVE      ' '           MINST1
     C                   MOVE      UUDATO        TOMDAT            6 0
     C     UUDATO        IFGT      501231
     C     UUDATO        ADD       19000000      UUDAT8            8 0
     C                   ELSE
     C     UUDATO        ADD       20000000      UUDAT8
     C                   END
      *
     C     PALKEY        KLIST
     C                   KFLD                    XXSTAT            1
     C                   KFLD                    XXPPNR
     C     PALKBI        KLIST
     C                   KFLD                    XXSTAT            1
     C                   KFLD                    XXPPNR
     C                   KFLD                    XXBILN           10
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    PPKUNR
     C     PKUSAK        KLIST
     C                   KFLD                    PKPNR
     C                   KFLD                    PKBILN
     C     PRISK1        KLIST
     C                   KFLD                    PFBTAB
     C                   KFLD                    PFVK
     C                   KFLD                    PRINT
     C     PRISK2        KLIST
     C                   KFLD                    PFBTAB
     C                   KFLD                    PFVK
     C     *LIKE         DEFINE    PKDT          XKDT
     C     *LIKE         DEFINE    PKNR          XKNR
     C     *LIKE         DEFINE    PKSALE        XKSALE
     C     *LIKE         DEFINE    TOTANT        XOTANT
     C     *LIKE         DEFINE    TOTMAN        XOTMAN
     C     *LIKE         DEFINE    TOTLAN        XOTLAN
      * Dato for purring i ÅÅMMDD format
     C                   MOVE      UDAY          IDAGD
     C                   MOVE      UMONTH        IDAGM
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          IDAGÅ
     C                   ELSE
     C     UYEAR         ADD       2000          IDAGÅ
     C                   END
      * Hent datoformat/firmakode
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *Konverter til og med dato til inhouseformat
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    TOMDAT
      * Hent headingtekst
     C     1             CHAIN(N)  PALFIR                             33
      *
     C                   ENDSR
      *
