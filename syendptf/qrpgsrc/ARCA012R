**********************************************************************************************
      * KOBLE TIL OPPDRAG VIA FRI SØKEVEI(FILNAVN) FRA MAPPE FRISOK
**********************************************************************************************
      * OBS - FØR KJØRING må typen være definert i fil ARKTXT
      *       og denne bør gjerne peke på "eksternt" arkivlager med
      *       avvikende bane (f.eks /pdf/scan ) (fil ARKEXT)
**********************************************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF11 finne extension
10XXX * 02 SH  PTF11 arc044 tilleggsparameter
      *%%%%
      * 03 JOV PTF12 Hvis lagring i google cloud send rett dit (den rutinen sletter lokalt)
10XXX * 04 sh  PTF12 arc044 må til batch
********************************************************************
      *
     H DFTACTGRP(*NO)
      *""""""""""""""""
     FFIRMARC   IF   E           K DISK
N03  FFIRMGOOG  IF   E           K DISK
     FARKIVP    O    E             DISK
     FFIRM      IF   E             DISK
     FFRISOL01  IF   E           K DISK
     FARKTXT    IF   E           K DISK    PREFIX(X_)
     FARKEXT    IF   E           K DISK    PREFIX(Y_)
      *""""""""""""""""
     D DirName         S            100A
     D EntryName       S            120A
     D EntryPath       S            256A
      *________________
      * Andre variabler
      *""""""""""""""""
     D TYPE            S              2
     D NULL            S              1A   INZ(X'00')
     D CMDLINE         S            512
     D CMDLEN          S             15  5
     D NEWDIR          S            100
     D NEWFILE         S            100
     D NEWOBJ          S            200
s01  D*NEWEND          S             10
     D NEWRAN          S             10

     C                   EXSR      INIT
     C                   EXSR      MOV
     C     OkMov         IFEQ      'J'
     C                   EXSR      ARK
     C                   end
     C                   EVAL      *inlr = *on
     C                   RETURN
      *
      ****************************************************************
     C     INIT          BEGSR
      ****************************************************************
      *
      *
     C     *entry        PLIST
     C                   PARM                    DirName
     C                   PARM                    EntryName
     C                   PARM                    EntryPath
      *
      *
     C     FriKey        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
      *
      * KODEN ER 3 første av filnavn
      *
     C                   EVAL      FSKODE = %subst(EntryName:1:3)
      *
      * Type er hardkodet
      *
     C                   EVAL      TYPE = 'ZH'
      *
      * FSSOK   fra posisjon 3  uten .PDF
      *
     C     '.'           SCAN      EntryName     X                 3 0
     C
     c     X             ifeq      0
     C                   EVAL      *inlr = *on
     C                   RETURN
     c                   else
     C                   EVAL      FSSOK = %subst(EntryName:4:x-4)
     c                   endif
      *
      * OM AKTUELL SAK IKKE FINNES HOPPER EN UT ALLETEDE HER..
      *
     C     FriKey        Chain     FRISOL01                           33
      *
     C     *IN33         IFEQ      *ON
     C                   EVAL      *inlr = *on
     C                   RETURN
     C                   end
      *
      * Definiere parameterliste
      *
     C     QCMDEXC       PLIST
     C                   PARM                    CMDLINE
     C                   PARM                    CMDLEN
     C                   READ      FIRMARC                                41

     C     EXKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    X_ARKLAG
      *
     C                   ENDSR
      *
      ***********************************************************************
     C     MOV           BEGSR
      ***********************************************************************
      *
      * Les firmafil for å få tak i firmakode.
     C                   READ      FIRM
      * Hent dato og klokkeslett
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Hent slutt på filnavn (ex. .pdf) fra frafil
s02  C*    '.'           SCAN      EntryName     X                 3 0
s02  C*    '_'           SCAN      EntryName:X   Y                 3 0
s02  C*    Y             ifeq      *zeros
s02  c*    ' '           SCAN      EntryName:X   Y                 3 0
s02  C*                  end
s02  c*                  EVAL      NEWEND = %SUBST(EntryName:X:y-x)
n02  c                   eval      par512=EntryName
n02  c                   call      'SYGE112'
n02  c                   parm                    par512          512
n02  c                   parm                    NEWEND           10
      * Generer random number
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN
      * Definier ny katalog Hent fra standard
     C                   EVAL      NEWDIR = '/' + %trimr(ARCANE)
      * Er det på dokumenttypenivå angitt avvikende katalog?(via EXT lager)
     C                   EVAL      *IN51 = *OFF
     C     TYPE          CHAIN     ARKTXT                             51
     C                   IF        *IN51 = *OFF
     C     X_ARKLAG      IFNE      *BLANKS
     C     EXKEY         CHAIN     ARKEXT                             51
     C                   IF        *IN51 = *OFF
     C                   EVAL      NEWDIR = '/' + %trimr(Y_ARCANE)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      * Definier nytt filnavn
     C                   MOVE      FSAVD         FSAVDA            4
     C                   MOVE      FSOPD         FSOPDA            7
     C                   EVAL      NEWFILE= %trim(TYPE)
     C                             + %SUBST(WDATO:1:4)
     C                             + %trimr(FSAVDA)
     C                             + %trimr(FSOPDA)
     C                             + NEWRAN
     C                             + NEWEND
      * Lag nytt objektnavn
     C                   EVAL      NEWOBJ = %trim(NEWDIR) + '/'
     C                             + %trimr(NEWFILE)
      * Flytte objekt
     C                   EVAL      CmdLine = 'MOV OBJ(' + X'7D'
     C                             + %trimr(EntryPath) + X'7D'
     C                             + ') TOOBJ(' +X'7D'
     C                             + %trimr(NEWOBJ) + X'7D' + ')'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   MOVE      'J'           OkMov             1
     C                   MOVEl     EntryPath     FRA             120
     C                   MOVEl     NewObj        Til             120
     C                   CALL      'ARCAMOVC'
     C                   PARM                    FRA
     C                   PARM                    TIL
     C                   PARM                    OkMov
N03   *Hvis google cloud benyttes - send det i skyen med en gang
N03  C                   IF        OkMov = 'J'
N03  C     FIFIRM        chain     FIRMGOOG
N03  C                   if        %found and GOBUID <> *blanks
N03  c                   eval      Companyid = GOBUID
N03  c                   eval      UlocalPath= %trim(NEWDIR)+'/'
N03  c                   eval      USrcFile  = NEWFILE
N03  c                   eval      UNewFile  = *blanks
N03  c                   eval      Udelete   = 'J'
N03  c                   call      'ARC99WSU1'
N03  c                   parm                    Companyid        10
N03  c                   parm                    UlocalPath      100
N03  c                   parm                    uSrcFile        120
N03  c                   parm                    udelete           1
N03  c                   parm                    uNewFile         60
N03  c                   parm                    Uok               1
N03  c                   endif
N03  c                   endif
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv til ARKIVP                                     ARK /
      *////////////////////////////////////////////////////////////
     C     ARK           BEGSR
      * Skriv til ARKIVP
     C                   CLEAR                   ARKIVR

     C                   EVAL      ARSTAT = 'A'
     C                   EVAL      ARTYPE = TYPE
     C                   EVAL      ARFIRM = FIFIRM
     C                   EVAL      ARAVD  = FSAVD
     C                   EVAL      AROPD  = FSOPD
     C                   EVAL      ARUSER = 'SY400USR'
     C                   move      WDATO         ARDATE
     C                   movel     WTID          ARTIME
     C                   EVAL      ARKUND = 0
     C                   EVAL      ARLINK = NEWFILE

     C                   WRITE     ARKIVR
N04   * GIDDER IKKE MERKE
     C                   MOVE      ARAVD         ARAVDX            4
     C                   MOVE      AROPD         AROPDX            7
     C                   MOVE      ARDATE        ARDATEX           8
     C                   CALL      'ARC044CL'
     C                   PARM                    ARAVDX
     C                   PARM                    AROPDX
     C                   PARM                    ARTYPE
     c                   parm                    ardateX
     c                   parm                    arlink
     C                   ENDSR
