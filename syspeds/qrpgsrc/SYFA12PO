      *  IFTMIN TIL POSTEN/BRING - S 93A
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 FIRTEKST TIL KUNDE - MAX 35 LANG IKL 'ESCAPE-TEGN' (?)
10XXX * 02 YBC PTF10 VED PROD.KODE=POZxx SEND TSR+7+Zxx+3+12
10XXX * 03 SH  PTF11 oppkrav kunde
********************************************************************
N03  FDOKUFE    IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FDOKUFV    IF   E           K DISK
     FDOKUFM    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFAK13     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FDOK29     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      IF A E             DISK
     FECNT93A   O  A E             DISK
     FECPI93A   O  A E             DISK
     FEDTM93A   O  A E             DISK
     FEFTX93A   O  A E             DISK
     FEGID93A   O  A E             DISK
     FELOC93A   O  A E             DISK
     FEMOA93A   O  A E             DISK
     FENAD93A   O  A E             DISK
     FECTA93A   O  A E             DISK
     FECOM93A   O  A E             DISK
     FEPCI93A   O  A E             DISK
     FERFF93A   O  A E             DISK
     FETDT93A   O  A E             DISK
     FETSR93A   O  A E             DISK
     FEUNH      O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *________________
      * DS for mottaker
      *""""""""""""""""
     D                 DS
     D  DFKNM                  1     12
     D  DFKNM2                 2     12
     D                 DS
     D  DFADM3                 1     30
     D  WADM3A                 1      4
     D  WADM3B                 6     30
     D  QADM3                  1     30
     D                 DS
     D  DF1004                 1     35
     D  DF1004A                2     10
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
N01   * Definere  hex appostrof
N01  D Ap              c                   x'7D'
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      */////////////////////////////////////////////////////////////////////////
      *// HOVEDRUTINE                                                         //
      */////////////////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * GET RECORDNUMBER FOR EDIM
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     *ON           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      * Hent fraktbrev
     C     DOKKEY        CHAIN     DOKUF                              51
     C     *IN51         CABEQ     *ON           SRA9
      * Hent oppdrag
     C     HEAKEY        CHAIN     HEADF                              51
     C     *IN51         CABEQ     *ON           SRA9
      * GET INTERCHG.ID
     C     TRAKEY        CHAIN     TRAN                               51
     C     *IN51         CABEQ     *ON           SRA9
      *  HENTER EVENTUELLT UTLEGG PÅ FAKTURA
     C     FAKTKE        CHAIN     FAK13                              51
     C                   IF        *IN51
     C                   MOVE      *BLANKS       WSVALK            3
     C                   Z-ADD     0             WSBELV           13 2
     C                   ELSE
     C                   MOVEL     FAVAL         WSVALK            3
     C                   Z-ADD     FABELV        WSBELV
     C                   ENDIF
      * ADD TO EDI-DB
     C                   EXSR      DTM00
     C                   EXSR      TSR00
     C                   EXSR      MOA00
     C                   EXSR      FTX00
     C                   EXSR      RFF03
     C                   EXSR      CPI06
     C                   EXSR      TDT08
     C                   EXSR      NAD10A
     C                   EXSR      NAD10B
     C                   EXSR      GID16
     C                   EXSR      CNT00
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTMIN'      U0065
     C                   MOVEL     'S'           U0052
     C                   MOVEL     '93A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'NOSM12'      U0057
     C                   MOVEL     '610'         U1001
     C                   MOVEL     DF1004        U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   MOVEL     VM0004        M0004
     C                   MOVEL     VM0010        M0010
     C                   MOVE      VM0035        M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   Z-ADD     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   Z-ADD     DFAVD         MAVD
     C                   Z-ADD     DFOPD         MTDN
     C                   Z-ADD     DFFBNR        MFFBNR
     C                   MOVE      DFVEN         MVEN
     C                   WRITE     EDIMR

     C     SRA9          TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kontrolltotaler                                    CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     0             WREP0
      * Vekt
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '7 '          C6069
     C                   Z-ADD     DFVKT         ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR
      * Antall kolli
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '11'          C6069
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'PCE'         C6411
     C                   WRITE     CNTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Betalingsbetingelser                               CPI06 /
      *////////////////////////////////////////////////////////////
     C     CPI06         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1

     C                   CLEAR                   CPIR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     6             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0

     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '1 '          C5237
     C                   MOVEL     'C'           C4237
     C                   WRITE     CPIR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sendningsdato                                      DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     0             WREP0

     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         DREP0

     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         D2380
     C                   MOVEL     '102'         D2379

     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR

     C                   Z-ADD     0             WREP0

     C     R29KEY        SETLL     DOK29
     C     R29KEY        READE     DOK29                                  90
     C     *IN90         DOWEQ     *OFF

     C     D29TXT        IFNE      *BLANKS
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     0             FGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   MOVEL     'ICN'         F4451
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
N01   * ta hensyn til ? i front av spesialtegn - max 35 inklusive ?
N01  C*    35            SUBST     D29TXT:1      F4440A
N01  c                   z-add     35            FTXLen            2 0
N01  c                   exsr      FTXLENSR
N01  C     FTXLen        SUBST     D29TXT:1      F4440A
     C                   WRITE     FTXR
     C                   GOTO      FTX00B
     C                   ENDIF

     C     R29KEY        READE     DOK29                                  90
     C                   ENDDO

     C     FTX00B        ENDSR
N01   ***********************************
N01  C     FTXLENSR      BEGSR
N01   ***********************************
N01   * +
N01  c     '+'           scan      D29TXT        X                 2 0    33
N01  c     *in33         doweq     '1'
N01  C                   SUB       1             FTXLEN
N01  C                   add       1             X
N01  c     '+'           scan      D29TXT:X      X                        33
N01  c                   enddo
N01   * :
N01  c     ':'           scan      D29TXT        X                 2 0    33
N01  c     *in33         doweq     '1'
N01  C                   SUB       1             FTXLEN
N01  C                   add       1             X
N01  c     ':'           scan      D29TXT:X      X                        33
N01  c                   enddo
N01   * ?
N01  c     '?'           scan      D29TXT        X                 2 0    33
N01  c     *in33         doweq     '1'
N01  C                   SUB       1             FTXLEN
N01  C                   add       1             X
N01  c     '?'           scan      D29TXT:X      X                        33
N01  c                   enddo
N01   * ' (appostrof)
N01  c     Ap            scan      D29TXT        X                 2 0    33
N01  c     *in33         doweq     '1'
N01  C                   SUB       1             FTXLEN
N01  C                   add       1             X
N01  c     Ap            scan      D29TXT:X      X                        33
N01  c                   enddo
N01  C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Steder                                             LOC08 /
      *////////////////////////////////////////////////////////////
     C     LOC08         BEGSR
      *_________________
      * Innleveringssted
      *"""""""""""""""""
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     8             LGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1

     C                   MOVEL     '5'           L3227
     C                   MOVEL     DFPNLS        L3225
     C                   MOVEL     '16'          L1131A

     C                   WRITE     LOCR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Pengebeløp                                         MOA00 /
      *////////////////////////////////////////////////////////////
     C     MOA00         BEGSR

     C                   Z-ADD     0             WREP0
      * Girobeløp
      * eller "X" - i bildet / fra faktura
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '22'          M5025
     C                   Z-ADD     WSBELV        ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   MOVEL     'NOK'         M6345

     C                   WRITE     MOAR
     C                   else
      * eller etterkrav etter avsender
     C     DFBELE        IFGT      0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '22'          M5025
     C                   Z-ADD     DFBELE        ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   MOVEL     'NOK'         M6345

     C                   WRITE     MOAR
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Godsavsender                     NAD10A /
      *////////////////////////////////////////////////////////////
     C     NAD10A        BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVE      DFKNSS        WUNDNR
     C     CUNKEY        CHAIN     CUNDL01                            51
     C                   MOVEL     'CZ'          N3035
     C                   MOVEL     DFKNS         N3039
     C                   MOVEL     '87'          N3055
     C                   MOVEL     DFNAVS        N3036A
     C                   MOVEL     DFAD1S        N3042A
     C                   MOVEL     DFAD3S        N3164
     C                   MOVEL     DFPNLS        N3251

     C                   WRITE     NADR

     C                   EXSR      RFF14

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Mottaker                         NAD10B /
      *////////////////////////////////////////////////////////////
     C     NAD10B        BEGSR
      * Juridisk mottaker
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'CN'          N3035
     C                   MOVEL     DFNAVM        N3036A
     C                   MOVEL     DFAD1M        N3042A
     C                   MOVEL     WADM3A        N3251
     C     N3251         IFEQ      *BLANKS
     C                   MOVEL     DFPOUL        N3251
     C                   ENDIF
     C                   MOVEL     'NO'          N3207

     C                   WRITE     NADR

      * Fysisk mottaker
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'DP'          N3035
     C                   MOVEL     DFAD1L        N3042A
     C     N3042A        IFEQ      *BLANKS
     C                   MOVEL     DFAD1M        N3042A
     C                   ENDIF
     C                   MOVEL     DFPOUL        N3251

     C                   WRITE     NADR

      * HVIS ETTERKRAV SÅ SKAL TLF.NR OPPGIS
     C     DFBELE        IFNE      *ZEROS
N03  C     DFPROK        oreq      'PO12'
     C                   EXSR      CTA11
     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kontakt / teleofnnr                                CTA11 /
      *////////////////////////////////////////////////////////////
     C     CTA11         BEGSR

     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     11            CGN
     C                   Z-ADD     NREP0         CREP0
     C                   Z-ADD     1             CREP1

     C                   MOVEL     'IC'          C3139
     C                   WRITE     CTAR

      * Det SKAL være tlf..
     C                   EXSR      COM11

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *            teleofnnr                                COM11 /
      *////////////////////////////////////////////////////////////
     C     COM11         BEGSR

     C                   CLEAR                   COMR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     11            CGN
     C                   Z-ADD     NREP0         CREP0
     C                   Z-ADD     1             CREP1
     C                   Z-ADD     1             CREP2

     C                   MOVEL     DFLEVI        C3148
     C                   MOVEL     'TE'          C3155
n03  C     DFPROK        ifeq      'PO12'
n03  C                   MOVEL     *BLANKS       C3148
n03  C                   MOVEL     'AL'          C3155
n03  C                   MOVEL     'DP '         FE_N3035
n03  C     DOKUFEK       CHAIN     DOKUFE                             33
n03  C   33              MOVEL     'CN '         FE_N3035
n03  C   33DOKUFEK       CHAIN     DOKUFE                             33
n03  C                   SELECT
n03  C                   WHEN      FE_C3148A <> *BLANKS
n03  C                   MOVEL     FE_C3148A     C3148
n03  C                   WHEN      FE_C3148B <> *BLANKS
n03  C                   MOVEL     FE_C3148B     C3148
n03  C                   WHEN      FE_C3148D <> *BLANKS
n03  C                   MOVEL     FE_C3148D     C3148
n03  C                   ENDSL
n03  C                   endif
     C                   WRITE     COMR

     C                   ENDSR

      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     0             WREP0
      * Senders referanse
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'CU'          R1153
n03  C     DFSREF        IFNE      *BLANKS
n03  C                   MOVEL     DFSREF        R1154
n03  C                   ELSE
     C                   MOVEL     DFAVD         R1154
     C     R1154         CAT       '/':0         R1154
     C                   MOVEL     DFOPD         WFOPDA            7
     C     R1154         CAT       WFOPDA:0      R1154
     C     R1154         CAT       '/':0         R1154
     C     R1154         CAT       HESG:0        R1154
n03  C                   END
     C                   WRITE     RFFR
N03   * kid nummer
N03  C                   CLEAR                   RFFR
N03  C                   Z-ADD     WMN           RMN
N03  C                   Z-ADD     3             RGN
N03  C                   ADD       1             WREP0
N03  C                   Z-ADD     WREP0         RREP0
N03  C                   MOVEL     'SS'          R1153
N03  C                   MOVEL     DFFRKU        KID20            20
N03  C                   EVAL      R1154 = KID20
N03  C                   WRITE     RFFR
N03   * modulus til beløp
N03  C                   CLEAR                   RFFR
N03  C                   Z-ADD     WMN           RMN
N03  C                   Z-ADD     3             RGN
N03  C                   ADD       1             WREP0
N03  C                   Z-ADD     WREP0         RREP0
N03  C                   MOVEL     'ZCH'         R1153
N03  C                   MOVE      DFFRKU        KIDBEL            1
N03  C                   EVAL      R1154 = KIDBEL
N03  C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF14 /
      *////////////////////////////////////////////////////////////
     C     RFF14         BEGSR
     C                   Z-ADD     0             WREP1
      * Gironummer (bank/post)
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C     BANKG         ANDNE     *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     14            RGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'ADE'         R1153
     C                   MOVEL     BANKG         R1154
     C                   WRITE     RFFR
     C                   ELSE
      * eller etterkrav etter avsender
     C     DFBELE        IFGT      0
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     14            RGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'ADE'         R1153
     C                   MOVEL     DFPRO2        R1154
     C                   WRITE     RFFR
     C                   ELSE
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    DFRI          WFRI
     C     *LIKE         DEFINE    DFAVD         WFAVD
     C     *LIKE         DEFINE    DFOPD         WFOPD
     C     *LIKE         DEFINE    DFFBNR        WFFBNR
     C     *LIKE         DEFINE    VMFIRM        WFIRM
     C     *LIKE         DEFINE    KUNDNR        WUNDNR

     C                   EVAL      WFRI='F'
      * DEF AV KEYS
     C     DOKKEY        KLIST
     C                   KFLD                    WFRI
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     TRAKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    DFTRAN
     C     R29KEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     CUNKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    WUNDNR
     C     HEAKEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C     FAKTKE        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
n03  C     DOKUFEK       KLIST
n03  C                   KFLD                    WFAVD
n03  C                   KFLD                    WFOPD
n03  C                   KFLD                    WFFBNR
n03  C                   KFLD                    FE_N3035
n03  C                   MOVEL     'DP '         FE_N3035
      * Input parameters
     C     *ENTRY        PLIST
     C                   PARM                    WFIRM
     C                   PARM                    WFAVD
     C                   PARM                    WFOPD
     C                   PARM                    WFFBNR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0
     C                   Z-SUB     ØN152         ØN152
     C                   MOVE      '-'           ØSIGN             1
     C                   ELSE
     C                   MOVE      '+'           ØSIGN
     C                   ENDIF
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'
     C                   MOVE      ØA152H        ØA12
     C                   ELSE
     C                   MOVEL     ØA152X        ØA12
     C                   MOVE      '.  '         ØA12
     C                   MOVE      ØA152D        ØA12
     C                   END
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C     Ø12(ØX)       IFEQ      '.'
     C                   SUB       1             ØX
     C                   ENDIF
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'
     C                   SUB       1             ØX
     C                   MOVE      '-'           Ø12(ØX)
     C                   ENDIF
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportdetaljer                                  TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      * Other segments
     C*                  EXSR      LOC08

     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     8             TGN
     C                   Z-ADD     WREP0         TREP0

     C                   MOVEL     '20'          T8051
     C                   MOVEL     '3'           T8067

     C                   WRITE     TDTR

     C                   EXSR      LOC08

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportprodukt                                   TSR00 /
      *////////////////////////////////////////////////////////////
     C     TSR00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      * Hent produktkode
     C     DFPROK        IFEQ      *BLANKS
     C                   EVAL      DFPROK='PO7  '
     C                   ENDIF
      * Produkt ?
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
N02  C                   IF        %SUBST(DFPROK:3:1) = 'Z'
N02  C                   EVAL      T4065 = '7'
N02  C                   EVAL      T7273A = %SUBST(DFPROK:3:3)
N02  C                   EVAL      T4219  = '3'
N02  C                   MOVEL     '12'          T7085
N02  C                   ELSE
     C     3             SUBST     DFPROK:3      T4065
     C                   MOVEL     '3'           T4219
n03  C     T4065         IFEQ      '12 '
n03  C                   MOVEL     'Z04'         T7085                          SMS-VARSLING
n03  C                   END
N02  C                   END

     C                   WRITE     TSRR
      * Giro ?
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     'ZPG'         T4065

     C                   WRITE     TSRR
     C                   ELSE
     C     DFBELE        IFNE      *ZEROS
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     'ZPG'         T4065

     C                   WRITE     TSRR
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Godsdetaljer                                       GID16 /
      *////////////////////////////////////////////////////////////
     C     GID16         BEGSR
      *____________
      * Varelinje-1
      *""""""""""""
      * Other segments (Write av GID er flyttet til PCI22)
     C                   EXSR      PCI22
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kolli ID                                           PCI22 /
      *////////////////////////////////////////////////////////////
     C     PCI22         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C     DOKUFMKEY     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C                   MOVE      DFRI          FMRI
     C                   MOVE      DFAVD         FMAVD
     C                   MOVE      DFOPD         FMOPD
     C                   MOVE      DFFBNR        FMFBNR
     C                   MOVE      *ZEROS        FMLINR
      *
     C     DOKUFMKEY     SETLL     DOKUFM
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C     FMST          OREQ      'E'
      * Kolliid
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     22            PGN
     C                   ADD       1             WREP0
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '18'          P4233
     C                   MOVEL     FMMRK1        P7102A
     C                   WRITE     PCIR
      * Antall av
     C                   Z-ADD     2             WREP1
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '24'          P4233
     C                   MOVE      *BLANKS       P7102A
     C                   MOVE      *BLANKS       W7102A1
     C                   MOVE      *BLANKS       W7102A2
     C                   Z-ADD     WREP0         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           W7102A1           7
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           W7102A2           7
     C                   MOVEL     W7102A1       P7102A
     C                   CAT       '/':0         P7102A
     C                   CAT       W7102A2:0     P7102A
     C                   WRITE     PCIR
      * Legg ut GID som er på et nivå Høyere
     C                   CLEAR                   GIDR
     C                   Z-ADD     WMN           GMN
     C                   Z-ADD     16            GGN
     C                   Z-ADD     WREP0         GREP0
      *
     C                   MOVEL     '1'           G1496
     C                   Z-ADD     WREP0         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G7224A
     C                   MOVEL     'PK'          G7065A
      *
     C                   WRITE     GIDR
      *
     C                   END
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C                   END
      *
     C                   ENDSR
