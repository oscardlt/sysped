     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            SFDTD          2S 0H
     A            WSNRO          1A  B  9  2VALUES(' ' '1' '2' '4' '5' '6' '7')
     A            SFST           1A  O  9  4DSPATR(HI)
     A            SFPOSN         4A  O  9  6
     A            SFUNR          2Y 0O  9 12EDTCDE(4)
     A                                      DSPATR(ND)
     A            SFAVD          4Y 0O  9 16EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            SFOPD          7Y 0O  9 21EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            SFVEKT         5Y 0O  9 29EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            SFVOL          7Y 3O  9 36EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            SFVTXT         2A  O  9 46
     A  44                                  DSPATR(ND)
     A            SFFRAN         3A  O  9 50
     A  44                                  DSPATR(ND)
     A            SFTXT         26A  O  9 54
     A            SFEINT        13Y 2O 10 46EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFEUTG        13Y 2O 10 63EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFAINT        13Y 2O 10  6EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFAUTG        13Y 2O 10 23EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFPOS7         7A  H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYAM01'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(009 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  2 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 23MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2KNA          8Y 0O  2 30EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAVN        30A  O  2 39COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2MSGCON(009 TRO0105 *LIBL/SYMSGF)
     A            W2EREF        15A  O  3 12COLOR(WHT)
     A                                  3 29MSGCON(009 TRO0107 *LIBL/SYMSGF)
     A            W2ADAT         6Y 0O  3 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3 49MSGCON(010 TRO0466 *LIBL/SYMSGF)
     A            W2VALK         3A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(009 TRO0479 *LIBL/SYMSGF)
     A            W2AREF        15A  O  4 12COLOR(WHT)
     A                                  4 29MSGCON(009 TRO0102 *LIBL/SYMSGF)
     A            W2BDAT         6Y 0O  4 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  4 49MSGCON(010 TRO0478 *LIBL/SYMSGF)
     A            W2PERM         2S 0O  4 60COLOR(WHT)
     A            W2PERÅ         2S 0O  4 63COLOR(WHT)
     A            W2KURS         7  4O  4 66EDTCDE(L)
     A                                      COLOR(WHT)
     A                                      DSPATR(ND)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(010 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 13MSGCON(010 DIV2227 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  62                              6 24MSGCON(025 TRO0123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 50'6=Se oppdrag'
     A                                      COLOR(BLU)
     A                                  6 66'7=Se faktura'
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  6MSGCON(025 TRO0480 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2VALA         3A  O  8 34COLOR(YLW)
     A                                  8 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2VALE         3A  O  8 74COLOR(YLW)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Ny linje')
     A N95                                  CF10(10 'FERDIG')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CF13(13 'Se utregning')
     A                                      CF07(07 'HENT ORIGINALE DATA')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Sum'
     A            TOAINT        13Y 2O  1  6EDTCDE(L)
     A                                      COLOR(WHT)
     A            TOAUTG        13Y 2O  1 23EDTCDE(L)
     A                                      COLOR(WHT)
     A            TOEINT        13Y 2O  1 46EDTCDE(L)
     A                                      COLOR(WHT)
     A            TOEUTG        13Y 2O  1 63EDTCDE(L)
     A                                      COLOR(WHT)
     A  98                              5  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(010 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 50MSGCON(015 TRO0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 32MSGCON(014 DIV2261 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 50MSGCON(012 DIV2523 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R DLTLIN
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6MSGCON(025 TRO0480 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            SFPOSN         4A  O  3  6
     A            SFUNR          2  0O  3 12EDTCDE(4)
     A                                      DSPATR(ND)
     A            SFAVD          4Y 0O  3 16EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            SFOPD          7Y 0O  3 21EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            SFVEKT         5Y 0O  3 29EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            SFVOL          7Y 3O  3 36EDTCDE(L)
     A  44                                  DSPATR(ND)
     A            SFVTXT         2A  O  3 46
     A  44                                  DSPATR(ND)
     A            SFFRAN         3A  O  3 50
     A  44                                  DSPATR(ND)
     A            SFTXT         26A  O  3 54
     A            SFAINT        13Y 2O  4  6EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFAUTG        13Y 2O  4 23EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFEINT        13Y 2O  4 46EDTCDE(L)
     A                                      COLOR(YLW)
     A            SFEUTG        13Y 2O  4 63EDTCDE(L)
     A                                      COLOR(YLW)
     A                                  5 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            SFPOS7         7A  H
     A          R CHGLIN
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6MSGCON(025 TRO0480 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            SFPOSN         4A  O  3  6
     A            SFUNR          2Y 0O  3 12EDTCDE(4)
     A                                      DSPATR(ND)
     A                                      DSPATR(PC)
     A            SFAVD          4Y 0O  3 16EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            SFOPD          7Y 0O  3 21EDTCDE(4)
     A  44                                  DSPATR(ND)
     A            SFVEKT         5Y 0B  3 29EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A            SFVOL          7Y 3B  3 36EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A            SFVTXT         2A  B  3 46
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A                                      VALUES('  ' 'LM' 'M3')
     A            SFFRAN         3A  B  3 50
     A  44                                  DSPATR(ND)
     A  44                                  DSPATR(PR)
     A            SFTXT         26A  B  3 54
     A            SFEINT        13Y 2B  4 46EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            SFEUTG        13Y 2B  4 63EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            SFAINT        13Y 2B  4  6EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            SFAUTG        13Y 2B  4 23EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A            SFPOS7         7A  H
     A          R ADDLIN
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6MSGCON(025 TRO0480 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            SFPOSN         4D  B  3  6
     A            SFUNR          2Y 0B  3 12EDTCDE(4)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            SFAVD          4Y 0B  3 16EDTCDE(4)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            SFOPD          7Y 0B  3 21EDTCDE(4)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            SFVEKT         5Y 0B  3 29EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            SFVOL          7Y 3B  3 36EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A            SFVTXT         2A  B  3 46VALUES('  ' 'LM' 'M3')
     A            SFFRAN         3A  B  3 50
     A            SFTXT         26A  B  3 54
     A            SFEINT        13Y 2B  4 46EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            SFEUTG        13Y 2B  4 63EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            SFAINT        13Y 2B  4  6EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A            SFAUTG        13Y 2B  4 23EDTCDE(L)
     A                                      CHECK(RB)
     A                                      CHECK(FE)
     A                                      COLOR(YLW)
     A  62                                  DSPATR(PR)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A            SFPOS7         7A  H
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Ny linje')
     A                                      CF07(07 'NY BEREGNING')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM01'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(009 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  2 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 23MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2KNA          8Y 0O  2 30EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAVN        30A  O  2 39COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2MSGCON(009 TRO0105 *LIBL/SYMSGF)
     A            W2EREF        15A  O  3 12COLOR(WHT)
     A                                  3 29MSGCON(009 TRO0107 *LIBL/SYMSGF)
     A            W2ADAT         6Y 0O  3 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3 49MSGCON(009 TRO0481 *LIBL/SYMSGF)
     A            W2VALK         3A  O  3 59COLOR(WHT)
     A            W2KURS         7Y 4O  3 63EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(009 TRO0479 *LIBL/SYMSGF)
     A            W2AREF        15A  O  4 12COLOR(WHT)
     A                                  4 29MSGCON(009 TRO0102 *LIBL/SYMSGF)
     A            W2BDAT         6Y 0O  4 39EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  4 49MSGCON(009 TRO0482 *LIBL/SYMSGF)
     A            W2PERM         2S 0O  4 59COLOR(WHT)
     A            W2PERÅ         2S 0O  4 62COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A                                  6  2MSGCON(010 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 13MSGCON(010 DIV2227 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  62                              6 24MSGCON(025 TRO0123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  4MSGCON(071 DIV2429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  6MSGCON(025 TRO0480 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  8 46MSGCON(025 TRO0124 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            XTOTXT        50A  O 21  2COLOR(WHT)
     A            XTOTBL    R        O 21 67REFFLD(TRAVHR/THAVT *LIBL/TRAVH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23 42MSGCON(015 TRO0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM01'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4 18MSGCON(010 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0B  4 29CHECK(FE)
     A                                      CHECK(RB)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(12)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM01'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3MSGCON(010 TRO0103 *LIBL/SYMSGF)
     A                                  2 67TIME
     A            W2TNR          8Y 0O  2 14DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  3  3MSGCON(010 TRO0486 *LIBL/SYMSGF)
     A            W2TYPT        16A  O  3 14DSPATR(HI)
     A            W2AMT         16A  O  3 32DSPATR(HI)
     A            W2NY           8A  O  3 50COLOR(RED)
     A            W2TYPE         1A  O  3 80DSPATR(ND)
     A                                  4  3MSGCON(010 TRO0483 *LIBL/SYMSGF)
     A            W2KNA          8Y 0B  4 14EDTCDE(4)
     A N77                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A            W2NAVN        30A  O  4 23DSPATR(HI)
     A                                  6  3MSGCON(010 TRO0105 *LIBL/SYMSGF)
     A            W2EREF        15A  B  6 14
     A                                  7  3MSGCON(010 TRO0477 *LIBL/SYMSGF)
     A            W2AREF        15A  B  7 14
     A            W2ENH         15A  B  8 66DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  9  3MSGCON(010 TRO0476 *LIBL/SYMSGF)
     A            W2BILN        10A  B  9 14
     A                                 10  3MSGCON(010 TRO0107 *LIBL/SYMSGF)
     A            W2ADAT         6Y 0B 10 14EDTCDE(Y)
     A                                 11  3MSGCON(010 TRO0102 *LIBL/SYMSGF)
     A            W2BDAT         6Y 0B 11 14EDTCDE(Y)
     A                                 12  3MSGCON(010 TRO0475 *LIBL/SYMSGF)
     A            W2KNT          8Y 0B 12 14EDTCDE(4)
     A            W2TRAN        30A  B 12 23DSPATR(PR)
     A                                 13  3MSGCON(010 TRO0474 *LIBL/SYMSGF)
     A            W2TRKO        13Y 2B 13 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A            W2TRKP         1A  B 13 32VALUES(' ' 'A' 'E' 'T')
     A                                 14  3MSGCON(010 TRO0473 *LIBL/SYMSGF)
     A            W2FERJ        13Y 2B 14 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A            W2FERP         1A  B 14 32VALUES(' ' 'A' 'E' 'T')
     A                                 14 38MSGCON(016 TRO0244 *LIBL/SYMSGF)
     A                                 15  3MSGCON(010 TRO0472 *LIBL/SYMSGF)
     A            W2TREK        13Y 2B 15 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A            W2TREP         1A  B 15 32VALUES(' ' 'A' 'E' 'T')
     A                                 15 38MSGCON(016 TRO0245 *LIBL/SYMSGF)
     A                                 16  3MSGCON(010 TRO0471 *LIBL/SYMSGF)
     A            W2DIV         13Y 2B 16 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A            W2DIVP         1A  B 16 32VALUES(' ' 'A' 'E' 'T')
     A            W2DIVT        30A  B 16 37
     A            W2DIV2        13Y 2B 17 14EDTCDE(L)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A            W2DIVQ         1A  B 17 32VALUES(' ' 'A' 'E' 'T')
     A            W2DIVU        30A  B 17 37
     A                                 18  3MSGCON(010 TRO0470 *LIBL/SYMSGF)
     A            W2PERM         2Y 0B 18 14CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  71                                  DSPATR(RI)
     A            W2PERÅ         2Y 0B 18 17CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  71                                  DSPATR(RI)
     A                                 18 21MSGCON(010 TRO0484 *LIBL/SYMSGF)
     A                                 19  3MSGCON(010 TRO0467 *LIBL/SYMSGF)
     A            W2VALA         3A  B 19 14
     A            W2KURA         7Y 4B 19 19CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(L)
     A                                 20  3MSGCON(010 TRO0468 *LIBL/SYMSGF)
     A            W2VALE         3A  B 20 14
     A            W2KURE         7Y 4B 20 19CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(L)
     A                                 21  3MSGCON(010 TRO0466 *LIBL/SYMSGF)
     A            W2VALK         3A  B 21 14
     A                                 22  3MSGCON(010 TRO0469 *LIBL/SYMSGF)
     A            W2AGNT         3Y 0B 22 14CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A                                 22 19MSGCON(010 TRO0485 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R AVREGN
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM01'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(007 DIV2423 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  2 10COLOR(WHT)
     A                                  2 20MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2KNA          8Y 0O  2 27EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAVN        30A  O  2 36COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(010 TRO0466 *LIBL/SYMSGF)
     A            W2VALK         3A  O  3 13COLOR(WHT)
     A                                  3 20MSGCON(008 DIV2426 *LIBL/SYMSGF)
     A            W2PERM         2S 0O  3 29COLOR(WHT)
     A            W2PERÅ         2S 0O  3 32COLOR(WHT)
     A                                  3 36MSGCON(014 DIV2427 *LIBL/SYMSGF)
     A            W2AGNT         3Y 0O  3 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A                                  6 62MSGCON(009 DIV2460 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 25MSGCON(007 DIV2459 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2VALA         3A  O  6 38COLOR(YLW)
     A            W2VALE         3A  O  6 76COLOR(YLW)
     A                                  7 15MSGCON(030 DIV2522 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 53MSGCON(026 DIV2522 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(078 DIV2512 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(007 DIV2515 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            SMAINT        13Y 2O  9 10EDTCDE(L)
     A            SMAUTG        13Y 2O  9 27EDTCDE(L)
     A            SMEINT        13Y 2O  9 48EDTCDE(L)
     A            SMEUTG        13Y 2O  9 65EDTCDE(L)
     A                                 10  2MSGCON(078 DIV2524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(007 DIV2516 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51        SLAINT        13Y 2O 11 10EDTCDE(L)
     A  52        SLAUTG        13Y 2O 11 27EDTCDE(L)
     A  53        SLEINT        13Y 2O 11 48EDTCDE(L)
     A  54        SLEUTG        13Y 2O 11 65EDTCDE(L)
     A                                 13  2MSGCON(007 TRO0487 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51        KSAINT        13Y 2O 13 10EDTCDE(L)
     A  52        KSAUTG        13Y 2O 13 27EDTCDE(L)
     A  53        KSEINT        13Y 2O 13 48EDTCDE(L)
     A  54        KSEUTG        13Y 2O 13 65EDTCDE(L)
     A                                 14  2MSGCON(007 DIV2517 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2KURS         7Y 4O 14 10EDTCDE(L)
     A                                      COLOR(YLW)
     A                                 15  2MSGCON(078 DIV2524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N50                             16  2MSGCON(007 DIV2518 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  50                             16  2MSGCON(007 DIV2519 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            GTBEL1        13Y 2O 16 65EDTCDE(L)
     A            PAPRO1         3Y 0O 18  2COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  6'%'
     A                                      COLOR(WHT)
     A            PABEL1        13Y 2O 18 65EDTCDE(L)
     A                                 19  2MSGCON(078 DIV2524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  55                             21 54MSGCON(006 DIV2520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  56                             21 54MSGCON(006 DIV2521 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2VALN         3A  O 21 61COLOR(WHT)
     A                                 22 54MSGCON(026 DIV2514 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  55        RSBEL1        13Y 2O 21 65EDTCDE(L)
     A                                      COLOR(WHT)
     A  56        RSBEL2        13Y 2O 21 65EDTCDE(L)
     A                                      COLOR(WHT)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
