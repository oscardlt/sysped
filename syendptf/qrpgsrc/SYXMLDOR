********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 Beløp til faktura snues
12XXX * 02 SH  PTF12 rettelse slette gamle arkivposter
********************************************************************
     H DATEDIT(*YMD)
     FHEADF     IF   E           K DISK
     FARKIVL02  UF A E           K DISK
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FCUNDC03   IF   E           K DISK
     FSADH      UF   E           K DISK
     FSAEH      UF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFIRMDAG   IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFAKT      IF   E           K DISK
     FFRISOK    IF A E           K DISK
     FKOSTA14   IF   E           K DISK
     FKOSTB     UF   E           K DISK
     FSYXMLDOP  O    E             PRINTER OFLIND(*IN28)
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  KBREFA                 1     35
     D  XSITLE                 1      6
     D  XSITLL                 7     16
     D                 DS
     D  av                     1     80  0 DIM(20)
     D  CAVD                   1     80
      *
     D                 DS
     D  OTY                    1     10    DIM(5)
     D  COPD                   1     10
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * lest poster
      *""""""""
     c     *in30         ifeq      '0'
     C                   EXSR      LES
     c                   end
     c                   seton                                        lr
     c                   return
      *********************************************************************
     C     INIT          BEGSR
      *********************************************************************
     C     KOSTAK        KLIST
     C                   KFLD                    KABNR2
     C     KOSTBK        KLIST
     C                   KFLD                    KABNR
     C     FAKTK         KLIST
     C                   KFLD                    KBAVD
     C                   KFLD                    KBOPD
     C     LEVEK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KALNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UBNR              7
      *
     C                   MOVE      UBNR          KABNR2
     C     KOSTAK        CHAIN     KOSTA14                            30
     c     *in30         ifeq      '0'
      *
     c                   read      firm                                   30
     c  N30fifirm        chain     firmdag                            30
     C  N30FIFIRM        CHAIN     FIRMARC                            30
     c  N30levek         chain     level01                            31
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   END
      *
     C                   ENDSR
      *********************************************************************
     C     les           BEGSR
      *********************************************************************
     c                   setoff                                       33
     c                   write     headp
     c     KOSTBK        setll     kostb
     c     *in33         doweq     '0'
     c     KOSTBK        reade     kostb                                  33
     c     *in33         ifeq      '0'
     c     kbavd         ifne      *zeros
     c     kbopd         andne     *zeros
     c                   exsr      lesfakt
     c                   setoff                                       41
     c                   else
     c                   seton                                        41
     c                   end
     c     *in28         ifeq      '1'
     c                   write     headp
     c                   setoff                                       28
     c                   end
     c                   z-add     kbblhb        kbblhbp
     c                   write     det
     c                   add       KBBLHB        kabl1
     c                   end
     c                   end
     c                   write     tot
     c                   endsr
      *********************************************************************
     C     lesfakt       BEGSR
      *********************************************************************
     c                   setoff                                       34
     c                   move      ' '           star              1
     C                   MOVE      'N'           KBKDMV
     C                   MOVE      *ZEROS        KBBLF
     c                   move      *zeros        fasum
     c                   move      *zeros        kbblf
     C     FAKTK         SETLL     FAKT
     C                   DOW       *IN34 = *OFF
     C     FAKTK         READE     FAKT                                   34
     c     *in34         ifeq      '0'
     C                   IF        (((FAKDA <> 'K') AND
     C                               (FAKDA <> 'D')) AND
     C                               (FAVK = KBVK))
     C                   ADD       FABELN        FASUM
     c     faopko        iflt      'D'
     c                   move      '*'           star
     C                   END
     C                   END
     C                   END
     C                   ENDDO
     C                   EVAL      DIFF = FASUM - KBBLHB
n01  c                   add       diff          difft
      * fakturere diff
     C                   IF        DGFAKD = 'J'
     C                   EVAL      KBBLF = DIFF
n01  C                   z-sub     KBBLF         KBBLF
     C                   END
     c                   update    kostbr
      * skriv til frisok
     c                   exsr      frisoksr
     c                   endsr
      *
      *********************************************************************
     C     frisoksr      BEGSR
      *********************************************************************
      * fakturanr
     c                   move      kbavd         fsavd
     c                   move      kbopd         fsopd
     C                   EVAL      FSKODE = 'TLI'
     C                   EVAL      FSSOK = KAFNR
     C                   MOVE      WDT           FSDTOP
     c                   write     frisokr
      * kid
     C                   EVAL      FSKODE = 'TLP'
     C                   EVAL      FSSOK = KALKID
     c                   write     frisokr
     c                   exsr      simdtk
     c                   endsr
      *
      *********************************************************************
     C     simdtk        BEGSR
      *********************************************************************
     C     ARKKEY        KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    ARAVD
     C                   KFLD                    AROPD
     C                   KFLD                    ARTYPE
     c                   move      'A'           arstat
     c                   move      kbavd         aravd
     c                   move      kbopd         aropd
     C     EPOKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    mail30
     C                   KFLD                    ARKUND
     c     faktk         chain     headf                              45
N02  c                   move      'si'          artype
     c     arkkey        chain     arkivl02                           45
N02  c   45              move      'se'          artype
N02  c   45arkkey        chain     arkivl02                           45
     c                   move      ' '           impeks            1
     c     faktk         chain     sadh                               44
     c  N44              move      'I'           impeks
     c  N44              move      'si'          artype
     C  N44              MOVE      SIKNK         ARKUND
     c     faktk         chain     saeh                               44
     c  N44              move      'E'           impeks
     c  N44              move      'se'          artype
     C  N44              MOVE      SEKNK         ARKUND
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     arkjn         andeq     'J'
      *----------------------------
      * skriver arkivpost
      *----------------------------
     c                   move      ' '           arstat
     c                   move      uusr          aruser
     c                   move      fifirm        arfirm
     c                   move      wdt           ardate
     c                   move      wtm           artime
     c   45              write     arkivr
      * også slette gamle ved oppdatering
     c     *in45         ifeq      '0'
     c                   update    arkivr
     c                   exsr      delpdf
     c                   move      'A'           arstat
     C     ARTYPE        IFEQ      'se'
     c                   move      'sf'          artype
     C     arkkey        chain     arkivl02                           35
     C  N35              delete    arkivr
     C  N35              exsr      delpdf
      *
     c                   move      'sg'          artype
     C     arkkey        chain     arkivl02                           35
     C  N35              delete    arkivr
     C  N35              exsr      delpdf
     c                   end
      *
      * sletter tillegg import
      *
     C     ARTYPE        IFEQ      'si'
     c                   move      'sj'          artype
     C     arkkey        chain     arkivl02                           35
     C  N35              delete    arkivr
     C  N35              exsr      delpdf
      *
     c                   move      'sk'          artype
     C     arkkey        chain     arkivl02                           35
     C  N35              delete    arkivr
     C  N35              exsr      delpdf
     c                   end
     c                   end
      *
      *
      *
      * skal tolldek skrives
      *
     C                   MOVE      ARTXT         MAIL16           16
     C                   MOVEL     '*'           MAIL16
     c                   movel     MAIL16        mail30           30
     c     epokey        CHAIN     cundc03                            34
     c  n34              exsr      testep
     C     *in34         ifeq      '0'
     c     cprint        andne     'J'
     c                   move      'P'           sist
     c                   move      'P'           sest
     c                   else
     c                   move      'K'           sist
     c                   move      'K'           sest
     c                   evaL      SIWS='TVINR'
     c                   evaL      SEWS='TVINR'
     c                   end
     c                   eval      simf='DTK'
     c                   eval      semf='DTK'
     c     impeks        ifeq      'I'
     c                   update    sadhr
     c                   end
     c     impeks        ifeq      'E'
     c                   update    saehr
     c                   end
     c                   end
     c                   endsr
      *
     C*******************************************************
     C     TESTEP        BEGSR
     C*******************************************************
      *
      * utelate epost i visse tilfelle
      *
     c     testny        tag
      *
      * tester oppdragstype
      *
     c     copd          ifne      *blanks
     C     heot          LOOKUP    oty                                    35
     c     copdio        ifeq      'I'
     c  N35              seton                                        34
     c                   end
     c     copdio        ifeq      'O'
     c   35              seton                                        34
     c                   end
     c                   end
      *
      * tester avdeling
      *
     C     cavd          ifne      *zeros
     C     heavd         LOOKUP    av                                     35
     c     cavdio        ifeq      'I'
     c  N35              seton                                        34
     c                   end
     c     cavdio        ifeq      'O'
     c   35              seton                                        34
     c                   end
     c                   end
      *
      * les videre hvis 34 er satt på
      *
     c     *in34         ifeq      '1'
     c                   setoff                                       35
     c     epokey        reade     cundc03                                35
     c     *in35         ifeq      '0'
     c                   setoff                                       34
     c                   goto      testny
     c                   end
     c                   end
     c                   endsr
     C***********************************************
     C     DELPDF        BEGSR
     C***********************************************
     C     LagKey        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ARKLAG
     C     ARKLAG        IFNE      *BLANKS
     C     LagKey        CHAIN     ARKEXT                             36
     c                   END
      *
     c                   eval      fradok=arcane
      *
     C     fradok        CAT       '/':0         fradok
     C     fradok        CAT       arlink:0      fradok
     C     '.'           SCAN      arlink                                 35
     C     *in35         ifeq      '0'
     C     fradok        CAT       '.pdf':0      fradok
     c                   end
     c                   call      'ARC041C1'
     c                   parm                    fradok           70
     c                   endsr
