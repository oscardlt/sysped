********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXX * 01 YBC PTF5 BESKRIVELSE ER ØKT FRA 30A TIL 55A
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt')
     A                                      CA12(12 'Forrige')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      *
     A          R WINDOWSZ
E01  A                                      WINDOW(*DFT 13 65 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R GSS014D1                  SFL
     A            WSNR           1A  B  6  2
     C  27                                  DSPATR(PR)
     A            GDLNO          3  0O  6  5EDTCDE(4)
E01  A            GDDSC         55A  O  6  9
      *
     A          R GSS014D2                  SFLCTL(GSS014D1)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      CA05(05 'Frisk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SHIPSYS/GSS014R'
     A                                      COLOR(BLU)
     A                                  1 20'Work with goods descriptions'
     A                                      COLOR(WHT)
     A                                  2  2'Order No.'
     A            UONO           7  0O  2 12DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A N27                              3  2'Select option:  1=Create  2=Change-
     A                                        4=Delete'
     A                                  4  2'Op Lno Goods descriptions'
     A                                      COLOR(WHT)
     A N27        W2NR           1A  B  5  2DSPATR(UL)
     A            W2LNO          3  0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R GSS014D3
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(13)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F3=End   F5=Refresh   F12=Canc-
     A                                      el'
     A                                      COLOR(BLU)
      *
     A          R GSS014D4
E01  A                                      WINDOW(*DFT 3 60 *NOMSGLIN)
     A                                  1  2'Goods description:'
E01  A            WSDSC         55A  B  2  2DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS014D5
     A                                      WINDOW(WINDOWSZ)
     A                                      CA05(05 'Frisk')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS014R'
     A                                      COLOR(BLU)
     A                                  1 20'Work with goods descriptions'
     A                                      COLOR(WHT)
     A                                  2  2'Order No.'
     A            UONO           7  0O  2 12DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A N27                              3  2'Select option:  1=Create  2=Change-
     A                                        4=Delete'
     A                                  4  2'Op Lno Goods descriptions'
     A                                      COLOR(WHT)
     A N27        W2NR           1A  B  5  2DSPATR(UL)
     A            W2LNO          3  0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 13  2'F3=End   F5=Refresh   F12=Canc-
     A                                      el'
     A                                      COLOR(BLU)
