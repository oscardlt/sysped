     A*%%TS  SD  20170831  084737  JOVO        REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR ARC007PNL)
     A                                      HLPTITLE('ARKIVERING AV DOKUMENTER')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20160414  083727  SVEIN       REL-V7R1M0  5770-WDS
     A            ARMRG          1A  H
     A            ARSBAN        50A  H
     A            V06            2A  H
     A            V07            2A  H
     A            V08            2A  H
     A            V09            2A  H
     A            V10            2A  H
     A            V11            2A  H
     A            V12            2A  H
     A            V13            2A  H
     A            V14            2A  H
     A            V15            2A  H
     A            V16            2A  H
     A            V17            2A  H
     A            V18            2A  H
     A            V19            2A  H
     A            V20            2A  H
     A            V21            2A  H
     A            V22            2A  H
     A            V23            2A  H
     A            V24            2A  H
     A            V25            2A  H
     A            V26            2A  H
     A            V27            2A  H
     A            V28            2A  H
     A            V29            2A  H
     A            V30            2A  H
     A            ARSLAB         1A  H
     A            ARVEDL        40A  H
N11  A            ARSFSK         1A  H
N11  A            ARSCTS         1A  H
N11  A            ARSRLE         2S 0H
N11  A            ARSRST         2S 0H
N11  A            ARSRPA         5A  H
N11  A            ARSRNO         1A  H
N12  A            ARSAIN        50A  H
N12  A            ARAVTY         2A  H
     A            WSNR           1A  B  6  2VALUES(' ' '1' '2' '4' '8' '9')
     A            ARTYPE         2A  O  6  4
     A            ARTXT         15A  O  6  7
     A            ARKJN          1A  O  6 23
     A            ARKDAG         5Y 0O  6 25EDTCDE(4)
     A            ARKSND         1A  O  6 31
     A            ARKLAG         2A  O  6 33
     A            ARCANE        30A  O  6 36
     A            V01            2A  O  6 67
     A            V02            2A  O  6 70
     A            V03            2A  O  6 73
     A            V04            2A  O  6 76
     A            V05            2A  O  6 79
     A            ARSB30        30   O  6 82
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160414  083727  SVEIN       REL-V7R1M0  5770-WDS
S11  A*                                     SFLSIZ(0013)
S11  A*                                     SFLPAG(0012)
N11  A                                      SFLSIZ(0021)
N11  A                                      SFLPAG(0020)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/ARC007'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(045 SHU0879 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A N31                              3 21MSGCON(030 SHU0885 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              3 53MSGCON(020 SHU1610 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                              3  2MSGCON(030 SHU1446 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A N31                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(125 SHU0880 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A*%%TS  SD  20170406  134540  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A N31                                  CF05(05 'Frisk')
     A N31                                  CF06(06 'NY')
     A N31                                  CF08(08 'SØK')
     A                                      CF10(10 'SKANN-KODER')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
S11  A*                                     SLNO(21)
     A*                                     SLNO(24)
     A*                                     CLRL(04)
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
S01  A* 98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
S11  A* 30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A  30        MSGTXT       130A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              1 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 82MSGCON(030 JOV1719 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R VARELI
     A*%%TS  SD  20170831  084737  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(19)
     A                                      CLRL(09)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  1MSGCON(077 SHU0880 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTYPE         2A  B  2  4
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSTXT         15A  B  2  7CHECK(LC)
     A  89                                  DSPATR(RI)
     A            WSKJN          1A  B  2 23VALUES('J' 'N')
     A            WSKDAG         5Y 0B  2 25EDTCDE(4)
     A            WSKSND         1A  B  2 31VALUES('J' 'N')
     A            WSKLAG         2A  B  2 33
     A  88                                  DSPATR(RI)
     A            ARCANE        30A  O  2 36
     A            W01            2A  B  2 67CHECK(LC)
     A            W02            2A  B  2 70CHECK(LC)
     A            W03            2A  B  2 73CHECK(LC)
     A            W04            2A  B  2 76CHECK(LC)
     A            W05            2A  B  2 79CHECK(LC)
     A            W06            2A  B  3  7CHECK(LC)
     A            W07            2A  B  3 10CHECK(LC)
     A            W08            2A  B  3 13CHECK(LC)
     A            W09            2A  B  3 16CHECK(LC)
     A            W10            2A  B  3 19CHECK(LC)
     A            W11            2A  B  3 22CHECK(LC)
     A            W12            2A  B  3 25CHECK(LC)
     A            W13            2A  B  3 28CHECK(LC)
     A            W14            2A  B  3 31CHECK(LC)
     A            W15            2A  B  3 34CHECK(LC)
     A            W16            2A  B  3 37CHECK(LC)
     A            W17            2A  B  3 40CHECK(LC)
     A            W18            2A  B  3 43CHECK(LC)
     A            W19            2A  B  3 46CHECK(LC)
     A            W20            2A  B  3 49CHECK(LC)
     A            W21            2A  B  3 52CHECK(LC)
     A            W22            2A  B  3 55CHECK(LC)
     A            W23            2A  B  3 58CHECK(LC)
     A            W24            2A  B  3 61CHECK(LC)
     A            W25            2A  B  3 64CHECK(LC)
     A            W26            2A  B  3 67CHECK(LC)
     A            W27            2A  B  3 70CHECK(LC)
     A            W28            2A  B  3 73CHECK(LC)
     A            W29            2A  B  3 76CHECK(LC)
     A            W30            2A  B  3 79CHECK(LC)
     A                                  4  7MSGCON(014 JOV1741 *LIBL/SYMSGF)
     A            WSVEDL        40A  B  4 22CHECK(LC)
     A  85                                  DSPATR(RI)
     A                                  4 64MSGCON(015 SHU1411 *LIBL/SYMSGF)
     A            WSMRG          1A  B  4 80
     A                                  5  7MSGCON(014 JOV1742 *LIBL/SYMSGF)
     A            WSSBAN        50A  B  5 22CHECK(LC)
     A  87                                  DSPATR(RI)
     A  41                              5 73MSGCON(045 JOV1729 *LIBL/SYMSGF)
     A                                  6  7'Lab'
     A            WSSLAB         1A  B  6 11
     A                                  6 13'Fjern Skilleark'
     A            WSSFSK         1A  B  6 29VALUES('J' 'N' ' ')
     A                                  6 31'Legg til Timestamp'
     A            WSSCTS         1A  B  6 50VALUES('J' 'N' ' ')
     A  41                              6 53MSGCON(009 JOV1743 *LIBL/SYMSGF)
     A  41        WSSAIN        50A  B  6 63CHECK(LC)
     A  86                                  DSPATR(RI)
     A                                  7  7'Skann spes-regel:'
     A                                  7 25'Len'
     A            WSSRLE         2Y 0B  7 29EDTCDE(4)
     A                                  7 32'Tegn/mønster'
     A            WSSRPA         5A  B  7 45CHECK(LC)
     A                                  7 51'i posisjon'
     A            WSSRST         2Y 0B  7 62EDTCDE(4)
     A                                  7 67'Kun numerisk'
     A            WSSRNO         1A  B  7 80VALUES('J' 'N' ' ')
     A  41                              7 92MSGCON(018 JOV1744 *LIBL/SYMSGF)
     A  41        WSAVTY         2A  B  7111
     A  86                                  DSPATR(RI)
     A                                  8  3MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8 25MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT       130A  O  9  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              9 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  2  2MSGCON(020 SHU0764 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2TYPE         2   I  2 23
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            ARTYPE         2   O  2  4
     A            ARTXT         15   O  2  7
     A            ARKJN          1A  O  2 30
     A            ARKSND         1A  O  2 38
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'SKANN-KODER')
     A                                  1  2'SYSPED/ARC007'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(045 SHU0879 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 34MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 82MSGCON(030 JOV1719 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 27 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
