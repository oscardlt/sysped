     A*%%TS  SD  20001211  122118  JOVO        REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A*%%TS  SD  20001211  122118  JOVO        REL-V4R4M0  5769-PW1
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Gå til rollbilde')
     A                                      CF22(22 'SE EDI LOGG')
     A                                      CF23(23 'bekreft sletting')
     A                                      CF10(10 'Gå til varelinjer')
     A                                      CF17(17 'Rubrikk 36')
     A                                      CF18(18 'Rubrikk 29')
     A                                  1  2'SYSPED/SYFM21C'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(013 SHU0441 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSSNDN        17A  O  1 36COLOR(RED)
     A            DFAVD          4Y 0O  1 56EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFOPD          5Y 0O  1 61EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFFBNR         3Y 0O  1 67EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  2  1MSGCON(006 DIV0151 *LIBL/SYMSGF)
     A            DFST           1A  O  2  8TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 10MSGCON(010 DIV0152 *LIBL/SYMSGF)
     A            DFKDME         1A  O  2 21TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNTLA         4Y 0O  2 23EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2 29MSGCON(012 DIV0153 *LIBL/SYMSGF)
     A            DFKDE          1A  O  2 42TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 45MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            DFSG           3A  O  2 55
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            DFKNSS         8Y 0O  4 12TEXT('Kundenr Sender')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFNAVS        30A  O  5 12COLOR(YLW)
     A            DFAD1S        30A  O  6 12COLOR(YLW)
     A N50        DFPNLS    R        O  7 12REFFLD(DOKUFR/DFPNLS *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            WSAD3S        24A  O  7 17COLOR(YLW)
     A            DFNAVM        30A  O  8 17TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD1M        30A  O  8 48TEXT('Adresse Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50        DFTOLL         2Y 0O  2 67TEXT('Tollsted')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFAD3M        30A  O  9 18TEXT('P-adr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  1MSGCON(016 DIV0158 *LIBL/SYMSGF)
     A            DFAD1L        30A  O 10 18TEXT('Leveringsadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFPOUL    R        O 12 49REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            WSADUL        24A  O 12 54TEXT('Utleveringssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50        DFOVER        17A  O 16 12TEXT('Over Tollsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50                             14  1MSGCON(016 DIV0163 *LIBL/SYMSGF)
     A N50        DFTRAN    R     Y  O 14 18REFFLD(DOKUFR/DFTRAN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A N50        DFNAT         20A  O 14 28TEXT('Navn Transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50                              3 10'EDIfact ?'
     A N50        DFCMN     R        O  3 21REFFLD(DOKUFR/DFCMN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A N50                              3 23'Y/N'
     A N50                              3 29'Ventekode'
     A N50        DFVEN     R        O  3 42REFFLD(DOKUFR/DFVEN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A N50        DFNTEU         2Y 0O 18 60TEXT('Ant EURO-plr utveksl')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A N50        DFNTAU         2Y 0O 18 76TEXT('Ant andr-plr utveksl')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A N50        DFKNS         12A  O  4 36TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50        DFKNM         12A  O 15 61TEXT('Kundenr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50                             18  2MSGCON(020 DIV5018 *LIBL/SYMSGF)
     A N50        DFFVCU    R        O 18 23REFFLD(DOKUFR/DFFVCU *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A N50        DFFVBL    R     Y  O 18 27REFFLD(DOKUFR/DFFVBL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A N50        DFFASE        25A  O  7 49COLOR(YLW)
     A                                 19  1MSGCON(075 TBR0256 *LIBL/SYMSGF)
     A N50        DFGM          12A  O 20  1TEXT('Godsmerking')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50        DFNT           7Y 0O 20 14TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A N50        DFVS          25A  O 20 23TEXT('Vareslag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVKT          9Y 0O 20 49TEXT('Vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A N50        DFLM           4Y 2O 20 60TEXT('Lastem.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A N50        DFM3           7Y 3O 20 67TEXT('Kubikk')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A N50        DFGM2         12A  O 21  1TEXT('Godsmerking')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50        DFVS2         25A  O 21 23TEXT('Vareslag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N50        DFVKTF         9Y 0O 21 49TEXT('Vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A  88                             22  4'*** S L E T T I N G   V A L G T   -
     A                                      B E K R E F T   M E D   F23'
     A                                      COLOR(RED)
     A  88                                  DSPATR(BL)
     A                                 23  1MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 20MSGCON(018 TRO0001 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 39MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(007 BBA0145 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 73MSGCON(007 BBA0146 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 60'Tollst'
     A                                  3 58'17.Utst.dato'
     A                                  4  1'01.Sender'
     A                                  4 21'15.Knr hos tr.'
     A N50        DFBREF        17A  O  5 64TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 49'18.Booking ref'
     A                                  6 49'Oppr. avsender/Fakturasender'
     A                                  4 49'16.Senders ref'
     A N50        DFSREF        17A  O  4 64TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 49'12.Utv.pll'
     A                                 18 63'13.Andre pll'
     A                                 16  1'11.Over'
     A          R SUBFIL                    SFL
     A*%%TS  SD  19991101  000200  SVEIN       REL-V4R2M0  5769-PW1
     A            WSNR           1A  I  7  2
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '5' '4')
     A            DFFBNR    R        O  7  4REFFLD(DOKUFR/DFFBNR *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            DFNAMW        29A  O  6 20
     A            DFNT      R        O  7 11REFFLD(DOKUFR/DFNT *LIBL/DOKUF)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            DFVS      R        O  7 19REFFLD(DOKUFR/DFVS *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            DFVKT     R        O  7 45REFFLD(DOKUFR/DFVKT *LIBL/DOKUF)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSSNDN        17A  O  6  2COLOR(YLW)
     A            DFPOUL    R        O  6 50REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A            WSADUL        24A  O  6 55
     A            DFNAT     R        O  7 55REFFLD(DOKUFR/DFNAT *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  19991101  000200  SVEIN       REL-V4R2M0  5769-PW1
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFM21C'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           5Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 65TIME
     A                                  3  2MSGCON(020 SHU0158 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(078 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 11MSGCON(069 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  1'op no'
     A*
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYFI10A'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           5Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76DSPATR(ND)
     A                                  2 65TIME
     A                                  4  3MSGCON(078 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 12MSGCON(069 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R POSTEN
     A                                      WINDOW(11 3 7 44)
     A                                  1  8'*** POSTENS BEDRIFTSPAKKE ***'
     A                                      DSPATR(HI)
     A            DFVT1         40   O  2  2COLOR(YLW)
     A            DFVT2         40   O  3  2COLOR(YLW)
     A            DFVT3         40   O  4  2COLOR(YLW)
     A            DFVT4         40   O  5  2COLOR(YLW)
     A                                  6  2MSGCON(030 BBA0138 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
