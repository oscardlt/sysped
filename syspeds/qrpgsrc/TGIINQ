      *
      ***********************************************************
      *** PROGRAM MAINTAINS DOKUFV FILE                       ***
      *** INDICATORS  01 - 26 FUNCTION  / ROLL KEYS           ***
      *** INDICATORS USED                                     ***
      ***                                                     ***
      ***  03 - EXIT                                          ***
      ***  05 - REFRESH                                       ***
      ***  06 - NEW                                           ***
      ***  08 - SEARCH                                        ***
      ***  12 - CANCEL                                        ***
      ***  25 - ROLL                                          ***
      ***                                                     ***
      ***  30 - GENERAL ERROR IN SCREEN                       ***
      ***  31 - NO RECORDS IN DOKUFV                          ***
      *** N40 - NEW RECORD IN DOKUFV                          ***
      ***  40 - CHANGE RECORD IN DOKUFV                       ***
      ***  50 - SEARCH ON ID                                  ***
      ***  51 - SEARCH ON NAME                                ***
      ***  61 - FEIL I TOTAL ANTALL ELLER VOLUM               ***
      ***  62 - REVERSE IMAGE INDICATOR ON FIELD              ***
      ***  63 - REVERSE IMAGE INDICATOR ON FIELD              ***
      ***  64 - REVERSE IMAGE INDICATOR ON FIELD              ***
      ***  65 - REVERSE IMAGE INDICATOR ON FIELD              ***
      ***  66 - READ INDICATOR FOR SUBCTL                     ***
      ***  67 - REVERSE IMAGE INDICATOR ON FIELD              ***
      ***  77 - CHAIN INDICATOR FOR DOKUFV                    ***
      ***  88 - PROTECT FIELDS IN SCREEN                      ***
      ***  89 - DISPLAY 'F11=Bekreft sletting'                ***
      ***  91 - SFLDSP                                        ***
      ***  92 - SFLDSPCTL                                     ***
      ***  93 - SFLCLR                                        ***
      ***  94 - SFLEND                                        ***
      ***  97 - NO OPTION SELECTED                            ***
      ***  98 - END OF READC SUBFILE                          ***
      ***  99 - CHANGE / COPY OR DELETE RECORD                ***
      ***********************************************************
      *
     FDOKUFV    UF A E           K DISK
     FDOKUFM    UF A E           K DISK
     FSYFA10FA  CF   E             WORKSTN
     F                                     SFILE(FA10AH50:ZRECNO)
     F                                     INFDS(FBAREA)
     D TXT             S             15    DIM(1) CTDATA PERRCD(1)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  DSGM                   1     24
     D  DSGM1                  1     12
     D  DSGM2                 13     24
     D                 DS
     D  UVSINN                 1     25
     D  XPAKN                  1      3
     D  XVT                    5     25
      *
     C                   EXSR      INIT
      *
     C     START         TAG
      *______________________
      * Regenerer linjenummer
      *""""""""""""""""""""""
     C                   Z-ADD     0             HOLINR
     C     DOKKY1        SETLL     DOKUFV
     C     DOKKY1        READE     DOKUFV                                 94
     C     *IN94         DOWEQ     *OFF
     C                   ADD       1             HOLINR
     C                   Z-ADD     HOLINR        FVLINR
     C                   UPDATE    DOKUFVR
     C     DOKKY1        READE     DOKUFV                                 94
     C                   ENDDO
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     FA10AK60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fill Subfile
      *"""""""""""""
     C     DOKKYV        SETLL     DOKUFV                             77
      *
     C     START1        TAG
     C                   DO        13
     C     DOKKY1        READE     DOKUFV                                 94
     C     *IN94         IFEQ      '0'
     C                   MOVE      FVAVD         SFAVD
     C                   MOVE      FVOPD         SFOPD
     C                   MOVE      FVFBNR        SFFBNR
     C                   MOVE      FVLINR        SFLINR
     C                   MOVE      FVANT         SFANT
     C                   MOVEL     FVPAKN        SFPAKN
     C                   MOVEL     FVVT          SFVT
     C                   MOVE      FVVKT         SFVKT
     C                   MOVE      FVVOL         SFVOL
     C                   ADD       1             ZRECNO
     C                   MOVE      FVLINR        MAXLIN
     C                   WRITE     FA10AH50
     C                   END                                                    <*IN94
     C  N94              ENDDO
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
      *________________
      * Display Subfile
      *""""""""""""""""
     C     START3        TAG
      *________________
      * Finn høyeste nr
      *""""""""""""""""
     C     DOKKY1        SETGT     DOKUFV
     C     DOKKY1        READPE    DOKUFV                                 94
     C     *IN94         IFEQ      *OFF
     C                   Z-ADD     FVLINR        HOLINR            2 0
     C                   ELSE
     C                   Z-ADD     0             HOLINR
     C                   ENDIF
      *
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   MOVE      '1'           *IN31
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   WRITE     FA10AB20
     C                   EXFMT     FA10AB40
     C                   READ      FA10AB20                               66
     C                   ELSE
     C                   MOVE      '0'           *IN31
     C                   WRITE     FA10AK60
     C                   Z-ADD     WSSUBN        XRECNO
     C                   EXFMT     FA10AB40
     C                   READ      FA10AK60                               66
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C     FKEYS         TAG
     C*
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       999830
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   EXSR      SUMMER
     C     ANTFEL        IFEQ      1
     C     *IN30         IFEQ      '1'
     C                   Z-ADD     1             ZLINR
     C                   GOTO      START
     C                   END                                                    <*IN30
     C                   END                                                    <ANTFEL
     C     TOTANT        IFNE      0
     C     TOTVKT        ORNE      0
     C     TOTLEN        ORNE      0
     C     TOTVOL        ORNE      0
     C                   Z-ADD     TOTANT        UANT
     C                   Z-ADD     TOTVKT        UVKT
     C                   Z-ADD     TOTLEN        ULM
     C                   Z-ADD     TOTVOL        UM3
     C                   END
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C*** F5=Refresh
     C     *IN05         IFEQ      '1'
     C                   CLEAR                   ZLINR
     C                   GOTO      START
     C                   END                                                    <*IN05
     C*** F6=New record
     C     *IN06         IFEQ      '1'
     C     UCMN          IFEQ      'N'
     C     YRECNO        IFGE      4
     C                   MOVE      'TRO0047'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      START
     C                   END                                                    <ZRECNO
     C                   END                                                    <UCMN
     C                   Z-ADD     09            LINNBX
     C                   Z-ADD     03            POSNBX
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVEL     'Ny      '    WMODE
     C                   EXSR      CLRSCR
     C                   MOVE      '0'           *IN40
     C                   ADD       1             MAXLIN
     C                   Z-ADD     MAXLIN        FVLINR
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   MOVE      '0'           *IN12
     C                   GOTO      START
     C                   END                                                    <*IN12
     C*** Roll
     C     *IN25         IFEQ      '1'
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C     START4        TAG
     C     *IN94         IFEQ      '0'
     C                   GOTO      START1
     C                   END                                                    <*IN94
     C                   END                                                    <*IN25
     C*** Get options from screen
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C*** No option selected
     C     *IN97         IFEQ      '1'
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   EXFMT     FA10AB40
     C                   READ      FA10AK60                               66
     C                   MOVE      '0'           *IN97
     C                   GOTO      FKEYS
     C                   END                                                    <*IN12
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   EXSR      SUMMER
     C     ANTFEL        IFEQ      1
     C     *IN30         IFEQ      '1'
     C                   Z-ADD     1             ZLINR
     C                   GOTO      START
     C                   END                                                    <*IN30
     C                   END                                                    <ANTFEL
     C     TOTANT        IFNE      0
     C     TOTVKT        ORNE      0
     C                   Z-ADD     TOTANT        UANT
     C                   Z-ADD     TOTVKT        UVKT
     C                   END
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C                   END                                                    <ZRECNO
     C     *IN98         IFEQ      '1'
     C     XRECNO        ANDNE     0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END                                                    <*IN98
     C                   ADD       1             LINNBR
     C                   GOTO      START3
     C*** New record
     C     START5        TAG
     C                   Z-ADD     ZAVD          FVAVD
     C                   Z-ADD     ZOPD          FVOPD
     C                   Z-ADD     ZFBNR         FVFBNR
     C     HOLINR        ADD       1             FVLINR
     C                   EXFMT     FA10AB10
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN30
     C*** F3=Exit
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   Z-ADD     LINNBX        LINNBR
     C                   Z-ADD     POSNBX        POSNBR
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   ELSE
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      START5
     C                   END                                                    <*IN30
     C                   EXSR      POSLIN
     C*                    ADD  8         LINNBR
     C                   SUB       1             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   MOVE      '0'           *IN12
     C                   END                                                    <*IN12
     C                   GOTO      START3
     C*** Search on ID or Name
     C     START6        TAG
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START
     C*
     C     ENDPGM        TAG
      *______________________
      * Regenerer linjenummer
      *""""""""""""""""""""""
     C                   Z-ADD     0             HOLINR
     C     DOKKY1        SETLL     DOKUFV
     C     DOKKY1        READE     DOKUFV                                 94
     C     *IN94         DOWEQ     *OFF
     C                   ADD       1             HOLINR
     C                   Z-ADD     HOLINR        FVLINR
     C                   UPDATE    DOKUFVR
     C     DOKKY1        READE     DOKUFV                                 94
     C                   ENDDO
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*          N40 = NEW
     C*           40 = CHANGE
     C***********************************************
     C                   EXSR      TSTSCR
     C     *IN30         IFEQ      '1'
     C                   GOTO      ENDTST
     C                   END                                                    <*IN30
     C                   MOVE      SFAVD         FVAVD
     C                   MOVE      SFOPD         FVOPD
     C                   MOVE      SFFBNR        FVFBNR
     C     FVVOL         IFEQ      *ZEROS
     C     FVLEN         MULT      FVHOY         FVVOL
     C     FVVOL         MULT      FVBRD         FVVOL
     C                   END                                                    <FVVOL
     C     *IN40         IFEQ      '1'
     C                   UPDATE    DOKUFVR                              38
     C                   EXSR      DOKMU
     C                   ELSE
     C                   WRITE     DOKUFVR
     C                   Z-ADD     0             FMMKNR
     C                   DO        FVANT
     C                   EXSR      DOKMA
     C                   ENDDO
     C                   END                                                    <*IN40
     C*
     C                   MOVE      FVAVD         SFAVD
     C                   MOVE      FVOPD         SFOPD
     C                   MOVE      FVFBNR        SFFBNR
     C                   MOVE      FVLINR        SFLINR
     C                   MOVE      FVANT         SFANT
     C                   MOVEL     FVPAKN        SFPAKN
     C                   MOVEL     FVVT          SFVT
     C                   MOVE      FVVKT         SFVKT
     C                   MOVE      FVVOL         SFVOL
     C     *IN40         IFEQ      '1'
     C                   UPDATE    FA10AH50
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     FA10AH50
     C                   END                                                    <*IN40
     C                   EXSR      CLRSCR
     C*
     C     ENDTST        ENDSR
     C*
     C***********************************************************
     C     TSTSCR        BEGSR
     C***********************************************************
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     TSTEND        ENDSR
     C*
     C***********************************************
     C     GETOPT        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C                   MOVE      '1'           *IN97
     C     *IN98         DOWEQ     '0'
     C                   READC     FA10AH50                               98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END                                                    <*IN99
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVE      '0'           *IN97
     C*** 2=Change
     C     WSNR          IFEQ      '2 '
     C     WSNR          OREQ      ' 2'
     C     WSNR          OREQ      '02'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C     *IN69         IFEQ      *ON
     C                   UPDATE    FA10AH50
     C                   GOTO      NYGET
     C                   ENDIF
     C                   MOVEL     'Endre   '    WMODE
     C                   MOVE      '1'           *IN40
     C                   EXSR      CHGST
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   MOVE      '0'           *IN12
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   MOVE      '0'           *IN40
     C                   END                                                    <WSNR=2
     C*** 3=Copy
     C     WSNR          IFEQ      '3 '
     C     WSNR          OREQ      ' 3'
     C     WSNR          OREQ      '03'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C     *IN69         IFEQ      *ON
     C                   UPDATE    FA10AH50
     C                   GOTO      NYGET
     C                   ENDIF
     C                   MOVEL     'Kopiere '    WMODE
     C                   EXSR      CPYST
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   MOVE      '0'           *IN12
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=3
     C*** 4=Delete
     C     WSNR          IFEQ      '4 '
     C     WSNR          OREQ      ' 4'
     C     WSNR          OREQ      '04'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C     *IN69         IFEQ      *ON
     C                   UPDATE    FA10AH50
     C                   GOTO      NYGET
     C                   ENDIF
     C                   MOVEL     'Slette  '    WMODE
     C                   EXSR      DLTST
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   MOVE      '0'           *IN12
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=4
     C*** 5=Display
     C     WSNR          IFEQ      '5 '
     C     WSNR          OREQ      ' 5'
     C     WSNR          OREQ      '05'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Vise    '    WMODE
     C                   MOVE      SFAVD         ZAVD
     C                   MOVE      SFOPD         ZOPD
     C                   MOVE      SFFBNR        ZFBNR
     C                   MOVE      SFLINR        ZLINR
     C     DOKKYV        CHAIN(N)  DOKUFV                             77
     C                   MOVE      '1'           *IN88
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   EXFMT     FA10AB10
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   UPDATE    FA10AH50
     C                   MOVE      '0'           *IN12
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN88
     C                   UPDATE    FA10AH50
     C                   END                                                    <WSNR=5
     C*
     C*
     C*** 6=Merke
     C     WSNR          IFEQ      '6 '
     C     WSNR          OREQ      ' 6'
     C     WSNR          OREQ      '06'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C*
     C* GÅ TIL VARELINJE VEDLIKEHOLD
     C*
     C                   CALL      'SYFA10B'
     C                   PARM                    SFAVD
     C                   PARM                    SFOPD
     C                   PARM                    SFFBNR
     C                   PARM                    SFLINR
     C                   PARM                    UVIS
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN88
     C                   UPDATE    FA10AH50
     C                   END                                                    <WSNR=6
     C     NYGET         TAG
     C                   ENDDO
     C     ENDGET        TAG
     C                   EXSR      POSLIN
     C     OPTNBR        IFGT      *ZEROS
     C                   ADD       7             LINNBR
     C                   ELSE
     C                   ADD       8             LINNBR
     C                   END                                                    <OPTNBR
     C                   Z-ADD     3             POSNBR
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C***********************************************
     C                   MOVE      SFAVD         ZAVD
     C                   MOVE      SFOPD         ZOPD
     C                   MOVE      SFFBNR        ZFBNR
     C                   MOVE      SFLINR        ZLINR
     C     DOKKYV        CHAIN     DOKUFV                             77
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C     STCHG         TAG
     C                   EXFMT     FA10AB10
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   UPDATE    FA10AH50
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCHG
     C                   END                                                    <*IN30
     C*
     C     CHGEND        TAG
     C                   ENDSR
     C*
     C***********************************************
     C     CPYST         BEGSR
     C***********************************************
     C     DOKKYV        CHAIN(N)  DOKUFV                             77
     C                   MOVE      '1'           *IN99
     C                   UPDATE    FA10AH50
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C     HOLINR        ADD       1             FVLINR
     C     STCPY         TAG
     C                   EXFMT     FA10AB10
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   GOTO      CPYEND
     C                   END                                                    <*IN12
     C                   MOVE      '0'           *IN40
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCPY
     C                   END                                                    <*IN30
     C*
     C     CPYEND        TAG
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVE      SFAVD         ZAVD
     C                   MOVE      SFOPD         ZOPD
     C                   MOVE      SFFBNR        ZFBNR
     C                   MOVE      SFLINR        ZLINR
     C     DOKKYV        CHAIN     DOKUFV                             77
     C                   EXFMT     FA10AB10
     C                   MOVEA     '00'          *IN(88)
     C     *IN11         IFEQ      '1'
     C     *IN77         IFEQ      '0'
     C                   DELETE    DOKUFVR
     C                   Z-ADD     1             ZMKNR
     C                   Z-ADD     1             ZLONR
     C     DOKKYM        SETLL     DOKUFM
     C                   DO        FVANT
     C                   EXSR      DOKMD
     C                   ENDDO
     C                   END                                                    <*IN77
     C                   CLEAR                   SFVT
     C                   MOVEL     TXT(1)        SFVT
     C                   END                                                    <*IN11
     C                   UPDATE    FA10AH50
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DOKMA         BEGSR
     C***********************************************
     C                   MOVE      FVRI          FMRI
     C                   MOVE      FVAVD         FMAVD
     C                   MOVE      FVOPD         FMOPD
     C                   MOVE      FVFBNR        FMFBNR
     C                   MOVE      FVLINR        FMLINR
     C                   ADD       1             FMMKNR
     C                   Z-ADD     1             FMLONR
     C                   MOVE      *BLANKS       FMMRK1
     C                   MOVEL     DSGM          FMMRK1
     C                   WRITE     DOKUFMR
     C*
     C                   ENDSR
     C***********************************************
     C     DOKMU         BEGSR
     C***********************************************
     C                   Z-ADD     SFAVD         ZAVD
     C                   Z-ADD     SFOPD         ZOPD
     C                   Z-ADD     SFFBNR        ZFBNR
     C                   Z-ADD     FVLINR        ZLINR
     C                   Z-ADD     1             ZMKNR
     C                   Z-ADD     1             ZLONR
     C                   Z-ADD     0             FMMKNR
     C     DOKKY2        SETLL     DOKUFM                                 55
     C     LESDOK        TAG
     C     DOKKY2        READE     DOKUFM                                 55
     C     *IN55         IFEQ      *OFF
     C     FMMKNR        IFGT      FVANT
     C                   DELETE    DOKUFMR
     C                   END                                                    <FMMKNR
     C                   GOTO      LESDOK
     C                   END                                                    <*IN55
     C     *IN55         IFEQ      *ON
     C     SKRDOK        TAG
     C     FMMKNR        IFLT      FVANT
     C                   MOVE      'F'           FMRI
     C                   Z-ADD     SFAVD         FMAVD
     C                   Z-ADD     SFOPD         FMOPD
     C                   Z-ADD     SFFBNR        FMFBNR
     C                   Z-ADD     FVLINR        FMLINR
     C                   ADD       1             FMMKNR
     C                   Z-ADD     1             FMLONR
     C                   MOVE      *BLANKS       FMMRK1
     C                   MOVEL     DSGM          FMMRK1
     C                   WRITE     DOKUFMR
     C                   GOTO      SKRDOK
     C                   END                                                    <FMMKNR
     C                   END                                                    <*IN55
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DOKMD         BEGSR
     C***********************************************
     C     DOKLES        TAG
     C                   READ      DOKUFM                                 78
     C     *IN78         IFEQ      *OFF
     C                   DELETE    DOKUFMR
     C                   ENDIF                                                  <*IN78
     C*
     C                   ENDSR
     C***********************************************
     C     SUMMER        BEGSR
     C***********************************************
     C                   Z-ADD     SFAVD         ZAVD
     C                   Z-ADD     SFOPD         ZOPD
     C                   Z-ADD     SFFBNR        ZFBNR
     C                   Z-ADD     0             TOTANT
     C                   Z-ADD     0             TOTVKT
     C     DOKKY1        SETLL     DOKUFV                                 55
     C     SUMDOK        TAG
     C     DOKKY1        READE     DOKUFV                                 55
     C     *IN55         IFEQ      *OFF
     C                   ADD       FVANT         TOTANT
     C                   ADD       FVVKT         TOTVKT
     C                   ADD       FVLEN         TOTLEN
     C                   ADD       FVVOL         TOTVOL
     C                   GOTO      SUMDOK
     C                   END                                                    <*IN55
     C*
     C                   MOVE      '0'           *IN30
     C     TOTANT        IFNE      UANT
     C                   MOVE      'TRO0045'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   ADD       1             ANTFEL
     C                   MOVE      '1'           *IN30
     C                   GOTO      SUMEND
     C                   END                                                    <TOTANT
     C     TOTVKT        IFNE      UVKT
     C                   MOVE      'TRO0046'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   ADD       1             ANTFEL
     C                   MOVE      '1'           *IN30
     C                   GOTO      SUMEND
     C                   END                                                    <TOTVKT
     C     SUMEND        TAG
     C                   ENDSR
     C***********************************************
     C     CLRSCR        BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   FVAVD
     C                   CLEAR                   FVOPD
     C                   CLEAR                   FVFBNR
     C                   CLEAR                   FVLINR
     C                   CLEAR                   FVANT
     C                   CLEAR                   FVPAKN
     C                   CLEAR                   FVVT
     C                   CLEAR                   FVVKT
     C                   CLEAR                   FVVOL
     C                   CLEAR                   FVLEN
     C                   CLEAR                   FVHOY
     C                   CLEAR                   FVBRD
     C                   CLEAR                   FVLM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     POSLIN        BEGSR
     C***********************************************
     C     OPTNBR        IFGT      *ZERO
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C                   END                                                    <OPTNBR
     C                   ENDSR
     C*
     C***********************************************
     C     LAGLIN        BEGSR
     C***********************************************
     C     DOKKYV        SETLL     DOKUFV                             93
     C     DOKKY1        READE     DOKUFV                                 93
     C   93              DO
     C                   MOVE      ZRI           FVRI
     C                   MOVE      UAVD          FVAVD
     C                   MOVE      UOPD          FVOPD
     C                   MOVE      UFBNR         FVFBNR
     C                   Z-ADD     1             FVLINR
     C                   Z-ADD     UANT          FVANT
     C                   MOVEL     XPAKN         FVPAKN
     C                   Z-ADD     UVKT          FVVKT
     C                   Z-ADD     ULM           FVLEN
     C                   Z-ADD     UM3           FVVOL
     C                   MOVEL     XVT           FVVT
     C                   WRITE     DOKUFVR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBNR             3 0
     C                   PARM                    UANT              7 0
     C                   PARM                    UVKT              9 0
     C                   PARM                    ULM               4 2
     C                   PARM                    UM3               7 3
     C                   PARM                    UGM              12
     C                   PARM                    UGM2             12
     C                   PARM                    UCMN              1
     C     UVSINN        PARM                    UVS              25
     C                   PARM                    UVIS              1
     C     UVIS          IFEQ      'J'
     C                   MOVE      *ON           *IN69
     C                   ENDIF
      *
     C                   MOVE      UGM           DSGM1
     C                   MOVE      UGM2          DSGM2
     C                   Z-ADD     0             TOTANT            9 0
     C                   Z-ADD     0             TOTVKT            9 0
     C                   Z-ADD     0             TOTLEN            9 0
     C                   Z-ADD     0             TOTVOL            9 0
     C                   Z-ADD     0             ANTFEL            1 0
     C                   Z-ADD     UAVD          ZAVD
     C                   Z-ADD     UOPD          ZOPD
     C                   Z-ADD     UFBNR         ZFBNR
     C                   Z-ADD     UAVD          SFAVD
     C                   Z-ADD     UOPD          SFOPD
     C                   Z-ADD     UFBNR         SFFBNR
     C*
     C*--------------------------------------------------------------
     C* DEFINE KEY FOR DOKUFV
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FVRI          ZRI
     C     *LIKE         DEFINE    FVAVD         ZAVD
     C     *LIKE         DEFINE    FVOPD         ZOPD
     C     *LIKE         DEFINE    FVFBNR        ZFBNR
     C     *LIKE         DEFINE    FVLINR        ZLINR
     C     *LIKE         DEFINE    FMMKNR        ZMKNR
     C     *LIKE         DEFINE    FMLONR        ZLONR
     C                   MOVE      'F'           FVRI
     C                   MOVE      'F'           ZRI
     C     DOKKY1        KLIST
     C                   KFLD                    ZRI
     C                   KFLD                    ZAVD
     C                   KFLD                    ZOPD
     C                   KFLD                    ZFBNR
     C     DOKKYV        KLIST
     C                   KFLD                    ZRI
     C                   KFLD                    ZAVD
     C                   KFLD                    ZOPD
     C                   KFLD                    ZFBNR
     C                   KFLD                    ZLINR
     C*
     C     DOKKYM        KLIST
     C                   KFLD                    ZRI
     C                   KFLD                    ZAVD
     C                   KFLD                    ZOPD
     C                   KFLD                    ZFBNR
     C                   KFLD                    ZLINR
     C                   KFLD                    ZMKNR
     C                   KFLD                    ZLONR
     C     DOKKY2        KLIST
     C                   KFLD                    ZRI
     C                   KFLD                    ZAVD
     C                   KFLD                    ZOPD
     C                   KFLD                    ZFBNR
     C                   KFLD                    ZLINR
     C*
     C     *LIKE         DEFINE    LINNBR        LINNBX
     C     *LIKE         DEFINE    POSNBR        POSNBX
     C                   Z-ADD     0             MAXLIN            2 0
     C                   MOVE      '1'           *IN50
      *
     C                   EXSR      LAGLIN
      *
     C                   ENDSR
     C*
**
*** SLETTET ***
