     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FELOC93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FETCC93A   IF   E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  M1004                  1     35
     D  W1004A                 1      4
     D  W1004B                 5      7
     D  W1004C                 8     10
     D                 DS
     D  N3164                  1     35
     D  N3164A                 1      2
     D  N3164B                 3     35
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   CLEAR                   FREETXTR
     C                   MOVE      HEUNIK        FRUNIK
     C                   MOVE      HEREFF        FRREFF
     C                   Z-ADD     MDT           FRTDT
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     F4451         IFEQ      'DEL'
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440A        FRTTXT
     C                   WRITE     FREETXTR
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440B        FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
     C     F4440C        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440C        FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
     C     F4440D        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440D        FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
     C     F4440E        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440E        FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
     C                   ENDIF
      *
     C  N51KEYMS0        READE     EFTX93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX16                                               FTX16 /
      *////////////////////////////////////////////////////////////
     C     FTX16         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     16            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'AAA'
     C     ZVS           IFEQ      '0'
     C     HEVS1         CAT       F4440A:1      HEVS1
     C                   MOVE      '1'           ZVS
     C                   ELSE
     C     ZVS           IFEQ      '1'
     C     HEVS2         CAT       F4440A:1      HEVS2
     C                   MOVE      '2'           ZVS
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID16                                               GID16 /
      *////////////////////////////////////////////////////////////
     C     GID16         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     16            ZGN0
     C     KEYMS0        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EGID93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7064A        HEVS1
      *
     C                   Z-ADD     GREP0         ZREPB0
     C                   EXSR      FTX16
      *
     C     KEYMS0        READE     EGID93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C                   MOVE      '0'           ZVS               1
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC07                                               LOC07 /
      *////////////////////////////////////////////////////////////
     C     LOC07         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     7             ZGN1
     C     KEYMS1        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEL     L3225         HESDFR
     C                   MOVEL     L3225         HESTFR
      *
     C  N52KEYMS1        READE     ELOC93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA19                                               MEA19 /
      *////////////////////////////////////////////////////////////
     C     MEA19         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     19            ZGN0
     C     KEYMS0        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMEA93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   Z-ADD     ZNUM          HEVKT
     C     M6311         WHENEQ    'VOL'
     C     ZNUM          DIV       1000          HEM3
     C                   ENDSL
      *
     C  N51KEYMS0        READE     EMEA93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA00                                               MOA00 /
      *////////////////////////////////////////////////////////////
     C     MOA00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EMOA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMOA93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C*                    MOVELMXXXX     FRTTXT
      *
     C  N51KEYMS0        READE     EMOA93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C     NAD10         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CZ'                                         GODSAVSENDER
     C     N3039         IFNE      *BLANKS
     C     N3036A        ANDEQ     *BLANKS
     C                   MOVEL     N3039         HENAS
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C                   MOVEL     'DK-'         HEADS3
     C     N3164         IFNE      *BLANKS
     C     HEADS2        CAT       N3164:0       HEADS3
     C                   ENDIF
     C     N3035         WHENEQ    'OB'                                         BESTILLT AV
     C                   MOVEL     N3124A        HENASF
     C     N3035         WHENEQ    'CN'                                         MOTTAKER
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAKF
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3164         IFNE      *BLANKS
     C     N3164A        IFEQ      'N-'
     C                   MOVEL     N3164B        HEADK3
     C                   ELSE
     C                   MOVEL     N3164         HEADK3
     C                   ENDIF
     C                   ENDIF
     C     N3035         WHENEQ    'DP'                                         LEV.ADR
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3164         IFNE      *BLANKS
     C     N3164A        IFEQ      'N-'
     C                   MOVEL     N3164B        HEADK3
     C                   ELSE
     C                   MOVEL     N3164         HEADK3
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF03                                               RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     3             ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'VE '
     C                   MOVEL     R1153         HEHAWB
     C     R1153         WHENEQ    'CU '
     C                   MOVEL     R1153         HERFA
     C                   ENDSL
      *
     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TCC07                                               TCC07 /
      *////////////////////////////////////////////////////////////
     C     TCC07         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     7             ZGN0
     C     KEYMS0        SETLL     ETCC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETCC93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   Z-ADD     TREP0         ZREPB0
     C                   EXSR      LOC07
      *
     C  N51KEYMS0        READE     ETCC93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   Z-ADD     14            HEAVD
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFTMIN'      HETYPE
     C                   MOVE      '93A'         HELVL
     C                   Z-ADD     ZMN           HESNN
     C                   Z-ADD     ZMN           HEUNIK
     C                   MOVEL     'FREJA'       HENAA
     C     W1004A        CAT       '.':0         HERFA
     C     HERFA         CAT       W1004B:0      HERFA
     C     HERFA         CAT       '.':0         HERFA
     C     HERFA         CAT       W1004C:0      HERFA
     C                   MOVEL     HERFA         HEREFF
     C     3             SUBST(P)  M1004:11      HEPOS1
     C*                    MOVEL'D'       HESDL
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      MOA00
     C                   EXSR      FTX00
     C                   EXSR      RFF03
     C                   EXSR      TCC07
     C                   EXSR      NAD10
     C                   EXSR      GID16
      *________________
      * WRITE TO SYSPED
      *""""""""""""""""
     C                   WRITE     HEADFR
      *
     C                   ENDSR
