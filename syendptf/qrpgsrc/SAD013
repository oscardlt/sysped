********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 ikke endre status hvis klokka og løpenummer
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 KUN ENDRE STATUS FRA 'E' ELLER BLANK
11XXX * 03 YBC PTF11 OGSÅ STATUS 'K' OG 'Å' KAN ENDRES
11XXX * 04 YBC PTF11 ENDRE STATUS PÅ OMBEREGNING
11XXX * 05 JOV PTF11 AUTO-forsøk med negativ verdi=omberegn.
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 06 YBC PTF12 Mulig å endre status ved innstikk.
********************************************************************
      *
      ************************************************************
      *** PROGRAM SKAL ENDRE STATUS PÅ SAD-IMPORT              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04 06-26                    ***
      *** LEDIGE INDIKATORER 27-97                             ***
      ************************************************************
      *
     Ffpxref    UF   E           K DISK
     Fsysthlp   IF   E             DISK
     FKODTSI    IF   E           K DISK
     FSADH      UF   E           K DISK
     FSTS       UF A E           K DISK
     FSAD013FM  CF   E             WORKSTN
     D*
      *** SI27 I SADH ER TATT I BRUK (FØRST 15 POS.)
     D                 DS
     D  SI28                   1     27
     D  SIBELC                 1     11  2
     D  SIRAB                 12     15  2
     D  SIRABX                12     15
     D                 DS
     D  SIFIDÅ                 1      4  0
     D  SIFIDM                 5      6  0
     D  SIFIDD                 7      8  0
     D  SIFID                  1      8  0
     D                 DS
     D  SIDTÅ                  1      4  0
     D  SIDTM                  5      6  0
     D  SIDTD                  7      8  0
     D  SIDT                   1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  WSFIDD                 1      2  0
     D  WSFIDM                 3      4  0
     D  WSFIDÅ                 5      6  0
     D  WSFID                  1      6  0
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      6  0
     D  XDAG                   7      8  0
     D  XMND                   9     10  0
     D  XAAR                  11     14  0
     D                 DS
     D  SSDTÅ                  1      4  0
     D  SSDTM                  5      6  0
     D  SSDTD                  7      8  0
     D  SSDT                   1      8  0
      *
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXFMT     BILDE01
N01  C                   setoff                                       30
     c   01              do
     c                   movel     'sad013'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START
     c                   end
     C                   SETOFF                                       9998
     C*
     C   03              GOTO      SLUTT
     C     SADHK         CHAIN(N)  SADH                               99
N05   * ferdig Normal-sad / finnes det omberegning
N05  C  N99SITLL         ifne      *zeros
N05  C     SITLL         andne     *blanks
N05  C     WSOPD         andgt     0
N05  C                   mult      -1            WSOPD
N05  C     SADHK         CHAIN(N)  SADH                               99
N05  c  N99              move      'JOV1707'     msgnr
N05  C  N99              SETON                                        30
N05  C  N99              GOTO      START
N05  C   99              mult      -1            WSOPD
N05  C   99SADHK         CHAIN(N)  SADH                               99
N05  c                   endif
n01  c   99              move      'SPA0021'     msgnr
N01  C   99              SETON                                        30
     C   99              GOTO      START
     C     KODSIK        CHAIN     KODTSI                             98
n01  c   98              move      'SPS0033'     msgnr
N01  C   98              SETON                                        30
     C   98              GOTO      START
N01  C     SITLL         ifne      *zeros
N01  C     SITLL         andne     *blanks
N06  C     SIMI          ANDNE     'I'
N04  C     WSOPD         ANDGT     0
N01  C                   seton                                        3099
n01  c                   move      'SHU1387'     msgnr
n01  c                   goto      start
n01  c                   end
N06  C                   IF        SIMI = 'I' AND WSST <> 'U' AND
N06  C                             SITLL <> *ZEROS AND SITLL <> *BLANKS
N06  C                   EVAL      WSST = 'U'
N06  C                   END
     C*
     C     SIRABX        IFEQ      *BLANKS
     C                   MOVE      *ZEROS        WSRAB
     C                   ELSE
     C                   MOVE      SIRAB         WSRAB
     C                   END
     C                   MOVE      SIFIDD        WSFIDD
     C                   MOVE      SIFIDM        WSFIDM
     C                   MOVE      SIFIDÅ        WSFIDÅ
     C                   MOVE      SIDTD         WSDTD
     C                   MOVE      SIDTM         WSDTM
     C                   MOVE      SIDTÅ         WSDTÅ
N02  C     SIST          COMP      'E'                                2828
N02  C   28SIST          COMP      ' '                                2828
N03  C   28SIST          COMP      'K'                                2828
N03  C   28SIST          COMP      'Å'                                2828
N04  C     WSOPD         COMP      0                                    29
     C*
     C     START3        TAG
     C                   EXFMT     BILDE02
     c   01              do
     c                   movel     'sad013'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C   05
     COR 07
     COR 09              DO
     C     START2        TAG
     C                   TIME                    XTIME
     C                   MOVE      XAAR          SSDTÅ
     C                   MOVE      XMND          SSDTM
     C                   MOVE      XDAG          SSDTD
     C                   MOVE      XTID          SSTM
     C     STSK          CHAIN     STS                                99
     C  N99              GOTO      START2
     C                   MOVE      WSAVD         SSAVD
     C                   MOVE      WSOPD         SSOPD
     C                   MOVE      WSUSER        SSUSR
     C                   MOVE      WSSG          SSSG
     C                   MOVE      'I'           SSRTYP
     C                   SELECT
     C     *IN05         WHENEQ    '1'
     C                   MOVE      SIST          SSSTG
     C                   MOVE      WSST          SSSTN
     C     *IN07         WHENEQ    '1'
     C                   MOVE      SIMI          SSSTG                          *FJERN MERKE
     C                   MOVE      '<'           SSSTN
     C     *IN09         WHENEQ    '1'
     C                   MOVE      SIMI          SSSTG                          *SETT  MERKE
     C                   MOVE      '>'           SSSTN
     C                   ENDSL
     C                   WRITE     STSR
     C*
     C     SADHK         CHAIN     SADH                               99
     C   05              MOVE      WSST          SIST
     C   07              MOVE      ' '           SIMI                           *FJERN MERKE
     C   09              MOVE      'I'           SIMI                           *SETT  MERKE
     C  N99              UPDATE    SADHR
N01  C     SADHK         CHAIN     FPXREF                             99
N01  C  N99              DELETE    FPXREFR
     C                   END                                                    *FLYTTET OPP
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *BLANKS       WSST
     C                   SETOFF                                       99
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODSIK        KLIST
     C                   KFLD                    WSIUNI
     C                   KFLD                    WSSG
     C     SADHK         KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     STSK          KLIST
     C                   KFLD                    SSDT
     C                   KFLD                    SSTM
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C                   KFLD                    WSUSER
     C*
     C     *ENTRY        PLIST
     C                   PARM                    WSUSER           10
     C*
     C                   MOVE      'SI'          WSIUNI            2
     C                   MOVE      *ZEROS        SIBELC
     C                   MOVE      *ZEROS        SIRAB
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
     C*
