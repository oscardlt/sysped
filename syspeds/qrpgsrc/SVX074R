      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED PEKER                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-89 95-97
      ***********************************************************
      *
     FSVNLPK    UF A E           K DISK
     FSVX074D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UCTP          IFNE      *BLANKS
     C                   MOVEL     UCTP          NVCTP
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SVLPKK        SETLL     SVNLPK
     C     SVLPKK        READE(N)  SVNLPK                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SVLPKK        READE(N)  SVNLPK                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVE      '0'           *IN90
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C  N27W2NR          IFEQ      '1'
     C                   EXSR      CRTPK
     C                   GOTO      START3
     C                   END
      *
     C  N27ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     SVLPKK        SETLL     SVNLPK
     C     SVLPKK        READE     SVNLPK                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVNLPKR
     C     SVLPKK        READE     SVNLPK                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UCTP
     C                   MOVE      *ZEROS        XANTL             3 0
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     NVCTP         IFNE      '*DELETE'
     C                   MOVEL     NVCTP         UCTP
     C                   ADD       1             XANTL
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     NVCTP         IFNE      '*DELETE'
     C     XANTL         IFEQ      0
     C                   MOVEL     NVCTP         UCTP
     C                   ADD       1             XANTL
     C                   ELSE
     C                   ADD       1             XANTL
     C                   WRITE     SVNLPKR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     NVCTP         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGPK
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     NVCTP         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTPK
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTPK         BEGSR
     C***********************************************
     C     W2CTP         IFLT      '44#'
     C     W2CTP         ORGT      '44#99'
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'SVX1844'     MSGNR
     C                   GOTO      SLCRT
     C                   END
      *
     C                   MOVEL     W2CTP         NVCTP
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2CTP
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGPK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVEL     NVCTP         WSCTP
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      SLCHG
      *
     C     WSCTP         IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'SVX1773'     MSGNR
     C                   GOTO      STCHG
     C                   END
      *
     C                   MOVEL     WSCTP         NVCTP
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTPK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       NVCTP
     C                   MOVEL     '*DELETE'     NVCTP
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVLPKK        KLIST
     C                   KFLD                    TVAVD
     C                   KFLD                    TVTDN
     C                   KFLD                    TVLI
      *
     C     *ENTRY        PLIST
     C                   PARM                    TVAVD
     C                   PARM                    TVTDN
     C                   PARM                    TVLI
     C                   PARM                    UCTP             35
     C                   PARM                    UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UIN27         *IN27
      *
     C                   ENDSR
      *
