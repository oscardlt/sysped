      *
      ***********************************************************
      * MANUELT VEDLIKEHOLD AV SKYTESTATUS PÅ OPPDRAGSNIVÅ
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 forkortet melding i subfil
********************************************************************
      *
     FSTSOPD    UF   E           K DISK
     FSTSFBR    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD14    IF   E           K DISK
     F                                     RENAME(headfr:hefr14)
     FSTSOPDFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SFBESK                 1     30
     D  AVSEND                 1      8
     D  MOTTAK                10     18
     D  STED                  20     30
N01  D  XSFBESK                1     28
     D                 DS
     D  SFLSM                  1     20
     D  SFLSML                 2      5
     D  SFLSMS                 9     12
     D  SFLSMM                16     18
     D  SFLSMT                 7     20
     C*
     C     *ENTRY        PLIST
     C                   PARM                    XXTYPE            5
     C                   PARM                    XXPRO             8 0
     C                   PARM                    XXANOP            4 0
     C                   PARM                    XXMANK            4 0
     C                   PARM                    XXBESK           30
      *
     C     HEAKEY        KLIST
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
      *
     C     STRKEY        KLIST
     C                   KFLD                    XXTYPE
     C                   KFLD                    XXPRO
     C     UPDKEY        KLIST
     C                   KFLD                    XXTYPE
     C                   KFLD                    XXPRO
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
      *
     C     FBRKEY        KLIST
     C                   KFLD                    XXTYPE
     C                   KFLD                    XXPRO
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
     C*
      * 55 ON = det er "XXPRO" = Løpenr ikke Turnr
     C     XXPRO         CHAIN     HEAD14                             55
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     STRKEY        SETLL     STSOPD
     C     STRKEY        READE(N)  STSOPD                                 94
      *
     C     START2        TAG
     C                   SETOFF                                       51
      *
     C  N94              DO
     C                   DO        14
     C                   SETOFF                                       50
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      *OFF
     C  N55HEPRO         COMP      XXPRO                              5050        TATT UT!!
     C   50              SETON                                        51          VARSEL!!
     C                   MOVEL     HENAS         AVSEND
     C                   MOVEL     HENAK         MOTTAK
     C                   MOVEL     HEADK3        STED
     C                   ELSE
     C                   CLEAR                   SFBESK
     C                   END
     C     SOANFB        SUB       SOANFK        MANKO             4 0
     C                   EXSR      GETCLI
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     STRKEY        READE(N)  STSOPD                                 94
     C  N94              END
     C                   END
      *
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0            NESTE SIDE
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   move      *off          *in30
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C                   GOTO      START2
     C                   END
     C*
     C                   MOVE      'N'           REFRES            1
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C     REFRES        IFEQ      'J'
     C                   GOTO      START
     C                   END
     C                   GOTO      START3
     C*
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     GETCLI        BEGSR
     C***********************************************
     C*
     C                   MOVE      *ZEROS        XXANKI            4 0
     C                   MOVE      *ZEROS        XXANKK            4 0
     C     FBRKEY        SETLL     STSFBR
     C     FBRKEY        READE     STSFBR                                 33
     C     *IN33         DOWEQ     *OFF
     C                   ADD       SFANKI        XXANKI
     C                   ADD       SFANKK        XXANKK
     C     FBRKEY        READE     STSFBR                                 33
     C                   END
     C     XXANKI        SUB       XXANKK        XXANKM            4 0
     C                   move      *blanks       SFLSM
     C                   move      *off          *in52
      * Test SF merknadstekst (flyttet til SR ikke merket..)
     C                   exsr      SRMTXT
     C                   eval      SFLSML = %trim(%editc(HENT:'Z'))
     C     XXANKI        ifeq      *Zeros
     C                   eval      SFLSMT = 'Ikke kollimerk'
     C                   else
      * Highlight delmanko (kun på kollimerket..)(men ikke v/50=Oppd er fjernet)
     C     SOSTAT        IFEQ      ' '
     C     XXANKM        andne     XXANKI
     C     *IN50         ANDEQ     *off
     C                   move      *on           *in52
     C                   END
     C                   eval      SFLSMS = %trim(%editc(XXANKK:'Z'))
     C                   eval      SFLSMM = %trim(%editc(XXANKM:'Z'))
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     SRMTXT        BEGSR
     C***********************************************
     C                   move      *blanks       SFMTXT
      * HELmanko vises med egen rødtekst (men ikke v/50=Oppd er fjernet)
     C     SOSTAT        ifeq      ' '
     C     XXANKK        ANDEQ     *Zeros
     C     *IN50         ANDEQ     *off
     C                   movel     'HEL-Manko!'  SFMTXT
     C                   END
     C     SFMTXT        Ifeq      *blanks
     C     SOMERK        Ifne      *blanks
     C                   movel     'T=Tekst! '   SFMTXT
     C                   else
     C     SOAMKK        IFNE      *zeros
     C     SOAMKK        ANDne     HENT
     C                   movel     'T=Tekst! '   SFMTXT
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   move      *off          *in30
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '1'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSF
     C                   END
     C     WSNR          IFEQ      '9'                                          FULLFØRE
     C                   MOVE      ' '           WSNR
     C                   EXSR      FULSF
     C                   END
     C     WSNR          IFEQ      'M'                                          GODKJ.DELMAN
     C                   MOVE      ' '           WSNR
     C                   EXSR      DELSF
     C                   END
     C     WSNR          IFEQ      'X'                                          FJERN FRA TUR
     C                   MOVE      ' '           WSNR
     C                   EXSR      SLETT
     C                   END
     C     WSNR          IFEQ      'T'                                          TEKST MM
     C                   MOVE      ' '           WSNR
     C                   EXSR      TEXT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
      * HVIS KUN ETT FRAKTBREV, GÅ RETT TIL KOLLI-NIVÅ
     C     SOANFB        IFEQ      1
     C                   Z-ADD     1             XXFBNR            3 0
     C                   CALL      'STSCLI'
     C                   PARM                    XXTYPE
     C                   PARM                    XXPRO
     C                   PARM                    SOAVD
     C                   PARM                    SOOPD
     C                   PARM                    XXFBNR
     C                   PARM                    XXANKI
     C                   PARM                    XXANKM
     C                   MOVE      'J'           REFRES            1
     C                   ELSE
     C                   CALL      'STSFBR'
     C                   PARM                    XXTYPE
     C                   PARM                    XXPRO
     C                   PARM                    SOAVD
     C                   PARM                    SOOPD
     C                   PARM                    SOANFB
     C                   PARM                    MANKO
     C                   END
      * RETURNERER EVT ENDREDE VERDIER
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     FULSF         BEGSR
     C***********************************************
     C     UPDKEY        CHAIN     STSOPD                             33
     C     *IN33         IFEQ      *OFF
     C                   SETOFF                                       50
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      *OFF
     C  N55HEPRO         COMP      XXPRO                              5050        TATT UT!!
     C   50              SETON                                        51          VARSEL!!
     C                   END
     C     *IN50         IFEQ      *OFF
     C                   SELECT
     C     SOSTAT        WHENEQ    ' '
     C                   MOVE      'Y'           SOSTAT
     C     SOSTAT        WHENEQ    'X'
     C     SOSTAT        OREQ      'Y'
     C     SOSTAT        OREQ      'M'
     C                   MOVE      ' '           SOSTAT
     C                   ENDSL
     C                   END
     C                   UPDATE    STSOPDR
     C                   END
      *
     C                   exsr      SRMTXT
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C                   ENDSR
     C*
     C***********************************************
     C     DELSF         BEGSR
     C***********************************************
     C     UPDKEY        CHAIN     STSOPD                             33
     C     *IN33         IFEQ      *OFF
     C                   SETOFF                                       50
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      *OFF
     C  N55HEPRO         COMP      XXPRO                              5050        TATT UT!!
     C   50              SETON                                        51          VARSEL!!
     C                   END
     C     *IN50         IFEQ      *OFF
     C                   SELECT
     C     SOSTAT        WHENEQ    ' '                                          BLANK(UFERDIG)
     C     SOSTAT        OREQ      'Y'                                          MAN.SATT OK
     C                   MOVE      'M'           SOSTAT
     C     SOSTAT        WHENEQ    'M'
     C                   MOVE      ' '           SOSTAT
     C                   ENDSL
     C                   END
     C                   UPDATE    STSOPDR
     C                   END
      *
     C                   exsr      SRMTXT
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C                   ENDSR
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
     C     UPDKEY        CHAIN(N)  STSOPD                             33
     C     *IN33         IFEQ      *OFF
     C                   SETOFF                                       50
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      *OFF
     C  N55HEPRO         COMP      XXPRO                              5050        TATT UT!!
     C   50              SETON                                        51          VARSEL!!
     C                   END
     C     *IN50         IFEQ      *OFF
     C     *IN55         ANDEQ     *OFF                                           LNR...
     C     SFMTXT        ANDEQ     'HEL-Manko!'
     C                   call      'SYGE21B'
     C                   parm                    HEPRO
     C                   parm                    HEAVD
     C                   parm                    HEOPD
     C                   parm                    UFLAGG            1
     C     UFLAGG        IFEQ      '1'
     C                   MOVE      'JOV1361'     MSGNR
     C                   move      *on           *in30
     C                   ELSE
     C                   MOVE      'J'           REFRES
     C                   END
     C                   ELSE
     C                   MOVE      'JOV1361'     MSGNR
     C                   move      *on           *in30
     C                   END
     C                   END
      *
     C                   exsr      SRMTXT
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C                   ENDSR
     C*
     C***********************************************
     C     TEXT          BEGSR
     C***********************************************
     C     UPDKEY        CHAIN(N)  STSOPD                             33
     C     *IN33         IFEQ      *OFF
     C                   SETOFF                                       50
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      *OFF
     C  N55HEPRO         COMP      XXPRO                              5050        TATT UT!!
     C   50              SETON                                        51          VARSEL!!
     C                   END
     C     *IN50         IFEQ      *OFF
     C                   z-add     HENT          HENT4
     C                   MOVEL     SOAMKK        WSAMKK
     C                   MOVEL     SOMERK        WSMERK
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     37            POSNBR
     C                   EXFMT     VARELI
     C     UPDKEY        CHAIN     STSOPD                             33
     C     *IN33         IFEQ      *OFF
     C     *IN12         IFEQ      *OFF
     C                   MOVEL     WSAMKK        SOAMKK
     C                   MOVEL     WSMERK        SOMERK
     C     SOSTAT        IFEQ      ' '                                          BLANK(UFERDIG)
     C     SOSTAT        OREQ      'Y'                                          MAN.SATT OK
     C                   MOVE      'M'           SOSTAT
     C                   END
     C                   END
     C                   UPDATE    STSOPDR
     C                   END
     C                   END
     C                   END
      *
     C                   exsr      SRMTXT
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
