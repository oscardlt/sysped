     A                                      REF(HEADF)
      *________
      * Forside
      *""""""""
     A          R SYIM03PA                  SKIPB(08)
     A                                    41'TOLLAGERREGNSKAP'
     A                                    59DATE EDTCDE(Y)
     A                                    69TIME EDTWRD('0 :  :  ')
     A                                    78'SYIM03R'
      *
     A                                    41'FRA AVDELING  . . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZAVDF          4S 0     91EDTCDE(3)
      *
     A                                    41'TIL AVDELING  . . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(1)
     A            ZAVDT          4S 0     91EDTCDE(3)
      *
     A                                    41'FRA GODSNUMMER  . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZGNF          15        91
      *
     A                                    41'TIL GODSNUMMER  . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(1)
     A            ZGNT          15        91
      *
     A                                    41'KUN LØPENR.. (3. siste siffer i Gn+
     A                                      r =). :'
     A                                      SPACEB(1)
     A            ZLNRF          1        91
     A                                    93'-'
     A            ZLNRT          1        95
      *
     A                                    41'FRA OG MED DATO . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZDTF           6S 0     91
      *
     A                                    41'TIL OG MED DATO . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(1)
     A            ZDTT           6S 0     91
      *
     A                                    41'UTELAT OPPDRAGSTYPER  . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZOT01          2        83
     A            ZOT02          2        87
     A            ZOT03          2        91
     A            ZOT04          2        95
     A            ZOT05          2        99
      *
     A            ZOT06          2        83
     A                                      SPACEB(1)
     A            ZOT07          2        87
     A            ZOT08          2        91
     A            ZOT09          2        95
     A            ZOT10          2        99
      *
     A                                    41'KUN OPPDRAG UTEN LØPENR/UTEN NYTT +
     A                                      GODSNR:'
     A                                      SPACEB(2)
     A            ZUNG           1        91
     A                                    10'Denne tollager regnskapslista tar +
     A                                      for seg alle import-oppdrag i ønske+
     A                                      t godsnummerserie.                 '
     A                                      SPACEB(5)
     A                                    10'Det forventes at HELE oppdraget ( +
     A                                      posisjonen) innfortolles/forpasses +
     A                                      samlet (med fullt antall/vekt)     '
     A                                      SPACEB(1)
     A                                    10'Dersom et oppdrag tollmessig IKKE +
     A                                      HÅNDTERES SLIK, må sideliggende (ma+
     A                                      nuelle) system dokumentere dette!! '
     A                                      SPACEB(1)
     A                                    10'Poster med uthevet trykk (og tekst+
     A                                      ten *EXPORT*) er reeksportert gods.'
     A                                      SPACEB(2)
     A                                    10'I noen tilfeller vil disse stå i d+
     A                                      irekte forhold til det importerte o+
     A                                      oppdrag (lenger opp i lista)       '
     A                                      SPACEB(1)
     A                                    10'I andre tilfeller kan det være del+
     A                                      er av et oppdrag som er reeksporter+
     A                                      t.                                 '
     A                                      SPACEB(1)
     A                                    10'Opplistingen av reeksport må betra+
     A                                      ktes som et HJELPEMIDDEL til nevnte+
     A                                       avstemming i sideliggende system. '
     A                                      SPACEB(1)
     A                                    10'Når et godsnr omfatter mer enn en +
     A                                      posisjon(importoppdrag) skrives en +
     A                                      sumpost med ant posisj. kolli/vekt '
     A                                      SPACEB(3)
     A                                    10'(Denne sumposten inneholder ikke/ +
     A                                      tar ikke hensyn til ant/vekt på de +
     A                                      evt. listede reeksportene)         '
     A                                      SPACEB(1)
      *________
      * Heading
      *""""""""
     A          R SYIM03PB                  SKIPB(4)
     A                                      HIGHLIGHT
     A                                     2'TOLLAGERREGNSKAP'
     A                                    84DATE EDTCDE(Y)
     A                                    93TIME EDTWRD('0 :  :  ')
     A                                   103'SYIM03R Side:'
     A                                    +1PAGNBR EDTCDE(Z)
      *
     A                                    89'-TOLLDEKLARERT/FORPASS/REEKS-'
     A                                      SPACEB(2)
      *
S01  A*                                    2'AVD OPPDNR. SIG OT GODSNUMMER     +
S01  A*                                      POS POS2  ANTALL ENHET/VARESLAG   +
S01  A*                                              ...VEKT.. EKSP.ENH./LØP.NR+
S01  A*                                     ./DATO'
S01  A*                                     SPACEB(1)
S01  A*                                   83'VEKT  EKS.E.  LØPENUMMER   DATO'
S04  A*                                    2'AVD OPPDNR. SIG OT GODSNUMMER'
S04  A*                                     SPACEB(1)
S04  A*                                   37'POS POS2  ANTALL ENHET/VARESLAG'
S04  A*                                   85'VEKT  EKS.E.  LØPENUMMER   DATO'
N04  A                                     3'AVD OPPDNR. SIG OT GODSNUMMER'
N04  A                                      SPACEB(1)
N04  A                                    38'POS POS2  ANTALL ENHET/VARESLAG'
N04  A                                    86'VEKT  EKS.E.  LØPENUMMER   DATO'
     A                                     2'TURNUMMER.AVSENDER.............ADR+
     A                                      .1................MOTTAKER.........+
     A                                      ....ADR.1................ADR.3.....+
     A                                      ..............'
     A                                      SPACEB(1)
      *
      *_____________
      * Detaljlinjer
      *"""""""""""""
     A          R SYIM03PC                  SPACEB(2)
S04  A*           HEAVD     R              1EDTCDE(Z)
S04  A*           HEOPD     R              6EDTCDE(Z)
S04  A*           HESG      R             14
S04  A*           HEOT      R             18
S04  A*           HEGN      R             21
S04  A*           HEPOS1    R             37
S04  A*           HEPOS2    R             41EDTCDE(Z)
S04  A*           HENT      R             46EDTCDE(Z)
S04  A*           HEVS1     R             54
S04  A*           HEVKT     R             80EDTCDE(Z)
S04  A*           HETLE     R             90HIGHLIGHT
S04  A*           HETLL     R             97HIGHLIGHT
S04  A*           SIDTG         10       108
N04  A            HEAVD     R              2EDTCDE(Z)
N04  A            HEOPD     R              7EDTCDE(Z)
N04  A            HESG      R             15
N04  A            HEOT      R             19
N04  A            HEGN      R             22
N04  A            HEPOS1    R             38
N04  A            HEPOS2    R             42EDTCDE(Z)
N04  A            HENT      R             47EDTCDE(Z)
N04  A            HEVS1     R             55
N04  A            HEVKT     R             81EDTCDE(Z)
N04  A            HETLE     R             91HIGHLIGHT
N04  A            HETLL     R             98HIGHLIGHT
N04  A            SIDTG         10       109
     A            HEPRO     R              3EDTCDE(Z)
     A                                      SPACEB(1)
     A            ZZNAS         20        12
     A            ZZADS1        20        33
     A            ZZNAK         20        54
     A            ZZADK1        20        75
     A            ZZADK3        20        96
     A                                     1' '
     A                                      SPACEB(1)
N06  A                                     2'La:'
N06  A            ZZKDLS         2         5
N14  A            ZZLS           8         8
S16  A*           SIKDLS         2         6
S04  A* 66                                79'Nytt godsnr:'
S14  A* 66                                 9'Nytt godsnr:'
N14  A  66                                17'Nytt godsnr:'
     A                                      HIGHLIGHT
S04  A* 66        HEGNN     R             92HIGHLIGHT
S14  A* 66        HEGNN     R             22HIGHLIGHT
N14  A  66        HEGNN     R             29HIGHLIGHT
S04  A* 66        TTDTG         10       108
S14  A* 66        TTDTG         10        37
N14  A  66        TTDTG         10        45
n04  A  67                                48'Vareverdi:'
N04  A  67        VAVEKO         3        58
n04  A  67        TRVERV         3        61
n04  A  67        TRVERB        13S 2     64EDTCDE(L)
n05  A  68                                80'Ekstern. ref:'
n05  A  68        EXTREF        15        94
N05  A                                   111'STATUS:'
N05  A            SIST           1       119HIGHLIGHT
N06  A                                     2'Transpmåte:'
N06  A                                      SPACEB(1)
N06  A            SITRM          2S 0     14
N06  A                                    18'Landkode/ID'
N06  A            SILKT          2        30
N06  A            SITRID        17        33
N13  A                                    54'Ku/Fo:'
N13  A            HEKNK          8S 0     60
N13  A                                    68'/'
N13  A            SYRG          14        69
     A* Totalsum denne posisjon
     A          R SYIM03PD                  SPACEB(1)
     A                                      HIGHLIGHT
     A                                     4'*TOTAL DENNE POSISJON*'
     A            POSANT         7S 0     28EDTCDE(Z)
     A                                    36'Poster'
     A            POSKLL         7S 0     44EDTCDE(Z)
     A                                    52'Kolli '
     A            POSVKT         9S 0     78EDTCDE(Z)
     A                                    88'Kilo  '
     A* Totalsum dette godsnr
     A          R SYIM03PE                  SPACEB(1)
     A                                      HIGHLIGHT
     A                                     2'**TOTAL DETTE GODSNUMMER**'
     A            GODANT         7S 0     28EDTCDE(Z)
     A                                    36'Poster'
     A            GODKLL         7S 0     44EDTCDE(Z)
     A                                    52'Kolli '
     A            GODVKT         9S 0     78EDTCDE(Z)
     A                                    88'Kilo  '
     A* REEKSPORT PÅ DETTE GODSNR??? - HELE RECORDEN HIGHLIGHTES
     A          R SYIM03RE                  SPACEB(1)
     A                                      HIGHLIGHT
     A            SEAVD          4S 0      1EDTCDE(Z)
     A            SETDN          7S 0      6EDTCDE(Z)
     A            SESG           3        14
     A            HEGN      R             21
     A            SEPOS          8        37
     A            SENTK          7S 0     46EDTCDE(Z)
     A                                    63'*EXPORT*'
     A            SEVKB          9S 0     78EDTCDE(Z)
     A            SETLE          6        89
     A            SETLL         10        97
     A            SEDTG         10       108
      *____________
      * Last Record
      *""""""""""""
     A          R SYIM03PØ                  SPACEB(2)
     A                                     2'----------------------------------+
     A                                      --------   S i s t e   s i d e n   +
     A                                      -----------------------------------+
     A                                      ----------------'
