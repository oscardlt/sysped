********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 Endret logikk for sletting
     FZADFL01   UF   E           K DISK
     FZADVL01   UF   E           K DISK
     FZADVJ     UF   E           K DISK
N01  DWOK              S              1
     ISADVR
     I              SVBELT                      I_SVBELT
      *_____
      * INIT
      *"""""
     C                   EXSR      INIT
      *___________________________________
      * Slett poster i ZADV som er plukket
N01   * og som ikke har uplukkede vareposter
      *"""""""""""""""""""""""""""""""""""
     C     *LOVAL        SETLL     ZADFL01
     C                   READ      ZADFL01                                56
     C                   DOW       *IN56 = *OFF
     C                   IF        SFAVD <> 0
     C                   IF        SFOPDN <> 0
N01  C                   EXSR      CHKVARE
N01  C                   IF        WOK=*BLANKS
     C                   DELETE    SADFR
N01  C                   ELSE
N01  C                   UPDATE    SADFR
N01  C                   ENDIF

     C                   ENDIF
     C                   ENDIF
     C                   READ      ZADFL01                                56
     C                   ENDDO
      *_________________________________________________
      * Slett poster i ZADV som ikke har tilhörende ZADF
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     *LOVAL        SETLL     ZADVL01
     C                   READ      ZADVL01                                53
     C                   DOW       *IN53 = *OFF
     C     ZADFK         CHAIN     ZADFL01                            55
     C                   IF        *IN55

     C     ZADVJK        SETLL     ZADVJ
     C     ZADVJK        READE     ZADVJ                                  54
     C                   DOW       *IN54 = *OFF
     C                   DELETE    SADVJR
     C     ZADVJK        READE     ZADVJ                                  54
     C                   ENDDO

     C                   DELETE    SADVR
     C                   ENDIF

     C                   READ      ZADVL01                                53
     C                   ENDDO

     C                   EVAL      *INLR=*ON
      *______________________________________________
     C     INIT          BEGSR
      *""""""""""""""""""""""""""""""""""""""""""""""
     C     ZADFK         KLIST
     C                   KFLD                    SVUNIK
     C                   KFLD                    SVREFF
N01  C     ZADVK         KLIST
N01  C                   KFLD                    SFUNIK
N01  C                   KFLD                    SFREFF
     C     ZADVJK        KLIST
     C                   KFLD                    SVREFF
     C                   KFLD                    SVUNIK
     C                   KFLD                    SVLI
     C                   ENDSR
N01   *______________________________________________
N01  C     CHKVARE       BEGSR
N01   *""""""""""""""""""""""""""""""""""""""""""""""
N01  C                   EVAL      WOK=' '
N01  C                   EVAL      *IN57=*OFF
N01  C     ZADVK         SETLL     ZADVL01
N01  C     ZADVK         READE(N)  ZADVL01                                57
N01  C     *IN57         DOWEQ     *OFF
N01  C                   IF        SVAVD=*ZEROS
N01  C                   EVAL      WOK='X'
N01  C                   LEAVE
N01  C                   ENDIF
N01  C     ZADVK         READE(N)  ZADVL01                                57
N01  C                   ENDDO
N01  C                   ENDSR
