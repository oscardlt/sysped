     FEDIC      UF   E             DISK
     FEUNH      O    E             DISK
     FEDIM      O    E             DISK
     FECNI01B   O    E             DISK
     FECNT01B   O    E             DISK
     FEDTM01B   O    E             DISK
     FEFTX01B   O    E             DISK
     FELOC01B   O    E             DISK
     FENAD01B   O    E             DISK
     FERFF01B   O    E             DISK
     FESTS01B   O    E             DISK
     FETDT01B   O    E             DISK
     FHEADF     IF   E           K DISK
     FTRACKT    IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FECMSG4    IF   E           K DISK
     FTURER14   IF   E           K DISK
     FTRAN      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FFIRM      IF   E           K DISK
      *____________
      * DS for Dato
      *""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTM4                   1      4
     D  WTM                    1      6
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12    DIM(12)
     D                SDS
     D  USER                 254    263
      *______________________
      * Initialize subroutine
      *""""""""""""""""""""""
     C                   EXSR      INIT
     C                   EXSR      SNDRQS
     C                   RETURN
      *////////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////////
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C                   PARM                    UTIM              4
     C                   PARM                    UDAT              8
     C                   PARM                    U4405             3
      *
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     ECMSG4K       KLIST
     C                   KFLD                    TRKNFA
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C     STED2DKF      KLIST
     C                   KFLD                    HELKA
     C                   KFLD                    HESDF
     C     STED2DKT      KLIST
     C                   KFLD                    HETRI
     C                   KFLD                    HESDT
     C     TRANK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VMTRAN
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    HEAVD
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
      *__________________
      * Define workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    CREP0         WREP0
     C     *LIKE         DEFINE    CREP1         WREP1
     C     *LIKE         DEFINE    CREP2         WREP2
      *__________________________
      * Retreive System Date/Time
      *""""""""""""""""""""""""""
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
     C                   MOVE      UAVD          HEAVD
     C                   MOVE      UOPD          HEOPD
     C                   MOVE      'S'           ECSR
     C                   MOVE      'IFTSTA'      EC0065
     C                   MOVE      'A'           KOAUNI
     C                   Z-ADD     0             ØX                2 0                      --I
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////////
     C     SNDRQS        BEGSR
      *////////////////////////////////////////////////////////////////
      *_____________________________________________________
      * Retrieve last used recordnumber from EDIC and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CMN
     C                   Z-ADD     CMN           WMN
     C                   UPDATE    EDICR
     C                   ENDIF
      *_____________________
      * Write record to EUNH
      *"""""""""""""""""""""
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTSTA'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '01B'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     WMN           U0062B
      *
     C                   MOVEL     '77'          U1001
     C                   MOVEL     WMN           U1004
     C                   MOVEL     '9'           U1225
     C                   WRITE     UNHR
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     1             DREP0
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDT           D2380
     C     D2380         CAT       WTM4:0        D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *
     C                   MOVE      'N'           XTROK             1
     C                   IF        HEPRO <> 0
      * HENT TRANSPORTØRSNR FRA TUR
     C     HEPRO         CHAIN     TURER14                            33
     C  N33              IF        TUKNT <> 0
     C     TRANK         CHAIN     TRAN                               33
     C                   IF        *IN33 = *OFF
     C                   EVAL      XTROK = 'J'
     C                   EVAL      KUNDNR = VMTRKU
     C                   END
     C                   END
     C                   END
     C                   IF        XTROK = 'N'
     C     KODTAK        CHAIN     KODTA                              33
     C                   EVAL      KUNDNR = KOAKNR
     C                   END
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C     HEADFK        CHAIN     HEADF                              33
     C     HEADFK        CHAIN     TRACKT                             33
     C     ECMSG4K       CHAIN     ECMSG4                             33
      *____________________
      * Write record to NAD
      *""""""""""""""""""""
      * TRANSPORTØR
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     1             NGN
     C                   Z-ADD     1             NREP0
     C                   MOVEL     'CA'          N3035
     C                   MOVEL     KUNDNR        N3039
     C                   EVAL      N3055  = 'ZZZ'
     C                   EVAL      N3124A = KNAVN
     C                   EVAL      N3036A = ADR1
     C                   EVAL      N3042A = ADR2
     C                   EVAL      N3164  = ADR3
     C                   MOVEL     POSTNR        N3251
     C                   EVAL      N3207  = SYLAND
     C                   WRITE     NADR
      *____________________
      * Write record to CNI
      *""""""""""""""""""""
     C                   CLEAR                   CNIR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     4             CGN
     C                   Z-ADD     1             CREP0
     C                   MOVEL     '1'           C1490
     C                   MOVEL     HERFA         C1004
     C                   WRITE     CNIR
      *_____________________________
      * Total number of COLLI
      *"""""""""""""""""""""""""""""
     C                   Z-ADD     CREP0         WREP0
      *
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     4             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     WREP1         CREP1
     C                   MOVE      '10 '         C6069
     C                   Z-ADD     HENT          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR
      *___________________
      * Total gross weight
      *"""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     4             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         CREP1
     C                   MOVE      '7  '         C6069
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR
      *___________________
      * Total cube
      *"""""""""""""""""""
     C                   IF        HEM3 <> 0
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     4             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         CREP1
     C                   MOVE      '15 '         C6069
     C                   Z-ADD     HEM3          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'MTQ'         C6411
     C                   WRITE     CNTR
     C                   END
      *___________________
      * Total loading metres
      *"""""""""""""""""""
     C                   IF        HELM <> 0
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         CREP1
     C                   MOVE      '15 '         C6069
     C                   Z-ADD     HELM          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'MTR'         C6411
     C                   WRITE     CNTR
     C                   END
      *____________________
      * Write record to STS
      *""""""""""""""""""""
     C                   CLEAR                   STSR
     C                   Z-ADD     WMN           SMN
     C                   Z-ADD     5             SGN
     C                   Z-ADD     WREP0         SREP0
     C                   Z-ADD     1             SREP1
     C                   MOVEL     '1'           S9015
     C                   SELECT
     C                   WHEN      U4405 = 'POD' OR U4405 = '021'
     C                   MOVEL     '21'          S4405
     C                   WHEN      U4405 = 'COL'
     C                   MOVEL     '13'          S4405
     C                   WHEN      U4405 = 'ARD'
     C                   MOVEL     '21'          S4405
     C                   WHEN      U4405 = 'ARC'
     C                   MOVEL     '13'          S4405
     C                   ENDSL
     C                   WRITE     STSR
      *____________________
      * Write record to RFF
      *""""""""""""""""""""
     C                   Z-ADD     SREP1         WREP1
     C                   Z-ADD     0             WREP2
      *
     C*                  MOVE      'N'           XCUREF            1
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C*                  IF        FSKODE = 'CU'
     C*                  EVAL      XCUREF = 'J'
     C*                  END
     C                   IF        FSKODE = 'BM' OR FSKODE = 'CU'
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     5             RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         RREP2
     C                   MOVEL     FSKODE        R1153
     C                   MOVEL     FSSOK         R1154
     C                   WRITE     RFFR
     C                   END
     C     HEADFK        READE     FRISOK                                 33
     c                   ENDDO
     C*                  IF        XCUREF = 'N'
     C*                  CLEAR                   RFFR
     C*                  Z-ADD     WMN           RMN
     C*                  Z-ADD     5             RGN
     C*                  Z-ADD     WREP0         RREP0
     C*                  Z-ADD     WREP1         RREP1
     C*                  Z-ADD     1             WREP2
     C*                  Z-ADD     WREP2         RREP2
     C*                  MOVEL     'CU'          R1153
     C*                  MOVEL     HERFA         R1154
     C*                  WRITE     RFFR
     C*                  END
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     5             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
     C                   Z-ADD     1             DREP2
     C                   IF        TXETAD = 0
     C                   MOVE      UDAT          X_ETAD            8
     C                   MOVE      UTIM          X_ETAK            4
     C                   ELSE
     C                   MOVE      TXETAD        X_ETAD
     C                   MOVE      TXETAK        X_ETAK
     C                   END
     C                   IF        TXETDD = 0
     C                   MOVE      UDAT          X_ETDD            8
     C                   MOVE      UTIM          X_ETDK            4
     C                   ELSE
     C                   MOVE      TXETDD        X_ETDD
     C                   MOVE      TXETDK        X_ETDK
     C                   END
     C                   IF        TXATDD = 0
     C                   MOVE      UDAT          X_ATDD            8
     C                   MOVE      UTIM          X_ATDK            4
     C                   ELSE
     C                   MOVE      TXATDD        X_ATDD
     C                   MOVE      TXATDK        X_ATDK
     C                   END
     C                   IF        TXATAD = 0
     C                   MOVE      UDAT          X_ATAD            8
     C                   MOVE      UTIM          X_ATAK            4
     C                   ELSE
     C                   MOVE      TXATAD        X_ATAD
     C                   MOVE      TXATAK        X_ATAK
     C                   END
     C                   SELECT
     C                   WHEN      U4405 = 'ARC'
     C                   EVAL      D2005 = '234'
     C                   EVAL      D2380 = X_ETDD + X_ETAK
     C                   WHEN      U4405 = 'ARD'
     C                   EVAL      D2005 = '117'
     C                   EVAL      D2380 = X_ETAD + X_ETDK
     C                   WHEN      U4405 = 'POD'
     C                   EVAL      D2005 = '2'
     C                   EVAL      D2380 = X_ATDD + X_ATDK
     C                   WHEN      U4405 = 'COL'
     C                   EVAL      D2005 = '235'
     C                   EVAL      D2380 = X_ATAD + X_ATAK
     C                   ENDSL
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *
     C                   ADD       1             DREP2
     C                   EVAL      D2005 = '334'
     C                   EVAL      D2380 = UDAT + UTIM
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *____________________
      * Write record to NAD CONSIGNOR
      *""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     6             NGN
     C                   Z-ADD     WREP0         NREP0
     C                   Z-ADD     WREP1         NREP1
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     WREP2         NREP2
     C                   EVAL      N3035  = 'CZ'
     C                   MOVEL     HEKNS         N3039
     C                   EVAL      N3055  = 'ZZZ'
     C                   EVAL      N3124A = HENAS
     C                   EVAL      N3036A = HEADS1
     C                   EVAL      N3042A = HEADS2
     C     ' '           SCAN      HEADS3        XPOS              3 0    33
     C                   IF        *IN33
     C                   EVAL      N3164  = %SUBST(HEADS3:XPOS+1:30-XPOS)
     C                   EVAL      N3251  = %SUBST(HEADS3:1:XPOS-1)
     C                   ELSE
     C                   EVAL      N3164  = HEADS3
     C                   MOVEL     HEADS3        N3251
     C                   END
     C                   EVAL      N3207  = HELKA
     C                   WRITE     NADR
      *____________________
      * Write record to NAD DELIVERY PARTY
      *""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     6             NGN
     C                   Z-ADD     WREP0         NREP0
     C                   Z-ADD     WREP1         NREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         NREP2
     C                   EVAL      N3035  = 'DP'
     C                   MOVEL     HEKNK         N3039
     C                   EVAL      N3055  = 'ZZZ'
     C                   EVAL      N3124A = HENAK
     C                   EVAL      N3036A = HEADK1
     C                   EVAL      N3042A = HEADK2
     C     ' '           SCAN      HEADK3        XPOS              3 0    33
     C                   IF        *IN33
     C                   EVAL      N3164  = %SUBST(HEADK3:XPOS+1:30-XPOS)
     C                   EVAL      N3251  = %SUBST(HEADK3:1:XPOS-1)
     C                   ELSE
     C                   EVAL      N3164  = HEADK3
     C                   MOVEL     HEADK3        N3251
     C                   END
     C                   EVAL      N3207  = HETRI
     C                   WRITE     NADR
      *____________________
      * Write record to TDT
      *""""""""""""""""""""
     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     8             TGN
     C                   Z-ADD     WREP0         TREP0
     C                   Z-ADD     WREP1         TREP1
     C                   Z-ADD     1             TREP2
     C                   EVAL      T8051  = '20'
     C                   EVAL      T8028  = HEHAWB
     C                   EVAL      T8067  = '12'
     C                   WRITE     TDTR
      *____________________
      * Write record to LOC
      *""""""""""""""""""""
     C                   Z-ADD     TREP2         WREP2
      *
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     9             LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   Z-ADD     WREP2         LREP2
     C                   Z-ADD     1             LREP3
     C                   SELECT
     C                   WHEN      U4405 = 'POD' OR U4405 = 'ARD'
     C                   MOVEL     '7'           L3227
     C     STED2DKT      CHAIN     STED2D                             33
     C                   WHEN      U4405 = 'COL' OR U4405 = 'ARC'
     C                   MOVEL     '11'          L3227
     C     STED2DKF      CHAIN     STED2D                             33
     C                   ENDSL
     C                   EVAL      L3224 = ST2NVN
     C                   WRITE     LOCR
      *__________________________
      * Write record to EDIM file
      *""""""""""""""""""""""""""
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVEL     U0065         M0065
     C                   MOVEL     USER          M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   EVAL      M0062 = 'E160R_01B'
     C                   Z-ADD     WMN           MMN
     C                   MOVEL     'S'           MSR
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   MOVEL     UAVD          MAVD
     C                   MOVEL     UOPD          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'                                        <-------------I
     C                   MOVE      ØA153H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA153X        ØA12                                         I
     C*                  MOVE      ',   '        ØA12                                         I
     C                   MOVE      '.   '        ØA12                                         I
     C                   MOVE      ØA153D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'                                        <-------------I
     C                   SUB       1             ØX                                           I
     C                   ENDIF                                                  <-------------I
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
