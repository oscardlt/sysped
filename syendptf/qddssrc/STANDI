********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10005 * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRE FRA 2.0 TIL 4.0
XXXXXX*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11016 * 02 YBC PTF11 TO NYE EKSPEDISJONS ENHETER FOR OMBEREGNING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12001 * 03 YBC PTF12 NYTT FELT FOR MVA-REGISTRTERT
12053 * 04 YBC PTF12 FLERE BELØP-FELTER ER ENDRET FOR Å TA STØRRE VERDI
12281 * 05 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
     A*__________________________________
     A* SYSPED / Tolldeklarasjoner Import
     A*""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R STANDIR
      * SIST er tatt i bruk for å bestemme når inngående TVINN-melding
      * skal utskrives       310193
     A            SIST      R               REFFLD(ST)
      *
     A            SIAVD          4S 0       COLHDG('AVDELING')
     A            SITDN     R               REFFLD(TDN)
E03  A            SISG      R               REFFLD(SG)
E03  A            SIDT      R               REFFLD(DT)
E03  A                                      COLHDG('Registreringsdato')
     A            SIDTY     R               REFFLD(DTY)
     A            SIDP      R               REFFLD(DP)
N05  A            SIDP2          2S 0       COLHDG('Siste 2 siffer pros')
     A            SIKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            SINAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            SIADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            SIADS2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Selger')
     A            SIADS3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Selger')
     A            SINTK     R               REFFLD(NTK)
     A            SISKI     R               REFFLD(KDSKI)
     A            SIKDDK    R               REFFLD(KDDK)
     A            SIVKB     R               REFFLD(VKT9)
     A                                      COLHDG('Bruttovekt')
     A            SIKDC     R               REFFLD(KDC)
     A            SIKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            SIRG          11A         COLHDG('FORETAKSNR.')
N03  A            SIMVA          1A         COLHDG('J/N MVA-REGISTRERT')
     A            SINAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            SIADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            SIADK2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Kjøper')
     A            SIADK3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Kjøper')
     A            SIVAL1    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode Frakt')
     A            SIBEL1    R               REFFLD(BEL11)
     A                                      COLHDG('Beløp tollb.frakt')
     A            SIVAL2    R               REFFLD(VAL)
     A                                      COLHDG('Val.kode A. kost')
     A            SIBEL2    R               REFFLD(BEL11)
     A                                      COLHDG('Beløp A. kost')
     A            SIFTG2    R               REFFLD(KDFT)
     A                                      COLHDG('Fortegn +/- a.kost')
     A            SILKA     R               REFFLD(LKA)
     A                                      COLHDG('Landk avs/utf-land')
     A            SITLF     R               REFFLD(TLF)
     A                                      COLHDG('Telefon Deklarant')
     A            SINAD     R               REFFLD(NA)
     A                                      COLHDG('Navn Deklarant')
     A            SILV      R               REFFLD(LV)
     A            SILVT     R               REFFLD(LVT)
     A            SITRID    R               REFFLD(TRID)
     A            SILKT     R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
     A            SIVAL3    R               REFFLD(VAL)
     A                                      COLHDG('Val.kode Faktsum')
     A            SIBEL3    R               REFFLD(BEL13)
     A                                      COLHDG('Beløp Faktsum')
     A            SIVKU     R               REFFLD(VKU)
S05  A*           SITST     R               REFFLD(TST)
N05  A            SITST          2S 0       COLHDG('Transaksjonstype')
     A            SITRT     R               REFFLD(TRT)
     A            SITRM     R               REFFLD(TRM)
     A            SIFIF     R               REFFLD(FIF)
     A            SIFID     R               REFFLD(DT)
     A                                      COLHDG('Finans oppl.fakdat')
S04  A*           SIBELT    R               REFFLD(BEL9)
S04  A*                                     COLHDG('Beløp Trafikkavg')
N04  A            SIBELT        11S 0       COLHDG('Beløp Trafikkavg')
     A            SI07           7          COLHDG('*blanks')
     A            SIKTA     R               REFFLD(KTA)
     A            SIKTB     R               REFFLD(KTB)
     A            SIGN          15          COLHDG('GODSNR.')
     A            SIFT1     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-1')
     A            SIFT2     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-2')
     A            SIFT3     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-3')
     A            SIFT4     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-4')
     A            SIDST     R               REFFLD(DST)
S04  A*           SIBEL4    R               REFFLD(BEL9)
S04  A*                                     COLHDG('Beløp CIFsum')
N04  A            SIBEL4        11S 0       COLHDG('Beløp CIFsum')
     A            SIDTG     R               REFFLD(DTG)
     A            SITLL     R               REFFLD(TLL)
     A            SITLE     R               REFFLD(TLE)
     A            SILS      R               REFFLD(LS)
     A            SIKDLS         2A         COLHDG('Lagringssted kode')
S01  A*           SIKDH     R               REFFLD(KDH)
N01  A            SIKDH          4S 0       COLHDG('HAVNEKODE')
     A            SIKDTR    R               REFFLD(KDTR)
     A            SIAS      R               REFFLD(AS)
     A                                      COLHDG('Avgiftssats transp')
S04  A*           SIBEL5    R               REFFLD(BEL9)
S04  A*                                     COLHDG('Beløp Tot Toll')
N04  A            SIBEL5        11S 0       COLHDG('Beløp Tot Toll')
S04  A*           SIBEL6    R               REFFLD(BEL9)
S04  A*                                     COLHDG('Beløp Tot MVA')
N04  A            SIBEL6        11S 0       COLHDG('Beløp Tot MVA')
     A            SIBEL7    R               REFFLD(BEL110)
     A                                      COLHDG('Beløp Tot avg')
     A            SIBEL8    R               REFFLD(BEL11)
     A                                      COLHDG('Frakt omberegnet')
     A            SIBEL9    R               REFFLD(BEL11)
     A                                      COLHDG('Andre kost.omber')
S04  A*          SIBELA    R               REFFLD(BEL7)
S04  A*                                     COLHDG('Beløp Forsikring')
N04  A            SIBELA         9S 0       COLHDG('Beløp Forsikring')
     A            SILV2     R               REFFLD(LV)
      * 1. POS AV SILV2 ER BRUKT TIL Å VISE OM SKRIVER UT UTLEVERINGSATTEST
      * SOM TOLLKVITTERING
     A            SIPOS     R               REFFLD(POS9)
     A            SITARF        16          COLHDG('TARIFFØRSNAVN')
     A            SIVALB    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode Work')
     A            SIBELB    R               REFFLD(BEL5S)
     A                                      COLHDG('Beløp Work')
     A            SIMID     R               REFFLD(MID)
     A            SIMIDK    R               REFFLD(MIDK)
     A            SIMF      R               REFFLD(MF)
     A            SIMP      R               REFFLD(MP)
     A            SIDTØ     R               REFFLD(DT)
     A                                      COLHDG('Ønsket Beh.dato')
     A            SIMI      R               REFFLD(MI)
     A            SIBELD    R               REFFLD(BEL10S)
     A                                      COLHDG('Beløp Depositum')
     A            SIPKL     R               REFFLD(PK)
     A                                      COLHDG('Print ved linje ?')
     A            SIKDV     R               REFFLD(KDV)
     A            SI28          28          COLHDG('*blanks')
     A            SIOPD     R               REFFLD(OPD)
     A            SIFRBN    R               REFFLD(FRBN)
     A            SIWS      R               REFFLD(WS)
     A            SIVALR    R               REFFLD(VAL)
     A            SIBELR    R               REFFLD(BEL11)
     A                                      COLHDG('Bearbeidskostnad')
     A*
     A* PRINTERSTYRING OG TAD KOMMUNIKASJON
     A            SIDL           1          COLHDG('BRUKE DATALISTE ?')
     A            SITOLK         1          COLHDG('BRUKE TOL.KOM. ?')
     A            SIDLK          1          COLHDG('D-LISTE VED KOM. ?')
     A            S0004     R               REFFLD(N0004)
     A            S0010     R               REFFLD(N0010)
     A            S0035     R               REFFLD(N0035)
     A            S3039D    R               REFFLD(N3039D)
     A            S3039E    R               REFFLD(N3039E)
N02  A            S3039EO1  R               REFFLD(N3039E)
N02  A                                      COLHDG('EKSP.ENHET FOR SO')
N02  A            S3039EO2  R               REFFLD(N3039E)
N02  A                                      COLHDG('EKSP.ENHET FOR EB/RE')
     A            SIA4           1          COLHDG('A4-FORMAT ?')
     A            S0026     R               REFFLD(N0026)
     A            SIKTC          1A         COLHDG('IKKE TOLLKREDITT')
     A            SIEKST         1A         COLHDG('EKSTERNE DATA')
     A          K SIAVD
