      *********************************************************************
      * Spørring på oppdrag (vis meny)
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/SYSO00 MODULE(*LIBL/SYSO00
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 HVIS PAR. "FRISO" FINNES VIS KUN DE DEFINERTE (m/egen tekst)
10XXX * 02 JOV PTF10 F.O.M dato sette IKKE dersom bruker er definert som INTERN/Ansatt
10XXX * 03 YBC PTF10 VIS KUN OPPDRAG SOM FAKTURAMOTTAGER ER SAMME SOM BRUKER-ID SIN
10XXX * 04 JOV PTF10 ptf02 reversert
********************************************************************
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FKODFRILF  IF   E           K DISK    USROPN
     FKODTOTY   IF   E           K DISK    USROPN
     FGSSCGL02  IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D Dager           s              3
     D DagerN          s              5  0
     D AddSub          s              3
     D Idag            s              8  0
     D Dato8N          s              8  0
     D Dato8A          s              8
     D KUNGR           s              8  0 DIM(30)
     D UsrSpcName      s             10
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  DFS                    1     10
     D                                     DIM(10)
     D  KFSODK                 1     10
     D                 DS
     D  OtString               1     48
     D  OT                     1     48    DIM(16)
n01  D                 DS
n01  D  PAVRDA                 1     50
n01  D    PAVRDA1_3            1      3
n01  D    PAVRDA5_50           5     50
      * For å konvertere uheldige bokstaver
     D UC              C                   CONST(' ')
     D LC              C                   CONST('&')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      * 2-Prod.koder -pulldown (+bunn)
     C                   exsr      DspKGR
     C                   exsr      DspOT
     C                   exsr      DspPart
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      DAGER    = zhbgetvar('DAGER')
     C                   eval      DAGERN   = c2n2(DAGER)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C     LC:UC         XLATE     KNAVN         KNAVN
     C                   callp     updHtmlVar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     c     Dato8n        Ifne      *zeros
     C                   callp     updHTMLvar('WSDTF':DATO8A)
     c                   else
     C                   callp     updHTMLvar('WSDTF':'')
     c                   end
     C                   callp     updHTMLvar('WSDTT':'')
      *
      *
     C                   endsr
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspKGR        begsr
      *
S03  C*                  IF        KUNGR(2) = 0
N03  C                   IF        KUNGR(2) = 0 OR XKUNEGEN = 'J'
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     wrtsection('KunEnKun')
     C                   ELSE
     C                   callp     wrtsection('Kunsltstr')
     C                   callp     updHtmlVar('WSKN1':'')
     C                   callp     updhtmlvar('WSKN2':'ALL')
     C                   callp     wrtsection('Kunsltrow')
     C     1             DO        30            XN                3 0
     C                   IF        KUNGR(XN) <> 0
     C                   MOVEL     KUNGR(XN)     KUNDNR
     C     CUNDL01K2     CHAIN     CUNDL01                            33
     C                   callp     updHtmlVar('WSKN1':
     C                             %trim(%editc(KUNDNR:'Z')))
     C                   callp     updhtmlvar('WSKN2':KNAVN)
     C                   callp     wrtsection('Kunsltrow')
     C                   END
     C                   ENDDO
      *
     C                   callp     wrtsection('Kunsltend')
     C                   END
      *
     C                   callp     wrtsection('KunSlutt')
      *
     C                   endsr
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspOT         begsr
      *
     C                   callp     updHtmlVar('WSOT1':'  ')
     C                   callp     updhtmlvar('WSOT2':'ALL')
     C                   callp     wrtsection('OTsltrow')
     C     1             DO        16            XN                3 0
     C                   IF        OT(XN) <> *BLANKS
     C                   MOVEL     OT(XN)        KO1KOD                   40
     C     KODTOTYK      CHAIN     KODTOTY                            33
     C                   callp     updHtmlVar('WSOT1':KO1KOD)
     C                   IF        *IN33
     C                   callp     updhtmlvar('WSOT2':'*UNKNOW')
     C                   ELSE
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('WSOT2':KO1ETX)
     C                   ELSE
     C                   callp     updhtmlvar('WSOT2':KO1NTX)
     C                   END
     C                   END
     C                   callp     wrtsection('OTsltrow')
     C                   END
     C                   ENDDO
      *
     C                   callp     wrtsection('OTsltend')
      *
     C                   endsr
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspPart       begsr
      *
     C                   callp     updHtmlVar('WSFRI1':'   ')
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('WSFRI1NV':'None')
     c                   else
     C                   callp     updhtmlvar('WSFRI1NV':'ingen')
     c                   end
     C                   callp     wrtsection('typsltrow')
N01   * Hvis definert som user-parameter tast både rekkefølge/tekst derfra
N01  C                   MOVEL     'FRISO'       PAID
N01  C     BRPARFK       setll     BRPARF
N01  C     BRPARFK       reade     BRPARF                                 33
N01  c     *in33         doweq     '0'
N01  C                   callp     updHtmlVar('WSFRI1':PAVRDA1_3)
N01  C                   callp     updhtmlvar('WSFRI1NV':PAVRDA5_50)
N01  C                   callp     wrtsection('typsltrow')
N01  c                   move      'J'           FriPFinnes        1
N01  C     BRPARFK       reade     BRPARF                                 33
N01  c                   enddo
N01  c     FripFinnes    cabeq     'J'           UtFriso
      *
N01   * IKKE definert som user-parameter, via alle frisok med Dok-kode A=På Inetrenett
     C     *loval        setll     kodfrilf
     C                   read      kodfrilf                               40
     C     *in40         doweq     '0'
     C     'A'           LOOKUP    DFS                                    33
     C     *IN33         IFEQ      '1'
     C                   callp     updHtmlVar('WSFRI1':KFSOKO)
     C                   callp     updhtmlvar('WSFRI1NV':KFSOTX)
     C                   callp     wrtsection('typsltrow')
     C                   END
     C                   read      kodfrilf                               40
     C                   enddo
      *
N01  C     UtFriso       tag
     C                   callp     wrtsection('typsltend')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYSO01'
     C                   CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   OPEN      CUNDL01
     C                   OPEN      kodfrilf
     C                   OPEN      KODTOTY
     C                   OPEN      GSSCGL02
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     CUNDL01K2     klist
     C                   kfld                    BIFIRM
     C                   kfld                    KUNDNR
     C     KODTOTYK      klist
     C                   kfld                    KO1UNI
     C                   kfld                    KO1KOD
     C                   MOVE      'OTY'         KO1UNI
     c                   move      *zeros        Idag
     c                   move      *zeros        Dato8N
     c                   move      'SUB'         AddSub
     C                   CALL      'SYGE20'
     C                   PARM                    AddSub
     C                   PARM                    Idag
     C                   PARM                    Dato8N
     C                   PARM                    DagerN
s04   * intern bruker er mest interessert i ANDRE begrep enn kundenr/f.o.m dato..
s04  c*    XINTERN       ifeq      'J'
s04  c*                  move      *zeros        DATO8N
s04  c*                  endif
     C                   MOVE      DATO8N        DATO8A
     C*                  END
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C                   open      BRPARF                               50
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVE      *BLANKS       OTSTRING
     C                   MOVEL     'HEURO'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   DOW       *IN33 = *OFF
     C                   MOVEL     PAVRDA        XAN02             2
     C                   IF        XAN02 = *BLANKS
     C                   MOVE      PAVRDA        OTSTRING
     C                   LEAVE
     C                   END
     C     BRPARFK       READE     BRPARF                                 33
     C                   ENDDO
N03   *
N03  C                   MOVEL     'HEKUN'       PAID
N03  C     BRPARFK       chain     BRPARF                             33
N03  C                   IF        (*IN33 OR
N03  C                             ((PAVRDA <> 'j') AND
N03  C                              (PAVRDA <> 'J')))
N03  C                   MOVE      'N'           XKUNEGEN          1
N03  C                   ELSE
N03  C                   MOVE      'J'           XKUNEGEN
N03  C                   END
      *
     C                   MOVE      *ZEROS        KUNGR
     C                   Z-ADD     1             XN                3 0
     C                   MOVE      BIKUNR        KUNGR(1)
     C                   MOVEL     'KUNGR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVEL     PAVRDA        CGCG
     C     CGCG          SETLL     GSSCGL02
     C     CGCG          READE     GSSCGL02                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        CGCNO <> BIKUNR
     C                   ADD       1             XN
     C                   MOVE      CGCNO         KUNGR(XN)
     c     XN            cabeq     30            UtKg
     C                   END
     C     CGCG          READE     GSSCGL02                               33
     C                   ENDDO
     C     UtKg          Tag
     C                   END
      *
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     CUNDL01
     C                   close     kodfrilf
     C                   close     KODTOTY
     C                   close     GSSCGL02
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
