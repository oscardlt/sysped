     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 6 20 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R STS01D01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                  1  2'Utlasting av tur '
     A                                      COLOR(WHT)
     A                                  3  2'Turnr:'
     A            WSPRO          8Y 0B  3 11DSPATR(UL)
     A                                      EDTCDE(4)
     A  30                                  COLOR(WHT)
     A N30                              5  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        18A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R STS01D02
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                      CA10(10 'Hent nye kolli-id')
     A                                      CA12(12 'AVSLUTT')
     A                                      CA20(20 'Kontroller på nytt')
     A            WSPRO          8Y 0O  1  2DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUBILN        10A  O  1 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2  2'Oppdrag:'
     A            STANOP         4Y 0O  2 11DSPATR(UL)
     A                                      EDTCDE(4)
     A            STANOK         4Y 0O  2 16DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2'Fraktbr:'
     A            WSANFB         4Y 0O  3 11DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSANFK         4Y 0O  3 16DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4  2'KolliId:'
     A            WSANKI         4Y 0O  4 11DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSANKK         4Y 0O  4 16DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSCLID        20A  B  5  1DSPATR(UL)
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A  30                                  COLOR(WHT)
     A N30                              6  2'F10=Nye F20=Ny ktr'
     A                                      COLOR(WHT)
     A  30        MSGTXT        18A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
