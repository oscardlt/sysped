      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJESARKF3 MODULE(*LIBL/TJESARKF3 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FHeadf     IF   E           K DISK    usropn
     Ffak8      IF   E           K DISK    usropn
     FKODTGE    IF   E           K DISK    usropn
     FKODTG2    IF   E           K DISK    usropn
     FKODSAM    IF   E           K DISK    usropn
     FBRPARF    IF   E           K disk    usropn
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      * Sammenslåing av F-linjer baserer seg på SAMME varekode/tekst ELLER sammensl.kode/tekst
     D FLI             S             85    DIM(100)
      *
      * Client input variables
     D user            s             10
     D FakNr           s              7
     D FakNrN          s              7  0
     D Kunr2           s              8
     D Kunr2N          s              8  0
     D Avd             s              4
     D AvdN            s              4  0
     D Opd             s              7
     D OpdN            s              7  0
     D SeIntern        s              1
      *
     D TOTFRI          s             11  2
     D TOTPLI          s             11  2
     D TOTN            s             11  2
     D TOTM            s             11  2
     D TOTB            s             11  2
     D MOMS            s             11  2
     D BelB            s             11  2
     D MKOD            s              1
     D VTXT            s             45
     D DREF            s             35
     D XN01            s              3  0
     D MaxXN           s              3  0
     D StrXN           s              3  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
     D                 DS
     D  VREF                   1     16
     D  HEAVD                  1      4  0
     D  HEAVDa                 1      4
     D  STREK1                 5      5
     D  HEOPD                  6     12  0
     D  HEOPDa                 6     12
     D  STREK2                13     13
     D  HESG                  14     16
     D                 DS
     D  FAVT                   1     20
     D    FAVT1                1      1
     D    FAVT2_20             2     20
     D                 DS
     D  GMDS                   1     85
     D    GM_SORT              1      3
     D    GM_SAMM              5      5
     D    GM_VT                7     47
     D    GM_MK               49     49
     D      GMDS_A             1     49
     D    GM_VAL              51     53
     D    GM_BELV             55     67  2
     D    GM_BELN             69     81  2
     D    GM_VK               83     85
     D                 DS
     D  NYDS                   1     85
     D    NY_SORT              1      3
     D    NY_SAMM              5      5
     D    NY_VT                7     47
     D    FAKDM               49     49
     D      NYDS_A             1     49
     D    FAVAL               51     53
     D    FABELV              55     67  2
     D    FABELN              69     81  2
     D    FAVK                83     85
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
     C  N50              exsr      Settop
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬faknr¬ : ¬'+%trim(%editc(FAKNRN:'Z'))+'¬, '
        +'¬kunr2¬ : ¬'+%trim(%editc(KUNR2N:'Z'))+'¬, '
        +'¬seintern¬ : ¬'+%trim(SEINTERN)+'¬, '
        +'¬besk¬ : ¬'+%trim(BIBESK)+'¬, '
        +'¬kunde¬ : ¬'+%trim(%editc(BIKUNR:'Z'))+'¬, '
        +'¬knavn¬ : ¬'+%trim(KNAVN)+'¬, '
        +'¬wsna¬ : ¬'+%trim(KNAVN)+'¬, '
        +'¬fakdt¬ : ¬'+%trim(%editc(FADATO:'Z'))+'¬, '
        +'¬fakdtff¬ : ¬'+%trim(%editc(FADAFF:'Z'))+'¬, '
        +'¬dref¬ : ¬'+%trim(DREF)+'¬, '
        +'¬vref¬ : ¬'+%trim(VREF)+'¬, '
        +'¬tur¬ : ¬'+%trim(%editc(HEPRO:'Z'))+'¬, '
        +'¬dato¬ : ¬'+%trim(%editc(HEDTOP:'Z'))+'¬, '
        +'¬henas¬ : ¬'+%trim(HENAS)+'¬, '
        +'¬heads1¬ : ¬'+%trim(HEADS1)+'¬, '
        +'¬heads2¬ : ¬'+%trim(HEADS2)+'¬, '
        +'¬heads3¬ : ¬'+%trim(HEADS3)+'¬, '
        +'¬henak¬ : ¬'+%trim(HENAK)+'¬, '
        +'¬headk1¬ : ¬'+%trim(HEADK1)+'¬, '
        +'¬headk2¬ : ¬'+%trim(HEADK2)+'¬, '
        +'¬headk3¬ : ¬'+%trim(HEADK3)+'¬, '
        +'¬kll¬ : ¬'+%trim(%editc(HENT:'Z'))+'¬, '
        +'¬vbesk¬ : ¬'+%trim(HEVS1)+'¬, '
        +'¬vkt¬ : ¬'+%trim(%editc(HEVKT:'Z'))+'¬, '
        +'¬m3¬ : ¬'+%trim(%editc(HEM3:'3'))+'¬, '
        +'¬lm¬ : ¬'+%trim(%editc(HELM:'3'))+'¬, '
        +'¬fvkt¬ : ¬'+%trim(%editc(HEFBV:'Z'))+'¬, '
        +'¬part¬ : ¬'+%trim(fask)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
      * HOVEDRUTINE
     C     Error         IFEQ      *blanks

     c                   eval      JSONstring ='"invlindet" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C     FAKKEY        SETLL     FAK8
     C     FAKKEY        READE     FAK8                                   33
     C     *in33         DOWEQ     *OFF
     c     FASK          cabeq     'I'           NEXTFA
     c     FAKDA         cabeq     'K'           NEXTFA
     c     FAKDA         cabeq     'D'           NEXTFA
     c     FAOPKO        cabeq     'D'           NEXTFA
     c     FAOPKO        cabeq     ' '           NEXTFA
     c     XINTERN       IFEQ      'J'
     C                   exsr      SortSamSR
     c                   ELSE
     C                   exsr      Setrow
     c                   ENDIF
     C                   MOVE      FASK          XXSK              1            (KEY FRITEKST/FRSOK)
     C     NEXTFA        TAG
     C     FAKKEY        READE     fak8                                   33
     c                   ENDDO
      *
      * Hent sortert/sammenslått faktura fra array...
     c     XINTERN       IFEQ      'J'
     c                   sorta     FLI
     c                   eval      strXn = 100 - MaxXn + 1                      5 lin: elem 96->
     C     strXN         DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     NYDS
     C                   exsr      Setrow
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF

     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     C                   ENDIF

     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
     C                   eval      FAKNR    = zhbgetvar('FAKNR')
     C                   eval      FAKNRN   = c2n2(FAKNR)
     C                   eval      KUNR2    = zhbgetvar('KUNR2')
     C                   eval      KUNR2N   = c2n2(KUNR2)
     C                   eval      AVD      = zhbgetvar('AVD')
     C                   eval      AVDN     = c2n2(AVD)
     C                   eval      OPD      = zhbgetvar('OPD')
     C                   eval      OPDN     = c2n2(OPD)
     C                   eval      user = zhbgetvar('user')
     C                   eval      seintern = zhbgetvar('SEINTERN')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
      *
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C  N50              CLOSE     cundl01
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     fak8
     C  N50              CLOSE     KODTGE
     C  N50              CLOSE     KODTG2
     C  N50              CLOSE     KODSAM
      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      * hvis *I*=Intern bruker - finn kunde via egen param (etter 20102007 parm XINTERN)
     c                   Move      BIBESK        TSTINTERN         3
     C     TSTINTERN     Ifeq      '*I*'
     C                   move      'J'           XINTERN
     C                   end
     C     XINTERN       Ifeq      'J'
     C     KUNR2n        Andne     *zeros
     c                   move      KUNR2N        BIKUNR
     C  N50KunKey        CHAIN     Cundl01                            33
     c                   end
      *
     C     FAKKEY        SETLL     fak8
     C     FAKKEY        READE     fak8                                   33
     C  N50              EVAL      *IN33=*ON
     C     *in33         DOWEQ     '0'
     C     FASK          cabeq     'I'           NEXTFA8
     C     FAKDA         cabeq     'K'           NEXTFA8
     C     FAKDA         cabeq     'D'           NEXTFA8
     C     FAOPKO        cabeq     'D'           NEXTFA8
     C     FAOPKO        cabeq     ' '           NEXTFA8
     C                   move      FAKUNR        BIKUNR
     C  N50KunKey        CHAIN     Cundl01                            33
     C                   LEAVE
     C     NEXTFA8       TAG
     C  N50FAKKEY        READE     fak8                                   33
     C                   ENDDO
     C                   EVAL      DREF = HERFA
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row   - NÅR INTERN/IKKE SAMMESLÅTT
      *=====================================================================
     C     Setrow        begsr
      * Ved kundeview-sortert/sammenslått så vises avvikende tekst.
     c     XINTERN       IFEQ      'J'
     C                   EVAL      VTXT = NY_VT
     c                   ELSE

     C     KGEKey        CHAIN     KODTGE                             33
     c     XSPRÅK        IFEQ      'EN'
     C   33              eval      KGEENT = '*Unknown'
     C                   eval      VTXT = KGEENT + FAVT
     c                   ELSE
     C   33              eval      KGENOT = '*Ukjent'
     C                   eval      VTXT = KGENOT + FAVT
     c                   ENDIF

     c                   ENDIF

     C     XINTERN       IFNE      'J'
     C                   MOVE      *BLANKS       FAVK
     C                   else
     c     XINTERN       IFNE      'J'
     c     FACD11        andne     ' '
     C                   eval      VTXT = %trim(VTXT)+'  ('+FACD11+')'
     C                   end
     C                   END
     C     FAKDM         IFEQ      'J'
     C                   ADD       FABELN        TOTPLI
     C     FABELN        MULT      0,25          MOMS
     c                   MOVE      '*'           MKOD
     c                   ELSE
     C                   ADD       FABELN        TOTFRI
     C                   MOVE      *ZEROS        MOMS
     c                   MOVE      ' '           MKOD
     c                   end
     C     FABELN        ADD       MOMS          BELB
      * akkumuler totaler
     C                   ADD       FABELN        TOTN
     C                   ADD       FABELN        TOTB
     C                   ADD       MOMS          TOTM
     C                   ADD       MOMS          TOTB

     C                   Add       1             AntLest

      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
        +'¬geb¬ : ¬'+%trim(FAVK)+'¬, '
        +'¬vtxt¬ : ¬'+%trim(VTXT)+'¬, '
        +'¬beln¬ : ¬'+%trim(%editc(FABELN:'J'))+'¬, '
        +'¬belm¬ : ¬'+%trim(%editc(MOMS:'J'))+'¬, '
        +'¬belb¬ : ¬'+%trim(%editc(BELB:'J'))+'¬, '
        +'¬mkod¬ : ¬'+%trim(MKOD)+'¬, '
        +'¬totn¬ : ¬'+%trim(%editc(TOTN:'J'))+'¬, '
        +'¬totm¬ : ¬'+%trim(%editc(TOTM:'J'))+'¬, '
        +'¬totb¬ : ¬'+%trim(%editc(TOTB:'J'))+'¬, '
        +'¬totpli¬ : ¬'+%trim(%editc(TOTPLI:'J'))+'¬, '
        +'¬totfri¬ : ¬'+%trim(%editc(TOTFRI:'J'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C                   endsr
      *
      ***********************************************************
     C     SortSamSR     begsr
      ***********************************************************
      *
      * sort/sammenslå
      * kriterium for sammenslåing  - IKKE FRITEKST
      *            A)  SAMME GEBYRKODE       / FRITEKST  / momskode,
      *  ELLER     B)  SAMME SAMMENSNL.KODE  / FRITEKST  / momskode,
      * SAMMENSLÅTTE LINJER KOMMER FØRST
      * DERETTER LINJER  MED SAM-KODE MEN SOM IKKES SLÅS SAMMEN PGA FRITEKST
      * DERETTER ANDRE LINJER BASERT PÅ SORT-KODE
      *
      * a) klargjør noen variable
      * varetekst
     C     KG2Key        CHAIN     KODTG2                             33
     C     KGEKey        CHAIN     KODTGE                             33
     c     XSPRÅK        ifeq      'EN'
     C                   eval      NY_VT = KGEENT+' '+FAVT
     c                   else
     C                   eval      NY_VT = KGENOT+' '+FAVT
     c                   end
     c     FAVT1         ifeq      'X'
     C                   eval      NY_VT =  FAVT2_20
     c                   end
      * Sort-nr/samkod(+evt tekst derfra.)
     C                   MOVE      KG2SRT        NY_SORT
     C                   MOVE      FACD11        NY_SAMM
     C     NY_SAMM       IFNE      *BLANKS
     C     FAVT          IFNE      *BLANKS
     C                   MOVE      '  1'         NY_SORT
     C                   MOVE      *BLANKS       NY_SAMM
     C                   else
     C                   MOVE      '  2'         NY_SORT
     C     NY_SAMM       CHAIN     KODSAM                             33
     c  N33XSPRÅK        ifeq      'EN'
     C                   eval      NY_VT = SAENGE
     c                   else
     C                   eval      NY_VT = SANORS
     c                   end
     C                   END
     C                   END
      *
      * b) slå sammen eller legg i første ledige
     C     1             DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     GMDS
     C     NYDS_A        IFEQ      GMDS_A
     C                   ADD       FABELV        GM_BELV
     C                   ADD       FABELN        GM_BELN
     C                   move      *blanks       GM_VK                          sammenslått
     C                   MOVE      GMDS          FLI(XN01)
     C                   LEAVE
     C                   END
     C                   ELSE
     C                   MOVE      NYDS          FLI(XN01)
     c                   z-add     XN01          MaxXN
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      Cundl01
     C  N50              OPEN      HEADF
     C  N50              OPEN      fak8
     C  N50              OPEN      KODTGE
     C  N50              OPEN      KODTG2
     C  N50              OPEN      KODSAM
     C  N50              OPEN      BRPARF
      *
     C     KGEKey        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C                   MOVE      'GE'          KGEUNI
     C     KG2Key        KLIST
     C                   KFLD                    KG2UNI
     C                   KFLD                    FAVK
     C                   MOVE      'G2'          KG2UNI
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     HeaKey        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C     FakKey        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    FAKNRN
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   EVAL      PABRID = BIBRID

     C  N50KunKey        CHAIN     Cundl01                            33
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     USER          tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
     C                   EVAL      STREK1  = '/'
     C                   EVAL      STREK2  = '/'
     C                   MOVE      *ZEROS        TOTFRI
     C                   MOVE      *ZEROS        TOTPLI
     C                   MOVE      *ZEROS        TOTN
     C                   MOVE      *ZEROS        TOTM
     C                   MOVE      *ZEROS        TOTB
      *
     C  N50HeaKey        CHAIN     HEADF                              33
     C   33              CLEAR                   HEADFR
      * finn bilagsdato mm.
     C  N50FakKey        CHAIN     fak8                               33
      *
     C                   endsr
      *
