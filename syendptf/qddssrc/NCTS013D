     A*%%TS  SD  20240618  082402  CB          REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXXA* 01 YBC PTF12 VIS ANTALL KOLLI OG OPPDRAG PR HOUSECONSIGNMENT
12307A* 02 CB  PTF12 Ikke krav til bestemmelseslandkode
      *              LAGT TIL MOTTAKER
XXXXX *              FJERNET MOTTAKER ETTER OVERGANGSPERIODEN
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR012PNL)
     A                                      HLPTITLE('REGISTRERING AV VAREPOSTE-
     A                                      R TET EKSPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5')
     A            TILI2          5Y 0O  4  5EDTCDE(4)
     A            TILIT          5Y 0H
     A            TICCH          6Y 0O  4 11EDTCDE(4)
     A            TICCCD         2A  H
     A            TICTX        120A  H
     A            TICTX30       30A  O  4 18
     A            TICCUS         9A  H
     A            TIVKB         13Y 3O  4 49EDTCDE(4)
     A            TIPTY          2A  O  4 64
     A            TIPNT          8Y 0O  4 67EDTCDE(4)
     A            TIDK           5A  O  4 76
     A            TIALK          2A  O  4 82
     A            TIBLK          2A  O  4 85
     A            TIPDTY         4A  O  4 88
     A            TIPDRF        70A  H
     A            TIPDRF_20     20A  O  4 93
     A            TIXEXT        35A  H
     A            TIXEXT_15     15A  O  4114
     A            TIPDGN         5Y 0H
     A            TIPDTP         2A  H
     A            TIPDAN         8Y 0H
     A            TIPDUN         4A  H
     A            TIPDPC        16Y 6H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 46SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS013R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS avgang Item'
     A                                      COLOR(WHT)
     A                                  1 51'Tdeknr.'
     A            UAVD           4Y 0O  1 59COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 64'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 66COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 74'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 76COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option:   2=Endre   4=Slett  -
     A                                       5=Vis'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option:   5=Vis'
     A                                      COLOR(BLU)
N01  A                                  2 80'Antall kolli:'
N01  A            WSTOTNT        5Y 0O  2 94COLOR(WHT)
N01  A                                      EDTCDE(4)
N01  A                                  2100'Antall oppdrag:'
N01  A            WSTOTOP        3Y 0O  2116COLOR(WHT)
N01  A                                      EDTCDE(4)
     A                                  3  2'Op Linje Varenr Varebeskrivelse.  -
     A                                      ............ Bruttovekt.... Enh. ko-
     A                                      lli. DeklT AL BL Tidligere dokument-
     A                                              Ekstern ref'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Tilbake')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              4  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til   F12=Forrige'
     A                                      COLOR(BLU)
     A  27                              4  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A*%%TS  SD  20240618  082402  CB          REL-V7R4M0  5770-WDS
     A                                      WINDOW(*DFT 21 100 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2CCH          6S 0H
     A            W2CTX        120A  H
     A            W2PMRK       120A  H
     A            W2AITX       120A  H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/NCTS013R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS avgang Item'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 71'-'
     A                                      COLOR(WHT)
     A            W2LI2          5Y 0O  1 73COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 79'-'
     A                                      COLOR(WHT)
     A            W2LIT          5Y 0O  1 81COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Dekl.ty'
     A            W2DK           5A  B  2 10TEXT('DEKL.TYPE')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 17'Av.land'
     A            W2ALK          2A  B  2 25TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 29'Be.land'
     A            W2BLK          2A  B  2 37TEXT('Bestemmelsesland')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 41'UCR-ref'
     A            W2UCR         35A  B  2 49TEXT('UCR-ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'Varenr.'
     A            W2CCHI         6A  B  3 10TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CCCD         2A  B  3 17TEXT('COMBINED NOMENCLATUR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'N' 'O')
     A                                  3 20'V.beskr.'
     A            W2CTX60       60A  B  3 29TEXT('Varebeskrivelse')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CCUS         9A  B  3 90TEXT('CUS kode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4  2'Br.vekt'
     A            W2VKB         13Y 3B  4 10TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4 25'Nt.vekt'
     A            W2VKN         13Y 3B  4 33TEXT('Nettovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4 48'Enhet'
     A            W2PTY          2A  B  4 54TEXT('Enhet')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 57'Kolli'
     A            W2PNT          8Y 0B  4 63TEXT('Antall')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4 72'Stykk'
     A            W2NTS         13Y 3B  4 78TEXT('Styall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5  2'Merking'
     A            W2PMRK70      70A  B  5 10TEXT('Merke&nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 83'UN-nr.'
     A            W2UNNR         4A  B  5 90TEXT('UN number')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'Containernr'
     A            W2CNR         17A  B  6 14TEXT('Containernr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 32'Seal'
     A            W2DFKD        20A  B  6 37TEXT('Seal')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'T.dok. '
     A            W2PDTY         4A  B  7 10TEXT('Tidligere dok.type')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PDRF        70A  B  7 16TEXT('Tidligere dok.ref.')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PDGN         5Y 0B  8 16TEXT('Goods item no')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2PDTP         2A  B  8 22TEXT('Pakketype')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PDAN         8Y 0B  8 25TEXT('Antall pakker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2PDUN         4A  B  8 34TEXT('Enhet')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PDPC        16Y 6B  8 39TEXT('Quantity')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2PDIN        35A  B  9 16TEXT('Tidligere dok.inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  2'S.dok. '
     A            W2SDTY         4A  B 10 10TEXT('Tilleggs dok.type')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2SDRF        70A  B 10 16TEXT('Tilleggs dok.ref.')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2SDIN        35A  B 11 16TEXT('Tilleggs dok.inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Tr.dok.'
     A            W2TDTY         4A  B 12 10TEXT('Transport dok.type')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TDRF        70A  B 12 16TEXT('Transport dok.ref.')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'T.ref.'
     A            W2ADTY         4A  B 13 10TEXT('Tilleggs ref.type')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ADRF        70A  B 13 16TEXT('Tilleggs ref.')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'T.inf.'
     A            W2AICD         5A  B 14 10TEXT('Tilleggs inf.kode')
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2AITX70      70A  B 14 16TEXT('Tilleggs inf.')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      COLOR(YLW)
S02  A*                                15  2'Tr.bet.'
S02  A*           W2TRCH         1A  B 15 10TEXT('Transportgebyr, bet.måte')
     A                                 14 87'Tr.bet.'
     A            W2TRCH         1A  B 14 95TEXT('Transportgebyr, bet.måte')
     A                                 20  2'F4=Søk   F12=Avbryt   F15=Tillegg -
     A                                      opplysning'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 21  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL3
N02  A                                      WINDOW(*DFT 21 100 *NOMSGLIN)
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/NCTS013R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS avgang Item'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 71'-'
     A                                      COLOR(WHT)
     A            TILI2          5Y 0O  1 73COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 79'-'
     A                                      COLOR(WHT)
     A            TILIT          5Y 0O  1 81COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Dekl.ty'
     A            TIDK           5A  O  2 10TEXT('DEKL.TYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 17'Av.land'
     A            TIALK          2A  O  2 25TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 29'Be.land'
     A            TIBLK          2A  O  2 37TEXT('Bestemmelsesland')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 41'UCR-ref'
     A            TIUCR         35A  O  2 49TEXT('UCR-ref')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Varenr.'
     A            TICCH          6Y 0O  3 10TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TICCCD         2A  O  3 17TEXT('COMBINED NOMENCLATUR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 20'V.beskr.'
     A            TICTX        120A  H
     A            TICTX60       60A  O  3 29TEXT('Varebeskrivelse')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TICCUS         9A  O  3 90TEXT('CUS kode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2'Br.vekt'
     A            TIVKB         13Y 3O  4 10TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 25'Nt.vekt'
     A            TIVKN         13Y 3O  4 33TEXT('Nettovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 48'Enhet'
     A            TIPTY          2A  O  4 54TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4 57'Kolli'
     A            TIPNT          8Y 0O  4 63TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 73'UN-nr.'
     A            TIUNNR         4A  O  4 80TEXT('UN number')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  2'Merking'
     A            TIPMRK70      70A  O  5 10TEXT('Merke&nr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIPMRK       120A  H
     A                                  6  2'Containernr'
     A            TICNR         17A  O  6 14TEXT('Containernr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 32'Seal'
     A            TIDFKD        20A  O  6 37TEXT('Seal')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'T.dok. '
     A            TIPDTY         4A  O  7 10TEXT('Tidligere dok.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIPDRF        70A  O  7 16TEXT('Tidligere dok.ref.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIPDGN         5Y 0O  8 16TEXT('Goods item no')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TIPDTP         2A  O  8 22TEXT('Pakketype')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIPDAN         8Y 0O  8 25TEXT('Antall pakker')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TIPDUN         4A  O  8 34TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIPDPC        16Y 6O  8 39TEXT('Quantity')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TIPDIN        35A  O  9 16TEXT('Tidligere dok.inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2'S.dok. '
     A            TISDTY         4A  O 10 10TEXT('Tilleggs dok.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TISDRF        70A  O 10 16TEXT('Tilleggs dok.ref.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TISDIN        35A  O 11 16TEXT('Tilleggs dok.inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Tr.dok.'
     A            TITDTY         4A  O 12 10TEXT('Transport dok.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TITDRF        70A  O 12 16TEXT('Transport dok.ref.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2'T.ref.'
     A            TIADTY         4A  O 13 10TEXT('Tilleggs ref.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIADRF        70A  O 13 16TEXT('Tilleggs ref.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2'T.inf.'
     A            TIAICD         5A  O 14 10TEXT('Tilleggs inf.kode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIAITX70      70A  O 14 16TEXT('Tilleggs inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TIAITX       120A  H
S02  A*                                15  2'Tr.bet.'
S02  A*           TITRCH         1A  O 15 10TEXT('Transportgebyr, bet.måte')
N02  A                                 14 87'Tr.bet.'
N02  A            TITRCH         1A  O 14 95TEXT('Transportgebyr, bet.måte')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
N02  A                                 20  2'F12=Forrige   F15=Tillegg opplysni-
N02  A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Tilbake')
     A                                  1  2'SYSPED/NCTS013R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS avgang Item'
     A                                      COLOR(WHT)
     A                                  1 51'Tdeknr.'
     A            UAVD           4Y 0O  1 59COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 64'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 66COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 74'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 76COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option:   2=Endre   4=Slett  -
     A                                       5=Vis'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option:   5=Vis'
     A                                      COLOR(BLU)
N01  A                                  2 80'Antall kolli:'
N01  A            WSTOTNT        5Y 0O  2 94COLOR(WHT)
N01  A                                      EDTCDE(4)
N01  A                                  2100'Antall oppdrag:'
N01  A            WSTOTOP        3Y 0O  2116COLOR(WHT)
N01  A                                      EDTCDE(4)
     A                                  3  2'Op Linje Varenr Varebeskrivelse.  -
     A                                      ............ Bruttovekt.... Enh. ko-
     A                                      lli. DeklT AL BL Tidligere dokument-
     A                                              Ekstern ref'
     A                                      COLOR(WHT)
     A N27                              4  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til   F12=Forrige'
     A                                      COLOR(BLU)
     A  27                              4  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hopp til postnr. -->'
     A            W3LI2          5S 0I  3 24DSPATR(UL)
     A                                  4  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  4  2
     A            TILI2          5Y 0O  4  5EDTCDE(4)
     A            TILIT          5Y 0H
     A            TICCH          6Y 0O  4 11EDTCDE(4)
     A            TICCCD         2A  H
     A            TICTX        120A  H
     A            TICTX30       30A  O  4 18
     A            TICCUS         9A  H
     A            TIVKB         13Y 3O  4 49EDTCDE(4)
     A            TIPTY          2A  O  4 64
     A            TIPNT          8Y 0O  4 67EDTCDE(4)
     A            TIDK           5A  O  4 76
     A            TIALK          2A  O  4 82
     A            TIBLK          2A  O  4 85
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS013R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS avgang Item'
     A                                      COLOR(WHT)
     A                                  1 51'Tdeknr.'
     A            UAVD           4Y 0O  1 59COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 64'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 66COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 74'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 76COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Velg option:   5=Vis'
     A                                      COLOR(BLU)
     A                                  3  2'Op Linje Varenr Varebeskrivelse.  -
     A                                      ............ Bruttovekt.... Enh. ko-
     A                                      lli. DeklT AL BL'
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
