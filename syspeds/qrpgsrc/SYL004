      *********************************************************************
      * ARBEID MED PLUKKLISTE (LINJE)
      * PROGRAM BEHANDLER INPUT FRA SYL004 OG VISER UTVALGT HEADLING
      *
CRTPG+*  CRTPGM SYSPED/SYL004 MODULE(*LIBL/SYL004 *LIBL/SYL003B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 OPPDATER LLAN2
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLLINE    IF A E           K DISK    USROPN
     FLLHEAD    UF   E           K DISK    USROPN
     FLLVARE01  IF   E           K DISK    USROPN
     FLLAGJS03  IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          s             10
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D i               s             10i 0
     D TB              S              1    DIM(7)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/SYL003B')
     D FEIL01          C                   CONST('Oppgitt artikkelnr finne-
     D                                      ikke.')
     D FEILEN01        C                   CONST('Article no. is not valid.')
     D FEIL02          C                   CONST('Antall stykk må oppgis.')
     D FEILEN02        C                   CONST('Pieces are not given.')
     D FEIL03          C                   CONST('Sjekk laveste salgsenhet-
     D                                     !!!')
     D FEILEN03        C                   CONST('Check lowest sales unit!!!')
     D FEIL04          C                   CONST('Du må vente. En annen br-
     D                                     uker holder på med å allokere f-
     D                                     ra dette varenummer.')
     D FEILEN04        C                   CONST('You have wait. This article -
     D                                     is allocked by another user just -
     D                                     now.')
     D FEIL05          C                   CONST('Antall uttak er større e-
     D                                     nn antall på lager.')
     D FEILEN05        C                   CONST('Antall uttak er større e-
     D                                     nn antall på lager.')
     D FEIL06          C                   CONST('Oppgitt artikkelnr er -
     D                                     inaktiv.')
     D FEILEN06        C                   CONST('Article is inactive.')
      *
     D r               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80
     D LHPNR           S              8  0
     D LHPNA           S             30
     D LHBNR           S             17
     D LHBNA           S             30
     D WSMN            S              7
     D BWSMN           S              7  0
     D WSANR           S             15
     D WSWNT0          S              7
     D BWSWNT0         S              7  0
     D VG              S              5    DIM(10)
     D VE              S              5    DIM(10)
     D                 DS
     D LLLOK                   1     25
     D LLLOK1                  1      5
     D LLLOK2                  5     25
     D                 DS
     D XXREF                   1    105
     D XXVN                    1      3
     D XXMN                    4     10
     D XXPNR                  11     18
     D XXPNA                  19     48
     D XXBNR                  49     65
     D XXBNA                  66     95
     D XXUSER                 96    105
     D                 DS
     D  PAVRDA                 1     50
     D  VV                     1     50    DIM(10)
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *------------------
      * Retrieve number of boats to be shown
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
      * Show the requested boats
     C                   IF        ItemCount = 0
     C                   eval      SernoC = zhbgetvar('xxref')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   ELSE
     C                   eval      i = 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   eval      WSUSER  = zhbgetvar('USER':i)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName':i)
     C                   END
     C                   MOVE      SERNOC        XXREF
     C                   MOVE      XXVN          LLVN
     C                   MOVE      XXMN          BWSMN
     C                   MOVE      XXPNR         LHPNR
     C                   MOVE      XXPNA         LHPNA
     C                   MOVE      XXBNR         LHBNR
     C                   MOVE      XXBNA         LHBNA
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C     BWSMN         CHAIN(N)  LLHEAD                             33
     C                   IF        LLVN = 0
     C                   MOVE      'J'           XNYLIN            1
     C                   EXSR      NYLINJE
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   MOVE      *BLANKS       WSANR
     C                   MOVE      *BLANKS       LVVB
     C                   MOVE      *ZEROS        LVSALD
     C                   MOVE      *BLANKS       WSWNT0
     C                   END
     C                   MOVE      LVSALD        WSSALD            7
     C                   CALLB     PGM2
     C                   PARM                    BWSMN
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFIRM
     C                   PARM                    BIFARG
     C                   PARM                    WSANR
     C                   PARM                    LVVB
     C                   PARM                    WSSALD
     C                   PARM                    WSWNT0
     C                   PARM                    FMELDNR
     C                   PARM                    FEILMELDING
     C                   ELSE
     C                   EXSR      ENDRING
     C                   END
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   IF        ((UINN = 'N') OR (XNYLIN <> 'J'))
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   END
      *
     C                   close     LLLINE
     C                   close     LLHEAD
     C                   close     LLVARE01
     C                   close     LLAGJS03
     C                   close     BRIDF
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSWNT0  = zhbgetvar('WSWNT0')
S01  C                   EVAL      BWSWNT0 = c2n2(WSWNT0)
     C                   eval      WSANR   = zhbgetvar('WSANR')
     C                   eval      WSANR   = uppify(WSANR)

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      LLLINE                               50
     C                   open      LLHEAD                               50
     C                   open      LLVARE01                             50
     C                   open      LLAGJS03                             50
     C                   open      BRPARF                               50
     C     LL00K         klist
     C                   kfld                    BWSMN
     C                   kfld                    LLVN
     C     LV01K         klist
     C                   kfld                    LHGRP
     C                   kfld                    LHPNR
     C                   kfld                    WSANR
     C     LV01KGRP      klist
     C                   kfld                    LHGRP
     C                   kfld                    LVPNRGRP
     C                   kfld                    WSANR
     C     LAS03K        klist
     C                   kfld                    LHGRP
     C                   kfld                    LVPNR
     C                   kfld                    WSANR
     c     LL00KSIST     klist
     c                   kfld                    LHMN
     c                   kfld                    LLVNSIST
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     *LIKE         DEFINE    LLVN          LLVNSIST
     C     *LIKE         DEFINE    LVPNR         LVPNRGRP
     C     *LIKE         DEFINE    LHGRP         UGRP
     C     *LIKE         DEFINE    LLWNT0        UWNT0
     C     *LIKE         DEFINE    LLWNT0        UWNTL
     C     *LIKE         DEFINE    LLWNT1        UWNT1
     C     *LIKE         DEFINE    LLWNT2        UWNT2
     C     *LIKE         DEFINE    LLWNT3        UWNT3
     C     *LIKE         DEFINE    LLWVEK        UWVEK
     C     *LIKE         DEFINE    LLWKUB        UWKUB
     C                   MOVE      *ZEROS        LVPNRGRP
     C                   MOVE      *BLANKS       FMELDNR           1
     C                   MOVE      *BLANKS       FEILMELDING
     c                   Z-ADD     999           LLVNSIST
      *
     C                   MOVE      *BLANKS       VG
      * VAREGRUPPER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = WSUSER
     C                   EVAL      PAKUNR = 0
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   EVAL      PAID   = 'LLVG'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   ELSE
      * VAREGRUPPER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   ELSE
      * VAREGRUPPER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      *BLANKS       VE
      * VAREEIER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = WSUSER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   ELSE
      * VAREEIER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   ELSE
      * VAREEIER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   END
     C                   END
     C                   END
      *
     C                   endsr
      *=====================================================================
      *  NY LINJE
      *=====================================================================
     C     NYLINJE       BEGSR
      * TEST INNGÅENDE DATA
     C                   EXSR      TESTB
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   EXSR      OPDLINJE
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
      * TEST ARTIKKELNR
     C     LV01K         CHAIN     LLVARE01                           33
     C   33LHGRP         IFNE      0
     C     LV01KGRP      CHAIN     LLVARE01                           33
     C                   END
     C  N33LVVKT0        COMP      0                                      33
     C  N33              IF        LVAKT = 'I'
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN06      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL06        FEILMELDING
     C                   END
     C                   MOVE      *ON           *IN33
     C                   END
      *
      * TEST VAREGRUPPE
     C  N33              IF        VG(1) <> *BLANKS
     C     1             DO        10            XN                3 0
     C                   IF        VG(XN) <> *BLANKS
     C     VG(XN)        IFEQ      LVAG01
     C     VG(XN)        OREQ      LVAG02
     C     VG(XN)        OREQ      LVAG03
     C     VG(XN)        OREQ      LVAG04
     C     VG(XN)        OREQ      LVAG05
     C                   GOTO      STTESTB11
     C                   END
     C                   END
     C                   ENDDO
     C                   MOVE      '1'           *IN33
     C                   END
      *
      * TEST VAREEIER
     C     STTESTB11     TAG
     C  N33              IF        VE(1) <> *BLANKS
     C     1             DO        10            XN                3 0
     C                   IF        VE(XN) <> *BLANKS
     C     VE(XN)        IFEQ      LVEIER
     C                   GOTO      STTESTB12
     C                   END
     C                   END
     C                   ENDDO
     C                   MOVE      '1'           *IN33
     C                   END
      *
     C     STTESTB12     TAG
     C     *IN33         IFEQ      '1'
     C                   IF        FEILMELDING = *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN01      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL01        FEILMELDING
     C                   END
     C                   MOVE      *BLANKS       LVVB
     C                   END
     C                   MOVE      '1'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
      * TEST ANTALL STYKK
     C     BWSWNT0       IFEQ      0
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN02      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL02        FEILMELDING
     C                   END
     C                   MOVE      '2'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
      *
      * TEST OM ANTALL UTTAK FINNES PÅ LAGER
     C                   MOVE      *ZEROS        XXSALD            7 0
     C                   MOVE      *ZEROS        XXNTLK            3 0
     C     XXSALD        IFLT      BWSWNT0
     C     LAS03K        SETLL     LLAGJS03
     C     STTESTB1      TAG
     C     LAS03K        READE     LLAGJS03                               30
     C     *IN30         IFEQ      '0'
     C     VADATO        IFNE      99999999
      *
      * FOR Å HINDRE FEIL (BRUKER ENDRER REGLER PÅ ARTIKKEL MED IKKE
      * PÅ LAGERBEHOLDING) BEREGNER PROGRAMMET SALDO PÅ NYTT OG VELG
      * DEN LAVSTE AV DE TO SALDOENE.
     C                   Z-ADD     VASAE0        XNYS1             7 0
     C     VASAE1        MULT      VAAN01        XNYS2             7 0
     C                   ADD       XNYS2         XNYS1
     C     VASAE2        MULT      VAAN02        XNYS2
     C                   ADD       XNYS2         XNYS1
     C     VASAE3        MULT      VAAN03        XNYS2
     C                   ADD       XNYS2         XNYS1
     C     XNYS1         IFLT      VASALD
     C                   ADD       XNYS1         XXSALD
     C                   ADD       1             XXNTLK
     C                   ELSE
     C                   ADD       VASALD        XXSALD
     C                   ADD       1             XXNTLK
     C                   END
      *
     C                   END
     C     XXSALD        CABLT     BWSWNT0       STTESTB1
     C                   END
     C     *IN30         IFEQ      '1'
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN05      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL05        FEILMELDING
     C                   END
     C                   MOVE      '5'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
     C                   END
      *
     C     SLTESTB       ENDSR
      *
      *=====================================================================
      *  Oppdater linje
      *=====================================================================
     C     OPDLINJE      begsr
      *
     C     LL00KSIST     SETLL     LLLINE
     C     LHMN          READPE    LLLINE                                 33
     C     *IN33         IFEQ      '0'
     C                   ADD       1             LLVN
     C                   ELSE
     C                   Z-ADD     1             LLVN
     C                   MOVE      LHMN          LLMN
     C                   ENDIF
     C                   MOVE      *ZEROS        XLLWNT0           7 0
     C                   MOVE      *ZEROS        XLLWNTL           7 0
     C                   MOVE      *ZEROS        XLLWNT1           5 0
     C                   MOVE      *ZEROS        XLLWNT2           5 0
     C                   MOVE      *ZEROS        XLLWNT3           5 0
     C                   MOVE      *ZEROS        XLLWVEK           7 2
     C                   MOVE      *ZEROS        XLLWKUB           7 4
     C                   MOVE      *ZEROS        LLWNT0
     C                   MOVE      *BLANKS       LLANR
N01  C                   MOVE      *BLANKS       LLAN2

     C                   EXSR      MOVWSLL
      *
     C                   endsr
      *=====================================================================
      *  OPPDATER FELTER I FIL
      *=====================================================================
     C     MOVWSLL       begsr
     C                   MOVE      *ZEROS        UFLAGG            1 0
      *
     C     *LIKE         DEFINE    LLLOK         ULOK
     C     *LIKE         DEFINE    LLSER         USER
     C                   MOVE      *BLANKS       ULOK
     C                   MOVE      *BLANKS       USER
     C                   MOVE      BWSWNT0       UWNT0
      *
     C                   Z-ADD     9             UFLAGG
     C                   CALL      'LL022R'
     C                   PARM      LHGRP         UGRP
     C                   PARM                    LVPNR
     C                   PARM                    WSANR
     C                   PARM                    USER
     C                   PARM                    UPRDT             8
     C                   PARM                    LLMN
     C                   PARM                    LLVN
     C                   PARM                    UWNT0
     C                   PARM                    UWNTL
     C                   PARM                    UWNT1
     C                   PARM                    UWNT2
     C                   PARM                    UWNT3
     C                   PARM                    UWVEK
     C                   PARM                    UWKUB
     C                   PARM                    ULOK
     C                   PARM                    UNTLK             3 0
     C                   PARM                    UFLAGG
      *
     C     UFLAGG        CABEQ     1             SLMWL
     C     UFLAGG        IFEQ      3
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN02      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL02        FEILMELDING
     C                   END
     C                   GOTO      SLMWL
     C                   END
     C     UFLAGG        IFEQ      9
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN03      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL03        FEILMELDING
     C                   END
     C                   GOTO      SLMWL
     C                   END
      *
     C                   MOVE      WSANR         LLANR
N01  C                   MOVE      LVANRL        LLAN2
     C                   MOVE      LVVB          LLVB
     C                   MOVE      ULOK          LLLOK
     C                   MOVE      UNTLK         LLNTLK
     C                   MOVE      UWNT0         LLWNT0
     C                   MOVE      UWNTL         LLWNTL
     C                   MOVE      UWNT1         LLWNT1
     C                   MOVE      UWNT2         LLWNT2
     C                   MOVE      UWNT3         LLWNT3
     C                   MOVE      UWVEK         LLWVEK
     C                   MOVE      UWKUB         LLWKUB
      *
     C     LHMN          CHAIN     LLHEAD                             33
     C     LLVN          IFEQ      1
     C                   ADD       1             LHTLIN
     C                   ELSE
     C                   SUB       XLLWNT0       LHANT0
     C                   SUB       XLLWNTL       LHANTL
     C                   SUB       XLLWNT1       LHANT1
     C                   SUB       XLLWNT2       LHANT2
     C                   SUB       XLLWNT3       LHANT3
     C                   SUB       XLLWVEK       LHWVEK
     C                   SUB       XLLWKUB       LHWKUB
     C                   END
     C                   ADD       LLWNT0        LHANT0
     C                   ADD       LLWNTL        LHANTL
     C                   ADD       LLWNT1        LHANT1
     C                   ADD       LLWNT2        LHANT2
     C                   ADD       LLWNT3        LHANT3
     C                   ADD       LLWVEK        LHWVEK
     C                   ADD       LLWKUB        LHWKUB
     C                   UPDATE    LLHEADR
      *
     C                   WRITE     LLLINER
      *
     C     SLMWL         ENDSR
      *
      *
      *=====================================================================
      *  ENDRE LINJE
      *=====================================================================
     C     ENDRING       BEGSR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')

     C     LL00K         CHAIN     LLLINE                             60
     C   60              CLEAR                   LLLINER
     C                   exsr      SetFound
     C                   callp     wrtsection('found')
      *
     C                   ENDSR
      *
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * Protocol
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C     BWSMN         CHAIN     LLHEAD                             33
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editC(LHGRP:'Z')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editC(LHPNR:'Z')))
      *
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
      * Sett inn i HTMLvariabler
     C                   callp     updhtmlvar('LHMN':
     C                             %trim(%editc(BWSMN:'Z')))
     C                   callp     updhtmlvar('LLVN':
     C                             %trim(%editc(LLVN:'Z')))
     C                   callp     updhtmlvar('LHPNR':
     C                             %trim(%editc(LHPNR:'Z')))
     C                   callp     updHtmlVar('KNAVN':LHPNA)
     C                   callp     updHtmlVar('LHBNR':LHBNR)
     C                   callp     updHtmlVar('LHBNA':LHBNA)
     C                   callp     updHtmlVar('WSANR':LLANR)
     C                   callp     updhtmlvar('WSWNT0':
     C                             %trim(%editC(LLWNT0:'Z')))
     C                   callp     updhtmlvar('WSVB':LLVB)
     C                   callp     updhtmlvar('FEILMELDING':spaces)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton member before its read into memory
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL005'
     C                   CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
