      *
      *****************************************************************
      *** PROGRAM FOR HURTIGBOOKING / NOTISBLOKK                    ***
      *****************************************************************
      *
     H DATEDIT(*YMD)
     FKODTSF    IF   E           K DISK
     FHUBOF     UF   E             DISK
     FHUBOL01   IF A E           K DISK
     F                                     RENAME(HUBOR:HUBO1)
     F                                     INFDS(HUBODS)
     FSYHU10FM  CF   E             WORKSTN
     D LDADS          UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
      *___________________________
      * Finner recordnummer
      *"""""""""""""""""""""""""""
     D HUBODS          DS
     D  RECNO1               397    400B 0
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZZUSER               254    263
      *_________________________________________________________
      * Deinerer felter fra skjermbildet for lett move til array
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D DSA1            DS
     D  OPDG1                  1     10
     D  AM1                   11     11
     D  FRA1                  12     19
     D  FDATO1                20     25  0
     D  TIL1                  26     33
     D  TDATO1                34     39  0
     D  ANT1                  40     43  0
     D  VAREB1                44     68
     D  M31                   69     75  3
     D  LM1                   76     79  2
     D  VEKT1                 80     86  1
     D  FRI11                 87    116
     D  FRI21                117    146
     D  FRI31                147    176
     D  FRI41                177    206
     D  DATE1                207    212  0
     D  TIME1                213    218  0
     D  RECN1                219    228  0
     D  TABNR1               229    232  0
     D DSA2            DS
     D  OPDG2                  1     10
     D  AM2                   11     11
     D  FRA2                  12     19
     D  FDATO2                20     25  0
     D  TIL2                  26     33
     D  TDATO2                34     39  0
     D  ANT2                  40     43  0
     D  VAREB2                44     68
     D  M32                   69     75  3
     D  LM2                   76     79  2
     D  VEKT2                 80     86  1
     D  FRI12                 87    116
     D  FRI22                117    146
     D  FRI32                147    176
     D  FRI42                177    206
     D  DATE2                207    212  0
     D  TIME2                213    218  0
     D  RECN2                219    228  0
     D  TABNR2               229    232  0
     D DSA3            DS
     D  OPDG3                  1     10
     D  AM3                   11     11
     D  FRA3                  12     19
     D  FDATO3                20     25  0
     D  TIL3                  26     33
     D  TDATO3                34     39  0
     D  ANT3                  40     43  0
     D  VAREB3                44     68
     D  M33                   69     75  3
     D  LM3                   76     79  2
     D  VEKT3                 80     86  1
     D  FRI13                 87    116
     D  FRI23                117    146
     D  FRI33                147    176
     D  FRI43                177    206
     D  DATE3                207    212  0
     D  TIME3                213    218  0
     D  RECN3                219    228  0
     D  TABNR3               229    232  0
     D DSA4            DS
     D  OPDG4                  1     10
     D  AM4                   11     11
     D  FRA4                  12     19
     D  FDATO4                20     25  0
     D  TIL4                  26     33
     D  TDATO4                34     39  0
     D  ANT4                  40     43  0
     D  VAREB4                44     68
     D  M34                   69     75  3
     D  LM4                   76     79  2
     D  VEKT4                 80     86  1
     D  FRI14                 87    116
     D  FRI24                117    146
     D  FRI34                147    176
     D  FRI44                177    206
     D  DATE4                207    212  0
     D  TIME4                213    218  0
     D  RECN4                219    228  0
     D  TABNR4               229    232  0
      *_________________________________________________________
      * Generell struktur som brukes til fellesbehandling
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D ADS             DS
     D  OPDGA                  1     10
     D  AMA                   11     11
     D  FRAA                  12     19
     D  FDATOA                20     25  0
     D  FDATAD                20     21  0
     D  FDATAM                22     23  0
     D  FDATAÅ                24     25  0
     D  TILA                  26     33
     D  TDATOA                34     39  0
     D  TDATAD                34     35  0
     D  TDATAM                36     37  0
     D  TDATAÅ                38     39  0
     D  ANTA                  40     43  0
     D  VAREBA                44     68
     D  M3A                   69     75  3
     D  LMA                   76     79  2
     D  VEKTA                 80     86  1
     D  FRI1A                 87    116
     D  FRI2A                117    146
     D  FRI3A                147    176
     D  FRI4A                177    206
     D  DATEA                207    212  0
     D  TIMEA                213    218  0
     D  RECNA                219    228  0
     D  TABNRA               229    232  0
     D                 DS
     D  HUFDAT                 1      8  0
     D  HUFDTÅ                 1      4  0
     D  HUFDTM                 5      6  0
     D  HUFDTD                 7      8  0
     D                 DS
     D  HUTDAT                 1      8  0
     D  HUTDTÅ                 1      4  0
     D  HUTDTM                 5      6  0
     D  HUTDTD                 7      8  0
     D                 DS
     D  HUDATE                 1      8  0
     D  HUDAÅÅ                 1      4  0
     D  HUDAMM                 5      6  0
     D  HUDADD                 7      8  0
      *****************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    OPDKOD            1
     C                   PARM                    PDATA           218
     C     OPDKOD        COMP      'J'                                    52
      *****************************************************************
      * Initiering setter bruker til de tre siste poster hver gang
     C     START         TAG
     C                   EXSR      SRA
     C     *IN03         CABEQ     *ON           AVSLUT
      *
     C     VIS01         TAG
     C                   Z-ADD     0             DSOPT1
     C                   Z-ADD     0             DSOPT2
     C                   EXFMT     BILDE01
     C     *IN03         IFEQ      *ON
     C                   GOTO      AVSLUT
     C                   ENDIF
     C                   MOVEA     '0000'        *IN(41)
      *
      * Ber om vindu for avvikende USERID
     C     *IN10         IFEQ      *ON
     C                   GOTO      START
     C                   ENDIF
      *
      * Sjekker om bruker prøver å rulle frem eller tilbake
      * Obs. All rulling er bare tre poster frem og tilbake
      *
     C     *IN07         IFEQ      *ON                                          <BACKWARDS
     C     RECN3         IFNE      *ZEROS
     C     RECN3         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA3          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C                   ENDIF
     C     RECN2         IFNE      *ZEROS
     C     RECN2         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA2          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C                   ENDIF
     C     RECN1         IFNE      *ZEROS
     C     RECN1         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA1          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C                   ENDIF
     C     *IN35         IFEQ      *OFF
     C     *IN90         IFEQ      *ON
     C     ZUSER         SETGT     HUBO1
     C     ZUSER         READPE    HUBO1                                  90
     C                   ENDIF
     C     ZUSER         READPE    HUBO1                                  90
     C     ZUSER         READPE    HUBO1                                  90
     C                   ENDIF
     C     ZUSER         READPE    HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA3
     C                   ELSE
     C                   EXSR      SRF
     C                   GOTO      VIS01
     C                   ENDIF
     C     ZUSER         READPE    HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA2
     C                   ELSE
     C                   EXSR      SRF
     C                   GOTO      VIS01
     C                   ENDIF
     C     ZUSER         READPE    HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA1
     C                   MOVE      *ON           *IN36
     C                   ELSE
     C                   EXSR      SRF
     C                   GOTO      VIS01
     C                   ENDIF
     C                   MOVE      *ON           *IN35
     C                   GOTO      VIS01
     C                   ENDIF
      *
     C                   MOVE      *OFF          *IN35
      *
      * Rulling fremover
      *
     C     *IN08         IFEQ      *ON                                          <FORWARDS
     C     RECN1         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA1          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C     RECN2         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA2          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C     RECN3         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA3          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C     *IN36         IFEQ      *ON
     C     ZUSER         READE     HUBO1                                  90
     C     ZUSER         READE     HUBO1                                  90
     C                   MOVE      *OFF          *IN36
     C                   ENDIF
     C     ZUSER         READE     HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA1
     C                   ELSE
     C                   EXSR      SRE
     C                   GOTO      VIS01
     C                   ENDIF
     C     ZUSER         READE     HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA2
     C                   ELSE
     C                   EXSR      SRE
     C                   GOTO      VIS01
     C                   ENDIF
     C     ZUSER         READE     HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA3
     C                   ELSE
     C                   EXSR      SRE
     C                   GOTO      VIS01
     C                   ENDIF
     C                   GOTO      VIS01
     C                   ENDIF
      *
     C                   MOVE      *OFF          *IN36
      *
     C     *IN17         IFEQ      *ON                                          <START
     C                   EXSR      SRF
     C                   GOTO      VIS01
     C                   ENDIF
     C     *IN18         IFEQ      *ON                                          <SLUTT
     C                   EXSR      SRE
     C                   GOTO      VIS01
     C                   ENDIF
      *
      * Ved Enter sjekkes det om bruker har gjort valg
      *
     C     OPDKOD        IFEQ      'J'
     C                   SELECT
     C     DSOPT1        WHENEQ    1
     C     RECN1         IFNE      *ZEROS
     C                   MOVEL     DSA1          PDATA
     C     RECN1         CHAIN     HUBOR                              90
     C                   DELETE    HUBOR                                90
     C                   EXSR      SRE
     C                   Z-ADD     0             DSOPT1
     C                   GOTO      AVSLUT
     C                   ENDIF
     C     DSOPT2        WHENEQ    1
     C     RECN2         IFNE      *ZEROS
     C                   MOVEL     DSA2          PDATA
     C     RECN2         CHAIN     HUBOR                              90
     C                   DELETE    HUBOR                                90
     C                   EXSR      SRE
     C                   Z-ADD     0             DSOPT2
     C                   GOTO      AVSLUT
     C                   ENDIF
     C     DSOPT3        WHENEQ    1
     C     RECN3         IFNE      *ZEROS
     C                   MOVEL     DSA3          PDATA
     C     RECN3         CHAIN     HUBOR                              90
     C                   DELETE    HUBOR                                90
     C                   EXSR      SRE
     C                   Z-ADD     0             DSOPT3
     C                   GOTO      AVSLUT
     C                   ENDIF
     C                   ENDSL
     C                   ENDIF
     C     DSOPT1        IFEQ      4
     C     RECN1         CHAIN     HUBOR                              90
     C                   DELETE    HUBOR                                90
     C                   ENDIF
     C     DSOPT2        IFEQ      4
     C     RECN2         CHAIN     HUBOR                              90
     C                   DELETE    HUBOR                                90
     C                   ENDIF
     C     DSOPT3        IFEQ      4
     C     RECN3         CHAIN     HUBOR                              90
     C                   DELETE    HUBOR                                90
     C                   ENDIF
     C     DSOPT1        IFEQ      4
     C     DSOPT2        OREQ      4
     C     DSOPT3        OREQ      4
     C                   EXSR      SRE
     C                   Z-ADD     0             DSOPT1
     C                   Z-ADD     0             DSOPT2
     C                   Z-ADD     0             DSOPT3
     C                   GOTO      VIS01
     C                   ENDIF
     C                   Z-ADD     0             DSOPT1
     C                   Z-ADD     0             DSOPT2
     C                   Z-ADD     0             DSOPT3
      *
      * Oppdaterer postene til fil
      *
     C     RECN1         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA1          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C     RECN2         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA2          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
     C     RECN3         CHAIN     HUBOR                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     DSA3          ADS
     C                   EXSR      SRC
     C                   UPDATE    HUBOR
     C                   ENDIF
      * Sjekker om det finnes data å skrive til fil
     C                   MOVEL     DSA4          ADS
     C                   EXSR      SRB
     C     *IN33         IFEQ      *ON                                          <DATA FINNES
     C                   MOVE      *ON           *IN34
     C                   EXSR      NYPOST
     C                   ENDIF                                                  <*IN33
      * Etter oppdatering vises alltid de tre siste poster
     C                   MOVE      *ON           *IN44
     C     *IN34         IFEQ      *ON
     C                   EXSR      SRE
     C                   ENDIF
     C                   MOVE      *OFF          *IN34
     C                   GOTO      VIS01
      *
     C     AVSLUT        TAG
     C                   UNLOCK    *DTAARA
     C                   RETURN
      *________________________________________________________________
      * SRA initierer bilde med de to siste poster
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C     *DTAARA       DEFINE    *LDA          LDADS
     C                   MOVE      *OFF          *IN03
     C                   Z-ADD     0             RECN1
     C                   Z-ADD     0             RECN2
     C                   Z-ADD     0             RECN3
     C                   Z-ADD     0             DSOPT1
     C                   Z-ADD     0             DSOPT2
     C                   Z-ADD     0             DSOPT3
     C                   Z-ADD     0             DSOPT4
     C                   CLEAR                   ADS
     C     *IN10         IFEQ      '0'
     C                   MOVE      ZZUSER        ZUSER            10
     C                   ELSE
     C                   EXFMT     WINDOW2
     C                   END
     C     ZUSER         COMP      ZZUSER                             5050
     C                   EXSR      SRE
      *
      * Sjekker at signatur finnes i LDA
     C     USG           IFEQ      *BLANKS
     C                   WRITE     BILDE01
     C     SRA10         TAG
     C                   EXFMT     WINDOW1
     C     *IN03         IFEQ      *ON
     C                   GOTO      SRA90
     C                   ENDIF
     C                   MOVE      *OFF          *IN51
     C     WSSIGN        IFEQ      *BLANKS
     C                   GOTO      SRA10
     C                   ENDIF
     C     KODKEY        KLIST
     C                   KFLD                    WSUNIK
     C                   KFLD                    WSSIGN
     C                   MOVEL     'SF'          WSUNIK            2
     C     KODKEY        SETLL     KODTSF                                 90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      *ON           *IN51
     C                   GOTO      SRA10
     C                   ELSE
     C                   MOVE      *OFF          *IN51
     C                   MOVEL     WSSIGN        USG
     C                   ENDIF
     C                   ENDIF
     C     SRA90         TAG
      *
     C                   ENDSR
      *________________________________________________________________
      * SRB Sjekker om det finnes data å skrive til fil
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRB           BEGSR
      *
     C     OPDGA         IFNE      *BLANKS
     C     AMA           ORNE      *BLANKS
     C     FRAA          ORNE      *BLANKS
     C     FDATOA        ORNE      *ZEROS
     C     TILA          ORNE      *BLANKS
     C     TDATOA        ORNE      *ZEROS
     C     ANTA          ORNE      *ZEROS
     C     VAREBA        ORNE      *BLANKS
     C     M3A           ORNE      *ZEROS
     C     LMA           ORNE      *ZEROS
     C     VEKTA         ORNE      *ZEROS
     C     FRI1A         ORNE      *BLANKS
     C     FRI2A         ORNE      *BLANKS
     C     FRI3A         ORNE      *BLANKS
     C     FRI4A         ORNE      *BLANKS
     C                   MOVE      *ON           *IN33
     C                   ELSE
     C                   MOVE      *OFF          *IN33
     C                   ENDIF
      *
     C                   ENDSR
      *________________________________________________________________
      * SRC Flytter fra ADS til filfelter
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
      *
     C                   MOVEL     OPDGA         HUOPDG
     C                   MOVEL     AMA           HUAM
     C                   MOVEL     FRAA          HUFRA
     C     FDATOA        IFEQ      0
     C                   MOVE      *ZEROS        HUFDAT
     C                   ELSE
     C                   MOVE      FDATAD        HUFDTD
     C                   MOVE      FDATAM        HUFDTM
     C     FDATAÅ        IFGT      50
     C     FDATAÅ        ADD       1900          HUFDTÅ
     C                   ELSE
     C     FDATAÅ        ADD       2000          HUFDTÅ
     C                   END
     C                   END
     C                   MOVEL     TILA          HUTIL
     C     TDATOA        IFEQ      0
     C                   MOVE      *ZEROS        HUTDAT
     C                   ELSE
     C                   MOVE      TDATAD        HUTDTD
     C                   MOVE      TDATAM        HUTDTM
     C     TDATAÅ        IFGT      50
     C     TDATAÅ        ADD       1900          HUTDTÅ
     C                   ELSE
     C     TDATAÅ        ADD       2000          HUTDTÅ
     C                   END
     C                   END
     C                   Z-ADD     ANTA          HUANT
     C                   MOVEL     VAREBA        HUVARE
     C                   Z-ADD     M3A           HUM3
     C                   Z-ADD     LMA           HULM
     C                   Z-ADD     VEKTA         HUVEKT
     C                   MOVEL     FRI1A         HUFRI1
     C                   MOVEL     FRI2A         HUFRI2
     C                   MOVEL     FRI3A         HUFRI3
     C                   MOVEL     FRI4A         HUFRI4
      *
     C                   ENDSR
      *________________________________________________________________
      * SRD Flytter fra filfelter til ADS
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
      *
     C                   MOVEL     HUOPDG        OPDGA
     C                   MOVEL     HUAM          AMA
     C                   MOVEL     HUFRA         FRAA
     C                   MOVE      HUFDTD        FDATAD
     C                   MOVE      HUFDTM        FDATAM
     C                   MOVE      HUFDTÅ        FDATAÅ
     C                   MOVEL     HUTIL         TILA
     C                   MOVE      HUTDTD        TDATAD
     C                   MOVE      HUTDTM        TDATAM
     C                   MOVE      HUTDTÅ        TDATAÅ
     C                   Z-ADD     HUANT         ANTA
     C                   MOVEL     HUVARE        VAREBA
     C                   Z-ADD     HUM3          M3A
     C                   Z-ADD     HULM          LMA
     C                   Z-ADD     HUVEKT        VEKTA
     C                   MOVEL     HUFRI1        FRI1A
     C                   MOVEL     HUFRI2        FRI2A
     C                   MOVEL     HUFRI3        FRI3A
     C                   MOVEL     HUFRI4        FRI4A
     C                   MOVE      HUDATE        DATEA
     C                   Z-ADD     HUTIME        TIMEA
     C                   Z-ADD     RECNO1        RECNA
      *
     C                   ENDSR
      *________________________________________________________________
      * NYPOST Skriver nye poster
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     NYPOST        BEGSR
     C                   EXSR      SRC
     C                   TIME                    HUTIME
     C                   MOVE      UDAY          HUDADD
     C                   MOVE      UMONTH        HUDAMM
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          HUDAÅÅ
     C                   ELSE
     C     UYEAR         ADD       2000          HUDAÅÅ
     C                   END
     C                   MOVE      *BLANK        HUSTAT
     C                   Z-ADD     0             HUAVD
     C                   Z-ADD     0             HUOPD
     C     USG           IFNE      *BLANKS
     C                   MOVEL     USG           HUSIGN
     C                   ELSE
     C                   MOVEL     ZUSER         HUUSER
     C                   ENDIF
     C                   MOVEL     ZUSER         HUUSER
     C                   WRITE     HUBO1
      *
     C                   ENDSR
      *________________________________________________________________
      * SRE    Posisjoner til slutt på filen.
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRE           BEGSR
     C                   CLEAR                   DSA1
     C                   CLEAR                   DSA2
     C                   CLEAR                   DSA3
     C                   CLEAR                   DSA4
     C                   CLEAR                   ADS
     C     ZUSER         SETGT     HUBO1
     C     ZUSER         READPE    HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA3
     C                   ELSE
     C                   GOTO      SREEND
     C                   ENDIF
     C     ZUSER         READPE    HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA2
     C                   ELSE
     C                   MOVEL     ADS           DSA1
     C                   CLEAR                   DSA2
     C                   CLEAR                   DSA3
     C                   CLEAR                   DSA4
     C                   GOTO      SREEND
     C                   ENDIF
     C     ZUSER         READPE    HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA1
     C                   ELSE
     C                   MOVEL     ADS           DSA1
     C                   MOVEL     DSA3          DSA2
     C                   CLEAR                   DSA3
     C                   CLEAR                   DSA4
     C                   GOTO      SREEND
     C                   ENDIF
     C     SREEND        TAG
     C                   MOVE      *ON           *IN44
     C                   MOVE      *ON           *IN35
     C                   ENDSR
      *________________________________________________________________
      * SRF    Posisjoner til start på filen.
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRF           BEGSR
     C                   CLEAR                   DSA1
     C                   CLEAR                   DSA2
     C                   CLEAR                   DSA3
     C                   CLEAR                   DSA4
     C                   CLEAR                   ADS
     C     ZUSER         SETLL     HUBO1
     C     ZUSER         READE     HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA1
     C                   ELSE
     C                   GOTO      SRFEND
     C                   ENDIF
     C     ZUSER         READE     HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA2
     C                   ELSE
     C                   GOTO      SRFEND
     C                   ENDIF
     C     ZUSER         READE     HUBO1                                  90
     C     *IN90         IFEQ      *OFF
     C                   EXSR      SRD
     C                   MOVEL     ADS           DSA3
     C                   ELSE
     C                   GOTO      SRFEND
     C                   ENDIF
     C                   MOVE      *ON           *IN44
     C                   MOVE      *OFF          *IN36
     C     SRFEND        ENDSR
