     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '1' '9')
     A            SFAVD          4Y 0O  6  6EDTCDE(4)
     A            SFOPD          7Y 0O  6 11EDTCDE(4)
     A            SFFBNR         3Y 0O  6 19EDTCDE(4)
     A            SFSTAT         1A  O  6 24TEXT('TERMKODE')
     A            SFBESK        35A  O  6 27
     A            SFANKI         4Y 0O  6 70TEXT('ANT FR.BREV')
     A                                      EDTCDE(4)
     A            MANKO          4Y 0O  6 76TEXT('AMNKO')
     A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/STSFBR'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 19'Fraktbrevsnivå        '
     A                                      COLOR(WHT)
     A            XXTYPE         5A  O  2 44
     A                                  2 50'Tur'
     A            XXPRO          8Y 0O  2 54EDTCDE(4)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'1=Neste nivå 9=Switch status     '
     A                                      COLOR(BLU)
     A                                  4 60'            Kolli '
     A                                      COLOR(WHT)
     A                                  5  2'Valg Avd/Oppd/Fb.nr Stat Avsender -
     A                                      Mottaker                           -
     A                                      Ant/Manko'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/STSFBR'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 19'Fraktbrevsnivå        '
     A                                      COLOR(WHT)
     A            XXTYPE         5A  O  2 44
     A                                  2 50'Tur'
     A            XXPRO          8Y 0O  2 54EDTCDE(4)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'1=Neste nivå 9=Switch status      -
     A                                             '
     A                                      COLOR(BLU)
     A                                  4 60'            Kolli '
     A                                      COLOR(WHT)
     A                                  5  2'Valg Avd/Oppd/Fb.nr Stat Avsender -
     A                                      Mottaker                           -
     A                                      Ant/Manko'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
