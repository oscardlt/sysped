      *_____________________________________
      * SYSPED / NCTS ENTRY, HENDELSE
      *"""""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R NCTSIVR
     A            TV01           1A         COLHDG('IKKE I BRUK')
     A            TVAVD     R               REFFLD(AVD)
     A            TVTDN     R               REFFLD(TDN)
     A            TVLI           1S 0       COLHDG('SEKVENSNR')
     A            TVKD           1A         COLHDG('HENDELSESKODE')
     A            TVTXT        512A         COLHDG('TEKST')
      * ENDORSEMENT
     A            TVADT          8S 0       COLHDG('DATO')
     A            TVAAUT        35A         COLHDG('AUTORITET')
     A            TVASD         35A         COLHDG('STED')
     A            TVALK          2A         COLHDG('LAND')
      * LOKASJON
     A            TVQID          1A         COLHDG('QUALIFIER OF ID.')
     A            TVSD          17A         COLHDG('UN LOCODE')
     A            TVLK           2A         COLHDG('LAND')
      *  GNSS
     A            TVLGLA        17A         COLHDG('LOC. LATITUDE')
     A            TVLGLO        17A         COLHDG('LOC. LONGITUDE')
      *  ADDRESS
     A            TVAD1         30A         COLHDG('ADRESSE')
     A            TVPN          17A         COLHDG('POSTNR')
     A            TVPS          24A         COLHDG('POSTSTED')
      * TRANSPORT EQUIPMENT
     A            TVCNR         17A         COLHDG('CONTAINERNR')
     A            TVANTS         4S 0       COLHDG('ANTALL SEAL')
      *  SEAL
     A            TVSID         20A         COLHDG('SEAL IDENTIFIER')
      *  GOODS REFERENCE
     A            TVDLI          5S 0       COLHDG('DEKL. GODS ITEM')
      * TRANSHIPMENT
     A            TVKDC          1S 0       COLHDG('CONTAINER 1/0')
      *  TRANSPORT MEANS
     A            TVTTY          2S 0       COLHDG('TRANSPORTTYPE')
     A            TVTID         35A         COLHDG('IDENTIFICATION NO.')
     A            TVTLK          2A         COLHDG('LAND')
      *
     A          K TVAVD
     A          K TVTDN
     A          K TVLI
