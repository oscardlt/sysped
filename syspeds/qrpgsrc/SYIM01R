********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 Sendes som epost ikke merket alt
10XXX * 02 SH  PTF10 P/S
********************************************************************
      *
N01  FSYIM01F   UF A E           K DISK
     FSYIM01D   CF   E             WORKSTN
n01   * Finner username
n01   *"""""""""""""""""""""""""""
n01  D                SDS
n01  D  ZUSER                254    263
     D                 DS
     D  ZDT                    1      6  0
     D  ZDTM                   3      4  0
     D  ZDTD                   5      6  0
     D                 DS
     D  ZST1                   1      5
     D  ZST01                  1      1
     D  ZST02                  2      2
     D  ZST03                  3      3
     D  ZST04                  4      4
     D  ZST05                  5      5
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C     *in03         ifne      '1'
     C   44              WRITE     SYIM01FR
     C  N44              UPDATE    SYIM01FR
     c                   end
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN12         WIN12
     C     *LIKE         DEFINE    *IN30         WIN30
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM      ZAVDF         ZAVDF
     C                   PARM      ZAVDT         ZAVDT
     C                   PARM      ZGNF          ZGNF
     C                   PARM      ZGNT          ZGNT
     C                   PARM      ZDTF          ZDTF                           ÅÅMMDD
     C                   PARM      ZDTT          ZDTT                           ÅÅMMDD
     C                   PARM      ZOT01         ZOT01
     C                   PARM      ZOT02         ZOT02
     C                   PARM      ZOT03         ZOT03
     C                   PARM      ZOT04         ZOT04
     C                   PARM      ZOT05         ZOT05
     C                   PARM      ZOT06         ZOT06
     C                   PARM      ZOT07         ZOT07
     C                   PARM      ZOT08         ZOT08
     C                   PARM      ZOT09         ZOT09
     C                   PARM      ZOT10         ZOT10
     C                   PARM      ZST1          ZST2              5
     C                   PARM      ZUNG          ZUNG
     C                   PARM      ZDEV          ZDEV
     C                   PARM      ZA4           ZA4
     C                   PARM      *IN03         WIN03
     C                   PARM      ZBRUDD        ZBRUDD
     C                   PARM      ZLNRF         ZLNRF
     C                   PARM      ZLNRT         ZLNRT
N01  C                   PARM      EPOST         EPOST
N02  C                   PARM      ZPS           ZPS
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM      ZDEV          ZDEV
     C     *IN30         PARM                    WIN30
     C                   PARM      ZOBJTY        ZOBJTY
     C                   MOVEL     '*OUTQ'       ZOBJTY           10
      *_____________
      * Init av felt
      *"""""""""""""
N01  C     ZUSER         CHAIN     SYIM01F                            44
     C   44              Z-ADD     0             ZAVDF
     C   44              Z-ADD     9999          ZAVDT
     C                   MOVE      '      '      ZGNF
     C                   MOVE      '      '      ZGNT
     C   44              Z-ADD     010101        ZDTF
     C   44              Z-ADD     051231        ZDTT
     C   44              MOVEL     UYEAR         ZDTF
     C   44              MOVEL     UYEAR         ZDTT
     C   44              MOVE      'N'           ZUNG
     C   44              MOVE      'J'           ZA4
     C   44              MOVEL     '*JOB'        ZDEV
     C   44              MOVE      'N'           ZBRUDD
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SYIM01DØ
     C                   EXFMT     SYIM01DA
     C                   MOVEA     '00000000'    *IN(30)
     C                   MOVEA     '0000'        *IN(38)
     C     *IN12         IFEQ      '1'
     C                   SETON                                        03
     C                   END
     C     *IN03         CABEQ     '1'           SRB9
      * Avdeling til må vare større en til.
     C     ZAVDF         IFGT      ZAVDT
     C                   MOVE      'SPA0003'     ZMSGNO
     C                   MOVEA     '111'         *IN(30)
     C                   GOTO      SRB1
     C                   END
      * Godsnummer til må vare større en til.
     C     ZGNF          IFGT      ZGNT
     C                   MOVE      'SPG0000'     ZMSGNO
     C                   MOVEA     '10011'       *IN(30)
     C                   GOTO      SRB1
     C                   END
      * Teste dato fra:
     C                   Z-ADD     ZDTF          ZDT
     C     ZDTM          COMP      1                                    30
     C  N30ZDTM          COMP      12                                 30
     C  N30ZDTD          COMP      1                                    30
     C  N30ZDTD          COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVE      '1'           *IN35
     C   30              GOTO      SRB1
      * Teste dato TIL:
     C                   Z-ADD     ZDTT          ZDT
     C     ZDTM          COMP      1                                    30
     C  N30ZDTM          COMP      12                                 30
     C  N30ZDTD          COMP      1                                    30
     C  N30ZDTD          COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVE      '1'           *IN40
     C   30              GOTO      SRB1
      * Uten nytt godsnummer J/N
     C     ZUNG          IFNE      'J'
     C     ZUNG          IFNE      'N'
     C                   MOVE      'SPG0005'     ZMSGNO
     C                   MOVEA     '10000001'    *IN(30)
     C                   GOTO      SRB1
     C                   END
     C                   END
      * Valid skriverenhet ?
     C     ZDEV          IFNE      '*JOB'
     C                   CALL      'EDI33C'      PLISTA
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPS0018'     ZMSGNO
     C                   MOVE      '1'           *IN38
     C                   GOTO      SRB1
     C                   END
     C                   END
      * Stående A4
     C     ZA4           IFNE      'J'
     C     ZA4           IFNE      'N'
     C                   MOVE      'DIV1448'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN39
     C                   GOTO      SRB1
     C                   END
     C                   END
      * ZPS
     C     EPOST         IFNE      *BLANKS
     C     ZPS           IFNE      'P'
     C     ZPS           ANDNE     'S'
     C                   MOVE      'SHU1430'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN41
     C                   GOTO      SRB1
     C                   END
     C                   END
     C     EPOST         IFEQ      *BLANKS
     C     ZPS           IFNE      ' '
     C                   MOVE      'SHU1431'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN41
     C                   GOTO      SRB1
     C                   END
     C                   END
      * LØPENR FRA-TIL , HVIS BARE FRA ER OPPGITT
     C     ZLNRF         IFNE      ' '
     C     ZLNRT         ANDEQ     ' '
     C                   MOVE      ZLNRF         ZLNRT
     C                   END
      *
     C     SRB9          ENDSR
