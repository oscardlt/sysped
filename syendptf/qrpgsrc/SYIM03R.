     H DECEDIT(',') debug(*yes)
      **************************************************************
      *** PROGRAM for utskrift av tollagerregnskap               ***
      **************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 hent løpenr fra SAD kun dersom blank i oppdtrag
      *              (det KAN ha vært fremmed-fortolling/ligger en ufullstendig/slettet SAD)
10XXX * 02 SH  PTF10 legg over lagersted i mottagernavn
10XXX * 03 SH  PTF10 SENDE FIL SOM EPOST
10XXX * 04 SH  PTF10 Vareverdi + noe korreksjoner
10XXX * 05 SH  PTF10 fri søkevei
10XXX * 06 SH  PTF10 innført lagerstedskode
***** *-------------------------------------------------------------------
10XXX * 07 SH  PTF11 snudd testen i PTF 01
10XXX * 08 SH  PTF11 lagersted ligger igjen fra SADH
10XXX * 09 SH  PTF11 noen nye felt innført
10XXX * 10 JOV PTF11 Stygg feil i PTF09  (+ legge . bak GN o SSV-fil. ungå Eksp-funk..)
10XXX * 11 SH  PTF11 nullstille verdier i SADH
10XXX * 12 SH  PTF11 Nye felt i sdv-file
10XXX * 13 SH  PTF11 kunde/foretaksnr mottaker
10XXX * 14 SH  PTF11 flytte ut til en sub rutine ikkesadh
10XXX * 15 SH  PTF12 får med godsnummer uten oppdrag
10XXX * 16 SH  PTF12 ikke ta med omberegninger
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 17 SH  PTF12 Videreutvikling på endring 16
********************************************************************
N13  FCUNDL01   IF   E           K DISK
N13  FFIRM      IF   E           K DISK
N05  FTURER14   IF   E           K DISK
N05  FFRISOK    IF   E           K DISK
     FHEADF     IP   E           K DISK
     FSADH      IF   E           K DISK
N15  FSAEH6B    IF   E           K DISK
N15  FSADH6B    IF   E           K DISK
N15  F                                     RENAME(SADHR:SADHR6)
N15  FHEADFDUM  IF   E           K DISK    PREFIX(H_)
N15  F                                     RENAME(HEADFR:HEADFRD)
     FTRACKL02  IF   E           K DISK
     FSYIM03P   O    E             PRINTER OFLIND(*IN02)
N03  FSYIM03F   O  A E             DISK
     D                 DS
     D  LESGNP                 1     22
     D  HEGN                   1     15
     D  HEGN13                13     13
     D  HEPOS1                16     22
     D                 DS
     D  TTTEXT                 1     70
     D  TTDTG                 21     30
N05  D                 DS
N05  D  FSSOK                  1     35
N05  D  EXTREF                 1     15
     D                 DS
     D  ZST1                   1      5
     D  ZST                    1      5    DIM(5)
     IHEADFR        01
     I                                          HEGN          L3
     I                                          HEPOS1        L2
     I                                          HEPOS2        L1
      *_____
      * Init
      *"""""
     C     WIN1P         CASEQ     *BLANKS       SRINIT
     C                   END
      * DERSOM KUN POSTER UTEN LØPENR/NYTT GODSNR ER VALGT VIL
      * OPNQRY HA VALGT UT DE SOM IKKE HAR LØPENR
      * DERSOM NYTT GODSNR FINNES SKAL DE IKKE PÅ LISTA, OK-> 01 ON
     C   01ZUNG          IFEQ      'J'
     C     HEGNN         COMP      *BLANKS                                01
     C                   END
      * KUN LØPENR... FYLT UT (3 SISTE SIFFER AV GN..)
     C   01ZLNRF         IFNE      ' '
     C     HEGN13        IFLT      ZLNRF
     C     HEGN13        ORGT      ZLNRT
     C                   MOVE      *OFF          *IN01
     C                   END
     C                   END
      *Flere med lik Godsn / posisj. -> skriv bruddsum
     C     FORGNP        IFNE      LESGNP
     C     POSANT        IFGT      1
N03  C     EPOST         IFEQ      *BLANKS
N03  C     ZPS           OREQ      'P'
     C                   WRITE     SYIM03PD
N03  C                   END
     C                   END
     C                   MOVE      *ZEROS        POSANT            7 0
     C                   MOVE      *ZEROS        POSKLL            7 0
     C                   MOVE      *ZEROS        POSVKT            9 0
     C                   END
      *NYTT Godsn  -> skriv bruddsum (MEN IKKE HVIS KUN EN POST)
     C     FORIGE        IFNE      *BLANKS
     C     HEGN          ANDNE     FORIGE
     C     GODANT        ANDNE     *ZEROS
S15  C*    GODANT        IFGT      1
N03  C     EPOST         IFEQ      *BLANKS
N03  C     ZPS           OREQ      'P'
N15  C                   EXSR      flere
     C                   WRITE     SYIM03PE
N03  C                   END
S15  C*                  END
     C                   MOVE      *ZEROS        GODANT            7 0
     C                   MOVE      *ZEROS        GODKLL            7 0
     C                   MOVE      *ZEROS        GODVKT            9 0
N16  C                   MOVE      *ZEROS        POSANT
N16  C                   MOVE      *ZEROS        POSKLL
N16  C                   MOVE      *ZEROS        POSVKT
     C                   END
      * SIDE BRUD  PR GODSNUMMER ??
     C     ZBRUDD        IFEQ      'J'
     C     FORIGE        IFNE      *BLANKS
     C     HEGN          IFNE      FORIGE
N03  C     EPOST         IFEQ      *BLANKS
N03  C     ZPS           OREQ      'P'
     C                   WRITE     SYIM03PB
N03  C                   END
     C                   MOVE      '0'           *IN02
     C                   END
     C                   END
     C                   END
      * Overflow ?
     C     *IN02         IFEQ      '1'
N03  C     EPOST         IFEQ      *BLANKS
N03  C     ZPS           OREQ      'P'
     C                   WRITE     SYIM03PB
N03  C                   END
     C                   MOVE      '0'           *IN02
     C                   END
     C     *IN01         IFEQ      *ON
      * Hent data fra SADH om det finnes
N08  C                   MOVE      *blanks       SIKDLS
N14  c                   move      *blanks       vaveko            3
     C     SADHK         CHAIN     SADH                               33
     C     *IN33         IFEQ      '1'
N14  C                   exsr      ikkesad
N09  C                   END
     C     *IN33         IFEQ      *OFF
      *
N14  c     trverb        ifeq      0
N14  c                   move      '(S)'         vaveko
N14  c                   z-add     sibel3        trverb
n14  c                   move      sival3        trverv
n14  c                   end
     C                   IF        ZST1 <> *BLANKS
     C     1             DO        5             XN                3 0
     C                   IF        ((ZST(XN) <> *BLANKS) AND (ZST(XN) = SIST))
     C                   GOTO      XNESTE
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   MOVEL     SINAS         ZZNAS            20
     C                   MOVEL     SIADS1        ZZADS1           20
     C                   MOVEL     SINAK         ZZNAK            20
N13  C                   move      *blanks       syrg
N13  c     heknk         ifne      *zeros
N13  C     kunkey        chain     cundl01                            33
N13  C                   end
N03  C                   MOVEL     SINAS         ZFNAS            30
N03  C                   MOVEL     SIADS1        ZFADS1           30
N03  C                   MOVEL     SINAK         ZFNAK            30
N02  C     sikdls        ifeq      'A8'
n02  c                   move      '(A8)'        ZZNAK
n02  c                   end
N14  C                   MOVEL     SIKDLS        ZZKDLS            2
N14  C                   MOVEL     SILS          ZZLS              8
     C                   MOVEL     SIADK1        ZZADK1           20
     C                   MOVEL     SIADK3        ZZADK3           20
N03  C                   MOVEL     SIADK1        ZFADK1           30
N03  C                   MOVEL     SIADK3        ZFADK3           30
s07  c*    HETLL         ifne      *blanks
n07  c     HETLL         ifeq      *blanks
     C                   MOVEL     SITLE         HETLE
     C                   MOVEL     SITLL         HETLL
N01  c                   endif
     C                   ELSE
     C                   MOVEL     HENAS         ZZNAS
     C                   MOVEL     HEADS1        ZZADS1
     C                   MOVEL     HENAK         ZZNAK
     C                   MOVEL     HEADK1        ZZADK1
     C                   MOVEL     HEADK3        ZZADK3
N03  C                   MOVEL     HENAS         ZFNAS
N03  C                   MOVEL     HEADS1        ZFADS1
N03  C                   MOVEL     HENAK         ZFNAK
N03  C                   MOVEL     HEADK1        ZFADK1
N03  C                   MOVEL     HEADK3        ZFADK3
     C                   MOVEL     *BLANKS       SIDTG
     C                   END
n05   * fri søkevei
N04  C                   MOVE      *OFF          *IN68
n05  C     FRIKEY        CHAIN     FRISOK                             33
n05  c   33              move      *blanks       fssok
n05  c  N33              seton                                        68
      * Hent data fra TRACKF om det finnes (RUCKATTEST)
     C     TTKEY         CHAIN     TRACKL02                           33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       TTDTG
     C                   END
     C     HETLL         IFNE      *BLANKS
     C     SIDTG         ANDEQ     *BLANKS
     C                   MOVE      TTDTG         SIDTG
     C                   END
      * Skriv ut detaljer
     C                   MOVE      *OFF          *IN66
N04  C                   MOVE      *OFF          *IN67
     C     HEGNN         IFNE      *BLANKS
     C                   MOVE      *ON           *IN66
     C                   ENDIF                                                  <HEGNN
      *
      * vareverdi
      *
N04  C     TRVERB        IFNE      *zeros
N04  C                   MOVE      *ON           *IN67
N04  C                   ENDIF                                                  vareverdi
N03  C     EPOST         IFEQ      *BLANKS
N03  C     ZPS           OREQ      'P'
N17   *
N17  C                   CALL      'SYIM03RA'
N17  C                   PARM      HEAVD         UAVD              4 0
N17  C                   PARM      HEOPD         UOPD              7 0
N17  C                   PARM                    UNT               7 0
N17  C                   PARM                    UVKT              9 0
N17  C                   PARM                    UTLL             10
N17  C                   PARM                    UTLE              6
N17  C                   PARM                    UDTG             10
N17  C                   PARM                    UFLAG             1 0
N17  C                   IF        UFLAG = 1
N17  C                   EVAL      POSKLL = POSKLL - HENT + SINTK
N17  C                   EVAL      POSVKT = POSVKT - HEVKT + SIVKB
N17  C                   EVAL      SINTK = UNT
N17  C                   EVAL      SIVKB = UVKT
N17  C                   EVAL      *IN41 = *ON
N17  C                   ELSE
N17  C                   EVAL      *IN41 = *OFF
N17  C                   END
N17   *
     C                   WRITE     SYIM03PC
N03  C                   ELSE
N03  C                   MOVEL     HEGN          HEGN16
N10  C                   MOVE      '.'           HEGN16
N03  C                   WRITE     SYIM03FR
N03  C                   END
     C                   MOVEL     LESGNP        FORGNP           22
     C                   ADD       1             POSANT
     C                   ADD       HENT          POSKLL
     C                   ADD       HEVKT         POSVKT
     C                   ADD       1             GODANT
     C                   ADD       HENT          GODKLL
     C                   ADD       HEVKT         GODVKT
      *
     C                   MOVEL     HEGN          FORIGE           15
     C                   END
     C     XNESTE        TAG
      *
      * REEKSP. PÅ DETTE NR?                                           TFYLT
     CL3   POSANT        IFGT      0
     CL3   HEGN          SETLL     SAEH6B
     CL3   HEGN          READE     SAEH6B                                 34
     CL3   *IN34         DOWEQ     '0'
     CL3   ZDTF8         IFLE      SEDT
     CL3   ZDTT8         ANDGE     SEDT
      * Overflow ?
     CL3   *IN02         IFEQ      '1'
N03  CL3   EPOST         IFEQ      *BLANKS
N03  CL3   ZPS           OREQ      'P'
     CL3                 WRITE     SYIM03PB
     CL3                 MOVE      '0'           *IN02
N03  CL3                 END
     CL3                 END
N03  CL3   EPOST         IFNE      *BLANKS
N03  CL3   ZPS           OREQ      'P'
     CL3                 WRITE     SYIM03RE
N03  CL3                 END
     CL3                 END
     CL3   HEGN          READE     SAEH6B                                 34
     CL3                 END
     CL3                 END
      * Skriv ut LR
      *Hadde siste Godsn/posisj. flere poster?? -> skriv bruddsum
     CLR   POSANT        IFGT      1
N03  CLR   EPOST         IFEQ      *BLANKS
N03  CLR   ZPS           OREQ      'P'
     CLR                 WRITE     SYIM03PD
N03  CLR                 END
     CLR                 END
S15  C*    GODANT        IFGT      1
N03  CLR   EPOST         IFEQ      *BLANKS
N03  CLR   ZPS           OREQ      'P'
N15  CLR                 EXSR      flere
     CLR                 WRITE     SYIM03PE
N03  CLR                 END
S15  C*                  END
N03  CLR   EPOST         IFEQ      *BLANKS
N03  CLR   ZPS           OREQ      'P'
     CLR                 WRITE     SYIM03PØ
N03  CLR                 END
      *___________________________________________________
      * Initiering                                  SRINIT
      *"""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
      * Input paramerer
     C     *ENTRY        PLIST
     C                   PARM                    ZAVDF
     C                   PARM                    ZAVDT
     C                   PARM                    ZGNF
     C                   PARM                    ZGNT
     C                   PARM                    ZDTF
     C                   PARM                    ZDTT
     C                   PARM                    ZOT01
     C                   PARM                    ZOT02
     C                   PARM                    ZOT03
     C                   PARM                    ZOT04
     C                   PARM                    ZOT05
     C                   PARM                    ZOT06
     C                   PARM                    ZOT07
     C                   PARM                    ZOT08
     C                   PARM                    ZOT09
     C                   PARM                    ZOT10
     C     ZST1          PARM                    ZST2              5
     C                   PARM                    ZUNG
     C                   PARM                    ZBRUDD            1
     C                   PARM                    ZLNRF             1
     C                   PARM                    ZLNRT             1
N03  C                   PARM                    EPOST            70
N03  C                   PARM                    ZPS               1
      *
     C*                  dump
     C     ZDTF          IFGT      501231
     C     ZDTF          ADD       19000000      ZDTF8             8 0
     C                   ELSE
     C     ZDTF          ADD       20000000      ZDTF8
     C                   END
     C     ZDTT          IFGT      501231
     C     ZDTT          ADD       19000000      ZDTT8             8 0
     C                   ELSE
     C     ZDTT          ADD       20000000      ZDTT8
     C                   END
      *
N13  C     kunkey        KLIST
N13  C                   KFLD                    fifirm
N13  C                   KFLD                    heknk
N13  c                   read      firm                                   33
     C     SADHK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TTKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXFBNR
     C                   KFLD                    XXACTI
n05  C     FRIKEY        KLIST
n05  C                   KFLD                    HEAVD
n05  C                   KFLD                    HEOPD
n05  C                   KFLD                    WSKODE
n05  C                   MOVEL     '*CR'         WSKODE            3
     C     *LIKE         DEFINE    TTFBNR        XXFBNR
     C     *LIKE         DEFINE    TTACTI        XXACTI
     C                   MOVE      *ZEROS        XXFBNR
     C                   MOVE      'RUC'         XXACTI
      * Skriv ut Første siden.
N03  C     EPOST         IFEQ      *BLANKS
N03  C     ZPS           OREQ      'P'
     C                   WRITE     SYIM03PA
      * Skriv ut Heading.
     C                   WRITE     SYIM03PB
N03  C                   END
      * 1-page indikator.
     C                   MOVE      '1'           WIN1P             1
      *
     C                   ENDSR
N14  C**********************************************************
N14  C     ikkesad       BEGSR
N14  C**********************************************************
N14   * start finnes ikke i SADH
N14  C                   MOVE      ' '           SIST
N14  C                   MOVE      *blanks       SILKT
N14  C                   MOVE      *zeros        SITRM
N14  C                   MOVE      *zeros        SIDTG
N14  C                   MOVE      *blanks       SIKDLS
N14  C                   MOVE      *blanks       SILS
N14  C                   MOVE      *BLANKS       SITRID
N14  C                   MOVEL     HEHAWB        SITRID
N14  C                   MOVE      'N'           XTUR              1
N14  C     HEUR          IFEQ      'H'
N14  C     HEPRO         andne     *zeros
N14  C     HEPRO         CHAIN     TURER14                            97
N14  C                   move      TUTRMA        HETRM
N14  c                   endif
N14  C     HETRM         IFNE      0
N14  C                   Z-ADD     HETRM         SITRM
N14  C* VED TRANSPORTMÅTE 16 HENTES TOLLSTED FRA TURER
N14  C                   MOVE      'N'           XTUR              1
N14  C     HETRM         IFEQ      16
N14  C     HEPRO         CHAIN     TURER14                            97
N14  C  N97              MOVE      'J'           XTUR
N14  C                   ELSE
N14  C     HEPRO         IFNE      0
N14  C*    HEPRO         CHAIN     TURER14                            33
N14  C* N33              MOVE      'J'           XTUR
N14  C     HEPRO         CHAIN     TURER14                            97
N14  C  N97              MOVE      'J'           XTUR
N14  C                   END
N14  C                   END
N14  C                   END
N14  C                   MOVE      HETRC         SIKDC
N14  C                   IF        XTUR = 'N'
N14  C                   MOVE      HETRI         SILKT
N14  C                   ELSE
N14  C     TULK          IFNE      *BLANKS
N14  C                   EVAL      SILKT  = TULK
N14  C                   ELSE
N14  C                   MOVE      HETRI         SILKT
N14  C                   ENDIF
N14  C     TUBILN        IFNE      *BLANKS
N14  C                   EVAL      SITRID = TUBILN
N14  C                   ENDIF
N14  C                   END
N14  C                   ENDsr
N15  C**************************************************************************
N15  C     flere         begsr
N15  C**************************************************************************
N15  C     KEYDUM        KLIST
N15  C                   KFLD                    SIAVD
N15  C                   KFLD                    SITDN
      *
N15  C     FORIGE        SETLL     SADH6B
N15  C     FORIGE        READE     SADH6B                                 34
N15  C     *IN34         DOWEQ     '0'
N15  C     keyDUM        chain     HEADFDUM                           35
N15  C     *IN35         IFEQ      '1'
N16  C     sitdn         andge     0
N17   *  Sjekk omberegning
N15   *  overflow ?
N15  C     *IN02         IFEQ      '1'
N15  C     EPOST         IFEQ      *BLANKS
N15  C     ZPS           OREQ      'P'
N15  C                   WRITE     SYIM03PB
N15  C                   MOVE      '0'           *IN02
N15  C                   END
N15  C                   END
N15  C                   ADD       1             POSANT
N15  C                   ADD       SINTK         POSKLL
N15  C                   ADD       SIVKB         POSVKT
N15  C                   ADD       1             GODANT
N15  C                   ADD       SINTK         GODKLL
N15  C                   ADD       SIVKB         GODVKT
N15  C     EPOST         IFEQ      *BLANKS
N15  C     ZPS           OREQ      'P'
N15  C                   WRITE     SYIM03TI
N15  C                   END
N15  C                   END
N15  C     FORIGE        READE     SADH6B                                 34
N15  C                   END
N15  C                   endsr
