     A*****************************************************************
     A*   UTLISTING AV TURSTATISTIKK
     A* ANTALL LINJE=72,  OVERFLOW 66
     A*****************************************************************
     A**
     A          R PHEAD                     SKIPB(2)
     A                                     3MSGCON(50 SHU0608 SYMSGF)
     A                                    55'Page:'
     A                                    60PAGNBR EDTCDE(Z)
     A                                     3MSGCON(12 DIV0205 SYMSGF)
     A                                      SPACEB(1)
     A            TUPRO          8S 0     16
     A                                    26MSGCON(6 SHU0609 SYMSGF)
     A            TUBILN        10        33
     A                                    55'Date:'
     A                                    70DATE EDTCDE(Y)
     A                                     3MSGCON(12 DIV5049 SYMSGF)
     A                                      SPACEB(1)
     A            TUNAT         30        16
     A                                    55MSGCON(10 SHU0611 SYMSGF)
     A            TUADAG         2S 0     70
     A                                    72'.'
     A            TUAMND         2S 0     73
     A                                    75'.'
     A            TUAAAR         2S 0     76
     A                                     3MSGCON(12 SHU0610 SYMSGF)
     A                                      SPACEB(1)
     A            TUSJN1        30        16
     A                                    55MSGCON(10 SHU0612 SYMSGF)
     A            TUBDAG         2S 0     70
     A                                    72'.'
     A            TUBMND         2S 0     73
     A                                    75'.'
     A            TUBAAR         2S 0     76
     A                                     3MSGCON(74 SHU0613 SYMSGF)
     A                                      SPACEB(2)
     A          R PDET                      SPACEB(2)
     A            HEAVD          4  0      3EDTCDE(Z)
     A            HEOPD          7  0     +1EDTCDE(Z)
     A            HESG           3        +1
     A            GMFAKT         7S 0     +1EDTCDE(Z)
     A            GMMND          2S 0     +1EDTCDE(Z)
     A            NAVN          20        +1
     A            PRINNT        11S 2     +1EDTCDE(M)
     A            PRKOST        11S 2     +1EDTCDE(M)
     A          R PDETT                     SPACEB(2)
     A                                     3MSGCON(27 SHU0615 SYMSGF)
     A            TOINNT        11S 2     51EDTCDE(M)
     A            TOKOST        11S 2     65EDTCDE(M)
     A                                     3MSGCON(12 SHU0614 SYMSGF)
     A                                      SPACEB(1)
     A            TORESU        11S 2     16EDTCDE(M)
