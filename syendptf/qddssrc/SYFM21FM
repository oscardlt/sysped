     A*%%TS  SD  20131127  111924  SVEIN       REL-V7R1M0  5770-WDS
N01  A* NYE FILTER / FJERNET CHECK(RB) FYSISK
N02  A* NYE FILTER  32 ON = PROTECT/NONDISPLAY EKSTRAFILTER
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPDOC('SPOP0010' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE02
     A*%%TS  SD  20131127  111924  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF02(02 'PROTECT FILTER')
     A                                      CF04(04 'Søking')
     A                                      CF08(08 'Direkt til oppdrag')
     A                                      CF09(09 'Direkt til faktura')
     A                                      CF12(12 'FORRIGE')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF20(20 'Direkte status forespørsel-
     A                                        (TdV IS)')
     A                                      CF21(21 'Status forespørsel        -
     A                                        (TdV IS)')
     A                                      CF22(22 'TdV IS loggfunksjon       -
     A                                        (TdV IS)')
     A                                      CF23(23 'Availability forespørsel  -
     A                                        (TdV IS)')
     A                                      CF19(19 'Overgang til Easy Mail    -
     A                                        (TdV IS)')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPDOC('SPOP011' 'HELPNU' 'HLP400')
     A  *DS3                                HLPARA(01 001 24 080)
     A  *DS4                                HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGN2K         7A  P
     A            MSGN2L         7A  P
     A            MSGN2M         7A  P
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFM21'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(023 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
S03  A*                                 4 38MSGCON(024 DIV0457 *LIBL/SYMSGF)
     A                                  4 34MSGCON(010 JOV1525 *LIBL/SYMSGF)
S02  A*                                 4 50'Tilleggs-filter:'
     A                                  4 50MSGCON(020 JOV1536 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  32                              4 71MSGCON(010 JOV1556 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4 71MSGCON(010 JOV1557 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            KOANVN        33A  O  5  2TEXT('AVDELINGSNAVN')
     A                                      COLOR(WHT)
     A                                  5 50'Vekt f.o.m:'
     A  32                                  DSPATR(ND)
     A            WSVKTO         5Y 0B  5 63DSPATR(UL)
     A  32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  5 69't.o.m:'
     A  32                                  DSPATR(ND)
     A            WSVKTU         5Y 0B  5 76DSPATR(UL)
     A  32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  6 10MSGCON(035 DIV0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 50'Kun Hen./Lev.navn som innehol.:'
     A  32                                  DSPATR(ND)
     A                                  7  2MSGCON(023 SHU1098 *LIBL/SYMSGF)
     A            WSDT9W         6Y 0B  7 28EDTCDE(Z)
     A                                  7 50'H:'
     A  32                                  DSPATR(ND)
     A            WSHNAV        12A  B  7 53
     A  32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
     A                                  7 66'L:'
     A  32                                  DSPATR(ND)
     A            WSLNAV        12A  B  7 69
     A  32                                  DSPATR(PR)
     A  32                                  DSPATR(ND)
     A                                  8  2MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A            WSFN           7Y 0B  8 28DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WSKOST         1A  B  8 36
     A                                  8 38MSGCON(020 SHU1089 *LIBL/SYMSGF)
     A                                  9  2MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B  9 28DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 10  2MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 10 33'(* = ALL)'
     A                                 10 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT1W         6Y 0B 10 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 11  2MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A            WSGNA          4A  B 11 28DSPATR(UL)
     A                                      COLOR(GRN)
     A            WSGNB         11A  B 11 33DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 11 50MSGCON(004 DIV3167 *LIBL/SYMSGF)
     A            WSGNÅR         2Y 0B 11 57DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 12  2MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A            WSKN           8Y 0B 12 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 12 37'Type:'
     A            WSTYPE         2A  B 12 43DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      VALUES('  ' 'A ' 'K ' 'S ' 'KF' 'SF-
     A                                      ')
     A                                 12 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A                                 12 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT2W         6Y 0B 12 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 12 61'(A /K /S /KF/SF)'
     A                                 13  2MSGCON(023 SHU1271 *LIBL/SYMSGF)
     A            WSGM1         15A  B 13 28
     A                                 14  2MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A            WSOT           2A  B 14 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 14 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT4W         6Y 0B 14 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 15  2MSGCON(023 JOV0767 *LIBL/SYMSGF)
     A            WSRFA         15A  B 15 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 15 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDTAW         6Y 0B 15 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 16  2MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B 16 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 17  2MSGCON(023 DIV5090 *LIBL/SYMSGF)
     A            WSHAWB         9A  B 17 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 17 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT5W         6Y 0B 17 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 18  2MSGCON(023 SHU0016 *LIBL/SYMSGF)
     A            WSCONT        17A  B 18 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 18 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT6W         6Y 0B 18 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 19  2MSGCON(023 BBA0037 *LIBL/SYMSGF)
     A            WAIRLI         3Y 0B 19 28TEXT('Airline Prefix')
     A                                      EDTCDE(4)
     A            WAWBNO         8Y 0B 19 33TEXT('AWB Serial Number')
     A                                      EDTCDE(4)
     A            WAIRCD         3A  B 19 43TEXT('Airline code')
     A                                 20  2MSGCON(023 SHU0128 *LIBL/SYMSGF)
     A            WSFRI1         3A  B 20 28
     A            WSFRI2        35A  B 20 33
     A                                 20 69MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT8W         6Y 0B 20 75DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 21  2MSGCON(023 JOV0768 *LIBL/SYMSGF)
     A            WSSAML         6A  B 21 28
     A                                 21 36MSGCON(014 SHU0622 *LIBL/SYMSGF)
     A            WSSDF          5A  B 21 51
     A            WSSDT          5A  B 21 58
     A                                 21 64MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT7W         6Y 0B 21 70DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 22  2MSGCON(023 JOV1015 *LIBL/SYMSGF)
     A            WSMRK1        35A  B 22 28
     A            MSGT2K        18A  O 23  2MSGID(&MSGN2K SYMSGF)
     A                                      COLOR(BLU)
     A            MSGT2L        18A  O 23 22MSGID(&MSGN2L SYMSGF)
     A                                      COLOR(BLU)
     A            MSGT2M        18A  O 23 42MSGID(&MSGN2M SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R SUBFIL                    SFL
N08   * FELT I SUBFIL FLUTTET FOR Å GI PLASS TIL TYPE (HEUR)
     A            TRFA11         7A  H
     A            TRFA21         7A  H
     A            WSNR           2A  I  6  2
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            HEAVD          4Y 0O  6  5EDTCDE(4)
     A  89                                  COLOR(RED)
     A            HEOPD          7Y 0O  6 10EDTCDE(4)
     A  42                                  COLOR(WHT)
     A  88                                  DSPATR(RI)
     A            HEST           1   O  6 18
     A            HESG           3A  O  6 23
     A  43                                  COLOR(WHT)
     A            WSGNDS        11A  O  6 27
     A  44                                  COLOR(WHT)
     A            WSPOSS         8A  O  6 39
     A  45
     AO 44
     AO 49
     AO 54                                  COLOR(WHT)
     A            WSPOSK         8A  O  6 48
     A  46
     AO 55                                  COLOR(WHT)
     A            HEOT           2A  O  6 57
     A  47                                  COLOR(WHT)
     A            HEFR           3A  O  6 60
     A            HESGM         10A  H
     A            HENT           7Y 0O  6 64EDTCDE(4)
     A  27                                  COLOR(RED)
     A            HEVKT          9Y 0O  6 72EDTCDE(4)
     A            QFSU           1A  O  7 80COLOR(RED)
SV5  A*           HEDTR          6S 0H
SV5  A*           WSDTR          6Y 0O  7  6DSPATR(UL)
     A            HEDTOP         8S 0H
     A            WSDTOP         6Y 0O  7  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            HESDF          5A  O  7 12DSPATR(UL)
     A            HESDT          5A  O  7 18DSPATR(UL)
     A            HEPRO          8Y 0O  7 24DSPATR(UL)
     A                                      EDTCDE(4)
     A  49                                  COLOR(WHT)
     A            HEALT         15A  O  7 33DSPATR(UL)
     A  48
     AO 53
     AO 56
     AO 57
     AO 59
     AO 67
     AO 51                                  COLOR(WHT)
     A            WSGNND        11A  O  7 49DSPATR(UL)
     A  57
     AO 59                                  COLOR(WHT)
     A            XFNFMA         7A  O  7 61DSPATR(UL)
     A  41
     AO 57                                  COLOR(WHT)
     A            XNAFM         10A  O  7 69
     A  41                                  COLOR(WHT)
     A                                      DSPATR(UL)
     A            HEUR           1   O  6 20
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPDOC('SPOP110' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/SYFM21'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A  41                              2 20MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A  42                              2 20MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A  43                              2 20MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A  44                              2 20MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55                              2 20MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A  47                              2 20MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A  48                              2 20MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A  49                              2 20MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A  57                              2 20MSGCON(023 JOV1015 *LIBL/SYMSGF)
     A  59                              2 20MSGCON(023 JOV1210 *LIBL/SYMSGF)
     A  41        WSFN           7Y 0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A  42        WSOPD          7Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  43        WSSG           3A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  44        WSGN          15A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSKN           8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  47        WSOT           2A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  48        WSRFA         15A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  49        WSPRO          8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  57
     AO 59        WSMRK1        35A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  43                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  47                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  43        WSDT1W         6S 0O  2 67DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSDT2W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A  47        WSDT4W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A  28                              3  2MSGCON(078 YBC0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N28        MSGB31        75A  O  3  2MSGID(&MSGF23 SYMSGF)
     A                                      COLOR(BLU)
S08  A*                                 4  2MSGCON(079 YBC0200 *LIBL/SYMSGF)
S08  A*                                     COLOR(WHT)
S08  A*                                 5  6MSGCON(074 YBC0201 *LIBL/SYMSGF)
S08  A*                                     COLOR(WHT)
     A                                  4  2MSGCON(079 JOV1582 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  5MSGCON(076 JOV1583 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            MSGF23         7A  P
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      CF23(23 'FLERE O')
     A                                      CF24(24 'FLERE F')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            MSGB31        18A  O  1  2MSGID(&MSGF11 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB32        18A  O  1 22MSGID(&MSGF12 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB33        18A  O  1 42MSGID(&MSGF13 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGF11         7A  P
     A            MSGF12         7A  P
     A            MSGF13         7A  P
     A*
     A          R NYVARE
     A                                      CF01(01 'BYTT AVDELNGER')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      OVERLAY
     A                                  1  3'SYSPED/SYFM21'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A  41                              2 20MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A  42                              2 20MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A  43                              2 20MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A  44                              2 20MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55                              2 20MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A  47                              2 20MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A  48                              2 20MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A  49                              2 20MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A  57                              2 20MSGCON(023 JOV1015 *LIBL/SYMSGF)
     A  59                              2 20MSGCON(023 JOV1210 *LIBL/SYMSGF)
     A  41        WSFN           7Y 0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A  42        WSOPD          7Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  43        WSSG           3A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  44        WSGN          15A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSKN           8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  47        WSOT           2A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  48        WSRFA         15A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  49        WSPRO          8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  57
     AO 59        WSMRK1        35A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  43                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  47                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  43        WSDT1W         6S 0O  2 67DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSDT2W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A  47        WSDT4W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A N28                              3  2MSGCON(017 DIV0048 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  28                              3  2MSGCON(078 YBC0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N28                              3 20MSGCON(010 SHU0245 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N28                              3 32MSGCON(015 SHU0314 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N28                              3 50MSGCON(018 DIV3576 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S08  A*N28                              3 69MSGCON(010 SHU0241 *LIBL/SYMSGF)
S08  A*                                     COLOR(BLU)
S08  A*                                 4  2MSGCON(079 YBC0200 *LIBL/SYMSGF)
S08  A*                                     COLOR(WHT)
S08  A*                                 5  6MSGCON(074 YBC0201 *LIBL/SYMSGF)
S08  A*                                     COLOR(WHT)
N08  A                                  4  2MSGCON(079 JOV1582 *LIBL/SYMSGF)
N08  A                                      COLOR(WHT)
N08  A                                  5  5MSGCON(076 JOV1583 *LIBL/SYMSGF)
N08  A                                      COLOR(WHT)
