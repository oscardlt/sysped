      *%%TS  SD  20150518  125624  SVEIN       REL-V7R1M0  5770-WDS
***** **************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers  Beskrivelse...........................
""""" *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 OPSJON 8 = FULL MODULS
10318 * 02 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
      *              MYE ENDRING. INGEN MERKING
11013 * 03 CB  PTF11 URSPRUNGSLAND TIL INTERNT BRUK VED FYLKESKODE=91
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 04 YBC PTF11 OMBEREGNING
11138 * 05 YBC PTF11 VIS STATUS PÅ FEILMELDING FRA TVINN
11176 * 06 YBC PTF11 TILLEGGSLINJER MED SAMME POST
11186 * 07 YBC PTF11 VIS TOTAL VEKT OG SUM AV LINJEVEKT
*****A**************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD052APNL)
     A                                      HLPTITLE('SAD EKSPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5')
     A            SVLI           5Y 0H
     A            SVLN           5Y 0H
     A            SVREFL         5Y 0H
     A            SVVKTB        12Y 3H
     A            SVFT01        28A  H
     A            SVNT01         6Y 0H
     A            SVEH01         4A  H
     A            SVTOP1        17A  H
     A            SVCRE1         3A  H
N03  A            SVLK           2A  H
N05  A            SVERR          1A  H
     A            WXLN           5Y 0O  4  5EDTCDE(4)
     A            SVFYL          2A  O  4 11
     A            SVVNT          8S 0O  4 14
N05  A  52                                  COLOR(YLW)
     A            SVVKTN        12Y 3O  4 23EDTCDE(L)
     A            SVNTM          9Y 0O  4 38EDTCDE(L)
     A            SVBELT        11Y 2O  4 49EDTCDE(L)
     A            SVAVT          1A  O  4 63
     A            SVAVTP         7Y 3O  4 66EDTCDE(4)
     A            SVAVTS         3A  O  4 75
     A            SVVT01        30A  O  4 81TEXT('Varetekst')
     A  95                                  COLOR(YLW)
     A            SV01           1A  O  4112COLOR(RED)
     A                                      DSPATR(BL)
N04  A            SVNYL          1A  O  4114DSPATR(BL)
N04  A                                      COLOR(RED)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD052A'
     A                                      COLOR(BLU)
     A                                  1 21'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
S04  A*                                     EDTCDE(4)
N04  A                                      EDTCDE(L)
     A                                  2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                      e'
     A                                      COLOR(BLU)
     A  39                              2 52'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              2 66'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A N32                              3  2'Op Linje FK Varenr.. Nettovekt....-
     A                                      . Mengde.... Tollverdi.... FF Sats.-
     A                                      ... Sekv. Varebeskrivelse..........-
S04  A*                                     ..... S'
N04  A                                      ..... S T'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn FK Varenr.. Nettovekt....-
     A                                      ... Mengde.. Tollverdi.... FF Sats.-
     A                                      ... Sekv. Varebeskrivelse..........-
S04  A*                                     ..... S'
N04  A                                      ..... S T'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A                                      CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'Kalkulator av/på')
     A                                      CF12(12 'Gå til bilde02')
     A                                      CF14(14 'SØK KUNDERS VAREREGISTER')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CA18(18 'UTVIDERT BILDE')
     A                                      CA19(19 'HENT FRA Z-FIL')
     A  51                                  CF22(22 'OMSTART OMBEREGING')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  2  2'Fsum->'
     A            UBEL1         13Y 2O  2 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 27'Vsum->'
     A            W2VSUM        13Y 2O  2 35TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 52'Diff->'
     A            W2DIFF        13Y 2O  2 60TEXT('Beløp Faktsum')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  2 77MSGCON(008 SHU1574 *LIBL/SYMSGF)
     A            XBEL3         13Y 2O  2 87COLOR(WHT)
     A                                      EDTCDE(L)
N07  A                                  3  2'Tvkt->'
N07  A            SEVKB          9Y 0O  3 12TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                  3 27'Lvkt->'
N07  A            W2VKB         13Y 3O  3 35TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                  3 52'Diff->'
N07  A            W2VDIF        13Y 3O  3 60TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(L)
     A                                  4  2'F3=Neste/avslutt'
     A  35                                  COLOR(RED)
     A N35                                  COLOR(BLU)
     A                                  4 20'F5=Forny  F6=Lage ny'
     A                                      COLOR(BLU)
     A N36                              4 42'F7=Se oppd'
     A                                      COLOR(BLU)
     A                                  4 54'F8=Hopp til  F9=Kalkulator  F12=Fo-
     A                                      rrige'
     A                                      COLOR(BLU)
     A                                  5  2'F14=Søking kunders vareregister  F-
     A                                      15=Auto kopi T.O.  F17=Ny sad kopi -
     A                                      T.O.  F18=Full modul  F19=Hent EDI--
     A                                      fil'
     A                                      COLOR(BLU)
     A  51                              5111'F22=Angre'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A N40                                  CF03(03 'Be om utskrift')
     A                                      CF04(04 'Søk')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A N40                                  CF09(09 'Kalkulator av/på')
     A                                      CF11(11 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A                                      CF12(12 'Forrige')
     A                                      CF13(13 'Oppdater søketekst på tari-
     A                                      ff-register')
     A N40                                  CF14(14 'SØK KUNDERS VAREREGISTER')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A N40                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CA18(18 'UTVIDERT BILDE')
     A                                      CA19(19 'HENT FRA Z-FIL')
     A  60                                  CF21(21 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A  60                                  CF22(22 'MERK B PÅ STATUS')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SADE009A SAD052APNL)
     A                                      HLPARA(02 002 02 003)
     A          H                           HLPPNLGRP(SADE009 SAD052APNL)
     A                                      HLPARA(02 005 02 012)
     A          H                           HLPPNLGRP(SADE010 SAD052APNL)
     A                                      HLPARA(02 014 02 025)
     A          H                           HLPPNLGRP(SADE011 SAD052APNL)
     A                                      HLPARA(02 028 02 048)
     A          H                           HLPPNLGRP(SADE011B SAD052APNL)
     A                                      HLPARA(02 050 02 056)
     A          H                           HLPPNLGRP(SADE011C SAD052APNL)
     A                                      HLPARA(02 058 02 070)
     A          H                           HLPPNLGRP(SADE012 SAD052APNL)
     A                                      HLPARA(03 002 03 052)
     A          H                           HLPPNLGRP(SADE014 SAD052APNL)
     A                                      HLPARA(03 055 03 078)
     A          H                           HLPPNLGRP(SADE013 SAD052APNL)
     A                                      HLPARA(04 002 04 054)
     A          H                           HLPPNLGRP(SADE014 SAD052APNL)
     A                                      HLPARA(04 055 04 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2LI           5S 0H
     A                                  1  2'FK Varenr.. Tollverdi.... Brutto v-
     A                                      ekt... Netto vekt.... Mengde.... FF-
     A                                       Sats.... Sekv.'
     A                                      COLOR(WHT)
     A            W2FYL          2A  B  2  2TEXT('OPPRINGELSESFYLKE')
     A                                      DSPATR(UL)
     A  38
     AO 39                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A  38
     AO 39                                  DSPATR(PR)
     A N38N39                               COLOR(YLW)
     A            W2VNTI         8A  B  2  5TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  38
     AO 39                                  DSPATR(HI)
     A  89                                  DSPATR(RI)
     A  38
N04  AO 28
     AO 39                                  DSPATR(PR)
     A N38N39                               COLOR(YLW)
     A                                      DUP(61)
     A            W2VNT          8S 0H
     A            W2BELT        11Y 2B  2 14TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A  38                                  DSPATR(HI)
     A  38                                  DSPATR(PR)
     A N38                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2VKTB        12Y 3B  2 28TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  38                                  DSPATR(HI)
     A  88                                  DSPATR(RI)
     A  38                                  DSPATR(PR)
     A N38                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2VKTN        12Y 3B  2 43TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A  38                                  DSPATR(HI)
     A  38                                  DSPATR(PR)
     A N38                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2NTM          9Y 0B  2 58TEXT('Mengde')
     A                                      DSPATR(UL)
     A  38                                  DSPATR(HI)
     A  87                                  DSPATR(RI)
     A  38                                  DSPATR(PR)
     A N38                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2AVT          1A  B  2 69TEXT('AVG. KONJUNKTURFOND')
     A                                      DSPATR(UL)
     A  38
     AO 39                                  DSPATR(HI)
     A  38
     AO 97
     AO 39                                  DSPATR(PR)
     A N38N39                               COLOR(YLW)
     A                                      VALUES(' ' 'N' 'X')
     A                                      DUP
     A            W2AVTP         7Y 3B  2 72TEXT('PROSENTSATS AV SVAVT')
     A                                      DSPATR(UL)
     A  38
     AO 39                                  DSPATR(HI)
     A  38
     AO 97
     AO 39                                  DSPATR(PR)
     A N38N39                               COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AVTS         3A  B  2 81DSPATR(UL)
     A  38
     AO 39                                  DSPATR(HI)
     A  38
     AO 97
     AO 39                                  DSPATR(PR)
     A N38N39                               COLOR(YLW)
     A                                  3  2'V.B'
     A            W2FT01        28A  B  3  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2NT01         6Y 0B  3 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH01         4A  B  3 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2VT01        30A  B  3 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A  38                              3 83'Extra Linje'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  4  2'T.O'
     A            W2TOP1        17A  B  4  6TEXT('TILLEGGSOPPLYSNING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A                                  4 52'Ref:'
     A            W2CRE1         3A  B  4 57TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
N03  A                                  4 61'LK_Oppr:'
N03  A            W2LK           2A  B  4 70DSPATR(UL)
N03  A  38
N03  AO 39                                  DSPATR(HI)
N03  A  96                                  DSPATR(RI)
N03  A  38
N03  AO 39                                  DSPATR(PR)
N03  A N38N39                               COLOR(YLW)
     A                                  4 77MSGCON(008 SHU1574 *LIBL/SYMSGF)
     A            XBEL3         13Y 2O  4 87COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  5  2'Fsum->'
     A            UBEL1         13Y 2O  5 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 27'Vsum->'
     A            W2VSUM        13Y 2O  5 35TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 52'Diff->'
     A            W2DIFF        13Y 2O  5 60TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  5 80'F11=Auto vekt'
     A                                      COLOR(BLU)
     A                                  5 95'F13=Opd.tar  F15=Auto T.O'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SAD052A'
     A                                      COLOR(BLU)
     A                                  1 21'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 39'Tdeknr.'
     A            UAVD           4Y 0O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 52'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 54COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            SVLI           5Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 92DATE
     A                                      EDTCDE(Y)
     A                                  1102TIME
     A                                  4  2'FK Varenr.. Tollverdi.... Brutto v-
     A                                      ...... Netto vekt.... Mengde.... FF-
     A                                       Sats.... Sekv.'
     A                                      COLOR(WHT)
     A            SVFYL          2A  O  5  2TEXT('OPPRINGELSESFYLKE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVVNT          8Y 0O  5  5TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTWRD('0       ')
     A            SVBELT        11Y 2O  5 14TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            SVVKTB        12Y 3O  5 28TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            SVVKTN        12Y 3O  5 43TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            SVNTM          9Y 0O  5 58TEXT('Mengde')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            SVAVT          1A  O  5 69TEXT('AVG. KONJUNKTURFOND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVAVTP         7Y 3O  5 72TEXT('PROSENTSATS AV SVAVT')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVAVTS         3A  O  5 81DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'V.B'
     A            SVFT01        28A  O  7  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT01         6Y 0O  7 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH01         4A  O  7 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVVT01        30A  O  7 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVFT02        28A  O  8  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT02         6Y 0O  8 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH02         4A  O  8 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVVT02        30A  O  8 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVFT03        28A  O  9  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT03         6Y 0O  9 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH03         4A  O  9 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVVT03        30A  O  9 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVFT04        28A  O 10  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT04         6Y 0O 10 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH04         4A  O 10 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVVT04        30A  O 10 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVFT05        28A  O 11  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT05         6Y 0O 11 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH05         4A  O 11 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVVT05        30A  O 11 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVFT06        28A  O 12  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT06         6Y 0O 12 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH06         4A  O 12 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVFT07        28A  O 13  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT07         6Y 0O 13 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVEH07         4A  O 13 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'T.O'
     A            SVTOP1        17A  O 15  6DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 25'Ref.'
     A            SVCRE1         3A  O 15 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 42'T.O'
     A            SVTOP2        17A  O 15 46DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 65'Ref.'
     A            SVCRE2         3A  O 15 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP3        17A  O 16  6DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE3         3A  O 16 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP4        17A  O 16 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE4         3A  O 16 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP5        17A  O 17  6DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE5         3A  O 17 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP6        17A  O 17 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE6         3A  O 17 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP7        17A  O 18  6DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE7         3A  O 18 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP8        17A  O 18 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE8         3A  O 18 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP9        17A  O 19  6DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE9         3A  O 19 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVTOP10       17A  O 19 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVCRE10        3A  O 19 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 26  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A                                      CF06(06 'LAGE NY')
     A    N36                               CF07(07 'SØKE OPPDRAG')
     A                                      CF09(09 'Kalkulator av/på')
     A                                      CF12(12 'Gå til bilde02')
     A                                      CF14(14 'SØK KUNDERS VAREREGISTER')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CA18(18 'UTVIDERT BILDE')
     A                                      CA19(19 'HENT FRA Z-FIL')
N04  A  51                                  CF22(22 'OMSTART OMBEREGING')
     A                                  1  2'SYSPED/SAD052A'
     A                                      COLOR(BLU)
     A                                  1 21'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
S04  A*                                     EDTCDE(4)
N04  A                                      EDTCDE(L)
     A                                  2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                      e'
     A                                      COLOR(BLU)
     A  39                              2 52'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              2 66'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A N32                              3  2'Op Linje FK Varenr.. Nettovekt....-
     A                                      . Mengde.... Tollverdi.... FF Sats.-
     A                                      ... Sekv. Varebeskrivelse..........-
S04  A*                                     ..... S'
N04  A                                      ..... S T'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn FK Varenr.. Nettovekt....-
     A                                      ... Mengde.. Tollverdi.... FF Sats.-
     A                                      ... Sekv. Varebeskrivelse..........-
S04  A*                                     ..... S'
N04  A                                      ..... S T'
     A                                      COLOR(WHT)
     A                                 23  2'Fsum->'
     A            UBEL1         13Y 2O 23 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23 27'Vsum->'
     A            W2VSUM        13Y 2O 23 35TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23 52'Diff->'
     A            W2DIFF        13Y 2O 23 60TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                 23 77MSGCON(008 SHU1574 *LIBL/SYMSGF)
     A            XBEL3         13Y 2O 23 87COLOR(WHT)
     A                                      EDTCDE(L)
N07  A                                 24  2'Tvkt->'
N07  A            SEVKB          9Y 0O 24 12TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                 24 27'Lvkt->'
N07  A            W2VKB         13Y 3O 24 35TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                 24 52'Diff->'
N07  A            W2VDIF        13Y 3O 24 60TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(L)
E07  A                                 24110'F19=Hent EDI-fil'
     A                                      COLOR(BLU)
     A                                 25  2'F14=Søking kunders vareregister  -
     A                                      register  F15=Auto kopi T.O.  F17=-
     A                                      Ny sad kopier fra denne  F18=Full -
     A                                       modus'
     A                                      COLOR(BLU)
     A                                 26  2'F3=Neste/avslutt'
     A  35                                  COLOR(RED)
     A N35                                  COLOR(BLU)
     A                                 26 20'F5=Forny  F6=Lage ny'
     A                                      COLOR(BLU)
     A N36                             26 42'F7=Se oppd'
     A                                      COLOR(BLU)
     A                                 26 54'F8=Hopp til  F9=Kalkulator  F12=F-
     A                                      orrige'
     A                                      COLOR(BLU)
N04  A  51                             26113'F22=Angre'
N04  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGFHOL)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                  2  2'Hopp til linje   -->'
     A            W3LN           5Y 0I  2 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'Hopp til varenr. -->'
     A            W3VNT          8Y 0B  3 24DSPATR(UL)
     A                                  4  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  4 24DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  6  2
     A            SVLI           5Y 0H
     A            SVREFL         5Y 0H
     A            WXLN           5Y 0O  6  5EDTCDE(4)
     A            SVFYL          2A  O  6 11TEXT('Varenr Tolltariff')
     A            SVVNT          8S 0O  6 14TEXT('Varenr Tolltariff')
     A            SVVKTN        12Y 3O  6 23TEXT('Vekt Netto')
     A                                      EDTCDE(L)
     A            SVNTM          9Y 0O  6 38TEXT('Mengde')
     A                                      EDTCDE(L)
     A            SVBELT        11Y 2O  6 49TEXT('Beløp Tollverdi')
     A                                      EDTCDE(L)
     A            SVAVT          1A  O  6 63
     A            SVAVTP         7Y 3O  6 66TEXT('PROSENTSATS AV SVAVF')
     A                                      EDTCDE(4)
     A            SVAVTS         3A  O  6 75
     A            SVVT01        30A  O  6 81TEXT('Varetekst')
     A  95                                  COLOR(YLW)
     A            SV01           1A  O  6112COLOR(RED)
     A                                      DSPATR(BL)
N08  A            SVNYL          1A  O  6114DSPATR(BL)
N08  A                                      COLOR(RED)
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0021)
     A                                      SFLPAG(0020)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD052A'
     A                                      COLOR(BLU)
     A                                  1 21'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
S04  A*                                     EDTCDE(4)
N04  A                                      EDTCDE(L)
     A                                  3  2'Trykk Enter for å bekrefte at du v-
     A                                      algte 4=Slette.'
     A                                      COLOR(BLU)
     A                                  4  2'Trykk F12 for å gå tilbake og endr-
     A                                      e valget.'
     A                                      COLOR(BLU)
     A N32                              5  2'Op Linje FK Varenr.. Nettovekt....-
     A                                      . Mengde... Tollverdi.... FF  Sats.-
     A                                      ... Sekv. Varebeskrivelse..........-
S04  A*                                     ..... S'
N04  A                                      ..... S T'
     A                                      COLOR(WHT)
     A  32                              5  2'Op Postn FK Varenr.. Nettovekt....-
     A                                      . Mengde... Tollverdi.... FF  Sats.-
     A                                      ... Sekv. Varebeskrivelse..........-
S04  A*                                     ..... S'
N04  A                                      ..... S T'
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R BEKREFT
     A                                      WINDOW(*DFT 6 40 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Hent varelinjer fra EDI-filer på n-
     A                                      ytt'
     A                                      COLOR(WHT)
     A                                  3  2'ENTER = Slette alle eksistrerende '
     A                                      COLOR(BLU)
     A                                  4  2'        linjer og hente linjene fr-
     A                                      a'
     A                                      COLOR(BLU)
     A                                  5  2'        EDI-filer på nytt!!'
     A                                      COLOR(BLU)
     A                                  6  2'F12   = Avbryt'
     A                                      COLOR(BLU)
N05   *
N05  A          R VISMELD
N05  A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
N05  A                                      OVERLAY
N05  A                                  2  2'Minst en linje med gull farge på'
N05  A                                      COLOR(YLW)
N05  A                                  3  2'tariff er ikke gjennomgått.'
N05  A                                      COLOR(YLW)
N05  A                                  4  2'Bruk opsjon 2 eller 8 på disse lin-
N05  A                                      jene.'
N05  A                                      COLOR(WHT)
