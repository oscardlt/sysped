********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 JOV PTF7 KAN OVERFØRE TIL EGET BIBLIOTEK
********************************************************************
     FECMSG     UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FIE001D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KNAVN                  1     30
     D  XDLT                   1      7
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     ECMSG
     C                   READ(N)   ECMSG                                  94
      *
     C     *IN94         DOWEQ     '0'
     C     EC0065        IFEQ      'SYSPED'
     C     CUN11K        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   MOVEL     ECPGM         XXKN
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   READ(N)   ECMSG                                  94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      *
     C                   SETOFF                                       9899
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *
      *** FRISK OPP
     C   05              GOTO      START
      *
      *** LAGE NY
     C   06              DO
     C                   MOVE      *ZEROS        WSCNO
     C                   MOVE      *BLANKS       WSNA
     C                   MOVE      *BLANKS       WSLIB
     C                   MOVE      *ZEROS        WSKN
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      '0'           *IN40
     C                   GOTO      START5
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     CUN12K        CHAIN     CUNDL01                            90
     C   90              MOVE      'SPK0001'     MSGNR
     C  N90ECM01K        CHAIN(N)  ECMSG                              33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'YBC0619'     MSGNR
     C                   END
     C   90              Z-ADD     22            LINNBR
     C   90              Z-ADD     5             POSNBR
     C   90              SETON                                        30
     C  N90              MOVE      KNAVN         WSNA
     C                   END
      *
     C                   MOVE      '0'           UFLAGG            1
     C                   CALL      'IE001C1'
     C                   PARM                    WSLIB
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      '1'
     C  N30              MOVE      'YBC0620'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     45            POSNBR
     C                   SETON                                        3089
     C                   END
N01  C     UFLAGG        IFEQ      '2'
N01  C                   MOVE      'J'           EGETBI            1
N01  C                   ELSE
N01  C                   MOVE      'N'           EGETBI            1
N01  C                   END
      *
     C     *IN30         IFEQ      '0'
     C                   MOVE      '0'           UFLAGG            1
     C                   MOVE      WSKN          UKN               8
     C                   CALL      'IE002C'
     C                   PARM                    WSLIB
     C                   PARM                    UKN
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      '1'
     C  N30              MOVE      'YBC0621'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     56            POSNBR
     C                   SETON                                        3088
     C                   END
     C                   END
      *
     C   30              GOTO      SLTB
      *
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     ECM01K        CHAIN     ECMSG                              33
     C                   EXSR      MOVWEC
     C  N33              UPDATE    ECMSGR
     C   33              WRITE     ECMSGR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
N01  C     EGETBI        IFEQ      'J'
N01  C                   SETON                                        30
N01  C                   MOVE      'JOV1228'     MSGNR
N01  C                   WRITE     BUNN
N01  C                   END
      *
     C     SLTB          ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGEC
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTEC
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTEC         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     ECM02K        CHAIN     ECMSG                              33
     C  N33              DELETE    ECMSGR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGEC         BEGSR
     C***********************************************
     C                   EXSR      MOVECW
     C                   SETON                                        9940
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              UPDATE    SUBFIL
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        ECCNO
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVEL     '*DELETE'     XDLT
     C                   MOVE      *BLANKS       ECLIB
     C                   MOVE      *BLANKS       ECPGM
     C                   MOVE      *ZEROS        XXKN
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVECW        BEGSR
     C***********************************************
     C                   MOVE      ECCNO         WSCNO
     C                   MOVE      KNAVN         WSNA
     C                   MOVE      ECLIB         WSLIB
     C                   MOVE      XXKN          WSKN
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     45            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWEC        BEGSR
     C***********************************************
     C     *IN33         IFEQ      '1'
     C                   CLEAR                   ECMSGR
     C                   MOVE      FIFIRM        ECFIR
     C                   MOVE      'S'           ECSR
     C                   MOVE      'SYSPED'      EC0065
     C                   END
      *
     C                   MOVE      WSCNO         ECCNO
     C                   MOVE      WSNA          KNAVN
     C                   MOVE      WSLIB         ECLIB
     C                   MOVE      *BLANKS       ECPGM
     C                   MOVE      WSKN          XXKN
     C                   MOVEL     XXKN          ECPGM
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    ECSR          YYSR
     C     *LIKE         DEFINE    EC0065        YY0065
     C     ECM01K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSCNO
     C                   KFLD                    YYSR
     C                   KFLD                    YY0065
     C     ECM02K        KLIST
     C                   KFLD                    ECFIR
     C                   KFLD                    ECCNO
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C     CUN11K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ECCNO
     C     CUN12K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSCNO
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      'S'           YYSR
     C                   MOVE      'SYSPED'      YY0065
      *
     C                   ENDSR
      *
