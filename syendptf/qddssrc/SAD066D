     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            SGKN           8Y 0O  5  5TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  5 14TEXT('Kundenavn')
     A            SGGKD          3A  O  5 45
     A            SGBEL          9Y 2O  5 49EDTCDE(4)
     A            SGMV           1A  O  5 60
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD066'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 16'Vedlikehold gebyr for spesifikasjo-
     A                                      n'
     A                                      COLOR(WHT)
     A                                  3  2'Velg opsjon:   2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op Kundenr. Kundenavn             -
     A                                               Kod Beløp      MV'
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Kundenr. Kundenavn             -
     A                                               Kod Beløp      MV'
     A                                      DSPATR(HI)
     A            WSKN           8Y 0B  2  5EDTCDE(Z)
     A N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            KNAVN         30A  O  2 14COLOR(WHT)
     A            WSGKD          3A  B  2 45
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(HI)
     A            WSBEL          9Y 2B  2 49EDTCDE(4)
     A            WSMV           1A  B  2 60VALUES('J' 'N')
     A                                  3  2'F4=Søk kundenr   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra kundenr:'
     A            W2KN           8S 0B  2 20DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'   Kundenr. Kundenavn             -
     A                                               Kod Beløp      MV'
     A            SGKN           8Y 0O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KNAVN         30   O  2 14COLOR(WHT)
     A            SGGKD          3A  O  2 45DSPATR(UL)
     A                                      DSPATR(HI)
     A            SGBEL          9Y 2O  2 49DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            SGMV           1A  O  2 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD066'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 16'Vedlikehold gebyr for spesifikasjo-
     A                                      n'
     A                                      COLOR(WHT)
     A                                  3  2'Velg opsjon:   2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op Kundenr. Kundenavn             -
     A                                               Kod Beløp      MV'
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
