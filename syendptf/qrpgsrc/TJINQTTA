      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQTTA MODULE(*LIBL/TJINQTTA *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKOFAST    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D SOKKOD          S              1
     D SOKNVN          S             10    varying
     D SOKNVN2         S             20
     D user            S             10
     D MaxV            S              5
     D MaxVis          S              5  0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D Error           S            150
     D JSONstring      s          32000

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬sokkod¬ : ¬'+%trim(SOKKOD)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"inqTTactList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C     ERROR         IFEQ      *blanks
     C                   EXSR      LINES
     C                   ENDIF
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         BEGSR
     C                   EVAL      user = zhbgetvar('user')
     C                   EVAL      SOKKOD = zhbgetvar('SOKKOD')
     C                   EVAL      MaxV    = zhbgetvar('MaxV')
     C                   EVAL      MaxVis  = c2n2(MaxV)
     C     MaxVis        IFEQ      *zeros
     C                   EVAL      MAXVIS=300
     C                   ENDIF
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KOFAST
     C                   EVAL      *INLR=*ON
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      * Find lins
      *=====================================================================
     C     LINES         BEGSR
     C                   MOVE      *zeros        AntVist           5 0
      * kommer inn med blank= vis alle - kjør 2 runmder/vis Z-koder først
     C                   IF        SOKKOD = '   '
     C                   EVAL      SOKKOD = 'Z  '
     C                   MOVE      '1'           Zforst            1
     C                   ENDIF
      *
     c     Lesmer        tag
     c                   eval      KFKOD=SOKKOD
     C     Key           SETLL     KOFAST
     C     KFTYP         READE     KOFAST                                 33
     C     *in33         DOWEQ     *OFF
      * ved vis alle - Z føst..
     c                   if        Zforst = '1'  and
     c                             %subst(KFKOD:1:1)<>'Z'
     c                   goto      nextrec
     c                   endif
     c                   if        Zforst = '2'  and
     c                             %subst(KFKOD:1:1)='Z'
     c                   goto      nextrec
     c                   endif

     c                   if        KFKOD <> ' DEFN'
     C                   ADD       1             AntVist           5 0
      /free
       if AntVist=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬kfkod¬ : ¬'+%trim(KFKOD)+'¬, '
          +'¬kftxt¬ : ¬'+%trim(%subst(KFTXT:1:49))+'¬, '
          +'¬kfekstern¬ : ¬'+%trim(%subst(KFTXT:50:1))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     c                   endif


     C     AntVist       CABGE     MaxVis        UtLes
     c     NextRec       tag
     C     KFTYP         READE     KOFAST                                 33
     C                   ENDDO
      *
     C                   IF        Zforst = '1'
     C                   EVAL      SOKKOD = '   '
     C                   MOVE      '2'           Zforst            1
     c                   goto      lesmer
     C                   ENDIF

     C     UTLES         TAG
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KOFAST

     c     Key           klist
     c                   kfld                    KFTYP
     c                   kfld                    KFKOD
     c                   eval      KFTYP= 'TRACKT'

     C                   ENDSR
