********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 HOPP OVER ALLE IKKE TVINN-IMP MELDINGER
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 02 YBC PTF11 OMBEREGNING
11XXX * 03 YBC PTF11 PRINTE FORTOLLING
11XXX * 04 YBC PTF11 FEILRETTING PÅ ENDRING 2
11109 * 05 YBC PTF11 OMEBEREGNING PÅ OMBEREGING
11XXX * 06 YBC PTF11 ETTER GODKJENNING AV OMB. KAN DET KOMME DTK.
11186 * 07 YBC PTF11 VIS TOTAL VEKT OG SUM AV LINJEVEKT
********************************************************************
      ***********************************************************
      * PROGRAM SKAL BE OM UTSKRIFT (SAD-EKSPORT)           ***
      * INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      * LEDIGE INDIKATORER 01 02 04-11 13-16 18-26          ***
      * LEDIGE INDIKATORER 27 28 30-32 35-98                ***
      ***********************************************************
     FSAEH      UF   E           K DISK
     FSAEV      IF   E           K DISK
     FEDIM1     UF A E           K DISK
     FEDIM2     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMR2)
     FEDIM4     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMR4)
     FEDIC      UF   E             DISK
     FTVIND     UF A E           K DISK
     FSTANDE    IF   E           K DISK
     FSYPTFF    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSAD052FT  CF   E             WORKSTN USROPN
     D                 DS
     D  XDTØÅ                  1      4  0
     D  XDTØM                  5      6  0
     D  XDTØD                  7      8  0
     D  SEDTØ                  1      8  0
     D                 DS
     D  X9DTØD                 1      2  0
     D  X9DTØM                 3      4  0
     D  X9DTØÅ                 5      6  0
     D  X9DTØ                  1      6  0
     D                 DS
     D  SEAVD                  1      4  0
     D  SETDN                  5     11  0
     D  SESG                  12     14
     D  X12S                   1     14
N02  D                 DS
N02  D  OM_SEAVD               1      4  0
N02  D  OM_SETDN               5     11  0
N02  D  OM_SESG               12     14
N02  D  X14SOM                 1     14
     D                 DS
     D  X0068Æ                 1      9  0
     D  X0068A                10     17  0
     D  X0068B                18     23  0
     D  X0068C                24     25  0
     D  X0068Å                 2     25
     D  XN068Å                 1     25
     D                 DS
     D  SYRG                   1     14
     D  SYFORE                 1      9
     D                 DS
     D  UAVD                   1      4  0
     D  UTDN                   5     11  0
     D  UWS                   13     22
     D  UTVIN                 23     23
      *** UIN34 = '1' -> SENDES PÅ TVINN
     D  UIN34                 25     25
     D  UDIFF                101    113  2
     D  UIN08                120    120
      * OBS! HUSK INITIERE ALLE FELTER PÅ SAD052
S07  D* UPARM2                 1    128
N07  D  UPARM2                 1    512
      *
     C                   OPEN      SAD052FT
     C                   EXSR      INIT
      *
     C     UDIFF         IFNE      0
N02  C     UDIFF         ANDNE     SEBEL1
N04  C     UDIFF         ORNE      0
N04  C     SETDN         ANDGT     0
     C     START         TAG
     C                   EXFMT     BILDE01
     C  N12
     CANN03
     CANN17              GOTO      START
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
     C                   END
      *
      *=========================================
      *
      *=========================================
     C     *IN08         IFEQ      '0'
N02  C                   IF        UTDN > 0 OR UDIFF <> SEBEL1
     C                   MOVE      *ZEROS        WSVKB
     C     SAEHK         SETLL     SAEV
     C     SAEHK         READE     SAEV                                   33
     C     *IN33         DOWEQ     '0'
N02  C                   IF        SVNYL <> 'S'
     C                   ADD       SVVKTB        WSVKB
N02  C                   END
     C     SAEHK         READE     SAEV                                   33
     C  N33              ENDDO
     C     SEVKB         SUB       WSVKB         WSVKTD
     C     WSVKTD        DIV       SEVKB         XVKTP            13 3
     C     XVKTP         IFLT      0
     C                   Z-SUB     XVKTP         XVKTP
     C                   END
     C     WSVKTD        IFGT      5
     C     WSVKTD        ORLT      -5
     C     XVKTP         ORGT      0.1
     C                   MOVE      'N'           WSJN
     C     START1        TAG
     C                   EXFMT     BILDE03
     C     *IN12         IFEQ      '1'
     C     WSJN          OREQ      'N'
     C                   MOVE      '0'           *IN03
     C                   Z-ADD     1             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C                   END
N02  C                   END
     C                   END
      *=========================================
      *
     C   34              DO
      *
      *** FINNER UT HVILKEN MEDLINGTYPE DETTE SKAL VÆRE  UT FRA DE
      *** SISTE LOGGELEMENTENE I EDIM4.
N02  C                   IF        UTDN < 0
N02  C                   EVAL      W1N07 = 'D  '
N02  C                   ELSE
     C                   EXSR      FINMTY
N02  C                   END
     C     START2        TAG
N02  C                   IF        SETDN < 0
N02  C                   EXFMT     BILDE04
N02  C                   ELSE
     C                   EXFMT     BILDE02
N02  C                   END
     C                   SETOFF                                       30
     C   12
     COR 03              GOTO      START3
      *
     C     *IN10         IFEQ      '1'
     C                   EXFMT     BILDE04
     C                   GOTO      START2
     C                   ENDIF
      *
     C                   EXSR      TESTB2
     C   30              GOTO      START2
     C                   END
      *
     C     START3        TAG
     C     SAEHK         CHAIN     SAEH                               33
     C  N12*IN03         IFEQ      '1'
     C     *IN34         OREQ      '0'
     C     UTVIN         OREQ      'J'
     C                   MOVE      'K'           SEST
     C                   MOVE      UWS           SEWS
     C                   ELSE
     C                   MOVE      'P'           SEST
     C                   MOVE      *BLANKS       SEWS
     C                   END
     C   12              MOVE      ' '           SEST
     C   03              DO                                                      2<-B
     C     SEMF          IFNE      'DTK'
     C                   MOVE      *BLANKS       SEMF
     C                   END
     C                   MOVE      *ZEROS        SEMP
     C                   MOVE      *ZEROS        X9DTØ
     C                   MOVE      *ZEROS        SEDTØ
     C                   MOVE      *ZEROS        SE3039
      *                    MOVE *ZEROS    SEBELD
     C                   END                                                     2<-E
      *
      *** OPPDATER KOMMUNIKASJONDATA PÅ SAD-REGISTER
     C  N12
     CANN03              DO                                                      2<-B
     C   34              DO                                                       3<-B
     C                   MOVE      W1N07         SEMF
     C                   MOVE      W9N01         SEMP
     C     W2005B        IFNE      0
     C                   MOVE      W2005B        X9DTØ
     C     X9DTØÅ        IFGT      50
     C     X9DTØÅ        ADD       1900          XDTØÅ
     C                   ELSE
     C     X9DTØÅ        ADD       2000          XDTØÅ
     C                   END
     C                   MOVE      X9DTØM        XDTØM
     C                   MOVE      X9DTØD        XDTØD
     C                   ELSE
     C                   MOVE      *ZEROS        SEDTØ
     C                   END
     C     UTVIN         IFNE      'J'                                             4<-B
     C                   MOVE      'Q'           SEST
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   MOVE      W5004D        SEBELD
     C                   MOVE      UTVIN         SEPKL
     C                   MOVE      WVEN          SEKDV
     C                   MOVE      W0035         SE0035
     C                   MOVE      W3039E        SE3039
     C                   END                                                     2<-E
     C   12              MOVE      'N'           SEPKL
     C   03              MOVE      'J'           SEPKL
     C  N03
     CANN12XBMI          IFEQ      'J'
     C                   MOVE      ' '           SEMI
     C                   END
     C                   UPDATE    SAEHR
      *
     C  N12
S03  C*ANN03
     CAN 34UTVIN         IFEQ      'J'                                           2<-B
     C                   CALL      'SAD52P01'
     C                   PARM                    UPRT1            10
     C                   PARM                    UPRT2            10
     C                   PARM                    UPRT3            10
     C     SAEHK         CHAIN     SAEH                               33
     C     *IN33         IFEQ      '0'
N03  C     *IN03         ANDEQ     '0'
     C                   MOVE      'Q'           SEST
     C                   UPDATE    SAEHR
     C                   ENDIF
     C                   END                                                     2<-E
      *
     C  N12
     CANN03
     CAN 34              EXSR      KOMLIN
     C                   MOVE      'J'           XWIN              1
     C   34
     CANN03
     CANN12              DO
     C                   WRITE     WINDOWSZ
     C                   EXFMT     BILDNOR
     C                   END
      *
     C     SLUTT         TAG
     C   03              Z-ADD     3             UFLAGG
     C   17              Z-ADD     2             UFLAGG
     C                   CLOSE     SAD052FT
      *                    SETON                     LR
N02  C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     FINMTY        BEGSR
      ***********************************************
     C                   MOVE      'N'           XBMI              1
     C                   MOVE      'N'           XINST             1
     C                   Z-ADD     3             W9N01
      *
     C     M2KDCK        SETLL     EDIM2                              33
     C     STFMTY        TAG
     C     M2KD2K        READPE    EDIM2                                  33
     C  N33M1001         IFEQ      '929'                                        1<-B
     C     MST           OREQ      'D'
     C     MSR           OREQ      'S'
     C     M1N07         OREQ      'DFI'
     C     M1N07         OREQ      'DII'
     C     M1N07         OREQ      'DPU'
     C     M1N07         OREQ      'DKO'
N01  C     M0068C        OREQ      0
N01  C     M0068C        CABEQ     0             STFMTY
S01  C*    M1001         CABEQ     'TET'         STFMTY
S01  C*    M1001         CABEQ     'TEI'         STFMTY
     C     MSR           IFEQ      'S'
     C     XINST         ANDEQ     'N'
     C     M1N07         IFEQ      'DMA'
     C     M1N07         OREQ      'DEN'
      * DENNE TOLLDEK. KAN VÆRE INNSTIKK HVIS MELDING FØR DENNE
      * ER EN 'DUA' FRA TOLLVESENET.
     C                   MOVE      'J'           XINST             1
     C                   END
     C                   END
     C                   GOTO      STFMTY
     C                   ELSE                                                   1
     C     M1N07         IFEQ      'DME'                                         2<-B
     C                   MOVE      'DKO'         W1N07
     C                   ELSE                                                    2
     C     M1N07         IFEQ      'DUA'                                          3<-B
     C     SEMI          IFEQ      'I'                                             4<-B
     C     XINST         OREQ      'J'
     C                   MOVE      'DEN'         W1N07
     C                   MOVE      'J'           XBMI
     C                   ELSE                                                      4
     C                   MOVE      'DKO'         W1N07
     C                   END                                                       4<-E
     C                   ELSE                                                     3
     C     M1N07         IFEQ      'DIU'                                           4<-B
     C                   MOVE      '1'           *IN33
     C                   MOVE      '1'           *IN99
     C                   ELSE                                                      4
S02  C*    M1N07         IFEQ      'DTK'                                            5<-B
S02  C*                  MOVE      'DOM'         W1N07
S02  C*                  ELSE                                                       5
     C                   MOVE      '1'           *IN33
S02  C*                  END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C   33
     CAN 08              MOVE      'DFO'         W1N07
     C   33
     CANN08SEDP          IFEQ      10
     C                   MOVE      'DFU'         W1N07
     C                   ELSE
     C                   MOVE      'DMA'         W1N07
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTB2        BEGSR
      ***********************************************
N02  C                   IF        SETDN > 0
     C     W1N07         COMP      'DFU'                              3030
     C   30W1N07         COMP      'DMA'                              3030
     C   30W1N07         COMP      'DKO'                              3030
     C   30W1N07         COMP      'DFO'                              3030
     C   30W1N07         COMP      'DEN'                              3030
N02  C                   ELSE
N02  C     W1N07         COMP      'DEB'                              3030
N02  C   30W1N07         COMP      'DRE'                              3030
N02  C   30W1N07         COMP      'DSO'                              3030
N02  C  N30              IF        W3039E = 0
N02  C                   SELECT
N02  C                   WHEN      W1N07  = 'DSO'
N02  C                   EVAL      W3039E = S3039EO1
N02  C                   WHEN      W1N07  = 'DRE'
N02  C                   EVAL      W3039E = S3039EO2
N02  C                   WHEN      W1N07  = 'DEB'
N02  C                   EVAL      W3039E = S3039EO2
N02  C                   END
N02  C                   END
N02  C                   END
      *
     C     SLTB2         ENDSR
      *
      ***********************************************
     C     KOMLIN        BEGSR
      ***********************************************
      *
      * HENT SISTE DATO/SEKVENSNR./VERSJONSNR.
      *   1. HVIS DET FINNES OG IKKE ER 'IU', ØKER VERSJONSNR. MED 1
      *   2. HVIS DET IKKE PKT.1, HENTER NY DATO/SEKV.NR./VERSJONSNR.
     C     MAKDCK        SETLL     EDIM4                              31
      * FOR Å FÅ UNIK NUMMERERING PÅ ALLE TVINNSAKER SKAL VERSJ.NR ØKER
      * ALLTID (OGSÅ VED FEIL I INP.KTR.!!! - NÅR NY SAK I TVINN (ELLER
      * BARE DFI HITTIL SKAL NY DATO/SEKV.NR./V.NR. ALLTID TILDELES))
     C                   MOVE      *ZEROS        Æ0068C            2 0
     C     STKL          TAG
     C     MAKD2K        READPE    EDIM4                                  31
     C  N31M0065         IFNE      'CUSDEC'                                     1<-B
     C     M0065         ANDNE     'CUSRES'
     C                   GOTO      STKL
     C                   END                                                    1<-E
     C  N31MST           IFEQ      'D'                                          1<-B
     C     M1001         OREQ      '929'
S01  C*    M1001         OREQ      'TET'
S01  C*    M1001         OREQ      'TEI'
N01  C     M0068C        OREQ      0
     C                   GOTO      STKL
     C                   END                                                    1<-E
N05  C  N31              IF        UTDN < 0 AND
S06  C*                            (M1N07='DSO' OR M1N07='DEB' OR M1N07='DRE')
N06  C                             (M1N07='DSO' OR M1N07='DEB' OR M1N07='DRE'
N06  C                              OR M1N07='DTK')
N05  C                   EVAL      *IN31 = *ON
N05  C                   END
     C  N31Æ0068C        IFEQ      0                                            1<-B
     C                   MOVE      M0068C        Æ0068C
     C                   END                                                    1<-E
     C  N31M1N07         IFEQ      'DFI'                                        1<-B
     C     M1N07         OREQ      'DII'
     C     M1N07         OREQ      'DPU'
     C                   GOTO      STKL
     C                   END                                                    1<-E
     C  N31M1N07         IFEQ      'DIU'                                        1<-B
     C                   MOVE      '1'           *IN31
     C                   ELSE                                                   1
N02  C                   IF        MTDN < 0
N02  C     Æ0068C        ADD       2             M0068C
N02  C                   ELSE
     C     Æ0068C        ADD       1             M0068C
N02  C                   END
     C                   END                                                    1<-E
      *
      *** DET FINNES IKKE TAD-SVAR DATO/SEKVENS/VERSJON MÅ KONSTRUERES
      *** ORDNE DEKLARANTENS DATO
     C   31              DO
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           M0068A
      *** HENT SEKVENSNR.
     C     TVINDK        CHAIN     TVIND                              32
     C   32              Z-ADD     M0068A        D0068A
     C   32              Z-ADD     0             D0068B
     C                   ADD       1             D0068B
     C   32              WRITE     TVINDF
     C  N32              UPDATE    TVINDF
     C                   MOVE      D0068B        M0068B
      *** VERSJONSNR. SETTES LIK 1.
     C                   Z-ADD     1             M0068C
     C                   END
      *
      *** HENTE MELDINGSNR
     C     1             CHAIN     EDIC                               32
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EXSR      MOVKD1
     C     TVIM1K        SETLL     EDIM1                              32
     C     STKL3         TAG
     C     TVIM1K        READE     EDIM1                                  32
     C  N32M0065         IFEQ      'CUSDEC'
     C     M1001         ANDEQ     '830'
     C                   DELETE    EDIMR
     C                   ELSE
     C                   UPDATE    EDIMR
     C                   GOTO      STKL3
     C                   END
     C                   EXSR      MOVKD2
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVKD1        BEGSR
      ***********************************************
     C                   MOVE      M0068A        X0068A
     C                   MOVE      M0068B        X0068B
     C                   MOVE      M0068C        X0068C
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVKD2        BEGSR
      ***********************************************
     C                   MOVE      X0004         M0004
     C                   MOVE      X0010         M0010
     C                   MOVE      W0035         M0035
     C                   MOVE      *BLANKS       M0062
N08  C                   IF        SETDN < 0
N08  C                   EVAL      OM_SEAVD = SEAVD
N08  C                   EVAL      OM_SETDN = SETDN * -1
N08  C                   EVAL      OM_SESG  = SESG
N08  C                   EVAL      M0062 = X14SOM
N08  C                   ELSE
     C                   MOVEL     X12S          M0062
N08  C                   END
     C                   MOVE      'CUSDEC'      M0065
     C                   MOVE      X0068A        M0068A
     C                   MOVE      X0068B        M0068B
     C                   MOVE      X0068C        M0068C
     C                   MOVE      *BLANKS       M0068
     C     PTFDT         IFGT      M0068A
     C                   MOVE      X3039D        X0068Æ
     C                   MOVEL     X0068Å        M0068
     C                   ELSE
     C                   MOVE      SYFORE        X0068Æ
     C                   MOVEL     XN068Å        M0068
     C                   END
     C                   MOVE      *ZEROS        M0068D
     C                   MOVE      *ZEROS        M0068E
     C                   MOVE      *ZEROS        M0068F
     C                   MOVE      '830'         M1001
     C                   MOVE      *BLANKS       M1004
     C                   SELECT
     C     W1N07         WHENEQ    'DFU'
     C                   MOVEL     '9  '         M1225
     C     W1N07         WHENEQ    'DMA'
     C                   MOVEL     'ZZ '         M1225
     C     W1N07         WHENEQ    'DFO'
     C                   MOVEL     '14 '         M1225
     C     W1N07         WHENEQ    'DEN'
     C                   MOVEL     '2  '         M1225
     C     W1N07         WHENEQ    'DKO'
     C                   MOVEL     '4  '         M1225
S02  C*    W1N07         WHENEQ    'DOM'
N02  C     W1N07         WHENEQ    'DEB'
     C                   MOVEL     '18 '         M1225
N02  C     W1N07         WHENEQ    'DRE'
N02  C                   MOVEL     '19 '         M1225
N02  C     W1N07         WHENEQ    'DSO'
N02  C                   MOVEL     '43 '         M1225
     C                   ENDSL
     C                   MOVE      SEDTØ         M2005B
     C                   MOVE      X3039D        M3039D
     C                   MOVE      W3039E        M3039E
     C                   MOVE      *ZEROS        M5004D
     C                   MOVE      W1N07         M1N07
     C                   MOVE      *BLANKS       M1N08
      *
      *** SJEKK AT BEHANDLINGSPRIORITET ER INNENFOR INTERVALLET
     C     W9N01         IFLT      1
     C                   Z-ADD     1             M9N01
     C                   ELSE
     C     W9N01         IFGT      4
     C                   Z-ADD     4             M9N01
     C                   ELSE
     C                   MOVE      W9N01         M9N01
     C                   END
     C                   END
      *
     C                   MOVE      *ZEROS        MSN
     C                   Z-ADD     CMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVE      *BLANKS       MST
     C                   MOVE      *ZEROS        MDT
     C                   MOVE      *ZEROS        MTM
     C                   MOVE      SEAVD         MAVD
     C                   MOVE      SETDN         MTDN
     C                   MOVE      *ZEROS        MFFBNR
     C                   MOVE      WVEN          MVEN
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *LIKE         DEFINE    MSR           XSR1
     C     *LIKE         DEFINE    MSR           X1STA
     C     *LIKE         DEFINE    M0068A        MAX0B
     C     *LIKE         DEFINE    M0068B        MAX0C
     C     *LIKE         DEFINE    M0068C        MAX0D
     C     *LIKE         DEFINE    S0004         X0004
     C     *LIKE         DEFINE    S0010         X0010
     C     *LIKE         DEFINE    S3039D        X3039D
     C     SAEHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     MAKDCK        KLIST
     C                   KFLD                    XSR1
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    MAX0B
     C                   KFLD                    MAX0C
     C                   KFLD                    MAX0D
     C     MAKD2K        KLIST
     C                   KFLD                    XSR1
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     M2KDCK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    MAX0B
     C                   KFLD                    MAX0C
     C                   KFLD                    MAX0D
     C     M2KD2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     TVIM1K        KLIST
     C                   KFLD                    X1STA
     C                   KFLD                    SEAVD
     C                   KFLD                    SETDN
     C     TVINDK        KLIST
     C                   KFLD                    M0068A
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    UAVD
     C                   MOVE      'A'           KOAUNI
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KOAKNR
      *
     C     *ENTRY        PLIST
S07  C*    UPARM2        PARM                    UPARM1          128
N07  C     UPARM2        PARM                    UPARM1          512
     C                   PARM                    UFLAGG            1 0
     C                   MOVE      UIN34         *IN34
     C                   MOVE      UIN08         *IN08
      *
     C   08              Z-ADD     3             UFLAGG
     C                   MOVE      'R'           XSR1
     C                   MOVE      *ZEROS        M0068A
     C                   MOVE      '99999999'    MAX0B
     C                   MOVE      '999999'      MAX0C
     C                   MOVE      '99'          MAX0D
      *
     C     UAVD          CHAIN     STANDE                             33
     C                   MOVE      S0035         W0035
     C                   MOVE      S0004         X0004
     C                   MOVE      S0010         X0010
     C                   MOVE      S3039D        X3039D
N02  C                   IF        UTDN > 0
     C                   MOVE      S3039E        W3039E
N02  C                   END
     C                   MOVE      *ZEROS        W2005B
     C                   MOVE      *ZEROS        W5004D
     C                   MOVE      *BLANKS       WVEN
     C     'TVI00004'    CHAIN     SYPTFF                             33
     C   33              MOVE      *ZEROS        PTFDT
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C     PTFDT         IFLE      WDT80
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     KODTAK        CHAIN     KODTA                              33
     C     CUND1K        CHAIN     CUNDL01                            33
     C                   END
     C                   MOVE      *ZEROS        XN068Å
     C     SAEHK         CHAIN(N)  SAEH                               33
N02  C     SETDN         COMP      0                                    39
      *
     C                   MOVE      '0'           *IN03
     C                   MOVE      '0'           *IN12
     C                   MOVE      '0'           *IN17
      *
     C                   ENDSR
      *
