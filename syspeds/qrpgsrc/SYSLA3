      *********************************************************************
      * SPØRRING PÅ LAGERPOSTER
      * PROGRAM BEHANDLER INPUT FRA SYSLA2 (VISER PL-HEADING) OG VISE LINJER
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYSLA3 MODULE(*LIBL/SYSLA3
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FLLLINE    IF   E           K DISK    USROPN
     FLLHEAD    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSMN            s              7
     D KUNDEREF1       S             50
     D KUNDEREF2       S             50
     D KUNDEREF3       S             50
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D TITLE           C                   CONST('Resultat av søk i historikk -
     D                                     (plukkliste detalje)')
     D TITLEEN         C                   CONST('Result of query archives -
     D                                     (packinglist details)')
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C                   MOVE      BIBESK        WINT              3
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTLIN
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   eval      WSMN    = zhbgetvar('serno')
     C                   EVAL      LHMN    = c2n2(WSMN)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':TITLEEN)
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':TITLE)
     C                   END
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('WSNA':KNAVN)
     C                   callp     updhtmlvar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      * MOTTAKER
     C     LHBNR         ifeq      *blanks
     C                   callp     updhtmlvar('WSBNR':spaces)
     C                   else
     C                   callp     updhtmlvar('WSBNR':LHBNR)
     C                   endif
     C     LHBNA         ifeq      *blanks
     C                   callp     updhtmlvar('WSBNA':spaces)
     C                   else
     C                   callp     updhtmlvar('WSBNA':LHBNA)
     C                   endif
      * TRANS.DATO
     C     LHTDT         ifeq      *ZEROS
     C                   callp     updhtmlvar('WSTDT':spaces)
     C                   else
     C                   callp     updhtmlvar('WSTDT':
     C                             %trim(%editw(LHTDT:'    .  .  ')))
     C                   endif
      * KUNDEREF.1
     C                   callp     updhtmlvar('KUNDEREF1':KUNDEREF1)
     C     LHPAK         ifeq      *blanks
     C                   callp     updhtmlvar('WSPAK':spaces)
     C                   else
     C                   callp     updhtmlvar('WSPAK':LHPAK)
     C                   endif
      * PLUKKLISTENR
     C                   callp     updhtmlvar('WSALN':
     C                             %trim(%editC(LHALN:'Z')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
      * REKORDNR
     C                   callp     updhtmlvar('LLMN':
     C                             %trim(%editC(LLMN:'Z')))
      * LINJE NØKKEL
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   MOVEL     LLMN          LLKEY            10
     C                   MOVE      LLVN          LLKEY
     C                   callp     updhtmlvar('LLKEY':LLKEY)
      * ARTIKKELNR
     C                   callp     updhtmlvar('LLANR':LLANR)
      * VAREBESKRIVELSE
     C                   callp     updhtmlvar('LLVB':LLVB)
      * TOTAL STYKK
     C                   callp     updhtmlvar('LLWNT0':
     C                             %trim(%editc(LLWNT0:'Z')))
      * LØSE STYKK
     C                   callp     updhtmlvar('LLWNTL':
     C                             %trim(%editc(LLWNTL:'Z')))
      * ESKE
     C                   callp     updhtmlvar('LLWNT1':
     C                             %trim(%editc(LLWNT1:'Z')))
      * KARTONG
     C                   callp     updhtmlvar('LLWNT2':
     C                             %trim(%editc(LLWNT2:'Z')))
      * PALL
     C                   callp     updhtmlvar('LLWNT3':
     C                             %trim(%editc(LLWNT3:'Z')))
      * TOTAL VEKT
     C                   callp     updhtmlvar('LLWVEK':
     C                             %trim(%editc(LLWVEK:'3')))
      * TOTAL KUBIKK
     C                   callp     updhtmlvar('LLWKUB':
     C                             %trim(%editc(LLWKUB:'3')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     begsr
     C                   move      'N'           selected          1
     C                   move      'Y'           selected
     C                   IF        *IN33 AND *IN33 = *OFF
     C                   MOVE      'N'           SELECTED
     C                   END
      *
     C     RecordSltX    tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   IF        XSPRÅK = 'EN'
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYSLA3EN.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   ELSE
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYSLA3.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTLIN       BEGSR
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C     LL01K         SETLL     LLLINE
     C     LL01K         READE     LLLINE                                 50
      *
     C     *in50         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTPL
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTPL
     C                   endif
      *
     C     LL01K         READE     LLLINE                                 50
     C                   ENDDO                                                  *hival
      *
     C     SLHENTPL      ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   open      BRPARF                               50
     C                   OPEN      CUNDL01
     C                   open      LLHEAD                               50
     C                   open      LLLINE                               50
      *
     c     LL01K         klist
     c                   kfld                    LHMN
     c     LH00K         klist
     c                   kfld                    LHMN
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     CUNDL01K      CHAIN     CUNDL01                            50
      * HENT LEDERTEKST
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF1'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF1 = 'Kunderef.1'
     C                   ELSE
     C                   EVAL      KUNDEREF1 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF2'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF2 = 'Kunderef.2'
     C                   ELSE
     C                   EVAL      KUNDEREF2 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF3'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF3 = 'Kunderef.3'
     C                   ELSE
     C                   EVAL      KUNDEREF3 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   MOVE      'Ingen '      XINGEN            6
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   MOVE      'No    '      XINGEN
     C                   END
      *
     C     LH00K         CHAIN     LLHEAD                             50
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     LLHEAD
     C                   close     LLLINE
     C                   close     BRPARF
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
