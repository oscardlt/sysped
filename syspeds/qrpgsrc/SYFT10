********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 overlay og brukerid innført
10XXX * 02 JOV PTF10 Overgang til styring kunde/avd til saksbehandler
10XXX * 03 SH  PTF10 Egen printerstyring
10XXX * 04 SH  PTF11 Viser om egen printerstyring
10XXX * 05 SH  PTF11 utgangspunkt fra avdelingen
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11105 * 06 YBC PTF11 EPOSTADRESSE FOR MOTTAK AV TVINN-MELDING
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR) SAD  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97       ***
      **************************************************************
      *
     Fsysthlp   IF   E             DISK
N04  FKODTPUSR  IF   E           K DISK
     FKODTSI    UF A E           K DISK
     FSYFT10FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KSINAV                 1     16
     D  XDLT                   1      7
n05  D                UDS
n05  D  UAVD                  15     18
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2SIG         YSIG              3
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSI1        SETLL     KODTSI
     C     START1        TAG
     C                   READ      KODTSI                                 94
     C  N94              UPDATE    KOSIR
     C  N94              MOVE      KSISIG        YSIG
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
n04  c                   move      *blanks       ksegen
n04  C     KSISIG        CHAIN     KODTPUSR                           55
n04  c  N55              move      'JA'          ksegen
     C                   WRITE     SUBFIL
     C                   READ      KODTSI                                 94
     C  N94              UPDATE    KOSIR
     C  N94              MOVE      KSISIG        YSIG
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     c   01              do
     c                   movel     'syft10'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     c   01              do
     c                   movel     'syft10'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
s01  C*                  Z-ADD     17            POSNBR
n01  C                   Z-ADD     8             POSNBR
     C                   MOVE      *BLANKS       WSISIG
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSI3        SETLL     KODTSI
     C  N94              READ      KODTSI                                 94
     C  N94              UPDATE    KOSIR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
N04  C     OPSJON        IFEQ      'J'
N04  C                   GOTO      START
N04  C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     c   01              do
     c                   movel     'syft10'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START5
     c                   end
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     c   01              do
     c                   movel     'syft10'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START6
     c                   end
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSISIG        COMP      *BLANKS                                90
     C   90              MOVE      'SPS0024'     MSGNR
     C  N90KODSIK        CHAIN     KODTSI                             33
     C  N90
     CANN33              DO
     C                   UPDATE    KOSIR
     C                   SETON                                        90
     C                   MOVE      'SPS0025'     MSGNR
     C                   END
s01  C*  90              Z-ADD     17            POSNBR
n01  C   90              Z-ADD     8             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSINAV        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPN0010'     MSGNR
     C   89
s01  C*NN30              Z-ADD     39            POSNBR
n01  CANN30              Z-ADD     13            POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     KODTSI                             33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    KOSIR
     C   33              WRITE     KOSIR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
N04  C                   MOVE      'N'           OPSJON            1
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
N04  C  N98              MOVE      'J'           OPSJON            1
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
      * endrer printerstyring
N03  C     WSNR          IFEQ      'P'                                          SLETTE
N03  C                   MOVE      ' '           WSNR
N03  C     XDLT          COMP      '*DELETE'                              98
N03  C   98              GOTO      SLHENT
N03  C                   EXSR      PRTSR
N03  C   12              GOTO      SLHENT
N03  C                   END
      * slette printerstyring
N03  C     WSNR          IFEQ      'S'                                          SLETTE
N03  C                   MOVE      ' '           WSNR
N03  C     XDLT          COMP      '*DELETE'                              98
N03  C   98              GOTO      SLHENT
N03  C                   EXSR      SLESR
N03  C   12              GOTO      SLHENT
N03  C                   END
N02  C     WSNR          IFEQ      '6'
N02  C                   MOVE      ' '           WSNR
N02  C     XDLT          COMP      '*DELETE'                              98
N02  C   98              GOTO      SLHENT
N02  C                   Call      'SAD105S'
N02  c                   parm      KSISIG        xxsig             3
N02  c                   parm      'N'           xxallws           1
N02  c                   parm      'J'           xxupdate          1
N02  C                   UPDATE    SUBFIL
N02  C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      KSISIG        WSISIG
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSIK        CHAIN     KODTSI                             33
     C  N33              DELETE    KOSIR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KSISIG
     C                   MOVE      *BLANKS       KSINAV
N01  C                   MOVE      *BLANKS       KSOVL
N01  C                   MOVE      *BLANKS       KSUSER
N06  C                   MOVE      *BLANKS       KSIXXX
     C                   MOVEL     '*DELETE'     XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSISIG
     C                   MOVE      *BLANKS       WSINAV
N01  C                   MOVE      *BLANKS       WSOVL
N01  C                   MOVE      *BLANKS       WSUSER
N06  C                   MOVE      *BLANKS       WSEP
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      KSISIG        WSISIG
     C                   MOVE      KSINAV        WSINAV
N01  C                   MOVE      KSOVL         WSOVL
N01  C                   MOVE      KSUSER        WSUSER
N06  C                   MOVE      KSIXXX        WSEP
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
s01  C*                  Z-ADD     39            POSNBR
n01  C                   Z-ADD     13            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      YSIUNI        KSIUNI
     C                   MOVE      WSISIG        KSISIG
     C                   END
     C*
     C                   MOVE      WSINAV        KSINAV
N01  C                   MOVE      WSOVL         KSOVL
N01  C                   MOVE      WSUSER        KSUSER
N06  C                   MOVE      WSEP          KSIXXX
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODSIK        KLIST
     C                   KFLD                    YSIUNI
     C                   KFLD                    WSISIG
     C     KODSI1        KLIST
     C                   KFLD                    YSIUNI
     C                   KFLD                    W2SIG
     C     KODSI3        KLIST
     C                   KFLD                    YSIUNI
     C                   KFLD                    YSIG
     C*
     C                   MOVE      'SI'          YSIUNI            2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
N03  C*
N03  C***********************************************
N03  C     PRTSR         BEGSR
N03  C***********************************************
N05  C                   move      uavd          uuavd
N03  C                   SETON                                        99
N03  C                   CALL      'SYFA28U'
N03  C                   PARM                    KSISIG
N05  C                   PARM                    UUAVD             4
N03  C     XLIN          IFEQ      0
N03  C                   Z-ADD     14            LINNBR
N03  C                   ELSE
N03  C                   Z-ADD     XLIN          LINNBR
N03  C                   END
N03  C                   UPDATE    SUBFIL
N03  C*
N03  C                   ENDSR
     C*
N03  C*
N03  C***********************************************
N03  C     SLESR         BEGSR
N03  C***********************************************
N03  C                   SETON                                        99
N03  C                   CALL      'SYFA28S'
N03  C                   PARM                    KSISIG
N03  C     XLIN          IFEQ      0
N03  C                   Z-ADD     14            LINNBR
N03  C                   ELSE
N03  C                   Z-ADD     XLIN          LINNBR
N03  C                   END
N03  C                   UPDATE    SUBFIL
N03  C*
N03  C                   ENDSR
     C*
