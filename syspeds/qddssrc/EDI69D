     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R EDI69DA
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 33MSGCON(018 EDI0269 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73'EDI69D A'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(028 EDI0603 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  6MSGCON(034 EDI0271 *LIBL/SYMSGF)
     A            M0068A    R     D  B  5 43
     A  31                                  COLOR(RED)
     A                                  6  8MSGCON(032 EDI0272 *LIBL/SYMSGF)
     A            M0068B    R     D  B  6 43COMP(NE 0)
     A                                  7 10MSGCON(030 EDI0273 *LIBL/SYMSGF)
     A            M0068C    R     D  B  7 43COMP(NE 0)
     A                                  8  6'Senders identification . . . . . :'
     A            M0004     R        B  8 43
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                  9  6MSGCON(034 EDI0274 *LIBL/SYMSGF)
     A            M0010     R        B  9 43
     A  33                                  DSPATR(PC)
     A  33                                  COLOR(RED)
     A                                 10  6MSGCON(034 EDI0275 *LIBL/SYMSGF)
     A            M0035     R        B 10 43VALUES(' ' '1' '2')
     A                                 11  6MSGCON(034 EDI0277 *LIBL/SYMSGF)
     A            M0062     R        O 11 43
     A                                 12  6MSGCON(034 EDI0278 *LIBL/SYMSGF)
     A            M0065     R        B 12 43VALUES('CUSDEC' 'CUSRES')
     A                                 13  6MSGCON(034 EDI0279 *LIBL/SYMSGF)
     A            M2005B    R     D  B 13 43
     A  34                                  DSPATR(PC)
     A  34                                  COLOR(RED)
     A                                 14  8MSGCON(032 EDI0272 *LIBL/SYMSGF)
     A            M0068E    R     D  B 14 43
     A                                 15 10MSGCON(030 EDI0273 *LIBL/SYMSGF)
     A            M0068F    R     D  B 15 43
     A                                 16  6MSGCON(034 EDI0282 *LIBL/SYMSGF)
     A            M0068D    R     D  B 16 43
     A  35                                  DSPATR(PC)
     A  35                                  COLOR(RED)
     A                                 17  6MSGCON(034 EDI0283 *LIBL/SYMSGF)
     A            M3039D    R     D  B 17 43COMP(NE 0)
     A                                 18  6MSGCON(034 EDI0284 *LIBL/SYMSGF)
     A            M3039E    R     D  B 18 43COMP(NE 0)
     A                                 19  6MSGCON(034 EDI0285 *LIBL/SYMSGF)
     A            M5004D    R     D  B 19 43
     A                                 20 72MSGCON(006 EDI0286 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(012 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI69DB
     A                                      OVERLAY
     A                                      ROLLDOWN(26)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 32MSGCON(018 EDI0288 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 73'EDI69D B'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(028 EDI0603 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  4MSGCON(033 EDI0290 *LIBL/SYMSGF)
     A            M1N07     R        B  5 40COMP(NE ' ')
     A                                  6  6MSGCON(031 EDI0291 *LIBL/SYMSGF)
     A            M1N08     R        B  6 40
     A                                  7  4MSGCON(033 EDI0292 *LIBL/SYMSGF)
     A            M9N01     R     D  B  7 40RANGE(1 5)
     A                                  8  4MSGCON(033 EDI0293 *LIBL/SYMSGF)
     A            MSN       R        O  8 40EDTCDE(4)
     A                                  9  6MSGCON(031 EDI0294 *LIBL/SYMSGF)
     A            MMN       R        O  9 40EDTCDE(4)
     A                                 10  4MSGCON(033 EDI0295 *LIBL/SYMSGF)
     A            MSR       R        O 10 40
     A                                 11  4'Status  . . . . . . . . . . . . :'
     A            MST       R        O 11 40
     A                                 12  6MSGCON(031 EDI0297 *LIBL/SYMSGF)
     A            MDT       R        O 12 40
     A                                 13  8MSGCON(029 EDI0298 *LIBL/SYMSGF)
     A            MTM       R        O 13 40EDTWRD('  :  :  ')
     A                                 14  4MSGCON(033 EDI0299 *LIBL/SYMSGF)
     A            MAVD      R        O 14 40
     A                                 15  6MSGCON(031 EDI0300 *LIBL/SYMSGF)
     A            MTDN      R        O 15 40
     A                                 16  4MSGCON(033 EDI0301 *LIBL/SYMSGF)
     A            MVEN      R        B 16 40
     A                                 21 72MSGCON(005 EDI0302 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI69DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
