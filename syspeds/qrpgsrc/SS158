      *********************************************************************
      * Endre artikkel
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS158 MODULE(*LIBL/SS158
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSORF    UF   E           k DISK    USROPN
     FGSSLGF    O    E             DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSUPD           S              1
     D WKEY            S            256
     D WSTYY           S              4
     D W2TYY           S              4  0
     D WSTWW           S              2
     D W2TWW           S              2  0
     D WSONO           S              7  0
     D lstbrd          s              1
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D I               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D XXTXT01         C                   CONST('Trans.week is changed at')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D WORD                    1    700
     D WSORD                   1    700    DIM(100)
     D                 DS
     D ORFT4                   1     30
     D ORAAR                   4      7
     D ORUKE                   8      9
     D ORAARUKE                4      9
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     c     WSUPD         IFEQ      'J'
     c                   exsr      OPPDAT
     c                   GOTO      SLUTT
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * UPPDATER TRANSPORT ÅR OG UKE
      *=====================================================================******
     C     OPPDAT        begsr
      *
     C                   IF        ((W2TYY <> 0) AND (W2TWW <> 0))
     C     1             DO        100           XN                3 0
     C                   IF        WSORD(XN) = *BLANKS
     C                   LEAVE
     C                   END
     C                   eval      WSONO   = c2n2(WSORD(XN))
     C     WSONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   EXSR      OPDLOGG
     C                   MOVEL     W2TYY         ORAAR
     C                   MOVE      W2TWW         ORUKE
     C                   UPDATE    GSSORFR
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      *
     C                   endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   MOVE      W2TYY         XXTYY             4
     C                   MOVE      W2TWW         XXTWW             2
     C                   IF        ((ORAAR <> XXTYY) OR (ORUKE <> XXTWW))
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS158'       LGPGM
     C                   Z-ADD     5             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      WSONO         LGONO
     C                   MOVE      *ZEROS        LGLNO
     C                   EVAL      LGTXT = XXTXT01
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGANY = 'To:'
     C                   CAT       XXTYY:1       LGANY
     C                   CAT       '.':0         LGANY
     C                   CAT       XXTWW:0       LGANY
     C                   EVAL      LGAGM = 'From:'
     C                   CAT       ORAAR:1       LGAGM
     C                   CAT       '.':0         LGAGM
     C                   CAT       ORUKE:0       LGAGM
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WKEY    = zhbgetvar('WKEY')
     C                   EVAL      WORD    = zhbgetvar('WORD')
     C                   EVAL      WSUPD   = zhbgetvar('UPD')
     C                   EVAL      WSTYY   = zhbgetvar('WSTYY')
     C                   eval      W2TYY   = c2n2(WSTYY)
     C                   EVAL      WSTWW   = zhbgetvar('WSTWW')
     C                   eval      W2TWW   = c2n2(WSTWW)
     C                   IF        WSUPD <> 'J'
     C                   MOVE      *BLANKS       WORD
     C                   EVAL      ItemCount = ZhbGetVarCnt('CHGYW')
     C                   eval      i = 1
     C     1             DO        ItemCount
     C                   eval      WSORD(i)  = zhbgetvar('chgyw':i)
     C                   eval      i = i + 1
     C                   ENDDO
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updhtmlvar('WORD':WORD)
     C                   IF        W2TYY = 0
     C                   MOVE      2000          WSTYY
     C                   MOVE      UYEAR         WSTYY
     C                   END
     C                   callp     updHtmlVar('WSTYY':WSTYY)
     C                   callp     updHtmlVar('WSTWW':WSTWW)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS158'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSORF
     C                   open      GSSLGF                               50
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     GSSORF
     C                   CLOSE     GSSLGF
     C                   eval      *inlr = *on
      *
     C                   endsr
