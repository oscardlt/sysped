      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE010R MODULE(*LIBL/TNOE010R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12230 * 11 YBC PTF12 Preferanse
12258 * 12 YBC PTF12 Nytt felt for VF-kode fra og med 01.12.2021
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSAEV4     IF   E           K DISK    USROPN
     FSAEV4E    IF   E           K DISK    USROPN
     F                                     RENAME(SAEVR:SAEVR4)
     FSAEV2     IF   E           K DISK    USROPN
     F                                     RENAME(SAEVR:SAEVR2)
     FSYPARL1   IF   E           K DISK    USROPN
     FPONRN     IF   E           K DISK    USROPN
     FSAEH      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SVAVDA          S              4
     D SVAVD           S              4  0
     D SVTDNA          S              7
     D SVTDN           S              7  0
     D LINA            S              5
     D LIN             S              5  0
     D VARENRA         S              8
     D VARENR          S              8  0
     D W2FYL           S              2
     D W2TOPL          S             45
     D W2CREF          S              3
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
     D WB1             S              7  0
     D                 DS
     D  SVVNT                  1      8S 0
     D  SVVNTA                 1      8
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      * Angre
     C                   IF        WMODE = 'R'
     C     *LIKE         DEFINE    SVAVD         UAVDOMS
     C     *LIKE         DEFINE    SVTDN         UTDNOMS
     C                   MOVE      SVAVD         UAVDOMS
     C                   MOVE      SVTDN         UTDNOMS
     C                   MOVE      'OMS'         UTYPEOMS          3
     C                   CALL      'SAD054'
     C                   PARM                    UAVDOMS
     C                   PARM                    UTDNOMS
     C                   PARM                    SESG
     C                   PARM                    UTYPEOMS
     C                   END
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SVAVD:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SVTDN:'M'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(LIN:'Z'))+'¬, '
        +'¬varenr¬ : ¬'+%trim(%editc(VARENR:'Z'))+'¬, '
        +'¬w2fyl¬ : ¬'+%trim(W2FYL)+'¬, '
        +'¬w2topl¬ : ¬'+%trim(W2TOPL)+'¬, '
        +'¬w2cref¬ : ¬'+%trim(W2CREF)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"orderList" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C     SAEVkey2      setll     SAEV4
     C     SAEVkey       setll     SAEV4E
     C     SAEVkey3      setll     SAEV2
     C                   IF        *IN61
     C     SAEVkey       READE     SAEV2                                  55
     C                   ELSE
     C                   IF        LIN=1
     C     SAEVkey       READE     SAEV4E                                 55
     C                   ELSE
     C     SAEVkey       READE     SAEV4                                  55
     C                   ENDIF
     C                   ENDIF
     C     *IN55         DOWEQ     '0'
     C                   IF        SVVKTB > *ZEROS
     C                   EVAL      WB1=*ZEROS
     C                   EVAL      WB1=SVNT01+SVNT02+SVNT03+SVNT04+SVNT05+SVNT06
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬sv01¬ : ¬'+%trim(sv01)+'¬, '
          +'¬svavd¬ : ¬'+%trim(%editc(svavd:'Z'))+'¬, '
          +'¬svtdn¬ : ¬'+%trim(%editc(svtdn:'M'))+'¬, '
          +'¬svli¬ : ¬'+%trim(%editc(svli:'Z'))+'¬, '
          +'¬svbelt¬ : ¬'+%trim(%editc(svbelt:'4'))+'¬, '
          +'¬svfyl¬ : ¬'+%trim(svfyl)+'¬, '
          +'¬svvnt¬ : ¬'+%trim(svvnta)+'¬, '
N02       +'¬svavt¬ : ¬'+%trim(svavt)+'¬, '
N02       +'¬svavts¬ : ¬'+%trim(svavts)+'¬, '
N03       +'¬svavtp¬ : ¬'+%trim(%editc(svavtp:'4'))+'¬, '
N03       +'¬svvktb¬ : ¬'+%trim(%editc(svvktb:'4'))+'¬, '
N03       +'¬svvktn¬ : ¬'+%trim(%editc(svvktn:'4'))+'¬, '
N04       +'¬svntm¬ : ¬'+%trim(%editc(svntm:'Z'))+'¬, '
N02       +'¬svtob¬ : ¬'+%trim(svtob)+'¬, '
N02       +'¬wa1¬ : ¬'+%trim(svft01)+'¬, '
          +'¬wb1¬ : ¬'+%trim(%editc(wb1:'Z'))+'¬, '
          +'¬wc1¬ : ¬'+%trim(sveh01)+'¬, '
N03       +'¬wd1¬ : ¬'+%trim(svvt01)+'¬, '
N02       +'¬we1¬ : ¬'+%trim(svcre1)+'¬, '
N02       +'¬wf1¬ : ¬'+%trim(svtop1)+'¬, '
          +'¬svln¬ : ¬'+%trim(%editc(svln:'Z'))+'¬, '
          +'¬svrefl¬ : ¬'+%trim(%editc(svrefl:'Z'))+'¬, '
N03       +'¬svexr01¬ : ¬'+%trim(svexr01)+'¬, '
N03       +'¬svexr02¬ : ¬'+%trim(svexr02)+'¬, '
N03       +'¬svexr03¬ : ¬'+%trim(svexr03)+'¬, '
N03       +'¬svexr04¬ : ¬'+%trim(%editc(svexr04:'4'))+'¬, '
N03       +'¬svexr05¬ : ¬'+%trim(%editc(svexr05:'4'))+'¬, '
          +'¬svexr06¬ : ¬'+%trim(svexr06)+'¬, '
          +'¬svexr07¬ : ¬'+%trim(%editc(svexr07:'4'))+'¬, '
          +'¬svlk¬ : ¬'+%trim(svlk)+'¬, '
N11       +'¬svpre¬ : ¬'+%trim(svpre)+'¬, '
N12       +'¬svvf¬ : ¬'+%trim(svvf)+'¬, '
          +'¬svnyl¬ : ¬'+%trim(svnyl)+'¬, '
          +'¬sverr¬ : ¬'+%trim(sverr)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   ENDIF

     C                   IF        *IN61
     C     SAEVkey       READE     SAEV2                                  55
     C                   ELSE
     C                   IF        LIN=1
     C     SAEVkey       READE     SAEV4E                                 55
     C                   ELSE
     C     SAEVkey       READE     SAEV4                                  55
     C                   ENDIF
     C                   ENDIF
     C  N55ANTLEST       COMP      100                                    55
     C                   ENDDO
     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SAEV4
     C  N50              CLOSE     SAEV4E
     C  N50              CLOSE     SAEV2
     C  N50              CLOSE     SYPARL1
     C  N50              CLOSE     PONRN
     C  N50              CLOSE     SAEH

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      SVAVDA = zhbgetvar('AVD')
     C                   EVAL      SVAVD  = c2n2(SVAVDA)
     C                   EVAL      SVTDNA = zhbgetvar('OPD')
     C                   EVAL      SVTDN  = c2n2(SVTDNA)
     C                   EVAL      LINA   = zhbgetvar('LIN')
     C                   EVAL      LIN    = c2n2(LINA)
     C                   EVAL      VARENRA= zhbgetvar('VARENR')
     C                   EVAL      VARENR = c2n2(VARENRA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   IF        LIN=*ZEROS
     C                   EVAL      LIN=1
     C                   ENDIF
     C     VARENR        COMP      *ZEROS                             61
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SAEVkey       klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C     SAEVkey2      klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C                   kfld                    LIN
     C     SAEVkey3      klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C                   kfld                    VARENR
     C     SYPARKEY      KLIST
     C                   KFLD                    SEKNK
     C                   KFLD                    WSYPAID
     C                   Z-ADD     1             X                 3 0
     C     *LIKE         DEFINE    SYPAID        WSYPAID
     C                   EVAL      WSYPAID='FISKE'
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SAEV4
     C  N50              OPEN      SAEV4E
     C  N50              OPEN      SAEV2
     C  N50              OPEN      SYPARL1
     C  N50              OPEN      PONRN
     C  N50              OPEN      SAEH
      * Fix standardverdier for ny varepost
     C                   IF        *IN50=*OFF
     C     SAEVKEY       CHAIN     SAEH                               51
     C                   IF        *IN51=*OFF

     C                   MOVEL     SEADK3        PONRAL            4
     C     PONRAL        CHAIN     PONRN                              51
     C  N51              MOVEL     PONKNR        W2FYL

     C     SYPARKEY      CHAIN     SYPARL1                            51
     C  N51              MOVEL     SYVRDA        W2TOPL
     C  N51              MOVE      'F1 '         W2CREF

     C                   ENDIF
     C                   ENDIF

     c                   endsr
