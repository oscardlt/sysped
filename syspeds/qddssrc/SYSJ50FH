     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK')
     A                                      CF09(09 'Bytt signatur')
     A                                  1  2'SYSPED/SYSJ50H'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N27                              1 20MSGCON(40 YBC0195 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(40 YBC0196 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            UFIRM          2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  6 15MSGCON(18 DIV0599 SYMSGF)
     A            WSSG           3A  B  6 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                  8 15MSGCON(18 DIV0600 SYMSGF)
     A            WSAVD          4Y 0B  8 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                 10 15MSGCON(18 DIV0601 SYMSGF)
     A            WSOPD          7Y 0B 10 35TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96N95                               DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10 50MSGCON(20 DIV0604 SYMSGF)
     A                                 12 15MSGCON(18 DIV2970 SYMSGF)
     A            WSKUNB         1   B 12 35TEXT('KUN B/L')
     A                                      DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 14 15MSGCON(18 DIV0602 SYMSGF)
     A            BLAVD          4Y 0B 14 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            BLOPD          7Y 0B 14 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 14 50MSGCON(20 DIV0036 SYMSGF)
     A                                 17 15MSGCON(18 DIV0603 SYMSGF)
     A            WSOPDG         7Y 0O 17 35COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23 15MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 35MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 55MSGCON(18 DIV0606 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
