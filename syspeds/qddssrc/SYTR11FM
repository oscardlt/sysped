     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYTR100000' 'SYTR10' 'SY400-
     A                                      LO/TRANS')
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R BILDE03
     A                                      WINDOW(*DFT 19 70 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF12(12)
     A                                      CF04(04 'SØK')
     A                                      CF23(23 'BEKREFT LABEL')
     A  27                                  ALARM
     A          H                           HLPDOC('SYTR100500' 'SYTR10' 'SY400-
     A                                      LO/TRANS')
     A                                      HLPARA(01 001 15 070)
     A          H                           HLPDOC('SYTR100510' 'SYTR10' 'SY400-
     A                                      LO/TRANS')
     A                                      HLPARA(16 001 16 070)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR2         7A  P
     A                                  1  1'SYSPED/SYTR11'
     A                                      COLOR(BLU)
     A                                  1 19'Bestille innh./utkj. via DUP-oppdr-
     A                                      ag'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  3  1'Tast avd. som skal utføre innh./ut-
     A                                      kj. Overstyr andre data etter ønske-
     A                                      .'
     A                                  4 10'Fra.. Lk Postnr Dato    Tid     Ti-
     A                                      l.. Lk Postnr Dato    Tid'
     A                                  5  1'Innhenting....'
     A                                      DSPATR(RI)
     A            WSLANF         2A  B  5 16
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSSDF          5A  B  5 19TEXT('Frakt fra')
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSSDFD         6Y 0B  5 26DSPATR(UL)
     A                                      EDTCDE(4)
     A  60                                  DSPATR(HI)
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSSDFK         4Y 0B  5 34EDTCDE(4)
     A  61                                  DSPATR(HI)
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                  5 40'------>'
     A            WSLAN1         2A  B  5 48
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSSDFF         5A  B  5 51TEXT('Forfrakt fra')
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSSFFD         6Y 0B  5 58EDTCDE(4)
     A  62                                  DSPATR(HI)
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSSFFK         4Y 0B  5 66EDTCDE(4)
     A  63                                  DSPATR(HI)
     A  91                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A  42        XXPNS1        20A  O  6 16COLOR(WHT)
     A  42        XXPNS3        20A  O  6 48COLOR(WHT)
     A                                  7  1'Avd'
     A            WSAVD1         4Y 0B  7  5EDTCDE(4)
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSOPD1         7Y 0O  7 10DSPATR(HI)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7 18'Pk'
     A            WSPKFF         1A  B  7 21
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                  7 23'Ot'
     A            WSOTFF         2A  B  7 26
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                  7 30'Lev.adr.'
     A            WSLNAV        30A  B  7 40
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                  8  1'Notis'
     A            WSFRD1         1A  B  8  8
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                      VALUES('J' 'N' ' ')
     A                                  8 10'(J=Ja)'
     A                                  8 17'Frank'
     A            WSFRFF         3A  B  8 23
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                  8 28'(For DUP-'
     A            WSLAD1        30A  B  8 40
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                  9  1'Frakt'
     A            WSFFBL         9Y 2B  9  8EDTCDE(J)
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSFKFF         1   O  9 23COLOR(WHT)
     A                                  9 28' oppdrag)'
     A            WSLAD2        30A  B  9 40
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSBNFF        10A  O 10  1COLOR(WHT)
     A            WSTUFF         8Y 0O 10 12DSPATR(HI)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 24'Mod:'
S26  A*           WSURF          1A  B 10 29VALUES(' ' 'A' 'B' 'C' 'D' 'E' 'F' -
S26  A*                                     'G' 'H' 'K' 'L')
N26  A            WSURF          1A  B 10 29
N26  A  68                                  DSPATR(HI)
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A            WSLAD3        30A  B 10 40
     A  91                                  DSPATR(PR)
     A  91                                  COLOR(WHT)
     A N91                                  COLOR(YLW)
     A                                 11 10'Fra.. Lk Postnr Dato    Tid     Ti-
     A                                      l.. Lk Postnr Dato    Tid'
     A                                 12  1'Utkjøring.....'
     A                                      DSPATR(RI)
     A            WSLAN2         2A  B 12 16
     A  92                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSSDVT         5A  B 12 19TEXT('Viderefrakt til')
     A  92                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSSVTD         6Y 0B 12 26DSPATR(UL)
     A                                      EDTCDE(4)
     A  64                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSSVTK         4Y 0B 12 34EDTCDE(4)
     A  65                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 12 40'------>'
     A            WSLANT         2A  B 12 48
     A  92                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSSDT          5A  B 12 51TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSSDTD         6Y 0B 12 58DSPATR(UL)
     A                                      EDTCDE(4)
     A  66                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSSDTK         4Y 0B 12 66EDTCDE(4)
     A  67                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A  42        XXPNS4        20A  O 13 16COLOR(WHT)
     A  42        XXPNS2        20A  O 13 48COLOR(WHT)
     A                                 14  1'Henteadr.'
     A            WSHNAV        30A  B 14 11
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 14 42'Avd'
     A            WSAVD2         4Y 0B 14 46EDTCDE(4)
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSOPD2         7Y 0O 14 51DSPATR(HI)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 14 59'Pk'
     A            WSPKVT         1A  B 14 61
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 14 64'Ot'
     A            WSOTVT         2A  B 14 67
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 15  1'(For DUP-'
     A            WSHAD1        30A  B 15 13
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 15 45'Notis'
     A            WSFRD2         1A  B 15 52
     A  92                                  DSPATR(PR)
     A                                      VALUES('J' 'N' ' ')
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 15 54'(J=Ja)'
     A                                 15 61'Frank'
     A            WSFRVT         3A  B 15 67
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 16  1' oppdrag)'
     A            WSHAD2        30A  B 16 13
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A                                 16 45'Frakt'
     A            WSVFBL         9Y 2B 16 52EDTCDE(J)
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSFKVT         1A  O 16 67COLOR(WHT)
     A            WSHAD3        30A  B 17 13
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
     A            WSBNVT        10A  O 17 45COLOR(WHT)
     A            WSTUVT         8Y 0O 17 56DSPATR(HI)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(015 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 18 20'Skriv merkelapp'
     A            WSMLVT         1A  B 18 36
     A N92                                  COLOR(YLW)
     A                                 18 38'Ant'
     A                                 18 42'kll:'
     A            WSNTKL         5Y 0O 18 47EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 18 53MSGCON(015 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTX2        65A  O 19  2MSGID(&MSGNR2 SYMSGF)
     A                                      COLOR(WHT)
     A                                 17 65'Mod:'
     A            WSURE          1A  B 17 70
     A  69                                  DSPATR(HI)
     A  92                                  DSPATR(PR)
     A  92                                  COLOR(WHT)
     A N92                                  COLOR(YLW)
