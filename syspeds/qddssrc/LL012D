********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 OPSJON D = VIS SAMMENSLÅINGS DETALJE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLHEAD LLHEADR)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A N49                                  CF02(02 'SPØR')
      *
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CA12(12)
     A N49                                  CA06(06 'LAG NY')
     A N49                                  CA09(09 'LAG NY')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL012R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1033 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1034 SYMSGF)
     A            LHPNR     R        O  2 16COLOR(WHT)
     A                                      EDTCDE(4)
     A            UNAVN         30A  O  2 25COLOR(WHT)
     A                                  2 56MSGCON(05 YBC1035 SYMSGF)
     A            LHOPR     R        O  2 62COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(09 YBC1036 SYMSGF)
     A            LHBNR     R        O  3 12COLOR(WHT)
     A            XXADR         50A  O  3 30COLOR(WHT)
     A                                  5  2MSGCON(78 YBC1037 SYMSGF)
     A                                      COLOR(WHT)
     A N49                             22 22'F9=Hurtigreg.'
     A                                      COLOR(BLU)
     A N49                             23  2MSGCON(78 YBC1038 SYMSGF)
     A                                      COLOR(BLU)
     A  49                             23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  49                             23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            LLMN           7S 0H
     A            LLVN           3S 0H
N01  A            LLLOK         25A  H
S01  A*           WSNR           1A  B  6  2VALUES(' ' '2' '4' '5' '8')
N01  A            WSNR           1A  B  6  2VALUES(' ' '2' '4' '5' '8' 'D')
     A  40                                  DSPATR(PR)
     A            LLANR         15A  O  6  6
     A  46                                  DSPATR(RI)
     A            XLVB           6A  O  6 22
     A  45                                  DSPATR(RI)
     A            LLWNT0         7  0O  6 29EDTCDE(4)
     A  45                                  DSPATR(RI)
     A            LLWNTL         7  0O  6 37EDTCDE(4)
     A  45                                  DSPATR(RI)
     A            LLAEH1         3A  H
     A            LLWNT1         5  0O  6 45EDTCDE(4)
     A  45                                  DSPATR(RI)
     A            LLAEH2         3A  H
     A            LLWNT2         5  0O  6 51EDTCDE(4)
     A  45                                  DSPATR(RI)
     A            LLAEH3         3A  H
     A            LLWNT3         5  0O  6 57EDTCDE(4)
     A  45                                  DSPATR(RI)
     A            LLWVEK         7  2O  6 63EDTCDE(4)
     A  45                                  DSPATR(RI)
     A            LLWKUB         7  4O  6 72EDTCDE(4)
     A  45                                  DSPATR(RI)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA05(05 'Forny')
     A N49                                  CA06(06 'LAG NY')
     A                                      CF07(07 'LAGERPOS/ART.NR')
     A N49                                  CA09(09 'LAG NY')
     A                                      CA12(12 'Annuller')
     A N49                                  CA18(18 'KONTROLL')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL012R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1033 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1034 SYMSGF)
     A            LHPNR     R        O  2 16COLOR(WHT)
     A                                      EDTCDE(4)
     A            UNAVN         30A  O  2 25COLOR(WHT)
     A                                  2 56MSGCON(05 YBC1035 SYMSGF)
     A            LHOPR     R        O  2 62COLOR(WHT)
     A            ZRECNO         4S 0O  2 66SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 73TIME
     A                                  3  2MSGCON(09 YBC1036 SYMSGF)
     A            LHBNR     R        O  3 12COLOR(WHT)
     A            XXADR         50A  O  3 30COLOR(WHT)
     A N49                              4  2MSGCON(78 YBC1039 SYMSGF)
     A                                      COLOR(BLU)
     A  49                              4  2MSGCON(78 YBC1040 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(78 YBC1037 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BOTREC
     A                                      OVERLAY
     A N49                             22  2MSGCON(18 YBC1042 SYMSGF)
     A                                      COLOR(BLU)
     A N49                             22 22'F9=Hurtigreg.'
     A                                      COLOR(BLU)
     A N49                             23  2MSGCON(58 YBC1041 SYMSGF)
     A                                      COLOR(BLU)
     A  49                             23  2MSGCON(58 YBC1043 SYMSGF)
     A                                      COLOR(BLU)
     A N41                             23 62MSGCON(18 YBC1044 SYMSGF)
     A                                      COLOR(BLU)
     A  41                             23 62MSGCON(18 YBC1045 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A  30        ZMSGTX        78A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
     A*
     A          R CHGREC
     A*%%TS  SD  20060928  095659  JOVO        REL-V5R2M0  5722-WDS
     A                                      TEXT('INFO')
     A                                      CA12(12 'Annuller')
     A                                      CF04(04 'Søk')
     A                                      CF07(07 'LAGERPOS')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL012R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 YBC1033 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(013 YBC1034 *LIBL/SYMSGF)
     A            LHPNR     R        O  2 16COLOR(WHT)
     A                                      EDTCDE(4)
     A            UNAVN         30A  O  2 25COLOR(WHT)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(005 YBC1035 *LIBL/SYMSGF)
     A            LHOPR     R        O  3  8COLOR(WHT)
     A                                  3 12MSGCON(009 YBC1036 *LIBL/SYMSGF)
     A            LHBNR     R        O  3 22COLOR(WHT)
     A                                  5  2MSGCON(016 YBC1046 *LIBL/SYMSGF)
     A            LLVN           3Y 0O  5 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(016 YBC1047 *LIBL/SYMSGF)
     A            WSANR         15A  B  6 19DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  29        LVTXT         40A  O  6 40DSPATR(BL)
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A                                  7  2MSGCON(016 YBC1057 *LIBL/SYMSGF)
     A            WSWNT0         7Y 0B  7 19DSPATR(UL)
     A  88                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(016 YBC1048 *LIBL/SYMSGF)
     A            WSVB          35A  B  8 19DSPATR(UL)
     A                                  9  2MSGCON(016 YBC1049 *LIBL/SYMSGF)
     A            WSHK           3A  B  9 19DSPATR(UL)
     A                                 10  2MSGCON(016 YBC1050 *LIBL/SYMSGF)
     A            WSLOK1         5A  B 10 19DSPATR(UL)
     A            WSLOK2        20A  B 10 25DSPATR(UL)
     A                                 10 52MSGCON(016 YBC1051 *LIBL/SYMSGF)
     A            WSNTLK         3Y 0O 10 69DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(016 YBC1052 *LIBL/SYMSGF)
     A            WSPRI         13Y 2B 11 19DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(016 YBC1053 *LIBL/SYMSGF)
     A            WSSER         25A  B 12 19DSPATR(UL)
     A  83                                  DSPATR(HI)
     A                                 12 52'Produksjonsdato:'
     A            WSPRDT         8Y 0B 12 69DSPATR(UL)
     A  87                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(016 YBC1056 *LIBL/SYMSGF)
     A            WSALLO         1A  B 13 19DSPATR(UL)
     A  61                             13 51MSGCON(006 YBC1054 *LIBL/SYMSGF)
     A  61        WSBSAL         7Y 0O 13 58DSPATR(UL)
     A                                      EDTCDE(4)
     A  61                             13 66MSGCON(006 YBC1055 *LIBL/SYMSGF)
     A  61        WSBPOS         3Y 0O 13 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A                                 14 50MSGCON(016 YBC1058 *LIBL/SYMSGF)
     A            WSANT0         7Y 0B 14 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(016 YBC1059 *LIBL/SYMSGF)
     A            WSWNTL         7Y 0O 15 19DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 15 27MSGCON(006 YBC1060 *LIBL/SYMSGF)
     A                                 15 33MSGCON(003 YBC1061 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15 58MSGCON(008 YBC1062 *LIBL/SYMSGF)
     A            WSANTL         7Y 0B 15 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 16  8MSGCON(008 YBC1063 *LIBL/SYMSGF)
     A            WSWNT1         5Y 0O 16 19DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 16 26MSGCON(006 YBC1060 *LIBL/SYMSGF)
     A            WSAEH1         3A  O 16 33DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 16 58MSGCON(008 YBC1063 *LIBL/SYMSGF)
     A            WSANT1         5Y 0B 16 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 17  8MSGCON(008 YBC1064 *LIBL/SYMSGF)
     A            WSWNT2         5Y 0O 17 19DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 17 26MSGCON(006 YBC1060 *LIBL/SYMSGF)
     A            WSAEH2         3A  O 17 33DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 17 58MSGCON(008 YBC1064 *LIBL/SYMSGF)
     A            WSANT2         5Y 0B 17 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 18  8MSGCON(008 YBC1065 *LIBL/SYMSGF)
     A            WSWNT3         5Y 0O 18 19DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 18 26MSGCON(006 YBC1060 *LIBL/SYMSGF)
     A            WSAEH3         3A  O 18 33DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 18 58MSGCON(008 YBC1065 *LIBL/SYMSGF)
     A            WSANT3         5Y 0B 18 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 19  8MSGCON(008 YBC1066 *LIBL/SYMSGF)
     A            WSWVEK         7Y 2O 19 19DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 19 58MSGCON(008 YBC1066 *LIBL/SYMSGF)
     A            WSVEK          7Y 2O 19 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 20  8MSGCON(008 YBC1067 *LIBL/SYMSGF)
     A            WSWKUB         7Y 4O 20 19DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 20 58MSGCON(008 YBC1067 *LIBL/SYMSGF)
     A            WSKUB          7Y 4O 20 67DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 21  2MSGCON(016 YBC1068 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O 21 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21 24'/'
     A                                      COLOR(WHT)
     A            WSOPD          7Y 0O 21 26EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21 34'/'
     A                                      COLOR(WHT)
     A            WSFBN          3Y 0O 21 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22  2'Årsak til manko:'
     A            WSMKD          5A  B 22 19DSPATR(UL)
     A  89                                  DSPATR(HI)
     A            WSMTXT        35A  B 22 26DSPATR(UL)
     A                                 23  2MSGCON(078 YBC1069 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSGTX        78A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
     A                                 15 50MSGCON(007 YBC1058 *LIBL/SYMSGF)
      *
     A          R DDREC
     A                                      CA12(12 'Annuller')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL012R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1033 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(13 YBC1034 SYMSGF)
     A            LHPNR     R        O  2 16COLOR(WHT)
     A                                      EDTCDE(4)
     A            UNAVN         30A  O  2 25COLOR(WHT)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(05 YBC1035 SYMSGF)
     A            LHOPR     R        O  3  8COLOR(WHT)
     A            LHBNR     R        O  3 22COLOR(WHT)
     A                                  5  2MSGCON(16 YBC1046 SYMSGF)
     A            LLVN           3Y 0O  5 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(16 YBC1047 SYMSGF)
     A            LLANR         15A  O  7 19DSPATR(UL)
     A                                      COLOR(WHT)
     A  29        LVTXT         40   O  7 40DSPATR(BL)
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A                                  8  2MSGCON(16 YBC1048 SYMSGF)
     A            LLVB          35A  O  8 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(16 YBC1049 SYMSGF)
     A            LLHK           3A  O  9 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(16 YBC1050 SYMSGF)
     A            LLLOK1         5A  O 10 19DSPATR(UL)
     A                                      COLOR(WHT)
     A            LLLOK2        20A  O 10 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 52MSGCON(16 YBC1051 SYMSGF)
     A            LLNTLK         3Y 0O 10 69DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(16 YBC1052 SYMSGF)
     A            LLPRI         13Y 2O 11 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(16 YBC1053 SYMSGF)
     A            LLSER         25A  O 12 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 52'Produksjonsdato:'
     A            LLPRDT         8Y 0O 12 69DSPATR(UL)
     A  87                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(16 YBC1056 SYMSGF)
     A            LLALLO         1A  O 13 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(16 YBC1057 SYMSGF)
     A            LLWNT0         7Y 0O 14 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 14 50MSGCON(16 YBC1058 SYMSGF)
     A            LLANT0         7Y 0O 14 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(16 YBC1059 SYMSGF)
     A            LLWNTL         7Y 0O 15 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 27MSGCON(06 YBC1060 SYMSGF)
     A                                 15 33MSGCON(03 YBC1061 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15 58MSGCON(08 YBC1062 SYMSGF)
     A            LLANTL         7Y 0O 15 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  8MSGCON(08 YBC1063 SYMSGF)
     A            LLWNT1         5Y 0O 16 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 26MSGCON(06 YBC1060 SYMSGF)
     A            LLAEH1         3A  O 16 33DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 58MSGCON(08 YBC1063 SYMSGF)
     A            LLANT1         5Y 0O 16 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17  8MSGCON(08 YBC1064 SYMSGF)
     A            LLWNT2         5Y 0O 17 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17 26MSGCON(06 YBC1060 SYMSGF)
     A            LLAEH2         3A  O 17 33DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17 58MSGCON(08 YBC1064 SYMSGF)
     A            LLANT2         5Y 0O 17 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  8MSGCON(08 YBC1065 SYMSGF)
     A            LLWNT3         5Y 0O 18 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 26MSGCON(06 YBC1060 SYMSGF)
     A            LLAEH3         3A  O 18 33DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18 58MSGCON(08 YBC1065 SYMSGF)
     A            LLANT3         5Y 0O 18 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19  8MSGCON(08 YBC1066 SYMSGF)
     A            LLWVEK         7Y 2O 19 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19 58MSGCON(08 YBC1066 SYMSGF)
     A            LLVEK          7Y 2O 19 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20  8MSGCON(08 YBC1067 SYMSGF)
     A            LLWKUB         7Y 4O 20 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20 58MSGCON(08 YBC1067 SYMSGF)
     A            LLKUB          7Y 4O 20 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 21  2MSGCON(16 YBC1068 SYMSGF)
     A            LHAVD          4Y 0O 21 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21 24'/'
     A                                      COLOR(WHT)
     A            LHOPD          7Y 0O 21 26EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21 34'/'
     A                                      COLOR(WHT)
     A            LHFBN          3Y 0O 21 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22  2'Årsak til manko:'
     A            LLMKD          5A  O 22 19DSPATR(UL)
     A                                      COLOR(WHT)
     A            LLMTXT        35A  O 22 26DSPATR(UL)
     A                                      COLOR(WHT)
     A N49                             23  2MSGCON(18 YBC1042 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R CHGANT
     A                                      WINDOW(*DFT 5 42 *NOMSGLIN)
     A                                      CA12(12 'Annuller')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL012R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(18 YBC1070 SYMSGF)
     A                                      COLOR(WHT)
     A  28                              2  2MSGCON(18 YBC1071 SYMSGF)
     A  28        X2SALD         7  0O  2 20COLOR(RED)
     A                                      EDTCDE(4)
     A  28                              2 28MSGCON(09 YBC1072 SYMSGF)
     A  28        X2NTLK         3  0O  2 38COLOR(RED)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(16 YBC1073 SYMSGF)
     A            XXSALD         7  0O  3 20COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 28MSGCON(09 YBC1072 SYMSGF)
     A            XXNTLK         3  0O  3 38COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(16 YBC1074 SYMSGF)
     A            WSWNT0         7  0B  4 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R CHGREC2
     A                                      CA12(12 'Annuller')
     A                                      CF04(04 'Søk')
     A                                      CF07(07 'LAGERPOS')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(16 YBC1047 SYMSGF)
     A            WSANR         15A  B  1 19DSPATR(UL)
     A  29        LVTXT         40A  O  1 40DSPATR(BL)
     A                                      DSPATR(RI)
     A                                      COLOR(RED)
     A                                  2  2MSGCON(16 YBC1057 SYMSGF)
     A            WSWNT0         7Y 0B  2 19DSPATR(UL)
     A  88                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  3  2'F3=Avslutt  F4=Søk  F7=Lagerpos.  -
     A                                      F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        ZMSGTX        78A  O  4  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
