             PGM        PARM(&AVD &SAML1 &SAML2 &STMF &BANE &VISE +
                          &INFOP)
             DCL        VAR(&UFAX) TYPE(*CHAR) LEN(1) VALUE('J')
             DCL        VAR(&FAKT) TYPE(*CHAR) LEN(1) /* Hvis &FAKT +
                          på, betyr at det skal kjøre faktura */
             DCL        VAR(&IN07) TYPE(*LGL) LEN(1) VALUE('1') /* +
                          Hvis &in07 på, betyr uten oppdatering. */
             DCL        VAR(&IN08) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in08 på, betyr prøve manifest. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7) /* Unikkode*/
             DCL        VAR(&INFOP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(152)
             DCL        VAR(&BANE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PRTNA) TYPE(*CHAR) LEN(10) +
                          VALUE('SYEF30P   ')
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAML1) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SAML2) TYPE(*CHAR) LEN(3)
             DCL        VAR(&VISE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
    /* */
             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &QNBR)
/*           SKJEKK OM PDF FINNES FRA FØR  */
             OVRPRTF    FILE(QSYSPRT) OUTQ(OUTMAIL)
             DSPLNK     OBJ(&BANE) OUTPUT(*PRINT) OBJTYPE(*STMF) +
                          DETAIL(*NAME) DSPOPT(*USER)
             MONMSG     MSGID(CPFA0A9) EXEC(GOTO CMDLBL(VIDERE))
             DLTSPLF    FILE(QSYSPRT) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             IF         COND(&VISE = 'J') THEN(DO)
             CALL       PGM(PDF001) PARM(&STMF)
             ENDDO
             RETURN
/*  FIKSER DIVERSE UDS POSISJONER */
VIDERE:      CHGDTAARA  DTAARA(*LDA (15 4)) VALUE(&AVD)
             CHGDTAARA  DTAARA(*LDA (36 3)) VALUE(&SAML1)
             CHGDTAARA  DTAARA(*LDA (39 3)) VALUE(&SAML2)
    /*                     */
    /* SKRIVER UT MANIFEST */
    /*                     */
             OVRPRTF    FILE(SYEF30P) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          80) CPI(10) OVRFLW(64) FRONTOVL(LOGOFAKT) +
                          OUTQ(OUTMAIL)
             RMVM       FILE(SUMMAN) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(SUMMAN) MBR(&UNIK)
             MONMSG     MSGID(CPF7306)
             OVRDBF     FILE(SUMMAN) TOFILE(SUMMAN) MBR(&UNIK)
    /*                     */
    /*                     */
             CALL       PGM(SYEK30) PARM(&IN07 &IN08 &FAKT &UFAX)
    /*                     */
    /*                     */
             RMVM       FILE(SUMMAN) MBR(&UNIK)
/*                                       */
             IF         COND(&INFOP = 'N') THEN(DO)
             ADDLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)
             CVTSPLFPDF SPLF(&PRTNA) JOB(&QNBR/&QUSER/&QJOB) +
                          TOPDF(&BANE) ORIENTAT(*V)
             RMVLIBLE   LIB(MMAIL)
             ENDDO
/*                                       */
/* LAGER PDF OG LEGGER I MAPPE           */
/* MED  INFOPRINT                        */
/*                                       */
             IF         COND(&INFOP = 'J') THEN(DO)
             CALL       PGM(INF001CL) PARM(&PRTNA &QNBR &QUSER &QJOB +
                          &BANE)
                          ENDDO
/*                                       */
             IF         COND(&VISE = 'J') THEN(DO)
             CALL       PGM(PDF001) PARM(&STMF)
             ENDDO
             DLTSPLF    FILE(&PRTNA) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
    /* */

             ENDPGM
