********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 POS-1 KUTTES TIL 4A PGA. SYSPED-REF.
10XXX * 23 SH  PTF10 FRITEKST VARELINJE
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 45 JOV PTF11 SKILLE PÅ KRED-NOTA OGSÅ PÅ SPESIFIK.
********************************************************************
     A*****************************************************************
     A*             S A M L E F A K T U R A   L I S T E               *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80,  OVERFLOW=63      *
     A*****************************************************************
     A          R KJØP1                     SPACEB(2)
     A N70N79                             68MSGCON(15 DIV0284 SYMSGF)
     A  70                                68MSGCON(15 DIV0323 SYMSGF)
     A  79                                68MSGCON(15 DIV0324 SYMSGF)
     A          R KJØP
     A  63                              1  1'¤(12U¤(s0p10h0s3b4099T'
     A            SFMT1         75      1  1
     A            SFMT2         75      1  1
     A  47        HOHT1         80A     1  1
     A  47        HOHT2         80A     2  1
     A  47        HOHT3         80A     3  1
     A  47        HOHT4         80A     4  1
     A  47        HOHT5         80A     5  1
     A  47        HOHT6         80A     6  1
     A  47        HOHT7         80A     7  1
     A  63                              8  1'¤(12U¤(s0p10h1s0b4099T'
     A            KNAVN         30A    11 11
     A    N70N79                       11 54MSGCON(24 DIV0286 SYMSGF)
     A                                      HIGHLIGHT
     A     70                          11 54MSGCON(24 DIV0321 SYMSGF)
     A                                      HIGHLIGHT
     A     79                          11 54MSGCON(24 DIV0322 SYMSGF)
     A                                      HIGHLIGHT
     A            PAGEPS         4  0  11 68
     A                                      HIGHLIGHT
     A                                      EDTCDE(Z)
     A            ADR1          30A    12 11
S45  A*   N70N79                       12 54MSGCON(24 DIV0285 SYMSGF)
S45  A*                                     HIGHLIGHT
S45  A*    70                          12 54MSGCON(24 DIV0325 SYMSGF)
S45  A*                                     HIGHLIGHT
S45  A*    79                          12 54MSGCON(24 DIV0326 SYMSGF)
S45  A*                                     HIGHLIGHT
N45  A N80N70N79                       12 54MSGCON(24 DIV0285 SYMSGF)
N45  A                                      HIGHLIGHT
N45  A N80 70                          12 54MSGCON(24 DIV0325 SYMSGF)
N45  A                                      HIGHLIGHT
N45  A N80 79                          12 54MSGCON(24 DIV0326 SYMSGF)
N45  A                                      HIGHLIGHT
N45  A  80N70N79                       12 54MSGCON(24 JOV1646 SYMSGF)
N45  A                                      HIGHLIGHT
N45  A  80 70                          12 54MSGCON(24 JOV1647 SYMSGF)
N45  A                                      HIGHLIGHT
N45  A  80 79                          12 54MSGCON(24 JOV1648 SYMSGF)
N45  A                                      HIGHLIGHT
     A            ADR2          30A    13 11
     A N28                             13 54'************************'
     A                                      HIGHLIGHT
     A  28                             13 54MSGCON(24 JOV0541 SYMSGF)
     A                                      HIGHLIGHT
     A N37 38     POSTNR    R          14 11REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37 38     ADR3      R          14 17REFFLD(ADR3 CUNDF)
     A N38        SYPOGE         9     14 11
     A N38        ADR3B     R          14 22REFFLD(ADR3 CUNDF)
     A  37 38     ADR3C     R          14 11REFFLD(ADR3 CUNDF)
     A N70N79                          14 54MSGCON(12 DIV0287 SYMSGF)
     A  70                             14 54MSGCON(12 DIV0317 SYMSGF)
     A  79                             14 54MSGCON(12 DIV0318 SYMSGF)
     A            XFN            7  0  14 71EDTCDE(3) HIGHLIGHT
     A N70N79                          15 54MSGCON(12 DIV0199 SYMSGF)
     A  79                             15 54MSGCON(12 DIV0315 SYMSGF)
     A  70                             15 54MSGCON(12 DIV0316 SYMSGF)
     A            KUNDNR         8  0  15 70EDTCDE(3) HIGHLIGHT
     A N70N79                          16 54MSGCON(12 DIV0288 SYMSGF)
     A  70                             16 54MSGCON(12 DIV0313 SYMSGF)
     A  79                             16 54MSGCON(12 DIV0314 SYMSGF)
     A            IDAGX          6  0  16 70EDTCDE(Y) HIGHLIGHT
     A N70N79                          17 54MSGCON(12 DIV0289 SYMSGF)
     A  70                             17 54MSGCON(12 DIV0311 SYMSGF)
     A  79                             17 54MSGCON(12 DIV0312 SYMSGF)
     A            XFFDT          6  0  17 70EDTCDE(Y) HIGHLIGHT
     A                                 18  1' '
     A          R KJØPHX                    SPACEB(1)
     A                                     1' _______________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A                                      SPACEA(1)
     A          R KJØP2                     SPACEB(1)
     A  89                                 1' ======================='
     A  89                                25'========================'
     A  89                                49'========================'
     A  89                                73'========'
     A  89                                  SPACEA(1)
     A N33        FRI001        75         2SPACEA(1)
     A N49
     AAN70N79N39                           2MSGCON(9 JOV0084 SYMSGF)
     A N49
     AAN70N79 39                           2MSGCON(9 JOV0086 SYMSGF)
     A N70N79 49                           2MSGCON(9 JOV0087 SYMSGF)
     A  70N49N39                           2'Shipper.:'
     A  70N49 39                           2'Consignee'
     A  70 49                              2'Shi/Cons:'
     A  79N49N39                           2'Absender:'
     A  79N49 39                           2'Empfang.:'
     A  79 49                              2'Abs/Emp.:'
     A            XXPART        20A       12
     A N70N79                             33MSGCON(48 JOV0085 SYMSGF)
     A                                      SPACEA(1)
     A  70                                36'Weight'
     A  70                                44'Cube M'
     A  70                                51'Load M'
     A  70                                58'Position'
     A  70                                69'Our ref'
     A                                      SPACEA(1)
     A  79                                35'Gewicht'
     A  79                                44' Kubik'
     A  79                                51'Lade M'
     A  79                                58'Position'
     A  79                                69'Unsere ref'
     A                                      SPACEA(1)
S40  A*N70N79                              2MSGCON(9 JOV0088 SYMSGF)
S40  A* 70                                 2'Terms...:'
S40  A* 79                                 2'Frankatur'
N40  A N70N79                              2MSGCON(5 JOV0088 SYMSGF)
N40  A  70                                 2'Terms'
N40  A  79                                 2'Frank'
S40  A*N70        XFRNTX        20A       12
S40  A* 70        XFRTTX        20A       12
N40  A N70        XFRNTX        15A        8
N40  A  70        XFRTTX        15A        8
N40  A N70N79                             24'Ant:'
N40  A  70                                24'Pcs:'
N40  A  79                                24'Anz:'
N40  A            XXNT           5  0     27EDTCDE(4)
     A            XXVKT          9  0     33EDTCDE(4)
     A            HEM3           7  3     43EDTCDE(4)
     A            HELM           4  2     52EDTCDE(4)
S01  A*           HEPOS1         7A       58
S01  A*           HEAVD          4  0     65EDTCDE(Z)
S01  A*                                   69'/'
S01  A*           HEOPD          7  0     70
S01  A*                                   77'/'
S01  A*           HESG           3A       78SPACEA(1)
N01  A            XPOS1          4A       58
N01  A            HEAVD          4  0     63EDTCDE(Z)
N01  A                                    67'/'
N01  A            HEOPD          7  0     68
N01  A                                    75'/'
N01  A            HESG           3A       76SPACEA(1)
     A N70N79                              2MSGCON(4 DIV0291 SYMSGF)
     A  70                                 2MSGCON(4 DIV0329 SYMSGF)
     A  79                                 2MSGCON(4 DIV0330 SYMSGF)
     A            HEPRO          8  0      7EDTCDE(4)
     A N70N79                             16MSGCON(6 DIV0292 SYMSGF)
     A  70                                16MSGCON(6 DIV0331 SYMSGF)
     A  79                                16MSGCON(6 DIV0332 SYMSGF)
     A            HEGN          15A       23
     A            HEHAWB         9        39
     A N70N79                             49MSGCON(5 DIV0294 SYMSGF)
     A  70                                49MSGCON(5 DIV0335 SYMSGF)
     A  79                                49MSGCON(5 DIV0336 SYMSGF)
     A*           HERFA         15A       55
     A            HERFA15       15A       55
SV5  A*           HEDTD          2  0     71
SV5  A*                                   73'.'
SV5  A*           HEDTM          2  0     74
SV5  A*                                   76'.'
SV5  A*           HEDTÅ          2  0     77
NV5  A            XEDTOP         6  0     71EDTCDE(Y)
N24  A                                      SPACEA(1)
N24  A N70N79                              2MSGCON(10 DIV1672 SYMSGF)
N24  A  70                                 2'From-to..:'
N24  A  79                                 2'Von-nach.:'
N24  A            FRATIL        68        12
     A* KUNDESPESIFIK HEADER....
     A          R KJØPH1                    SPACEB(1)
     A            CPSPH1        79         2
     A          R KJØPH2                    SPACEB(1)
     A            CPSPH2        79         2
     A          R KJØPH3                    SPACEB(1)
     A            CPSPH3        79         2
      * KUNDESPESIFIK OPPDRAGSDETALJER....
     A          R KJØP21                    SPACEB(1)
     A  89                                 1' ======================='
     A  89                                25'========================'
     A  89                                49'========================'
     A  89                                73'========'
     A  89                                  SPACEA(1)
     A            XKULI1        79         2
     A          R KJØP22                    SPACEB(1)
     A            XKULI2        79         2
     A          R KJØP23                    SPACEB(1)
     A            XKULI3        79         2
      *
     A          R KJØPFS                    SPACEB(1)
     A            KFSOTX        30         2
     A                                    32':'
     A            FSSOK         35        35
     A          R KJØP3                     SPACEB(1)
     A            FRTTXT        70         2
     A          R KJØP4                     SPACEB(1)
     A                                    25'                --------'
     A                                    49'------------------------'
     A                                    73'--------'
      *_______________________________
      * FORMAT FOR FRITEKST
      *"""""""""""""""""""""""""""""""
N23  A          R DETTXT                    SPACEB(1)
N23  A            FXVT          40        20
     A          R DET                       SPACEB(1)
S23  A*           FAVT          20A       17
N23  A            FAVT          20A       20
     A N70N79     KGENOT        20A       41
     A  70        KGEENT        20A       41
     A  79        KG2TYT        20A       41
     A  36        XVAL           3A       61
     A  36        XPFNOK        13  2     60EDTCDE(J)
     A  36        XMVAK          1        78
      *
      * OPPDRAGSTOTAL
      *
      * TOTMB - MOMSGRL/BELØP FOR GITT MVAKODE
     A          R TOTMB                     SPACEB(1)
     A N70N79                              2MSGCON(24 DIV0296 SYMSGF)
     A  70                                 2'Basis for VAT (sum of  )'
     A  79                                 2'MWS-basis (summe von   )'
     A            XMVAK          1        24
     A            XMVAGR         9  2     28EDTCDE(J)
     A N70N79                             41MSGCON(4 JOV0089 SYMSGF)
     A  70                                41'VAT '
     A  79                                41'MWS '
     A            MVAPRO         4  2     46EDTCDE(4)
     A                                    52'%'
     A            XVAL           3A       61
     A            XPMOMS         9  2     65EDTCDE(J)
      *
      * TOT-lin har Tot. momsfri (vensterstilt)i front -> "luft" over totbeløp
     A          R TOT                       SPACEB(1)
     A N70N79                              2MSGCON(17 SHU1120 SYMSGF)
     A  79                                 2'Taxfree total:  '
     A  70                                 2'Taxfree total:  '
     A            XMVAFR         9  2     28EDTCDE(J)
     A                                      SPACEA(1)
     A N70N79                             41MSGCON(14 JOV0566 SYMSGF)
     A  70                                41'Order total: '
     A  79                                41'Auftragsgesamt:'
     A            XVAL1          3A       61UNDERLINE HIGHLIGHT
     A            XBRUTT        13  2     60EDTCDE(J) HIGHLIGHT
     A                                      UNDERLINE
      *
      * FAKTURATOTAL
      *
      * VARIABLBELT ANT MOMSLINJER TOT2 SPLITTES I HEADER/LINJER/TOT
      * VIS ALLTID SUM BELØP FRI + EVT MVA-LIN (IND 72 TATT BORT UTEN MERKING)
     A          R TOT2H                     SPACEB(1)
N31  A                                     1' _______________________'
N31  A                                    25'________________________'
N31  A                                    49'________________________'
N31  A                                    73'________'
     A                                      SPACEA(1)
     A N70N79                             41MSGCON(17 SHU1120 SYMSGF)
     A  79                                41'Taxfree total:  '
     A  70                                41'Taxfree total:  '
     A            XMVFR          9  2     65EDTCDE(J)
     A          R TOT2L                     SPACEB(1)
     A N70N79                             41MSGCON(22 SHU1123 SYMSGF)
     A  70                                41'VAT  OF AMOUNT'
     A  79                                41'MWS VON:'
     A                                    60'('
     A            XMVAK          1        61
     A                                    62')'
     A            XMVGR          9  2     65EDTCDE(J)
     A                                      SPACEA(1)
     A            MVAPRO         4  2     41EDTCDE(4)
     A                                    47'%'
     A N70N79                             49'MOMS   '
     A  70                                49'VAT    '
     A  79                                49'MWS    '
     A            XMOM           9  2     65EDTCDE(J)
     A          R TOT2T                     SPACEB(1)
     A N70N79                             41MSGCON(13 DIV0298 SYMSGF)
     A                                      HIGHLIGHT
     A     70                             41'Invoice Total:'
     A                                      HIGHLIGHT
     A     79                             41'Gesamtbetrag:'
     A                                      HIGHLIGHT
     A            XVAL           3A       61UNDERLINE HIGHLIGHT
     A            XBRUT         13  2     60EDTCDE(J) HIGHLIGHT
     A                                      UNDERLINE
     A          R SUMLH                     SPACEB(02)
     A N70N79                              2MSGCON(40 SHU0411 SYMSGF)
     A  70                                 2'Totals pr charge type'
     A  79                                 2'Totalen pr belastungsart'
     A N63                                 1' -----------------------'
     A N63                                  SPACEB(2)
     A N63                                25'------------------------'
     A N63                                49'------------------------'
     A N63                                73'--------'
     A                                      SPACEA(1)
     A  63                                 1 ' '
     A                                      SPACEB(2)
     A  63                                 1'¤*c2350A¤*c55B¤*c11G¤*c2P'
     A                                      SPACEA(1)
     A N70N79                              2MSGCON(75 SHU1121 SYMSGF)
     A  70                                 2'Text'
     A  79                                 2'Text'
     A*N70N79                             36MSGCON(10 SHU0415 SYMSGF)
     A  70                                28'Net No MWS'
     A  79                                28'Netto O/tax'
     A  70                                40'Net Wi MWS'
     A  79                                40'Net Mi tax'
     A  70                                55'VAT'
     A  79                                55'MWS'
     A  70                                68'Total'
     A  79                                68'Gesamt'
     A N63                                 1' -----------------------'
     A N63                                  SPACEB(1)
     A N63                                25'------------------------'
     A N63                                49'------------------------'
     A N63                                73'--------'
     A                                      SPACEA(1)
     A  63                                 1' '
     A                                      SPACEB(2)
     A          R SUMLD                     SPACEA(1)
     A            WGTEXT        20         2
     A            XVAL           3A       24
      * EDTCDE M I STEDET FOR 3 PÅ ALLE BELØP (FÅ MED -)
     A            NETTOF        11  2     28EDTCDE(M)
     A            NETTO         11  2     40EDTCDE(M)
     A            MVA           11  2     52EDTCDE(M)
     A            BRUTTO        11  2     64EDTCDE(M)
     A          R SUMLT                     SPACEB(1)
     A  63                                 1'¤(12U¤(s0p10h1s3b4099T'
     A N70N79                              2MSGCON(20 SHU0418 SYMSGF)
     A  70                                 2'* Invoice Totals *'
     A  79                                 2'* Rechnungs-Gesamt *'
     A            XVAL           3A       24
     A            XETTOF        11  2     28EDTCDE(M)
     A            XETTO         11  2     40EDTCDE(M)
     A            XVA           11  2     52EDTCDE(M)
     A            XRUTTO        11  2     64EDTCDE(M)
     A  63                                 1'¤(12U¤(s0p10h1s0b4099T'
     A                                      SPACEB(1)
     A          R SUMLH2                    SPACEB(02)
     A N70N79                              2MSGCON(40 SHU0411 SYMSGF)
     A  70                                 2'TOTALS PR CHARGE TYPE'
     A  79                                 2'TOTALEN PR BELASTUNGSART'
     A                                     1' -----------------------'
     A                                      SPACEB(2)
     A                                    25'------------------------'
     A                                    49'------------------------'
     A                                    73'--------'
     A                                      SPACEA(1)
     A N70N79                              2MSGCON(20 SHU0414 SYMSGF)
     A  70                                 2'TEXT'
     A  79                                 2'TEXT'
     A N70N79                             23MSGCON(10 YBC1800 SYMSGF)
     A  70                                23'   NUMBERS'
     A  79                                23'    ANZAHL'
     A N70N79                             34MSGCON(8 YBC1801 SYMSGF)
     A  70                                34'   PRICE'
     A  79                                34'    PRIZ'
     A N70N79                             43MSGCON(10 SHU0415 SYMSGF)
     A  70                                43'  NET AMT.'
     A  79                                43'     NETTO'
     A N70N79                             55MSGCON(10 SHU0416 SYMSGF)
     A  70                                55'     VAT'
     A  79                                55'     MWS'
     A N70N79                             67MSGCON(10 SHU0417 SYMSGF)
     A  70                                67'    TOTAL'
     A  79                                67'   GESAMT'
     A                                     1' -----------------------'
     A                                      SPACEB(1)
     A                                    25'------------------------'
     A                                    49'------------------------'
     A                                    73'--------'
     A                                      SPACEA(1)
     A          R SUMLD2                    SPACEA(1)
     A            WGTEXT        20         2
     A            X2ANT          9  2     23EDTCDE(4)
     A            X2PRIS         7  2     34EDTCDE(4)
     A            X2NET          9  2     43EDTCDE(M)
     A            X2MVA          9  2     55EDTCDE(M)
     A            X2BRT          9  2     67EDTCDE(M)
     A          R SUMLT2                    SPACEB(1)
     A N70N79                              2MSGCON(20 SHU0418 SYMSGF)
     A  70                                 2'* INVOCE TOTALS *'
     A  79                                 2'* RECHNUNGS-GESAMT *'
     A            X2NETT         9  2     43EDTCDE(M)
     A            X2MVAT         9  2     55EDTCDE(M)
     A            X2BRTT         9  2     67EDTCDE(M)
N31  A          R TOTFGE                    SPACEB(1)
N31  A                                     1' _______________________'
N31  A                                    25'________________________'
N31  A                                    49'________________________'
N31  A                                    73'________'
N31  A                                     1' '
N31  A                                      SPACEA(1)
E41  A N70N79                              2'FAKTURAGEBYR(HELE SAMLEFAKT.)'
N41  A  70                                 2'INVOICEFEE(WHOLE INVOICE.)'
N41  A  79                                 2'RECN.GEBUER(GANZE RECHNUNG.)'
N41  A N70N79     KGENOT        20A       41
N41  A  70        KGEENT        20A       41
N41  A  79        KG2TYT        20A       41
N31  A            FABELN        13  2     60EDTCDE(J)
N31  A            XMVAK          1        78
