/*  TESTER OM BILAG I LØSNING 1  */
/*  QPGMR TATT UT                */
/*  L1KJOR NY PARM SYFA35        */
             PGM
             DCL        VAR(&FAKO) TYPE(*CHAR) LEN(1) VALUE('F')
             DCL        VAR(&BILL1) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN01) TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&AUT) TYPE(*CHAR) LEN(1) VALUE('O')
                        /* O=OMKJØRING IKKE MERK MED T. (G->O) */
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10)
/*           DCL        VAR(&PGMR) TYPE(*CHAR) LEN(10)      */
/*______________*/
/* LES FIRMAFIL */
/*""""""""""""""*/
             DCLF       FILE(FIRML1) RCDFMT(*ALL)
             RCVF       DEV(*FILE) RCDFMT(*FILE)
/*                      */
             RTVJOBA    JOB(&WS)
             CALL       PGM(INLIBL)

/*********************************************************************/
/* SKJEKKER FILLAG I LØSNING 1                                       */
/*********************************************************************/
             RTVDTAARA  DTAARA(EDIDTA (193 1)) RTNVAR(&BILL1)
             IF         COND(&BILL1 = 'Y') THEN(DO)
             CALL       PGM(HR900SY) PARM(&BILL1)
             IF         COND(&BILL1 = 'J') THEN(DO)
             RETURN
             ENDDO
             ENDDO
/**********************************************************************/
             ALCOBJ     OBJ((SYFA35CL *PGM *EXCL) (SYKS35CL *PGM +
                          *EXCL) (SYFA39 *PGM *EXCL))
             MONMSG     MSGID(CPF0000) EXEC(DO)
             SNDBRKMSG  MSG('En annen kjører overføring til regnskap +
                          eller oppdatering på statistikk') +
                          TOMSGQ(&WS)
             GOTO       CMDLBL(SLUTT)
             ENDDO
             CALL       PGM(*LIBL/SYFAFICL) PARM(&IN01)
             IF         COND(&IN01 *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('Du er ikke registrert som +
                          regskapsbruker. Overføringen er ikke +
                          startet') TOMSGQ(&WS)
             GOTO SLUTT
             ENDDO
/*           RTVUSRPRF  GRPPRF(&PGMR)                                  */
/*           IF         COND(&PGMR *NE QPGMR) THEN(DO)                 */
/*           SNDBRKMSG  MSG('Du mangler henvisning til gruppeprofil +  */
/*                        QPGMR. Overføringen er ikke startet.') +     */
/*                        TOMSGQ(&WS)                                  */
/*           GOTO SLUTT                                                */
/*           ENDDO                                                     */
/* */
/* KALLER SYOM35 FOR Å SETTE 'O' TILBAKE TIL 'G'  */
/* */
             CALL       PGM(SYOM35) PARM(&IN01)
             IF         COND(&IN01 *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('Det finnes merkede fakturaer (linjer +
                          med status G) - disse må overføres +
                          først.    Omkjøringen avbrytes') +
                           TOMSGQ(&WS)
             GOTO SLUTT
             ENDDO
             IF         COND(&IN01 *EQ '2') THEN(DO)
             SNDBRKMSG  MSG('Det finnes fakturalinjer som er +
                          overført til regnskap, men ikke er +
                          oppdatert i statistikk (har status T). +
                          Hvis programmet SYFA39 går (WRKACTJOB), +
                          vent til det er ferig. Hvis det ikke går, +
                          kan det startes ved å taste Call +
                          SYFA39.   Forsøk så omkjøringen senere') +
                           TOMSGQ(&WS)
             GOTO SLUTT
             ENDDO
             IF         COND(&IN01 *EQ '3') THEN(DO)
             SNDBRKMSG  MSG('F3-avslutt uten å kjøre er valgt.  +
                          Omkjøringen avbrytes') +
                           TOMSGQ(&WS)
             GOTO SLUTT
             ENDDO
/*                                     */
/*REGNSKAP    */
/*                                     */
/* */
/* KALLER SYFA35 FOR Å OPPDATERE POSTERINGER      */
/* */
             CALL       PGM(SYFA35) PARM(&AUT &L1KJOR)
/* INGENTING Å OVERFØRE - HOPP UT */
             IF         COND(&AUT *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('Det finnes ingen poster i +
                          posteringsfilen til økonomi - overføring +
                          avbrytes') TOMSGQ(&WS)
             GOTO       CMDLBL(SLUTT)
             ENDDO
/* */
/* KALLER V005NY FOR Å OPPDATERE NATIVE ØKONOMI   */
/* FOR LØSNING 1 KUNDENE                          */
/* */
             DLTOVR     FILE(*ALL)
             CHGDTAARA  DTAARA(*LDA (1 1024)) VALUE('                 -
             ')
             IF         COND(&L1KJOR = 'J') THEN(DO)
             CALL       PGM(VC005SY)
             CALL       PGM(INLIBL)
             ENDDO
             CPYF       FROMFILE(POSTA0) TOFILE(BACKUPA) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
             CPYF       FROMFILE(POSTB0) TOFILE(BACKUPB) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
/*                                             */
/* VISMA.NET TILPASSING                        */
/*                                             */
             IF         COND(&L1KJOR = 'V') THEN(DO)
             RMVM       FILE(VISTRAFAK) MBR(&WS)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(POSTB0) TOFILE(VISTRAFAK) +
                          TOMBR(&WS) MBROPT(*ADD) FMTOPT(*MAP +
                          *DROP)
             MONMSG     MSGID(CPF2817)
             CALL       PGM(VISM35CL) PARM(&FAKO &WS)
             ENDDO
             CLRPFM     FILE(POSTB0)
             CLRPFM     FILE(POSTA0)
             CHGDTAARA  DTAARA(*LDA (1 1024)) VALUE('                 -
             ')

             SNDPGMMSG  MSG('Utskrift av dagbok er sendt i jobkø +
                          QBATCH i bibliotek QGPL')


 SLUTT:      DLCOBJ     OBJ((SYFA35CL *PGM *EXCL) (SYKS35CL *PGM +
                          *EXCL) (SYFA39 *PGM *EXCL))

             ENDPGM
