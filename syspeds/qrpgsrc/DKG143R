     FEDIM      IF   E           K DISK
     FERFF96B   IF   E           K DISK
     FEERC96B   IF   E           K DISK
     FEERP96B   IF   E           K DISK
     FDKTKD     IF   E           K DISK
     FDKIH      UF   E           K DISK
     FDKEH      UF   E           K DISK
     FDKIHH     O  A E             DISK
     FDKEHH     O  A E             DISK
N01  FTVINF     O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WTX256                 1    256
     D  WTX70A                 1     65
     D  WTX70B                66    135
     D  WTX70C               136    205
     D  WTX70D               206    256
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N51              EXSR      SRA

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0

     C     *LIKE         DEFINE    EMN           ZMN
     C     *LIKE         DEFINE    EGN           ZGN0
     C     *LIKE         DEFINE    EGN           ZGN1
     C     *LIKE         DEFINE    EREP0         ZREPB0
     C     *LIKE         DEFINE    DKIH_SYAV     WVIH_SYAV
     C     *LIKE         DEFINE    DKIH_SYOP     WVIH_SYOP
     C     *LIKE         DEFINE    DKKD_TYP      WDKKD_TYP
     C     *LIKE         DEFINE    DKKD_KD       WDKKD_KD
      * INIT WORKFIELDS

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
     C                   EVAL      WDKKD_TYP='016'
      * Definiere Nøkkler
     C     DKIH_KEY      KLIST
     C                   KFLD                    WVIH_SYAV
     C                   KFLD                    WVIH_SYOP
     C     KODKEY        KLIST
     C                   KFLD                    WDKKD_TYP
     C                   KFLD                    WDKKD_KD
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     ZMN           CHAIN     ERFF96B                            54

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Vilken typ av svar ?                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Finnes ærendet ?
      *"""""""""""""""""
     C                   MOVEL     R1154         WVIH_SYAV
     C     7             SUBST     R1154:5       M0062B            7
     C                   MOVE      M0062B        WVIH_SYOP
     C     DKIH_KEY      CHAIN(N)  DKIH                               51
     C  N51DKIH_SYST     COMP      'D '                                   51
     C                   IF        *IN51 = *OFF
     C                   EXSR      SRAI
     C                   ELSE
     C     DKIH_KEY      CHAIN(N)  DKEH                               51
     C                   IF        *IN51 = *OFF
     C                   EXSR      SRAE
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Import                                               SRAI /
      *////////////////////////////////////////////////////////////
     C     SRAI          BEGSR
      *_______________________
      * Læs alla fel ERP + ERC
      *"""""""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     4             ZGN0
     C     KEYMS0        SETLL     EERP96B
     C     KEYMS0        READE     EERP96B                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      ERCI
      *
     C     KEYMS0        READE     EERP96B                                51
     C  N51              ENDDO
      *__________________
      * Uppdatera ærendet
      *""""""""""""""""""
     C     DKIH_KEY      CHAIN     DKIH                               61
     C                   IF        *IN61 = *OFF
     C                   EVAL      DKIH_SYST='M '
     C                   EVAL      DKIH_07=M1004
     C                   EVAL      DKIH_07C=M1004
     C                   IF        DKIH_AJOU = *BLANKS
     C                   EVAL      DKIH_AJOU = '1'
     C                   ENDIF
     C                   UPDATE    DKIHR
     C                   ENDIF
      *__________________________________________
      * Skriv ut ærendet (felmeddelande lista A4)
      *""""""""""""""""""""""""""""""""""""""""""
     C                   CALL      'DKI012R'
     C                   PARM                    DKIH_SYAV
     C                   PARM                    DKIH_SYOP
     C                   PARM                    DKIHH_DTH
     C                   PARM                    DKIHH_TMH

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Export                                               SRAE /
      *////////////////////////////////////////////////////////////
     C     SRAE          BEGSR
      *_______________________
      * Læs alla fel ERP + ERC
      *"""""""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     4             ZGN0
     C     KEYMS0        SETLL     EERP96B
     C     KEYMS0        READE     EERP96B                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      ERCE
      *
     C     KEYMS0        READE     EERP96B                                51
     C  N51              ENDDO
      *__________________
      * Uppdatera ærendet
      *""""""""""""""""""
     C     DKIH_KEY      CHAIN     DKEH                               61
     C                   IF        *IN61 = *OFF
     C                   EVAL      DKEH_SYST='M '
     C                   EVAL      DKEH_07=M1004
     C                   EVAL      DKEH_A2=M1004
     C                   IF        DKEH_AJOU = *BLANKS
     C                   EVAL      DKEH_AJOU = '1'
     C                   ENDIF
     C                   UPDATE    DKEHR
     C                   ENDIF
      *__________________________________________
      * Skriv ut ærendet (felmeddelande lista A4)
      *""""""""""""""""""""""""""""""""""""""""""
     C*                  CALL      'DKE012R'
     C*                  PARM                    DKEH_SYAV
     C*                  PARM                    DKEH_SYOP
     C*                  PARM                    DKEHH_DTH
     C*                  PARM                    DKEHH_TMH

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Læs ERC - Felinformation Import                      ERCI /
      *////////////////////////////////////////////////////////////
     C     ERCI          BEGSR
     C                   Z-ADD     EREP0         ZREPB0
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     4             ZGN1
     C     KEYMS1        SETLL     EERC96B                                20
     C     KEYMS1        READE     EERC96B                                52
     C     *IN52         DOWEQ     '0'

      * Skriv til historikfil
     C                   CLEAR                   DKIHHR
     C                   EVAL      DKIHH_SYA = DKIH_SYAV
     C                   EVAL      DKIHH_SYO = DKIH_SYOP
     C                   MOVEL     MDT           DKIHH_DTH
     C                   MOVEL     MTM           DKIHH_TMH
     C                   EVAL      DKIHH_SR  = 'R'
     C                   IF        E1049='1'
     C                   EVAL      DKIHH_TXH  = 'CUSRES FEJL Hoveddel'
     C                   ELSE
     C                   EVAL      DKIHH_TXH  = 'CUSRES FEJL Varepost:'
     C     DKIHH_TXH     CAT       E1052:1       DKIHH_TXH
     C                   ENDIF
     C                   MOVEL     E9321         WDKKD_KD
     C     KODKEY        CHAIN     DKTKD                              53
     C                   EVAL      WTX256=DKKD_TXT
     C  N53E9321         CAT       WTX70A:1      DKIHH_TX1
     C                   EVAL      DKIHH_TX2=WTX70B
     C                   EVAL      DKIHH_TX3=WTX70C
     C                   EVAL      DKIHH_TX4=WTX70D
     C                   WRITE     DKIHHR
      * Skriv til TVINF
     C                   CLEAR                   TVINFF
     C                   EVAL      FMN = ZMN
     C                   EVAL      FLN = 1
     C                   EVAL      F0078A = DKIHH_TXH
     C                   EVAL      F0078B = DKIHH_TX1
     C                   EVAL      F0078C = DKIHH_TX2
     C                   EVAL      F0078D = DKIHH_TX3
     C                   EVAL      F0078E = DKIHH_TX4
     C                   WRITE     TVINFF

     C  N52KEYMS1        READE     EERC96B                                52
     C  N52              ENDDO

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Læs ERC - Felinformation Export                      ERCE /
      *////////////////////////////////////////////////////////////
     C     ERCE          BEGSR
     C                   Z-ADD     EREP0         ZREPB0
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     4             ZGN1
     C     KEYMS1        SETLL     EERC96B                                20
     C     KEYMS1        READE     EERC96B                                52
     C     *IN52         DOWEQ     '0'

      * Skriv til historikfil
     C                   CLEAR                   DKEHHR
     C                   EVAL      DKEHH_SYA = DKEH_SYAV
     C                   EVAL      DKEHH_SYO = DKEH_SYOP
     C                   MOVEL     MDT           DKEHH_DTH
     C                   MOVEL     MTM           DKEHH_TMH
     C                   EVAL      DKEHH_SR  = 'R'
     C                   IF        E1049='1'
     C                   EVAL      DKEHH_TXH  = 'CUSRES FEJL Hoveddel'
     C                   ELSE
     C                   EVAL      DKEHH_TXH  = 'CUSRES FEJL Varepost:'
     C     DKEHH_TXH     CAT       E1052:1       DKEHH_TXH
     C                   ENDIF
     C                   MOVEL     E9321         WDKKD_KD
     C                   EVAL      WDKKD_TYP='101'
     C     KODKEY        CHAIN     DKTKD                              53
     C                   EVAL      WTX256=DKKD_TXT
     C  N53E9321         CAT       WTX70A:1      DKEHH_TX1
     C                   EVAL      DKEHH_TX2=WTX70B
     C                   EVAL      DKEHH_TX3=WTX70C
     C                   EVAL      DKEHH_TX4=WTX70D
     C                   WRITE     DKEHHR
      * Skriv til TVINF
     C                   CLEAR                   TVINFF
     C                   EVAL      FMN = ZMN
     C                   EVAL      FLN = 1
     C                   EVAL      F0078A = DKEHH_TXH
     C                   EVAL      F0078B = DKEHH_TX1
     C                   EVAL      F0078C = DKEHH_TX2
     C                   EVAL      F0078D = DKEHH_TX3
     C                   EVAL      F0078E = DKEHH_TX4
     C                   WRITE     TVINFF

     C  N52KEYMS1        READE     EERC96B                                52
     C  N52              ENDDO

     C                   ENDSR
