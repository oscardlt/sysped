      *
     FKALENDER  IF   E           K DISK
     D                 DS
     D  XDTNO                  1      8  0
     D  XDTNOA                 1      4  0
     D  XDTNOM                 5      6  0
     D  XDTNOD                 7      8  0
     D  XTDNO                  9     14  0
     D  XDTTDNO                1     14  0
     D                 DS
     D  XDTGM                  1      8  0
     D  XDTGMA                 1      4  0
     D  XDTGMM                 5      6  0
     D  XDTGMD                 7      8  0
     D  XTDGM                  9     14  0
     D  XDTTDGM                1     14  0
     D                 DS
     D  XDTSS                  1      8  0                                      SOMMERTID START
     D  XDTSSA                 1      4  0
     D  XDTSSM                 5      6  0
     D  XDTSSD                 7      8  0
     D  XTDSS                  9     14  0
     D  XDTTDSS                1     14  0
     D                 DS
     D  XDTVS                  1      8  0                                      VINTERTID START
     D  XDTVSA                 1      4  0
     D  XDTVSM                 5      6  0
     D  XDTVSD                 7      8  0
     D  XTDVS                  9     14  0
     D  XDTTDVS                1     14  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UDTNO             8 0
     C                   PARM                    UTDNO             6 0
     C                   PARM                    UDTGM             8 0
     C                   PARM                    UTDGM             6 0
     C                   PARM                    UMOD              1
      * UMOD = '1': CONVERTER NORSK TIL GMT
      * UMOD = '2': CONVERTER GMT TIL NORSK
      *
     C                   EVAL      XDTNO = UDTNO
     C                   EVAL      XTDNO = UTDNO
     C                   EVAL      XDTGM = UDTGM
     C                   EVAL      XTDGM = UTDGM
      *
     C                   SELECT
     C                   WHEN      UMOD = '1'
     C                   EVAL      XDTSSA = XDTNOA
     C                   EVAL      XDTSSM = 3
     C                   EVAL      XDTSSD = 31
     C                   EVAL      XDTVSA = XDTNOA
     C                   EVAL      XDTVSM = 10
     C                   EVAL      XDTVSD = 31
     C                   WHEN      UMOD = '2'
     C                   EVAL      XDTSSA = XDTGMA
     C                   EVAL      XDTSSM = 3
     C                   EVAL      XDTSSD = 31
     C                   EVAL      XDTVSA = XDTGMA
     C                   EVAL      XDTVSM = 10
     C                   EVAL      XDTVSD = 31
     C                   ENDSL
      *
     C     XDTSS         SETGT     KALENDER
     C                   READP     KALENDER                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        KAUDG = 'Sø'
     C                   EVAL      XDTSS = KADATE
     C                   EVAL      XTDSS = 020000
     C                   LEAVE
     C                   END
     C                   READP     KALENDER                               33
     C                   ENDDO
     C     XDTVS         SETGT     KALENDER
     C                   READP     KALENDER                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        KAUDG = 'Sø'
     C                   EVAL      XDTVS = KADATE
     C                   EVAL      XTDVS = 030000
     C                   LEAVE
     C                   END
     C                   READP     KALENDER                               33
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      UMOD = '1'                                   NORSK -> GMT
     C                   EVAL      XDTGM = XDTNO
     C                   IF        XDTTDNO < XDTTDSS OR XDTTDNO > XDTTDVS       VINTERTID
     C                   EVAL      XTDGM = XTDNO - 10000
     C                   ELSE                                                   SOMMERTID
     C                   EVAL      XTDGM = XTDNO - 20000
     C                   END
     C                   IF        XTDGM < 0
     C                   EVAL      XTDGM = XTDGM + 240000
     C     XDTGM         CHAIN     KALENDER                           33
     C                   READP     KALENDER                               33
     C                   EVAL      XDTGM = KADATE
     C                   END
     C                   EVAL      UDTGM = XDTGM
     C                   EVAL      UTDGM = XTDGM
     C                   WHEN      UMOD = '2'                                   GMT -> NORSK
     C                   EVAL      XDTNO = XDTGM
     C                   IF        XDTTDGM < XDTTDSS OR XDTTDGM > XDTTDVS       VINTERTID
     C                   EVAL      XTDNO = XTDGM + 10000
     C                   ELSE                                                   SOMMERTID
     C                   EVAL      XTDNO = XTDGM + 20000
     C                   END
     C                   IF        XTDNO > 240000
     C                   EVAL      XTDNO = XTDNO - 240000
     C     XDTGM         CHAIN     KALENDER                           33
     C                   READ      KALENDER                               33
     C                   EVAL      XDTNO = KADATE
     C                   END
     C                   EVAL      UDTNO = XDTNO
     C                   EVAL      UTDNO = XTDNO
     C                   ENDSL
      *
      *
     C                   SETON                                        LR
     C                   RETURN
      *
