     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/DOKUFE)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A          R SYCTA01DA
     A  *DS3                                WINDOW(*DFT 12 74 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 12 74 *NOMSGLIN)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1 19'Kontaktopplysninger'
     A                                      COLOR(WHT)
     A            ZE_TYP        20O  O  1 39COLOR(RED)
     A                                      DSPATR(BL)
     A                                  1 67'SYCTA01D'
     A                                      COLOR(BLU)
     A                                  3  1MSGCON(020 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1'Avd/Opd/Fbnr:'
     A            ZE_DFAVD  R        O  4 15REFFLD(FE_DFAVD)
     A                                      EDTCDE(4)
     A            ZE_DFOPD  R        O  4 20REFFLD(FE_DFOPD)
     A                                      EDTCDE(4)
     A            ZE_DFFBNR R        O  4 28REFFLD(FE_DFFBNR)
     A                                      EDTCDE(4)
     A                                  5  1'ID / Navn . :'
     A            ZE_C3413  R        B  5 15REFFLD(FE_C3413)
     A                                      CHECK(LC)
     A            ZE_C3412  R        B  5 33REFFLD(FE_C3412)
     A                                      CHECK(LC)
     A                                  6  2'Telefon  . :'
     A            ZE_C3148A R        B  6 15REFFLD(FE_C3148A)
     A                                  7  2'Mobil  . . :'
     A            ZE_C3148B R        B  7 15REFFLD(FE_C3148B)
     A                                  8  2'Telefax  . :'
     A            ZE_C3148C R        B  8 15REFFLD(FE_C3148C)
     A                                  9  2'SMS  . . . :'
     A            ZE_C3148D R        B  9 15REFFLD(FE_C3148D)
     A                                 10  2'E-Post . . :'
     A            ZE_C3148E R        B 10 15REFFLD(FE_C3148E)
     A                                      CHECK(LC)
     A                                 12  1'F3=Avslutt   F4=Søk i adresseregis-
     A                                      ter   F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SYCTA01DB
     A                                      ASSUME
     A                                  1  4' '
