      *
      ************************************************************
      * LEGGER UT PRINTFOREAKSYMBOL VED "SKIPP" I FIL KOPIERT INN FRA SPOOL
      ************************************************************
      *
     FDAYJO     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADF11)
     FHESTED    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYTR50PF  O    E             PRINTER
     D OP              S             28    DIM(99) ASCEND
     D                 DS
     D  WSDTÅ                  3      4  0
     D  WSDTM                  5      6  0
     D  WSDTD                  7      8  0
     D  DADATO                 1      8  0
     D                 DS
     D  DSDATA                 1     28
     D  XXDATO                 1      8
     D  XXTID                  9     12
     D  XXSTED                13     17
     D  HEAVD                 18     21  0
     D  HEOPD                 22     28  0
     D                 DS
     D  DSKEY                  1     28
     D  ARAVD                 18     21  0
     D  AROPD                 22     28  0
     C*
     C                   EXSR      INIT
     C*
     C                   READ      DAYJO                                  94
     C*
     C     *IN94         DOWNE     '1'
     C     DABILK        IFNE      GMBILK
     C                   ADD       1             SIDE              3 0
     C                   WRITE     HEADP
     C                   Z-ADD     7             LINNR             2 0
     C                   END
     C     LINNR         IFGT      43
     C                   ADD       1             SIDE
     C                   WRITE     HEADP
     C                   Z-ADD     7             LINNR
     C                   END
     C* SKRIV LEDIG-HEADING VED 1. LEDIGE PÅ HVER BILKODE
     C     DADIST        IFEQ      'U'                                          UDISPON. UNITS
     C     DABILK        ANDNE     GMLEBK
     C                   MOVE      DABILK        GMLEBK
     C                   WRITE     HEADL
     C                   END
     C                   MOVEL     DASJN1        DASJNX           17
     C                   MOVE      DADTF         XADTF
     C                   MOVE      DADTT         XADTT
     C     FIDTFM        IFNE      3
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    XADTF
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    XADTT
     C                   END
     C                   WRITE     DET
     C*
     C* PÅ BILER I TRAFIKK - SPESIFISER OPPDRAG DERSOM BEDT OM DET
     C     DADIST        IFEQ      ' '
     C     WSOPPD        ANDEQ     'F'
     C     WSOPPD        OREQ      'T'
     C                   EXSR      OPPDR
     C                   END
     C*
     C                   MOVE      DABILK        GMBILK
     C                   ADD       1             LINNR
     C                   READ      DAYJO                                  94
     C  N94              END
     C*
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     OPPDR         BEGSR
     C***********************************************
     C* SPESIFISER OPPDRAG - BRUK ARRAY FOR SORTERING PÅ
     C* ARRAY FOR SORTERING PÅ FRADARO/TID/STED ELLER TILDATO/TID/STED
     C*
     C                   MOVEA     *BLANKS       OP
     C                   MOVE      *ZEROS        NR                3 0
     C     DAPRO         SETLL     HEAD11
     C     DAPRO         READE     HEAD11                                 33
     C     *IN33         DOWEQ     '0'
     C     WSOPPD        IFEQ      'F'
     C                   MOVE      TRSDFD        XXDATO
     C                   MOVE      TRSDFK        XXTID
     C                   MOVE      HESDF         XXSTED
     C                   ELSE
     C                   MOVE      TRSDTD        XXDATO
     C                   MOVE      TRSDTK        XXTID
     C                   MOVE      HESDT         XXSTED
     C                   END
     C                   ADD       1             NR
     C                   MOVE      DSDATA        OP(NR)
     C     DAPRO         READE     HEAD11                                 33
     C                   END
     C*
     C*
     C* SORTER ARRAY (ALLE NONBLANK HAVNER SIST)  - SKRIV UT
     C                   SORTA     OP
     C*
     C* FINN FØRSTE NONBLANK....
     C                   Z-ADD     1             FR                3 0
     C                   MOVE      *BLANKS       BLANKO           28
     C     BLANKO        LOOKUP    OP(FR)                             32
     C*HVIS DET FINNES ETT ELEMENT STØRRE ENN BLANK --
     C*     BEHANDLE DERFRA OG UT ARRAYET (NR ER LØPENDE TELLER)
     C     *IN32         IFEQ      '1'
     C     FR            DO        99            NR
     C                   MOVE      OP(NR)        DSKEY
     C                   MOVE      ARAVD         ARRAVD
     C                   MOVE      AROPD         ARROPD
     C     HEKEY2        CHAIN     HEADF                              33
     C     HEKEY2        CHAIN     HESTED                             33
     C  N33              MOVEL     HSFROM        HESDF
     C  N33              MOVEL     HSTO          HESDT
     C                   MOVEL     HENAA         HENAAX           20
     C* OVERFLOW ?
     C     LINNR         IFGT      43
     C                   ADD       1             SIDE
     C                   WRITE     HEADP
     C                   Z-ADD     7             LINNR
     C                   END
     C                   MOVE      TRSDFD        XRSDFD
     C                   MOVE      TRSDTD        XRSDTD
     C     FIDTFM        IFNE      3
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    XRSDFD
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    XRSDTD
     C                   END
     C                   WRITE     DETOP
     C                   ADD       1             LINNR
     C                   END
     C                   END
     C*
     C* BLANK LINJE FØR NY TUR
     C                   WRITE     DETBLA
     C                   ADD       1             LINNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM      WSDT          WSDT              6 0
     C                   PARM      WSAV01        WSAV01            4 0
     C                   PARM      WSAV02        WSAV02            4 0
     C                   PARM      WSAV03        WSAV03            4 0
     C                   PARM      WSAV04        WSAV04            4 0
     C                   PARM      WSAV05        WSAV05            4 0
     C                   PARM      WSAV06        WSAV06            4 0
     C                   PARM      WSAV07        WSAV07            4 0
     C                   PARM      WSAV08        WSAV08            4 0
     C                   PARM      WSAV09        WSAV09            4 0
     C                   PARM      WSAV10        WSAV10            4 0
     C                   PARM      WSBK01        WSBK01            3
     C                   PARM      WSBK02        WSBK02            3
     C                   PARM      WSBK03        WSBK03            3
     C                   PARM      WSBK04        WSBK04            3
     C                   PARM      WSBK05        WSBK05            2
     C                   PARM      WSOPPD        WSOPPD            1
     C                   PARM      WSDOC         WSDOC            12
     C     HEKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEKEY2        KLIST
     C                   KFLD                    ARRAVD            4 0
     C                   KFLD                    ARROPD            7 0
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    KOAAVD
     C*
     C     *LOVAL        SETLL     DAYJO
     C*
     C                   MOVE      '...'         GMBILK            3
     C                   MOVE      '...'         GMLEBK            3
     C*
     C                   READ      FIRM                                   33
     C*
     C     WSAV01        IFEQ      *ZEROS                                       INGEN AVD.FELT
     C                   SETON                                        51
     C                   ELSE
     C     WSAV02        IFEQ      *ZEROS                                       AVD.FELT1 FYLT UT
     C     WSAV06        ANDEQ     *ZEROS                                       (MEN IKKE ANDRE)
     C                   MOVE      WSAV01        KOAAVD
     C                   MOVE      'A'           KOAUNI
     C     KODTAK        CHAIN     KODTA                              33
     C                   SETON                                        50
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
