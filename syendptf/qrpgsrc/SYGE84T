*****q**************************************************************
      * Sjekke om det er farlig gods som ikke kan samlastes
******* KUN HOVEDREGEL :
      * Det er IKKE tillatt å samlaste Eksplosiver=Faresedler 1, 1.4  (unntatt 1.4S), 1.5 og 1.6
      *                                med IKKE eksplosiver  (= andre faresedler ...)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Ved 1.4S = Ikke eksplosiv returneres S - for å kunne teste vider...
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%
12038 * 02 JOV PTF12 rekomp pga DOKUFVF
********************************************************************
     FHEAD11    IF   E           K DISK
     FDOKUFVF   IF   E           K DISK
      *
     c     HeaKey        KLIST
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
      *
     c     *ENTRY        PLIST
     c                   parm                    Uavd              4 0
     c                   parm                    UPRO              8 0
     c                   parm                    FAREKT            1
     c                   parm                    FAREST           10
     c                   move      ' '           FAREKT
     c                   move      *blanks       FAREST
      *
     c                   exsr      GetAdrT
     c     *in35         cabeq     '1'           Slutt
     c                   exsr      TstAdr
      *
     c     Slutt         tag
     c                   seton                                        lr
     c                   return
     C***********************************************
     c     GetAdrT       BEGSR
     C***********************************************
     c* finnes det på turen minst ett oppdrag med farlig gods
      * (første/beste - har det restriksjon skal resten ha samme / IKKE ->resten samme)
     C     UPRO          SETLL     HEAD11
     C     UPRO          READE     HEAD11                                 35
     C     *IN35         doweq     '0'
     C     Heakey        chain     DOKUFVF                            35
     c     *in35         ifeq      '0'
     c                   leave
     c                   endif
     C     UPRO          READE     HEAD11                                 35
     C                   ENDDO
      * 35 ON = leste gjennom hele turen (evt tom tur) uten å finne Fralig gods
     C                   ENDSR
     C*
     C***********************************************
     c     TstAdr        BEGSR
     C***********************************************
      * 1. Hovedregel:
      * Det er IKKE tillatt å samlaste Eksplosiver=Faresedler 1, 1.4  (unntatt 1.4S), 1.5 og 1.6
      *                                med IKKE eksplosiver
      * (Slik det ser ut i ADR-tabellene er alle hvir fareklasse begynner på 1..(og unntatt 1.4S)
      *  i de aktuelle fareseddel-gruppene, derfor tiltrekkelig å teste mot klasse= 1... )
      * (mange har klass=blank men seddel<>blank (det betyr jo et de definitivt IKKE er ekplosiver))
      *
     c                   If        %subst(FFklas:1:1)='1'
     c                             and FFklas<>'1.4S'                           (1.4S= ikke ...)
     c                   move      'E'           FAREKT            1            Eksplosiv
     c                   else
     c                   move      'I'           FAREKT                         IKKE Eksplosiv
N01  c                   if        FFklas='1.4S'
N01  c                   move      'S'           FAREKT                         IKKE Eksplos/1.4S
N01  C                   endif
     C                   endif
      *
      * 2. Hovedregel:  (denne seddel kan ikka lastes med noe annet..)
      * 3. Hovedregel:  (denne seddel kan ikka lastes med noe annet..)
     C                   if        FFSEDD = '4.1 ( 1 )' or
     C                             FFSEDD = '5.2 ( 1 )'
     C                   movel     FFSEDD        FAREST
     C                   endif
      *
     C                   ENDSR
