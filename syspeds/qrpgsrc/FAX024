      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE OPPDRAGNR./TURNR.         ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FKODTA     IF   E           K DISK
     FFAXCOVP   UF A E           K DISK
     FFAX024FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2AVD         YAVD              4 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2AVD         SETLL     FAXCOVP
     C     START1        TAG
     C                   READ(N)   FAXCOVP                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   FAXCOVP                                94
     C  N94              MOVE      TEAVD         YAVD
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      *ZEROS        WSAVD
     C                   GOTO      START5
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94YAVD          SETLL     FAXCOVP
     C  N94              READ      FAXCOVP                                94
     C  N94              UPDATE    FAXCOVPR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       30
     C                   MOVEA     '0000'        *IN(87)
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C                   SETOFF                                       90
     C  N14WSAVD         IFNE      *ZEROS
     C     KODTAK        CHAIN     KODTA                              90
     C                   END
     C   90              MOVE      'SPA0001'     MSGNR
     C  N90WSAVD         CHAIN(N)  FAXCOVP                            33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'SPA0005'     MSGNR
     C                   END
     C   90              Z-ADD     14            POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSPRTF        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SHU0605'     MSGNR
     C   89
     CANN30              Z-ADD     20            POSNBR
     C   89              SETON                                        30
     C     WSLIB         COMP      *BLANKS                                88
     C   88
     CANN30              MOVE      'SHU0606'     MSGNR
     C   88
     CANN30              Z-ADD     31            POSNBR
     C   88              SETON                                        30
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSAVD         CHAIN     FAXCOVP                            33
     C                   EXSR      MOVWST
     C  N33              UPDATE    FAXCOVPR
     C   33              WRITE     FAXCOVPR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGT
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C                   EXSR      DLTT
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTT          BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      TEAVD         WSAVD
     C                   EXFMT     VAREL3
     C  N12              DO
     C     WSAVD         CHAIN     FAXCOVP                            33
     C  N33              DELETE    FAXCOVPR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGT          BEGSR
     C***********************************************
     C                   EXSR      MOVTWS
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       30
     C                   MOVEA     '0000'        *IN(87)
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        TEAVD
     C                   MOVE      *BLANKS       TEPRTF
     C                   MOVE      *BLANKS       TELIB
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *BLANKS       WSPRTF
     C                   MOVE      *BLANKS       WSLIB
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVTWS        BEGSR
     C***********************************************
     C                   MOVE      TEAVD         WSAVD
     C                   MOVE      TEPRTF        WSPRTF
     C                   MOVE      TELIB         WSLIB
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     20            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWST        BEGSR
     C***********************************************
     C                   MOVE      WSAVD         TEAVD
     C                   MOVE      WSPRTF        TEPRTF
     C                   MOVE      WSLIB         TELIB
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C*
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      *ZEROS        W2AVD             4 0
     C*
     C                   ENDSR
     C*
