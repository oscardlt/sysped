********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5070 * 01 YBC PTF5 GRENSEVERDI FOR "LAV-VERDI" FORTOLLING
P6083 * 02 YBC PTF6 DATOSTYRT VALUTAKODE
P7XXX * 03 YBC PTF7 OPT 3 = KOPIERING
P8068F* 05 TH  PTF8 ny helpfunksjon
********************************************************************
NV5   * MYE ENDRINGER. INGEN MERKING
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S05  A*                                     HLPDOC('SYFT0201' 'SYFT02 ' 'SY400-
S05  A*                                     LO/SADTVINN')
S05  A*                                     HELP
S05  A*                                     ALTHELP
N06  A                                      HELP(01)
N06  A                                      CF01(01)
     A          R SUBFIL                    SFL
S03  A*           WSNR           1A  I  5  2VALUES(' ' '2' '4')
N03  A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4')
     A            KVAKOD         3   O  5  6TEXT('VALUTAKODE')
     A            KVAXXX        27   O  5 11TEXT('VALUTAKODE')
     A            KVAKRS        11  3O  5 39TEXT('VALUTAKURS')
     A                                      EDTCDE(1)
     A            KVAOMR         3  0O  5 55TEXT('OMREGNINGSFAKTOR')
     A                                      EDTCDE(4)
S02  A*           KVAGKR        11  3O  5 62TEXT('GAMMELT VALUTAKURS')
S02  A*                                     EDTCDE(1)
N02  A            KVADT          8  0H
N02  A            XVADT          6  0O  5 62TEXT('F.O.M. DATO')
N02  A                                      EDTCDE(Y)
N01  A            KVAGV         11  0H
     A          R SUBCTL                    SFLCTL(SUBFIL)
S01  A*                                     SFLSIZ(0017)
S01  A*                                     SFLPAG(0016)
N01  A                                      SFLSIZ(0016)
N01  A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  3'SYSPED/SYFT02'
     A                                      COLOR(BLU)
     A            MSG101        45A  O  1 20MSGID(DIV 1359 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 66TIME
     A                                      COLOR(BLU)
S03  A*                                 3  2MSGCON(40 DIV1360 SYMSGF)
N03  A                                  3  2MSGCON(50 YBC1809 SYMSGF)
     A                                      COLOR(BLU)
S02  A*                                 4  2MSGCON(78 DIV1361 SYMSGF)
N02  A                                  4  2MSGCON(78 YBC1803 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
S02  A*                                     CF01(01 'Flytt over alle kurser til-
S02  A*                                      gammel kurs')
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
S02  A*                                 2  2MSGCON(30 DIV1363 SYMSGF)
S02  A*                                     COLOR(BLU)
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
S01  A*                                     SLNO(21)
S01  A*                                     CLRL(04)
N01  A                                      SLNO(20)
N01  A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
S02  A*                                 1  6MSGCON(69 DIV1362 SYMSGF)
N02  A                                  1  6MSGCON(69 YBC1804 SYMSGF)
     A                                      COLOR(WHT)
     A            WVAKOD         3A  B  2  6TEXT('VALUTAKODE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A N40                                  COLOR(GRN)
     A            WVAXXX        27A  B  2 11TEXT('LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(GRN)
     A            WVAKRS        11Y 3B  2 39TEXT('VALUTAKURS')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WVAOMR         3Y 0B  2 55TEXT('OMREGNINGSFAKTOR')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1 100)
     A                                      COLOR(GRN)
S02  A*           WVAGKR        11Y 3B  2 62TEXT('GAMMELT VALUTAKURS')
S02  A*                                     DSPATR(UL)
S02  A*                                     EDTCDE(4)
S02  A*                                     COLOR(GRN)
N02  A            WVADT          6Y 0B  2 62TEXT('F.O.M.DATO')
N02  A                                      DSPATR(UL)
N02  A  40                                  DSPATR(HI)
N02  A  40                                  DSPATR(PR)
N02  A  86                                  DSPATR(RI)
N02  A                                      EDTCDE(Y)
N02  A N40                                  COLOR(GRN)
N01  A                                  3  6'Grenseverdi:'
N01  A            WVAGV         11  0B  3 20DSPATR(UL)
N01  A                                      EDTCDE(4)
E01  A                                  4  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
E01  A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
S01  A*                                     SLNO(21)
S01  A*                                     CLRL(04)
N01  A                                      SLNO(20)
N01  A                                      CLRL(05)
     A                                      OVERLAY
S02  A*                                 1  6MSGCON(69 DIV1362 SYMSGF)
N02  A                                  1  6MSGCON(69 YBC1804 SYMSGF)
     A                                      COLOR(WHT)
     A            KVAKOD         3   O  2  6TEXT('VALUTAKODE')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            KVAXXX        27   O  2 11TEXT('LAND')
     A                                      COLOR(WHT)
     A            KVAKRS        11  3O  2 39TEXT('VALUTAKURS')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KVAOMR         3  0O  2 55TEXT('OMREGNINGSFAKTOR')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
S02  A*           KVAGKR        11  3O  2 62TEXT('GAMMELT VALUTAKURS')
S02  A*                                     COLOR(WHT)
S02  A*                                     EDTCDE(4)
N02  A            XVADT          6  0O  2 62TEXT('F.O.M.DATO')
N02  A                                      COLOR(WHT)
N02  A                                      EDTCDE(Y)
N01  A                                  3  6'Grenseverdi:'
N01  A            KVAGV         11  0O  3 20COLOR(WHT)
N01  A                                      EDTCDE(4)
E01  A                                  4  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A                                  1  2MSGCON(18 DIV1359 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
S03  A*                                 3  2MSGCON(40 DIV1360 SYMSGF)
N03  A                                  3  2MSGCON(50 YBC1809 SYMSGF)
     A                                      COLOR(BLU)
S02  A*                                 4  2MSGCON(78 DIV1361 SYMSGF)
N02  A                                  4  2MSGCON(78 YBC1803 SYMSGF)
     A                                      COLOR(WHT)
S02  A*                                22  2MSGCON(30 DIV1363 SYMSGF)
S02  A*                                     COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SOKVAL
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(24 DIV1364 SYMSGF)
     A            W2AXXX        27A  I  2 29DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
