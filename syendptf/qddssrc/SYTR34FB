     A*%%TS  SD  20170928  113801  SYSTEMA     REL-V7R1M0  5770-WDS
N01  A* 8=NOTIS
N03  A* F02 = BILDE FOR ANNEN FBET.. (EGET FORMAT ANNENW )
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20170928  100750  SYSTEMA     REL-V7R1M0  5770-WDS
     A            HSLNR          7S 0H
     A            HSLNR2         3S 0H
S01  A*           WSNR           1A  I  6  2VALUES(' ' '2' '4' '9')
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4' '8' '9' 'D')
     A            HXSNDN10      10A  O  6  4
     A  55 59                               COLOR(RED)
     A            HXNAK         19A  O  6 15
     A  55 57                               COLOR(YLW)
     A            HSPNRK         4S 0O  6 35COLOR(WHT)
     A            HSANT          5Y 0O  6 40EDTCDE(4)
     A            HSVKT          5Y 0O  6 46EDTCDE(4)
     A  52                                  COLOR(YLW)
     A  52                                  DSPATR(RI)
     A            HSM3           5Y 3O  6 52EDTCDE(4)
     A            HSLM           4Y 2O  6 59EDTCDE(4)
     A            HXBELH         5Y 0O  6 65EDTCDE(4)
     A            HXBELM         4Y 0O  6 71EDTCDE(4)
     A            HXBEL          4Y 0O  6 76EDTCDE(4)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20170928  100750  SYSTEMA     REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0016)
     A  *DS4                                SFLSIZ(0016)
     A  *DS3                                SFLPAG(0015)
     A  *DS4                                SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  55                                  CA02(02 'MANKO MODUS')
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CF09(09 'TUR-PLANL')
     A                                      CF10(10 'SAMTAKSER')
     A                                      CF11(11 'OVERFØR')
     A                                      CA12(12 'TILBAKE')
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 18'Henteoppdrag Detalj-linjer'
     A                                      COLOR(WHT)
     A  55                              1 46'EDI'
     A                                      COLOR(WHT)
     A  55                                  DSPATR(RI)
     A                                  1 52'Term:'
     A            HOPNRT         4Y 0O  1 58COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A                                  2  2'Kunde:'
     A            HOKNS          8Y 0O  2  9EDTCDE(4)
     A                                      COLOR(WHT)
     A            HONAS         30A  O  2 18COLOR(WHT)
     A            HOPNRS         4Y 0O  2 49COLOR(WHT)
     A            HOSTDS        25A  O  2 54COLOR(WHT)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Dato :'
     A            HODT4          4Y 0O  3  9EDTWRD('  .  .')
     A                                      COLOR(WHT)
     A                                  3 18'D=Direkte'
     A                                      COLOR(BLU)
     A                                  3 35'8=Notis/mld'
     A                                      COLOR(BLU)
     A                                  3 47'Turnr:'
     A            HOPRO          8Y 0O  3 54EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 63'Bil.:'
     A            HOBILN        10A  O  3 69COLOR(WHT)
     A                                  4  2MSGCON(032 YBC0963 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              4 35'9=MANKO OK'
     A                                      COLOR(BLU)
     A  55                              4 47'Pll til trans:'
     A  55        HOPLL2         3Y 0O  4 62COLOR(WHT)
     A                                      EDTCDE(4)
     A  55                              4 66'Fra Trans:'
     A  55        HOPLL1         3Y 0O  4 77COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2'Op  Send.nr  Mottaker            P-
     A                                      .nr  Ant.  Vekt Volum  L.mt  H.fr. -
     A                                      Mini I.Bl'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A            MSGNR          7A  P
     A  56                             21  2'Alt overf:'
     A                                      COLOR(WHT)
     A  56        ANTOVF         3Y 0O 21 13EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 18'(Hfr:'
     A                                      COLOR(WHT)
     A  56        SOMBEH         7Y 0O 21 24EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 32'M:'
     A                                      COLOR(WHT)
     A  56        SOMBEM         7Y 0O 21 35EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 43'I:'
     A                                      COLOR(WHT)
     A  56        SOMBEL         7Y 0O 21 46EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 54')'
     A                                      COLOR(WHT)
     A            SUMBEH         7Y 0O 21 63EDTCDE(4)
     A                                      COLOR(WHT)
     A            SUMBEL         7Y 0O 21 73EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22'F9=Tur/bil-oversikt'
     A                                      COLOR(BLU)
     A                                 22 42'F10=Takser/fordel'
     A                                      COLOR(BLU)
     A            SUMBEM         7Y 0O 22 68EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62'F11=Overf. oppdr.'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A  55                             23  2'F2=V.Manko'
     A                                      COLOR(BLU)
     A                                 23 13'F3=Avsl.'
     A                                      COLOR(BLU)
      *
     A          R VARELI
N03  A                                      CA02(02 'ANNEN')
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      CF10(10 'Switch ktr-modus')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Henteoppd.linje'
     A                                      COLOR(WHT)
     A                                  1 18'Send.nr.:'
     A            WSSNDN        20A  B  1 28DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  1 49'Kontrollmodus:'
     A N41                              1 64'Halv'
     A                                      COLOR(TRQ)
     A  41                              1 64'Full'
     A                                      COLOR(TRQ)
     A                                  1 69'(F10=Switch)'
     A                                      COLOR(BLU)
     A                                  2  2'Til Postnr'
     A            WSTILP         4Y 0B  2 13
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 18'Ant'
     A            WSANT          5Y 0B  2 22EDTCDE(4)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 28'Vkt'
     A            WSVKT          5Y 0B  2 32EDTCDE(4)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 38'M3'
     A            WSM3           5Y 3B  2 41COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 48'LM'
     A            WSLM           4Y 2B  2 51COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 57'Fr'
     A            WSKODH         1A  B  2 60COLOR(YLW)
     A            WSBELH         7Y 0B  2 62EDTCDE(4)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPROD         1A  B  2 70COLOR(YLW)
     A  73                                  DSPATR(RI)
     A                                  2 72'Va'
     A            WSVARM         1A  B  2 75COLOR(YLW)
     A                                  2 77'Fa'
     A            WSFARL         1A  B  2 80COLOR(YLW)
     A                                  3  2'Enh/besk'
     A            WSVB          25A  B  3 13COLOR(YLW)
     A                                  3 39'Betal'
     A            WSFR           3A  B  3 45DSPATR(UL)
     A                                      COLOR(YLW)
     A  79                                  DSPATR(RI)
     A                                  3 49'Knr v/A'
     A            WSKNA          8Y 0B  3 57EDTCDE(4)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WXANAV        15A  B  3 66COLOR(WHT)
     A                                  4  2'Mott.:'
     A            WSKNK          8Y 0B  4  9EDTCDE(4)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNAK         30A  B  4 18DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADK1        30A  B  4 49DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  2'Se.ref'
     A            WSRFA         15A  B  5  9DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5 25'Pll'
     A            WSANTP         3Y 0B  5 29EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSPNRK         4Y 0B  5 49
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSTDK        25A  B  5 54DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'Via..:'
     A            WSPNRT         4Y 0B  6  9COLOR(YLW)
     A  75                                  DSPATR(RI)
     A                                  6 14'Tra'
     A            WSAGTR         8Y 0B  6 18EDTCDE(4)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTRJN         1A  B  6 27COLOR(YLW)
     A            WXTNAV        15A  B  6 29COLOR(WHT)
     A                                      DSPATR(PR)
     A                                  6 45'Bel'
     A            WSBELA         7Y 0B  6 49EDTCDE(4)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6 57'Dato'
     A            WSDT           6Y 0B  6 62EDTCDE(4)
     A                                      COLOR(YLW)
     A  87                                  DSPATR(RI)
     A                                  6 70'Avd'
     A            WSAVD          4Y 0B  6 75
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2'F4=Søk  '
     A                                      COLOR(BLU)
     A                                  7 11'F12=Forrige'
     A                                      COLOR(BLU)
     A                                  7 24'U.Fb'
     A            WSUTKO         2A  B  7 29COLOR(YLW)
     A  74                                  DSPATR(RI)
     A            WXUTKT        12A  O  7 32COLOR(WHT)
     A            WSUTTX        15A  B  7 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSEKBL         6Y 0B  7 62EDTCDE(4)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 70'Løpenr:'
     A            WSLNR2         3Y 0B  7 78EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(PR)
     A  30        MSGTXT        78A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30 60                           8  2'Manu.Manko. Sts:'
     A                                      COLOR(WHT)
     A N30 60     HSDSDM         1A  O  8 19COLOR(RED)
     A N30 60     HSDSUS        10A  O  8 21COLOR(RED)
     A N30 60     HSDSDT         8A  O  8 32COLOR(RED)
     A N30 60     HSDSTI         4A  O  8 41COLOR(RED)
     A N30 60                           8 48'Motpost(v.O=Orig/R=Rest):'
     A                                      COLOR(WHT)
     A N30 60     HSDSMOTP       3A  O  8 75COLOR(RED)
     A N30N60                           8 40' C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC0965 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA06(06 'LAGE NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 19'Henteoppdrag Detalj-linjer       '
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2  2'Kunde:'
     A            HOKNS          8Y 0O  2  9EDTCDE(4)
     A                                      COLOR(WHT)
     A            HONAS         30A  O  2 18COLOR(WHT)
     A            HOPNRS         4Y 0O  2 49COLOR(WHT)
     A            HOSTDS        25A  O  2 54COLOR(WHT)
     A                                  3  2'Dato :'
     A            HODT4          4Y 0O  3  9EDTWRD('  .  .')
     A                                      COLOR(WHT)
     A                                  3 18'Term:'
     A            HOPNRT         4Y 0O  3 24COLOR(WHT)
     A                                  3 47'Turnr:'
     A            HOPRO          8Y 0O  3 54EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 63'Bil.:'
     A            HOBILN        10A  O  3 69COLOR(WHT)
     A                                  4  2MSGCON(032 YBC0963 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              4 35'9=MANKO OK'
     A                                      COLOR(BLU)
     A                                  5  2'Op  Send.nr  Mottaker            P-
     A                                      .nr  Ant.  Vekt Volum  L.mt  H.fr. -
     A                                      Mini I.B'
     A                                      COLOR(WHT)
     A  56                             21  2'Alt overf:'
     A                                      COLOR(WHT)
     A  56        ANTOVF         3Y 0O 21 13EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 18'(Hfr:'
     A                                      COLOR(WHT)
     A  56        SOMBEH         7Y 0O 21 24EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 32'M:'
     A                                      COLOR(WHT)
     A  56        SOMBEM         7Y 0O 21 35EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 43'I:'
     A                                      COLOR(WHT)
     A  56        SOMBEL         7Y 0O 21 46EDTCDE(4)
     A                                      COLOR(WHT)
     A  56                             21 54')'
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A                                  3 35'8=Notis/mld'
     A                                      COLOR(BLU)
     A          R WINDOW
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CF10(10 'FORDEL')
     A                                      CA12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'Taksering/fordeling HENTE-oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Samlet...'
     A                                  3  2' Vekt(1)  Kubikk(2)  Lastem(3) Hov-
     A                                      .frakt(4)'
     A  75        IFIPRP         4Y 2O  3 46EDTCDE(1)
     A  75                              3 52'% ='
     A            SUMVKT         6Y 0O  4  3EDTCDE(1)
     A  71                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            SUMM3          5Y 3O  4 12EDTCDE(4)
     A  72                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            SUMLM          4Y 2O  4 25EDTCDE(4)
     A  73                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            SUMHFR         7Y 0O  4 35EDTCDE(4)
     A  74                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A  75        SUMHFP         7Y 0O  4 45EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  6  2'Innh.beløp'
     A            WSFRAK         1A  B  6 15COLOR(YLW)
     A            WSBEL          7Y 0B  6 17EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  6 27'Fordel etter:'
     A            WSNØKK         1A  B  6 41COLOR(YLW)
     A N71                              6 43'1=Vkt'
     A N72                              6 49'2=Kub'
     A                                  7  2'Min. fra kunde '
     A            WSMIKK         1A  B  7 18COLOR(YLW)
     A            WSMINK         4Y 0B  7 20EDTCDE(4)
     A                                      COLOR(YLW)
     A N73                              7 43'3=Lm.'
     A N74                              7 49'4=H.fr '
     A                                 10  2'Grl. M-beregn:'
     A            WSFPNR         4A  B 10 17
     A                                 10 23'->'
     A            WSTPNR         4A  B 10 26
     A                                 11  2'Franka'
     A            WSFRAN         3A  B 11  9
     A                                 11 13'Prodkode:'
     A            WSPRO2         1A  B 11 23
     A                                 11 25'Avd'
     A            WSAVD          4Y 0B 11 29EDTCDE(4)
     A                                 12  2'F10=Utfør fordeling'
     A                                      COLOR(BLU)
     A                                 12 30MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R MANKOW
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CA12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'Valg av Manko-håndtering         '
     A                                      COLOR(WHT)
     A                                  2  2'Hvordan skal manko på valgt(e) pos-
     A                                      t(er) håndteres? '
     A                                      COLOR(WHT)
     A N58                              3  2'Du bør ha oversikt over ant. mank/-
     A                                      tot før kjøring! '
     A                                      COLOR(RED)
     A N58                              4  2'ER DU SIKKER - trykk ENTER=Kjør, e-
     A                                      llers F12 + F2 ! '
     A                                      COLOR(RED)
     A                                  5  2'Valg:'
     A            WSMANK         1   B  5 12VALUES('1' '2')
     A                                  5 16'1 = Manko skilles ut på nytt send.-
     A                                      nr '
     A                                  6 16'    som blir stående åpen. Origina-
     A                                      l  '
     A                                  7 16'    reduseres tilsv. og merkes fer-
     A                                      dig'
     A                                  9 16'2 = Sendingene merkes ferdig/klar -
     A                                      for'
     A                                 10 16'    overf. til trafikk. TROSS MANK-
     A                                      O!!'
     A                                 11 16'    (med gjeldende ant/vekt beløp.-
     A                                      .) '
     A                                 12 12'Enter=Utfør valg'
     A                                      COLOR(BLU)
     A                                 12 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R MANKOW2
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CF11(11 'OVERFØR')
     A                                      CA12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'MANGLENDE SKYTING INN PÅ TERMINAL'
     A                                      COLOR(WHT)
     A                                  3  2'Dette er EDI-mottatt henteoppdrag -
     A                                      hvor ennå ikke      '
     A                                      COLOR(WHT)
     A                                  4  2'alle sendinger er ferdig SKUTT INN-
     A                                       på terminal.       '
     A                                      COLOR(WHT)
     A                                  5  2'Uferdige send.nr -  rød farge i li-
     A                                      sta (F2=Se mank/tot)'
     A                                  6  2'Valg 9 i lista er manuell manko-hå-
     A                                      ndtering.      '
     A                                  8  2'Totalt ant sendinger .............-
     A                                      .......:'
     A            ANTSEN         3Y 0O  8 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9  2'Antall send. IKKE SKUTT/IKKE MANUE-
     A                                      LT OK..:'
     A            IKKEKV         3Y 0O  9 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12  2'F11=Overf. de send SOM ER OK    '
     A                                      COLOR(BLU)
     A                                 12 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R PALLW
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CF11(11 'OVERFØR')
     A                                      CA12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'MANGLENDE PALLER PÅ UTGÅENDE OPPDR-
     A                                      AG ?'
     A                                      COLOR(WHT)
     A                                  3  2'Dette er EDI-mottatt henteoppdrag -
     A                                      hvor antall paller  '
     A                                      COLOR(WHT)
     A                                  4  2'INN fra kunde er større enn det so-
     A                                      m viderebelastes UT!'
     A                                      COLOR(WHT)
     A                                  5  2'Velg F12, og sjekk/endre pallant. -
     A                                      på den enkelte send.'
     A                                  6  2'Tast F11 KUN NÅR manuelle pallkvit-
     A                                      t. dekker opp diff. '
     A                                  8  2'Antall paller INN fra kunde/hente--
     A                                      transp.:'
     A            HOPLL2         3Y 0O  8 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9  2'Antall paller beslastet på UTgåred-
     A                                      e send.:'
     A            ANTPLL         3Y 0O  9 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12  2'F11=Overfør tross manglende PLL '
     A                                      COLOR(BLU)
     A                                 12 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R ENDRET
     A                                      WINDOW(*DFT 10 45 *NOMSGLIN)
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 11MSGCON(032 JOV1397 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(040 JOV1398 *LIBL/SYMSGF)
     A                                  4  2MSGCON(040 JOV1399 *LIBL/SYMSGF)
     A                                  5  2MSGCON(040 JOV1400 *LIBL/SYMSGF)
     A                                  6 24MSGCON(010 JOV1401 *LIBL/SYMSGF)
     A                                  6  2MSGCON(010 JOV1402 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 24MSGCON(010 JOV1403 *LIBL/SYMSGF)
     A                                  7  2MSGCON(010 JOV1404 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(040 JOV1405 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(040 JOV1406 *LIBL/SYMSGF)
     A                                 10 32'Enter'
     A                                      COLOR(BLU)
     A            HSVKT     R        O  6 14REFFLD(HENOSR/HSVKT *LIBL/HENOS)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HSM3      R        O  7 13REFFLD(HENOSR/HSM3 *LIBL/HENOS)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A            GMM3      R        O  7 35REFFLD(HENOSR/HSM3 *LIBL/HENOS)
     A                                      EDTCDE(M)
     A            GMVKT     R        O  6 36REFFLD(HENOSR/HSVKT *LIBL/HENOS)
     A                                      EDTCDE(4)
N03
N03  A          R ANNENW
N03  A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
N03  A                                      CF04(04 'SØK')
N03  A                                      CA12(12 'Avslutt')
N03  A                                      BLINK
N03  A            MSGNR          7A  P
N03  A                                  1  2'SYTR34B'
N03  A                                      COLOR(BLU)
N03  A                                  1 12'AVVIKENDE FAKTURABETALER'
N03  A                                      COLOR(WHT)
N03  A                                  1 37'('
N03  A                                      COLOR(WHT)
N03  A            MOTANNTX      12A  O  1 39COLOR(WHT)
N03  A                                  1 52')'
N03  A                                      COLOR(WHT)
N03  A                                  2  2'Avsender:'
N03  A            HOKNS          8S 0O  2 12COLOR(WHT)
N03  A            HONAS         30A  O  2 21COLOR(WHT)
N03  A                                  3  2'Navn / adresse mottatt i EDI:'
N03  A            EDINAV        30A  O  4  9COLOR(WHT)
N03  A            EDIAD1        30A  O  5  9COLOR(WHT)
N03  A            EDIAD2        30A  O  6  9COLOR(WHT)
N03  A            EDIAD3        30A  O  7  9COLOR(WHT)
N03  A                                  8  2'Fakturabetalers knr hos avsender:'
N03  A            EDIKNR        17A  O  8 36COLOR(WHT)
N03  A                                  9  2'Kobles (hvis oppgitt) til...     '
N03  A                                 10  2'  InternKn:'
N03  A            INTKNR         8Y 0B 10 14EDTCDE(3)
N03  A            INTNAV        30A  B 10 23COLOR(WHT)
N03  A                                 11  2'F4=Søk F12=Forrige'
N03  A                                      COLOR(BLU)
N03  A  30        MSGTX2        50A  O 12  2MSGID(&MSGNR SYMSGF)
N03  A                                      COLOR(WHT)
     A          R DIRBEKR
     A*%%TS  SD  20170928  113801  SYSTEMA     REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(*DFT 7 55 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 7 55 *NOMSGLIN)
     A                                      CF11(11 'OVERFØR')
     A                                      CA12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'Gjøre om til DIREKTE-oppdrag??    -
     A                                          '
     A                                      COLOR(WHT)
     A                                  3  2'Trykk endre for å fjerne oppdraget-
     A                                       fra hentingen.     '
     A                                      COLOR(WHT)
     A                                  4  2'Oppdraget vil da legges som et åpe-
     A                                      nt oppdrag på       '
     A                                      COLOR(WHT)
     A                                  5  2'trafikkavdeling'
     A                                      COLOR(WHT)
     A            HSAVDH         4Y 0O  5 20COLOR(WHT)
     A                                  7  2'Enter=Overfør til direkteoppdrag'
     A                                      COLOR(BLU)
     A                                  7 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AVD0TXT       50   O  6  2COLOR(RED)
