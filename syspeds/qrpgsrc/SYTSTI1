      *********************************************************************
      *  RPG ILE MODULE CGIDEV2/sytst1
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/sytsti1 MODULE(*LIBL/sytsti1)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 FEILMELDING DERSOM......
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FHEADF     IF   E           K DISK    USROPN
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEAF17R)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Number of variables
     DnbrVars          s             10i 0
      *
      *
      * Saved query string
     Dsavedquerystring...
     D                 s          32767    varying
      *
      * Client input variables
S01  D*cgiinp01        s             15
N01  D cgiinp01        s             35
     D cgiinp02        s              4
     D cgiinp03        s              7
      * Indicators for chknbr subprocedure
     D ChkNbrInds      ds
     D  Indicators                     n   dim(7)
      *
      * Constant for updHTMLvar subprocedure
     D initHTMLVars    c                   '0'
      *
      *--------------------------------------------------------------------
      * Other variables
      *--------------------------------------------------------------------
     D HeadfOpn        s              1
     D Hea17Opn        s              1
      * Return code
     D rc              s             10i 0 inz(0)                               return code
      *=====================================================================
      *
      * Override database files
      *
     C                   eval      rc = docmd('OVRDBF FILE(HEADF) +
     C                             TOFILE(SYSPEDF/HEADF) SECURE(*YES)')
      * rc=0=OK, rc=1=FAILED,
      *  (her bør resultat sjekkes og evt melding gis hvis FAILED....)
      *
     C                   eval      rc = docmd('OVRDBF FILE(HEAD17) +
     C                             TOFILE(SYSPEDF/HEAD17) SECURE(*YES)')
      * rc=0=OK, rc=1=FAILED,
      *  (her bør resultat sjekkes og evt melding gis hvis FAILED....)
      *
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
      * Write qualified job name to debug file.  The *on
      * parameter forces output even if debugging is off.
      * Remove this parameter or change it to *off if you
      * want the output only if debugging is on.
     C                   callp     wrtjobdbg(*on)
      * Get input
     C                   eval      nbrVars =
     C                             zhbgetinput(savedquerystring:qusec)
      *
      * Parse variables from QUERY_STRING environment variable
      * Søkeref
     C                   eval      cgiinp01 = zhbgetvar('cgiinp01')
      * Avd..
     C                   eval      cgiinp02 = zhbgetvar('cgiinp02')
      * Oppd..
     C                   eval      cgiinp03 = zhbgetvar('cgiinp03')
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Send section
     C                   callp     wrtsection('all')
      * Quit
     C                   exsr      Exit
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtmlifs('/syspeddev/sytsti1.html':
     C                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$all
      *=====================================================================
     C     SetAll        begsr
     C     cgiinp01      ifne      *blanks
     C     Hea17Opn      ifeq      *blank
     c                   open      head17
     C                   eval      Hea17Opn = 'x'
     C                   endif
     C     cgiinp01      chain     head17                             33
     C                   else
     C     cgiinp02      ifne      *blanks
     C     cgiinp03      andne     *blanks
     c     opdkey        klist
     c                   kfld                    heavd
     c                   kfld                    heopd
     C                   move      cgiinp02      heavd
     C                   move      cgiinp03      heopd
     C     HeadfOpn      ifeq      *blank
     C                   open      HEADF
     C                   eval      HeadfOpn = 'x'
     C                   endif
     C     opdkey        chain     headf                              33
     C                   end
     C                   end
      * ref
     C                   callp     updHTMLvar('REF':herfa:
     C                             InitHTMLVars)
      * avd/opd
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(HEOPD:'Z')))
      * mottaker
     C                   callp     updHTMLvar('MOTT':henak)
     C                   callp     updHTMLvar('ADR1':headk1)
     C                   callp     updHTMLvar('ADR2':headk2)
     C                   callp     updHTMLvar('ADR3':headk3)
      * ant vareslag vekt
     C                   callp     updHTMLvar('ANT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('VS':HEVS1)
     C                   callp     updHTMLvar('VKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHTMLvar('M3':
     C                             %trim(%editw(HEM3:'    ,   ')))
      * Kvitt mottatt
     C                   MOVEL     HESGM         xxkvmo
     C                   MOVE      HEDTMO        xxkvmo           20
     C                   callp     updHTMLvar('KVMO':xxkvmo)
      *
     C                   endsr
