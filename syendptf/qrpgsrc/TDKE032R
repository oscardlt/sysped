********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12100 * 01 CB  PTF12 MRN-nr  ikke merket, sök etter MRN
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKE032R MODULE(*LIBL/TDKE032R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKEF      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D DKEF_SYAV       S              4S 0
     D DKEF_SYOP       S              7S 0
     D DKEF_FATY       S              4
     D DKEF_FATX       S             35
     D DKEF_VAKD       S              3
     D DKEF_VAKU       S              7S 4
     D DKEF_FABL       S             11S 3
     D DKEF_MRN        S             18

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(DKEF_SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(DKEF_SYOP:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(DKEF_FATX)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invoiceUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKEF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      DKEF_SYAV  = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      DKEF_SYOP  = c2n2(WA)
     C                   EVAL      DKEF_FATX  = zhbgetvar('FAK')
     C                   EVAL      DKEF_FATY  = zhbgetvar('DKEF_FATY')
     C                   EVAL      DKEF_VAKD  = zhbgetvar('DKEF_VAKD')
     C                   EVAL      WA      = zhbgetvar('DKEF_VAKU')
N01  C                   EVAL      DKEF_VAKU  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('DKEF_FABL')
N01  C                   EVAL      DKEF_FABL  = c2n2(WA)
     C                   EVAL      DKEF_MRN   = zhbgetvar('DKEF_MRN')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DKEFKEY       KLIST
     C                   KFLD                    DKEF_SYAV
     C                   KFLD                    DKEF_SYOP
     C                   KFLD                    DKEF_FATX
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKEF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     DKEFKEY       CHAIN     DKEF                               55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   DKEFR
     C                   EVAL      XDKEF_SYAV=DKEF_SYAV
     C                   EVAL      XDKEF_SYOP=DKEF_SYOP
     C                   EVAL      XDKEF_FATX=DKEF_FATX
     C                   WRITE     DKEFR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    DKEFR
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XDKEF_FATY=DKEF_FATY
     C                   EVAL      XDKEF_VAKD=DKEF_VAKD
     C                   EVAL      XDKEF_VAKU=DKEF_VAKU
     C                   EVAL      XDKEF_FABL=DKEF_FABL
     C*                  EVAL      XDKEF_MRN=DKEF_MRN
     C                   UPDATE    DKEFR
     C                   ENDSR
