********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10244 * 01 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
10262 * 02 YBC PTF10 TOLLSATS ER UTVIDERT TIL 7.2
10286 * 03 YBC PTF10 VEKT ER ENDRET FRA 9.0 TIL 12.3
10318 * 04 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
      *              MYE ENDRING. INGEN MERKING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 05 YBC PTF11 OMBEREGNING
11176 * 06 YBC PTF11 TILLEGGSLINJER MED SAMME POST
11186 * 07 YBC PTF11 VIS TOTAL VEKT OG SUM AV LINJEVEKT
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC(SADE010 SAD052 SY400LO/SADTV-
     A                                      INN)
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           3Y 0O  7  2EDTCDE(4)
     A            SLKNR          8S 0H
     A            SLALFA        28A  O  7  6TEXT('ALFASØKING')
     A            SLTXT2        20A  O  7 35
     A            SLR31          1A  O  7 56
     A            SLOPPL         2A  O  7 59TEXT('OPPRINGELSE LAND')
     A            SLTANR         8S 0O  7 62TEXT('TARIFFNR.')
     A            SLTN           1A  O  7 71TEXT('TOLLNEDSETTELSE')
     A            SLPRE          1A  O  7 74
S03  A*           SLVEKT         5Y 0O  7 76TEXT('VEKR')
N03  A            SLVEKT        12Y 3O  7 76TEXT('VEKR')
     A                                      EDTCDE(4)
     A            SLPVA          1A  O  7 90
S02  A*           SLSATS         5Y 2O  7 85TEXT('PVA SATS')
N02  A            SLSATS         7Y 2O  7 83TEXT('PVA SATS')
     A                                      EDTCDE(4)
S02  A*           SLMFR          1A  O  7 92
N02  A            SLMFR          1A  O  7102
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  9'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 30'Tdeknr.'
     A            UAVD           4Y 0O  1 38COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 43'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 45COLOR(BLU)
S05  A*                                     EDTCDE(4)
N05  A                                      EDTCDE(L)
E07  A                                  2  2'Fsum->'
E07  A            UBEL1         13Y 2O  2 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E07  A                                  2 25'Vsum->'
E07  A            UVSUM         13Y 2O  2 32TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E07  A                                  2 47'Diff->'
E07  A            UDIFF         13Y 2O  2 54TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
N07  A                                  2 70'Tvkt->'
N07  A            UVKB           9Y 0O  2 77TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                  2 87'Lvkt->'
N07  A            U2VKB         13Y 3O  2 94TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                  2109'Diff->'
N07  A            UVDIF         13Y 3O  2116TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(L)
     A                                  3  2'Siste li Varenr.. Nettovekt..... M-
     A                                      engde.... Tollverdi.... FK FF  Sats-
     A                                      .... Varebeskrivelse...............-
     A                                       S'
     A                                      COLOR(WHT)
     A            SVVNT          8Y 0O  4 11COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVVKTN        12Y 3O  4 20COLOR(BLU)
     A                                      EDTCDE(L)
     A            SVNTM          9Y 0O  4 35COLOR(BLU)
     A                                      EDTCDE(L)
     A            SVBELT        11Y 2O  4 46COLOR(BLU)
     A                                      EDTCDE(L)
     A            SVFYL          2A  O  4 60COLOR(BLU)
     A            SVAVTS         3A  O  4 63COLOR(BLU)
     A            SVAVTP         7Y 3O  4 67COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVVT01        30A  O  4 76COLOR(BLU)
     A            SV01           1A  O  4107COLOR(RED)
     A  39                              5 50'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              5 65'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Lin Alfasøkingtekst............. T-
     A                                      ekst.............. R31 Op Tariffnr -
     A                                      TN P VEKT          PVA Tol.sats MF'
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF04(04 'SØK KODER')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'Kalkulator av/på')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF03(03 'Be om utskrift')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CF11(11 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A                                      CF12(12 'Gå til bilde02')
     A  34                                  CF21(21 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A  34                                  CF22(22 'MERK B PÅ STATUS')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPDOC('SADE010' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HLPARA(02 014 02 042)
     A          H                           HLPDOC('SADE011' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HLPARA(02 044 02 072)
     A          H                           HLPDOC('SADE012' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HLPARA(03 008 03 052)
     A          H                           HLPDOC('SAD015' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HLPARA(04 008 05 078)
     A          H                           HLPDOC('SAD016' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HLPARA(06 001 06 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  4'Linjenr  Brutto vekt.... Netto vek-
     A                                      t.... Mengde.... Tollverdi.... FK F-
     A                                      F Promille Sekv.'
     A                                      COLOR(WHT)
     A            W2NR           4Y 0B  2  5TEXT('LINJENR.')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  89                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A            W2VKTB        12Y 3B  2 14TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2VKTN        12Y 3B  2 29TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2NTM          9Y 0B  2 44TEXT('Mengde')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2BELT        11Y 2B  2 55TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2FYL          2A  B  2 69TEXT('OPPRINGELSESFYLKE')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A  39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A            W2AVT          1A  B  2 72TEXT('AVG. KONJUNKTURFOND')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  97
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      VALUES(' ' 'N' 'X')
     A                                      DUP
     A            W2AVTP         7Y 3B  2 75TEXT('PROSENTSATS AV SVAVT')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  97
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AVTS         3A  B  2 84
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  97
     AO 39                                  DSPATR(PR)
     A N39                                  COLOR(YLW)
     A                                  3  2'V.B'
     A            W2FT01        28A  B  3  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2NT01         6Y 0B  3 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH01         4A  B  3 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2VT01        30A  B  3 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A                                  4  2'T.O'
     A            W2TOP1        17A  B  4  6TEXT('TILLEGGSOPPLYSNING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A                                  4 52'Ref:'
     A            W2CRE1         3A  B  4 57TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A N85                                  COLOR(YLW)
     A  85                                  COLOR(RED)
     A                                      DUP
     A                                  5  2'F3/F17=Avsl.'
     A N35                                  COLOR(BLU)
     A  35                                  COLOR(RED)
     A                                  5 15'F8=Søk F9=Kal.'
     A                                      COLOR(BLU)
     A                                  5 30'F11=Auto vekt'
     A                                      COLOR(BLU)
     A                                  5 60'F15=Auto kopi T.O.'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                  1  9'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 30'Tdeknr.'
     A            UAVD           4Y 0O  1 38COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 43'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 45COLOR(BLU)
S05  A*                                     EDTCDE(4)
N05  A                                      EDTCDE(L)
E07  A                                  2  2'Fsum->'
E07  A            UBEL1         13Y 2O  2 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E07  A                                  2 25'Vsum->'
E07  A            UVSUM         13Y 2O  2 32TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
E07  A                                  2 47'Diff->'
E07  A            UDIFF         13Y 2O  2 54TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
N07  A                                  2 70'Tvkt->'
N07  A            UVKB           9Y 0O  2 77TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                  2 87'Lvkt->'
N07  A            U2VKB         13Y 3O  2 94TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(4)
N07  A                                  2109'Diff->'
N07  A            UVDIF         13Y 3O  2116TEXT('Beløp Faktsum')
N07  A                                      COLOR(WHT)
N07  A                                      EDTCDE(L)
     A  39                              5 50'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A  37                              5 65'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Lin Alfasøkingtekst............. T-
     A                                      ekst.............. R31 Op Tariffnr -
     A                                      TN P VEKT          PVA Tol.sats MF'
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  3  2'Søk fra kundenr.  --->'
     A            W2KNR          8Y 0B  3 25TEXT('KUNDENR.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4  2'  fra vare (alfa) --->'
     A            W2ALFA        28A  I  4 25TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
