      *
      ***********************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV FLYFRAKTPRISER (NETTO)  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 07-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-50 54-66 95-97
      ***********************************************************
     FFIRM      IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTFS    IF   E           K DISK
     FAIRNRH    UF A E           K DISK
     FAIRNRHL1  IF   E           K DISK
     F                                     RENAME(AIRNRHR:AIRNRH1)
     FAIRNRHL2  IF   E           K DISK
     F                                     RENAME(AIRNRHR:AIRNRH2)
     FAIRNRD    UF A E           K DISK
     FSYEF61D   CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  WSRLI1                 1      3  0
     D  WSRLI2                 4      6  0
     D  WSRLI3                 7      9  0
     D  WSRLI4                10     12  0
     D  WSRLI5                13     15  0
     D  WSRLI6                16     18  0
     D  WSRLI7                19     21  0
     D  WSRLI8                22     24  0
     D  LI                     1     24  0
     D                                     DIM(8)
     D                 DS
     D  WSRPF1                 1      3
     D  WSRPF2                 4      6
     D  WSRPF3                 7      9
     D  WSRPF4                10     12
     D  WSRPF5                13     15
     D  WSRPF6                16     18
     D  WSRPF7                19     21
     D  WSRPF8                22     24
     D  FR                     1     24
     D                                     DIM(8)
     D                 DS
     D  WSRPT1                 1      3
     D  WSRPT2                 4      6
     D  WSRPT3                 7      9
     D  WSRPT4                10     12
     D  WSRPT5                13     15
     D  WSRPT6                16     18
     D  WSRPT7                19     21
     D  WSRPT8                22     24
     D  TO                     1     24
     D                                     DIM(8)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D  WSCLO2               382    383B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     *IN05         IFEQ      '0'
     C                   MOVE      W2RLIN        YLIN              3 0
     C                   MOVE      W2RPFR        YSDF              3
     C                   MOVE      W2RPTO        YSDT              3
     C                   END
     C                   MOVE      *ZEROS        W2RLIN
     C                   MOVE      *BLANKS       W2RPFR
     C                   MOVE      *BLANKS       W2RPTO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   51AIRNHK        SETLL     AIRNRH
     C   51              READ(N)   AIRNRH                                 94
     C   52AIR1HK        SETLL     AIRNRHL1
     C   52              READ      AIRNRHL1                               94
     C   53AIR2HK        SETLL     AIRNRHL2
     C   53              READ      AIRNRHL2                               94
      *
     C     START2        TAG
     C     *IN94         IFEQ      '0'
     C                   DO        14
     C                   ADD       1             ZRECNO
     C     KOFS2K        CHAIN     KODTFS                             33
     C  N33              MOVEL     KFSNAV        XNAF
     C   33              MOVE      *BLANKS       XNAF
     C                   MOVEL     AIRPFR        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XSDF
     C   33              MOVE      *BLANKS       XSDF
     C                   MOVEL     AIRPTO        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XSDT
     C   33              MOVE      *BLANKS       XSDT
     C                   WRITE     SUBFIL
     C   51              READ(N)   AIRNRH                                 94
     C   52              READ      AIRNRHL1                               94
     C   53              READ      AIRNRHL2                               94
     C     *IN94         IFEQ      '0'
     C                   MOVE      AIRLIN        YLIN2             3 0
     C                   MOVE      AIRPFR        YSDF2             3
     C                   MOVE      AIRPTO        YSDT2             3
     C                   ENDIF
     C  N94              ENDDO
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C     START3        TAG
     C                   Z-ADD     XRECNO        ZRECNO
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** SØK
     C   04              DO
     C                   EXSR      SØKDT1
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   MOVE      *ZEROS        WSRLIN
     C                   MOVE      *BLANKS       WSRPFR
     C                   MOVE      *BLANKS       WSRPTO
     C                   GOTO      START5
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C  N94AIRN2K        CHAIN(N)  AIRNRH                             94
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      ' '                                          1<-B
     C     W2RLIN        ORNE      0
     C     W2RPFR        ORNE      *BLANKS
     C     W2RPTO        ORNE      *BLANKS
     C     W2NR          IFEQ      ' '                                           2<-B
     C                   MOVEA     '000'         *IN(51)
     C     W2RLIN        COMP      0                                  5151
     C  N51W2RPFR        COMP      *BLANKS                            5252
     C  N51
     CANN52              MOVE      '1'           *IN53
     C                   GOTO      START
     C                   ELSE                                                    2
     C     W2NR          COMP      '1'                                    06
     C   06              DO                                                       3<-B
     C                   MOVE      W2RLIN        WSRLIN
     C                   MOVE      W2RPFR        WSRPFR
     C                   MOVE      W2RPTO        WSRPTO
     C                   MOVE      *ZEROS        W2RLIN
     C                   MOVE      *BLANKS       W2RPFR
     C                   MOVE      *BLANKS       W2RPTO
     C                   GOTO      START5
     C                   END                                                      3<-E
     C     AIRN3K        CHAIN(N)  AIRNRH                             98
     C     *IN98         IFEQ      '1'                                            3<-B
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0549'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYOPT
     C     UFLAGG        CABEQ     3             SLUTT
     C                   MOVE      *ZEROS        W2RLIN
     C                   MOVE      *BLANKS       W2RPFR
     C                   MOVE      *BLANKS       W2RPTO
     C                   END                                                      3<-E
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
      *
     C     START5        TAG
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     3             LINNBR
     C     START6        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
     C     WSCLO2        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDT2
     C                   GOTO      START6
     C                   END
     C   12              DO
     C                   Z-ADD     6             POSNBR
     C                   Z-ADD     2             LINNBR
     C                   GOTO      START3
     C                   END
      *
     C                   EXSR      TESTB
     C   30              GOTO      START6
      * OPPDATERER REGISTER
     C     AIRN4K        CHAIN(N)  AIRNRH                             33
     C     *IN33         IFEQ      '1'
     C                   MOVE      WSRLIN        AIRLIN
     C                   MOVE      WSRPFR        AIRPFR
     C                   MOVE      WSRPTO        AIRPTO
     C                   MOVE      FICURR        AIRCUR
     C                   MOVE      *ZEROS        AIROMR
     C                   WRITE     AIRNRHR
     C     KOFS2K        CHAIN     KODTFS                             33
     C  N33              MOVEL     KFSNAV        XNAF
     C   33              MOVE      *BLANKS       XNAF
     C                   MOVEL     AIRPFR        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XSDF
     C   33              MOVE      *BLANKS       XSDF
     C                   MOVEL     AIRPTO        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XSDT
     C   33              MOVE      *BLANKS       XSDT
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   MOVE      YRECNO        XRECNO
     C                   WRITE     SUBFIL
     C                   END
      *
     C                   MOVEL     WSRPTO        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C                   CALL      'SYEF61RA'
     C                   PARM                    WSRLIN
     C                   PARM                    WSRPFR
     C                   PARM                    WSRPTO
     C                   PARM                    ST2NVN
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    FIFIRM
     C     UFLAGG        CABNE     3             START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
      * TEST AIRLINER
     C     KODFSK        CHAIN     KODTFS                             90
     C  N90              MOVE      KFSNAV        WSNAF
     C   90              DO
     C                   SETON                                            30
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     3             LINNBR
     C                   MOVE      'SPF0009'     MSGNR
     C                   GOTO      SLTB0
     C                   END
      *
      * TEST FROM PLASS
     C                   MOVEL     WSRPFR        ST2KOD
     C     ST2KOD        CHAIN     STED2                              89
     C  N89              MOVE      ST2NVN        WSSDF
     C   89              DO
     C                   SETON                                        30
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     4             LINNBR
     C                   MOVE      'SPD0016'     MSGNR
     C                   GOTO      SLTB0
     C                   END
      *
      * TEST TO
     C                   MOVEL     WSRPTO        ST2KOD
     C     ST2KOD        CHAIN     STED2                              88
     C  N88              MOVE      ST2NVN        WSSDT
     C   88              DO
     C                   SETON                                        30
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     5             LINNBR
     C                   MOVE      'SPD0017'     MSGNR
     C                   GOTO      SLTB0
     C                   END
      *
     C     SLTB0         ENDSR
      *
     C***********************************************
     C     TESTB2        BEGSR
     C***********************************************
     C     1             DO        8             XN
     C                   SELECT
     C     LI(XN)        WHENEQ    AIRLIN
     C     FR(XN)        ANDEQ     AIRPFR
     C     TO(XN)        ANDEQ     AIRPTO
     C                   MOVE      *ZEROS        LI(XN)
     C                   MOVE      *BLANKS       FR(XN)
     C                   MOVE      *BLANKS       TO(XN)
      *
     C     FR(XN)        WHENEQ    *BLANKS
     C     TO(XN)        OREQ      *BLANKS
     C                   MOVE      *ZEROS        LI(XN)
     C                   MOVE      *BLANKS       FR(XN)
     C                   MOVE      *BLANKS       TO(XN)
      *
     C                   OTHER
      * TEST AIRLINER
     C                   MOVE      LI(XN)        WSRLIN
     C     KODFSK        CHAIN     KODTFS                             33
     C   33              DO
     C     91            SUB       XN            X2                3 0
     C                   MOVE      '1'           *IN(X2)
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     3             LINNBR
     C     5             MULT      XN            X2
     C     18            ADD       X2            POSNBR
     C                   MOVE      'SPF0009'     MSGNR
     C                   GOTO      SLTB2
     C                   END
      *
      * TEST FROM PLASS
     C                   MOVEL     FR(XN)        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C   33              DO
     C     83            SUB       XN            X2
     C                   MOVE      '1'           *IN(X2)
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     4             LINNBR
     C     5             MULT      XN            X2
     C     18            ADD       X2            POSNBR
     C                   MOVE      'SPD0016'     MSGNR
     C                   GOTO      SLTB2
     C                   END
      *
      * TEST TO
     C                   MOVEL     TO(XN)        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C   33              DO
     C     75            SUB       XN            X2
     C                   MOVE      '1'           *IN(X2)
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C     5             MULT      XN            X2
     C     18            ADD       X2            POSNBR
     C                   MOVE      'SPD0017'     MSGNR
     C                   GOTO      SLTB2
     C                   END
      *
     C                   ENDSL
     C                   END
      *
     C     SLTB2         ENDSR
      *
      ***********************************************
     C     NYOPT         BEGSR
      ***********************************************
     C                   MOVE      '1'           *IN90
     C                   MOVE      W2RLIN        AIRLIN
     C                   MOVE      W2RPFR        AIRPFR
     C                   MOVE      W2RPTO        AIRPTO
      *
     C                   SELECT
     C     W2NR          WHENEQ    '2'
     C                   EXSR      CHGFR
     C     W2NR          WHENEQ    '3'
     C                   EXSR      CPYFR
     C     W2NR          WHENEQ    '4'
     C                   EXSR      DLTFR
     C                   OTHER
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   GOTO      SLOPT
     C                   ENDSL
      *
     C     SLOPT         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      ' '           W2NR
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGFR         BEGSR
      ***********************************************
     C     *IN90         IFEQ      '0'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C                   MOVEL     AIRPTO        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C                   CALL      'SYEF61RA'
     C                   PARM                    AIRLIN
     C                   PARM                    AIRPFR
     C                   PARM                    AIRPTO
     C                   PARM                    ST2NVN
     C                   PARM                    UFLAGG
     C                   PARM                    FIFIRM
      *
     C                   ENDSR
      *
      ***********************************************
     C     CPYFR         BEGSR
      ***********************************************
     C     *IN90         IFEQ      '0'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C     1             DO        8             XN                1 0
     C                   MOVE      AIRLIN        LI(XN)
     C                   MOVE      AIRPFR        FR(XN)
     C                   MOVE      *BLANKS       TO(XN)
     C                   END
     C                   Z-ADD     23            POSNBR
     C                   Z-ADD     5             LINNBR
      *
     C     STCPY         TAG
     C                   EXFMT     BILDE02
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVEA     '00000000'    *IN(75)
     C                   MOVEA     '00000000'    *IN(67)
     C                   MOVE      '0'           *IN30
     C     WSCLO2        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDT3
     C                   GOTO      STCPY
     C                   END
     C   12              GOTO      SLCPY
      *
     C                   EXSR      TESTB2
     C   30              GOTO      STCPY
      *
     C                   EXSR      CPYFR2
      *
     C     SLCPY         ENDSR
      *
     C***********************************************
     C     CPYFR2        BEGSR
     C***********************************************
     C                   MOVE      AIRLIN        CPYLIN            3 0
     C                   MOVE      AIRPFR        CPYPFR            3
     C                   MOVE      AIRPTO        CPYPTO            3
     C     AIRN5K        CHAIN(N)  AIRNRH                             33
     C     1             DO        8             XN
     C     FR(XN)        IFNE      *BLANKS
     C     TO(XN)        ANDNE     *BLANKS
     C                   MOVE      LI(XN)        AIRLIN
     C                   MOVE      FR(XN)        AIRPFR
     C                   MOVE      TO(XN)        AIRPTO
     C     AIRN6K        CHAIN     AIRNRH                             33
     C                   MOVE      FICURR        AIRCUR
     C                   MOVE      *ZEROS        AIROMR
     C  N33              UPDATE    AIRNRHR
     C   33              WRITE     AIRNRHR
      *
     C     KOFS2K        CHAIN     KODTFS                             33
     C  N33              MOVEL     KFSNAV        XNAF
     C   33              MOVE      *BLANKS       XNAF
     C                   MOVEL     AIRPFR        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XSDF
     C   33              MOVE      *BLANKS       XSDF
     C                   MOVEL     AIRPTO        ST2KOD
     C     ST2KOD        CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XSDT
     C   33              MOVE      *BLANKS       XSDT
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   MOVE      YRECNO        XRECNO
     C                   WRITE     SUBFIL
      * SLETTE ALLE GAMLE RATER
     C     AIRN6K        SETLL     AIRNRD
     C     AIRN6K        READE     AIRNRD                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    AIRNRDR
     C     AIRN6K        READE     AIRNRD                                 33
     C  N33              ENDDO
      * KOPIERE RATER
     C     AIRN5K        SETLL     AIRNRD
     C     AIRN5K        READE(N)  AIRNRD                                 33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      LI(XN)        AIRLIN
     C                   MOVE      FR(XN)        AIRPFR
     C                   MOVE      TO(XN)        AIRPTO
     C                   WRITE     AIRNRDR
     C     AIRN5K        READE(N)  AIRNRD                                 33
     C  N33              ENDDO
      *
     C                   END
     C                   END
      *
     C*                    MOVELWSRPTO    ST2KOD
     C*          ST2KOD    CHAINSTED2                33
     C*                    CALL 'SYEF61RA'
     C*                    PARM           WSRLIN
     C*                    PARM           WSRPFR
     C*                    PARM           WSRPTO
     C*                    PARM           ST2NVN
     C*                    PARM           UFLAGG
     C*                    PARM           FIFIRM
      *
     C                   ENDSR
      *
      ***********************************************
     C     DLTFR         BEGSR
      ***********************************************
     C                   MOVE      '1'           *IN27
     C                   MOVE      AIRLIN        WSRLIN
     C                   MOVE      AIRPFR        WSRPFR
     C                   MOVE      AIRPTO        WSRPTO
     C     STDLT         TAG
     C                   EXFMT     BILDE01
     C   12              GOTO      SLDLT
      *
     C                   EXSR      DLTFR2
     C     SLDLT         TAG
     C     *IN90         IFEQ      '0'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   MOVE      '0'           *IN27
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTFR2        BEGSR
     C***********************************************
     C     AIRN6K        SETLL     AIRNRD
     C     AIRN6K        READE     AIRNRD                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    AIRNRDR
     C     AIRN6K        READE     AIRNRD                                 33
     C  N33              ENDDO
      *
     C     AIRN6K        CHAIN     AIRNRH                             33
     C                   DELETE    AIRNRHR
      *
     C     *IN90         IFEQ      '0'
     C                   MOVE      *ZEROS        AIRLIN
     C                   MOVE      *BLANKS       XNAF
     C                   MOVEL     '*DELETE'     XNAF
     C                   MOVE      *BLANKS       AIRPFR
     C                   MOVE      *BLANKS       XSDF
     C                   MOVE      *BLANKS       AIRPTO
     C                   MOVE      *BLANKS       XSDT
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     1             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C   98              DO                                                      2<-B
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END                                                     2<-E
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              3 0
     C     XLIN          IFEQ      0                                             2<-B
     C                   Z-ADD     14            LINNBR
     C                   ELSE                                                    2
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                     2<-E
     C     XNAF          IFNE      '*DELETE'                                     2<-B
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    '2'
     C                   EXSR      CHGFR
     C     WSNR          WHENEQ    '3'
     C                   EXSR      CPYFR
     C     WSNR          WHENEQ    '4'
     C                   EXSR      DLTFR
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDT1        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     7
     C                   CALL      'SYFI10K'
     C                   PARM                    W2RLIN
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK1
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     27
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    W2RPFR
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     45            POSNBR
     C                   GOTO      SLSØK1
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     45
     C     POSNBR        ANDLE     47
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    W2RPTO
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     2             POSNBR
     C                   GOTO      SLSØK1
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK1        ENDSR
      *
     C***********************************************
     C     SØKDT2        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   CALL      'SYFI10K'
     C                   PARM                    WSRLIN
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    WSRPFR
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     25
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    WSRPTO
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK2        ENDSR
      *
     C***********************************************
     C     SØKDT3        BEGSR
     C***********************************************
     C     POSNBR        IFGE      23
     C     POSNBR        ANDLE     60
     C     POSNBR        SUB       18            X2
     C                   DIV       5             X2
     C                   MVR                     XN
     C                   ELSE
     C                   Z-ADD     5             XN
     C                   END
      *
     C     LINNBR        IFEQ      3
     C     XN            ANDLT     3
     C                   MOVE      LI(X2)        ULIN              3 0
     C                   CALL      'SYFI10K'
     C                   PARM                    ULIN
     C                   MOVE      ULIN          LI(X2)
     C     X2            IFEQ      8
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     23            POSNBR
     C                   ELSE
     C     X2            ADD       1             POSNBR
     C                   MULT      5             POSNBR
     C                   ADD       18            POSNBR
     C                   END
     C                   GOTO      SLSØK3
     C                   END
      *
     C     LINNBR        IFEQ      4
     C     XN            ANDLT     3
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   MOVE      *BLANKS       UFR
     C                   MOVEL     FR(X2)        UFR
     C                   CALL      'SYFI10F'
     C                   PARM                    UFR               5
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   MOVEL     UFR           FR(X2)
     C     X2            IFEQ      8
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     23            POSNBR
     C                   ELSE
     C     X2            ADD       1             POSNBR
     C                   MULT      5             POSNBR
     C                   ADD       18            POSNBR
     C                   END
     C                   GOTO      SLSØK3
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     XN            ANDLT     3
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   MOVE      *BLANKS       UTO
     C                   MOVEL     TO(X2)        UTO
     C                   CALL      'SYFI10F'
     C                   PARM                    UTO               5
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   MOVEL     UTO           TO(X2)
     C     X2            IFEQ      8
     C                   Z-ADD     23            POSNBR
     C                   ELSE
     C     X2            ADD       1             POSNBR
     C                   MULT      5             POSNBR
     C                   ADD       18            POSNBR
     C                   END
     C                   GOTO      SLSØK3
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK3        ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODFSK        KLIST
     C                   KFLD                    KFSUNI
     C                   KFLD                    WSRLIN
     C                   MOVE      'FS'          KFSUNI
     C     KOFS2K        KLIST
     C                   KFLD                    KFSUNI
     C                   KFLD                    AIRLIN
     C     AIRNHK        KLIST
     C                   KFLD                    YLIN
     C                   KFLD                    YSDF
     C                   KFLD                    YSDT
     C     AIR1HK        KLIST
     C                   KFLD                    YSDF
     C                   KFLD                    YLIN
     C                   KFLD                    YSDT
     C     AIR2HK        KLIST
     C                   KFLD                    YSDT
     C                   KFLD                    YLIN
     C                   KFLD                    YSDF
     C     AIRN2K        KLIST
     C                   KFLD                    YLIN2
     C                   KFLD                    YSDF2
     C                   KFLD                    YSDT2
     C     AIRN3K        KLIST
     C                   KFLD                    W2RLIN
     C                   KFLD                    W2RPFR
     C                   KFLD                    W2RPTO
     C     AIRN4K        KLIST
     C                   KFLD                    WSRLIN
     C                   KFLD                    WSRPFR
     C                   KFLD                    WSRPTO
     C     AIRN5K        KLIST
     C                   KFLD                    CPYLIN
     C                   KFLD                    CPYPFR
     C                   KFLD                    CPYPTO
     C     AIRN6K        KLIST
     C                   KFLD                    AIRLIN
     C                   KFLD                    AIRPFR
     C                   KFLD                    AIRPTO
      *
     C                   READ      FIRM                                   33
     C                   MOVEA     '100'         *IN(51)
      *
     C                   ENDSR
      *
