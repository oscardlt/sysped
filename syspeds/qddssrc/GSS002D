     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R GSS002DA                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4')
     A            CCCG           2A  O  5  6
     A            CCDSC         50A  O  5 12
      *
     A          R GSS002DB                  SFLCTL(GSS002DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS002D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N27                              1 21'Work with customer group codes'
     A                                      DSPATR(HI)
     A  27                              1 21'Inquiry customer group codes'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Choose option,press ENTER:'
     A                                      COLOR(BLU)
     A N27                              3 32'2=Change  4=Delete'
     A                                      COLOR(BLU)
     A  27                              3 32'1=Pick   2=Change'
     A                                      COLOR(BLU)
     A                                  4  2'Op  Code  Description'
     A                                      DSPATR(HI)
      *
     A          R GSS002DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N27                                  CF03(03 'End')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A  27                                  CF12(12 'Previous')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2'F3=End F5=Refresh F6=New entry F8=-
     A                                       Search'
     A                                      COLOR(BLU)
     A  27                              1  2'F5=Refresh F6=New entry F8=Searc-
     A                                      h F12=Previous'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS002DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  6'Code Description'
     A                                      DSPATR(HI)
     A            WSCG           2A  B  2  6DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSDSC         50A  B  2 12DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  3  2'F12=Previous'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS002DE
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra  --> '
     A            W2DSC         50A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R GSS002DF
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  6'Code  Description'
     A                                      DSPATR(HI)
     A            CCCG           2A  O  2  6DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            CCDSC         50A  O  2 12DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R GSS002DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF03(03 'End')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A  27                                  CF12(12 'End')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS002D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N27                              1 21'Work with customer group codes'
     A                                      DSPATR(HI)
     A  27                              1 21'Inquiry customer group codes'
     A                                      DSPATR(HI)
     A                                  3  2'Choose option, press ENTER:'
     A                                      COLOR(BLU)
     A N27                              3 32'2=Change  4=Delete'
     A                                      COLOR(BLU)
     A  27                              3 32'1=Pick 2=Change'
     A                                      COLOR(BLU)
     A                                  4  2'Op  Code  Description'
     A                                      DSPATR(HI)
     A                                  6  6'No code found'
     A                                      COLOR(RED)
     A N27                             23  2'F3=End   F5=Refresh F6=Create new -
     A                                      F8=Search'
     A                                      COLOR(BLU)
     A  27                             23  2'F5=Refresh F6=Create new F8=Search-
     A                                       F2=Previous'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
