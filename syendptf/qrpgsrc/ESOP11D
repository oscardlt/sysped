      *********************************************************************
      * PROGRAM SKAL DIREKT LAGE OPPDRAG I HEADF
      *********************************************************************
      * 01 PTF10 JOV - hvis til innlandsavd tildel sendingsnr
10172 * 02 SH  PTF10 norsk text til Track and trace
      * 03 JOV PTF10 INNLAND MED AUTOSETT AV AVDELING ETTER POSTNR
      * 04 JOV PTF10 setter IKKE utlev-forbehold ved henting
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 05 YBC PTF11 OPPDATER TRACKT
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 06 JOV PTF12 OPPDATER Arkiv med opplastede dokumenter (endre logg)
      * 07 JOV PTF12 Hvis Tr-modul innland og dato blank - sett dagens
      *********************************************************************
     FZEADL01   UF   E           K DISK
     FZREETXT1  IF   E           K DISK
     FZRISOL01  IF   E           K DISK
     FHEADF     UF A E           K DISK    RENAME(HEADFR:HEADFRNY)
     FDISP      O  A E             DISK
     FFREETXT   O  A E             DISK    RENAME(FREETXTR:FREETXTRNY)
     FFRISOK    O  A E             DISK    RENAME(FRISOKR:FRISOKRNY)
     FTRACKF    O  A E             DISK
N05  FTRACKT    O  A E             DISK
N03  FDOKUFE    O  A E             DISK
     FTELL      UF   E           K DISK
     FKODFRI    IF   E           K DISK
     FHEDUMMY   IF   E           K DISK    RENAME(HEADFR:HEDUMR)
N01  FKODTA     IF   E           K DISK
N03  FKODTRI    IF   E           K DISK
N03  FNOPNR     IF   E           K DISK
N06  FARKIVL10  UF   E           K DISK
      *
      *
E01  D TTXSTR          C                   CONST('Order entered (sent -
E01  D                                     in from WEB-booking)')
N02  D TTXSTL          C                   CONST('Ordre opprettet (sendt -
N02  D                                     inn fra WEB-booking)')
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRPLK
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRPLK         BEGSR
      ***********************************************
S03  C**   ZEADL01K      CHAIN(N)  ZEADL01                            33
      *
      *
      * HENTER FRA DUMMY
     C     DUMKEY        CHAIN     HEDUMMY                            33
     C                   IF        *IN33 = *OFF
     C                   MOVE      HEKNA         DUKNA             8 0
     C                   MOVE      HEKDFS        DUKDFS            1
     C                   MOVE      HELKA         DULKA             2
     C                   MOVE      HELKK         DULKK             2
     C                   MOVE      HELKS         DULKS             2
     C                   MOVE      HESTD         DUSTD             4 0
     C                   MOVE      HEKNT         DUKNT             8 0
     C                   MOVE      HEVALT        DUVALT            3
     C                   MOVE      HEVALP        DUVALP            3 3
     C                   MOVE      HEOT          DUOT              2
     C                   ENDIF
      *
     C     ZEADL01K      CHAIN     ZEADL01                            33
      *
N03   * HEAVD=9997 = Innland Henting/term/utkjøring  (autom utkj.DUP)
N03   * HEAVD=9998 = Innland Henting/term (HO stopper på term - ingen VF)
      * HEAVD=9999 = Innland UTGÅENDE
N03   * (innland bruker ikke HEDUMMY, så vi blåser i AVD lenger oppe.:=)
N03  c                   if        HEAVD=9999 or HEAVD=9998 or HEAVD=9997
N03  c                   exsr      InnlAvd
N03  c                   endif
      *
     C                   MOVE      'P'           HEST
     C                   IF        *IN33 = *OFF
     C     HEKNA         IFEQ      0
     C                   MOVE      DUKNA         HEKNA
     C                   ENDIF
     C     HEKDFS        IFEQ      *BLANKS
     C                   MOVE      DUKDFS        HEKDFS
     C                   ENDIF
     C     HELKA         IFEQ      *BLANKS
     C                   MOVE      DULKA         HELKA
     C                   ENDIF
     C     HELKK         IFEQ      *BLANKS
     C                   MOVE      DULKK         HELKK
     C                   ENDIF
     C     HELKS         IFEQ      *BLANKS
     C                   MOVE      DULKS         HELKS
     C                   ENDIF
     C     HESTD         IFEQ      0
     C                   MOVE      DUSTD         HESTD             4 0
     C                   ENDIF
     C     HEKNT         IFEQ      0
     C                   MOVE      DUKNT         HEKNT
     C                   ENDIF
     C     HEVALT        IFEQ      *BLANKS
     C                   MOVE      DUVALT        HEVALT
     C                   MOVE      DUVALP        HEVALP
     C                   ENDIF
     C     HEOT          IFEQ      *BLANKS
     C                   MOVE      DUOT          HEOT
     C                   ENDIF
     C                   ENDIF
      *
N02  C                   EXSR      GETSNR
      * HENTE OPPDRAGSNUMMER
      *
     C     HEAVD         CHAIN     TELL                               33
     C                   MOVE      TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
      *
      * OPPDATER ZEADF MED AVD/OPPDR.NR, STAUS=P (+ SIGN + DEFAULTER..)
     C                   UPDATE    HEADFR
      *
     C                   MOVE      'U'           HEST
     C     HEUR          IFEQ      'H'
     C     TRSLAG        ANDEQ     '  '
     C                   MOVE      'HO'          TRSLAG
     C                   END
S02  C* FORAN UPD. ZHE.. EXSR      GETSNR
     C                   EXSR      GETFRT
N03   * Hvis det skal lages VF-oppdrag- ta vare på HOAVD/OPD og tildel VFOPD..
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      HEOPD         HOOPD             7 0
N03  C     VFAVD         CHAIN     TELL                               33
N03  C                   MOVE      TEOPDN        VFOPD             7 0
N03  C                   ADD       1             TEOPDN
N03  C                   UPDATE    TELLR
N03   * avvikende når det er utkjøring på..
N03  c                   move      VFAVD         TRAVD2
N03  c                   move      VFOPD         TROPD2
N03  c                   move      HETRI         HELKK
N03  c                   movel     XXTERM        HESDVT
N03  c                   move      TRSDTD        TRSVTD
N03  c                   endif
     C                   WRITE     HEADFRNY
N05   *
N05  C                   MOVE      HEAVD         TXAVD
N05  C                   MOVE      HEOPD         TXOPD
N05  C                   MOVE      TRSDFD        TXETDD
N05  C                   MOVE      TRSDFK        TXETDK
N05  C                   MOVE      TRSDTD        TXETAD
N05  C                   MOVE      TRSDTK        TXETAK
N05  C                   WRITE     TRACKTR
      *
N03   * DUP videre-oppdrag dersom via Term/utkj
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      *ZEROS        TRAVD2
N03  c                   move      *ZEROS        TROPD2
N03  C                   MOVE      'VF'          TRSLAG
N03  c                   move      VFAVD         HEAVD
N03  c                   move      VFOPD         HEOPD
N03  c                   move      HOAVD         TRAVD0
N03  c                   move      HOOPD         TROPD0
N03  c                   move      HESDF         GMSDF             5
N03  c                   move      HESDT         GMSDT             5
N03  c                   MOVE      HEKNSF        GMKNSF            8 0
N03  c                   MOVE      HEKNKF        GMKNKF            8 0
N03  c     HEKNSF        ifne      *zeros
N03  c                   MOVE      KOAKNR        HEKNSF
N03  c                   else
N03  c                   MOVE      KOAKNR        HEKNKF
N03  c                   endif
N03  c                   move      *blanks       HELKK
N03  c                   move      *blanks       HESDVT
N03  c                   move      *zeros        TRSVTD
N03  c                   movel     XXTERM        HESDF
N03  c                   MOVE      *blanks       HEKF
N03  c                   MOVE      *blanks       HEFT1                          ikke UF på videreFr.
N03  C                   WRITE     HEADFRNY
N05   *
N05  C                   MOVE      HEAVD         TXAVD
N05  C                   MOVE      HEOPD         TXOPD
N05  C                   MOVE      TRSDFD        TXETDD
N05  C                   MOVE      TRSDFK        TXETDK
N05  C                   MOVE      TRSDTD        TXETAD
N05  C                   MOVE      TRSDTK        TXETAK
N05  C                   WRITE     TRACKTR
N05   *
N03  c                   MOVE      GMSDF         HESDF
N03  c                   MOVE      GMKNSF        HEKNSF
N03  c                   MOVE      GMKNKF        HEKNKF
N03  c                   move      HOAVD         HEAVD
N03  c                   move      HOOPD         HEOPD
N03  c                   endif
      *
      * SKRIV EN "MINIMUMS" DISP-RECORD FOR Å SIKRE AT DATA IKKE "MISTES" V/F12 FRA OPPDRAG
     C     HEUR          IFEQ      'H'
     C                   MOVE      HEAVD         DIAVD
     C                   MOVE      HEOPD         DIOPD
     C                   MOVE      HEOT          DIOT
     C                   MOVE      HESDF         DISTEF
     C                   MOVE      HESDT         DISTET
N03  c     VFAVD         Ifne      *zeros
N03  C                   MOVE      *BLANKS       DISTET
N03  C                   MOVEL     XXTERM        DISTET
N03  C                   ENDIF
     C                   MOVE      TRSDFD        DIDATF
     C                   MOVE      TRSDTD        DIDATT
     C                   MOVE      TRSDFK        DITIMF
     C                   MOVE      TRSDTK        DITIMT
     C                   WRITE     DISPR
N03  c     VFAVD         Ifne      *zeros
N03  C                   MOVE      'E'           DISTA1
N03  c                   move      VFAVD         DIAVD
N03  c                   move      VFOPD         DIOPD
N03  C                   MOVE      *BLANKS       DISTEF
N03  C                   MOVEL     XXTERM        DISTEF
N03  C                   MOVE      HESDT         DISTET
N03  C                   WRITE     DISPR
N03  c                   endif
     C                   END
      * FRI SØKEVEI "ORD"=ORDRENR FOR SØK FRA INTERNETT
     C     HERFA         IFNE      *BLANKS
     C                   MOVE      HEAVD         FSAVD
     C                   MOVE      HEOPD         FSOPD
     C                   MOVE      'ORD'         FSKODE
     C                   MOVEL     HERFA         FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C   33              MOVE      *BLANKS       FSDOKK
     C                   WRITE     FRISOKRNY
N05   *
N05  C                   MOVE      HEAVD         TXAVD
N05  C                   MOVE      HEOPD         TXOPD
N05  C                   MOVE      TRSDFD        TXETDD
N05  C                   MOVE      TRSDFK        TXETDK
N05  C                   MOVE      TRSDTD        TXETAD
N05  C                   MOVE      TRSDTK        TXETAK
N05  C                   WRITE     TRACKTR
N05   *
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      VFAVD         FSAVD
N03  c                   move      VFOPD         FSOPD
N03  C                   WRITE     FRISOKRNY
N05   *
N05  C                   MOVE      HEAVD         TXAVD
N05  C                   MOVE      HEOPD         TXOPD
N05  C                   MOVE      TRSDFD        TXETDD
N05  C                   MOVE      TRSDFK        TXETDK
N05  C                   MOVE      TRSDTD        TXETAD
N05  C                   MOVE      TRSDTK        TXETAK
N05  C                   WRITE     TRACKTR
N05   *
N03  c                   endif
     C                   END
      *
     C                   IF        (((HELIB  = *BLANKS) AND
     C                               (HEFILE <> *BLANKS)) AND
     C                               (HEMBR = *BLANKS))
      * OPPDATER BRUKER-ID TIL DEN SOM REGISTRERTE WEB-OPPDRAG
     C                   MOVE      HEAVD         FSAVD
     C                   MOVE      HEOPD         FSOPD
     C                   MOVE      'WUI'         FSKODE
     C                   EVAL      FSSOK = HEFILE
     C                   MOVE      HEDTOP        FSDTOP
     C                   MOVE      *BLANKS       FSDOKK
     C                   WRITE     FRISOKRNY
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      VFAVD         FSAVD
N03  c                   move      VFOPD         FSOPD
N03  C                   WRITE     FRISOKRNY
N03  c                   endif
     C                   END
      *
     C                   EXSR      TTSR
      *
      * GENERERER FRAKTBREV
      *
     C                   CALL      'SYOP30R'
     C                   PARM                    UUNIK
     C                   PARM                    UREFF
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
      *
      * FLYTTER FRI SØKEVEI
      *
     C     PPUNIK        SETLL     ZRISOL01
     C     PPUNIK        READE     ZRISOL01                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        FSKODE = '#VD'
     C                   MOVE      HEAVD         XAVD              4
     C                   MOVE      HEOPD         XOPD              7
     C                   MOVE      FSUNIK        XAN09             9
     C                   EVAL      UDOKNV = XAN09
     C                   CAT       FSDOKK:0      UDOKNV
     C                   CAT       FSSOK:0       UDOKNV
     C                   CALL      'SYOP23C'
     C                   PARM                    XAVD
     C                   PARM                    XOPD
     C                   PARM                    HEFILE
     C                   PARM                    UDOKNV           70
     C                   PARM                    FSSOK
     C                   ELSE
     C                   IF        ((FSKODE <> 'ORD') OR (HERFA <> FSSOK))
     C                   Z-ADD     HEAVD         FSAVD
     C                   Z-ADD     HEOPD         FSOPD
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C                   WRITE     FRISOKRNY
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      VFAVD         FSAVD
N03  c                   move      VFOPD         FSOPD
N03  C                   WRITE     FRISOKRNY
N03  c                   endif
     C                   END
     C                   END
     C     PPUNIK        READE     ZRISOL01                               33
     C  N33              ENDDO
      *
      * FLYTTER OVER FREETXT
     C                   Z-ADD     0             XRTLI             2 0
     C     PPUNIK        SETLL     ZREETXT1
     C     PPUNIK        READE(N)  ZREETXT1                               33
     C     *IN33         DOWEQ     *OFF
     C                   MOVE      WDT           FRTDT
     C                   Z-ADD     HEAVD         FRTAVD
     C                   Z-ADD     HEOPD         FRTOPD
N03  C                   select
N03  C                   when      FRTKOD = 'M'
N03  C                   MOVEL     FRTTXT        HenMail
N03  C                   when      FRTKOD = 'T'
N03  C                   MOVEL     FRTTXT        HenTLF
N03  C                   MOVE      FRTTXT        HenSMS
N03  c                   other
      * NORMAL FRITEKST SOM SKAL OVER TIL OPPDRAGET
     C                   ADD       1             XRTLI
     C                   Z-ADD     XRTLI         FRTLI
     C                   WRITE     FREETXTRNY
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      VFAVD         FRTAVD
N03  c                   move      VFOPD         FRTOPD
N03  C                   WRITE     FREETXTRNY
N03  c                   endif
N03  c                   endsl
      *
     C     PPUNIK        READE     ZREETXT1                               33
     C  N33              ENDDO
      **
N06
N06   * Oppdater arkivdokumenter (ligger med 0 i avd/opd - Z123456789 i ARUNDE )
N06  C     ARK10key      setll     ARKIVL10
N06  C     ARK10key      READE     ARKIVL10                               33
N06  C     *IN33         DOWEQ     *OFF
N06  c                   eval      ARAVD = HEAVD
N06  c                   eval      AROPD = HEOPD
N06  c                   update    ARKIVR
N06  C     ARK10key      READE     ARKIVL10                               33
N06  C  N33              ENDDO
N06   **
N06
N03  C                   if        OKinlA='J' and HenMail<>' '
N03   *
N03  c                   eval      FE_DFAVD     =  HEAVD                        FE_C3148A  Telef
N03  c                   eval      FE_DFOPD     =  HEOPD                        FE_C3148B  Mobil
N03  c                   eval      FE_DFFBNR    =  1                            FE_C3148C  Fax
N03  c                   eval      FE_N3035     =  'CZ'                         FE_C3148D  Sms
N03  c                   eval      FE_C3148A    =  HenTlf                       FE_C3148E  E-post
N03  c                   eval      FE_C3148D    =  HenSMS
N03  c                   eval      FE_C3148E    =  HenMail
N03  C                   WRITE     DOKUFER
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      VFAVD         FE_DFAVD
N03  c                   move      VFOPD         FE_DFOPD
N03  C                   WRITE     DOKUFER
N03  c                   endif
N03   *
N03  c                   move      HEAVD         HentAvd           4
N03  c                   move      HEOPD         HentOpd           7
N03  c                   call      'ESOP11H'
N03  c                   parm                    HentAvd
N03  c                   parm                    HentOpd
N03  c                   parm                    HenMail          50
N03  c                   parm                    HenTLF           15
N03  c                   parm                    HenSMS           15
     C                   PARM                    ANNEN             1
     c                   parm                    AnnSMS           15
     C                   PARM                    TRAA              8
     C                   PARM                    TRANAV           20
     c                   parm                    Merknad          50
N03  c                   parm      HEFILE        HenUSR           10
N03  C                   endif
      * logikke for knytting HO/dup flyttet (ingen annen merking)
      * (jov 2012.12.12- merkelig?? nyskapt eSped-opdrag del av DUPstruktur før PTF03 ?? TVILER..)
N03  C     VFAVD         IFeq      *zeros                                       Ikke hvis hent/VF
     C     TROPD0        IFne      *zeros
     C     TROPD1        orne      *zeros
     C     TROPD2        orne      *zeros
     c                   exsr      DUPSR
     c                   end
N03  c                   end
      *
     C     UTSR          ENDSR
      *
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
      * Registrert (plukket fra WEB-booking)
N01  C                   MOVE      HEAVD         TTAVD
N01  C                   MOVE      HEOPD         TTOPD
     C                   MOVE      'STR'         TTACTI
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      HEFILE        TTUSER
     C                   MOVEL     TTXSTR        TTTEXT
N02  C                   MOVEL     TTXSTL        TTTEXL
     C                   WRITE     TRACKFR
N03  c     VFAVD         Ifne      *zeros
N03  c                   move      VFAVD         TTAVD
N03  c                   move      VFOPD         TTOPD
N03  C                   WRITE     TRACKFR
N03  c                   endif
      *
     C                   ENDSR
      *
N01  C***********************************************
N01  C     GETSNR        BEGSR
N01  C***********************************************
N01   * Innland - tildel sendingsnr (burde flyttes til esop11)
N05  c                   IF        TRSDFD = 0
     c                   move      HEDTOP        TRSDFD
N05  c                   END
N05  c                   IF        TRSDTD = 0
     c                   move      HEDTOP        TRSDTD
N05  c                   END
N01  C     KODAKA        KLIST
N01  C                   KFLD                    XKAUNI
N01  C                   KFLD                    HEAVD
N01  C                   MOVE      'A'           XKAUNI            1
N01  c     KODAKA        chain     KODTA                              33
N01  c     *in33         ifeq      '0'
N01  c     HXSNDN        andeq     *blanks
N01  c     KOAIE         ifeq      '1'
N01  C     KOAIE         oreq      '2'
N01   * Fra frisok/utstedt brev - eller tildel
N01  C     PPUNIK        SETLL     ZRISOL01
N01  C     PPUNIK        READE     ZRISOL01                               33
N01  C                   DOW       *IN33 = *OFF
N01  C                   IF        FSKODE = 'IFB'
N01  c                   eval      HXSNDN = '401'+FSSOK
N01  c                   leave
N01  c                   endif
N01  C     PPUNIK        READE     ZRISOL01                               33
N01  c                   enddo
N01  c     HXSNDN        ifeq      *blanks
N01  C                   CALL      'SYGE15'
N01  C                   PARM                    HXSNDN
N01  c                   endif
N01   *
N01  c                   endif
N01  c                   endif
N01   *
N01  C                   ENDSR
     C***********************************************
     C     GETFRT        BEGSR
     C***********************************************
      * SR SOM HENTER VERDI FOR FRAKTTILLEGG (OLJE/VAL...)
     C     HEVALT        IFEQ      *BLANKS
     C                   MOVE      *ZEROS        UVALP
     C     HEUR          IFEQ      'B'
     C     HEUR          OREQ      'D'
     C     HEKNSF        IFNE      *ZEROS
     C     HEKDFS        ANDNE     'X'                                          VED EKSPORT.
     C                   MOVE      HEKNSF        UKNOP                          PRIM. SELGER
     C                   ELSE
     C                   MOVE      HEKNKF        UKNOP                          SEK. KJØPER
     C                   END
     C                   ELSE
     C     HEKNKF        IFNE      *ZEROS
     C     HEKDFK        ANDNE     'X'                                          VED IMPORT..
     C                   MOVE      HEKNKF        UKNOP                          PRIM. KJØPER
     C                   ELSE
     C                   MOVE      HEKNSF        UKNOP                          SEK. SELGER
     C                   END
     C                   END
     C     UKNOP         IFNE      *ZEROS
     C                   CALL      'SYFA24B'
     C                   PARM                    HEUR              1
     C                   PARM      HEAVD         UAVDOP            4 0
     C                   PARM                    UKNOP             8 0
     C                   PARM                    HEVALT
     C                   PARM                    UVALP             3 1
     C                   MOVE      UVALP         HEVALP
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     DUPSR         BEGSR
      ***********************************************
     C     HEAKYD        KLIST
     C                   KFLD                    TRAVD0
     C                   KFLD                    TROPD0
     C                   MOVE      TRSLAG        XRSLAG            2
     C                   Z-ADD     HEAVD         XEAVD             4 0
     C                   Z-ADD     HEOPD         XEOPD             7 0
      * DET SKAPTE OPPDRAGET ER et DUP-oppdrag, oppdater ref i overordnet/bestillende
     C     TRAVD0        IFNE      0
     C     TROPD0        ANDNE     0
     C     HEAKYD        CHAIN     HEADF                              65
     C     *IN65         IFEQ      *OFF
     C     XRSLAG        IFEQ      'FF'
     C                   Z-ADD     XEAVD         TRAVD1
     C                   Z-ADD     XEOPD         TROPD1
     C                   ENDIF
     C     XRSLAG        IFEQ      'VF'
     C                   Z-ADD     XEAVD         TRAVD2
     C                   Z-ADD     XEOPD         TROPD2
     C                   ENDIF
     C                   UPDATE    HEADFRNY
     C                   ENDIF
     C                   ELSE
      * DET SKAPTE OPPDRAGET ER HOVEDOPPDRAG, OPPRINNELIG ER HENTEOPPDRAG..
      * Oppdater DUP-AV-ref i det opprinnelige
     C     TRAVD1        IFNE      0
     C     TROPD1        ANDNE     0
     C     XRSLAG        IFEQ      'HO'
     C     XRSLAG        OREQ      '  '
     C     HEAKYD1       KLIST
     C                   KFLD                    TRAVD1
     C                   KFLD                    TROPD1
     C     HEAKYD1       CHAIN     HEADF                              65
     C     *IN65         IFEQ      *OFF
     C                   MOVE      'FF'          TRSLAG
     C                   Z-ADD     XEAVD         TRAVD0
     C                   Z-ADD     XEOPD         TROPD0
     C                   UPDATE    HEADFRNY
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     c                   endsr
      *
N03   ***********************************************
N03  C     InnlAvd       BEGSR
N03   ***********************************************
N03  C     KODRIK        KLIST
N03  C                   KFLD                    XXTERM            4 0
N03  C                   KFLD                    NUMPNR            4 0
HARD C                   MOVE      1000          XXTERM
     c                   move      HEAVD         EsAvd             4 0
      * dymmyverdi i Esped HEUR-AVD...
N03   * AVD=9997 = Innland Henting TERM
N03   * AVD=9998 = Innland Henting TERM/UTKJ
N03   * AVD=9999 = Innland UTGÅENDE
N03  c                   if        EsAvd=9999
N03  C                   MOVEL     HESDT         TSTPNR            4            TIL-pnr v/Utgående
N03  c                   else                                                   9998/9997
N03  C                   MOVEL     HESDF         TSTPNR            4            FRA-pnr v/Inngående
N03  c                   endif
N03  C                   MOVE      'N'           OKinlA            1
N03   *
N03  c     FinnAvd       tag
N03  C                   TESTN                   TSTPNR               33
N03  C     *IN33         IFEQ      '1'
N03  C                   MOVE      TSTPNR        NUMPNR
N03   * FINN AVD
N03   *   fra detaljert pnr-tab
N03  C     KODRIK        CHAIN     NOPNR                              33
N03  C     *IN33         IFEQ      *OFF
N03  C     NOAVDU        ANDNE     *ZEROS
N03  C                   MOVE      NOAVDU        RIAVDU
N03  C                   MOVE      NOAVDI        RIAVDI
N03  C                   MOVE      NOAVDH        RIAVDH
N03  C                   MOVE      'J'           OKinlA
N03  C                   goto      AvdOK
N03  C                   END
N03   *   eller fra postnr sonetabell
N03  C     KODRIK        SETGT     KODTRI
N03  C     XXTERM        READPE    KODTRI                                 33
N03  C  N33NUMPNR        IFGE      RIPNRF
N03  C     NUMPNR        ANDLE     RIPNRT
N03  C                   MOVE      'J'           OKinlA
N03  C                   END
N03  C                   END
N03   *
N03  C     AvdOk         Tag
N03  C                   if        OKinlA='J'
N03  C                   if        EsAvd=9999
N03  c                   move      RIAVDU        HOAVD                          Utgående
N03  c                   else
N03  c                   if        Runde2<>'J'
N03  c                   move      RIAVDI        HOAVD             4 0          Inng.    9998/9997
N03  c                   else
N03  c                   move      RIAVDU        VFAVD             4 0          Utg. VF-avd   9998
N03  c                   endif
S03  c* Utlev-forbeh(ikke på TUR) hardkodet KH  - tekst normalt Klar for henting må bekreftes!
S04  c*                  eval      HEKF  = 'KH'                                 Utlevforbehold
S04  c*                  eval      HEFT1='Klar for henting må bekreftes!'
N03  c                   endif
N03  c                   endif
N03   * finn avd for VF dersom 9998 = Inn TERM + UTK.
N03  c                   if        EsAvd=9998 and Runde2<>'J'
N03  C                   MOVEL     HESDT         TSTPNR            4            Til-pnr v/Utgående
N03  c                   move      'J'           Runde2            1
N03  c                   goto      FinnAvd
N03  c                   endif
N03   * Hovedavd.
N03  c                   move      HOAVD         HEAVD
N07   * innland og dato blank  = sett dagens
N07  c                   if        TRSDFD = *zeros
N07  c                   move      WDT           TRSDFD
N07  c                   endif
N07  c                   if        TRSDTD = *zeros
N07  c                   move      WDT           TRSDTD
N07  c                   endif
N03  c                   endsr
N03   *
      ***********************************************
     C     INIT          begsr
      ***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UREFF            17
     C                   PARM                    UUNIK             9 0
      *
     C     DUMKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    OPDNUL            7 0
     C     ZEADL01K      KLIST
     C                   KFLD                    UREFF
     C                   KFLD                    UUNIK
     C     PPUNIK        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    UREFF
      *
N06  C     ARK10key      KLIST
N06  C                   KFLD                    ARSTAT
N06  C                   KFLD                    NulAVD            4 0
N06  C                   KFLD                    NulOPD            7 0
N06  C                   KFLD                    ARUNDE
N06  C                   move      'A'           ARSTAT
N06  C                   movel     'Z'           ARUNDE
N06  C                   move      UUNIK         ARUNDE
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   ENDSR
