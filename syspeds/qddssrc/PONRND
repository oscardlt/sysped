     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 70 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            PONNR          4A  O  5  4
     A            PONNAV        30A  O  5  9
     A            PONTYX        18A  O  5 40
     A            PONKNX        12A  O  5 59
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'PONRND '
     A                                      COLOR(BLU)
     A                                  1 10'Søking på postnummer NORGE '
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 45SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  1'Op Pnr  Navn                      -
     A                                           Type postnr        Kommune    '
     A                                      COLOR(WHT)
     A            W2PNFR         4A  I  4  4DSPATR(UL)
     A            W2SØK         30A  I  4  9DSPATR(UL)
      *
     A          R BUNN
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                      WINDOW(WINDOWSZ)
     A  30        MSGTXT        28   O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A N30                              1  2'F5 = Frisk opp F3/12=Avslutt'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'PONRND '
     A                                      COLOR(BLU)
     A                                  1 10'Søking på postnummer NORGE '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  1'Op Pnr  Navn                      -
     A                                           Type postnr        Kommune    '
     A                                      COLOR(WHT)
     A            W2PNFR         4A  I  4  4DSPATR(UL)
     A            W2SØK         30A  I  4  9DSPATR(UL)
     A  30        MSGTXT        28   O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             15  2'F5 = Frisk opp F3/12=Avslutt'
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
