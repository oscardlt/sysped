      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED KOLLI INNHOLD              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-84 95-97    ***
      ***********************************************************
      *
     FGSSKAF    UF A E           K DISK
     FGSSKAL01  IF   E           K DISK
     F                                     RENAME(GSSKAFR:GSSKAFR1)
     FGSSAKF    IF   E           K DISK
     FGSS027D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XLNO              3 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UONO          SETLL     GSSAKF
     C     UONO          READE     GSSAKF                                 94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      AKSTKM        XXSTKM
     C                   MOVE      *ZEROS        X2STKM            5 0
     C     KAL01K        SETLL     GSSKAL01
     C     KAL01K        READE     GSSKAL01                               33
     C     *IN33         DOWEQ     '0'
     C     KALNOK        IFEQ      ULNO
     C                   MOVE      KASTKM        X2STKM
     C                   ELSE
     C                   SUB       KASTKM        XXSTKM
     C                   END
     C     KAL01K        READE     GSSKAL01                               33
     C  N33              ENDDO
     C                   MOVE      X2STKM        KASTKM
     C                   MOVE      ULNO          KALNOK
     C                   MOVE      AKLNO         KALNOA
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     UONO          READE     GSSAKF                                 94
     C  N94              ENDDO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *
     C  N27ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDKA
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     WSSTKM        COMP      XXSTKM                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'YBC1851'     MSGNR
     C                   SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ SUBFIL
     C                   EXSR      MOVWKA
     C                   UPDATE    SUBFIL
     C                   WRITE     SUBCTL
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C  N27WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGKA
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGKA         BEGSR
     C***********************************************
     C                   EXSR      MOVKAW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     BILDE01
     C                   MOVE      '0'           *IN(90)
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWKA        BEGSR
     C***********************************************
     C                   MOVE      WSSTKM        KASTKM
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVKAW        BEGSR
     C***********************************************
     C                   MOVE      KASTKM        WSSTKM
     C                   SETON                                        40
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDKA         BEGSR
     C***********************************************
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     KASTKM        IFEQ      0
     C     KAL00K        CHAIN     GSSKAF                             33
     C     *IN33         IFEQ      '0'
     C                   DELETE    GSSKAFR
     C                   END
     C                   ELSE
     C                   MOVE      KASTKM        XXSTKM
     C     KAL00K        CHAIN     GSSKAF                             33
     C                   MOVE      XXSTKM        KASTKM
     C     *IN33         IFEQ      '0'
     C                   UPDATE    GSSKAFR
     C                   ELSE
     C                   WRITE     GSSKAFR
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KAL01K        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    AKLNO
     C     KAL00K        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    ULNO
     C                   KFLD                    KALNOA
      *
     C     *ENTRY        PLIST
     C                   PARM                    UONO              7 0
     C                   PARM                    ULNO              2 0
     C                   PARM                    UTYPE             3
      *
     C     UTYPE         COMP      'DSP'                                  27
     C                   WRITE     WINDOWSZ
     C                   MOVE      UONO          KAONO
      *
     C                   ENDSR
      *
