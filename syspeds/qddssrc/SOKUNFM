*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8XXXA* 01 YBC PTF8 TOLLKREDITNR 8.0
P8XXXA* 02 SH  PTF8 OVER I FRISØK
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R HEADER
     A                                  1 19MSGCON(050 DIV1694 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(010 DIV1695 *LIBL/SYMSGF)
     A            WFIRMA         2A  O  2 13COLOR(WHT)
     A                                  1  2'SYSPED/SOEKUN'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  1MSGCON(078 SHU0114 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL
     A                                      SLNO(*VAR)
     A                                      CLRL(02)
     A            WSN            1S 0O  1  2DSPATR(HI)
N01  A  70                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A            KNAVN         30A  O  1  4
     A  77                                  DSPATR(HI)
N01  A  70                                  COLOR(YLW)
     A            KADR1         30A  O  1 35
     A  77                                  DSPATR(HI)
N01  A  70                                  COLOR(YLW)
     A            TLF           15A  O  1 66
     A  77                                  DSPATR(HI)
N01  A  70                                  COLOR(YLW)
     A            WADR3         30   O  2  4
     A  77                                  DSPATR(HI)
     A            FORETA         9   B  2 35
     A  77                                  DSPATR(HI)
     A  85                                  COLOR(RED)
S01  A*           SYKONT    R        O  2 45REFFLD(KUNDR/SYKONT *LIBL/CUNDF)
S01  A*                                     EDTWRD('     -  ')
N01  A            SYKONT    R        H      REFFLD(KUNDR/SYKONT *LIBL/CUNDF)
N01  A            SYFR02    R        H      REFFLD(KUNDR/SYFR02 *LIBL/CUNDF)
N01  A            XXTKRE         8Y 0O  2 45EDTCDE(4)
     A  77                                  DSPATR(HI)
     A            SYLIKV    R        O  2 54REFFLD(KUNDR/SYLIKV *LIBL/CUNDF)
     A                                      DSPATR(BL)
     A            KUNDNR         8Y 0O  2 57EDTCDE(4)
     A  77                                  DSPATR(HI)
     A            TFAXNR        15A  O  2 66
     A  77                                  DSPATR(HI)
      *
     A          R VARELI
     A                                      CF10(10 'FRISØK')
     A                                      CF06(06 'LAGE NY KUNDE')
     A                                      CF09(09 'SYSPED VEDL.')
     A                                      CF11(11 'KONTAKTP.')
     A                                      CF12(12 'Go til callingsbilde')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25 'Rull frem')
     A                                      ROLLDOWN(26 'Rull tilbake')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(008 DIV1697 *LIBL/SYMSGF)
     A            WLAND          2A  I  1 11
     A            WALFA         10A  I  1 15DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  1 26MSGCON(008 DIV1698 *LIBL/SYMSGF)
     A                                  1 35MSGCON(006 SHU0117 *LIBL/SYMSGF)
     A            WSPOST         4D  B  1 42
     A            WSPOGE        10A  B  1 47
     A                                  1 58'For'
     A            WSFORE        11A  B  1 62
     A                                  1 74'Vis'
     A            WSS            1D 0I  1 78DSPATR(UL)
     A            WTXT1          8A  O  2  2
     A            WS1            1Y 0B  2 11
     A  51                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A N51                                  DSPATR(ND)
     A N50                                  DSPATR(PC)
     A N51                                  DSPATR(PR)
     A  91                                  DSPATR(RI)
     A            WKNR1          8Y 0B  2 13
     A  51                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A N51                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A  91                                  DSPATR(RI)
     A            WTXT2          8A  O  2 22
     A            WS2            1Y 0B  2 31
     A  52                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  92                                  DSPATR(RI)
     A  92                                  DSPATR(PC)
     A N52                                  DSPATR(ND)
     A N52                                  DSPATR(PR)
     A            WKNR2          8Y 0B  2 33
     A  52                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  92                                  DSPATR(RI)
     A N52                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A            WTXT3          8A  O  2 42
     A            WS3            1Y 0B  2 51
     A  53                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  93                                  DSPATR(RI)
     A  93                                  DSPATR(PC)
     A N53                                  DSPATR(ND)
     A N53                                  DSPATR(PR)
     A            WKNR3          8Y 0B  2 53
     A  53                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  93                                  DSPATR(RI)
     A N53                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A            WTXT4          8A  O  2 62
     A            WS4            1Y 0B  2 71
     A  54                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A N54                                  DSPATR(ND)
     A N54                                  DSPATR(PR)
     A            WKNR4          8Y 0B  2 73
     A  54                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  94                                  DSPATR(RI)
     A N54                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A            WTXT5          8A  O  3  2
     A            WS5            1Y 0B  3 11
     A  55                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  95                                  DSPATR(RI)
     A  95                                  DSPATR(PC)
     A N55                                  DSPATR(ND)
     A N55                                  DSPATR(PR)
     A            WKNR5          8Y 0B  3 13
     A  55                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  95                                  DSPATR(RI)
     A N55                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A            WTXT6          8A  O  3 22
     A            WS6            1Y 0B  3 31
     A  56                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A N56                                  DSPATR(ND)
     A N56                                  DSPATR(PR)
     A            WKNR6          8Y 0B  3 33
     A  56                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  96                                  DSPATR(RI)
     A N56                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A            WTXT7          8A  O  3 42
     A            WS7            1Y 0B  3 51
     A  57                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A N57                                  DSPATR(ND)
     A N57                                  DSPATR(PR)
     A            WKNR7          8Y 0B  3 53
     A  57                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A N57                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A            WTXT8          8A  O  3 62
     A            WS8            1Y 0B  3 71
     A  58                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A N58                                  DSPATR(ND)
     A N58                                  DSPATR(PR)
     A            WKNR8          8Y 0B  3 73
     A  58                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A N58                                  DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV1699 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40MSGCON(018 JOV0998 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 59MSGCON(018 SHU0680 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 40MSGCON(012 SHU1027 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BILDESE
     A                                  1  2'SYSPED/SOEKKUN'
     A                                      COLOR(BLU)
     A                                  2 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1590 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(025 DIV1592 *LIBL/SYMSGF)
     A                                  5  3MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A                                  7  3MSGCON(025 DIV1594 *LIBL/SYMSGF)
     A                                  8  3MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A                                  9  3MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A                                 10  3MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A                                 11  3MSGCON(025 DIV2005 *LIBL/SYMSGF)
     A                                 15  3MSGCON(025 DIV1598 *LIBL/SYMSGF)
     A                                 12  3MSGCON(025 DIV1599 *LIBL/SYMSGF)
     A                                 13  3MSGCON(025 DIV1600 *LIBL/SYMSGF)
     A                                 14  3MSGCON(025 DIV1601 *LIBL/SYMSGF)
     A                                 16  3MSGCON(025 DIV3560 *LIBL/SYMSGF)
     A                                 17  3MSGCON(025 DIV1602 *LIBL/SYMSGF)
     A                                 18  3MSGCON(025 DIV1603 *LIBL/SYMSGF)
     A                                 18 42MSGCON(040 DIV1612 *LIBL/SYMSGF)
     A                                 19  3MSGCON(025 DIV1604 *LIBL/SYMSGF)
     A                                 19 42MSGCON(025 DIV1605 *LIBL/SYMSGF)
     A                                 20  3MSGCON(025 DIV1606 *LIBL/SYMSGF)
     A                                 20 42MSGCON(025 DIV1607 *LIBL/SYMSGF)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 21  3MSGCON(025 SHU0024 *LIBL/SYMSGF)
     A            KUNDNR    R        O  3 31REFFLD(KUNDR/KUNDNR SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            KNAVN     R        O  5 31REFFLD(KUNDR/KNAVN SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            KPERS     R        O  7 31REFFLD(KUNDR/KPERS SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            ADR1      R        O  8 31REFFLD(KUNDR/ADR1 SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            ADR2      R        O  9 31REFFLD(KUNDR/ADR2 SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            POSTNR    R        O 10 31REFFLD(KUNDR/POSTNR SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            ADR3      R        O 10 36REFFLD(KUNDR/ADR3 SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SONAVN    R        O 12 31REFFLD(KUNDR/SONAVN SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            FMOT      R        O 14 31REFFLD(KUNDR/FMOT SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            TLF       R        O 15 31REFFLD(KUNDR/TLF SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            TFAXNR    R        O 16 31REFFLD(KUNDR/TFAXNR SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SYREGN    R        O 19 31REFFLD(KUNDR/SYREGN SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SYOPDT    R        O 20 31REFFLD(KUNDR/SYOPDT SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
S01  A*           SYKONT    R        O 19 68REFFLD(KUNDR/SYKONT SYSPEDF/CUNDF)
N01  A            SYFR02    R        O 19 68REFFLD(KUNDR/SYFR01 SYSPEDF/CUNDF)
N01  A            SYKONT    R        O 19 70REFFLD(KUNDR/SYKONT SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A                                      EDTWRD('     -  ')
     A            SYLIKV    R        O 20 68REFFLD(KUNDR/SYLIKV SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SYMINU    R        O 17 31REFFLD(KUNDR/SYMINU SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SYUTLP    R        O 18 31REFFLD(KUNDR/SYUTLP SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SYLAND    R        O 11 31REFFLD(KUNDR/SYLAND SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            VALKOD    R        O 13 31REFFLD(KUNDR/VALKOD SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A            SFAKT     R        O 21 31REFFLD(KUNDR/SFAKT SYSPEDF/CUNDF)
     A                                      COLOR(WHT)
     A                                  1 20MSGCON(050 DIV1694 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 11 50MSGCON(016 SHU0116 *LIBL/SYMSGF)
     A            VADRNA    R        O 12 50REFFLD(VADRR/VADRNA *LIBL/VADR)
     A                                      COLOR(WHT)
     A            VADRN1    R        O 13 50REFFLD(VADRR/VADRN1 *LIBL/VADR)
     A                                      COLOR(WHT)
     A            VADRN2    R        O 14 50REFFLD(VADRR/VADRN2 *LIBL/VADR)
     A                                      COLOR(WHT)
     A            VADRN3    R        O 15 50REFFLD(VADRR/VADRN3 *LIBL/VADR)
     A                                      COLOR(WHT)
