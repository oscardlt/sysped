      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE enhetskode   FRA-TIL      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FENHETFT   UF A E           K DISK
     FFIRM      IF   E           K DISK
     FENHETF    IF   E           K DISK
     FEDII      IF   E           K DISK
     FSYFA100FM CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSTST                  1      4
     D  WSOTF                  1      2
     D  WSOT                   3      4
     D                 DS
     D  SFTST                  1      4
     D  SFOTF                  1      2
     D  SFOT                   3      4
     C*
     C                   EXSR      INIT
     C*
     C     CHOISE        TAG
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     42            POSNBR
     C                   EXFMT     BILDE1
     C                   SETOFF                                       9930
     C*
     C**  AVSLUTT
     C*
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*
     C**  SØK
     C*
     C     *IN04         IFEQ      '1'
     C                   CALL      'EDI52R'
     C                   PARM                    W0004
     C     W0004         CHAIN     EDII                               99
     C     *IN99         IFEQ      '1'
     C                   MOVE      *BLANKS       W0004
     C                   SETOFF                                       99
     C                   GOTO      CHOISE
     C                   END                                                    <*IN99
     C                   END                                                    <*IN04
     C*
     c                   move      w0004         S0004
     c                   eval      wnavn='Firmastandard'
     c     W0004         ifne      *blanks
     C     W0004         CHAIN     EDII                               35
     C     *IN35         IFEQ      '0'
     C                   MOVE      INNA          WNAVN
     C                   ELSE
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SHU1722'     MSGNR
     C                   GOTO      CHOISE
     C                   END                                                    <*IN35
     C                   END                                                    <*IN35
     C*
     C     START         TAG
     C                   CLEAR                   ENHETF0
     C                   CLEAR                   ENHETT0
     C     STARTF        TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SFKEYR        SETLL     ENHETFT
     C     START1        TAG
     C     SFKEY         READE     ENHETFT                                94
     C  N94              MOVE      ENHETF0       XENHETF0          7
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SFKEY         READE     ENHETFT                                94
     C  N94              MOVE      ENHETF0       XENHETF0          7
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYKUNDE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   Z-ADD     WSSUBN        XRECNO
     C                   EXFMT     BUNN
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       999830
     C*** AVSLUTT
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*** FRISK OPP
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN05
     C*** LAGE
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** SØK
     C     *IN08         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      START6
     C                   END                                                    <*IN08
     C*** FORRIGE
     C     *IN12         IFEQ      '1'
     C                   GOTO      CHOISE
     C                   END                                                    <*IN12
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94SFKEYR        SETLL     ENHETFT
     C     START4        TAG
     C  N94SFKEY         READE     ENHETFT                                94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C*** NY
      *
     C     START5        TAG
     c                   setoff                                       7071
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   SETOFF                                           30
     C   12              EXSR      MOVBWS
     c* søking til kode
     c     *in04         ifeq      '1'
     C                   MOVE      'P'           UMODE             1
     C                   CALL      'SYTR27'
     C                   PARM                    UMODE
     C                   PARM                    WENHT
     C                   Z-ADD     20            POSNBR
     c                   goto      start5
     c                   end
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*** SØK FRA AVDELING
     C     START6        TAG
     C                   EXFMT     VAREL2
     C     *IN12         IFNE      '1'
     C                   MOVE      W2ENHF        XENHETF0
     C                   GOTO      STARTF
     C                   END                                                    <*IN12
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     c                   setoff                                       7071
     C*          N40 = NY
     C*           40 = ENDRING
     C*
     C*  TEST at ikke blank i noen koder
     C*
     c     wenhf         ifeq      *blanks
     C                   MOVE      'SHU1725'     MSGNR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN70
     C                   GOTO      SLTB
     c                   end
     C*
     c     wenht         ifeq      *blanks
     C                   MOVE      'SHU1726'     MSGNR
     C                   Z-ADD     20            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN71
     C                   GOTO      SLTB
     c                   end
     C*
     C*  TEST FOR GYLDIG FRA ENHETSKODE i til verdi
     C*
     C     WSKEYT        CHAIN     ENHETF                             37
     C     *IN37         IFNE      '0'
     C                   MOVE      'SHU1723'     MSGNR
     C                   Z-ADD     10            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN71
     C                   GOTO      SLTB
     C                   END                                                    <*IN37
     C*
     C*
     C     WSKEYFT       CHAIN     ENHETFT                            33
     C*
     C*
     C*  TEST OM FRA OPPDRAGSTYPE FINNES FRA FØR VED NYREGISTRERING
     C*
     C     *IN33         IFEQ      '0'
     C     *IN40         ANDEQ     '0'
     C                   MOVE      'DIV1727'     MSGNR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN70
     C                   GOTO      SLTB
     C                   END                                                    <*IN33
     C*
     C                   EXSR      MOVWKA
     C  N33              UPDATE    ENHETFTR                             38
     C   33              WRITE     ENHETFTR
     C*
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   EXSR      MOVBWS
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              MOVE      '1'           *IN30
     C******************** Z-ADD1         LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     SFOTF         COMP      '*D'                                   98
     C   98              MOVE      'SPS0029'     MSGNR
     C   98              MOVE      '1'           *IN30
     C   98              GOTO      SLHENT
     C                   EXSR      CHGST
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     SFOTF         COMP      '*D'                                   98
     C   98              MOVE      'SPS0029'     MSGNR
     C   98              MOVE      '1'           *IN30
     C   98              GOTO      SLHENT
     C                   EXSR      DLTST
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SFKEYD        CHAIN     ENHETFT                            33
     C  N33              DELETE    ENHETFTR
     C                   EXSR      MOVBSF
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   MOVE      '1'           *IN99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVBWS
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      ENHETF0       WENHF
     C                   MOVE      ENHETT0       WENHT
     C                   MOVE      '1'           *IN40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWKA        BEGSR
     C***********************************************
     C                   MOVE      WENHF         ENHETF0
     C                   MOVE      WENHT         ENHETT0
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     MOVBSF        BEGSR
     C***********************************************
     C                   EVAL      ENHETF0='*DEL*'
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBWS        BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WENHF
     C                   MOVE      *BLANKS       WENHT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     SFKEYD        KLIST
     C                   KFLD                    W0004
     C                   KFLD                    ENHETF0
     C                   KFLD                    ENHETT0
     C     SFKEY         KLIST
     C                   KFLD                    W0004
     C     SFKEYR        KLIST
     C                   KFLD                    W0004
     C                   KFLD                    XENHETF0
     C*
     C     WSKEYFT       KLIST
     C                   KFLD                    W0004
     C                   KFLD                    WENHF
     C                   KFLD                    WENHT
     C     WSKEYT        KLIST
     C                   KFLD                    WENHT
     C*
     C*
     C*
     C*
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C*
