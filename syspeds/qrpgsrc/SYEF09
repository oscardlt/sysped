      *
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE WIDERØ-PRISER             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-97 99 ***
      ***********************************************************
      *
     FKODTPV    UF A E           K DISK
     FSTED2     IF   E           K DISK
     FSYEF09FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2FRA         YFRA              3
     C                   MOVE      W2TIL         YTIL              3
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODPV1        SETLL     KODTPV
     C     START1        TAG
     C                   READ      KODTPV                                 94
     C  N94              UPDATE    KODTPVR
     C   94              MOVE      *BLANKS       KPVFRA
     C   94              MOVE      *BLANKS       KPVTIL
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        17
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      KODTPV                                 94
     C  N94              UPDATE    KODTPVR
     C  N94              MOVE      KPVFRA        YFRA
     C  N94              MOVE      KPVTIL        YTIL
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94KODPV3        SETLL     KODTPV
     C  N94              READ      KODTPV                                 94
     C  N94              UPDATE    KODTPVR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODPV2        CHAIN     KODTPV                             33
     C  N33              DELETE    KODTPVR
     C                   EXSR      MOVB1
     C                   MOVE      '*DL'         KPVFRA
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       908988
     C                   SETOFF                                       30
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C*
     C* TEST FRA-DESTINASJON
     C                   MOVEL     WSFRA         XSTED             5
     C     XSTED         CHAIN     STED2                              90
     C   90              MOVE      'SPD0016'     MSGNR
     C   90              SETON                                        30
     C*
     C* TEST TIL-DESTINASJON
     C                   MOVEL     WSTIL         XSTED
     C     XSTED         CHAIN     STED2                              89
     C   89
     CANN30              MOVE      'SPD0017'     MSGNR
     C   89              SETON                                        30
     C*
     C* TEST OM FRA-DESTINASJON OG TIL-DESTINASJON ER LIKE
     C  N89WSFRA         COMP      WSTIL                                  89
     C   89
     CANN30              MOVE      'SPD0018'     MSGNR
     C   89              SETON                                        30
     C                   END
     C*
     C* TEST OM BRUKER HAR TASTET KG-PRIS
     C   30              GOTO      SLTB1
     C*
     C     KODPVK        CHAIN     KODTPV                             33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPV
     C  N33              UPDATE    KODTPVR
     C   33              WRITE     KODTPVR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     KPVFRA        COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVPVW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       908988
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     KPVFRA        COMP      '*DL'                                  98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPV        BEGSR
     C***********************************************
     C                   MOVE      WSFRA         KPVFRA
     C                   MOVE      WSTIL         KPVTIL
     C                   MOVE      WSSON         KPVSON
     C                   MOVE      WSPRI         KPVPRI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPVW        BEGSR
     C***********************************************
     C                   MOVE      KPVFRA        WSFRA
     C                   MOVE      KPVTIL        WSTIL
     C                   MOVE      KPVSON        WSSON
     C                   MOVE      KPVPRI        WSPRI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KPVFRA
     C                   MOVE      *BLANKS       KPVTIL
     C                   MOVE      *ZEROS        KPVSON
     C                   MOVE      *ZEROS        KPVPRI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSFRA
     C                   MOVE      *BLANKS       WSTIL
     C                   MOVE      *ZEROS        WSSON
     C                   MOVE      *ZEROS        WSPRI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODPVK        KLIST
     C                   KFLD                    WSFRA
     C                   KFLD                    WSTIL
     C     KODPV1        KLIST
     C                   KFLD                    W2FRA
     C                   KFLD                    W2TIL
     C     KODPV2        KLIST
     C                   KFLD                    KPVFRA
     C                   KFLD                    KPVTIL
     C     KODPV3        KLIST
     C                   KFLD                    YFRA
     C                   KFLD                    YTIL
     C*
     C                   ENDSR
     C*
