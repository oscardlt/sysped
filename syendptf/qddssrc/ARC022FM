     A*%%TS  SD  20200302  140833  JOVO        REL-V7R3M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20200302  140833  JOVO        REL-V7R3M0  5770-WDS
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            ARCEXT    R        O  6  7REFFLD(ARKEXTR/ARCEXT *LIBL/ARKEXT)
     A            ARCANE    R        O  6 10REFFLD(ARKEXTR/ARCANE *LIBL/ARKEXT)
     A            ARCPAD    R        O  6 41REFFLD(ARKEXTR/ARCPAD *LIBL/ARKEXT)
     A            ARCPSI    R        O  6 76REFFLD(ARKEXTR/ARCPSI *LIBL/ARKEXT)
     A            ARBHISF   R        O  7 10REFFLD(ARKEXTR/ARBHISF *LIBL/ARKEXT)
     A            ARCPAE    R        O  7 41REFFLD(ARKEXTR/ARCPAE *LIBL/ARKEXT)
     A            ARCPSE    R        O  7 78REFFLD(ARKEXTR/ARCPSE *LIBL/ARKEXT)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20200302  140833  JOVO        REL-V7R3M0  5770-WDS
     A  *DS3                                SFLSIZ(0010)
     A  *DS4                                SFLSIZ(0010)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                SFLPAG(0009)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/ARC022'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 SHU1225 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 76'Sec'
     A                                      COLOR(WHT)
     A                                  5  4MSGCON(070 SHU1224 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 76'I/E'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A*%%TS  SD  20200302  134819  JOVO        REL-V7R3M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3MSGCON(070 SHU1224 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WRCEXT    R        B  2  7REFFLD(ARKEXTR/ARCEXT *LIBL/ARKEXT)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WRCANE    R        B  2 10REFFLD(ARKEXTR/ARCANE *LIBL/ARKEXT)
     A                                      CHECK(LC)
     A            WRCPAD    R        B  2 41REFFLD(ARKEXTR/ARCPAD *LIBL/ARKEXT)
     A                                      CHECK(LC)
     A                                  2 72'Sec'
     A            WRCPSI         1   B  2 76
     A                                  2 78'(J)'
     A            WRBHISF       30A  B  3 10CHECK(LC)
     A            WRCPAE    R        B  3 41REFFLD(ARKEXTR/ARCPAE *LIBL/ARKEXT)
     A                                      CHECK(LC)
     A                                  3 72'Sec'
     A            WRCPSE         1   B  3 76
     A                                  3 78'(J)'
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 SHU1226 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2CEXT         2A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VAREL3
     A*%%TS  SD  20171220  140626  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  3MSGCON(070 SHU1224 *LIBL/SYMSGF)
     A            ARCEXT    R        O  2  7REFFLD(ARKEXTR/ARCEXT *LIBL/ARKEXT)
     A            ARCANE    R        O  2 10REFFLD(ARKEXTR/ARCANE *LIBL/ARKEXT)
     A            ARCPAD    R        O  2 41REFFLD(ARKEXTR/ARCPAD *LIBL/ARKEXT)
     A            ARBHISF       30   O  3 10
     A            ARCPAE    R        O  3 41REFFLD(ARKEXTR/ARCPAE *LIBL/ARKEXT)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R NYWRK
     A*%%TS  SD  20200302  140833  JOVO        REL-V7R3M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/ARC022'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 SHU1225 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  4MSGCON(070 SHU1224 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 76'Sec'
     A                                      COLOR(WHT)
     A                                  5 76'I/E'
     A                                      COLOR(WHT)
