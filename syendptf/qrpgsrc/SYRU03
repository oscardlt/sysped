      ****************************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV RUCKATTEST-POSTER             ***
      ****************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 DATOFORMAT
10XXX * 02 JOV PTF10 felt CAVD i CUNDC må initieres med *zeros
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 03 JOV PTF12 Utvidet bilde 27x132 / Kolonne fro Ank-meld sendt/visning stat A / Ank-meld
      * 04 JOV PTF12 Varsel dersom det ligger A-status med dato blank.. ved Vis A / Test arkiv  ZH
      * 05 JOV PTF12 Delete er = Slette-merke ikke fysisk fjerne
      * 06 JOV PTF12 Delete er = Vis kunde/speditørnavn
      * 07 JOV PTF12 Sende ankomsmelding manuelt (Se ogs SYFA40M - oppdat samtidig PTF07 i den)
      * 08 JOV PTF12 Initier hedtop
      * 09 JOV PTF12 Vis kode for skannet HF uavhengig av om det er A / Ank.melding
      *              Status * Ved søk = uavh av status
********************************************************************
      *
     FRUCKP     UF   E           K DISK
     FRUCKPL01  IF   E           K DISK
     F                                     RENAME(RUCKPR:RUCKPR1)
N04  FRUCKPL04  IF   E           K DISK
N04  F                                     RENAME(RUCKPR:RUCKPR4)
N04  FArkivL02  IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKODTA     IF   E           K DISK
N06  FCUNDl01   IF   E           K DISK
     FCUNDC03   IF A E           K DISK
     FSYRU03FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  1      4  0
     D  HEDTÅ2                 3      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D                 DS
     D  RUDTP1                 1      8  0
     D  DTP1Å                  1      4  0
     D  DTP1M                  5      6  0
     D  DTP1D                  7      8  0
     D                 DS
     D  RUDTP2                 1      8  0
     D  DTP2Å                  1      4  0
     D  DTP2M                  5      6  0
     D  DTP2D                  7      8  0
     D                 DS
     D  RUDTP3                 1      8  0
     D  DTP3Å                  1      4  0
     D  DTP3M                  5      6  0
     D  DTP3D                  7      8  0
     D                 DS
     D  RUDTPS                 1      8  0
     D  DTPSÅ                  1      4  0
     D  DTPSM                  5      6  0
     D  DTPSD                  7      8  0
N03  D                 DS
N03  D  RUDTAM                 1      8  0
N03  D  DTAMÅ                  1      4  0
N03  D  DTAMM                  5      6  0
N03  D  DTAMD                  7      8  0
N03  D                 DS
N03  D  SFDTAM                 1      6  0
N03  D  SFDTAMD                1      2  0
N03  D  SFDTAMM                3      4  0
N03  D  SFDTAMÅ                5      6  0
N07   *Array over adresser en vil sende mail til
N07  D MoNa            S             50    DIM(5)
N07  D MoAd            S             50    DIM(5)
N07  D MoSMS           S             50    DIM(5)
      *
     C*
     C                   EXSR      INIT
      *
N04   * Varsel dersom det finne usendte ankomsmeldinger - men kun føste gang / ved inngang
n04  C     RUL04Key      chain     RUCKPL04
n04  C                   if        %found
n04  C                   MOVE      'JOV1769'     MSGNR
n04  C                   SETON                                            30
n04  C                   endif
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2HST         YHST              1
     C                   MOVE      W2HAVD        YHAVD             4 0
     C                   MOVE      W2HOPD        YHOPD             7 0
     c                   move      *OFF          *IN55
      *_____________
      * Fyll Subfile
      *"""""""""""""
      * status * = på tvers av status
N09  c                   if        YHST='*'
N09  C     RUallKey      SETLL     RUCKP
N09  c                   else
     C     RUL01Key      SETLL     RUCKPL01
N09  c                   endif
     C     START1        TAG
N09  c                   if        YHST='*'
N09  C     YHAVD         READE(N)  RUCKP                                  94
N09  c                   else
     C     YHST          READE     RUCKPL01                               94
N09  c                   endif
      *
     C     START2        TAG
      *
     C  N94              DO
S03  C*                  DO        14
N03  C                   DO        17
N09  c*?                 if        YHST='*' and
N09  c*?                           YHOPD <> *zeros and YHOPD <> RUOPD
N09  C*?                 leave
N09  C*?                 endif
     C                   EXSR      SETSUB
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
N09  c                   if        YHST='*'
N09  C     YHAVD         READE(N)  RUCKP                                  94
N09  c                   else
     C     YHST          READE     RUCKPL01                               94
N09  c                   endif
     C  N94              MOVE      RUAVD         YHAVD
     C  N94              MOVE      RUOPD         YHOPD             7 0
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94RUL01Key      SETLL     RUCKPL01
     C  N94YHST          READE     RUCKPL01                               94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     c                   move      'N'           Refresh           1
     C                   EXSR      HENTSR
     C     Refresh       Cabeq     'J'           Start
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       3090
     c     *in04         ifeq      *ON
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKU
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   goto      start5
     c                   end
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SETSUB        BEGSR
     C***********************************************
     C* FORMATER SUBFIELD-FELT
     C     RUKEY         CHAIN     HEADF                              33
     C                   MOVEL     HEGN          SFGN
     C     HEPOS2        IFEQ      *ZEROS
     C                   EVAL      SFPOS = HEPOS1
     C                   ELSE
     C                   EVAL      SFPOS = %trim(HEPOS1) +'/' +
     C                             %trim(%editc(HEPOS2:'Z'))
     C                   END
N06  C     Kunkey        CHAIN     Cundl01
N06  c                   if        %found
N06  c                   eval      KNAVN15 = KNAVN
N06  c                   else
N06  c                   eval      KNAVN15 = '*UKJENT*'
N06  c                   endif
      * FINNES IKKE avdelingens kundenr med FUNKSJON '*RUCKATTEST' -> 51 ON
     c     AvdKey        chain     KODTA                              33
     c                   move      KOAKNR        YYRUKU
     C     CONKEY        CHAIN     Cundc03                            51
     C  N51CEMAIL        COMP      *Blanks                                51
     c   51              move      *ON           *IN55
      * FINNES IKKE K.PERS MED FUNKSJON '*RUCKATTEST' -> 50 ON
     c                   move      RUKU          YYRUKU
     C     CONKEY        CHAIN     Cundc03                            50
     C  N50CEMAIL        COMP      *Blanks                                50
     c   50              move      *ON           *IN55
      * Datoer (SATSER PÅ AT DETTE ER PROG KUN FOR NORGE - IKKE GENERELLE KONVRUTINER)
      * Oppdr.dato (DDMMÅÅ)
S02  C*                  MOVEL     HEDTÅ2        SFDTOP
     C                   MOVEL     HEDTD         SFDTO4            4 0
     C                   MOVE      HEDTM         SFDTO4
     C*                  MOVE      SFDTO4        SFDTOP
N02  C                   MOVEL     SFDTO4        SFDTOP
N02  C                   MOVE      HEDTÅ2        SFDTOP
      * Dato  1.   purring
     C                   MOVEL     DTP1D         SFDTP1
     C                   MOVE      DTP1M         SFDTP1
      * Dato  2.   purring
     C                   MOVEL     DTP2D         SFDTP2
     C                   MOVE      DTP2M         SFDTP2
      * Dato  3.   purring
     C                   MOVEL     DTP3D         SFDTP3
     C                   MOVE      DTP3M         SFDTP3
      * Dato siste purring
     C                   MOVEL     DTPSD         SFDTPS
     C                   MOVE      DTPSM         SFDTPS
N03   * Dato Ankomstmelding
N03  C                   MOVE      DTAMÅ         SFDTAMÅ
N03  C                   MOVE      DTAMM         SFDTAMM
N03  C                   MOVE      DTAMD         SFDTAMD
N03   * Klokkeslett Ankomstmelding
N03  C                   MOVE      RUKLAM        SFKLAM
N04   * Ved visning av ankomstmeldinger - vis også om  arkiv av tupe ZH(el avvikende) finnes
n04  C                   MOVE      *blanks       SFZH
n04  C                   MOVE      *off          *in52
S09  C**                 if        RUST = 'A'
n04  C     ArkL02Key     chain     ARKIVl02                           52
n04  C  N52              MOVE      ARTYPE        SFZH
S09  C**                 endif
N03  C                   ENDSR
N03  C
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     RUKEY         CHAIN     RUCKP                              33
     C                   EXSR      MOVWSH
     C  N33              UPDATE    RUCKPR
     C   40              UPDATE    SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTEST        ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     RUST          COMP      'D'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGFR
     C   12              GOTO      SLHENT
     C                   END
      * Slettemerk
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     RUST          COMP      'D'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTFR
     C   12              GOTO      SLHENT
     C                   END
      * Se
     C     WSNR          IFEQ      '5'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     RUST          COMP      'D'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPFR
     C                   END
      * send
     C     WSNR          IFEQ      'S'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     RUST          COMP      'D'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      Send
     C                   END
      * avsender/avdelingesn kundenr-
     C     WSNR          IFEQ      'A'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     RUST          COMP      'D'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      Avsend
     C                   END
      * Mottaker
     C     WSNR          IFEQ      'M'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     RUST          COMP      'D'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      Mottak
     C                   END
      *
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
N04  C                   if        *in98=*on
n04  C                   MOVE      'DIV0012'     MSGNR
n04  C                   SETON                                            30
n04  C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTFR         BEGSR
     C***********************************************
     c                   move      'J'           Refresh
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     RUKEY         CHAIN     RUCKP                              33
     C                   EXSR      MOVB1
S05  C* N33              DELETE    RUCKPR
N05  C  N33              move      'D'           RUST
N05  C  N33              UPDATE    RUCKPR
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGFR         BEGSR
     C***********************************************
     C                   EXSR      MOVHWS
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       30
     c     *in04         ifeq      *ON
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKU
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   goto      stChg
     c                   end
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     DSPFR         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    RUAVD
     C                   PARM                    RUOPD
     C                   PARM                    DUMFLA
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     Send          BEGSR
     C***********************************************
     c                   move      'J'           Refresh
     C                   SETON                                        99
     C                   move      RUAVD         XXavd             4
     C                   move      RUOPD         XXopd             7
N07  c                   if        RUST = 'A'
N07  c                   exsr      SendAnk
N07  c                   else
     C                   call      'SYRU02'
     C                   parm                    XXavd
     C                   parm                    XXopd
     C                   parm                    UUnik
N07  c                   endif
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
N07
N07  C***********************************************
N07  C     SendAnk       BEGSR
N07  C***********************************************
N07  c                   exsr      MailAdr
N07  C                   IF        XXavNa  <> *BLANKS
N07  C                             and mn  >  0
N07   * Oppdatere Ryckattest-log med info om at Ank-melding er sent
N07  C     RUKEY         CHAIN     RUCKP
N07  c                   if        %found
N07  c                   move      WDT           RUDTAM
N07  c                   movel     WTM           RUKLAM
N07  c                   update    RUCKPR
N07   *
N07  C                   move      RUAVD         UAVD
N07  C                   move      RUOPD         UOPD
N07  C                   eval      mn = 1
N07  c                   dow       mn <= 5
N07  c                   if        mona(mn) = *blanks
N07  c                   leave
N07  c                   endif
N07  C                   CALL      'SYFA40M'
N07  C                   PARM                    UAVD              4
N07  C                   PARM                    UOPD              7
N07  C                   PARM                    XXavNa           50
N07  C                   PARM                    XXavAd           50
N07  C                   PARM      moNa(mn)      UmoNa            50
N07  C                   PARM      moAd(mn)      UmoAd            50
N07  C                   PARM      moSMS(mn)     USMS             15
N07  C                   PARM      'X'           UDummy            1
N07  C                   eval      mn = mn+1
N07  c                   enddo
N07  c
N07  C*
N07  C                   endif
N07  C                   endif
N07  C*
N07  C                   ENDSR
N07  C*
     C***********************************************
     C     Avsend        BEGSR
     C***********************************************
     c                   move      'J'           Refresh
     C                   SETON                                        99
     c     AvdKey        chain     KODTA                              33
     C                   Z-ADD     KOAKNR        UUKUNR            8 0
      * "låner" returparameter UCONTA for å sende inn filter på type
     C                   eval      UCONTA    =  '*RUCKATTEST'
      * Om den ikke fins - lag en (VCUNDCR forutsetter minst en..)
     c                   exsr      LagRuckC
     C                   CALL      'VCUNDCR'
     C                   PARM                    FIFIRM
     C                   PARM                    UUKUNR            8 0
     C                   PARM                    URRN             10 0
     C                   PARM                    UEMFA             1
     C                   PARM                    UCONTA           30
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     Mottak        BEGSR
     C***********************************************
     c                   move      'J'           Refresh
     C                   SETON                                        99
     C                   Z-ADD     RUKU          UUKUNR            8 0
      * Om den ikke fins - lag en (VCUNDCR forutsetter minst en..)
     c                   exsr      LagRuckC
      * "låner" returparameter UCONTA for å sende inn filter på type
     C                   eval      UCONTA    =  '*RUCKATTEST'
     C                   CALL      'VCUNDCR'
     C                   PARM                    FIFIRM
     C                   PARM                    UUKUNR            8 0
     C                   PARM                    URRN             10 0
     C                   PARM                    UEMFA             1
     C                   PARM                    UCONTA           30
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LagRuckC      BEGSR
     C***********************************************
     c                   move      UUKUNR        YYRUKU
     C     CONKEY        CHAIN     Cundc03                            33
     c     *in33         ifeq      *ON
     c                   clear                   CundcR
     c                   move      FIFIRM        CFIRMA
     c                   move      UUKUNR        CCOMPN
N02  c                   move      *zeros        CAVD
     C                   eval      CTYPE     =  '*RUCKATTEST'
     c                   write     CundcR
     c                   end
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSKU
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVHWS        BEGSR
     C***********************************************
     C                   SETON                                        40
     c                   Move      RUKU          WSKU
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     48            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSH        BEGSR
     C***********************************************
     C*
     c                   Move      WSKU          RUKU
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *entry        PLIST
     c                   parm                    UUnik             7
      *
     C     RUKEY         KLIST
     C                   KFLD                    RUAVD
     C                   KFLD                    RUOPD
N09  C     RUallKey      KLIST
N09  C                   KFLD                    YHAVD
N09  C                   KFLD                    YHOPD
     C     RUL01Key      KLIST
s01  C*                  KFLD                    W2HST
s01  C*                  KFLD                    W2HAVD
s01  C*                  KFLD                    W2HOPD
n01  C                   KFLD                    YHST
n01  C                   KFLD                    YHAVD
n01  C                   KFLD                    YHOPD
n04   *
n04  C     RUL04Key      KLIST
n04  C                   KFLD                    AnkStat           1
n04  C                   KFLD                    AnkDt0            8 0
n04  C                   move      'A'           AnkStat
n04  C     ArkL02Key     KLIST
n04  C                   KFLD                    ARSTAT
n04  C                   KFLD                    RUAVD
n04  C                   KFLD                    RUOPD
n04  C                   KFLD                    ZHTYPE            2
n04  C                   move      'A'           ARSTAT
n04  C                   move      'ZH'          ZHTYPE
N06  C     KunKEY        KLIST
N06  C                   KFLD                    FIFIRM
N06  C                   KFLD                    RUKU
     C     CONKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YYTYPE           30
     C                   KFLD                    YYRUKU            8 0
     C                   eval      YYTYPE     =  '*RUCKATTEST'
     C     AvdKey        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    RUAVD
     C                   MOVE      'A'           KOAUNI
     C     *loval        setll     FIRM
     c  N34              read      FIRM                                   34
     C*
N08  C                   MOVE      *ZEROS        HEDTOP
n04  C                   MOVE      *ZEROS        SFDTAM
N07  c                   CALL      'SYGE15C'
N07  c                   parm                    WDT               8
N07  c                   parm                    WTM               6
     C                   ENDSR
     C*
      ******************************************************************************
     C     MailAdr       BEGSR
      ******************************************************************************
      * sjekk om det finnes kontaktpersoner på avdelingens kundenummer (avsender)
     c                   Movel     *blanks       XXavNa
     c                   movel     *blanks       XXavAD
     c     AvdKey        chain     KODTA                              33
     c                   move      KOAKNR        YYRUKU
     C     KUNKEY        chain     CundL01                            33
     c  n33              eval      fift=knavn
     C     CONKEY        chain     Cundc03                            33
     C  n33CEMAIL        COMP      *Blanks                                33
     C  n33CCONTA        COMP      *Blanks                                33
     c     *in33         cabeq     '1'           UtMaila
     c                   Movel     CCONTA        XXavNa
     c                   movel     CEMAIL        XXavAD
      * sjekk om det finnes kontaktpersoner Kj.f.mot kundenummer (avsender)
     c                   clear                   moad
     c                   clear                   moad
     c                   clear                   mosms
     c                   move      *zeros        mn                2 0
     c                   move      RUKU          YYRUKU
     C     CONKEY        setll     Cundc03
     C     CONKEY        reade     Cundc03                                34
     C     *in34         DOWEQ     '0'
     C     Mn            andlt     5
     C     CEMAIL        Ifne      *Blanks
     C     CCONTA        andne     *Blanks
     c                   add       1             mn
     c                   movel     CCONTA        mona(mn)
     c                   movel     CEMAIL        moad(mn)
     c                   movel     CMOBIL        moSMS(mn)
     c                   end
     C     CONKEY        reade     Cundc03                                34
     C                   END
      *
     C     UtMaila       ENDSR
