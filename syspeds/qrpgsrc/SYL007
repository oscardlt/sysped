      *********************************************************************
      * SLETT MERKET SLETT-LINJER
      * PROGRAM BEHANDLER INPUT FRA SYL006
      *
CRTPG+*  CRTPGM SYSPED/SYL007 MODULE(*LIBL/SYL007 *LIBL/SYL003B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLHEAD    UF   E           K DISK    USROPN
     FLLLINE    UF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D TB              S              1    DIM(15)

     D WSMN            S              7
     D WSVN            S              3
     D WSUSER          S             10
     D BWSMN           S              7  0
     D BWSVN           S              3  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/SYL003B')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Slett plukklinje
     C                   exsr      SLETT
      *
     C                   EXSR      VISLINJE
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
     C                   CLOSE     LLHEAD
     C                   CLOSE     LLLINE
     C                   CLOSE     BRIDF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')

     C                   MOVE      *ZEROS        BWSMN
     C                   eval      WSMN    = zhbgetvar('LHMN')
     C                   MOVEA     WSMN          TB
     C     1             DO        7             XN                3 0
     C     TB(XN)        IFNE      ' '
     C                   MULT      10            BWSMN
     C                   MOVE      TB(XN)        BWSMN
     C                   ENDIF
     C                   ENDDO
     C                   MOVE      *ZEROS        BWSVN
     C                   eval      WSVN   = zhbgetvar('LLVN')
     C                   MOVEA     WSVN          TB
     C     1             DO        3             XN                3 0
     C     TB(XN)        IFNE      ' '
     C                   MULT      10            BWSVN
     C                   MOVE      TB(XN)        BWSVN
     C                   ENDIF
     C                   ENDDO
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')

     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      LLHEAD                               50
     C                   open      LLLINE                               50
     c     LL00K         klist
     c                   kfld                    BWSMN
     c                   kfld                    BWSVN
      *
     C                   endsr
      *
      *=====================================================================
      *  Slett merket linje
      *=====================================================================
     C     SLETT         begsr
     C     BWSMN         CHAIN     LLHEAD                             33
     C     LL00K         CHAIN     LLLINE                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      'DL2'         UTYPE             3
     C                   CALL      'LL019R'
     C                   PARM                    LHGRP
     C                   PARM                    LHPNR
     C                   PARM                    LLANR
     C                   PARM                    LLMN
     C                   PARM                    LLVN
     C                   PARM                    UTYPE
      *
     C  N33              DELETE    LLLINER
      *
     C                   SUB       1             LHTLIN
     C                   SUB       LLWNT0        LHANT0
     C                   SUB       LLWNTL        LHANTL
     C                   SUB       LLWNT1        LHANT1
     C                   SUB       LLWNT2        LHANT2
     C                   SUB       LLWNT3        LHANT3
     C                   SUB       LLWVEK        LHWVEK
     C                   SUB       LLWKUB        LHWKUB
     C                   UPDATE    LLHEADR
      *
     C                   END
      *
     C                   endsr
      *=====================================================================
      * VIS LINJER
      *=====================================================================
     C     VISLINJE      begsr
      *
     C                   CALLB     PGM2
     C                   PARM                    BWSMN
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFIRM
     C                   PARM                    BIFARG
     C                   PARM                    WSANR            15
     C                   PARM                    WSVB             35
     C                   PARM                    WSSALD            7
     C                   PARM                    WSWNT0            7
     C                   PARM                    FMELDNR           1
     C                   PARM                    FEILMELDING      80
      *
     C                   endsr
