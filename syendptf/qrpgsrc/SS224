      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SS222 OG SEND EPOST FOR MOTTATT GODS
      *
CRTPG+*  CRTPGM SYSPED/SS224 MODULE(*LIBL/SS224
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSLGF    O  A E             DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSONO           S              7
     D WBONO           S              7  0
     D WSLNO           S              3
     D WBLNO           S              3  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   MOVE      WBONO         WSONO
     C                   MOVE      WBLNO         WSLNO
     C                   CALL      'SIEM03R'
     C                   PARM                    WSONO
     C                   PARM                    WSLNO
      *
     C                   EVAL      LGUSER = WSUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   EVAL      LGPGM  = 'SS224'
     C                   MOVE      WDT           LGHDT
     C                   MOVE      WTM           LGHTID
     C                   EVAL      LGFTYP = '*CONF'
     C                   EVAL      LGONO  = WBONO
     C                   EVAL      LGLNO  = WBLNO
     C                   EVAL      LGTXT  = 'Confirmation is sent.'
     C                   EVAL      LGAGM  = 'Confirmation is sent.'
     C                   EVAL      LGANY  = 'Confirmation is sent.'
     C                   WRITE     GSSLGFR
      *
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('*fini')
      *
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   EVAL      WSONO   = zhbgetvar('WSONO')
     C                   eval      WBONO   = c2n2(WSONO)
     C                   EVAL      WSLNO   = zhbgetvar('WSLNO')
     C                   eval      WBLNO   = c2n2(WSLNO)
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton VED FEIL
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SS224'
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      GSSLGF
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     GSSLGF
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
