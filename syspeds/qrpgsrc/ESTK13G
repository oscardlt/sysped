      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK13G MODULE(*LIBL/ESTK13G *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Billabel har med index (rekkefølge) i tilleg til bilnr
10183 * 02 JOV PTF10 Skipp post dersom breddegrad(og lengdegr) = 0
10183 * 03 JOV PTF10 Ved TURNR som input - vis turens sjåfør aktuell(e) datoer
      * 04 JOV PTF10 - versj.nr
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 05 JOV PTF11 - versj.nr
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTKGPS     IF   E           K DISK    USROPN
     FUNITL04   IF   E           K DISK    USROPN
N03  FTURER14   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D SJID            s             10
     D SJNR            s              8  0
     D TURNR           s              8
     D TURNRN          s              8  0
     D FRADT           s              8
     D FRADTN          s              8  0
     D FRAKL           s              4
     D FRAKLN          s              4  0
     D TILDT           s              8
     D TILDTN          s              8  0
     D TILKL           s              4
     D TILKLN          s              4  0
     D SISPOS          s              1
     D Streng          s           1024
     D SeTur           s            500
     D Post1           s              1
     D CallBack        s           1000
N01  D BilLabel        s             20
N01  D BilLabel2       s             30
N04  D VERSJ           s             10
N04  D IcoOffX         s              3
N04  D IcoOffY         s              3
N04  D LabOffX         s              3
N04  D LabOffY         s              3
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      *
     D TkdtDs          ds
     D TKDTTI                  1     14  0
     D TKDT                    1      8  0
     D TKTI                    9     14  0
     D                 ds
     D WSDTTI                  1     14  0
     D wsTILDT                 1      8  0
     D wsTILKL                 9     14  0
     D                 DS
     D  TKBRE_GD               1      8  0
     D  TKBRE_G                1      2
     D  TKBRE_D                3      8
     D                 DS
     D  TKLEN_GD               1      9  0
     D  TKLEN_G                1      3
     D  TKLEN_D                4      9
      * Definere  hex appostrof
     D Ap              c                   x'7D'
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
      * i webinterface kan bruker velge Sjåførnr el Bilnr,
      *   dette omsettes uansett til parameter SJNr = PD00000002 (f.eks)     Redefineres: SJID
     C                   eval      SjID = zhbgetvar('SjNr')
     C                   MOVE      SJID          SJNR                           8 siste = SJNR
     C                   eval      TURNR= zhbgetvar('TURNR')
     C                   eval      TURNRN= c2n2(TURNR)
     C                   MOVE      TURNRN        TURNR
     C                   eval      FRADT= zhbgetvar('FRADT')
     C                   eval      FRADTN= c2n2(FRADT)
     C                   MOVE      FRADTN        FRADT
     C                   eval      FRAKL= zhbgetvar('FRAKL')
     C                   eval      FRAKLN= c2n2(FRAKL)
     C                   MOVE      FRAKLN        FRAKL
     C                   eval      TILDT= zhbgetvar('TILDT')
     C                   eval      TILDTN= c2n2(TILDT)
     C                   MOVE      TILDTN        TILDT
     C                   eval      TILKL= zhbgetvar('TILKL')
     C                   eval      TILKLN= c2n2(TILKL)
     C                   MOVE      TILKLN        TILKL
     C                   eval      SISPOS= zhbgetvar('SISPOS')
     C                   eval      CallBack = zhbgetvar('CallBack')
N04   * versjonsnr av klientprogramvare
N04  C                   eval      VERSJ = zhbgetvar('V')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     TKGPS
     C                   CLOSE     UNITL04
N03  C                   CLOSE     TURER14
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTK11G':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      *
      * Flagg for at det evt er første post som skrives (init av JSON-object)
     C                   move      'J'           Post1
      * cross domain oppkall
     c                   eval      Streng=%trim(CallBack)+'('
     C                   callp     updHTMLvar('LINTXT':Streng)
     C                   callp     wrtsection('lin')
      *
     C     StartLes      tag
N03  c     SISPOS        ifeq      'Y'
N03  C     KeySIS        setgt     TKGPS
N03  C     SJID          Readpe    TKGPS                                  50
N03  c                   else
     C     Key           setll     TKGPS
     C     SJID          Reade     TKGPS                                  50
N03  c                   endif
     c     *in50         DOWEQ     '0'
     c     TKDTTI        CABGT     WSDTTI        Sluttles
N02  c     TKBRE         Cabeq     *zeros        Next
      * Post funnet,
     C                   exsr      EnPost
     c     SISPOS        CABEQ     'Y'           Sluttles
      *
N02  c     Next          tag
N03  c     SISPOS        ifeq      'Y'
N03  C     SJID          Readpe    TKGPS                                  50
N03  c                   else
     C     SJID          Reade     TKGPS                                  50
N03  c                   endif
     c                   end
     C     SluttLes      tag
      *
     C     Post1         ifeq      'N'                                          minst en skrevet
     C                   callp     updHTMLvar('LINTXT':']}')
     C                   callp     wrtsection('lin')
     C                   end
      * cross domain call ( svaret er pakket inn i funksjon, sett sluttparantes )
     C                   callp     updHTMLvar('LINTXT':')')
     C                   callp     wrtsection('lin')
     C
     C                   endsr
     C
      ***************************************************************************
     C     EnPost        BEGSR
      ***************************************************************************
     c                   add       1             postID            5 0
     c                   move      *blanks       Streng
     C     Post1         Ifeq      'J'
     C                   eval      Streng = '{"points": [ '
     C                   move      'N'           Post1
     C                   else
     c                   movel     ','           Streng                         komma mellom hver
     C                   end
      *
      * FINN BILNR via sjåfør-ID (forutsetter at det er definert fast sjåfør på bilen)
     c                   move      TKDT          TKDTA             8
     C                   move      TKBRID        SJÅFØR            8
     C                   eval      UNRETU = 'PDA:'+SJÅFØR
     C     UNRETU        chain     UnitL04                            33
S05  c*  33              movel     TKBRID        UNBILN
N05  C   33              eval      UNBILN = 'Sj:'+%trim(%editc(SJNR:'Z'))
     c                   eval      BilLabel  = %trim(UNBILN)+'('
     c                             + %trim(%editc(PostID:'Z'))+')'
     c                   eval      BilLabel2 = %trim(UNBILN)+' ( '
     c                             + %trim(%editc(PostID:'Z'))+' )'
     c                   move      '+'           BreFor            1
     c                   move      '+'           LenFor            1
     c     TKLEN         iflt      0
     c                   mult      -1            TKLEN
     c                   move      '-'           LenFor
     c                   end
     c     TKBRE         iflt      0
     c                   mult      -1            TKBRE
     c                   move      '-'           BreFor
     c                   end
     C                   MOVE      TKLEN         TKLEN_GD
     C                   MOVE      TKBRE         TKBRE_GD
N04   * Offset for Icon  og Label (bilnr)
N04  c                   if        Versj >= '02.01'
N04  c                   eval      IcoOffX='3'
N04  c                   eval      IcoOffY='30'
N04  c                   eval      LabOffX='2'
N04  c                   eval      LabOffY='11'
N04  c                   else
N04  c                   eval      IcoOffX='3'
N04  c                   eval      IcoOffY='31'
N04  c                   eval      LabOffX='2'
N04  c                   eval      LabOffY='-22'
N04  c                   end
      /free
       // Ap=Hexavedi for single Appostrof for å kunne bruke inne i tekstkonst.(mellom '       ')


         Streng = %trim(Streng)+'{'
         +' "id": "' + %trim(%editc(PostID:'Z')) + '",'
         +' "timestamp": "' + %trim(%editw(TKTI:'  :  :  ')) + '",'
         +' "pos":{"lat": "'+BreFor+TKBRE_G + '.' + TKBRE_D +'", '
                 +'"lng": "'+LenFor+TKLEN_G + '.' + TKLEN_D +'"},'
         +' "address": {"address": "" ,"contrycode":""},'
         +' "icon": {"url":"images/truck.png", '
                    +'"size": {"width":"74", "height":"32"}, '
       // S02       +'"title": "Title linje 1 \n'+'Title linje 2", '
                    +'"title": "'+ %trim(BilLabel2) +'\n'
                    +   %trim(%editw(TKDT:'    .  .  '))  +' '
                         + %trim(%editw(TKTI:'  :  :  '))+'", '
        // s04      +'"offset": { "X":"3", "Y":"30"}},'
                    +'"offset": { "X":"'+%trim(IcoOffX)+'",'
                                +'"Y":"'+%trim(IcoOffY)+'"}},'
       // S02 +' "label": {"text":"'+%trim(UNBILN)+'", '
         +' "label": {"text":"'+%trim(BilLabel)+'", '
        //          +'"offset":{"X":"2","Y":"11"}},'
                    +'"offset": { "X":"'+%trim(LabOffX)+'",'
                                +'"Y":"'+%trim(LabOffY)+'"}},'
         +' "info":"'
         +'<h1>'+%trim(UNBILN)+'</h1>'
         +'SjåførID: '+ %trim(TKBRID)+'<br>'
         +'GPSTid: '+   %trim(%editw(TKDT:'    .  .  '))  +' '
         + %trim(%editw(TKTI:'  :  :  '))
         +'" ,'
         +' "unit": "'+%trim(UNBILN)+'", '
         +' "type":"car", '
         +' "group":""'
         +'}';
      /end-free
     C                   callp     updHTMLvar('LINTXT':Streng)
     C                   callp     wrtsection('lin')
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * Ugyldig userID
NUSR c     *in50         ifeq      '1'
NUSR C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
NUSR c                             '/CGITAG/')
NUSR C                   callp     updhtmlvar('User':USER)
NUSR C                   callp     wrtsection('all')
NUSR C                   callp     wrtsection('*fini')
NUSR C                   close     BRIDF
NUSR C                   eval      *inlr = *on
NUSR C                   return
NUSR c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      TKGPS
     C                   OPEN      UNITL04
N03  C                   OPEN      TURER14
      *
N03
N03   * VED TURNR OPPGITT - FINN SJÅFØR VIA TURNR - VIS DENNE/AKTUELL DATO
N03  C                   if        TURNRN <> *zeros
N03  c     TURNRN        chain     turer14                            50
N03   * korrekt tur med PDA-sjåfør = søk på dennen sjåfør/aktuell dato
N03  c                   move      TUSJA1        SJNR
N03  c                   movel     'PD'          SJID
N03  c                   move      SJNR          SJID
N03  c                   if        TUDT<>99999999 and TUDT<>0
N03  c                             and TUDTT<>99999999 and TUDTT<>0
N03  c                   move      TUDT          FRADT
N03  c                   move      TUDT          FRADTN
N03  c                   move      TUDTT         TILDT
N03  c                   move      TUDTT         TILDTN
N03  c                   move      0001          FRAKL
N03  c                   move      0001          FRAKLN
N03  c                   move      2359          TILKL
N03  c                   move      2359          TILKLN
N03  c                   endif
N03  c                   endif
      *
     C     KEY           KLIST
     C                   KFLD                    SJID
     C                   KFLD                    FRADTN
     C                   KFLD                    FRAKLN6           6 0
N03  C     KEYSIS        KLIST
N03  C                   KFLD                    SJID
N03  C                   KFLD                    TILDTN
N03  C                   KFLD                    TILKLN6           6 0
      *
     C                   move      *zeros        FRAKLN6
     C                   movel     FRAKLN        FRAKLN6
      * DS for Til-dato/kl
     C                   move      TILDTN        wsTILDT
     C                   move      000059        wsTILKL
     C                   movel     TILKLN        wsTILKL
N03  C                   movel     WSTILKL       TILKLN6
     C     UtInit        endsr
