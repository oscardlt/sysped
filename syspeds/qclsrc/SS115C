/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/*       * 01 JOV PTV11 bruk TOSTMF('xxx') WSCST(*PDF) for PDF-konvertering*/
/*       *              RYDDET GAMMELT TJAFS                               */
/*       * 02 JOV PTV11 LTYP M=merkelappsPRT/PDF  - UFERDIG!!!             */
/* ********************************************************************    */
             PGM        PARM(&FI &CURTUR &LTYP &KOP &LAY &UNIK)
             DCL        VAR(&SPL) TYPE(*CHAR) LEN(10) +
                          VALUE('SYFA12SAP')
             DCL        VAR(&FI) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LAY) TYPE(*CHAR) LEN(2)
             DCL        VAR(&CURTUR) TYPE(*CHAR) LEN(11)
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TMP196) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TMP196M) TYPE(*CHAR) LEN(50)
             DCL        VAR(&AVD) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&OPD) TYPE(*DEC) LEN(7 0)
             DCL        VAR(&FBNR) TYPE(*DEC) LEN(3 0) VALUE(001)
             DCL        VAR(&KOP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(50)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             CHGVAR     VAR(&TMP196) VALUE('TMP' *CAT &UNIK)
             CHGVAR     VAR(&TMP196M) VALUE('/QSYS.LIB/QTEMP.LIB/' +
                          *CAT &TMP196 *CAT '.FILE/' *CAT &TMP196 +
                          *CAT '.MBR')
             IF         COND(&LAY = '  ') THEN(CHGVAR VAR(&LAY) +
                          VALUE('WW'))

             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)

             CHGVAR     VAR(&AVD) VALUE(%SUBSTRING(&CURTUR 1 4))
             CHGVAR     VAR(&OPD) VALUE(%SUBSTRING(&CURTUR 5 7))

             CLRPFM     FILE(QTEMP/&TMP196)
             MONMSG     MSGID(CPF3142) EXEC(CRTPF +
                          FILE(QTEMP/&TMP196) RCDLEN(196) +
                          SIZE(1000000 10000))
             OVRDBF     FILE(TMP196) TOFILE(QTEMP/&TMP196)


 /*LEGG INN SPOOL-INNHOLD*/
 /*ULIKT SPOOLFILENAVN/PROGRAM VED ZEBRA / ELTRON    */
 /****************************/
 /*    ZEBRA-LABEL           */
 /****************************/
             IF         COND(&LTYP = 'Z') THEN(DO)
 /*LEGG INN START-HTML*/
 /* kun zebra stripe takler HTML for øyeblikket...*/
             CALL       PGM(SPLHTMSTA)
 /*ZEBRA (ZPL II KODE)   */
             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/ZBARC' +
                          *CAT &CURTUR *CAT '.html')
             OVRPRTF    FILE(ZBARC) OUTQ(SYCGIOUTQ) HOLD(*YES) +
                          SPLFOWN(*JOB)
             CALL SYFA12SN PARM(&FI &LAY &AVD &OPD &FBNR &KOP)
             CPYSPLF    FILE(ZBARC) TOFILE(QTEMP/&TMP196) +
                          JOB(&QNBR/&QUSER/&QJOB) SPLNBR(*LAST) +
                          MBROPT(*ADD) CTLCHAR(*NONE)
             MONMSG     MSGID(CPF0000)
                        ENDDO
 /****************************/
 /*N01 LASER-LABEL           */
 /****************************/
             IF         COND(&LTYP = 'L') THEN(DO)
         /*LEGG INN START-HTML*/
             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/LASLAB' +
                          *CAT &CURTUR *CAT '.pdf')
             RMVLNK     OBJLNK(&STMF)
             MONMSG     MSGID(CPF0000)
             OVRPRTF    FILE(SYFA12SAP) DEVTYPE(*AFPDS) PAGESIZE(70 +
                          80) LPI(8) CPI(10) OVRFLW(70) +
                          OUTQ(SYCGIOUTQ) FORMTYPE(*STD) +
                          SPLFOWN(*JOB) TOSTMF(&STMF) WSCST(*PDF)
    /*N02  foreløpig - teste med kode L det som skal bli M..*/
    /*N02    OVRPRTF    FILE(SYFA12SAP) DEVTYPE(*AFPDS) PAGESIZE(60 +
                          45) LPI(8) CPI(10) OUTQ(SYCGIOUTQ) +
                          FORMTYPE(*STD) SPLFOWN(*JOB) +
                          TOSTMF(&STMF) WSCST(*PDF)  */
             CALL SYFA12SA PARM(&FI &LAY &AVD &OPD &FBNR &KOP)
                        ENDDO
 /****************************/
 /*    ZEBRA-LABEL           */
 /****************************/
             IF         COND(&LTYP = 'E') THEN(DO)
 /*ELTRON (MODELLER( LP2844 M.M.) SOM IKKE KJØRER ZPL II KODE)   */
             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/ELTRON' +
                          *CAT &CURTUR *CAT '.txt')
             OVRPRTF    FILE(SYLA03P) OUTQ(SYCGIOUTQ) HOLD(*YES) +
                          SPLFOWN(*JOB)
             CALL SYFA12SE PARM(&FI &LAY &AVD &OPD &FBNR &KOP)
             CPYSPLF    FILE(SYLA03P) TOFILE(QTEMP/&TMP196) +
                          JOB(&QNBR/&QUSER/&QJOB) SPLNBR(*LAST) +
                          MBROPT(*ADD) CTLCHAR(*NONE)
             MONMSG     MSGID(CPF0000)
                        ENDDO

 /*LEGG INN STOPP  -HTML*/
 /* kun zebra stripe takler HTML for øyeblikket...*/
             IF         COND(&LTYP = 'Z') THEN(DO)
             CALL       PGM(SPLHTMSTO)
             ENDDO
 /*              */
 /*              */
 /*KOPIER TIL IFS*/
 /*              */
             IF         COND(&LTYP *NE 'L') THEN(DO)
             CPYTOSTMF  FROMMBR(&TMP196M) TOSTMF(&STMF) +
                          STMFOPT(*REPLACE) CVTDTA(*AUTO) +
                          STMFCODPAG(*STDASCII)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CHGAUT     OBJ(&STMF) USER(QTMHHTTP) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)

             DLTOVR     FILE(TMP196)
             CLRPFM     FILE(QTEMP/&TMP196)
             MONMSG     MSGID(CPF0000)
             ENDPGM
