********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P12XXX* 01 CB  PTF12  Ersätt vissa tecken
      *********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCI001R MODULE(*LIBL/TDCI001R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKNH      IF   E           K DISK    USROPN
     FDKNSTD    IF   E           K DISK    USROPN
     FEDIM4     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D hexba           c                   const(X'BA')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"oneorder" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   IF        OPDN = 0
     C     AVDN          CHAIN     DKNSTD                             55
     C                   EVAL      TITDN = 0
     C                   EVAL      TIST  = ' '
     C                   EVAL      DKNH_0035=DKNS_0035
     C                   IF        %SUBST(TIGN:1:3) <> *BLANKS AND
     C                             %SUBST(TIGN:4:4) = *BLANKS
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C*                  EVAL      %SUBST(TIGN:4:1) = '-'
     C*                  EVAL      %SUBST(TIGN:5:2) = %SUBST(WDT:3:2)
     C*                  EVAL      %SUBST(TIGN:7:1) = '-'
     C                   EVAL      JN043='N'
     C                   END
     C                   ELSE
     C     DKNhKey       CHAIN     DKNH                               55
     C                   END
     C     *IN55         IFEQ      '0'
      * Har 043 tagits emot ?
     C                   EXSR      SR043
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
N01
N01       TINAA=%scanRpl(HEXBA:' ':TINAA);
N01       TIADA1=%scanRpl(HEXBA:' ':TIADA1);
N01       TIPNA=%scanRpl(HEXBA:' ':TIPNA);
N01       TIPSA=%scanRpl(HEXBA:' ':TIPSA);
N01       TINAS=%scanRpl(HEXBA:' ':TINAS);
N01       TIADS1=%scanRpl(HEXBA:' ':TIADS1);
N01       TIPNS=%scanRpl(HEXBA:' ':TIPNS);
N01       TIPSS=%scanRpl(HEXBA:' ':TIPSS);
N01
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tiavd¬ : ¬'+%trim(%editc(TIAVD:'Z'))+'¬, '
          +'¬titdn¬ : ¬'+%trim(%editc(TITDN:'Z'))+'¬, '
          +'¬dknh_0035¬ : ¬'+%trim(DKNH_0035)+'¬,'
          +'¬tisg¬ : ¬'+%trim(TISG)+'¬, '
          +'¬tist¬ : ¬'+%trim(TIST)+'¬, '
          +'¬tidt¬ : ¬'+%trim(%editc(TIDT:'Z'))+'¬, '
          +'¬tidtf¬ : ¬'+%trim(%editc(TIDTF:'Z'))+'¬, '
          +'¬tienkl¬ : ¬'+%trim(TIENKL)+'¬, '
          +'¬titrnr¬ : ¬'+%trim(TITRNR)+'¬, '
          +'¬tign¬ : ¬'+%trim(TIGN)+'¬, '
          +'¬tignsk¬ : ¬'+%trim(TIGNSK)+'¬, '
          +'¬tialk¬ : ¬'+%trim(TIALK)+'¬, '
          +'¬tialsk¬ : ¬'+%trim(TIALSK)+'¬, '
          +'¬tials¬ : ¬'+%trim(TIALS)+'¬, '
          +'¬tialss¬ : ¬'+%trim(TIALSS)+'¬, '
          +'¬tiglsk¬ : ¬'+%trim(TIGLSk)+'¬, '
          +'¬tiacts¬ : ¬'+%trim(TIACTS)+'¬, '
          +'¬tiskb¬ : ¬'+%trim(TISKB)+'¬, '
          +'¬titsb¬ : ¬'+%trim(TITSB)+'¬, '
          +'¬titarf¬ : ¬'+%trim(TITARF)+'¬, '
          +'¬tikn¬ : ¬'+%trim(%editc(TIKN:'Z'))+'¬, '
          +'¬tina¬ : ¬'+%trim(TINA)+'¬, '
          +'¬tiad1¬ : ¬'+%trim(TIAD1)+'¬, '
          +'¬tipn¬ : ¬'+%trim(TIPN)+'¬, '
          +'¬tips¬ : ¬'+%trim(TIPS)+'¬, '
          +'¬tilk¬ : ¬'+%trim(TILK)+'¬, '
          +'¬tisk¬ : ¬'+%trim(TISK)+'¬, '
          +'¬jn043¬ : ¬'+%trim(JN043)+'¬, '
          +'¬titin¬ : ¬'+%trim(TITIN)+'¬, '
          +'¬tiga1¬ : ¬'+%trim(TIGA1)+'¬, '
          +'¬tiga2¬ : ¬'+%trim(TIGA2)+'¬, '
          +'¬tiga3¬ : ¬'+%trim(TIGA3)+'¬, '
          +'¬tiga4¬ : ¬'+%trim(TIGA4)+'¬, '
          +'¬tivers¬ : ¬'+%trim(TIVERS)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C                   ENDIF
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKNH
     C  N50              CLOSE     DKNSTD
     C  N50              CLOSE     EDIM4

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   eval      OPDN    = c2n2(OPD)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DKNhKey       klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
     C     EDIMKEY       KLIST
     C                   KFLD                    WMSR
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C     *LIKE         DEFINE    MSR           WMSR
     C                   EVAL      WMSR   = 'R'
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKNH
     C  N50              OPEN      DKNSTD
     C  N50              OPEN      EDIM4

     c                   endsr
      *
      *********************************************************
     C     SR043         BEGSR
      *********************************************************
     C                   MOVE      'N'           JN043             1
     C     EDIMKEY       SETLL     EDIM4                              51
     C     EDIMKEY       READE     EDIM4                                  51
     C     *IN51         DOWEQ     *OFF
     C                   IF        M1N07='043'
     C                   EVAL      JN043='J'
     C                   EVAL      *IN51=*ON
     C                   ENDIF
     C     EDIMKEY       READE     EDIM4                                  51
     C                   ENDDO
     C                   ENDSR
