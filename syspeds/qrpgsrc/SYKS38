********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 PGM-variabel endret 9->10 (enklere v kundespesialer)
10XXX * 02 YBC PTF10 7 SIFFER FAKTURANR
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 7 SIFFER FAKTURANR
********************************************************************
     FINTFAS    UF   E           K DISK
     FINTFA80   O  A F   80        DISK
     D                 DS
     D  HEAD1                  1     80
     D  INTYPE                 1      4
     D  INST                   5      5
     D  INSEID                 6     40
     D  INMOID                41     75
      *
     D                 DS
     D  HEAD2                  1     80
     D  XXTYP2                 1      4
     D  INFANR                 5     11  0
     D  INDATO                12     19  0
     D  INDAFF                20     27  0
     D  INAAR                 28     29  0
     D  INMND                 30     31  0
     D  INKUNR                32     39  0
     D  INKID                 40     59
     D  INUDAT                60     67  0
     D  INUTID                68     71  0
      *
     D                 DS
     D  DETALJ                 1     80
     D  XXTYPD                 1      4
     D  INAVD                  5      8  0
     D  INOPD                  9     15  0
     D  INANNE                16     35
     D  INANN2                36     55
     D  INVK                  56     58
     D  INBELN                59     71  2
     D  FORTEG                72     72
     D  INMVA                 73     73
     D  INUAVD                74     77  0
     D  INUOPD                78     84  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUSEID           35
     C                   PARM                    UUMOID           35
     C                   PARM                    UUOK              1
      *
     C     KEY           KLIST
     C                   KFLD                    ZZST              1
     C                   KFLD                    INSEID
     C                   KFLD                    INMOID
      *
     C                   MOVE      '*I2#'        XXTYP2
     C                   MOVE      '*ID#'        XXTYPD
     C                   MOVE      ' '           FORTEG
     C                   MOVE      '1'           UUOK
      *  FINNES MINST 1??
     C                   MOVE      ' '           ZZST
     C     ZZST          SETLL     INTFAS
     C     ZZST          READE(N)  INTFAS                                 94
     C     *IN94         IFEQ      '0'
     C                   MOVE      INSEID        UUSEID
     C                   MOVE      INMOID        UUMOID
     C                   MOVE      'J'           FØRSTE            1
      * .. skriv alle åpne poster for aktuell avs/mott. til INTFAW
     C     KEY           SETLL     INTFAS
     C     KEY           READE     INTFAS                                 94
     C     *IN94         DOWEQ     '0'
      * Skal kunden motta EDIFACT isteden for flatfil ?
      * TESTEN HARDKODES ETTERSOM DET UANSETT ER SPESIAL.
     C     INKUNR        IFEQ      -1
     C                   UPDATE    INTFASR
S02  C*                  MOVE      INFANR        UUFAK             6
N02  C                   MOVE      INFANR        UUFAK             7
s01  C*                  MOVEL     'E316R_'      WPGM              9
s01  C*                  MOVE      '94B'         WPGM
n01  C                   MOVEL     'E316R_'      WPGM             10
n01  C                   MOVE      '94B '        WPGM
     C                   CALL      WPGM
     C                   PARM                    INSEID
     C                   PARM                    INMOID
     C                   PARM                    UUFAK
     C                   GOTO      EDIF
     C                   ENDIF
     C     FØRSTE        IFEQ      'J'
     C                   MOVE      HEAD1         DATA
     C                   EXCEPT    UTDATA
     C                   MOVE      '0'           UUOK
     C                   MOVE      HEAD2         DATA
     C                   EXCEPT    UTDATA
     C                   MOVE      'N'           FØRSTE
S03  C*                  MOVE      INFANR        GMFANR            6 0
N03  C                   MOVE      INFANR        GMFANR            7 0
     C                   END
      *
     C     INFANR        IFNE      GMFANR
     C                   MOVE      HEAD2         DATA
     C                   EXCEPT    UTDATA
     C                   END
      *
     C     INBELN        IFLT      *ZEROS
     C                   MULT      -1            INBELN
     C                   MOVE      '-'           FORTEG
     C                   ELSE
     C                   MOVE      ' '           FORTEG
     C                   END
     C                   MOVE      DETALJ        DATA             80
     C                   EXCEPT    UTDATA
      *
     C                   MOVE      INFANR        GMFANR
      * Oppdater status, X=BEHANDLET
     C                   MOVE      'X'           INST
     C                   UPDATE    INTFASR
      *
     C     EDIF          TAG
      *
     C     KEY           READE     INTFAS                                 94
     C                   END
      *
     C                   END
      *
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     OINTFA80   EADD         UTDATA
     O                       DATA                80
