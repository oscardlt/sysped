     FEDIM      IF   E           K DISK
     FEALC93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEIMD93A   IF   E           K DISK
     FELIN93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FEPIA93A   IF   E           K DISK
     FETAX93A   IF   E           K DISK
     FEUNH      IF   E           K DISK
     FSYSHL01   IF   E           K DISK
     FSYSRL01   UF   E           K DISK
     FVAREL01   IF   E           K DISK
     FTRAVDF    IF   E           K DISK
     FPOSTA0    O  A E             DISK
     FPOSTB0    O  A E             DISK
     FE125F2    UF   E           K DISK
     FE127F     UF   E           K DISK
     FE127D     CF   E             WORKSTN
     FLISTE     O    F  132        PRINTER OFLIND(*IN51)
     D                SDS
     D  USERID               254    263
     D                 DS
     D TXT01           C                   CONST('NKL el.kre.nota')
     D TXT02           C                   CONST('NKL el.faktura ')
     D TXT03           C                   CONST('DIFF. PGA. MOMS-
     D                                     AVRUNDING      ')
     D TXT04           C                   CONST('FAKTURA/KREDITN-
     D                                     OTA TOTALL     ')
     D                 DS
     D  XAN15                  1     15
     D  V1                     1     15
     D                                     DIM(15)
     D                 DS
     D  VARENR                 1     15
     D  V2                     1     15
     D                                     DIM(15)
     D                 DS
     D  X5004                  1     18
     D  BL                     1     18
     D                                     DIM(18)
     D                 DS
     D  XAN10                  1     10
     D  XAN02                  1      2
     D  XAN08                  3     10
     D  XAN07                  1      7
     D                 DS
     D  XFKDT                  1      8
     D  BILAAR                 3      4  0
     D  BILMND                 5      6  0
     D  BILDAG                 7      8  0
     D  KRDAAR                 3      4  0
     D  KRDMND                 5      6  0
     D  KRDDAG                 7      8  0
     D                 DS
     D  XFKDTF                 1      8
     D  FFDAAR                 3      4  0
     D  FFDMND                 5      6  0
     D  FFDDAG                 7      8  0
     D                 DS
     D  WTIME                  1     12  0
     D  WTID                   1      6  0
     D  WDAT                   7     12  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  W≈≈                   11     12  0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   EXFMT     E127D01
     C                   MOVE      '1'           *IN99
     C   03              GOTO      SLUTT
     C     WSAVD         IFNE      0
     C     WSAVD         CHAIN     TRAVDF                             99
     C  N99AVEDI         COMP      'J'                                9999
     C   99              GOTO      START
     C                   END
      *
     C     WSAVD         SETLL     E125F2
     C     WSAVD         IFEQ      0
     C                   READ(N)   E125F2                                 30
     C                   ELSE
     C     WSAVD         READE(N)  E125F2                                 30
     C                   END
      *
     C     *IN30         DOWEQ     '0'                                          1<-B
     C     XAVD          IFNE      EF2AVD                                        2<-B
     C     XAVD          IFNE      0                                              3<-B
     C   51              EXCEPT    PHEAD
     C   51              MOVE      '0'           *IN51
     C                   EXCEPT    DETTOT
     C                   ADD       XBLTD         XBLTT            11 2
     C                   ADD       XBLTDM        XBLTTM           11 2
     C                   ADD       XBLTDU        XBLTTU           11 2
     C                   MOVE      *ZEROS        XBLTD            11 2
     C                   MOVE      *ZEROS        XBLTDM           11 2
     C                   MOVE      *ZEROS        XBLTDU           11 2
     C                   END                                                      3<-E
     C     EF2AVD        CHAIN     TRAVDF                             33
     C                   MOVE      EF2AVD        XAVD              3 0
     C*          XAVD      IFEQ 0                            3<-B
     C                   EXCEPT    PHEAD
     C                   MOVE      '0'           *IN51
     C*                    END                               3<-E
     C                   END                                                     2<-E
      * Oppdater pÂ POSTA0
     C                   EXSR      SRA
      * Oppdater totall pÂ POSTB0
     C                   EXSR      SRB
      * Oppdater linjer pÂ POSTB0
     C                   EXSR      SRC
      * Oppdater fradrag/tilleg av fakturanivÂ pÂ POSTB0
     C                   EXSR      SRD
      * Oppdater differanse pga. momsavrunding pÂ linjer
     C                   EXSR      SRE
      * Oppdater POSTA0
     C                   WRITE     POSAR
      *
     C*                    DELETE125F2R
      * Les neste faktura/kreditnota
     C     WSAVD         IFEQ      0                                             2<-B
     C                   READ(N)   E125F2                                 30
     C                   ELSE                                                    2
     C     WSAVD         READE(N)  E125F2                                 30
     C                   END                                                     2<-E
     C  N30              ENDDO                                                  1<-E
      *
     C   51              EXCEPT    PHEAD
     C   51              MOVE      '0'           *IN51
     C     WSAVD         IFEQ      0
     C                   EXCEPT    DETTOT
     C                   END
     C                   ADD       XBLTD         XBLTT
     C                   ADD       XBLTDM        XBLTTM
     C                   ADD       XBLTDU        XBLTTU
     C                   EXCEPT    TOTAL
      *
      * Slett faktura/kreditnota nÂr overf¯ring har gÂtt greit.
     C     WSAVD         SETLL     E125F2
     C     WSAVD         IFEQ      0
     C                   READ      E125F2                                 30
     C                   ELSE
     C     WSAVD         READE     E125F2                                 30
     C                   END
     C     *IN30         DOWEQ     '0'                                          1<-B
     C                   DELETE    E125F2R
     C     WSAVD         IFEQ      0                                             2<-B
     C                   READ      E125F2                                 30
     C                   ELSE                                                    2
     C     WSAVD         READE     E125F2                                 30
     C                   END                                                     2<-E
     C  N30              ENDDO                                                  1<-E
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *____________________________________________________
     C     SRA           BEGSR
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     E127FK        CHAIN     E127F                              61
     C                   ADD       1             EFBILN
     C                   UPDATE    E127FR
     C                   MOVE      EFBILN        BILNR             7 0
      *
      * Hente bilagsdato (NKL's faktura/kreditnotas dato)
      *       forfallsdato
     C                   MOVE      *ZEROS        XFKDT
     C                   MOVE      *ZEROS        XFKDTF
     C     DTM08K        SETLL     EDTM93A
     C     DTM08K        READE     EDTM93A                                33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C                   SELECT                                                  2<-B
     C     D2005         WHENEQ    '7  '
     C                   MOVEL     D2380         XFKDT
     C     D2005         WHENEQ    '8  '                                         2<-B
     C                   MOVEL     D2380         XFKDTF
     C                   ENDSL                                                   2<-E
     C  N33DTM08K        READE     EDTM93A                                33
     C  N33              END                                                    1<-E
      *       regnskapsÂr/periode (=bilagsÂr/mÂned)
     C     BILAAR        IFLT      XERAAR
     C                   Z-ADD     XERAAR        PERAAR
     C                   Z-ADD     XERNR         PERNR
     C                   ELSE
     C     BILMND        IFLT      XERNR
     C                   Z-ADD     XERAAR        PERAAR
     C                   Z-ADD     XERNR         PERNR
     C                   ELSE
     C                   Z-ADD     BILAAR        PERAAR
     C                   Z-ADD     BILMND        PERNR
     C                   END
     C                   END
      *
     C*                    WRITEPOSAR
     C                   MOVE      *ZEROS        POSNR
      *
     C                   ENDSR
      *
      *____________________________________________________
     C     SRB           BEGSR
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     EF2MN         CHAIN     EDIM                               33
      *
     C     M1001         IFEQ      '381'                                        1<-B
     C                   MOVE      'K'           KRNOKD
     C                   MOVE      'K'           XFK               1
     C                   MOVEL     TXT01         BILTXT
     C                   ELSE                                                   1
     C                   MOVE      'F'           XFK
     C                   MOVE      ' '           KRNOKD
     C                   MOVEL     TXT02         BILTXT
     C                   MULT      -1            EF2TOT
     C                   END                                                    1<-E
     C                   MOVE      'H'           TYPE
     C                   Z-ADD     EF2TOT        BBELOP
      * Oppdater faktura/kreditnotasdato (har gjort pÂ POSTA0)
      * Oppdater forfalsdato (har gjort pÂ POSTA0 hvis datoen er sent)
     C     XFKDTF        IFEQ      *ZEROS
     C                   MOVE      XFKDT         XFKDTF
      * Forfallsdato settes til 30 dager fra bilagsdato
     C     FFDMND        IFEQ      12
     C                   ADD       1             FFDAAR
     C                   Z-ADD     1             FFDMND
     C                   ELSE
     C                   ADD       1             FFDMND
     C                   END
     C     FFDMND        IFEQ      4
     C     FFDMND        OREQ      6
     C     FFDMND        OREQ      9
     C     FFDMND        OREQ      11
     C     FFDDAG        IFGT      30
     C                   Z-ADD     30            FFDDAG
     C                   END
     C                   ELSE
     C     FFDMND        IFEQ      2
     C     FFDDAG        ANDGT     28
     C                   Z-ADD     28            FFDDAG
     C                   END
     C                   END
     C                   END
      * Oppdater kostnadssted
     C                   MOVEL     M0068         XAN03             3
     C                   MOVE      XAN03         KSTED
      *
     C     EF2MN         CHAIN     EUNH                               33
      * Oppdater leverand¯rs faktura/kreditnota nr.
     C                   MOVEL     M1004         XAN10
     C     XAN02         IFEQ      'KD'
     C                   MOVEL     XAN08         KRNR
     C                   ELSE
     C                   MOVEL     XAN07         KRNR
     C                   END
      *
     C                   MOVE      '20100'       KONTO
     C                   MOVE      *ZEROS        KBARER
     C                   MOVE      EF2MVA        MOMSK
     C                   EXSR      GETSUR
      *
     C                   WRITE     POSBR
     C*                    SUB  XBLTD     XBLTT
      *
     C                   MOVE      *BLANKS       XEANNR
     C                   MOVE      TXT04         XTXT
     C   51              EXCEPT    PHEAD
     C   51              MOVEA     '01'          *IN(51)
     C                   MOVE      '1'           *IN52
     C                   EXCEPT    DET1
     C                   MOVE      '0'           *IN52
      *
     C                   ENDSR
      *
      *____________________________________________________
     C     SRC           BEGSR
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C*                    MOVE '15110'   KONTO
     C                   MOVE      *ZEROS        XTOT             11 2
      *
     C     LIN22K        SETLL     ELIN93A
     C     STSRC         TAG
     C     LIN22K        READE     ELIN93A                                31
     C   31              GOTO      SLSRC
      *
      * Hente kostnadsbÊre
     C*          AVB∆RE    IFEQ 'J'
     C                   EXSR      HVGR
     C*                    END
      * Hente momssats
     C     TAX30K        SETLL     ETAX93A
     C     TAX30K        READE     ETAX93A                                32
     C     *IN32         IFEQ      '1'
     C     T5305         OREQ      'E'
     C                   Z-ADD     1             XMSATS            5 4
     C                   MOVE      '15100'       KONTO
     C                   MOVE      '0'           MOMSK
     C                   ELSE
     C                   MOVE      '15110'       KONTO
     C                   MOVE      '1'           MOMSK
     C                   MOVEL     T5278         X5004
     C                   EXSR      KNVBL
     C     XBL           DIV       100           XMSATS
     C                   ADD       1             XMSATS
     C                   END
      * Hente bel¯p
     C     MOA23K        SETLL     EMOA93A
     C                   MOVE      *ZEROS        BBELOP
     C     MOA23K        READE     EMOA93A                                32
     C     *IN32         DOWEQ     '0'
     C     M5025         IFEQ      '203'
     C                   MOVE      M5004         X5004
     C                   EXSR      KNVBL
     C     M1001         IFEQ      '381'
     C                   Z-SUB(H)  XBL           BBELOP
     C                   ELSE
     C                   Z-ADD(H)  XBL           BBELOP
     C                   END
     C                   MOVE      '1'           *IN32
     C                   END
     C  N32MOA23K        READE     EMOA93A                                32
     C  N32              END
     C                   MULT(H)   XMSATS        BBELOP
     C                   ADD       BBELOP        XTOT
      * Oppdater POSTB0
     C                   EXSR      GETSUR
     C                   WRITE     POSBR
     C     MOMSK         IFEQ      '0'
     C                   ADD       BBELOP        XBLTDU
     C                   ELSE
     C                   ADD       BBELOP        XBLTDM
     C                   END
     C                   ADD       BBELOP        XBLTD
      * Hente varebeskrivelse
     C     IMD22K        SETLL     EIMD93A
     C     IMD22K        READE     EIMD93A                                32
     C     *IN32         DOWEQ     '0'
     C     I7077         IFEQ      'FL'
     C                   MOVEL     I7008A        XTXT             30
     C                   MOVE      '1'           *IN32
     C                   END
     C  N32IMD22K        READE     EIMD93A                                32
     C  N32              ENDDO
      *
     C   51              EXCEPT    PHEAD
     C   51              MOVEA     '01'          *IN(51)
     C                   EXCEPT    DET2
     C                   MOVE      '0'           *IN52
      *
     C                   GOTO      STSRC
      *
     C     SLSRC         ENDSR
      *
      *____________________________________________________
     C     SRD           BEGSR
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C*                    MOVE '15110'   KONTO
     C                   Z-ADD     11            KBARER
     C                   MOVE      *BLANKS       XEANNR           15
     C                   MOVE      *BLANKS       XSANR            15
     C                   MOVE      *BLANKS       VARENR
      *
     C     ALC14K        SETLL     EALC93A
     C     STSRD         TAG
     C     ALC14K        READE     EALC93A                                31
     C   31              GOTO      SLSRD
      *
      * Hente momssats
     C     TAX20K        SETLL     ETAX93A
     C     TAX20K        READE     ETAX93A                                32
     C     *IN32         IFEQ      '1'
     C     T5305         OREQ      'E'
     C                   Z-ADD     1             XMSATS            5 4
     C                   MOVE      '15100'       KONTO
     C                   MOVE      '0'           MOMSK
     C                   ELSE
     C                   MOVE      '15110'       KONTO
     C                   MOVE      '1'           MOMSK
     C                   MOVEL     T5278         X5004
     C                   EXSR      KNVBL
     C     XBL           DIV       100           XMSATS
     C                   ADD       1             XMSATS
     C                   END
      * Hente bel¯p
     C     MOA18K        SETLL     EMOA93A
     C                   MOVE      *ZEROS        BBELOP
     C     MOA18K        READE     EMOA93A                                32
     C     *IN32         IFEQ      '0'
     C                   MOVE      M5004         X5004
     C                   EXSR      KNVBL
     C     M1001         IFEQ      '381'
     C     A5463         IFEQ      'A'
     C                   Z-ADD(H)  XBL           BBELOP
     C                   ELSE
     C                   Z-SUB(H)  XBL           BBELOP
     C                   END
     C                   ELSE
     C     A5463         IFEQ      'A'
     C                   Z-SUB(H)  XBL           BBELOP
     C                   ELSE
     C                   Z-ADD(H)  XBL           BBELOP
     C                   END
     C                   END
     C                   END
     C                   MULT(H)   XMSATS        BBELOP
     C                   ADD       BBELOP        XTOT
      * Oppdater POSTB0
     C                   EXSR      GETSUR
     C                   WRITE     POSBR
     C     MOMSK         IFEQ      '0'
     C                   ADD       BBELOP        XBLTDU
     C                   ELSE
     C                   ADD       BBELOP        XBLTDM
     C                   END
     C                   ADD       BBELOP        XBLTD
      *
     C                   MOVEL     A7160         XTXT
     C   51              EXCEPT    PHEAD
     C   51              MOVEA     '01'          *IN(51)
     C                   EXCEPT    DET2
     C                   MOVE      '0'           *IN52
      *
     C                   GOTO      STSRD
      *
     C     SLSRD         ENDSR
      *
      *____________________________________________________
     C     SRE           BEGSR
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     EF2TOT        ADD       XTOT          XTOT
     C     XTOT          IFNE      0
     C                   MOVE      *BLANKS       VARENR
     C                   MOVE      *BLANKS       XEANNR
     C                   MOVE      *BLANKS       XSANR
     C                   MOVE      '15110'       KONTO
     C                   MOVE      '1'           MOMSK
     C     XTOT          MULT      -1            BBELOP
     C                   EXSR      GETSUR
     C                   WRITE     POSBR
     C                   ADD       BBELOP        XBLTDM
     C                   ADD       BBELOP        XBLTD
      *
     C                   MOVEL     TXT03         XTXT
     C   51              EXCEPT    PHEAD
     C   51              MOVEA     '01'          *IN(51)
     C                   EXCEPT    DET2
     C                   MOVE      '0'           *IN52
     C                   END
      *
     C                   ENDSR
      *
      ******************************************
     C     HVGR          BEGSR
      ******************************************
     C                   MOVE      *BLANKS       XEANNR           15
     C                   MOVE      *BLANKS       XSANR            15
     C                   MOVE      *BLANKS       VAREGR
      *
     C     L7143         COMP      'EN'                               3434
     C     *IN34         IFEQ      '0'                                          1<-B
     C                   MOVEL     L7140         XAN15
     C                   Z-ADD     15            XN                3 0
     C     XN            DOWGE     1                                             2<-B
     C     V1(XN)        IFNE      *BLANKS                                        3<-B
     C                   MOVE      *BLANKS       VARENR
     C     16            SUB       XN            XN3               3 0
     C     1             DO        XN            XN2               3 0             4<-B
     C                   MOVE      V1(XN2)       V2(XN3)
     C                   ADD       1             XN3
     C                   END                                                       4<-E
     C     VARE1K        CHAIN     VAREL01                            34
     C                   MOVE      VARENR        XEANNR
     C                   MOVE      *ZEROS        XN
     C                   END                                                      3<-E
     C                   SUB       1             XN
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     PIA22K        SETLL     EPIA93A
     C     STHVG1        TAG
     C     PIA22K        READE     EPIA93A                                34
     C  N34P7143A        CABNE     'SA'          STHVG1
     C     *IN34         IFEQ      '0'                                          1<-B
     C                   MOVE      *BLANKS       VARENR
     C                   MOVEL     P7140A        XAN15
     C                   Z-ADD     15            XN                3 0
     C     XN            DOWGE     1                                             2<-B
     C     V1(XN)        IFNE      *BLANKS                                        3<-B
     C     16            SUB       XN            XN3               3 0
     C     1             DO        XN            XN2               3 0             4<-B
     C                   MOVE      V1(XN2)       V2(XN3)
     C                   ADD       1             XN3
     C                   END                                                       4<-E
     C     VARE1K        CHAIN     VAREL01                            34
     C                   MOVE      VARENR        XSANR
     C                   MOVE      *ZEROS        XN
     C                   END                                                      3<-E
     C                   SUB       1             XN
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     AVB∆RE        IFEQ      'J'                                          1<-B
     C     VAREGR        IFEQ      *BLANKS                                       2<-B
     C     PIA22K        SETLL     EPIA93A
     C     PIA22K        READE     EPIA93A                                34
     C     *IN34         DOWEQ     '0'                                            3<-B
     C     P7143A        IFEQ      'GN'                                            4<-B
     C     P7143A        OREQ      'GD'
     C                   MOVEL     P7140A        XAN03
     C                   MOVE      '1'           *IN34
     C                   END                                                       4<-E
     C  N34              ENDDO                                                    3<-E
     C                   ELSE                                                    2
     C                   MOVEL     VAREGR        XAN03             3
     C                   END                                                     2<-E
     C                   MOVE      XAN03         KBARER
     C                   ELSE                                                   1
     C                   MOVE      *ZEROS        KBARER
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVBL         BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XBL              17 2
     C                   Z-ADD     100           XFAKT             9 6
     C                   Z-ADD     1             XFTEGN            1 0
     C                   MOVE      '0'           *IN33
     C     1             DO        18            XN                3 0
     C                   SELECT
     C     BL(XN)        WHENGE    '0'
     C     BL(XN)        ANDLE     '9'
     C                   MULT      10            XBL
     C                   MOVE      BL(XN)        XBL
     C   33              DIV       10            XFAKT
     C     BL(XN)        WHENEQ    ','
     C     BL(XN)        OREQ      '.'
     C                   MOVE      '1'           *IN33
     C     BL(XN)        WHENEQ    '-'
     C                   Z-SUB     1             XFTEGN
     C                   OTHER
     C                   Z-ADD     100           XN
     C                   ENDSL
     C                   END
     C                   MULT      XFAKT         XBL
     C                   MULT      XFTEGN        XBL
      *
     C                   ENDSR
      *
      ******************************************
     C     INIT          BEGSR
      ******************************************
     C     E127FK        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    EFTYP
     C                   MOVE      'NKL   '      EFTYP
     C     SURKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    REGNA
     C                   MOVE      'NKL   '      REGNA             6
     C     VARE1K        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    VARENR
     C     DTM08K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN08
     C     *LIKE         DEFINE    DGN           YGN08
     C                   Z-ADD     8             YGN08
     C     ALC14K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN14
     C     *LIKE         DEFINE    LGN           YGN14
     C                   Z-ADD     14            YGN14
     C     MOA18K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN18
     C     *LIKE         DEFINE    MGN           YGN18
     C                   Z-ADD     18            YGN18
     C     TAX20K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN20
     C     *LIKE         DEFINE    TGN           YGN20
     C                   Z-ADD     20            YGN20
     C     LIN22K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN22
     C     *LIKE         DEFINE    LGN           YGN22
     C                   Z-ADD     22            YGN22
     C     PIA22K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN22
     C                   KFLD                    LREP0
     C     IMD22K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN22
     C                   KFLD                    LREP0
     C     MOA23K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN23
     C                   KFLD                    LREP0
     C     *LIKE         DEFINE    MGN           YGN23
     C                   Z-ADD     23            YGN23
     C     TAX30K        KLIST
     C                   KFLD                    EF2MN
     C                   KFLD                    YGN30
     C                   KFLD                    LREP0
     C     *LIKE         DEFINE    TGN           YGN30
     C                   Z-ADD     30            YGN30
      *
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      'J'           OPDKOD
     C                   MOVE      'H'           TYPE
     C                   MOVE      'NKL'         ATTEST
      * HENTE SYSTEMDATO OG TID
     C                   TIME                    WTIME
     C*                    MOVE '1'       *IN51
      * HENTE REGNSKAPSPERIODE (SISTE BRUKT + 1)
     C                   MOVE      '01'          FIRMA
     C     FIRMA         CHAIN     SYSHL01                            33
     C     SPERIA        IFLE      ANTPER
     C     SPERIA        ADD       1             XERNR             3 0
     C                   ELSE
     C                   Z-ADD     1             XERNR
     C                   END
     C     UMONTH        IFLT      XERNR
     C     UYEAR         SUB       1             XERAAR            3 0
     C                   ELSE
     C                   Z-ADD     UYEAR         XERAAR
     C                   END
      *
     C                   ENDSR
      *
     C******************************************
     C     GETSUR        BEGSR
     C******************************************
      * HENT NYTT SURRUGATNR - FRA SYSRF
     C     SURKEY        CHAIN     SYSRL01                            30
     C  N30              ADD       1             SISSGT
     C  N30              UPDATE    SYSRR
     C                   Z-ADD     SISSGT        POSLNR
     C                   ADD       1             POSNR
     C                   ENDSR
      *
     OLISTE     E            PHEAD            03
     O                                           25 'JOURNAL FOR EL.OVERFÿRTE'
     O                                           43 'FAKTURA/KREDITNOTA'
     O                                           55 'K.sted:'
     O                       XAVD                59
     O                       AVNV                90
     O                       WDAT          Y    100
     O                       WTID               110 '  .  .  '
     O                       PAGE               120
     O          E            PHEAD            05
     O                                            8 'Bil.nr.'
     O                                           15 'Bil.DT'
     O                                           31 'Bilagstekst    '
     O                                           43 'F./k.notanr'
     O                                           83 'Moms'
     O                                           97 '        Bel¯p'
     O                                          103 'Ksted'
     O                                          109 'BÊrer'
     O                                          115 'Konto'
     O          E            PHEAD            06
     O                                           46 'Varenr(EAN)    '
     O                                           62 'Varebeskrivelse'
     O                                           83 'Moms'
     O*                                  97 '        Bel¯p'
     O*                                 103 'Ksted'
     O*                                 109 'BÊrer'
     O*                                 115 'Konto'
     O          E            PHEAD            07 08
     O                                           24 '________________________'
     O                                           48 '________________________'
     O                                           72 '________________________'
     O                                           96 '________________________'
     O                                          120 '________________________'
     O          E            DET1           1
     O                       BILNR         Z      8
     O                       BILDAG              11
     O                       BILMND              13
     O                       BILAAR              15
     O                       BILTXT              31
     O                       XFK                 33
     O                       KRNR                44
     O                       MOMSK               83
     O                       BBELOP        L     97
     O                       KSTED              103
     O                       KBARER             109
     O                       KONTO              115
     O          E            DET2           1
     O                       XEANNR              46
     O*                        XSANR     41
     O                       XTXT                77
     O                       MOMSK               80
     O                       BBELOP        L     97
     O                       KSTED              103
     O                       KBARER             109
     O                       KONTO              115
     O          E            DETTOT         2
     O                                           12 'TOTALL FOR '
     O                       XAVD                17
     O                       XBLTD         L     32
     O                                           57 'DER AV MED MOMS'
     O                       XBLTDM        L     72
     O                                           95 'UTEN MED MOMS'
     O                       XBLTDU        L    110
     O          E            TOTAL       2  1
     O                                           17 'TOTALL FOR LISTE'
     O                       XBLTT         L     32
     O                                           57 'DER AV MED MOMS'
     O                       XBLTTM        L     72
     O                                           95 'UTEN MED MOMS'
     O                       XBLTTU        L    110
