********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTPF)
     A                                      PRINT
S01  A*                                     HLPDOC('SYEF03010' 'SYEF03 ' 'SY400-
S01  A*                                     LO/MAINT')
N01  A                                      HLPPNLGRP(HLPSCR SYEF03PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
N01  A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            XDLT           7A  H
     A            WSNR           1A  I  6  2
     A            AIRCOD    R        O  6  5REFFLD(AIRLDR/AIRCOD *LIBL/AIRLD)
     A            AIRWEI    R        O  6 11REFFLD(AIRLDR/AIRWEI *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRGRN    R        O  6 21REFFLD(AIRLDR/AIRGRN *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRAWB    R        O  6 36REFFLD(AIRLDR/AIRAWB *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRHAW    R        O  6 51REFFLD(AIRLDR/AIRHAW *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRHAM    R        O  6 66REFFLD(AIRLDR/AIRHAW *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF03(03 'AVSLUTT')
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A  91N93                               CF12(12 'FORRIGE')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
S01  A*         H                           HLPDOC('SYEF03020' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03020 SYEF03PNL)
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYEF03'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1418 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AIRLIN    R        O  3 33REFFLD(AIRLDR/AIRLIN *LIBL/AIRLD)
     A                                      COLOR(WHT)
     A            WSTO          30A  O  3 38COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AIRPFR    R        O  4 33REFFLD(AIRLDR/AIRPFR *LIBL/AIRLD)
     A                                      COLOR(WHT)
     A                                  4 37'-->'
     A            AIRPTO    R        O  4 41REFFLD(AIRLDR/AIRPTO *LIBL/AIRLD)
     A                                      COLOR(WHT)
     A                                  4 46MSGCON(007 SHU0079 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            AIROMR    R        O  4 54REFFLD(AIRLHR/AIROMR *LIBL/AIRLH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                      DSPATR(ND)
     A                                  4 63MSGCON(010 SHU0080 *LIBL/SYMSGF)
     A            AIRCUR    R        O  4 74REFFLD(AIRLHR/AIRCUR *LIBL/AIRLH)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 SHU0077 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF03(03 'AVSLUTT')
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'Søk')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                      PROTECT
S01  A*         H                           HLPDOC('SYEF03030' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03030 SYEF03PNL)
     A                                      HLPARA(02 001 02 008)
S01  A*         H                           HLPDOC('SYEF03040' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03040 SYEF03PNL)
     A                                      HLPARA(02 009 02 019)
S01  A*         H                           HLPDOC('SYEF03050' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03050 SYEF03PNL)
     A                                      HLPARA(02 020 02 035)
S01  A*         H                           HLPDOC('SYEF03060' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03060 SYEF03PNL)
     A                                      HLPARA(02 036 02 050)
S01  A*         H                           HLPDOC('SYEF03070' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03070 SYEF03PNL)
     A                                      HLPARA(02 051 02 065)
S01  A*         H                           HLPDOC('SYEF03080' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03080 SYEF03PNL)
     A                                      HLPARA(02 066 02 080)
     A            MSGNR          7A  P
     A                                  1  2MSGCON(078 SHU0077 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSRCOD    R        B  2  5REFFLD(AIRLDR/AIRCOD *LIBL/AIRLD)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  80                                  DSPATR(RI)
     A  80                                  DSPATR(PC)
     A            WSRWEI    R        B  2 11REFFLD(AIRLDR/AIRWEI *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSRGRN    R        B  2 21REFFLD(AIRLDR/AIRGRN *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PC)
     A            WSRAWB    R        B  2 36REFFLD(AIRLDR/AIRAWB *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            WSRHAW    R        B  2 51REFFLD(AIRLDR/AIRHAW *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            WSRHAM    R        B  2 66REFFLD(AIRLDR/AIRHAW *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 50MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF03(03 'AVSLUTT')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(04)
     A                                  1  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2RLIN    R        I  1 15REFFLD(AIRLDR/AIRLIN *LIBL/AIRLD)
     A  30                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A            W2RPFR    R        I  1 21REFFLD(AIRLDR/AIRPFR *LIBL/AIRLD)
     A  30                                  DSPATR(RI)
     A            W2RPTO    R        I  1 27REFFLD(AIRLDR/AIRPTO *LIBL/AIRLD)
     A  30                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Forrige')
S01  A*         H                           HLPDOC('SYEF03020' 'SYEF03' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYEF03020 SYEF03PNL)
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYEF03'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1418 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AIRLIN    R        O  3 33REFFLD(AIRLDR/AIRLIN *LIBL/AIRLD)
     A                                      COLOR(WHT)
     A            WSTO          30A  O  3 38COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AIRPFR    R        O  4 33REFFLD(AIRLDR/AIRPFR *LIBL/AIRLD)
     A                                      COLOR(WHT)
     A                                  4 37'-->'
     A            AIRPTO    R        O  4 41REFFLD(AIRLDR/AIRPTO *LIBL/AIRLD)
     A                                      COLOR(WHT)
     A                                  4 46MSGCON(007 SHU0079 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            AIROMR    R        O  4 54REFFLD(AIRLHR/AIROMR *LIBL/AIRLH)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                      DSPATR(ND)
     A                                  4 63MSGCON(010 SHU0080 *LIBL/SYMSGF)
     A            AIRCUR    R        O  4 74REFFLD(AIRLHR/AIRCUR *LIBL/AIRLH)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 SHU0077 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CF03(03 'AVSLUTT')
     A                                      CF12(12 'forrige')
     A                                      CF04(04 'SØK')
     A                                      CF02(02 'kopier')
     A                                      CF11(11 'Slett avtale')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEF03'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1418 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4  2MSGCON(020 SHU0068 *LIBL/SYMSGF)
     A            AIRLIN    R        B  4 23REFFLD(AIRLHR/AIRLIN *LIBL/AIRLH)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A  20                                  DSPATR(PR)
     A            WSAIRL        30A  O  4 29
     A                                  6  2MSGCON(020 SHU0069 *LIBL/SYMSGF)
     A            AIRPFR    R        B  6 23REFFLD(AIRLHR/AIRPFR *LIBL/AIRLH)
     A  71                                  DSPATR(RI)
     A  20                                  DSPATR(PR)
     A            WSFROM        30A  O  6 29
     A                                  8  2MSGCON(020 SHU0070 *LIBL/SYMSGF)
     A            AIRPTO    R        B  8 23REFFLD(AIRLHR/AIRPTO *LIBL/AIRLH)
     A  72                                  DSPATR(RI)
     A  20                                  DSPATR(PR)
     A            WSTO          30A  O  8 29
     A                                 10  2MSGCON(020 SHU0071 *LIBL/SYMSGF)
     A            WSRCUR    R        B 10 23REFFLD(AIRLHR/AIRCUR *LIBL/AIRLH)
     A  20                                  DSPATR(PR)
     A                                 12  2MSGCON(020 SHU0072 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            WSROMR    R        B 12 23REFFLD(AIRLHR/AIROMR *LIBL/AIRLH)
     A  20                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A* 20                                  DSPATR(ND)
     A                                 14  2MSGCON(040 SHU0229 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 14 44MSGCON(025 SHU0231 *LIBL/SYMSGF)
     A            ANTKOP         5S 0O 14 71COLOR(WHT)
     A                                 16  2MSGCON(020 SHU0068 *LIBL/SYMSGF)
     A  20        FLYLIN    R        B 16 23REFFLD(AIRLHR/AIRLIN *LIBL/AIRLH)
     A                                      EDTCDE(4)
     A  76                                  DSPATR(RI)
     A                                 18  2MSGCON(020 SHU0069 *LIBL/SYMSGF)
     A  20        FLYPFR    R        B 18 23REFFLD(AIRLHR/AIRPFR *LIBL/AIRLH)
     A  76                                  DSPATR(RI)
     A                                 20  2MSGCON(020 SHU0070 *LIBL/SYMSGF)
     A  20        FLYPTO    R        B 20 23REFFLD(AIRLHR/AIRPTO *LIBL/AIRLH)
     A  76                                  DSPATR(RI)
     A                                 22 61MSGCON(018 SHU0230 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 BBA0054 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  1  2MSGCON(070 SHU0101 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            AIRCOD    R        O  3  5REFFLD(AIRLDR/AIRCOD *LIBL/AIRLD)
     A            AIRWEI    R        O  3 11REFFLD(AIRLDR/AIRWEI *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRGRN    R        O  3 21REFFLD(AIRLDR/AIRGRN *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRAWB    R        O  3 36REFFLD(AIRLDR/AIRAWB *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRHAW    R        O  3 51REFFLD(AIRLDR/AIRHAW *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A            AIRHAM    R        O  3 66REFFLD(AIRLDR/AIRHAW *LIBL/AIRLD)
     A                                      EDTCDE(4)
     A                                  5  1MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
