********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TVINN BRUKER KUN 6 LANG POSISJON
10XXX * 02 SH  PTF10 LÅSE DEKLARANT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD061PNL)
     A                                      HLPTITLE('SAD IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'HENTE REFERANSE FRA Z-FIL')
     A                                      CF10(10 'Slette ferdigmeldt TVINN')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SAD000 SAD061PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD061'
     A                                      COLOR(BLU)
     A            MSGTXH        30A  O  1 20MSGID(&HEAMSG SYMSGF)
     A                                      COLOR(WHT)
     A            HEAMSG         7A  P
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  4  2'Avdeling:'
     A            UAVD           4  0B  4 20EDTCDE(4)
     A  95                                  DSPATR(PC)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  7  2'Ekstern ref....:'
     A            EXTREF        35A  B  7 20
     A                                      DSPATR(PC)
     A                                  8  2'Signatur.......:'
     A            WSSG           3A  B  8 20TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                  9  2'Tolldeknummer..:'
     A            WSTDN          7Y 0B  9 20TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9 27'Tast oppdragsnummer'
     A                                 10  2'Fraktbrevnummer:'
     A            WSFRBN         3Y 0B 10 20TEXT('Fraktbrevnummer')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  35N32                          11  2'Sjø-oppdrag'
     A  35N32     WSUR           1A  B 11 20DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A  35N32                          11 27'F = Sjø-oppdrag'
     A                                 13  2'Sendes på tvinn:'
     A            WSTOLK         1A  B 13 20VALUES(' ' 'J' 'N')
     A  49                                  DSPATR(PR)
     A  96                                  DSPATR(PC)
     A                                 13 27'(J/N/blank)'
     A                                 14  2'Skrives nå.....:'
     A            WSTVIN         1A  B 14 20DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 14 27'(J/N/blank - Kun ved J i TVINN)'
     A                                 23  2'F4=Søk ref.   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CF02(02 'BILFORTOLLING')
     A                                      CF04(04 'Søking')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF08(08 'Innstikk')
     A                                      CF10(10 'Se på ME-logg')
     A                                      CA12(12 'Gå til inngangsbilde')
     A                                      CF13(13 'Oppdater reg.nr./konto')
     A                                      CF14(14 'Søk kundes fritekst')
     A                                      CF15(15 'Mer rubr.28')
     A                                      CF24(24 'FLERE F-TASTER')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SAD001 SAD061PNL)
     A  *DS3                                HLPARA(01 001 01 080)
     A  *DS4                                HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SAD002 SAD061PNL)
     A  *DS3                                HLPARA(02 001 03 080)
     A  *DS4                                HLPARA(02 001 03 080)
     A          H                           HLPPNLGRP(SAD002 SAD061PNL)
     A  *DS3                                HLPARA(04 001 04 030)
     A  *DS4                                HLPARA(04 001 04 030)
     A          H                           HLPPNLGRP(SAD009 SAD061PNL)
     A  *DS3                                HLPARA(04 040 04 080)
     A  *DS4                                HLPARA(04 040 04 080)
     A          H                           HLPPNLGRP(SAD002 SAD061PNL)
     A  *DS3                                HLPARA(05 001 05 030)
     A  *DS4                                HLPARA(05 001 05 030)
     A          H                           HLPPNLGRP(SAD009 SAD061PNL)
     A  *DS3                                HLPARA(05 040 05 080)
     A  *DS4                                HLPARA(05 040 05 080)
     A          H                           HLPPNLGRP(SAD002 SAD061PNL)
     A  *DS3                                HLPARA(06 001 06 030)
     A  *DS4                                HLPARA(06 001 06 030)
     A          H                           HLPPNLGRP(SAD003 SAD061PNL)
     A  *DS3                                HLPARA(07 001 07 030)
     A  *DS4                                HLPARA(07 001 07 030)
     A          H                           HLPPNLGRP(SAD003A SAD061PNL)
     A  *DS3                                HLPARA(07 040 07 080)
     A  *DS4                                HLPARA(07 040 07 080)
     A          H                           HLPPNLGRP(SAD003 SAD061PNL)
     A  *DS3                                HLPARA(08 001 08 030)
     A  *DS4                                HLPARA(08 001 08 030)
     A          H                           HLPPNLGRP(SAD003A SAD061PNL)
     A  *DS3                                HLPARA(08 040 08 080)
     A  *DS4                                HLPARA(08 040 08 080)
     A          H                           HLPPNLGRP(SAD003 SAD061PNL)
     A  *DS3                                HLPARA(09 001 09 040)
     A  *DS4                                HLPARA(09 001 09 040)
     A          H                           HLPPNLGRP(SAD004 SAD061PNL)
     A  *DS3                                HLPARA(09 040 09 080)
     A  *DS4                                HLPARA(09 040 09 080)
     A          H                           HLPPNLGRP(SAD003 SAD061PNL)
     A  *DS3                                HLPARA(10 001 10 030)
     A  *DS4                                HLPARA(10 001 10 030)
     A          H                           HLPPNLGRP(SAD003 SAD061PNL)
     A  *DS3                                HLPARA(11 001 11 030)
     A  *DS4                                HLPARA(11 001 11 030)
     A          H                           HLPPNLGRP(SAD004A SAD061PNL)
     A  *DS3                                HLPARA(11 040 11 080)
     A  *DS4                                HLPARA(11 040 11 080)
     A          H                           HLPPNLGRP(SAD004B SAD061PNL)
     A  *DS3                                HLPARA(12 001 12 038)
     A  *DS4                                HLPARA(12 001 12 038)
     A          H                           HLPPNLGRP(SAD005 SAD061PNL)
     A  *DS3                                HLPARA(12 039 12 080)
     A  *DS4                                HLPARA(12 039 12 080)
     A          H                           HLPPNLGRP(SAD004B SAD061PNL)
     A  *DS3                                HLPARA(13 001 13 038)
     A  *DS4                                HLPARA(13 001 13 038)
     A          H                           HLPPNLGRP(SAD006 SAD061PNL)
     A  *DS3                                HLPARA(13 040 13 080)
     A  *DS4                                HLPARA(13 040 13 080)
     A          H                           HLPPNLGRP(SAD007 SAD061PNL)
     A  *DS3                                HLPARA(14 001 14 038)
     A  *DS4                                HLPARA(14 001 14 038)
     A          H                           HLPPNLGRP(SAD006 SAD061PNL)
     A  *DS3                                HLPARA(14 040 14 080)
     A  *DS4                                HLPARA(14 040 14 080)
     A          H                           HLPPNLGRP(SAD007B SAD061PNL)
     A  *DS3                                HLPARA(15 001 15 080)
     A  *DS4                                HLPARA(15 001 15 080)
     A          H                           HLPPNLGRP(SAD008 SAD061PNL)
     A  *DS3                                HLPARA(16 001 17 080)
     A  *DS4                                HLPARA(16 001 17 080)
     A          H                           HLPPNLGRP(SAD008B SAD061PNL)
     A  *DS3                                HLPARA(18 001 18 050)
     A  *DS4                                HLPARA(18 001 18 050)
     A          H                           HLPPNLGRP(SAD008D SAD061PNL)
     A  *DS3                                HLPARA(18 052 18 064)
     A  *DS4                                HLPARA(18 052 18 064)
     A          H                           HLPPNLGRP(SAD008B SAD061PNL)
     A  *DS3                                HLPARA(19 001 19 080)
     A  *DS4                                HLPARA(19 001 19 080)
     A          H                           HLPPNLGRP(SAD008C SAD061PNL)
     A  *DS3                                HLPARA(20 001 20 049)
     A  *DS4                                HLPARA(20 001 20 049)
     A          H                           HLPPNLGRP(SAD008C SAD061PNL)
     A  *DS3                                HLPARA(21 001 21 049)
     A  *DS4                                HLPARA(21 001 21 049)
     A          H                           HLPPNLGRP(SAD008E SAD061PNL)
     A  *DS3                                HLPARA(22 001 22 018)
     A  *DS4                                HLPARA(22 001 22 018)
     A          H                           HLPPNLGRP(SAD008F SAD061PNL)
     A  *DS3                                HLPARA(22 019 22 042)
     A  *DS4                                HLPARA(22 019 22 042)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A                                  1  2'SYSPED/SAD061'
     A                                      COLOR(BLU)
     A            MSGTXH        30A  O  1 20MSGID(&HEAMSG SYMSGF)
     A                                      COLOR(WHT)
     A            HEAMSG         7A  P
     A                                  1 57'1.Dekl.'
     A            WSDTY          2A  B  1 65TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES('  ' 'EU' 'IM')
     A            WSDP           2Y 0B  1 68TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                  2  1'2.Avsender'
     A            WSKNS          8Y 0B  2 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A N61                                  COLOR(YLW)
     A  61                                  DSPATR(BL)
     A  61                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                  2 31'EXT ref:'
     A            WSTREF        35A  B  2 40
     A  63                                  DSPATR(PR)
     A  63                                  COLOR(WHT)
     A N63                                  COLOR(YLW)
     A            WSNAS         30A  B  3  1TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A N61                                  COLOR(YLW)
     A  61                                  DSPATR(BL)
     A  61                                  COLOR(RED)
     A                                  3 40'6.Kolli'
     A            WSNTK          7Y 0B  3 49TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 58'Ref'
     A            SIAVD          4Y 0O  3 62COLOR(WHT)
     A                                      EDTCDE(4)
     A            SITDN          7Y 0O  3 67COLOR(WHT)
     A                                      EDTCDE(4)
     A            SISG           3A  O  3 75COLOR(WHT)
     A            WSADS1        30A  B  4  1TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 40'Toll/mva S/K/I'
     A                                  4 60'D/K'
     A                                  4 68'Brutto vekt'
     A            WSADS2        30A  B  5  1TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSKI          1A  B  5 48TEXT('Selg,Kjøper,Ingen')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'S' 'K' 'I')
     A            WSKDDK         1A  B  5 61TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'D' 'K')
     A            WSVKB          9Y 0B  5 69TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADS3        30A  B  6  1TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  1'8.Mottaker'
     A            WSKNK          8Y 0B  7 13TEXT('Kundenr Kjøper')
     A  62                                  DSPATR(BL)
     A  62                                  COLOR(RED)
     A                                      DSPATR(UL)
     A N62                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 22'Regnr'
     A            WSRG          11A  B  7 28TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 40'12.Verdioppl.'
     A                                  7 58'Andre kostnad'
     A                                  7 72'+/-(P/M)'
     A            WSNAK         30A  B  8  1TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A N62                                  COLOR(YLW)
     A  62                                  DSPATR(BL)
     A  62                                  COLOR(RED)
     A            WSVAL1         3A  B  8 40TEXT('Valutakode Frakt')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBEL1        11Y 2B  8 44TEXT('Beløp tollb.frakt')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVAL2         3A  B  8 58TEXT('Val.kode A. kost')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBEL2        11Y 2B  8 62TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,00)
     A            WSFTG2         1A  B  8 77TEXT('Fortegn +/- a.kost (el. P/M --
     A                                       uten add til rub.22)')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '+' '-' 'P' 'M')
     A            WSADK1        30A  B  9  1TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'15.Avs/utf.land'
     A            WSLKA          2A  B  9 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  90
     AO 69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 62'Rabatt'
     A            WSRAB          4Y 2B  9 69EDTWRD('  .  ')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      COMP(GE ,00)
     A            WSADK2        30A  B 10  1TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 11  1TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 40'19.Cont.'
     A            WSKDC          1A  B 11 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('0' '1')
     A                                 12  1'14.Deklarant'
     A            WSTLF         12A  B 12 14TEXT('Telefon Deklarant')
S02  A*                                     DSPATR(UL)
N02  A                                      DSPATR(UL PR)
     A                                      COLOR(YLW)
     A                                 12 40'20.Lev.vilk.'
     A            WSLV           3A  B 12 53TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLVT         17A  B 12 57TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNAD         30A  B 13  1TEXT('Navn Deklarant')
S02  A*                                     DSPATR(UL)
N02  A                                      DSPATR(UL PR)
     A                                      COLOR(YLW)
     A                                 13 40'22.Fakturasum'
     A                                 13 59'23.Omregn '
     A                                 13 70'24.Tr.type'
     A                                 14  1'21.Ident.'
     A            WSTRID        17A  B 14 11TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKT          2A  B 14 29TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVAL3         3A  B 14 40TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBEL3        13Y 2B 14 44TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,00)
     A            WSVKU          7Y 3B 14 60TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,000)
     A            WSTST          1Y 0B 14 73TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15  1'25.Transportmåte'
     A            WSTRM          2Y 0B 15 18TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 15 40'28.Dato'
     A            WSFID          6N 0B 15 49DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15 56'Finans'
     A            WSFIF         17A  B 15 63TEXT('Finans oppl.faknr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  1'30.Lag.st.'
     A            WSLS          16A  B 16 12TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKDLS         2A  B 16 29TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 16 32'48.KT.'
     A            WSKTC          1A  B 16 39DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKTA          5Y 0B 16 41TEXT('Tollkontonr-1')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      COMP(GE 0)
     A            WSKTB          2Y 0B 16 47TEXT('Tollkontonr-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A                                 16 50'49.Godsn'
     A            WSGN          15A  B 16 59TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  4'Fritekst (benyttes ved F8/EU-70,72-
     A                                      /Dataliste)'
S01  A*           WSPOS          9A  B 17 59TEXT('Posisjon')
N01  A            WSPOS          6A  B 17 59TEXT('Posisjon')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18  1'31.'
     A            WSFT1         45A  B 18  5DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18 52MSGCON(011 SHU0653 *LIBL/SYMSGF)
     A            WSSLAA         1Y 0B 18 64EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                 18 66MSGCON(007 SHU0654 *LIBL/SYMSGF)
     A            WSFT2         45A  B 19  5DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19 52'Tariffør'
     A            SITARF        16A  O 19 64TEXT('TARIFFØRSNAVN')
     A                                      COLOR(WHT)
     A                                 20  1'44.'
     A            WSFT3         45A  B 20  5DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 20 52'Dato'
     A                                 20 64'Løpenr'
     A                                 20 75'Eksp.'
     A            WSFT4         45A  B 21  5DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDTG         10A  B 21 52TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTLL         10A  B 21 64TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLE          6A  B 21 75TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  2MSGCON(012 YBC0868 *LIBL/SYMSGF)
     A            WSDFVF         1Y 0B 22 15EDTCDE(4)
     A  67                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 22 19MSGCON(017 SHU0792 *LIBL/SYMSGF)
     A            WSBVNV         4Y 2B 22 37COLOR(YLW)
     A                                      EDTCDE(J)
     A                                 22 50'Opdnr.'
     A            SIOPD          7Y 0O 22 57TEXT('Oppdragsnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 63'Fraktbrevnr.'
     A            SIFRBN         3Y 0O 22 76TEXT('Fraktbrevnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R WINDOWSZ
     A                                      WINDOW(5 30 5 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE03
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A          H                           HLPPNLGRP(SADBEA SAD061PNL)
     A                                      HLPARA(*RCD)
     A                                  1  2'SAD061'
     A                                      COLOR(BLU)
     A                                  1 10'Bearb.kost./kunst.'
     A                                      COLOR(WHT)
     A                                  2  2'Val.kd:'
     A            WSVALR         3A  B  2 10DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(HI)
     A                                  3  2'Beløp:'
     A            WSBELR        11Y 2B  3 10DSPATR(UL)
     A N99                                  DSPATR(PC)
     A  98                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  4  2'F4=Søk   F12=Forrige'
     A                                      COLOR(BLU)
     A  99                              5  2'Tastet val.kode er ugyldig.'
     A                                      COLOR(WHT)
     A  98                              5  2'Beløp må være større enn 0.'
     A                                      COLOR(WHT)
      *
     A          R BILDE04
     A                                      WINDOW(8 44 5 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                      CA23(23 'BEKREFT')
     A                                  1  2'SAD061'
     A                                      COLOR(BLU)
     A                                  2  2'Trykk på F23 for å sende'
     A                                      COLOR(WHT)
     A                                  3  2'tollkreditnr og dagsoppgjør'
     A                                      COLOR(WHT)
     A                                  4  2'sammentidig.'
     A                                      COLOR(WHT)
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R BILDE05
     A                                      WINDOW(*DFT 4 32 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'SAD061'
     A                                      COLOR(BLU)
     A                                  2  2'Godsnr. finnes fra før.'
     A                                      COLOR(WHT)
     A                                  3  2'Vil du lage SAD med inntastet'
     A                                      COLOR(WHT)
     A                                  4  2'godsnr?'
     A                                      COLOR(WHT)
     A            WSJN           1A  B  4 20DSPATR(UL)
     A                                  4 22'(J/N)'
      *
     A          R BILDE06
     A                                      WINDOW(*DFT 4 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'SAD061'
     A                                      COLOR(BLU)
     A                                  3  2'Vil du kopiere vareliner?'
     A            WSJNV          1A  B  3 28DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  4  2'Vil du kopiere fakturanr?'
     A            WSJNF          1A  B  4 28DSPATR(UL)
     A                                      VALUES('J' 'N')
