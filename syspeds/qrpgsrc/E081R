********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Duplikatkontroll på sendningsref. + avs.
10023 * 02 YBC PTF10 TAKLE INVOIC MED CUSDEC/CUSRES FRA TOLLVESENET
10XXX * 03 CB  PTF10 DATOFORMAT
10105 * 04 CB  PTF10 Duplikatkontroll på sendningsref. + avs. + mot.
10257 * 05 CB  PTF10 Partsstyrt duplikatkontroll
10XXX * 06 CB  PTF10 Les forbi linefeed
11043 * 07 CB  PTF11 Omkompilering grunne ny SYSPEDS/QEDSRC/BGM_I
********************************************************************
     FEDIRE     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      UF A E           K DISK
     FEDIM      UF A E             DISK
     FECMSG1    IF   E           K DISK
     FEUNB      UF A E           K DISK
     FEUNH      O  A E             DISK
N01  FEDIS6     IF   E           K DISK
N01  F                                     RENAME(EDISR:EDISR6)
N05  FEDII      IF   E           K DISK
     D NU              S              1    DIM(15)
S03  D*                DS
S03  D* WTIME                  1     14  0
S03  D* WTID                   1      6  0
S03  D* WDD                    7      8  0
S03  D* WMM                    9     10  0
S03  D* WÅÅ                   11     14  0
S03  D*                DS
S03  D* WDATO                  1      8  0
S03  D* WÅ                     1      4  0
S03  D* WM                     5      6  0
S03  D* WD                     7      8  0
     D LDADTA         UDS          1024
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D  AA                   301    380
     D                                     DIM(80)
     D  AADS                 301    380
     D  AB                   401    480
     D                                     DIM(80)
     D  ABDS                 401    480
     D  U0065                501    506
     D  SEGNAM               507    509
     D  TN                   601    602  0
     D  EOF                  603    605
     D  TEGN                 606    606
     D  LOKAHE               607    607
     D  UMN                  608    616  0
     D  UMBR                 617    626
N06  D  ULF                  627    627
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     IEDICR
     I              CMN                         TVICMN
     IEDIMR
     I              MMN                         TVIMMN
     I              M5004D                      TVI04D
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  N91              EXSR      GETSEG
     C   91              GOTO      SLUTT
     C                   SELECT
     C     SEGNAM        WHENEQ    'UNB'
     C                   EXSR      UNB
     c                   exsr      UNBWRI
     C     SEGNAM        WHENEQ    'UNH'
     C                   EXSR      UNH
      * PARTTC har ikke BGM segment
     C     U0065         IFEQ      'PARTTC'
     C                   EXSR      CVTLVL
     C     SEGNAM        IFEQ      'UNT'
     C                   EXSR      UNT
S01  C*                  EXSR      USRJOB
N01  C   56              EXSR      USRJOB
     C                   ENDIF
     C                   ENDIF
      *
     C     SEGNAM        WHENEQ    'BGM'
     C                   EXSR      BGM
     C                   EXSR      CVTLVL
     C     SEGNAM        IFEQ      'UNT'
     C                   EXSR      UNT
ELARMC*    U0065         IFNE      'IFTMCS'
S01  C*                  EXSR      USRJOB
N02  C     U0004         IFNE      'NO000101'
N02  C     U0065         OREQ      'INVOIC'
N01  C   56              EXSR      USRJOB
N02  C                   END
ELARMC*                  ENDIF
     C                   ENDIF
      *
     C     SEGNAM        WHENEQ    'UNZ'
     C                   EXSR      UNZ
ELARMC*    U0065         IFEQ      'IFTMCS'
ELARMC*                  EXSR      USRJOB2
ELARMC*                  ENDIF
     C                   MOVE      *BLANKS       SEGNAM
     C                   GOTO      SLUTT
     C                   ENDSL
     C                   GOTO      SEGSTA
     C     SLUTT         TAG
      * Close input file
     C                   OUT       LDADTA
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
     C     *DTAARA       DEFINE    *LDA          LDADTA
     C                   OUT       LDADTA
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WLIB             10
     C                   PARM                    WFIL             10
     C                   PARM                    WMBR             10
     C                   PARM                    WNUMR            10 0
      * Open input file
     C   91              GOTO      ENDINI
      * HENTE DAGENS DATO OG TID
S03  C*                  TIME                    WTIME
S03  C*                  Z-ADD     WÅÅ           WÅ
S03  C*                  Z-ADD     WDD           WD
S03  C*                  Z-ADD     WMM           WM
N03  C                   CALL      'SYGE15C'
N03  C                   PARM                    WDT               8
N03  C                   PARM                    WTM               6
N03  C                   MOVE      WDT           WDATO             8 0
N03  C                   MOVE      WTM           WTID              6 0
      * DUMMY CHAIN
     C   91
     CANN911             CHAIN     EDIM                               20
      * INIT AV ARBEIDSFELT
     C                   MOVE      *ZEROS        NUMVRD           15 0
     C                   MOVE      *ZEROS        NUTEGN            1 0
     C                   MOVE      *BLANKS       SEGNAM
     C                   MOVE      *BLANKS       AADS
     C                   MOVE      *BLANKS       ABDS
     C                   MOVEA     *BLANKS       FE
     C                   MOVEA     *BLANKS       NU
     C     *LIKE         DEFINE    UIN           WIN
     C     *LIKE         DEFINE    UMN           WMN
     C     *LIKE         DEFINE    *IN91         WIN91
     C     *LIKE         DEFINE    *IN99         WIN99
     C                   MOVE      'R'           WCSR              1
     C     ECMKEY        KLIST
     C                   KFLD                    WCSR
     C                   KFLD                    U0065
     C                   KFLD                    U0054
     C                   KFLD                    U0004
     C                   KFLD                    U0010
     C     KEYEDIS6      KLIST
     C                   KFLD                    U0020A
     C                   KFLD                    U0004
N04  C                   KFLD                    U0010

      * Datafelt AB i EDIRE (80 lang) går rett inn i array AB ved READ
      * TN=81 soerger for automatisk overflyt array AB->AA ved kall av GETCH
     C                   MOVE      '0'           *IN91
     C                   MOVE      '0'           *IN98
     C                   MOVE      '0'           *IN99
     C                   EVAL      UMBR=WMBR
     C     WMBR          SETLL     EDIRE
     C     WMBR          READE     EDIRE                                  91
     C     *IN91         DOWEQ     '0'
     C     'UNB+'        SCAN      RDATA         TN                       20
     C  N20WMBR          READE     EDIRE                                  91
     C   91              GOTO      ENDINI
     C  N20              ENDDO

     C                   MOVE      RDATA         ABDS
     C     TN            IFGT      1
     C                   SUB       1             TN
     C                   ELSE
     C                   Z-ADD     81            TN
     C                   ENDIF
     C                   BITON     '17'          TEGN
     C                   BITOFF    '023456'      TEGN
     C                   MOVE      'NO '         EOF
     C                   MOVE      ' '           LOKAHE
N06  C                   EVAL      ULF=X'25'

      * Is UNA - servicestring included ?
     C                   MOVEL     RDATA         WUNA              3
     C     WUNA          IFNE      'UNB'
     C                   MOVE      AB(1)         UNA1
     C                   MOVE      AB(2)         UNA2
     C                   MOVE      AB(3)         UNA3
     C                   MOVE      AB(4)         UNA4
     C                   MOVE      AB(5)         UNA5
     C                   MOVE      AB(6)         UNA6
     C                   ENDIF

      * Spesielt reserverte tegn defineres her med både grafisk verdi og
      * med en logisk verdi. Den grafiske verdien testes det mot i rutinen
      * som henter tegn. Finner denne rutinen at at tegnet er ment som en
      * spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
      * (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
      * innkommende datastroemmen). '?' når brukt som opphevelse foran annet
      * spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
      * kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.

      * Ved andre standarder for spesialtegn er det bare å bytte verdiene til
      * de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      * Logiske verdier for spesialtegn
      * LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
      * HVIS KOMMUNEDATA, INITIERE MED LOGAPP (GÅ RETT PÅ UNB)
     C                   MOVE      LOGAPP        TEGN
      * LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   BITON     '16'          LOGPLU            1
     C                   BITOFF    '023457'      LOGPLU
      * LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   BITON     '167'         LOGKOL            1
     C                   BITOFF    '02345'       LOGKOL
      * LOGBLA = utenlandske måten å vise blank.
     C                   BITON     '01234567'    LOGBLA            1

     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste tegn i datastroem             GETCH  /
      *////////////////////////////////////////////////////////////
     C     GETCH         BEGSR
      * Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
      * i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
      * 91 settes når en ved "Toemming" finner at AB-arrayet er tomt.

      * "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
      *Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
      *    98 ON -> kun grafisk betydning (uforandret)
      *    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   MOVE      '0'           *IN98
     C     STACH         TAG
      * Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            91
     C                   GOTO      ENDCH
     C                   ENDIF
     C                   MOVE      ABDS          AADS
     C  N99WMBR          READE     EDIRE                                  99
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF
     C                   Z-ADD     1             TN
     C                   ENDIF
      * Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      80
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   ENDIF
      * Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
      * Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
      *        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
      * Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
      * foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   ELSE
     C     TEGN          IFEQ      LOGBLA
     C                   MOVE      ' '           TEGN
     C                   END
     C                   END
     C                   END
     C                   END

     C     ENDCH         ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste Segment                       GETSEG /
      *////////////////////////////////////////////////////////////
     C     GETSEG        BEGSR
      * "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   END
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
N06  C     TEGN          OREQ      ULF
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
      * Har nå lest appostrof - de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          SEGNAM
      * La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
      * Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
      *////////////////////////////////////////////////////////////
      * Get Data Element                                   GETDED /
      *////////////////////////////////////////////////////////////
     C     GETDED        BEGSR
     C                   MOVEA     *BLANKS       FE
     C     1             DO        256           NR                3 0
     C                   EXSR      GETCH
     C     TEGN          IFEQ      LOGAPP
     C     TEGN          OREQ      LOGPLU
     C     TEGN          OREQ      LOGKOL
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C     ENDDED        ENDSR
      *////////////////////////////////////////////////////////////
      * Call externel pgm depending on EDIfact-lvl         CVTLVL /
      *////////////////////////////////////////////////////////////
     C     CVTLVL        BEGSR
     C                   MOVEL     'E082R_'      WPGM              9
     C                   MOVE      U0054         WPGM
     C                   OUT       LDADTA
     C                   MOVE      *IN91         WIN91
     C                   MOVE      *IN99         WIN99
     C                   CALL      WPGM                                 20
     C                   PARM                    WIN91
     C                   PARM                    WIN99
     C                   MOVE      WIN91         *IN91
     C                   MOVE      WIN99         *IN99
     C                   IN        LDADTA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Call externel user job depending rcd in ECMSG      USRJOB /
      *////////////////////////////////////////////////////////////
     C     USRJOB        BEGSR
     C     ECMKEY        CHAIN     ECMSG1                             20
     C     *IN20         IFEQ      '0'
     C     ECLIB         ANDNE     *BLANKS
     C     ECPGM         IFEQ      *BLANKS
     C     ECDTAQ        ANDEQ     *BLANKS
     C                   GOTO      USREND
     C                   ENDIF

     C                   MOVE      UMN           UMN2              9
      *_________
      * VIA JOBQ
      *"""""""""
     C     ECPGM         IFNE      *BLANKS
     C                   MOVEL(P)  'SBMJOB'      QCMDR           128
     C                   CAT       ' CMD(C':0    QCMDR
     C                   CAT       'ALL PG':0    QCMDR
     C                   CAT       'M(':0        QCMDR
     C                   CAT       ECLIB:0       QCMDR
     C                   CAT       '/':0         QCMDR
     C                   CAT       ECPGM:0       QCMDR
     C                   CAT       ') PARM':0    QCMDR
     C                   CAT       '(':0         QCMDR
     C                   CAT       GRAAPP:0      QCMDR
     C                   CAT       UMN2:0        QCMDR
     C                   CAT       GRAAPP:0      QCMDR
     C                   CAT       '))':0        QCMDR
     C                   CAT       ' JOBD(':0    QCMDR
     C                   CAT       'SY400J':0    QCMDR
     C                   CAT       'OBD) J':0    QCMDR
     C                   CAT       'OBQ(SY':0    QCMDR
     C                   CAT       '400QER)':0   QCMDR
     C                   Z-ADD     128           QCMDLE           15 5
     C                   CALL      'QCMDEXC'                            3131
     C                   PARM                    QCMDR
     C                   PARM                    QCMDLE
      *__________
      * VIA DATAQ
      *""""""""""
     C                   ELSE
      *
     C                   Z-ADD     9             QRLEN             5 0
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    ECDTAQ
     C                   PARM                    ECLIB
     C                   PARM                    QRLEN
     C                   PARM                    UMN2
     C                   ENDIF
      *
     C                   ENDIF
     C     USREND        ENDSR
ELARM *////////////////////////////////////////////////////////////
ELARM * Call externel user job depending rcd in ECMSG     USRJOB2 /
ELARM *////////////////////////////////////////////////////////////
ELARMC     USRJOB2       BEGSR
ELARMC     ECMKEY        CHAIN     ECMSG1                             20
ELARMC     *IN20         IFEQ      '0'
ELARMC     ECLIB         ANDNE     *BLANKS
ELARMC     ECPGM         IFEQ      *BLANKS
ELARMC     ECDTAQ        ANDEQ     *BLANKS
ELARMC                   GOTO      USREND2
ELARMC                   ENDIF
ELARM *
ELARMC                   MOVE      UIN           UMN2              9
ELARM *_________
ELARM * VIA JOBQ
ELARM *"""""""""
ELARMC     ECPGM         IFNE      *BLANKS
ELARMC                   MOVEL(P)  'SBMJOB'      QCMDR           128
ELARMC                   CAT       ' CMD(C':0    QCMDR
ELARMC                   CAT       'ALL PG':0    QCMDR
ELARMC                   CAT       'M(':0        QCMDR
ELARMC                   CAT       ECLIB:0       QCMDR
ELARMC                   CAT       '/':0         QCMDR
ELARMC                   CAT       ECPGM:0       QCMDR
ELARMC                   CAT       ') PARM':0    QCMDR
ELARMC                   CAT       '(':0         QCMDR
ELARMC                   CAT       GRAAPP:0      QCMDR
ELARMC                   CAT       UMN2:0        QCMDR
ELARMC                   CAT       GRAAPP:0      QCMDR
ELARMC                   CAT       '))':0        QCMDR
ELARMC                   CAT       ' JOBD(':0    QCMDR
ELARMC                   CAT       'SY400J':0    QCMDR
ELARMC                   CAT       'OBD) J':0    QCMDR
ELARMC                   CAT       'OBQ(SY':0    QCMDR
ELARMC                   CAT       '400QER)':0   QCMDR
ELARMC                   Z-ADD     128           QCMDLE           15 5
ELARMC                   CALL      'QCMDEXC'                            3131
ELARMC                   PARM                    QCMDR
ELARMC                   PARM                    QCMDLE
ELARM *__________
ELARM * VIA DATAQ
ELARM *""""""""""
ELARMC                   ELSE
ELARM *
ELARMC                   Z-ADD     9             QRLEN             5 0
ELARMC                   CALL      'QSNDDTAQ'
ELARMC                   PARM                    ECDTAQ
ELARMC                   PARM                    ECLIB
ELARMC                   PARM                    QRLEN
ELARMC                   PARM                    UMN2
ELARMC                   ENDIF
ELARM *
ELARMC                   ENDIF
ELARMC     USREND2       ENDSR
      *////////////////////////////////////////////////////////////
      * SR for Write av EUNB / EDIS                               /
      *////////////////////////////////////////////////////////////
     C     UNBWRI        BEGSR
     C                   Z-ADD     WIN           UIN
     C                   WRITE     UNBR
      *_____________________
      * WRITE RECORD TO EDIS
      *"""""""""""""""""""""
     C                   MOVEL     U0004         S0004
     C                   MOVEL     U0010         S0010
     C                   MOVEL     U0020A        S0020
     C                   MOVEL     U0026         S0026
     C                   MOVEL     U0035         S0035
      *
     C                   MOVEA     U0036         NU
     C                   MOVE      *ZEROS        NUMVRD
     C     1             DO        6             NR                                         '+'
     C     NU(NR)        IFLT      '0'
     C     NU(NR)        ORGT      '9'
     C                   GOTO      UNZ001
     C                   END
     C                   MULT      10            NUMVRD
     C                   MOVE      NU(NR)        NUTEGN
     C                   ADD       NUTEGN        NUMVRD
     C                   END
     C     UNZ001        TAG
N01   *Duplikatkontroll
N05  C                   EVAL      *IN56=*ON
N05  C     U0004         CHAIN     EDII                               57
N05  C     *IN57         IFEQ      *OFF
N05  C     INDUPCTL      IFEQ      'J'
N05  C     INDUPCTL      OREQ      'Y'
N01  C     KEYEDIS6      CHAIN     EDIS6                              56
N05  C                   ENDIF
N05  C                   ENDIF
     C                   Z-ADD     NUMVRD        S0036
      *
     C                   Z-ADD     UIN           SSN
     C                   MOVE      'R'           SSR
S01  C*                  MOVE      ' '           SST
N01  C   56              MOVE      ' '           SST
N01  C  N56              MOVE      'K'           SST
     C                   MOVEL     WDATO         SDT
     C                   MOVEL     WTID          STM
     C                   MOVEL     WLIB          SLIB
     C                   MOVEL     WFIL          SFILE
     C                   MOVEL     WMBR          SMBR
     C     WNUMR         MULT      80            SMBRS
     C                   WRITE     EDISR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNZ                                           UNZ  /
      *////////////////////////////////////////////////////////////
      * TIDLIGERE EKSTERN RUTINE TATT INN - write til EDIS flyttet
     C     UNZ           BEGSR
     c     UIN           chain     eunb                               55
      *_______________________________
      * 0036 INTERCHANGE CONTROL COUNT
      *"""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          U0036
     C                   MOVEA     U0036         NU
     C                   MOVE      *ZEROS        NUMVRD
     C     1             DO        6             NR                                         '+'
     C     NU(NR)        IFLT      '0'
     C     NU(NR)        ORGT      '9'
     C                   GOTO      UNZ009
     C                   END
     C                   MULT      10            NUMVRD
     C                   MOVE      NU(NR)        NUTEGN
     C                   ADD       NUTEGN        NUMVRD
     C                   END
     C     UNZ009        TAG
     C                   Z-ADD     NUMVRD        S0036
      *
      *___________________________________
      * 0020 INTERCHANGE CONTROL REFERENCE
      *"""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          U0020B
      *_____________________
      * WRITE RECORD TO UNBF
      *"""""""""""""""""""""
     C   55              WRITE     UNBR
     C  N55              UPDATE    UNBR
      *________________________________
      * Oppdater status på EDIS
      *""""""""""""""""""""""""""""""""
     c     UIN           chain     edis                               55
     C                   Z-ADD     NUMVRD        S0036
S01  C*                  MOVE      'C'           SST
N01  C   56              MOVE      'C'           SST
N01  C  N56              MOVE      'K'           SST
     C   55              WRITE     EDISR
     C  N55              UPDATE    EDISR
      *________________________________
      * IS THERE ONE MORE UNB-SEGMENT ?
      *""""""""""""""""""""""""""""""""
     C     *IN91         IFEQ      '0'
     C                   MOVEA     '00'          *IN(20)
     C     'UNB+'        SCAN      AADS          TN                       20
     C  N20'UNB+'        SCAN      ABDS          TN                       21
     C   21              MOVE      ABDS          AADS
      *
     C     *IN20         IFEQ      '1'
     C     *IN21         OREQ      '1'
     C                   SUB       1             TN
     C     TN            IFLE      1
     C                   Z-ADD     81            TN
     C                   MOVE      AADS          ABDS
     C                   ENDIF
     C                   BITON     '17'          TEGN
     C                   BITOFF    '023456'      TEGN
     C                   ELSE
     C                   Z-ADD     81            TN
     C                   ENDIF
     C                   ENDIF
      *
     C     UNZEND        ENDSR
      /COPY QEDSRC,BGM_I
      /COPY QEDSRC,UNB_I
      /COPY QEDSRC,UNH_I
      /COPY QEDSRC,UNT_I
      *COPY QEDSRC,UNZ_I
