      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT ANTALL BRUKTE LAGERPOS. web  ***
      ***********************************************************
      *
     FBRPARF    IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FLLAGJL09  UF   E           K DISK
     FLLAGJD01  IF   E           K DISK
     FLLVARE01  IF   E           K DISK
     FLLMRF     IF   E           K DISK
     FLLW043    UF A E           K DISK    USROPN
     D VE              S              5    DIM(500)
     D LEV             S             17    DIM(500)
     D                 DS
     D  PAVRDA                 1     50
     D  VV                     1     50    DIM(10)
     D                 DS
     D  WSDT                   1      8  0
     D  WSDTÅ                  1      4  0
     D  WSDTM                  5      6  0
     D  WSDTD                  7      8  0
      *
     C                   EXSR      INIT
      *
     C                   MOVE      *BLANKS       VE
     C                   IF        UVE = '*ALL'
     C                   EXSR      HENTVE
     C                   ELSE
     C                   MOVE      UVE           VE(1)
     C                   END
      *
     C                   MOVE      *BLANKS       LEV
     C                   IF        ULEV = '*ALL'
     C                   EXSR      HENTLEV
     C                   ELSE
     C                   MOVE      ULEV          LEV(1)
     C                   END
      *
     C                   EXSR      LESPOS
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENTVE        BEGSR
     C***********************************************
     C                   MOVE      UUSER         PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   EVAL      PAID = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      UFIRM         PABRID
     C                   MOVE      UPNR          PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   END
     C                   END
      *
     C                   MOVE      *ZEROS        XN                3 0
     C                   IF        *IN33
     C     KUFASTK       SETLL     KUFAST
     C     KUFASTK       READE     KUFAST                                 33
     C  N33              IF        KFKOD = ' DEFN'
     C     KUFASTK       READE     KUFAST                                 33
     C                   END
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVE      KFKOD         VE(XN)
     C     KUFASTK       READE     KUFAST                                 33
     C                   ENDDO
      *
     C                   ELSE
     C                   MOVEA     VV            VE
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENTLEV       BEGSR
     C***********************************************
     C                   MOVE      UUSER         PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   EVAL      PAID = 'LLLEV'
     C     BRPARFK       CHAIN     BRPARF                             40
      *
     C                   MOVE      *ZEROS        XN                3 0
     C                   IF        *IN40
     C                   EVAL      LEV(1) = '*ALL'
      *
     C                   ELSE
      *
     C                   DOW       *IN40 = *OFF
     C                   ADD       1             XN
     C                   MOVEL     PAVRDA        LEV(XN)
     C     BRPARFK       READE     BRPARF                                 40
     C  N40              ENDDO
      *
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESPOS        BEGSR
     C***********************************************
     C                   OPEN      LLW043
      * HENT ALLE POSTER MED OPPTELLINGSDATO >= INNTASTET DATO
     C                   MOVE      WSDT          YDT
     C     LAGJ9K        SETLL     LLAGJL09
     C     LAG92K        READE     LLAGJL09                               33
     C     *IN33         DOWEQ     '0'
     C     UANR          IFEQ      *BLANKS
     C     UANR          OREQ      VAANR
     C     VADATO        IFLE      WSDT
     C                   EXSR      SUMKLL
     C                   END
     C                   END
     C     LAG92K        READE     LLAGJL09                               33
     C  N33              ENDDO
      *
      * LES ALLE POSTER MED OPPTELLINGSDATO = 0
     C                   MOVE      *ZEROS        YDT
     C     LAGJ9K        SETLL     LLAGJL09
     C     LAGJ9K        READE     LLAGJL09                               33
     C     *IN33         DOWEQ     '0'
     C     UANR          IFEQ      *BLANKS
     C     UANR          OREQ      VAANR
     C     VADATO        IFLE      WSDT
     C                   EXSR      SUMKLL
     C                   END
     C                   END
     C     LAGJ9K        READE     LLAGJL09                               33
     C  N33              ENDDO
      *
     C                   CLOSE     LLW043
      *
     C                   ENDSR
      *
     C***********************************************
     C     SUMKLL        BEGSR
     C***********************************************
      *
     C     LLVARE01K     CHAIN     LLVARE01                           33
     C     1             DO        500           XN
     C                   IF        VE(XN) = *BLANKS
     C                   GOTO      SLSK
     C                   ELSE
     C     VE(XN)        CABEQ     LVEIER        STOPD1
     C                   END
     C                   ENDDO
      *
     C     STOPD1        TAG
     C                   IF        LEV(1) <> '*ALL'
     C     LLMRFK        CHAIN     LLMRF                              33
     C     1             DO        500           XN
     C                   IF        LEV(XN) = *BLANKS
     C                   GOTO      SLSK
     C                   ELSE
     C     LEV(XN)       CABEQ     MRLNR         STOPD2
     C                   END
     C                   ENDDO
     C                   END
      *
     C     STOPD2        TAG
     C                   MOVE      VASALD        XSALD
      * SUMMER ANTALL KOLLI PÅ UTLEVERTE
     C     LAGJDK        SETLL     LLAGJD01
     C     LAGJDK        READE     LLAGJD01                               34
     C     *IN34         DOWEQ     '0'
     C     VDLDAT        IFGE      WSDT
     C                   ADD       VDLANT        XSALD
     C                   END
     C     LAGJDK        READE     LLAGJD01                               34
     C  N34              ENDDO
      *
      * SALDO <= 0  HOPP OVER
     C     XSALD         IFLE      0
     C     YDT           IFEQ      0
     C                   GOTO      SLSK1
     C                   ELSE
     C                   EXCEPT    DUMAGJ
     C                   GOTO      SLSK
     C                   END
     C                   END
      *
      * SKRIV TIL ARBEIDSFIL
     C     LW043K        CHAIN     LLW043                             34
     C     *IN34         IFEQ      '1'
     C                   Z-ADD     1             L43ANT
     C                   MOVE      VALLOK        L43LOK
     C                   MOVE      WSGRUP        L43GRU
     C                   MOVE      WSKJED        L43KN
     C                   MOVE      VALLT         L43LLT
     C                   WRITE     LLW043R
     C                   ELSE
     C                   ADD       1             L43ANT
     C                   UPDATE    LLW043R
     C                   END
      *
      * OPPDATER OPPTELLINGSDATO PÅ FILEN
     C     SLSK1         TAG
     C     VASALD        IFEQ      0
     C                   MOVE      WSDT          VALTDT
     C                   EXCEPT    NYAGJ
     C                   ELSE
     C                   EXCEPT    DUMAGJ
     C                   END
      *
     C     SLSK          ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UUSER            10
     C                   PARM                    UFIRM             2
     C                   PARM                    UGRP              8
     C                   PARM                    UPNR              8
     C                   PARM                    UVE               5
     C                   PARM                    ULEV             17
     C                   PARM                    UANR             15
     C                   PARM                    UDATO             8
      *
     C     *LIKE         DEFINE    VASALD        XSALD
      *
     C     LAGJ9K        KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSKJED
     C                   KFLD                    YDT               8 0
     C     LAG92K        KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSKJED
     C     LAGJDK        KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSKJED
     C                   KFLD                    VAMN
     C     LW043K        KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSKJED
     C                   KFLD                    VALLOK
     C     LLVARE01K     KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    VAANR
     C     LLMRFK        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    VAMRNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     KUFASTK       klist
     C                   kfld                    KFTYP
      *
     C                   MOVEL     'VE'          KFTYP
     C                   MOVE      UPNR          KFTYP
      *
     C                   MOVE      'B'           WSOMRF            1
     C                   MOVE      UGRP          VAGRUP
     C                   MOVE      UPNR          VAKJED
      *
     C                   MOVE      UDATO         WSDT
     C                   MOVE      UGRP          WSGRUP            8 0
     C                   MOVE      UPNR          WSKJED            8 0
      *
     C                   ENDSR
      *
     OLLAGJR    E            DUMAGJ
     O          E            NYAGJ
     O                       VALTDT
