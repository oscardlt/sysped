********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SLUTT DATO
10XXX * 02 YBC PTF10 BEREGNINGSMÅTE PÅ HVER SATS
10262 * 03 YBC PTF10 TOLLSATS ER UTVIDERT TIL 7.2, INGEN MERKING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11020 * 04 YBC PTF11 FLERE NYE LAND OG REGIONER
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      HELP(01)
     A                                      CF01(01)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  4VALUES(' ' '1' '2' '3' '4' '9')
     A            TATANR         8Y 0O  5 10
     A  31                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            TAORDB         7Y 2O  5 21EDTCDE(4)
     A            TAORDK         1A     5 30
     A            TAEFTB         7Y 2O  5 33EDTCDE(4)
     A            TAEFTK         1A     5 42
     A            TAEFB          7Y 2O  5 45EDTCDE(4)
     A            TAEFK          1A     5 54
     A            TASTK          1A  O  5 57
     A            TAKAP          1A  O  5 61
     A            TAALFA        15A  O  5 64
     A N31                                  COLOR(WHT)
     A            TAENHE         3A  O  5 81
     A            WXDATO         6Y 0O  6  1DSPATR(UL)
     A                                      EDTCDE(4)
     A            WXDTR          6Y 0H
S01  A*           TATXT2        72A  O  6  8DSPATR(UL)
N01  A            WXDTS          6Y 0O  6  8DSPATR(UL)
N01  A                                      EDTCDE(4)
     A            TATXT         80A  O  6 15DSPATR(UL)
     A            TADATO         8S 0H
N01  A            TADTS          8S 0H
     A            TADTR          8S 0H
     A            TAEØSB         7Y 2H
     A            TAEØSK         1A  H
     A            TATSJB         7Y 2H
     A            TATSJK         1A  H
     A            TATYRB         7Y 2H
     A            TATYRK         1A  H
     A            TAISRB         7Y 2H
     A            TAISRK         1A  H
     A            TAELLB         7Y 2H
     A            TAELLK         1A  H
     A            TABULB         7Y 2H
     A            TABULK         1A  H
     A            TAPOLB         7Y 2H
     A            TAPOLK         1A  H
     A            TAROMB         7Y 2H
     A            TAROMK         1A  H
     A            TAUNGB         7Y 2H
     A            TAUNGK         1A  H
     A            TASLOB         7Y 2H
     A            TASLOK         1A  H
     A            TAMULB         7Y 2H
     A            TAMULK         1A  H
     A            TAOULB         7Y 2H
     A            TAOULK         1A  H
     A            TAGRLB         7Y 2H
     A            TAGRLK         1A  H
     A            TAFÆRB         7Y 2H
     A            TAFÆRK         1A  H
     A            TAISTB         7Y 2H
     A            TAISTK         1A  H
     A            TAMARB         7Y 2H
     A            TAMARK         1A  H
     A            TAMEXB         7Y 2H
     A            TAMEXK         1A  H
     A            TAVGAB         7Y 2H
     A            TAVGAK         1A  H
     A            TAN01B         7Y 2H
     A            TAN01K         1A  H
     A            TAN02B         7Y 2H
     A            TAN02K         1A  H
     A            TAN03B         7Y 2H
     A            TAN03K         1A  H
     A            TAN04B         7Y 2H
     A            TAN04K         1A  H
     A            TAN05B         7Y 2H
     A            TAN05K         1A  H
     A            TAN06B         7Y 2H
     A            TAN06K         1A  H
     A            TAN07B         7Y 2H
     A            TAN07K         1A  H
     A            TAN08B         7Y 2H
     A            TAN08K         1A  H
     A            TAN09B         7Y 2H
     A            TAN09K         1A  H
     A            TAN10B         7Y 2H
     A            TAN10K         1A  H
N04  A            TAN11B         7Y 2H
N04  A            TAN11K         1A  H
N04  A            TAN12B         7Y 2H
N04  A            TAN12K         1A  H
N04  A            TAN13B         7Y 2H
N04  A            TAN13K         1A  H
N04  A            TAN14B         7Y 2H
N04  A            TAN14K         1A  H
N04  A            TAN15B         7Y 2H
N04  A            TAN15K         1A  H
N04  A            TAN16B         7Y 2H
N04  A            TAN16K         1A  H
N04  A            TAN17B         7Y 2H
N04  A            TAN17K         1A  H
N04  A            TAN18B         7Y 2H
N04  A            TAN18K         1A  H
N04  A            TAN19B         7Y 2H
N04  A            TAN19K         1A  H
N04  A            TAN20B         7Y 2H
N04  A            TAN20K         1A  H
     A            TAKDAÆ         2A  H
     A            TAKDSÆ         3A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD010'
     A                                      COLOR(BLU)
     A N27                              1 14MSGCON(036 DIV1771 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 14'Plukking/Vedlikehold av tolltariff'
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(035 YBC1810 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34'1=Plukk   2=Endre'
     A                                      COLOR(BLU)
     A                                  3 71'9=TARIC'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(100 YBC1986 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A  27                                  CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              5  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              5  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(16)
     A                                      CLRL(12)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 10MSGCON(100 YBC1987 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTANR         8Y 0B  2 10DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSORDB         7Y 2B  2 21DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSORDK         1A  B  2 30DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSEFTB         7Y 2B  2 33DSPATR(UL)
     A* 87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSEFTK         1A  B  2 42DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSEFB          7Y 2B  2 45DSPATR(UL)
     A* 86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSEFK          1A  B  2 54DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSSTK          1A  B  2 57DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKAP          1A  B  2 55DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSALFA        15A  B  2 64DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSENHE         3A  B  2 81DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            WSTXT         80A  B  3  1DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
S04  A*                                 4  2'EØS         Kroatia     Tyrkia    -
N04  A                                  4  2'EØS         Swaziland   Tyrkia    -
     A                                        Israel      Singapore   Chile    -
N04  A                                         Makedonia   Sør-Korea   Serbia  -
N04  A                                          Albania     Ukraina'
S04  A*                                        Makedonia   Sør-Korea'
     A                                      DSPATR(HI)
     A            WSEØSB         7Y 2B  5  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSEØSK         1A  B  5 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSTSJB         7Y 2B  5 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTSJK         1A  B  5 23DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSTYRB         7Y 2B  5 26DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTYRK         1A  B  5 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSISRB         7Y 2B  5 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSISRK         1A  B  5 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSELLB         7Y 2B  5 50DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSELLK         1A  B  5 59DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSBULB         7Y 2B  5 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSBULK         1A  B  5 71DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSPOLB         7Y 2B  5 74DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPOLK         1A  B  5 83DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSROMB         7Y 2B  5 86DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSROMK         1A  B  5 95DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN05B         7Y 2B  5 98DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN05K         1A  B  5107DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN06B         7Y 2B  5110DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN06K         1A  B  5119DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN07B         7Y 2B  5122DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN07K         1A  B  5131DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A                                  6  2'Jordan      Tunisia     Min.uland -
     A                                        Ord.uland   Grønnland   Færøyen  -
N04  A                                         IndiaSrTh   Marokka     Peru    -
N04  A                                          Montenegro  Hong Kong'
S04  A*                                        IndiaSrTh   Marokka '
     A                                      DSPATR(HI)
     A            WSUNGB         7Y 2B  7  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSUNGK         1A  B  7 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSSLOB         7Y 2B  7 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSSLOK         1A  B  7 23DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSMULB         7Y 2B  7 26DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSMULK         1A  B  7 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSOULB         7Y 2B  7 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSOULK         1A  B  7 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSGRLB         7Y 2B  7 50DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSGRLK         1A  B  7 59DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSFÆRB         7Y 2B  7 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSFÆRK         1A  B  7 71DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSISTB         7Y 2B  7 74DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSISTK         1A  B  7 83DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSMARB         7Y 2B  7 86DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSMARK         1A  B  7 95DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN08B         7Y 2B  7 98DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN08K         1A  B  7107DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN09B         7Y 2B  7110DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN09K         1A  B  7119DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN10B         7Y 2B  7122DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN10K         1A  B  7131DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A                                  8  2'Mexico      VB Gaza     Libanon   -
     A                                        SACU        Egypt       Canada   -
N04  A                                         BW, NA, SZ  Lavere MIL  Reserver-
N04  A                                          Reserver    Reserver'
S04  A*                                               '
     A                                      DSPATR(HI)
     A            WSMEXB         7Y 2B  9  2DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSMEXK         1A  B  9 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSVGAB         7Y 2B  9 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVGAK         1A  B  9 23DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSN01B         7Y 2B  9 26DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSN01K         1A  B  9 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSN02B         7Y 2B  9 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSN02K         1A  B  9 47DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSN03B         7Y 2B  9 50DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSN03K         1A  B  9 59DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A            WSN04B         7Y 2B  9 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSN04K         1A  B  9 71DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN11B         7Y 2B  9 74DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN11K         1A  B  9 83DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN12B         7Y 2B  9 86DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN12K         1A  B  9 95DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN13B         7Y 2B  9 98DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN13K         1A  B  9107DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN14B         7Y 2B  9110DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN14K         1A  B  9119DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
N04  A            WSN15B         7Y 2B  9122DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
N04  A            WSN15K         1A  B  9131DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES(' ' 'A' 'F' 'P' 'V' 'W')
     A                                 10  5MSGCON(015 DIV1774 *LIBL/SYMSGF)
     A            WSREST         2A  B 10 21DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 25MSGCON(015 DIV1775 *LIBL/SYMSGF)
     A            WSKAPA         4A  B 10 41DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 47'Opd.DT:'
     A            WSDTR          6Y 0O 10 55DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 63'F.o.m DT:'
     A            WSDATO         6Y 0B 10 73DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  83                                  DSPATR(RI)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 11 63'T.o.m DT:'
     A            WSDTS          6Y 0B 11 73DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  30        MSGTXT        78A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1 10MSGCON(070 YBC1987 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TATANR         8Y 0O  2 10DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A            TAORDB         7Y 2O  2 21DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A            TAORDK         1A  O  2 30DSPATR(UL)
     A                                      COLOR(RED)
     A            TAEFTB         7Y 2O  2 33DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A            TAEFTK         1A  O  2 42DSPATR(UL)
     A                                      COLOR(RED)
     A            TAEFB          7Y 2O  2 45DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A            TAEFK          1A  O  2 54DSPATR(UL)
     A                                      COLOR(RED)
     A            TASTK          1A  O  2 57DSPATR(UL)
     A                                      COLOR(RED)
     A            TAKAP          1A  O  2 61DSPATR(UL)
     A                                      COLOR(RED)
     A            TAALFA        15A  O  2 64DSPATR(UL)
     A                                      COLOR(RED)
     A            TAENHE         3A  O  2 81DSPATR(UL)
     A                                      COLOR(RED)
     A            TATXT         80A  O  3  1DSPATR(UL)
     A                                      COLOR(RED)
     A                                  4  5MSGCON(015 DIV1774 *LIBL/SYMSGF)
     A            TAREST         2A  O  4 21DSPATR(UL)
     A                                      COLOR(RED)
     A                                  4 25MSGCON(015 DIV1775 *LIBL/SYMSGF)
     A            TAKAPA         4A  O  4 41DSPATR(UL)
     A                                      COLOR(RED)
     A                                  4 47'Opd.DT:'
     A            WXDTR          6Y 0O  4 55DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4 63'F.o.m DT:'
     A            WXDATO         6Y 0O  4 73DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 63'T.o.m DT:'
     A            WXDTS          6Y 0O  5 73DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
      *
     A          R NYWRK
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A  27                                  CF12(12 'Forrige')
     A N27                              1 14MSGCON(36 DIV1771 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 14'Plukking/Vedlikehold av tolltariff'
     A                                      COLOR(WHT)
     A                                  1  2'SAD010'
     A                                      COLOR(BLU)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(35 YBC1810 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34'1=Plukk   2=Endre'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1986 SYMSGF)
     A                                      COLOR(WHT)
     A N27                             26  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                             26  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SOKTAR
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  2  2'Søk fra tariffnr.  --->'
     A            W2TANR         8Y 0B  2 26TEXT('TARIFFNR.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4  2'Søk fra alfabet    --->'
     A            W2ALFA        15A  I  4 26TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
