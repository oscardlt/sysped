      ***********************************************************
      *** PROGRAM skal finne hvilken ukedag en gitt dato er.  ***
      *** (det finnes faktisk ikke funksjon for det i RPG ILE,
      *** men ved å ta utgangspunkt i kjent dato og regne hele
      *** uker + rest derfra så er det enkelt å finne...)
      * Inn-parameter: Dato
      * Ret-parameter: Ukedag - 0=Sø,1=Ma,2=Ti,3=On,4=To,5=Fr,6=Lø
      ***********************************************************
      *
     DKnownDate        S               D   datfmt(*iso)
     DThisDate         S               D   datfmt(*iso)
     D                 DS
     DDato                     1      8  0
     DAAR                      1      4  0
     DMND                      5      6  0
     DDAG                      7      8  0
     D                 DS
     DDatoi                    1     10
     Daari                     1      4
     Dhyf1                     5      5
     Dmndi                     6      7
     Dhyf2                     8      8
     Ddagi                     9     10
     c     *entry        plist
     c                   parm                    Dat               8 0
     c                   parm                    Ukedag            1 0
     c                   move      Dat           Dato
      *
      * Initier utgangspkt med kjent dato (Søndag 31.12.1899)
     c                   eval      KnownDate = D'1899-12-31'
      * gjør om inndato til ISO
      *
     c                   move      aar           aari
     c                   move      '-'           hyf1
     c                   move      mnd           mndi
     c                   move      '-'           hyf2
     c                   move      dag           dagi
     c                   move      Datoi         ThisDate
      *
      * Antall dager diff...
     c     ThisDate      subdur    KnownDate     Work:*D           5 0
      * Antall hele uker ...
     c                   div       7             Work
      * Resten er ukedagen..
     c                   MVR                     Ukedag
      *
     c                   seton                                        lr
     c                   return
