********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NYTT PARAMETER TIL GSS049C
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 IKKE LOV Å ENDRE LINJE MED STATUS=F
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED FAKTURABASIS               ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-11 13-24             ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FGSSOVF    UF A E           K DISK
     FGSSOVL01  IF   E           K DISK
     F                                     RENAME(GSSOVFR:GSSOVFR1)
     FGSSORF    IF   E           K DISK
     FGSSSFF    IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FGSS012D   CF   E             WORKSTN
     F                                     SFILE(GSS012DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SFNM                   1     30
     D  XSNM                   1     20
     D                 DS
     D  CBNAB                  1     30
     D  XBNM                   1     20
     D                UDS
     D  UFIRM                  1      2
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     GSS012DB
     C                   MOVEA     '00'          *IN(93)
     C                   MOVE      *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        YRECNO            4 0
     C                   MOVE      *ZEROS        XLNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UONO          SETLL     GSSOVL01
     C     START1        TAG
     C     UONO          READE     GSSOVL01                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        999
     C                   ADD       1             ZRECNO
     C     XLNO          IFLT      OVLNO
     C                   MOVE      OVLNO         XLNO
     C                   END
     C                   ADD       1             YRECNO
     C                   WRITE     GSS012DA
     C     UONO          READE     GSSOVL01                               94
     C  N94              END
     C     YRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   END
      *
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS012DG
     C                   ELSE
     C                   WRITE     GSS012DB
     C                   EXFMT     GSS012DC
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C                   MOVE      *ZEROS        UFLAGG
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      START5
     C                   END
      *** Autobergning
     C   07              DO
     C                   exsr      autosr
     C                   GOTO      START
     C                   END
      *** AVSLUTT
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      *
      *** BLA VIDERE
     C   25              DO
     C                   ADD       15            ZRECNO
     C     ZRECNO        IFGT      YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   END
     C                   SETOFF                                       25
     C                   GOTO      START3
     C                   END
      *** BLA TILBAKE
     C   26              DO
     C                   SUB       15            ZRECNO
     C     ZRECNO        IFLE      0
     C     YRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   ELSE
     C                   MOVE      *ZEROS        ZRECNO
     C                   END
     C                   END
     C                   SETOFF                                       26
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     GSS012DD
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     WSFEEC        COMP      *BLANKS                                90
     C   90              MOVE      'GSS0043'     MSGNR
     C   90              SETON                                        30
     C  N90KODGEK        CHAIN     KODTGE                             33
     C  N90
     CAN 33              DO
     C                   MOVE      'GSS0044'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     22            LINNBR
     C   90              Z-ADD     10            POSNBR
     C  N90WSFT          IFEQ      *BLANKS
     C                   MOVEL     KGEENT        WSFT
     C                   END
      *
     C     KODVAK        CHAIN     VALUL01                            89
     C   89
     CANN30              MOVE      'GSS0045'     MSGNR
     C   89
     CANN30              Z-ADD     22            LINNBR
     C   89
     CANN30              Z-ADD     18            POSNBR
     C   89              SETON                                        30
      *
     C     WSAMT         COMP      0                                    8888
     C   88
     CANN30              MOVE      'GSS0046'     MSGNR
     C   88
     CANN30              Z-ADD     22            LINNBR
     C   88
     CANN30              Z-ADD     27            POSNBR
     C   88              SETON                                        30
      *
     C   30              GOTO      SLTB1
     C     WSVAT         IFNE      'J'
     C                   MOVE      'N'           WSVAT
     C                   END
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C  N40              ADD       1             XLNO
     C  N40              MOVE      XLNO          OVLNO
     C     GOVKEY        CHAIN     GSSOVF                             33
     C  N33
     CANN40              UPDATE    GSSOVFR
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWOV
     C  N33              UPDATE    GSSOVFR
     C   33              WRITE     GSSOVFR
     C   40              UPDATE    GSS012DA
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     GSS012DA
     C                   WRITE     GSS012DB
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS012DA                               98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
S02  C*    WSST          ANDNE     'F'
N02  C     OVST          ANDNE     'F'
     C                   MOVE      ' '           WSNR
     C     OVFEEC        COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGOV
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
S02  C*    WSST          ANDNE     'F'
N02  C     OVST          ANDNE     'F'
     C                   MOVE      ' '           WSNR
     C     OVFEEC        COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTOV
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'GSS0009'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGOV         BEGSR
     C***********************************************
     C                   EXSR      MOVOVW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     GSS012DD
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    GSS012DA
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTOV         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     GSS012DF
     C  N12              DO
     C     GOVK2         CHAIN     GSSOVF                             33
     C  N33              DELETE    GSSOVFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    GSS012DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWOV        BEGSR
     C***********************************************
     C                   MOVE      UONO          OVONO
     C                   MOVE      WSFEEC        OVFEEC
     C                   MOVE      WSCURC        OVCURC
     C                   MOVE      WSAMT         OVAMT
     C                   MOVE      WSFT          OVFT
     C                   MOVE      WSVAT         OVVAT
     C                   MOVE      WSLC          OVLC
     C                   MOVE      WSST          OVST
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVOVW        BEGSR
     C***********************************************
     C                   MOVE      OVLNO         XLNO
     C                   MOVE      OVFEEC        WSFEEC
     C                   MOVE      OVCURC        WSCURC
     C                   MOVE      OVAMT         WSAMT
     C                   MOVE      OVFT          WSFT
     C                   MOVE      OVVAT         WSVAT
     C                   MOVE      OVLC          WSLC
     C                   MOVE      OVST          WSST
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        OVONO
     C                   MOVE      *ZEROS        OVLNO
     C                   MOVE      *BLANKS       OVFEEC
     C                   MOVE      '*DL'         OVFEEC
     C                   MOVE      *BLANKS       OVCURC
     C                   MOVE      *ZEROS        OVAMT
     C                   MOVE      *BLANKS       OVFT
     C                   MOVE      *BLANKS       OVVAT
     C                   MOVE      *BLANKS       OVLC
     C                   MOVE      *BLANKS       OVST
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG            1 0
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     10
     C     POSNBR        ANDLE     12
     C                   CALL      'SYFI10D'
     C                   PARM                    WSFEEC
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     20
     C                   MOVE      'J'           USØK              1
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    WSCURC
     C                   Z-ADD     27            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'GSS0013'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSFEEC
     C                   MOVE      *ZEROS        WSAMT
     C                   MOVE      *BLANKS       WSFT
     C                   MOVE      'N'           WSVAT
     C                   IF        ORLC = 'ONWAY'
     C                   MOVE      ORLC2         WSLC
     C                   ELSE
     C                   MOVE      ORLC          WSLC
     C                   END
     C                   MOVE      *BLANKS       WSST
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GOVKEY        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    XLNO
     C     *LIKE         DEFINE    OVLNO         XLNO
     C     GOVK2         KLIST
     C                   KFLD                    UONO
     C                   KFLD                    OVLNO
     C     KODGEK        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    WSFEEC
     C                   MOVE      'GE'          KGEUNI
     C     KODVAK        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSCURC
      *
     C     *ENTRY        PLIST
     C                   PARM                    UONO              7 0
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
     C     UONO          CHAIN     GSSORF                             27
     C     ORSNO         CHAIN     GSSSFF                             27
     C     ORBCD         CHAIN     CDBOAT                             27
     C     UTYPE         COMP      'DSP'                                  27
     C                   MOVE      'N'           WSVAT
     C                   IF        ORLC = 'ONWAY'
     C                   MOVE      ORLC2         WSLC
     C                   ELSE
     C                   MOVE      ORLC          WSLC
     C                   END
     C                   MOVE      *BLANKS       WSST
      *
     C                   ENDSR
      *
     C***********************************************
     C     AUTOSR        BEGSR
     C***********************************************
      *
     C                   CALL      'GSS049C'
     C                   PARM                    UONO              7 0
N01  C                   PARM                    UTNO             11 0
     C                   ENDSR
