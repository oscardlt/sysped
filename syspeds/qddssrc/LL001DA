     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 35 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2')
     A            LTLK           2A  O  5  5
     A            LTTANR        20A  O  5  8
     A            LTFYL          2A  O  5 29
     A            LTLKOP         2A  H
     A            LTTO          17A  H
     A            LTCRI          3A  H
     A            LTCRE          3A  H
     A            LTMOT          1A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL001RA'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med tariffnr'
     A                                      COLOR(WHT)
     A            UGRUP          8S 0H
     A            UPNR           8S 0H
     A                                  2  2'Art:'
     A            UANR          15A  O  2  7DSPATR(UL)
     A                                  3  2'Velg option:  2=Endre'
     A                                      COLOR(BLU)
     A                                  4  2'Op LK Tolltariffnr         FK'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Avsl F5=Frisk F6=Lage ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        33A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL001RA'
     A                                      COLOR(BLU)
     A                                  1 10'Aribed med tariffnr'
     A                                      COLOR(WHT)
     A            UGRUP          8  0H
     A            UPNR           8  0H
     A                                  2  2'Art:'
     A            UANR          15A     2  7DSPATR(UL)
     A                                  3  2'Velg option:  2=Endre'
     A                                      COLOR(BLU)
     A                                  4  2'Op LK Tolltariffnr         FK'
     A                                      COLOR(WHT)
     A                                 10  2'F3/F12=Avsl F5=Frisk F6=Lage ny'
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL001RA'
     A                                      COLOR(BLU)
     A                                  1 10'Vedlikehold tariffnr'
     A                                      COLOR(WHT)
     A                                  2  2'Art:'
     A            UANR          15A  O  2  7DSPATR(UL)
     A                                  3  2'Landkode:'
     A            WSLK           2A  B  3 13DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  40                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A                                  4  2'Tariffnr:'
     A            WSTANR        20A  B  4 13DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                  5  2'Fylkekod:'
     A            WSFYL          2A  B  5 13DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                  5 18'Motorvogn:'
     A            WSMOT          1A  B  5 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  5 31'(J/N)'
     A                                  6  2'Till.opl:'
     A            WSTO          17A  B  6 13DSPATR(UL)
     A                                  7  2'Ref.kd-E:'
     A            WSCRE          3A  B  7 13DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                  7 18'Ref.kd-I:'
     A            WSCRI          3A  B  7 28DSPATR(UL)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A                                  8  2'Oppr.LK :'
     A            WSLKOP         2A  B  8 13DSPATR(UL)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A                                  9  2'F4=Søk F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        33A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
