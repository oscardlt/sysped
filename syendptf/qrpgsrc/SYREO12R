     H DFTACTGRP(*NO)
     FHEADF     UF   E           K DISK
     FDIMM      UF   E           K DISK
     FDISP      UF   E           K DISK
     FCARGOF    UF   E           K DISK
     FCARGOO    UF   E           K DISK
     FENADSF    UF   E           K DISK
     FESOSF1    UF   E           K DISK
     FESOSF2    UF   E           K DISK
     FESOSF3    UF   E           K DISK
     FE170F     UF   E           K DISK
     FE171F     UF   E           K DISK
     FE307F     UF   E           K DISK
     FFAKT      UF   E           K DISK
     FFAIN      UF   E           K DISK
     FFAKTXT    UF   E           K DISK
     FFAXLOG    UF   E           K DISK
     FFERDF     UF   E           K DISK
     FFERFAKT   UF   E           K DISK
     FFORDEL    UF   E           K DISK
     FFREETUR   UF   E           K DISK
     FFREETXT   UF   E           K DISK
     FREIS      UF   E           K DISK
     FTILL      UF   E           K DISK
     FTRAVH     UF   E           K DISK
     FTRAVV     UF   E           K DISK
     FTURER     UF   E           K DISK
     FTUSTS     UF   E           K DISK
      *
     FEDIM      UF   E           K DISK
     FEDIS      UF   E           K DISK
     FEDISE     UF   E           K DISK
     FEDIRE     UF   E           K DISK
     FEDISE1K   UF   E           K DISK    RENAME(EDISER:EDISER1K)
     F                                     PREFIX(K_)
     FEDIRE1K   UF   E           K DISK    RENAME(EDIRER:EDIRER1K)
     F                                     PREFIX(K_)
     FEDISX     UF   E           K DISK
     FXMLRCVF   UF   E           K DISK
     FXMLRCAF   UF   E           K DISK
     FXMLRCLF   UF   E           K DISK
     FXMLINH    UF   E           K DISK
     FXMLUFF    UF   E           K DISK
     FXMLU2F    UF   E           K DISK
     FXMLULG    UF   E           K DISK
     FFPSTD1    UF   E           K DISK
     FFPXREF    UF   E           K DISK
     FGODSJF    UF   E           K DISK
     FGODSHF    UF   E           K DISK
     FFRISOK    UF   E           K DISK
     FTRACKF    UF   E           K DISK
     FTRACKK    UF   E           K DISK
     FTRACKT    UF   E           K DISK
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKIVL17  UF   E           K DISK
     FARKIVL19  UF   E           K DISK    RENAME(ARKIVR:ARKIVR19)

     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows0         s             10i 0
     d myrows          s             10i 0
     d myrows2         s             10i 0
     d myrows4         s             10i 0
     D XDTF            S              8  0
     D XDTP            S              8  0
     D XPOS            S              3  0
     D XMBR            S             10A
     D Error           S            150
     d X               S             10  0
     d X2              S             10  0
     d X4              S             10  0
     D ufil            S            256A
     D utyp            S              5A
     D XSLUTT          S              1
     D XSLUTT2         S              1
     D XSLUTT4         S              1
     D UOK             S              1
     D UOK2            S              1
      *
      *  Prototype for 'SYREO01C3'
     d SYREO01C3       pr                  extpgm('SYREO01C3')
     d ufil_                               like(ufil)
      *  Prototype for 'SYREO01R3'
     d SYREO01R3       pr                  extpgm('SYREO01R3')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d utyp_                               like(utyp)
     d uok_                                like(uok)
      *  Prototype for 'SYREO12R0'
     d SYREO12R0       pr                  extpgm('SYREO12R0')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d udtp_                               like(udtp)
     d uok_                                like(uok)
      *  Prototype for 'SYREO12C1'
     d SYREO12C1       pr                  extpgm('SYREO12C1')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d upro_                               like(upro)
      *  Prototype for 'SYREO12C2'
     d SYREO12C2       pr                  extpgm('SYREO12C2')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d upro_                               like(upro)
      *  Prototype for 'SYREO02R2'
     d syreo02r2       pr                  extpgm('SYREO02R2')
     d usn_                                like(usn)
     d udtp_                               like(udtp)
     d uok2_                               like(uok2)
      *  Prototype for 'SYREO12C3'
     d SYREO12C3       pr                  extpgm('SYREO12C3')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d upro_                               like(upro)
      *  Prototype for 'SYREO03R2'
     d syreo03r2       pr                  extpgm('SYREO03R2')
     d usn_                                like(usn)
     d udtp_                               like(udtp)
     d uok2_                               like(uok2)
      *  Prototype for 'SYREO12C4'
     d SYREO12C4       pr                  extpgm('SYREO12C4')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d upro_                               like(upro)
      *  Prototype for syreo12r
     d syreo12r        pr
     d udtf_                               like(udtf)
     d udtp_                               like(udtp)
      *  *ENTRY Interface for Main Procedure
     d syreo12r        pi
     d udtf                           8
     d udtp                           8
      *
     d MyPoDS0         ds                  Qualified  dim(1)
     d  FIFIRM                        2A
     d MyPoDS          ds                  Qualified  dim(10000)
     d  HEAVD                         4S 0
     d  HEOPD                         7S 0
     d  HESG                          3A
     d  HEDTOP                        8S 0
     d  HEGN                         15A
     d  HEPRO                         8S 0
     d MyPoDS2         ds                  Qualified  dim(10000)
     d  MAVD                          4S 0
     d  MTDN                          7S 0
     d  MSR                           1A
     d  MMN                           9S 0
     d  MSN                           9S 0
      *
     d                 ds
     d  HEAVD                  1      4s 0
     d  UAVD                   1      4a
     d                 ds
     d  HEOPD                  1      7s 0
     d  UOPD                   1      7a
     d                 ds
     d  HEPRO                  1      8s 0
     d  UPRO                   1      8a
     d                 ds
     d  usn_n                  1      9  0
     d  usn                    1      9
     d                 ds
     d  smbr                   1     10A
     d  smbr_1                 1      1A
     d  smbr_2                 2      8A
     d  smbr_3                 9     10A
     d  smbr_4                 4     10A
     d                 ds
     d  x_arunde               1     10
     d  x_arunde1              1      2
     d  x_arunde2              3     10  0
      /free

         exsr init;

         dow xslutt <> 'J';
           exsr srhoved;
         enddo;

         exec sql
         delete from xmlulg
         where xlgdt < :xdtf
         with NC;

       *inlr = *on;
       return;

       //=====================================================================
       // Hoved logikk
       //=====================================================================
       begsr srhoved;

       clear MyPoDS;
       SqlStm = 'Select HEAVD, HEOPD, HESG, HEDTOP, HEGN, HEPRO ' +
                'from HEADF ' +
                'Where HEAVD > 0 AND HEOPD > 0' +
                ' AND HEDTOP > 0 AND HEDTOP <= ' + %trim(udtf) +
                ' AND HEDTR > 0 AND HEDTR <= ' + %trim(udtf);
       Exec SQL PREPARE DynSel from :SqlStm;
       EXEC SQL DECLARE  A33  cursor for DynSel;
       Exec SQL OPEN A33;
       Myrows = %elem(MyPoDS);
       exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       Exec SQL CLOSE A33;

       if myrows > 0;
         for x = 1 to myrows;
           heavd = mypods(x).heavd;
           hesg  = mypods(x).hesg;
           if hesg = *blanks and heavd = 0;
             xslutt = 'J';
             leave;
           endif;

             heopd = mypods(x).heopd;
             hegn  = mypods(x).hegn;
             hepro = mypods(x).hepro;

             // Sjekk om fortolling skal bevares. I så fall sletter ikke arkiv og edi.
             syreo12r0 (uavd: uopd: udtp: uok);

             exsr sra;              // oppdrag, faktura, tur
             exsr srb;              // ship-sys, POMS, ,,,
             if uok = 'J';
               xslutt2 = 'N';
               dow xslutt2 <> 'J';
                 exsr src;            // EDI
               enddo;
             endif;
             exsr srd;              // CARGO, E170F, FAXLOG, ,,,
             exsr sre;              // GODSJF, GODSHF
             exsr srg;              // FPSTD1, FPXREF, TRACKK

           // slett hovedpost
             exec sql
             delete from HEADF
             where heavd = :heavd and heopd = :heopd
             with NC;

         endfor;
       endif;

       endsr;
      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM DIMM, DISP, FAKT, FAIN, FRATTXT,,,
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sra;

       exec sql
       delete from dimm
       where dimavd=:heavd and dimopd=:heopd
       with NC;

       exec sql
       delete from disp
       where diavd=:heavd and diopd=:heopd
       with NC;

       setll (heavd: heopd) fakt;
       reade (heavd: heopd) fakt;
       dow not %eof;
         if fafakt <> 0;
           exec sql
           delete from e170f
           where e170fn = :fafakt and e170ar = :faÅr
           with NC;
           exec sql
           delete from ferfakt
           where ferfak = :fafakt
           with NC;
         endif;
         if fakda <> 'K';
           exec sql
           delete from xmluff
           where xffn = :fafakt
           with NC;
           exec sql
           delete from xmlu2f
           where xffn = :fafakt
           with NC;
         endif;
         delete faktr;
         reade (faavd: faopd) fakt;
       enddo;

       exec sql
       delete from fain
       where fiavd=:heavd and fiopd=:heopd
       with NC;

       exec sql
       delete from faktxt
       where fxavd=:heavd and fxopd=:heopd
       with NC;

       exec sql
       delete from ferdf
       where feavd=:heavd and feopd=:heopd
       with NC;

       exec sql
       delete from fordel
       where foavd=:heavd and foopd=:heopd
       with NC;

       exec sql
       delete from freetxt
       where frtavd=:heavd and frtopd=:heopd
       with NC;

       if hepro <> 0;
         exec sql
         delete from reis
         where repro = :hepro
         with NC;

         exec sql
         delete from till
         where tipro = :hepro
         with NC;

         exec sql
         delete from travv
         where tvtunr = :hepro
         with NC;

         exec sql
         delete from travh
         where thtunr = :hepro
         with NC;

         exec sql
         delete from freetur
         where frttur=:hepro
         with NC;

         exec sql
         delete from tusts
         where sstur=:hepro
         with NC;

         // slett arkiv
         x_arunde2 = hepro;
         setll x_arunde arkivl19;
         reade x_arunde arkivl19;
         dow not %eof;
           chain (fifirm) firmarc;
           chain (artype) arktxt;
           if %found and arklag <> *blanks;
             chain (fifirm:arklag) arkext;
           endif;
           if arbhis <> *blanks;
           // Bane for historikk
             arcane = arbhis;
           endif;
           xpos = %scan ('.' : arlink : 1);
           if xpos = 0;
             ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
           else;
             ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
           endif;
           syreo01c3 (ufil);

           delete arkivr19;
           reade x_arunde arkivl19;
         enddo;

         exec sql
         delete from turer
         where tupro=:hepro
         with NC;
       endif;

       exec sql
       delete from frisok
       where fsavd = :heavd and fsopd = :heopd
       with NC;

       exec sql
       delete from trackf
       where ttavd = :heavd and ttopd = :heopd
       with NC;

       exec sql
       delete from trackt
       where txavd = :heavd and txopd = :heopd
       with NC;

       exec sql
       delete from xmlinh
       where xmavd = :heavd and xmopd = :heopd
       with NC;

       if uok = 'J';
         // Sjekk om oppdrag ligger i andre modul. Ved N sletter KUN oppdrag.
         syreo01r3 (uavd: uopd: utyp: uok);
         setll (heavd:heopd) arkivl17;
         reade (heavd:heopd) arkivl17;
         dow not %eof;
           if uok = 'J';
             chain (fifirm) firmarc;
             chain (artype) arktxt;
             if %found and arklag <> *blanks;
               chain (fifirm:arklag) arkext;
             endif;
             if arbhis <> *blanks;
             // Bane for historikk
               arcane = arbhis;
             endif;
             xpos = %scan ('.' : arlink : 1);
             if xpos = 0;
               ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
             else;
               ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
             endif;
             syreo01c3 (ufil);

             delete arkivr;
           else;
             update arkivr;
           endif;
           reade (heavd:heopd) arkivl17;
         enddo;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SHIP-SYS, POMS, ,,,
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srb;

       syreo12c1 (uavd: uopd: upro);   // SJØ-sys
       syreo12c2 (uavd: uopd: upro);   // DOXXXX
       syreo12c3 (uavd: uopd: upro);   // AXXXX
       syreo12c4 (uavd: uopd: upro);   // Andre

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIM/EDIS
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src;

       clear MyPoDS2;
       SqlStm = 'Select MAVD, MTDN, MSR, MMN, MSN ' +
                'from EDIM ' +
                'Where MAVD = ' + %trim(%editc(heavd:'Z')) +
                ' and MTDN = ' + %trim(%editc(heopd:'Z'));
       Exec SQL PREPARE DynSel2 from :SqlStm;
       EXEC SQL DECLARE  A34  cursor for DynSel2;
       Exec SQL OPEN A34;
       Myrows2 = %elem(MyPoDS2);
       exec sql FETCH A34 for :MyRows2 rows
                  INTO :MyPoDs2;
       Exec SQL CLOSE A34;

       if myrows2 > 0;
         for x2 = 1 to myrows2;
           if mypods2(x2).msr = *blanks;
             xslutt2 = 'J';
             leave;
           endif;

           mmn = mypods2(x2).mmn;
           msn = mypods2(x2).msn;

           exec sql
           delete from XMLRCVF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCAF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCLF
           where xrsn = :msn
           with NC;

           usn_n = msn;
           syreo02r2 (usn: udtp: uok2); //Test om samme fil inneholder SAD-imp til privatkunde
           if uok2 ='J';
             syreo03r2 (usn: udtp: uok2); //Test om samme fil inneholder SAD-exp til privatkunde
             if uok2 ='J';
               chain (msn) edis;
               if %found;
               // slett komponent
                 exsr src2;
                // slett fil i IFS
                 if sifs <> *blanks;
                   if %subst(sifs:1:1) <> '/';
                     sifs = '/' + %trim(sifs);
                   endif;
                   ufil = sifs;
                   syreo01c3 (ufil);
                 endif;
                 delete edisr;
               endif;
             endif;
           endif;

         // slett hovedpost
           exec sql
           delete from EDIM
           where mmn = :mmn
           with NC;

         endfor;
       endif;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIRE/EDISE/EDIRE1K/EDISE1K/XMLRCVF/AF/LF/EDISX
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src2;

       exec sql
       delete from edire
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise
       where sembr = :smbr
       with NC;

       exec sql
       delete from edire1k
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise1k
       where sembr = :smbr
       with NC;

       chain smbr edisx;
       if %found;
         if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
           sxifsobj = '/' + %trim(sxifsobj);
         endif;
         syreo01c3 (sxifsobj);
         delete edisxr;
       else;
         if smbr_3 = ' ';
         // MBR = R1234567 -> PRØV R001234567
           xmbr = smbr_1 + '00' + smbr_2;
         else;
         // MBR = R001234567 -> PRØV R1234567
           xmbr = smbr_1 + smbr_4 + '  ';
         endif;
         chain xmbr edisx;
         if %found;
           if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
             sxifsobj = '/' + %trim(sxifsobj);
           endif;
           syreo01c3 (sxifsobj);
           delete edisxr;
         endif;
       endif;

       endsr;

      //*___________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM CARGOF/CARGOO/ENADSF,,,
      //*"""""""""""""""""""""""""""""""""""""""""""
       begsr srd;

       exec sql
       delete from cargof
       where koavd = :heavd and koopd = :heopd
       with NC;

       exec sql
       delete from cargoo
       where coavd = :heavd and coopd = :heopd
       with NC;

       exec sql
       delete from enadsf
       where navd = :heavd and nopd = :heopd
       with NC;

       exec sql
       delete from esosf1
       where o1avd = :heavd and o1opd = :heopd
       with NC;

       exec sql
       delete from esosf2
       where o2avd = :heavd and o2opd = :heopd
       with NC;

       exec sql
       delete from esosf3
       where o3avd = :heavd and o3opd = :heopd
       with NC;

       exec sql
       delete from e171f
       where e171avd = :heavd and e171opd = :heopd
       with NC;

       exec sql
       delete from e307f
       where efavd = :heavd and efopd = :heopd
       with NC;

       exec sql
       delete from faxlog
       where loavd = :heavd and loopd = :heopd
       with NC;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM GODSJF, GODSHF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sre;

       if %subst(hegn:14:2) <> *blanks;
         exec sql
         delete from godsjf
         where gogn = :hegn
         with NC;

         exec sql
         delete from godshf
         where gogn = :hegn
         with NC;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FPSTD/FPXREF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srg;

       exec sql
       delete from fpstd1
       where fpstda = :heavd and fpstdo = :heopd
       with NC;

       exec sql
       delete from fpxref
       where fpavd = :heavd and fpopd = :heopd
       with NC;

       exec sql
       delete from trackk
       where tkavd = :heavd and tkopd = :heopd
       with NC;

       endsr;

      //***********************************************
       begsr init;
      //***********************************************
       xdtf = %float(udtf);
       xdtp = %float(udtp);
       x_arunde1 = 'T:';
       utyp = 'OPD';

       SqlStm = 'Select * ' +
                'from FIRM ' +
                'FOR READ ONLY ';
       Exec SQL PREPARE DynSel0 from :SqlStm;
       EXEC SQL DECLARE  A30  cursor for DynSel0;
       Exec SQL OPEN A30;
       Myrows0 = %elem(MyPoDS0);
       exec sql FETCH A30   for :MyRows0 rows
                  INTO :MyPoDs0;
       Exec SQL CLOSE A30;
       FIFIRM = mypods0(1).fifirm;

       endsr;

      /end-free
