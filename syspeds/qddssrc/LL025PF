     A*****************************************************************
     A* UTSKRIFT AV SVENSK TOLLAGERLISTE                             *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132 OVERFLOW=45       *
     A*****************************************************************
     A* HEADING
     A          R HEAD
     A            KFMT1         75      1  1
     A  88                              1 76'¤&l'
     A  88        COPY           2  0   1 79
     A  88                              1 81'X¤(s15H¤&l1O¤&a-3R'
     A            KFMT2         75      2  1
     A  88                              2 76'¤(12U¤(s0p15h0s3b4099T'
     A*
     A                                  3 58MSGCON(30 YBC1333 SYMSGF)
     A                                      HIGHLIGHT
     A                                  3101MSGCON(14 YBC1334 SYMSGF)
     A                                    +7PAGNBR EDTCDE(Z)
     A                                  4  2MSGCON(14 YBC1335 SYMSGF)
     A            NVNGRU        30A     4 17
     A                                      HIGHLIGHT
     A                                  4 47MSGCON(16 YBC1336 SYMSGF)
     A                                  4 64MSGCON(17 JOV1251 SYMSGF)
     A            XDT            8S 0   4 82EDTWRD('    .  .  ')
     A                                      HIGHLIGHT
     A                                  4101MSGCON(14 YBC1337 SYMSGF)
     A            XIDAG          8S 0   4116EDTWRD('    .  .  ')
     A                                  5  2MSGCON(14 YBC1338 SYMSGF)
     A            NVNKJE        30A     5 17
     A                                      HIGHLIGHT
     A                                  5 60MSGCON(21 YBC1339 SYMSGF)
     A                                  6  2MSGCON(08 YBC1340 SYMSGF)
     A            WSARTF        15A     6 11
     A                                      HIGHLIGHT
     A                                  6 26'->'
     A            WSARTT        15A     6 29
     A                                      HIGHLIGHT
     A                                  6 47MSGCON(08 YBC1341 SYMSGF)
     A            WSLAGF        25A     6 56
     A                                      HIGHLIGHT
     A                                  6 77'->'
     A            WSLAGT        25A     6 80
     A                                      HIGHLIGHT
     A                                  6101MSGCON(17 YBC1342 SYMSGF)
     A            WSDET          1A     6119
     A                                  7  2MSGCON(08 YBC1343 SYMSGF)
     A            WSDATF         6S 0   7 11HIGHLIGHT
     A                                  7 18'->'
     A            WSDATT         6S 0   7 21HIGHLIGHT
     A                                  7 60MSGCON(24 YBC1344 SYMSGF)
     A            WSSALD         1A     7 85
     A                                      HIGHLIGHT
     A                                  7101MSGCON(15 YBC1345 SYMSGF)
     A            WSBRUD         1A     7119
     A  55                              8  2MSGCON(15 YBC1806 SYMSGF)
     A  55        WSAG01         5A     8 18HIGHLIGHT
     A  55        WSAG02         5A     8 24HIGHLIGHT
     A  55        WSAG03         5A     8 30HIGHLIGHT
     A  55        WSAG04         5A     8 36HIGHLIGHT
     A  55        WSAG05         5A     8 42HIGHLIGHT
     A  55                              8 52MSGCON(15 YBC1807 SYMSGF)
     A  55        WSEIER         5A     8 68HIGHLIGHT
      * kolonneoverskrift
     A                                  9  2MSGCON(25 YBC1346 SYMSGF)
     A  52                                   HIGHLIGHT
     A                                  9 28MSGCON(15 YBC1347 SYMSGF)
     A  51                                   HIGHLIGHT
     A                                  9 44MSGCON(15 YBC1348 SYMSGF)
     A N60                              9 61MSGCON(10 YBC1349 SYMSGF)
     A  53                                   HIGHLIGHT
     A N60                              9 72MSGCON(07 YBC1350 SYMSGF)
     A N60                              9 80MSGCON(07 YBC1351 SYMSGF)
     A N60                              9 88MSGCON(05 YBC1352 SYMSGF)
     A N60                              9 94MSGCON(05 YBC1353 SYMSGF)
     A N60                              9100MSGCON(05 YBC1354 SYMSGF)
     A N60                              9106MSGCON(07 YBC1355 SYMSGF)
     A N60                              9114MSGCON(08 YBC1356 SYMSGF)
     A N60                              9123MSGCON(08 YBC1357 SYMSGF)
     A  60                              9 81MSGCON(10 YBC1349 SYMSGF)
     A  53                                   HIGHLIGHT
     A  60                              9 92MSGCON(07 YBC1350 SYMSGF)
     A  60                              9102MSGCON(07 YBC1351 SYMSGF)
     A  60                              9113MSGCON(08 YBC1356 SYMSGF)
     A  60                              9123MSGCON(08 YBC1357 SYMSGF)
     A* DELESTREK ETTER HEAD (NÅR SUB-DETALJER IKKE SKRIVES)
     A          R HEASTR                    SPACEB(1)
     A                                     1'-------------------------'
     A                                    26'-------------------------'
     A                                    51'-------------------------'
     A                                    76'-------------------------'
     A                                   101'-------------------------------'
     A* DELESTREK FØR DET-POST (NÅR SUB-DETALJER SKRIVES)
     A          R DETSTR                    SPACEB(1)
     A                                     1'========================='
     A                                    26'========================='
     A                                    51'========================='
     A                                    76'========================='
     A                                   101'==============================='
     A*DETALJER (Dette gods/artikkelnr)
     A          R DETALJ                    SPACEB(1)
     A            VALLOK        25A        2
     A  52                                   HIGHLIGHT
     A            VAANR         15A       28
     A  51                                   HIGHLIGHT
     A            VAVARX        15A       44
     A            VADATO         8S 0     61EDTWRD('    .  .  ')
     A  53                                   HIGHLIGHT
     A            VAOANT         7S 0     72EDTCDE(L)
     A            VASALD         7S 0     80EDTCDE(L)
     A            VASAE3         5S 0     88EDTCDE(Z)
     A            VASAE2         5S 0     94EDTCDE(Z)
     A            VASAE1         5S 0    100EDTCDE(Z)
     A            VASAE0         7S 0    106EDTCDE(Z)
     A            VAVKTS         7S 2    114EDTCDE(4)
     A            VAKUBS         7S 4    123EDTCDE(4)
      *
     A*DETALJER (Dette gods/artikkelnr)
     A          R DETALJU                   SPACEB(1)
     A            VALLOK        25A        2
     A  52                                   HIGHLIGHT
     A            VAANR         15A       28
     A  51                                   HIGHLIGHT
     A            VAVARE        35A       44
     A            VADATO         8S 0     81EDTWRD('    .  .  ')
     A  53                                   HIGHLIGHT
     A            VAOANT         7S 0     92EDTCDE(L)
     A            VASALD         7S 0    101EDTCDE(L)
     A            VAVKTS         7S 2    113EDTCDE(4)
     A            VAKUBS         7S 4    123EDTCDE(4)
      *
     A          R DETALJ2                   SPACEB(1)
     A  41                                28MSGCON(08 YBC1358 SYMSGF)
     A  41        VASENR        25A       37
     A  42                                68MSGCON(11 YBC1359 SYMSGF)
     A  42        VAULDT         8S 0     80EDTWRD('    .  .  ')
      *TOTALER PR ARTIKKEL PÅ DENNE LOKASJONEN'
     A          R DTOTAL                    SPACEB(1)
     A                                    28MSGCON(37 YBC1360 SYMSGF)
     A            TDTUPL         7S 0     72EDTCDE(L)
     A            TDTSAL         7S 0     80EDTCDE(L)
     A            TDTPAL         5S 0     88EDTCDE(Z)
     A            TDTKRT         5S 0     94EDTCDE(Z)
     A            TDTESK         5S 0    100EDTCDE(Z)
     A            TDTSTK         7S 0    106EDTCDE(Z)
     A            TDTKTS         7S 2    114EDTCDE(4)
     A            TDTUBS         9S 4    121EDTCDE(4)
      *TOTALER PR ARTIKKEL PÅ DENNE LOKASJONEN'
     A          R DTOTALU                   SPACEB(1)
     A                                    28MSGCON(37 YBC1360 SYMSGF)
     A            TDTUPL         7S 0     92EDTCDE(L)
     A            TDTSAL         7S 0    101EDTCDE(L)
     A            TDTKTS         7S 2    113EDTCDE(4)
     A            TDTUBS         9S 4    121EDTCDE(4)
     A*TOTALER (DETTE BRUDDNIVÅ)
     A          R TOTAL                     SPACEB(1)
     A                                     2MSGCON(25 YBC1361 SYMSGF)
     A            TOTPOS         9S 0     30EDTCDE(L)
     A            TOTUPL         9S 0     70EDTCDE(L)
     A            TOTPAL         7S 0     86EDTCDE(Z)
     A            TOTESK         7S 0     98EDTCDE(Z)
     A            TOTKTS         9S 2    112EDTCDE(4)
     A            TOTSAL         9S 0     78EDTCDE(L)
     A                                      SPACEB(1)
     A            TOTKRT         7S 0     92EDTCDE(Z)
     A            TOTSTK         9S 0    104EDTCDE(Z)
     A            TOTUBS         9S 4    121EDTCDE(4)
     A                                      SPACEA(1)
     A*TOTALER (DETTE BRUDDNIVÅ)
     A          R TOTALU                    SPACEB(1)
     A                                     2MSGCON(25 YBC1361 SYMSGF)
     A            TOTPOS         9S 0     30EDTCDE(L)
     A            TOTUPL         9S 0     90EDTCDE(L)
     A            TOTSAL         9S 0     99EDTCDE(L)
     A            TOTKTS         9S 2    111EDTCDE(4)
     A            TOTUBS         9S 4    121EDTCDE(4)
     A                                      SPACEA(1)
     A*TOTALER (GRANDTOTAL)
     A          R GTOTAL                    SPACEB(1)
     A                                     2MSGCON(20 YBC1362 SYMSGF)
     A            GTOPOS         9S 0     30EDTCDE(L)
     A            GTOUPL         9S 0     70EDTCDE(L)
     A            GTOPAL         7S 0     86EDTCDE(Z)
     A            GTOESK         7S 0     98EDTCDE(Z)
     A            GTOKTS         9S 2    112EDTCDE(4)
     A            GTOSAL         9S 0     78EDTCDE(L)
     A                                      SPACEB(1)
     A            GTOKRT         7S 0     92EDTCDE(Z)
     A            GTOSTK         9S 0    104EDTCDE(Z)
     A            GTOUBS         9S 4    121EDTCDE(4)
     A*TOTALER (GRANDTOTAL)
     A          R GTOTALU                   SPACEB(1)
     A                                     2MSGCON(20 YBC1362 SYMSGF)
     A            GTOPOS         9S 0     30EDTCDE(L)
     A            GTOUPL         9S 0     90EDTCDE(L)
     A            GTOSAL         9S 0     99EDTCDE(L)
     A            GTOKTS         9S 2    111EDTCDE(4)
     A            GTOUBS         9S 4    121EDTCDE(4)
      *
     A*SUB-DETALJ-HEADING (motposter på ett gn/art.nr)
      * ALLE DATA FLYTTET 15 TIL HØYRE
     A          R HEADET                    SPACEB(1)
     A                                    18MSGCON(100 YBC1363 SYMSGF)
     A*SUB-DETALJER (motposter på ett art.nr)
     A          R DETDET                    SPACEB(1)
     A            VDAVD          4S 0     44EDTCDE(Z)
     A            VDOPD          7S 0     49EDTCDE(Z)
     A            VDFBNR         3S 0     58EDTCDE(Z)
     A*
     A            VDLDAT         8S 0     67EDTWRD('    -  -  ')
     A            VDLANT         7S 0     80EDTCDE(Z)
     A            VDTEXT        30A       88
