             PGM        PARM(&FILE &LIBRARY &MEMBER)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) /* File    */
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10) /* Library */
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10) /* Member  */
             DCL        VAR(&AVD) TYPE(*DEC) LEN(4 0) /* Avdeling */
             DCL        VAR(&MRKP) TYPE(*CHAR) LEN(10) /* Printer +
                          Device for Merknader */
             DCL        VAR(&OK) TYPE(*CHAR) LEN(1) /* Status fra +
                          program, N=Nei(Ikke OK), J=Ja(OK) */
             DCL        VAR(&OKPRT) TYPE(*CHAR) LEN(1) /* Printerdata+
                          finnes for avdelingen J/N         */
             DCL        VAR(&IN39) TYPE(*CHAR) LEN(1) VALUE('1') /* +
                          Print av TK kalles fra TVINN eller +
                          registrering */
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) VALUE(TVINR) /* +
                          "Skjerm ID"= Membernavn arbeidsfil*/
             DCL        VAR(&LENGTH) TYPE(*DEC) LEN(5 0) VALUE(128)
             DCL        VAR(&DTA) TYPE(*CHAR) LEN(128)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
           /*______________________________________________*/
           /* Konvertere meldinger.                        */
           /* - Oppdatere Transitt register med en unik    */
           /*   status for: Mottatt men ikke utskrevet.    */
           /* - Hvis det er noe feil med datane skrives    */
           /*   det ut en feillogg.                        */
           /*""""""""""""""""""""""""""""""""""""""""""""""*/
             CALL       PGM(SVX104R) PARM(&LIBRARY &FILE &MEMBER &WS +
                          &OK)
             IF         COND(&OK *EQ N) THEN(GOTO CMDLBL(END))
           /*_________________________________________________*/
           /* Skriv ut El. transittering                      */
           /*"""""""""""""""""""""""""""""""""""""""""""""""""*/
             RTVUSRPRF  RTNUSRPRF(&USER)
             CHGVAR     VAR(&DTA) VALUE(&WS *CAT &USER *CAT &IN39)
             CALL       PGM(QSNDDTAQ) PARM(SVX014RQ *LIBL &LENGTH &DTA)
           /*______________________________________________*/
           /* Hente avdelingsopplysninger for å overstyre  */
           /* printerfiler. Ingen med A/B/Å i staus-OK=N   */
           /*""""""""""""""""""""""""""""""""""""""""""""""*/
 GETAVD:     CALL       PGM(SVX105R) PARM(&AVD &MRKP &WS &OK &OKPRT)
             IF         COND(&OK *EQ N) THEN(GOTO CMDLBL(END))

             IF         COND(&OKPRT *EQ N) THEN(CHGVAR VAR(&MRKP) +
                          VALUE(*SYSVAL))

             OVRPRTF    FILE(TVINME) PAGESIZE(72 80) OVRFLW(66) +
                          OUTQ(&MRKP) SECURE(*YES)
           /*_________________________*/
           /* Skriv ut Merknader (ME) */
           /*"""""""""""""""""""""""""*/
             CALL       PGM(SVX106R) PARM(&AVD &WS)
             GOTO       CMDLBL(GETAVD)
 END:        ENDPGM
