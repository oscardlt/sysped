      *********************************************************************
      *  Kobling av sendingsnr / kolliID
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/esla01b MODULE(*LIBL/esla01b *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FWSNRCLI   UF A E           K DISK    USROPN
     FWSNRCLI1  IF   E           K DISK    USROPN
     F                                     RENAME(WSNRCLIR:WSNRCLI1R)
     FDOKUFM1   IF   E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Varibles
     D WSUSER          S             10
     D SNNR            S             20
     D WSCLID          S             20
     D WSCLID18        S             18
     D XXMRK1          s             35
     D KOBLET          S              4
     D KOBLETN         S              4  0
     D Ant             S              4
     D AntK            S              4  0
     D PostNr          S              4
     D TEXT            S             20
     D MELDING         S             70
     D SEMI            S              1
     D TKfeilK         S              1
     D Avslutt         S              1
     D Cmd             S             70
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  PRT                    1     50
     D  LABTYPE                1      1
     D  LABOUTQ                2     11
     D  TRANSPORTØR           12     31
     D                 DS
     D  FMMRK1                 1     35
     D  FMMRK18                1     18
     D                 DS
     D  TMP                    1      4
     D  TMP1                   1      1
     D  TMP2                   2      2
     D  TMP3                   3      3
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
     D                 DS
     D  WscDat                 1      8  0
     D  WscDatÅ                1      4  0
     D  WscDatM                4      6  0
     D  WscDatD                7      8  0
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
      * Hent inputparametre
     C                   EVAL      WSUSER   = zhbgetvar('user')
     C                   EVAL      SNNR     = zhbgetvar('snnr')
     C                   EVAL      WSCLID   = zhbgetvar('CLID')
     C                   EVAL      Ant      = zhbgetvar('ant')
     C                   eval      AntK     = c2n2(ant)
     C                   EVAL      Koblet   = zhbgetvar('Koblet')
     C                   eval      KobletN  = c2n2(koblet)
     C                   EVAL      TEXT     = zhbgetvar('TEXT')
     C                   EVAL      PRT      = zhbgetvar('PRT')
     C                   EVAL      SEMI     = zhbgetvar('SEMI')
     c
      * Sett libr.list etter UserID
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              33
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      * Open files
     C                   open      WSNRCLI
     C                   open      WSNRCLI1
     C                   open      Dokufm1
      * Initier
     c                   clear                   WscDat
     c                   clear                   xtime
     C     SEMI          IFNE      'Y'
     C                   eval      MELDING   = 'N'
     C                   end
     C                   eval      Avslutt   = 'N'
      * Behandle Innparameter
     c                   select
     c     wsclid        wheneq    *blanks
     c                   exsr      getant
     c     wsclid        wheneq    '99'
     c                   exsr      slett
     c     wsclid        wheneq    '98'
     C                   eval      Avslutt   = 'J'
     c                   other
     c                   exsr      UPDFIL
     c                   end
      *
      * Set/Read skeleton output html, etc.
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'ESLA01B'
      * Ferdig??
     C     Avslutt       IFEQ      'J'
     C     SEMI          andne     'Y'
     C                   eval      Mbr = 'ESLA01'
     c                   z-add     1             antk
     c                   move      *blanks       snnr
     C                   end
     C     SEMI          IFEQ      'Y'
     C                   eval      Mbr = 'ESLA01BS'
     C                   end
      *
      * Hent svar-skjema
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * sett html-variable
     C                   callp     updhtmlvar('USER':WSUSER:
     C                             InitHTMLVars)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('SNNR':SNNR)
     C                   move      *blanks       wsclid
     C                   callp     updhtmlvar('CLID':WSCLID)
     C                   callp     updHTMLvar('MELDING':MELDING)
     C                   callp     updHTMLvar('TKFeilK':TKFeilK)
     C                   callp     updhtmlvar('KOBLET':
     c                             %trim(%editc(KOBLETN:'3')))
     C                   callp     updhtmlvar('ANT':
     c                             %trim(%editc(ANTK:'3')))
     C                   callp     updhtmlvar('TEXT':TEXT)
     C                   callp     updhtmlvar('PRT':PRT)
      * Write HTML-section
     C                   callp     wrtsection('all')
      * dummy fini-section
     C                   callp     wrtsection('*fini')
      * Quit
     C                   close     BRIDF
     C                   close     WSNRCLI
     C                   close     WSNRCLI1
     C                   close     dokufm1
     C                   MOVE      *ON           *INLR
     C                   return
     C***********************************************
     C     UPDFIL        BEGSR
     C***********************************************
      * Er det sletting
     C     WSCLID        IFEQ      '99'
     c                   exsr      slett
     C                   Eval      MELDING   = 'Evt koblinger er slettet'
     c                   goto      utles
     c                   end
      *
      * Ved kall fra TKterm kan det komme MANGE i en gang...
     c     SEMI          ifeq      'Y'
      * For alle forekomster av &CLID
     C                   eval      ItemCount = ZhbGetVarCnt('CLID')
     C                   eval      i = 1
     C     i             DOWLE     ItemCount
     C                   EVAL      WSCLID   = zhbgetvar('CLID':i)
     C                   exsr      SKRIVpost
     C                   eval      i = i +1
     C                   ENDDO
     C                   Eval      MELDING   = 'Sendingsnr '
     C                             + ' er koblet med kolliene'
     c                   z-add     ANTK          Kobletn
     c                   goto      utles
     c                   end
      *
      * Riktig kolliID??
     C*                  MOVEL     WSCLID        XWSCLID           3
     C                   MOVEL     WSCLID        XWSCLID           2
     C     XWSCLID       IFNE      '00'
     C                   Eval      MELDING   = 'Gyldig Kolli-ID må leses'
     c                   goto      utles
     c                   end
      * KolliID finnes alt i w.fila
     C                   MOVE      WSCLID        WSCLID18
     C     WSCLID18      chain     WSNRCLI1                           33
     C     *in33         IFeq      '0'
     C                   Eval      MELDING   = 'Kolli-ID finnes alt (snd: '+
     C                                         wscsnr + ')'
     c                   move      'X'           TKfeilk
     c                   goto      utles
     c                   end
      * KolliID finnes alt i oppdrag..
     C                   MOVEL     WSCLID18      XXMRK1
     C     XXMRK1        SETLL     DOKUFM1
     C                   READ      DOKUFM1                                33
     C  N33FMMRK18       COMP      WSCLID18                           3333
     C     *in33         IFeq      '0'
     C                   Eval      MELDING   = 'Kolli-ID finnes alt på '+
     C                                         'registrert oppdrag'
     c                   move      'X'           TKfeilk
     c                   goto      utles
     c                   end
      *
     C                   exsr      SKRIVpost
     C                   ADD       1             KOBLETN
     C     KOBLETN       IFGE      ANTK
     c                   move      kobletn       TMP
     c     TMP1          Ifeq      '0'
     c                   move      ' '           TMP1
     c     TMP2          Ifeq      '0'
     c                   move      ' '           TMP2
     c     TMP3          Ifeq      '0'
     c                   move      ' '           TMP3
     c                   end
     c                   end
     c                   end
     C                   Eval      MELDING   = 'Snr: '+SNNR+' ferdig koblet  '+
     C                                         'med  ' +TMP+ '  kolli'
     C                   Move      'J'           Avslutt
     c                   end
      *
     c     utles         tag
     c                   endsr
     C***********************************************
     C     SKRIVPOST     BEGSR
     C***********************************************
      * WscKOD: M=Merket gods mottatt/koblet, (U=Umerket gods mottatt)
     C                   MOVE      'M'           WSCKOD
     C                   MOVE      SNNR          WSCSNR
     C                   MOVE      WSCLID        WSCCLI
     C                   MOVEL     TEXT          WSCTXT
     C                   TIME                    XTIME
     C                   MOVE      XDD           WscDatD
     C                   MOVE      XMM           WscDatM
     C                   MOVE      XÅÅ           WscDatÅ
     C                   MOVE      XTID          WscTIM
     C                   MOVE      WSUSER        WscUse
     C                   WRITE     WSNRCLIR
     c                   endsr
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
     C                   MOVE      SNNR          WSCSNR
     c     WSCSNR        SETLL     WSNRCLI
     c     WSCSNR        reade     WSNRCLI                                33
     c     *in33         doweq     '0'
     C                   delete    WSNRCLIR
     c     WSCSNR        reade     WSNRCLI                                33
     c                   end
     C                   Move      'J'           Avslutt
     c     Semi          ifne      'Y'
     c                   move      *zeros        ANTK
     c                   end
     c                   move      *zeros        KOBLETN
     c                   endsr
     C***********************************************
     C     GetAnt        BEGSR
     C***********************************************
      * HER BURDE EN ENTEN TESTE AT DET IKKE ER EKSISTERENDE OPPDRAG
      * ELLER LOGIKK FOR Å KOBLE MOT EKSISTERENDE OPPDRAG MÅ UTVIKLES
      *  (inkl gi antallet som skal kobles fra oppdraget. (HENT i oppdraget)
      *   og i så fall bør vel allerede eksisternde koblede sendes ti TKterm..)
     c                   move      *zeros        KOBLETN
     C                   MOVE      SNNR          WSCSNR
     c     WSCSNR        SETLL     WSNRCLI
     c     WSCSNR        reade(N)  WSNRCLI                                33
     c     *in33         doweq     '0'
     C                   add       1             KOBLETN
     c     WSCSNR        reade(N)  WSNRCLI                                33
     c                   end
      * Om ikke gitt/for lavt - sett lik nåværende....
     c     ANTK          IFLT      Kobletn
     c                   move      kobletn       antk
     c                   end
      * hvis kall fra TKterm OG koblede finnes, varsle om Nullstill
     c     SEMI          ifeq      'Y'
     c     Kobletn       ifne      *zeros
     c                   eval      Melding='Sending ER alt koblet. '
     c                             +'Bruk Nullstill om du vil koble på ny.'
     c                   move      'X'           TKfeilk
     c                   end
     c     Melding       ifeq      *blanks
     c                   eval      Melding='Sending ' + SNNR
     c                   end
     c                   end
     c                   endsr
