********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 FEIL POSISJON PÅ STATUS.
10027 * 02 YBC PTF10 ORDRETYPE
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R GSS040DA                  SFL
     A            WSNR           1A  B  6  2VALUES(' ' '2' '5' '6' '7' '8' '9' +
     A                                      'A' 'C' 'I')
     A            TRTNO         11Y 0O  6  5EDTCDE(4)
     A  41                                  COLOR(WHT)
S01  A*           TRST           1A  O  6 12
N01  A            TRST           1A  O  6 17
     A            TRAD1         30A  H
     A            XTRAD1        28A  O  6 19
     A            TRLOCF         5A  H
     A            TRBCD          5A  O  6 48
     A  43                                  COLOR(WHT)
     A            TRCNO          8Y 0O  6 54EDTCDE(4)
     A  42                                  COLOR(WHT)
EV5  A            TRDTL          8Y 0H
     A            WXDTL          6Y 0O  6 63EDTCDE(4)
EV5  A            TRDTS          8Y 0H
     A            WXDTS          6Y 0O  6 70EDTCDE(4)
     A            TRFC           3A  O  6 77
      *
     A          R GSS040DB                  SFLCTL(GSS040DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'Frisk')
     A                                      CF23(23 'BEKREFTE')
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS040D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with invoice basis'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Opt:  2=Change  5=Display  6=Print-
     A                                        7=Copy pack.l.  8=Copy proforma  -
     A                                      9=Confirm'
     A                                      COLOR(BLU)
     A                                  3  2'      A=Archive  C=Change status  -
     A                                      I=Box info'
     A                                      COLOR(BLU)
     A                                  4  2'Op Trip No... St Reciever         -
     A                                                  BoatC ClientsN Del.DT D-
     A                                      is.DT Fir'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2TNO         11Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  5 48DSPATR(UL)
     A            W2CNO          8Y 0B  5 54DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R GSS040DC
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=End   F4=Query   F5=Refresh   F-
     A                                      6=Create   F23=Confirm'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS040DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF23(23 'BEKREFTE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS040D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with invoice basis'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Opt:  2=Change  5=Display  6=Print-
     A                                        7=Copy pack.l.  8=Copy proforma  -
     A                                      9=Confirm'
     A                                      COLOR(BLU)
     A                                  3  2'      A=Archive  C=Change status  -
     A                                      I=Box info'
     A                                      COLOR(BLU)
     A                                  4  2'Op Trip No... St Reciever         -
     A                                                  BoatC ClientsN Del.DT D-
     A                                      is.DT Fir'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2TNO         11Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  5 48DSPATR(UL)
     A            W2CNO          8Y 0B  5 54DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  8  2'No trip found'
     A                                      COLOR(RED)
     A                                 23  2'F3=End   F4=Query   F23=Confirm'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS040DS
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      CF23(23 'Confirm')
     A                                      OVERLAY
     A                                  1  2'Confirm changing of status'
     A                                      COLOR(WHT)
     A                                  2  2'From status:'
     A            TRST           1A  O  2 15DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'To status  :'
     A                                  3 15'3'
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2'F12=Cancel  F23=Confirm'
     A                                      COLOR(BLU)
      *
     A          R GSS040DX
     A                                      WINDOW(*DFT 8 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A            MSGHOD        25A  O  1  2MSGID(&MSGHN SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'Printer name :'
     A            WSPRT         10A  B  2 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A                                      DSPATR(PC)
     A                                  3  2'No. of copies:'
     A            WSCPY          1  0B  3 17DSPATR(UL)
     A                                  4  2'Send fax/Email'
     A            WSFAX          1A  B  4 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  4 20'Y/N'
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99                              6  2MSGCON(25 GSS0042 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGHN          7A  P
      *
     A          R GSS040DY
     A                                      WINDOW(*DFT 11 73 *NOMSGLIN)
     A                                      CF04(04 'Søking')
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A            MSGHOD        25A  O  1  2MSGID(&MSGHN SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'HEADING TEXT'
     A            WSFT1         70A  B  3  2DSPATR(UL)
     A N98N99                               DSPATR(PC)
     A            WSFT2         70A  B  4  2DSPATR(UL)
     A                                  5  2'Printer name :'
     A            WSPRT         10A  B  5 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
N02  A                                  5 32'Type of order:'
N02  A            WSTYPE         1A  B  5 47DSPATR(UL)
N02  A                                      VALUES(' ' 'C' 'T' 'X')
N02  A                                  5 49'(Blank=ALL)'
     A                                  6  2'No. of copies:'
     A            WSCPY          1  0B  6 17DSPATR(UL)
     A                                  7  2'Layout:       '
     A            WSNYL          1A  B  7 17DSPATR(UL)
     A                                      VALUES('P' 'L' 'O')
     A                                  7 20'P=Portrait L=Landscape O=Portrait2'
     A                                  8  2'Curr. code   :'
     A            WSCUR          3A  B  8 17DSPATR(UL)
     A  98                                  DSPATR(HI)
     A  98                                  DSPATR(PC)
     A                                  9  2'Send fax/Email'
     A            WSFAX          1A  B  9 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  9 20'Y/N'
     A                                 10  2'Comm.invoice?:'
     A            WSCOMM         1A  B 10 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                 10 22'Clientsnr:'
     A            WSKNC          8  0B 10 34DSPATR(UL)
     A  97                                  DSPATR(HI)
     A  97                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N99N98N97                       11  2'F4=Search   F12=Cancel'
     A                                      COLOR(BLU)
     A  99                             11  2MSGCON(25 GSS0042 SYMSGF)
     A                                      COLOR(WHT)
     A  98                             11  2MSGCON(25 GSS0066 SYMSGF)
     A                                      COLOR(WHT)
     A  97                             11  2MSGCON(25 SPK0001 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGHN          7A  P
