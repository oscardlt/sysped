      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE042R MODULE(*LIBL/TNOE042R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 Bytt SADL med SADVARE
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
S01  F*SADL      IF   E             DISK    USROPN
N01  FSADVARE   IF   E             DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
      * Varible for
     D WSUSER          S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d myrows          s             10i 0
     d  X              S              5  0
     d  WERR           S              1
     d  W              S             20
N01  D                 DS
N01  D  W2VNTI                 1      8
N01  D  W2VNT                  1      8  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
        /free

        // Parse input
         exsr Parse;
        // Open files etc
         exsr Init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"kundvaradd" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         // Hæmta til array
         exsr sradd;
         // Avsluta
         exsr avslut;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       // Quit
       exsr exit;

       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         if *in50 = *off;
S01      //close sadl;
N01      close sadvare;
         endif;

         // Do not delete the call to wrtsection with section name *fini.  It is needed
         // to ensure that all output html that has been buffered gets output.
         wrtsection('*fini');
         // Quit
         *inlr = *on;
         return;
       endsr;
       //********************************************************
       begsr parse;
       //********************************************************
       // Get input
         wsuser    = zhbgetvar('USER');
S01      //slstat    = zhbgetvar('SLSTAT');
         w         = zhbgetvar('SLKNR');
S01      //slknr     = c2n2(w);
N01      LEVENR    = c2n2(w);
S01      //slalfa    = zhbgetvar('SLALFA');
N01      VARENR    = zhbgetvar('SLALFA');
S01      //sltxt     = zhbgetvar('SLTXT');
N01      VAREBE    = zhbgetvar('SLTXT');
S01      //sloppl    = zhbgetvar('SLOPPL');
N01      W2LK      = zhbgetvar('SLOPPL');
         w         = zhbgetvar('SLVEKT');
S01      //slvekt    = c2n2(w);
N01      W2VKTB    = c2n2(w);
         w         = zhbgetvar('SLTANR');
S01      //sltanr    = c2n2(w);
N01      W2VNT     = c2n2(w);
S01      //sltar     = zhbgetvar('SLTAR');
S01      //slpva     = zhbgetvar('SLPVA');
N01      W2PVA     = zhbgetvar('SLPVA');
         w         = zhbgetvar('SLSATS');
S01      //slsats    = c2n2(w);
N01      W2AS      = c2n2(w);
S01      //sltn      = zhbgetvar('SLTN');
N01      W2TN      = zhbgetvar('SLTN');
S01      //SLKDAÆ    = zhbgetvar('SLKDAE');
N01      W2AKD1    = zhbgetvar('SLKDAE');
S01      //SLKDSÆ    = zhbgetvar('SLKDSE');
N01      W2ASV1    = zhbgetvar('SLKDSE');
S01      //SLTO      = zhbgetvar('SLTO');
N01      W2TOP1    = zhbgetvar('SLTO');
S01      //SLCREF    = zhbgetvar('SLCREF');
N01      W2CRE1    = zhbgetvar('SLCREF');

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;
         *in50 = not %found;

         if *in50 = *off;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
        //Dummy read for feltbeskrivelser, kuttes ut i java
S01      //open sadl;
N01      open sadvare;
         if *in50 = *on;
S01      //read sadl;
N01      read sadvare;
         endif;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
        // __________________________________________________
        // Addera post till fil                         SRADD
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr sradd;

          exec sql

S01     //INSERT INTO SADL
S01     //(SLSTAT, SLKNR, SLALFA, SLTXT, SLOPPL, SLVEKT, SLTANR,
S01     //SLTAR, SLPVA, SLSATS, SLTN, SLKDAÆ, SLKDSÆ, SLTO, SLCREF)
S01     //VALUES(:SLSTAT, :SLKNR, :SLALFA, :SLTXT, :SLOPPL, :SLVEKT,
S01     //:SLTANR, :SLTAR, :SLPVA, :SLSATS, :SLTN, :SLKDAÆ, :SLKDSÆ,
S01     //:SLTO, :SLCREF);
N01       INSERT INTO SADVARE
N01       (LEVENR, VARENR, VAREBE, W2LK, W2VKTB, W2VNTI,
N01       W2PVA, W2AS, W2TN, W2AKD1, W2ASV1, W2TOP1, W2CRE1)
N01       VALUES(:LEVENR, :VARENR, :VAREBE, :W2LK, :W2VKTB,
N01       :W2VNTI, :W2PVA, :W2AS, :W2TN, :W2AKD1, :W2ASV1,
N01       :W2TOP1, :W2CRE1);

       endsr;
        // __________________________________________________
        // Avsluta                                     AVSLUT
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr avslut;
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬result¬ : ¬'+ 'ok' + '¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

       endsr;
      /end-free
