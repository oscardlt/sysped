      *
      **************************************************************
      *** PROGRAM SOM SKRIVER TRANSPORTØR-KREDITNOTA DETALJVEDLEGG**
      **************************************************************
      *
     FTRAVH3    IF   E           K DISK
     FTRAVV2    IF   E           K DISK
     FKOSBU     IF   E           K DISK
     FKOSTA     IF   E           K DISK
     FTURER     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYPARL1   IF   E           K DISK
     FSYTR12PH  O    E             PRINTER OFLIND(*IN88)
     D                 DS
     D  XDAG1                  1      4  0
     D  XDAG2                  5      6  0
     D  XDAG3                  7      8  0
     D  XDAG                   1      8  0
     C*
     C                   EXSR      INIT
      *
     C                   EXSR      PRINT
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C                   EXSR      GETTRA
     C* TVUNGEN OVERFLOW GIR HEADING
     C                   SETON                                        88
     C                   MOVE      *ZEROS        XSIDE             2 0
     C                   MOVE      *ZEROS        XTOTAL           13 2
     C                   MOVE      *ZEROS        XMVAGR           13 2
     C                   EXSR      NEWPAG
     C*
     C                   SETOFF                                       94
     C     TRAHK2        SETLL     TRAVH3
     C*
     C     TRAHK2        READE     TRAVH3                                 94
     C     *IN94         DOWEQ     '0'
     C* VANLIG TUR...
     C     THRTNR        IFNE      99999998
     C                   EXSR      DECI
     C                   ADD       THTOT         XTOTAL
     C     THKDM         COMP      'J'                                    34
     C   34              ADD       THTOT         XMVAGR
     C* VANLIG TUR...
     C     TURERK        CHAIN     TURER                              33
     C* KONVERTER TURDATO TIL LOKALT FORMAT
     C   33              MOVE      *ZEROS        TUDT
     C                   MOVE      TUDT          TUDATO            6 0
     C     FIDTFM        IFNE      3                                            SNU DATO TIL
     C                   CALL      'DATFM3'                                     LOKALT FORMA
     C                   PARM                    FIDTFM
     C                   PARM                    TUDATO
     C                   END
     C     TUTLM         COMP      *ZEROS                                 96
     C     TUTM3         COMP      *ZEROS                                 97
     C     THSTAR        COMP      ' '                                    99    *UBESKYTTET
     C                   EXSR      NEWPAG
     C                   WRITE     DET
     C                   WRITE     DETST
     C                   EXSR      VARELI
     C                   END
     C     TRAHK2        READE     TRAVH3                                 94
     C  N94              END
     C*
     C* GODSKRIVINGER/TREKK UTENOM TUR???
     C                   EXSR      GODSKR
     C* SKRIV BUNNTOTAL FOR DENNE TRANSPORTØR
     C                   EXSR      TOTAL
     C*
     C     UTTRAN        ENDSR
     C*
     C***********************************************
     C     VARELI        BEGSR
     C***********************************************
     C* 61 - 67 - TYPE TEKST ER SKREVET.
     C                   MOVEA     '0000000'     *IN(61)
     C                   SETOFF                                       95
     C*
     C                   EXSR      NEWPAG
     C                   WRITE     DET2H
     C*
     C     TRAVVV        SETLL     TRAVV2
     C     TRAVVV        READE     TRAVV2                                 95
     C     *IN95         DOWEQ     '0'
     C     TVTYP         COMP      'A'                                    51    *AVTALT
     C     TVTYP         COMP      'B'                                    52    *BR-PROV
     C     TVTYP         COMP      'C'                                    53    *INNH.
     C     TVTYP         COMP      'D'                                    54    *UTKJ.
     C     TVTYP         COMP      'E'                                    55    *FØRT REK
     C     TVTYP         COMP      'F'                                    56    *BUD.REK
     C     TVTYP         COMP      'G'                                    57    *JUSTER.
     C   51TVBLL         IFNE      *ZEROS
     C                   EXSR      NEWPAG
     C                   WRITE     DET2
     C                   END
     C     *IN52         IFEQ      '1'
     C     TVOPKO        COMP      ' '                                    50    *U-FAKT*
     C                   END
     C  N51              EXSR      NEWPAG
      * Ved *AVTALT honorar OG param for at da skal detalj undertykkes -
     C     UNDETA        IFEQ      'J'
     C     TUTAKO        ANDEQ     'A'
      * arb-liste skriver her evt U-FAK ...
     C                   ELSE
     C  N51              WRITE     DET2
     C                   END
     C     TRAVVV        READE     TRAVV2                                 95
     C   51              SETON                                            61
     C   52              SETON                                            62
     C   53              SETON                                            63
     C   54              SETON                                            64
     C   55              SETON                                            65
     C   56              SETON                                            66
     C   57              SETON                                            67
     C  N95              END
     C*
     C* SIKRER AT TOTAL FOR TUREN KOMMER PÅ SAMME SIDE - IKKE TEST OVERFLOW
     C                   WRITE     DETTOT
     C                   ENDSR
     C***********************************************
     C     GODSKR        BEGSR
     C***********************************************
     C*
     C* RUNDTURNR 99999998 ER REKVISISJONER HVOR DENNE TRANSPORTØR ER
     C* DEN UTFØRENDE PART. OG SKAL GODSKRIVES PÅ KREDITNOTA.
     C* SKRIV "HEADING" - SPESIFISER ALLE REKVISISJONER
     C* OGSÅ TREKK FOR UTLEGG SOM IKKE ER KNYTTET TIL TUR/OPPDRAG
     C*
     C                   EXSR      NEWPAG
     C                   MOVE      'Y'           FØRSTT            1            skriv THEAD
     C                   MOVE      'Y'           FØRSTG            1            skriv GHEAD
     C                   MOVE      *ZEROS        THTOT
     C*
     C                   MOVE      *ZEROS        THTAVD
     C                   MOVE      THTNR         THTUNR
     C                   SETOFF                                           95
     C     TRAVVB        SETLL     TRAVV2
     C     TRAVVB        READE     TRAVV2                                 95
     C     *IN95         DOWEQ     '0'
     C     TVBNRB        CHAIN     KOSBU                              33
     C   33              MOVE      *ZEROS        BUFEDT
     C                   MOVE      BUFEDT        BUDATO            6 0
     C     FIDTFM        IFNE      3                                            SNU DATO TIL
     C     BUFEDT        ANDNE     *ZEROS
     C                   CALL      'DATFM3'                                     LOKALT FORMA
     C                   PARM                    FIDTFM
     C                   PARM                    BUDATO
     C                   END
     C                   EXSR      NEWPAG
     C* Er det TREKK - momskode fra KOSBU ("N"=DEFAULT)
     C     BUTUNR        IFEQ      *ZEROS
     C     BUOPD         ANDEQ     *ZEROS
     C     BUKDM         COMP      'J'                                    34
     C     FØRSTT        IFEQ      'Y'
     C                   WRITE     THEAD
     C                   MOVE      'N'           FØRSTT                         ikke skriv
     C                   END
     C* Hvis det er kostn.ført OG endelig bil.nr gitt, vis det
     C     BUBNR2        IFNE      *ZEROS
     C     BUBNR2        CHAIN     KOSTA                              33
     C  N33KABNR2        IFNE      *ZEROS
     C                   MOVE      KABNR2        BUBNR2
     C                   END
     C                   END
     C                   WRITE     TDET
     C                   ELSE
     C* Er det Godskr. - momskode default J (antar at det er innenlands)
     C     BUKDM         COMP      'N'                                3434
     C     FØRSTG        IFEQ      'Y'
     C                   WRITE     GHEAD
     C                   MOVE      'N'           FØRSTG                         ikke skriv
     C                   END
     C                   WRITE     GDET
     C                   END
     C                   ADD       TVBLL         THTOT
     C   34              ADD       TVBLL         XMVAGR
     C     TRAVVB        READE     TRAVV2                                 95
     C  N95              END
      *
     C                   EXSR      DECI
     C                   ADD       THTOT         XTOTAL
     C* SIKRER AT TOTAL  KOMMER PÅ SAMME SIDE - IKKE TEST OVERFLOW
     C                   WRITE     GDETT
     C                   ENDSR
     C***********************************************
     C     GETTRA        BEGSR
     C***********************************************
     C*
     C                   MOVE      *ZEROS        YKUNDN            8 0
     C                   MOVE      *BLANKS       YNAVN            30
     C                   MOVE      *BLANKS       YADR1            30
     C                   MOVE      *BLANKS       YADR2            30
     C                   MOVE      *BLANKS       YADR3            35
     C* TRANSPORTØRENS KUNDENR/ HENT NAVN- SPRÅK
     C     TRANKE        CHAIN     TRAN                               33
     C   33              MOVE      9             VMINCR
     C  N33VMTRKU        IFNE      *ZEROS
     C                   MOVE      VMTRKU        YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVE      KUNDNR        YKUNDN            8 0
     C                   MOVE      KNAVN         YNAVN            30
     C                   MOVE      ADR1          YADR1            30
     C                   MOVE      ADR2          YADR2            30
     C* KOMPONER SISTE ADRESSELINJE UTFRA GITT POSTNR/LANGT POSTNR...
     C                   MOVE      *BLANKS       YADR3            35
     C     POSTNR        COMP      0                                      37
     C     SYPOGE        COMP      *BLANKS                                38
     C  N37              MOVEL     POSTNR        YADR3                          *NORSK
     C  N37              MOVEL     ADR3          YADR32           30              POSTNR
     C  N37              MOVE      YADR32        YADR3
     C   37
     CANN38              MOVEL     SYPOGE        YADR3                          *UTENLANDSK
     C   37
     CANN38              MOVE      ADR3          YADR3                            POSTNR
     C   37
     CAN 38              MOVEL     ADR3          YADR3                          *IKKE POSTNR
     C                   ELSE
     C                   MOVE      VMNAVN        YNAVN            30
     C                   END
     C                   ENDSR
     C***********************************************
     C     TOTAL         BEGSR
     C***********************************************
     C     XMVAGR        MULT(H)   FITAX         XMOMS            13 2
     C     XTOTAL        ADD       XMOMS         XBRUTT
     C*
     C* KAN RISIKERE AT MOMS + TOTAL KOMMER PÅ EGEN SIDE. MEN FOR Å UNNGÅ
     C* DET MÅTTE EN HA SATT OVERFLOW TIL 42 OG FÅR LITE UTAV HVER SIDE
     C     XMOMS         IFNE      *ZEROS
     C                   EXSR      NEWPAG
     C                   WRITE     TOTA
     C                   END
     C* ALLTID PLASS FOR SLUTTTOTAL ( 1 BLANK + 2 LINJER) - IKKE TEST OVERFL.
     C                   WRITE     TOTC
     C                   ENDSR
     C*
     C***********************************************
     C     NEWPAG        BEGSR
     C***********************************************
     C*  88 ER OVERFLOWINDIKATOR
     C   88              DO
     C                   SETOFF                                       88
     C                   ADD       1             XSIDE             2 0
     C                   EXSR      KDTFM1
     C                   WRITE     KJØP
     C                   EXSR      KDTFM1
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      IDAGX         XKDTFM            4 0
     C                   MOVEL     IDAGX         UDT1              2 0
     C                   MOVEL     XKDTFM        UDT2              2 0
     C                   MOVE      XKDTFM        UDT3              2 0
     C                   EXSR      KDTFMT
     C                   MOVEL     UDT1          IDAGX
     C                   MOVEL     UDT2          XKDTFM
     C                   MOVE      UDT3          XKDTFM
     C                   MOVE      XKDTFM        IDAGX
     C*
     C                   ENDSR
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     DECI          BEGSR
     C***********************************************
     C* TEST FIRMS NUMBER OF DECIMALS
     C*
     C* NO DECIMALS
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  THTOT         FABEL0           11 0
     C                   Z-ADD     FABEL0        THTOT
     C                   ELSE
     C* 1 DECIMAL
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  THTOT         FABEL1           12 1
     C                   Z-ADD     FABEL1        THTOT
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C* TIL SLUTT LEGGES PARAMETRE SOM SKILLER SEG FRA PROG TIL PROG.
     C                   PARM                    UUBNR             7            *BIL.NR
     C**
     C                   MOVE      UUBNR         UUBNRN            7 0          *BIL.NR
     C*
     C* KUNDEKEY
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN               8 0
     C* KUNDES LANDKODE
     C     LKKEY         KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    SYLAND
     C* FOR READE  PÅ EN FUNNET TRANSPORTØR
     C     TRAHK2        KLIST
     C                   KFLD                    YYST              1
     C                   KFLD                    UUBNRN
     C*
     C     TURERK        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C     TRAVVV        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    YYTVST            1
     C                   MOVE      ' '           YYTVST
     C     TRAVVB        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    YYST
     C                   KFLD                    UUBNRN
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    THTNR
     C     UdetaKey      KLIST
     C                   KFLD                    XXPAKN            8 0
     C                   KFLD                    XXPAID            5
     C                   movel     'UDETA'       XXPAID
     C*
     C                   MOVEL     'LK'          YLKUNI            3
     C                   MOVE      *ZEROS        YKUNDN
     C*
      * Finnes avr.post på aktuelt nummer....
     C                   MOVE      'F'           YYST
     C     TRAHK2        SETLL     TRAVH3
     C     TRAHK2        READE     TRAVH3                                 33
     C   33              MOVE      'O'           YYST
     C   33TRAHK2        SETLL     TRAVH3
     C   33TRAHK2        READE     TRAVH3                                 33
     C*
     C     *IN33         IFEQ      '1'
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C*
     C                   MOVE      THDATO        IDAGX             6 0
     C*
     C                   READ      FIRM                                   33
     C* SJEKK EVT AVVIKENDE MOMSSATS MOT DATO SAMMENSATT AV PERIODE/ÅR
     C                   MOVE      31            XDAG3
     C     THMND         IFEQ      4
     C     THMND         OREQ      6
     C     THMND         OREQ      9
     C     THMND         OREQ      11
     C                   MOVE      30            XDAG3
     C                   ELSE
     C     THMND         IFEQ      2
     C                   MOVE      28            XDAG3
     C                   END
     C                   END
     C                   MOVE      THMND         XDAG2
     C     THCC          MULT      100           XDAG1
     C                   MOVE      THÅR          XDAG1
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     XDAG          ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   END
     C*
     C                   MOVE      FITAX         PRITAX            4 2
     C                   MOVE      FICURR        XVAL              3
      * Undertrykk detaljer ved *AVTALT  (J=alltid(arb.L. OG Vedlegg), V = undertrykk på vedlegg)
     c     UdetaKey      chain     SYPARL1                            33
     C  N33              MOVEL     SYVRDA        UNDETA            1
     C     UNDETA        IFEQ      'V'
     C                   MOVE      'J'           UNDETA
     c                   end
     C*
     C*
     C                   ENDSR
     C*
