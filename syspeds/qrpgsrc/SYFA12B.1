********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FRITEKST PRINTES EN LINJE AV GANGEN
10XXX * 02 jov PTF10 sikre key (user)p brparf
10XXX * 03 YBC PTF10 HENT FIRMAKODE
10XXX * 04 JOV PTF10 overlay hentes nå i CL / FRALIG BRUK AV IND 51
10XXX * 05 YBC PTF10 SKRIV UT TOLLPASS FRAKTBREV
      * 06 JOV PTF10 DIGIT OR SIGN.. DFPNLS
      * 07 JOV PTF10 endring ved Betaler   Sender/mottaker/Annen
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 08 YBC PTF11 SLETT GAMMEL FRAKTBREV OG LAG NYTT
********************************************************************
      ************************************************** *************
      * UTSKRIFT AV FRAKTBREV fra ZEADF (WEB)
      **************************************************************
      *
     FZEADL02   IF   E           K DISK
S08  F*ZRISOL01  IF A E           K DISK
N08  FZRISOL01  UF A E           K DISK
S07  F*ODTFR    IF   E           K DISK
N07  FCUNDL01   IF   E           K DISK
     Fzreetxt1  IF   E           K DISK
     FBRPARF    IF   E           K DISK
N03  FFIRM      IF   E           K DISK
     FSYFA12P   O    E             PRINTER
      *Array for fjerning av foranstilte 0 i tallverdi
     D NUM             S              1    DIM(9)
     D MERKNA          C                   CONST('M E R K N A D E R:')
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D  XULDDMM                7     10  0
     D  XULÅÅ2                13     14  0
     D                 DS
     D  PDATDD                 1      2  0
     D  PDATMM                 3      4  0
     D  PDATÅÅ                 5      6  0
     D  PDATE1                 1      6  0
S01  D*WSMTxt          S            700
S01  D*WSTTxt          S            700
N01  D                 DS
N01  D  WSMTXT                 1    700
N01  D  WMT                    1    700    DIM(10)
N01  D                 DS
N01  D  WSTTXT                 1    700
N01  D  WTT                    1    700    DIM(10)
      *Array for fri-tekster
     D                 DS
     D  TEXT                   1    700
     D  TX                     1    700    DIM(700)
     D                 DS
     D  FTX                    1     70
     D  FT                     1     70    DIM(70)
     D Wpos            s              4  0
     D Gmpos           s              4  0
     D BLF             s              4  0
     D Tst700          s            700
N04  D                 DS
N04  D  DFBREF                 1     17
N04  D  DFBREFX                1     17
N04  D                 DS
N04  D  DFSREF                 1     17
N04  D  DFSREFX                1     17
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'
N08  D UCMD            DS          1024
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      ******************************************
     C     HOVED         BEGSR
      ******************************************
      *
     C     UUUNIK        CHAIN     ZEADL02                            33
      *
     C                   MOVEL     HENAS         DFNAVS
     C                   MOVEL     HEADS1        DFAD1S
S06  C*                  MOVEL     HEADS3        DFPNLS
N06  C                   MOVEL     HEADS3        TMPPUL            4
N06  C                   TESTN                   TMPPUL               34
N06  C     *IN34         IFEQ      '1'
N06  C                   MOVE      TMPPUL        DFPNLS
N06  C                   end
     C                   MOVE      HEADS3        TMPAD325         25
     C                   MOVEL     TMPAD325      DFAD3S
     C                   MOVEL     HENAK         DFNAVM
     C                   MOVEL     HEADK1        DFAD1M
     C                   MOVEL     HEADK3        DFAD3M
     C                   MOVEL     HENAK         DFNAVL
     C                   MOVEL     HEADK1        DFAD1L
     C                   MOVE      *ZEROS        DFPOUL                          UTL.POSTNR
     C                   MOVEL     HEADK3        TMPPUL            4
     C                   TESTN                   TMPPUL               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      TMPPUL        DFPOUL                          UTL.POSTNR
     C                   MOVE      HEADK3        TMPAD325         25
     C                   MOVEL     TMPAD325      DFADUL
     C                   else
     C                   MOVEL     HEADK3        DFADUL
     C                   end
S07   *
S07   *    Belastningskode utfra CC/PP på frankatur
S07  C*    KODFRK        CHAIN     KODTFR                             33
S07  C*                  SELECT
S07  C*    KFRCP         WHENEQ    'PP'
S07  C*    KFRCP         OREQ      'S '
S07  C*                  MOVEL     'S'           DFBELA            1
S07  C*    KFRCP         WHENEQ    'CC'
S07  C*    KFRCP         OREQ      'M '
S07  C*                  MOVEL     'M'           DFBELA
S07  C*                  OTHER
S07  C*                  MOVEL     'S'           DFBELA
S07  C*                  END
      * KODE FOR BETALER SETTES ETTER REELL BETALER, SENDER / MOTTAKER (ELLER ANNEN)
N07  C     HEKNSF        IFNE      *ZEROS
N07  C                   MOVEL     'S'           DFBELA            1
N07  C     HEKNSF        IFNE      HEKNS
N07  C                   MOVEL     'A'           DFBELA
N07  C                   MOVE      HEKNSF        KUNDNR
N07  C                   EXSR      GetAnnen
N07  C                   ENDIF
N07  C                   ELSE
N07  C                   MOVEL     'M'           DFBELA
N07  C     HEKNKF        IFNE      HEKNK
N07  C                   MOVEL     'A'           DFBELA
N07  C                   MOVE      HEKNKF        KUNDNR
N07  C                   EXSR      GetAnnen
N07  C                   ENDIF
N07  C                   ENDIF
      *Belastes: 46 ON=Sender, 47 ON=Mottaker 48 ON=Annen
     C     DFBELA        COMP      'S'                                    46
     C     DFBELA        COMP      'M'                                    47
     C     DFBELA        COMP      'A'                                    48
      *
      * AREF=SENDERS REF
     C                   MOVEL     HERFA         DFSREF
      *
      * Finnes sendingsnr - HVIS IKKE, TILDEL
     C                   MOVE      *BLANKS       WSSN17
     C                   MOVE      *BLANKS       USTMF
     C                   MOVEL     HEFILE        PABRID
     c     HEUNIK        setll     Zrisol01
     c     HEUNIK        Reade     Zrisol01                               33
     c     *in33         doweq     '0'
     C                   SELECT
     C                   WHEN      FSKODE = 'IFB'
     c                   movel     FSSOK         WSSN17
N08  c                   DELETE    FRISOKR
     C                   WHEN      ((FSKODE = '#VD') AND
     C                              (FSSOK = 'Fraktbrev.pdf'))
     C     BrParKey      CHAIN     BRPARF                             33
     C     *in33         ifeq      '1'
S03  c*                  eval      PABRID='*KUNDFT*SY'
N03  C                   eval      PABRID = '*KUNDFT*' + %TRIM(FIFIRM)
     C     BrParKey      CHAIN     BRPARF                             33
     c                   end
     C                   EVAL      USTMF = PAVRDA
     C                   MOVEL     FSUNIK        XAN09             9
     C                   CAT       XAN09:0       USTMF
     C                   CAT       FSDOKK:0      USTMF
     C                   CAT       FSSOK:0       USTMF
N08  c                   DELETE    FRISOKR
N08  c                   OTHER
N08  c                   UPDATE    FRISOKR
     c                   end
     c     HEUNIK        Reade     Zrisol01                               33
     c                   end
N08  C                   IF        USTMF <> *BLANKS
N08  C                   BITON     '123457'      WAPSTR            1
N08  C                   BITOFF    '06'          WAPSTR
N08  C                   Z-ADD     1024          UCMDL            15 5
N08  C                   EVAL      UCMD = 'RMVLNK OBJLNK(' + WAPSTR
N08  C                             + %TRIM(USTMF) + WAPSTR + ')'
N08  C                   CALL      'QCMDEXC'                            33
N08  C                   PARM                    UCMD
N08  C                   PARM                    UCMDL
N08  C                   EVAL      USTMF = *BLANKS
N08  C                   END
      *
     C                   SELECT
     C                   WHEN      ((WSSN17 <> *BLANKS) AND (USTMF <> *BLANKS))
     C                   GOTO      FbnrOK
     C                   WHEN      WSSN17 <> *BLANKS
     C                   MOVE      HEAVD         FSAVD
     C                   MOVE      HEOPD         FSOPD
     C                   MOVE      HEUNIK        FSUNIK
     C                   MOVE      HEREFF        FSREFF
     C                   MOVE      HEOPD         FSOPD
     C                   GOTO      FbnrOK1
     C                   ENDSL
      *
     C                   call      'SYGE15'
     c                   parm                    sndnr20          20
     c                   move      sndnr20       WSSN17           17
     c                   move      HEAVD         FSAVD
     c                   move      HEOPD         FSOPD
     c                   move      'IFB'         FSKODE
     C                   MOVE      *BLANKS       FSSOK
     C                   MOVEL     WSSN17        FSSOK
     c                   move      HEDTOP        FSDTOP
     c                   move      *BLANKS       FSDOKK
     c                   move      HEUNIK        FSUNIK
     c                   move      HEREFF        FSREFF
     C                   write     FRISOKR
      *
     C     FbnrOK1       TAG
N02  C                   MOVEL     HEFILE        PABRID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *in33         ifeq      '1'
S03  c*                  eval      PABRID='*KUNDFT*SY'
N03  C                   eval      PABRID = '*KUNDFT*' + %TRIM(FIFIRM)
     C     BrParKey      CHAIN     BRPARF                             33
     c                   end
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN           10
     C                   EVAL      FSDOKK = NEWRAN
     C                   EVAL      FSSOK = 'Fraktbrev.pdf'
     C                   MOVE      '#VD'         FSKODE
     C                   WRITE     FRISOKR
     C                   EVAL      USTMF = PAVRDA
     C                   MOVEL     FSUNIK        XAN09             9
     C                   CAT       XAN09:0       USTMF
     C                   CAT       NEWRAN:0      USTMF
     C                   CAT       FSSOK:0       USTMF
      *
     C     FbnrOK        TAG
     C                   MOVEL     '401'         WSSN20
     C                   MOVE      WSSN17        WSSN20
      *
      * SKRIV FRAKTBREVET (51 = forminsket formuler som i fax/mail..)
N04  c                   seton                                        51
     C                   WRITE     FAXLAY
      * Skriv varelinjer (ant/vekt...) + FRITEKST
     C                   EXSR      GETVL
      *
s04  C**                 MOVEL     'FB1'         OVL03             3
s04  C**                 MOVEL     OVL03         FB01
s04  C**                 write     ovl
      *
     C                   ENDSR
      *
      *
N07   ******************************************
N07  C     GETANNEN      BEGSR
N07   ******************************************
N07   *
N07  C     KUND1K        CHAIN     CUNDL01                            33
N07  C     *in33         IFEQ      *off
N07  C                   MOVE      KNAVN         DFNAVX           30
N07  C                   MOVE      ADR1          DFAD1X           30
N07  C                   MOVE      ADR2          DFAD2X           30
N07  C                   MOVEL     POSTNR        DFAD3X           30
N07  C                   MOVE      ADR3          DFAD3X
N07  C                   END
N07  C                   ENDSR
      *
      ******************************************
     C     GETVL         BEGSR
      ******************************************
      * LINJE holde rede på hvor en står ETTER siste write
     C                   Z-ADD     28            LINJE             2 0
     C                   MOVE      *ZEROS        X2                3 0
      * Skriv TOTALER hentet fra selve fraktbrevet
     C                   MOVEL     HEGM1         R33MRK
     C                   Z-ADD     HENT          R34ANT
     C                   MOVEL     HEVS1         R35VSL
     C                   Z-ADD     HEVKT         R36VKT
     C     HEM3          IFGT      0
     C     HEM3          MULT      1000          R37DM3
     C                   MOVE      'DM3'         R37TXT
     C                   ELSE
     C                   MOVE      *ZEROS        R37DM3
     C                   MOVE      *BLANKS       R37TXT
     C                   END
     C     HELM          IFGT      0
     C                   Z-ADD     HELM          R37LM
     C                   MOVE      'LM'          R37TX2
     C                   ELSE
     C                   MOVE      *ZEROS        R37LM
     C                   MOVE      *BLANKS       R37TX2
     C                   END
     C                   ADD       1             LINJE
     C                   WRITE     VARELIF
      *
     C                   MOVE      *ZEROS        R34ANT
     C                   MOVE      *ZEROS        R36VKT
     C                   MOVE      *ZEROS        R37DM3
     C                   MOVE      *ZEROS        R37LM
     C                   MOVE      *BLANKS       R37TXT
     C                   MOVE      *BLANKS       R37TX2
     C     HEGM2         IFNE      *BLANKS
     C     HEVS2         ORNE      *BLANKS
     C                   MOVEL     HEGM2         R33MRK
     C                   MOVEL     HEVS2         R35VSL
     C                   ADD       1             LINJE
     C                   WRITE     VARELIF
     C                   END
      *
      *
      * POSISJONER TIL RUBR 38
     C     LINJE         DOWLT     36
     C                   ADD       1             LINJE
     C                   WRITE     BLANKLIN
     C                   END
      * Linjer i rubr R38,  HENT FRA ZREETXT
     C                   exsr      FixFreeTxt
      *
     C                   ENDSR
      *
      *
      *=====================================================================******
      * Fix freetekst
      *=====================================================================******
     C     FixFreeTxt    begsr
N01  C                   Z-ADD     0             XNMT              3 0
N01  C                   Z-ADD     0             XNTT              3 0
      * Hent Fritekst til samlet streng max 700 lang
     C     UUUNIK        SETLL     Zreetxt1
     C     UUUNIK        Reade     Zreetxt1                               33
     C     *in33         doweq     '0'
     c     frtkod        IFEQ      'R'
S01  c*                  cat       FRTTXT:0      WSMTXT
N01  C                   IF        XNMT < 10
N01  C                   ADD       1             XNMT
N01  C                   MOVE      FRTTXT        WMT(XNMT)
N01  C                   END
     c                   end
     c     frtkod        IFEQ      'G'
S01  c*                  cat       FRTTXT:0      WSTTXT
N01  C                   IF        XNTT < 10
N01  C                   ADD       1             XNTT
N01  C                   MOVE      FRTTXT        WTT(XNTT)
N01  C                   END
     c                   end
     C     UUUNIK        Reade     Zreetxt1                               33
     c                   end
      *
     c                   movel     Hex0D         CrLf              2
     c                   move      Hex25         CrLf
      * Melding til mottaker....
     c     WSMtxt        Ifne      *blanks
     C                   ADD       1             LINJE
     c                   eval      D29TXT = 'Melding til mottaker:'
     C                   WRITE     R38LIN
     c                   moveA     WSMTXT        TX
     c                   exsr      SkrivFrt
     c                   end
      * Melding til Transportør.
     c     WSTtxt        Ifne      *blanks
     C                   ADD       1             LINJE
     C                   WRITE     BLANKLIN
     C                   ADD       1             LINJE
     c                   eval      D29TXT = 'Melding til transportør:'
     C                   WRITE     R38LIN
     c                   moveA     WSTTXT        TX
     c                   exsr      SkrivFrt
     c                   end
     c                   endsr
      *=====================================================================******
      * Skriv freetekst
      *=====================================================================******
     C     SkrivFrt      begsr
     C                   z-add     1             GMPOS
     C                   z-add     1             WPOS
      * Neste linje er fram til neste CR/LF eller max 70 tegn
     c     NextCRLF      Tag
S04  C*    CRLF          SCAN      TEXT:GMPOS    WPOS                     51
N04  C     CRLF          SCAN      TEXT:GMPOS    WPOS                     41
      * fant ikke flere CRLF, resten blank? Hopp ut / ellers lagre 70+70.
S04  C*    *IN51         IFEQ      *OFF
N04  C     *IN41         IFEQ      *OFF
     c                   MOVE      *BLANKS       TST700
     c                   movea     TX(GMpos)     TST700
     C     TST700        CABEQ     *BLANKS       UTSKRIV
     c                   z-add     9999          Wpos
     c                   end
      *
      * Lagre uansett fra og med start.sted
     C                   MOVEA     TX(GMPOS)     FT
      * Finn lengde på siste streng(max 70) - blank resten (fra lengde +1)
     C                   EVAL      BLF = WPOS - GMPOS + 1
     C     BLF           IFGT      70
      *(41 OFF. skal IKKE hoppe over CRLF før neste linje.)
S04  C*                  MOVE      *OFF          *IN51
N04  C                   MOVE      *OFF          *IN41
     C                   eval      wpos = GMPOS +70
     c                   end
     C     BLF           IFge      1
     C     BLF           andle     70
     C                   MOVEA     *BLANKS       FT(BLF)
     C                   END
      *
      * Skriv til rubr 38...
     c                   Movea     FT            D29TXT
     C                   ADD       1             LINJE
     C     LINJE         CABGT     60            Utskriv
     C     LINJE         IFGT      51
     C                   MOVE      BLANK20       D29TXT
     C                   END
     C                   WRITE     R38LIN
      *
      * Finn neste start-posisjon (HVIS CRLF - hopp pver)
     C                   Z-add     WPOS          GMPOS
s04  C*    *IN51         IFEQ      *ON
N04  C     *IN41         IFEQ      *ON
     C                   ADD       2             GMPOS
     C                   END
     C     GMPOS         CABLE     700           NEXTCRLF
     C
      *
     c     UtSKRIV       endsr
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUUNIKA           9
     C                   PARM                    USTMF            50
N05  C                   PARM                    UTOLLP            1
N05  C     UTOLLP        COMP      'T'                                    50
     C                   MOVE      UUUNIKA       UUUNIK            9 0
      *
S07  C*    KODFRK        KLIST
S07  C*                  KFLD                    KFRUNI
S07  C*                  KFLD                    HEFR
S07  C*                  MOVE      'FR'          KFRUNI
N07  C     KUND1K        KLIST
N07  C                   KFLD                    FIFIRM
N07  C                   KFLD                    KUNDNR
     C     BrParKey      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'OLBAN'       PAID
      *
     C                   MOVE      *BLANKS       BLANK20          20
      * HENT DATO/KLOKKESLETT Utstedelsesdato
     C                   TIME                    XULTID
     C                   MOVE      XULDD         PDATDD
     C                   MOVE      XULMM         PDATMM
     C                   MOVE      XULÅÅ2        PDATÅÅ
N03   *
N03  C     *LOVAL        SETLL     FIRM
N03  C                   READ      FIRM                                   33
      *
     C                   ENDSR
