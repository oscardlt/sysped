     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '5')
     A            AKONO          7  0H
     A            RFLNO          3  0H
     A            RFDTR          8  0H
     A            RFDTE          8  0H
     A            RFKD           5A  O  5  5
     A            RFREF         35A  O  5 11
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 35SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'GSS019R'
     A                                      COLOR(BLU)
     A                                  1 10'Work with referance'
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7  0O  2 11EDTCDE(4)
     A N27                              3  2'Choose option 2=change 4=delete 5=-
     A                                      display'
     A                                      COLOR(BLU)
     A  27                              3  2'Choose option 5=display'
     A                                      COLOR(BLU)
     A                                  4  2'Op Code  Referance'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30N27                           1  2'F3/F12=End  F5=refresh F6=make new'
     A                                      COLOR(BLU)
     A N30 27                           1  2'F3/F12=End  F5=Refresh'
     A                                      COLOR(BLU)
     A  30        MSGTXT        42A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS019R'
     A                                      COLOR(BLU)
     A                                  1 10'Work with referance'
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7  0O  2 11EDTCDE(4)
     A                                  4  2'Op Code  Referance'
     A                                      COLOR(WHT)
     A N27                             10  2'F3/F12=End F5=refresh F6=make new'
     A                                      COLOR(BLU)
     A  27                             10  2'F3/F12=End  F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A                                      WINDOW(*DFT 06 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS019R'
     A                                      COLOR(BLU)
     A                                  1 10'Work with refarance'
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7  0O  2 11EDTCDE(4)
     A                                  3  2'Kode:'
     A            WSKD           5A  B  3  8DSPATR(UL)
     A N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                  4  2'Ref.:'
     A            WSREF         35   B  4  8DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                  5  2'F4=Search F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        42A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 06 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'GSS013R'
     A                                      COLOR(BLU)
     A                                  1 10'Work with referance'
     A                                      COLOR(WHT)
     A                                  2  2'Orderno.'
     A            UONO           7  0O  2 11EDTCDE(4)
     A                                  3  2'Code:'
     A            RFKD           5A  O  3  8DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                  3 15'R.dt:'
     A            W2DTR          6  0O  3 21DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  3 30'E.dt:'
     A            W2DTE          6  0O  3 36DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  4  2'Ref.:'
     A            RFREF         35A  O  4  8DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  2'F4=Search F12=Cancel'
     A                                      COLOR(BLU)
