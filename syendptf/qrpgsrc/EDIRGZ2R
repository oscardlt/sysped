     fedis4     if   e           k disk    usropn
     fedire1k   uf   e             disk    usropn
     fedise1k   uf   e             disk    usropn
     d runcmd          pr                  extpgm('QCMDEXC')
     d  cmd                          36a   const
     d  len                          15p 5 const
     d wcmd            s             36
     ‚*//////////////
     ‚*  Hodelogikk /
     ‚*//////////////
      /free
       exsr sra;
       exsr srb;
       exsr src;
       *inlr=*on;
       //////////////////////////////////////////////////////////////
       //  Initiering                                           SRA /
       //////////////////////////////////////////////////////////////
       begsr sra;

       wcmd = *blanks;
       wcmd = 'OVRDBF FILE(EDIS4) FRCRATIO(*NONE)';
       runcmd(wcmd:36);
       open edis4;

       wcmd = *blanks;
       wcmd = 'OVRDBF FILE(EDIRE1K) FRCRATIO(*NONE)';
       runcmd(wcmd:36);
       open edire1k;

       wcmd = *blanks;
       wcmd = 'OVRDBF FILE(EDISE1K) FRCRATIO(*NONE)';
       runcmd(wcmd:36);
       open edise1k;

       endsr;
       //////////////////////////////////////////////////////////////
       //  Les EDIRE1K                                          SRB /
       //////////////////////////////////////////////////////////////
       begsr srb;

           dou %eof(edire1k);
             read edire1k;
             if %eof(edire1k);
               leave;
             endif;

               setll rembr edis4;
                 if not %equal();
                   delete edirer;
                 endif;

           enddo;

       endsr;
       //////////////////////////////////////////////////////////////
       //  Les EDISE1K                                          SRC /
       //////////////////////////////////////////////////////////////
       begsr src;

           dou %eof(edise1k);
             read edise1k;
             if %eof(edise1k);
               leave;
             endif;

               setll rembr edis4;
                 if not %equal();
                   delete ediser;
                 endif;

           enddo;

       endsr;
      /end-free
