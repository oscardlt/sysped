     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            SFNR           1A  B  8  3
     A  51                                  DSPATR(PR)
     A                                      VALUES(' ' '1' '2' '4' '5')
     A            SUKUND    R        O  8  7REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A            KNAVN     R        O  8 17REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A            ADR1      R        O  8 48REFFLD(KUNDR/ADR1 *LIBL/CUNDF)
     A            ADR2      R        O  9 17REFFLD(KUNDR/ADR2 *LIBL/CUNDF)
     A            POSTNR    R        O  9 48REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
     A            ADR3      R        O  9 53REFFLD(KUNDR/ADR3 *LIBL/CUNDF)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      COLOR(BLU)
     A                                  1  2'SYSPED/SYPO30'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0268 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(012 DIV0688 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF12(12 'tilbake')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNNN
     A                                      CF12(12 'tilbake')
     A                                      CF04(04 'søking')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            SUKUND    R        B  1  6REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A                                  2  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNNS
     A                                      CF12(12 'tilbake')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            SUKUND    R        O  1  6REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A            KNAVN     R        O  1 15REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A                                  1 48MSGCON(015 SHU0267 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
