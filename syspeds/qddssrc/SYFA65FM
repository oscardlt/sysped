     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA65PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KODER FRIE-
     A                                       SØKEVEIER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '2' '4' '1')
     A            KFSOKO    R        O  6  8REFFLD(KODFRIR/KFSOKO *LIBL/KODFRI)
     A            KFSOTX    R        O  6 20REFFLD(KODFRIR/KFSOTX *LIBL/KODFRI)
     A            KFSODK    R        O  6 54REFFLD(KODFRIR/KFSODK *LIBL/KODFRI)
     A            KFSTTU    R        O  6 68REFFLD(KODFRIR/KFSTTU *LIBL/KODFRI)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA65'
     A                                      COLOR(BLU)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A N97                              1 19MSGCON(050 SHU0135 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  97                              1 19MSGCON(050 JOV0256 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  97                              4  2MSGCON(015 DIV2237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 18MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU0136 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'søk')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(078 SHU0136 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSSOKO         3A  B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      TEXT('SIGNATUR')
     A            WSSOTX        30A  B  2 20DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      TEXT('NAVN PÅ SIGNATUR')
     A                                      CHECK(LC)
     A            WSSODK    R        B  2 54REFFLD(KODFRIR/KFSODK *LIBL/KODFRI)
     A  88                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            WSSTTU    R        B  2 68REFFLD(KODFRIR/KFSTTU *LIBL/KODFRI)
     A  87                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2SOKO         3A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(070 SHU0101 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A            KFSOKO         3A  O  2  8DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            KFSOTX        30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFSODK    R        O  2 54REFFLD(KODFRIR/KFSODK *LIBL/KODFRI)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            KFSTTU    R        O  2 69REFFLD(KODFRIR/KFSTTU *LIBL/KODFRI)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA65'
     A                                      COLOR(BLU)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A N97                              1 19MSGCON(050 SHU0135 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  97                              1 19MSGCON(050 JOV0256 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  97                              4  2MSGCON(015 DIV2237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 18MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU0136 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
