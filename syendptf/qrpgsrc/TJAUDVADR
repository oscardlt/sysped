      *********************************************************************
      * Add/update/delete av vareadresse
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJAUDVADR MODULE(*LIBL/TJAUDVADR *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 oppdater kundes kundenummer
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FVADR      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FVADR1     IF   E           K DISK    USROPN
     F                                     RENAME(VADRR:VADRR1)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D AVD             S              4  0
     D OPD             S              7  0

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬kundnr¬ : ¬'+%trim(%editc(wkundnr:'Z'))+'¬, '
          +'¬vadrnr¬ : ¬'+%trim(%editc(wvadrnr:'Z'))+'¬, '
          +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
          +'¬errMsg¬ : ¬'+%trim(Error)+'¬} ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     VADR
     C  N50              CLOSE     VADR1

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('wkundnr')
     C                   EVAL      wkundnr    = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('wvadrnr')
     C                   EVAL      wvadrnr    = c2n2(WA)
     C                   EVAL      WMODE      = zhbgetvar('MODE')
      * WMODE = A --> Lage ny post
      *         U --> Oppdater post
      *         D --> Slett post
      *         I --> Spør post
     C                   EVAL      vadrna     = zhbgetvar('vadrna')
     C     LO:UPC        XLATE     vadrna        vadrna
     C                   EVAL      vadrn1     = zhbgetvar('vadrn1')
     C     LO:UPC        XLATE     vadrn1        vadrn1
     C                   EVAL      vadrn2     = zhbgetvar('vadrn2')
     C     LO:UPC        XLATE     vadrn2        vadrn2
     C                   EVAL      vadrn3     = zhbgetvar('vadrn3')
     C     LO:UPC        XLATE     vadrn3        vadrn3
     C                   EVAL      valand     = zhbgetvar('valand')
     C     LO:UPC        XLATE     valand        valand
     C                   EVAL      vakure     = zhbgetvar('vakure')             Kundes kundeNr
     C     LO:UPC        XLATE     vakure        vakure
     C                   EVAL      vatlf      = zhbgetvar('vatlf')
     C     LO:UPC        XLATE     vatlf         vatlf
     C                   EVAL      vafax      = zhbgetvar('vafax')
     C     LO:UPC        XLATE     vafax         vafax
     C                   EVAL      vamail     = zhbgetvar('vamail')
     C     LO:UPC        XLATE     vamail        vamail
      *
     C                   ENDSR
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     VADRKEY       KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WKUNDNR           8 0
     C                   KFLD                    WVADRNR           5 0
     C     VADRKEYE      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WKUNDNR
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      VADR
      * dummy for å få feltnavn som arbeids/inputvariable
     C     *IN50         IFEQ      *OFF
     C     *IN50         ANDEQ     *ON
     C                   OPEN      VADR1
     C                   READ      VADR1
     C                   ENDIF
      *
     c                   endsr
      *
      *********************************************************
     C     TEST          BEGSR
      *********************************************************
     C                   IF        WMODE = 'A'
      * Ved ADD - enten et styrt nummer eller legg til neste
     C                   IF        WVADRNR <> 0
     C     VADRKEY       CHAIN(N)  VADR                               33
     C                   IF        *IN33 = *OFF
     C                   eval      Error = 'AdresseNummeret finnes allerede'
     c                   goto      UtTest
     C                   ENDIF
     C                   ELSE
     c                   z-add     99999         WVADRNR
     C     VadrKey       setgt     VADR
     C     VadrKeyE      readpe    VADR
     c                   eval      WVADRNR = XVADRNR +1
     c                   clear                   VADRR
     C                   ENDIF
      * Mode Update/Delete
     C                   ELSE
      * Feil hvis vadr ikke finnes
     C     VADRKEY       CHAIN(N)  VADR                               33
     C                   IF        *IN33=*ON
     C                   eval      Error = 'AdresseNummeret finnes ikke'
     c                   goto      UtTest
     C                   ENDIF
     C                   ENDIF
      *
     C                   IF        WMODE = 'A' OR WMODE = 'U'
     C                   IF        VADRNA = *BLANKS  or
     C                             VADRN1 = *BLANKS  or
     C                             VADRN3 = *BLANKS
     C                   eval      Error = 'Navn/Adr 1/Postnr+Sted MÅ angis'
     c                   goto      UtTest
     C                   ENDIF
      *
     C                   ENDIF
      *
      * MODE A=Add   U=Update   D=Delete
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C     WMODE         OREQ      'U'
     C                   EXSR      SRAU
     C     WMODE         WHENEQ    'D'
     C                   EXSR      SRD
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
      *
     C     UtTest        ENDSR
      *
      *********************************************************
     C     SRAU          BEGSR
      *********************************************************
      * forberede visse felter
     c                   exsr      FixData
      * Add/update
     C     WMODE         ifeq      'A'
     C                   EVAL      XAKTKOD = 'A'
     C                   EVAL      XFIRMA  = BIFIRM
     C                   EVAL      XKUNDNR = wkundnr
     C                   EVAL      XVADRNR = wvadrnr
     C                   EVAL      XSONAVN = VADRNA                             10 første av navn
     C                   else
     C     VADRKEY       CHAIN     VADR                               33
     c                   endif
     C                   EVAL      XVADRNA  = VADRNA
     C                   EVAL      XVADRN1  = VADRN1
     C                   EVAL      XVADRN2  = VADRN2
     C                   EVAL      XVADRN3  = VADRN3
     C                   EVAL      XVALAND  = VALAND
     C                   EVAL      XVATLF   = VATLF
     C**                 EVAL      XVAFAX   = VAFAX
     C                   EVAL      XVAMAIL  = VAMAIL
N01  C                   EVAL      XVAKURE  = VAKURE

     C     WMODE         ifeq      'A'
     C                   WRITE     VADRR
     C                   else
     C                   UPDATE    VADRR
     C                   end
      *

     C                   ENDSR
      *********************************************************
     C     FixData       BEGSR
      *********************************************************
      *

     C                   ENDSR
      *
      *********************************************************
     C     SRD           BEGSR
      *********************************************************
      * Delete
     C     VADRKEY       CHAIN     VADR                               33
     C  N33              DELETE    VADRR
      *
     C                   ENDSR
