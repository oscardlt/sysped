     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 42 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4')
     A  27                                  DSPATR(PR)
     A            TVCNR         17A  O  5  5
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX018R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll container  (rubr. 31)'
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny  2=Ændra 4=Radera'
     A                                      COLOR(BLU)
     A                                  3  2'Op Containernr'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2CNR         17A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                 11  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(10)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2'   Containernr'
     A                                      COLOR(WHT)
     A            WSCNR         17A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A N30                              3  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX018R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll container  (rubr. 31)'
     A                                      COLOR(WHT)
     A N27                              2  2'1=Lag 2=Ændra 4=Radera'
     A                                      COLOR(BLU)
     A                                  3  2'Op Containernr'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2CNR         17A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A N30                             11  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
