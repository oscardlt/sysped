     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R WINDOW
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CF10(10 'FORDEL')
     A                                      CA12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'Taksering/fordeling HENTE-oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Samlet...'
     A                                  3  2' Vekt(1)  Kubikk(2)  Lastem(3) Hov-
     A                                      .frakt(4)'
     A  75        IFIPRP         4Y 2O  3 46EDTCDE(1)
     A  75                              3 52'% ='
     A            SUMVKT         6Y 0O  4  3EDTCDE(1)
     A  71                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            SUMM3          5Y 3O  4 12EDTCDE(4)
     A  72                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            SUMLM          4Y 2O  4 25EDTCDE(4)
     A  73                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            SUMHFR         7Y 0O  4 35EDTCDE(4)
     A  74                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A  75        SUMHFP         7Y 0O  4 45EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  6  2'Innh.beløp'
     A            WSFRAK         1A  B  6 15COLOR(YLW)
     A            WSBEL          7Y 0B  6 17EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  6 27'Fordel etter:'
     A            WSNØKK         1A  B  6 41COLOR(YLW)
     A N71                              6 43'1=Vkt'
     A N72                              6 49'2=Kub'
     A                                  7  2'Min. fra kunde '
     A            WSMIKK         1A  B  7 18COLOR(YLW)
     A            WSMINK         4Y 0B  7 20EDTCDE(4)
     A                                      COLOR(YLW)
     A N73                              7 43'3=Lm.'
     A N74                              7 49'4=H.fr '
     A                                 10  2'Grl. M-beregn:'
     A            WSFPNR         4A  B 10 17
     A                                 10 23'->'
     A            WSTPNR         4A  B 10 26
     A                                 11  2'Franka'
     A            WSFRAN         3A  B 11  9
     A                                 11 13'Prodkode:'
S02  A*           WSPROD         1A  B 11 23
     A            WSPRO2         1A  B 11 23
     A                                 11 25'Avd'
     A            WSAVD          4Y 0B 11 29EDTCDE(4)
     A                                 12  2'F10=Utfør fordeling'
     A                                      COLOR(BLU)
     A                                 12 30MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
