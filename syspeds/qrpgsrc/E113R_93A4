     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI93A   IF   E           K DISK
     FEDOC93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FETOD93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FEPCI93A   IF   E           K DISK
     FETDT93A   IF   E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     FZRISOK    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WPOS1                  1      3  0
     D  WPOS1A                 1      3
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * FTX15                                               FTX15 /
      *////////////////////////////////////////////////////////////
     C     FTX15         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     15            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'

     C     F4440A        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  F4451         FRTTXT
     C     FRTTXT        CAT       ':':0         FRTTXT
     C     FRTTXT        CAT       F4440A:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  F4451         FRTTXT
     C     FRTTXT        CAT       ':':0         FRTTXT
     C     FRTTXT        CAT       F4440B:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF

     C     KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX31                                               FTX31 /
      *////////////////////////////////////////////////////////////
     C     FTX31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'

     C     F4451         IFEQ      'AAA'
     C     HEVS1         CAT       F4440A:1      HEVS1
     C                   MOVEL     F4440B        HEVS2
     C                   MOVE      '1'           *IN52
     C                   END

     C     KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID31                                               GID31 /
      *////////////////////////////////////////////////////////////
     C     GID31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID93A                                52
     C     *IN52         DOWEQ     '0'

     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          HENT
     C     HEVS1         IFEQ      *BLANKS
     C                   SELECT
     C     G7065A        WHENEQ    'RO'
     C                   MOVEL     'RLL'         HEVS1
     C     G7065A        WHENEQ    'BI'
     C                   MOVEL     'CNT'         HEVS1
     C     G7065A        WHENEQ    'CS'
     C                   MOVEL     'KSS'         HEVS1
     C     G7065A        WHENEQ    'CT'
     C                   MOVEL     'KRT'         HEVS1
     C     G7065A        WHENEQ    'CG'
     C                   MOVEL     'BUR'         HEVS1
     C     G7065A        WHENEQ    'SK'
     C                   MOVEL     'KSS'         HEVS1
     C     G7065A        WHENEQ    'BK'
     C                   MOVEL     'KURV'        HEVS1
     C     G7065A        WHENEQ    'DR'
     C                   MOVEL     'TRM'         HEVS1
     C     G7065A        WHENEQ    'BH'
     C                   MOVEL     'BHT'         HEVS1
     C     G7065A        WHENEQ    'BN'
     C                   MOVEL     'BLL'         HEVS1
     C     G7065A        WHENEQ    'SA'
     C                   MOVEL     'SKK'         HEVS1
     C     G7065A        WHENEQ    'EN'
     C                   MOVEL     'KNV'         HEVS1
     C     G7065A        WHENEQ    'SW'
     C                   MOVEL     'PLL'         HEVS1
     C     G7065A        WHENEQ    'PE'
     C                   MOVEL     'PLL'         HEVS1
     C     G7065A        WHENEQ    'EP'
     C                   MOVEL     'PLL'         HEVS1
     C     G7065A        WHENEQ    'GP'
     C                   MOVEL     'PLL'         HEVS1
     C     G7065A        WHENEQ    'ST'
     C                   MOVEL     'STK'         HEVS1
     C                   OTHER
     C                   MOVEL     'KLL'         HEVS1
     C                   ENDSL

     C                   ELSE
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  G7224A        FRTTXT
     C     FRTTXT        CAT       G7065A:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF

     C     KEYMS1        READE     EGID93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    HEHAWB        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HEREFF        WEREFF
     C     *LIKE         DEFINE    D2380         W2380
     C     *LIKE         DEFINE    HEKNA         WEKNA
     C     *LIKE         DEFINE    HENAA         WENAA
     C     *LIKE         DEFINE    HERFA         WERFA
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
     C                   Z-ADD     0             WEKNA
     C                   MOVE      *BLANKS       WERFA
     C                   MOVE      *BLANKS       WENAA

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA34                                               MEA34 /
      *////////////////////////////////////////////////////////////
     C     MEA34         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     34            ZGN1
     C     KEYMS1        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA93A                                52
     C     *IN52         DOWEQ     '0'

     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   ADD       ZNUM          HEVKT
     C     M6311         WHENEQ    'LMT'
     C                   ADD       ZNUM          HELM
     C     M6311         WHENEQ    'VOL'
     C                   ADD       ZNUM          HEM3
     C                   ENDSL

     C     KEYMS1        READE     EMEA93A                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD25                                               NAD25 /
      *////////////////////////////////////////////////////////////
     C     NAD25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD93A                                52
     C     *IN52         DOWEQ     '0'

     C                   SELECT
     C     N3035         WHENEQ    'CZ'                                         GODSAVSENDER
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAS
     C                   MOVEL     N3124A        HENASF
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADS1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADS2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADS3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   MOVEL     N3036A        HENASF
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADS3
     C                   MOVE      WEADS2        HEADS3
     C                   ENDIF
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1
     C                   MOVEL     N3164         WEADS2
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   ENDIF

     C                   ENDSL

     C     KEYMS1        READE     ENAD93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * PCI37                                               PCI37 /
      *////////////////////////////////////////////////////////////
     C     PCI37         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     37            ZGN1
     C     KEYMS1        SETLL     EPCI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPCI93A                                52
     C     *IN52         IFEQ      '0'

     C                   MOVEL     P7102A        HEGM1
     C                   MOVEL     P7102B        HEGM2

     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF18                                               RFF18 /
      *////////////////////////////////////////////////////////////
     C     RFF18         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     18            ZGN1
     C     KEYMS1        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF93A                                52
     C     *IN52         DOWEQ     '0'

     C                   MOVEL(P)  R1153         FSKODE
     C                   MOVEL(P)  R1154         FSSOK
     C                   WRITE     FRISOKR

     C  N52KEYMS1        READE     ERFF93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF29                                               RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     29            ZGN1
     C     KEYMS1        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF93A                                52
     C     *IN52         DOWEQ     '0'

     C     R1153         IFEQ      'ACL'
     C                   MOVEL     R1154         HERFA
     C                   MOVEL     R1154         HEREFF
     C                   MOVE      '1'           *IN52
     C                   ENDIF

     C  N52KEYMS1        READE     ERFF93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      *______
      * RFF01
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     01            ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'

     C     R1153         IFEQ      'FF '
     C                   MOVEL     R1154         WEREFF
     C                   MOVE      '1'           *IN51
     C                   END

     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              END
     C                   END
      *______
      * TDT05
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT93A                                51
     C     *IN51         DOWEQ     '0'

     C                   MOVEL     T8212         WEHAWB
     C                   MOVEL     T8453         WETRI

     C  N51KEYMS0        READE     ETDT93A                                51
     C  N51              END
     C                   END
      *______
      * DTM05
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     EDTM93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM93A                                51
     C     *IN51         DOWEQ     '0'

     C     D2005         IFEQ      '186'
     C                   MOVEL     D2380         W2380
     C                   MOVE      '1'           *IN51
     C                   END

     C  N51KEYMS0        READE     EDTM93A                                51
     C  N51              END
     C                   END
      *______
      * NAD09
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     9             ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'

     C     N3035         IFEQ      'CS '
     C                   SELECT
     C     N3039         WHENEQ    '04900000002'
     C                   Z-ADD     1199305       WEKNA
     C                   MOVEL     'Neuss       'WENAA
     C     N3039         WHENEQ    '04900000003'
     C                   Z-ADD     1199306       WEKNA
     C                   MOVEL     'Frankfurt   'WENAA
     C     N3039         WHENEQ    '04900000004'
     C                   Z-ADD     1199307       WEKNA
     C                   MOVEL     'Hamburg     'WENAA
     C     N3039         WHENEQ    '04900000005'
     C                   Z-ADD     1199419       WEKNA
     C                   MOVEL     'Køln        'WENAA
     C     N3039         WHENEQ    '04900000006'
     C                   Z-ADD     1199420       WEKNA
     C                   MOVEL     'Memmingen   'WENAA
     C     N3039         WHENEQ    '04900000007'
     C                   Z-ADD     1199314       WEKNA
     C                   MOVEL     'Munchen     'WENAA
     C     N3039         WHENEQ    '04900000008'
     C                   Z-ADD     1199424       WEKNA
     C                   MOVEL     'Wangen      'WENAA
     C     N3039         WHENEQ    '04900000010'
     C                   Z-ADD     1199421       WEKNA
     C                   MOVEL     'Neu-Ulm     'WENAA
     C     N3039         WHENEQ    '04900000011'
     C                   Z-ADD     1199311       WEKNA
     C                   MOVEL     'Kornwestheim'WENAA
     C     N3039         WHENEQ    '04900000012'
     C                   Z-ADD     1199301       WEKNA
     C                   MOVEL     'Augsburg    'WENAA
     C     N3039         WHENEQ    '04900000013'
     C                   Z-ADD     1199315       WEKNA
     C                   MOVEL     'Nurnberg    'WENAA
     C     N3039         WHENEQ    '04900000014'
     C                   Z-ADD     1199308       WEKNA
     C                   MOVEL     'Hannover    'WENAA
     C     N3039         WHENEQ    '04900000016'
     C                   Z-ADD     1199302       WEKNA
     C                   MOVEL     'Berlin      'WENAA
     C     N3039         WHENEQ    '04900000017'
     C                   Z-ADD     1199313       WEKNA
     C                   MOVEL     'Mannheim    'WENAA
     C     N3039         WHENEQ    '04900000018'
     C                   Z-ADD     1199413       WEKNA
     C                   MOVEL     'Dortmund    'WENAA
     C     N3039         WHENEQ    '04900000019'
     C                   Z-ADD     1199415       WEKNA
     C                   MOVEL     'Alsdorf     'WENAA
     C     N3039         WHENEQ    '04900000020'
     C                   Z-ADD     1199423       WEKNA
     C                   MOVEL     'Regensburg  'WENAA
     C     N3039         WHENEQ    '04900000021'
     C                   Z-ADD     1199316       WEKNA
     C                   MOVEL     'Bremen      'WENAA
     C     N3039         WHENEQ    '04900000022'
     C                   Z-ADD     1199425       WEKNA
     C                   MOVEL     'Saarbrucken 'WENAA
     C     N3039         WHENEQ    '04900000024'
     C                   Z-ADD     1199417       WEKNA
     C                   MOVEL     'Freiburg    'WENAA
     C     N3039         WHENEQ    '04900000027'
     C                   Z-ADD     1199303       WEKNA
     C                   MOVEL     'Bielefeld   'WENAA
     C     N3039         WHENEQ    '04900000028'
     C                   Z-ADD     1199309       WEKNA
     C                   MOVEL     'Hof         'WENAA
     C     N3039         WHENEQ    '04900000030'
     C                   Z-ADD     1199414       WEKNA
     C                   MOVEL     'Singen      'WENAA
     C     N3039         WHENEQ    '04900000032'
     C                   Z-ADD     1199312       WEKNA
     C                   MOVEL     'Landsberg   'WENAA
     C     N3039         WHENEQ    '04900000033'
     C                   Z-ADD     1199416       WEKNA
     C                   MOVEL     'Radeburg    'WENAA
     C     N3039         WHENEQ    '04900000039'
     C                   Z-ADD     1199437       WEKNA
     C                   MOVEL     'Rheine      'WENAA
     C     N3039         WHENEQ    '04900000058'
     C                   Z-ADD     1200329       WEKNA
     C                   MOVEL     'Lauterach   'WENAA
     C     N3039         WHENEQ    '04900000063'
     C                   Z-ADD     1200019       WEKNA
     C                   MOVEL     'Innsbruck   'WENAA
     C                   ENDSL
     C                   MOVE      '1'           *IN51
     C                   END

     C  N51KEYMS0        READE     ENAD93A                                51
     C  N51              END
     C                   END
      *______
      * DOC11
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     11            ZGN0
     C     KEYMS0        SETLL     EDOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDOC93A                                51
     C     *IN51         DOWEQ     '0'

     C                   MOVEL     D1004         WERFA

     C  N51KEYMS0        READE     EDOC93A                                51
     C  N51              END
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI93A                                51
     C     *IN51         DOWEQ     '0'

     C                   EXSR      RTVDB

     C     KEYMS0        READE     ECNI93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
      * OPPDRAG
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   Z-ADD     1201          HEAVD
     C                   MOVEL     'EDI'         HESG
     C                   MOVEL     '???'         HESDF
     C                   MOVEL     '1000'        HESDT
     C                   Z-ADD     WEKNA         HEKNA
     C                   MOVEL     '1'           HESDLK
     C                   MOVEL     '1'           HESDL
     C                   MOVEL     WENAA         HENAA
     C                   MOVEL     WERFA         HERFA
     C                   MOVEL     'DE'          HELKA
     C                   MOVEL     'X'           HEKDFS
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFCSUM'      HETYPE
     C                   MOVE      '93A'         HELVL
     C                   Z-ADD     ZMN           HESNN
     C     MSN           MULT      10            HEUNIK
     C                   MOVEA     C1490         A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          HEUNIK
     C                   Z-ADD     ZNUM          WPOS1
     C                   MOVEL     WPOS1A        HEPOS1
     C                   MOVEL     WEREFF        HEREFF
     C                   MOVEL     WEHAWB        HEHAWB
     C                   MOVEL     WETRI         HETRI
     C                   MOVEL     WETRC         HETRC
     C                   MOVEL     WETRCN        HETRCN
      * FRITEKST
     C                   CLEAR                   FREETXTR
     C                   Z-ADD     HEAVD         FRTAVD
     C                   Z-ADD     HEUNIK        FRUNIK
     C                   MOVEL     HEREFF        FRREFF
      * FRIE SØKEVEIER
     C                   CLEAR                   FRISOKR
     C                   Z-ADD     HEAVD         FSAVD
     C                   Z-ADD     HEUNIK        FSUNIK
     C                   MOVEL     HEREFF        FSREFF
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      FTX15
     C                   EXSR      TOD17
     C                   EXSR      RFF18
     C                   EXSR      NAD25
     C                   EXSR      RFF29
     C                   EXSR      GID31
     C                   EXSR      FTX31
     C                   EXSR      MEA34
     C                   EXSR      PCI37
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR

     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD17                                               TOD17 /
      *////////////////////////////////////////////////////////////
     C     TOD17         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     17            ZGN1
     C     KEYMS1        SETLL     ETOD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD93A                                52
     C     *IN52         IFEQ      '0'

     C                   SELECT
     C     T4053         WHENEQ    'EXW'
     C                   MOVEL     '001'         HEFR
     C     T4053         WHENEQ    'DDP'
     C                   MOVEL     '024'         HEFR
     C     T4053         WHENEQ    'DDU'
     C                   MOVEL     '022'         HEFR
     C     T4053         WHENEQ    'DAF'
     C                   MOVEL     '016'         HEFR
     C     T4053         WHENEQ    'DD1'
     C                   MOVEL     '019'         HEFR
     C     T4053         WHENEQ    'DD2'
     C                   MOVEL     '019'         HEFR
     C     T4053         WHENEQ    'DD3'
     C                   MOVEL     '024'         HEFR
     C     T4053         WHENEQ    'DD4'
     C                   MOVEL     '024'         HEFR
     C     T4053         WHENEQ    'ZZZ'
     C                   MOVEL     'XXX'         HEFR
     C                   ENDSL

     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
