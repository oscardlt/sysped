     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Gå til forrige bilde')
     A                                      HELP
      *
     A          R SVX053D1
     A                                  1  3'SYSPED/SVX053R'
     A                                      COLOR(BLU)
     A                                  1 34'NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  2  3'Firmakod:'
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 24'Underhåll standardupplysningar'
     A                                      COLOR(WHT)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2'Vælj och trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'Avdelning. . . . . . . . . . . . :'
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2'F3 = Avsluta'
     A                                      COLOR(BLU)
     A  99                             24  2'Angiven avdelning ogiltig.'
     A                                      COLOR(WHT)
      *
     A          R SVX053D2
     A                                  1  3'SYSPED/SVX053R'
     A                                      COLOR(BLU)
     A                                  1 35'NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 76'Del-1'
     A                                      COLOR(YLW)
     A                                  2 24'Underhåll standardupplysningar'
     A                                      COLOR(WHT)
     A                                  4  2'Avdelning'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A            WSTDN          7Y 0B  7 40TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 13  4'Førenklad procedur? '
     A            WSENKL         1A  B 13 40TEXT('FORENKLET PROSEDYRE?')
     A                                      DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 13 48'(J=Førenklad  N=Normal)'
     A            W0004         35A  B 14 40TEXT('NODI ID AVDELINGEN')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  COLOR(RED)
     A            W0010         35A  B 15 40TEXT('NODI ID TOLLVESENET')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A  97                                  COLOR(RED)
     A                                 16  8'Aplication Reference: '
     A            W0026         14A  B 16 40
     A                                 17  4MSGCON(033 DIV1723 *LIBL/SYMSGF)
     A            W0035          1A  B 17 40TEXT('Testindikator')
     A                                      VALUES(' ' '1' '2')
     A                                 23  2'F3=Avsluta             '
     A                                      COLOR(BLU)
     A                                 23 28'F12=Avbryt                     '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  6  2'Vælj och tryck Enter:'
     A                                      COLOR(BLU)
     A                                  7  4'Rækneverk intern referans:'
     A                                 14  4'UNB ID Avdelningen:'
     A                                 15  6'UNB ID Tullverket: '
      *
     A          R SVX053D3
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SVX053R'
     A                                      COLOR(BLU)
     A                                  1 29'NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 52'Ref.'
     A            WSAAVD         4Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A            TISG           3A  O  1 70COLOR(WHT)
     A                                  1 76'Del-2'
     A                                      COLOR(YLW)
     A                                  3  2'MRN-nr.'
     A            WSTRNR        18A  B  3 13TEXT('MRN-nr')
     A                                      COLOR(YLW)
     A                                  4  2'Godsnr.'
     A            WSGN          35A  B  4 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 52'Språkkod '
     A            WSGNSK         2A  B  4 62DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  2'Avs.land'
     A            WSALK          2A  B  5 13TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2'Avt.lag.pl'
     A            WSALSK        17A  B  7 13DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSALS         35A  B  7 31DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 67'Språkkod '
     A            WSALSS         2A  B  7 77DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8  2'Godk.lagpl'
     A            WSGLSK        17A  B  8 13DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Contr.pl  '
     A            WSACTS        17A  B  9 13DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11  2'Freml.t.pl'
     A            WSTSB          8A  B 11 13DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 28'Språkkod '
     A            WSSKB          2A  B 11 38DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'Næringsliv'
     A            WSKN           8Y 0B 13 13TEXT('Kundenr')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTIN         17A  B 13 22TEXT('Tin kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNA          30A  B 14 13TEXT('Navn')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1         30A  B 15 13TEXT('Adresse-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPN           9A  B 16 13TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPS          24A  B 17 13TEXT('Poststed')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'Landkod '
     A            WSLK           2A  B 18 13TEXT('Landkode')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18 22'Språkkod '
     A            WSSK           2A  B 18 32TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 22  2'Registreringsdat '
     A            THDT           8Y 0H
     A            WSDT           6Y 0O 22 20TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 23  1'F3=Avsluta              '
     A                                      COLOR(BLU)
     A                                 23 28'F4=Søk koder '
     A                                      COLOR(BLU)
     A                                 23 44'F12=Avbryt                      '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  2 19'Underhåll av standardupplysningar'
     A                                      COLOR(WHT)
