      * Kun hovedopplysninger i dette programet.
      * For repeterbar informasjon finnes egne programer:
      * TJGE11R     RPGLE       TomCat/JSON Oppdragsfritekst liste (kun A, 1)
      * TJGE12R     RPGLE       TomCat/JSON Oppdragsdokumenter
      * TJGE13R     RPGLE       TomCat/JSON Oppdragsfakturar
      * TJGE14R     RPGLE       TomCat/JSON OppdragsFrie søkeveier
      * TJGE15R     RPGLE       TomCat/JSON Oppdrags T&T
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 OBS CRTPGM var: ACTGRP(CGI) (og TJSYSO02R) endret til: ACTGRP(*NEW)
11XXX *
********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJSYSO02 MODULE(*LIBL/TJSYSO02 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(headfr:headfr17)
     FFIRM      IF   E           K DISK    USROPN
     FFRISOL01  IF   E           K DISK    USROPN
     FKOFAST    IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
     FTRACKT    IF   E           K DISK    USROPN
     FGSSCGL02  IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D LNG             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D KN              S              8  0 DIM(30)
     D DIR             s              1
     D WSAVD           S              4
     D WSOPD           S              7
     D EXTREF          S             35
     D WSSOK           S             80
     D WSSOKURL        S            200
     D WSETA           S              8S 0
     D WSETD           S              8S 0
     D UtText          s            150
     D Error           S            150
     D JSONstring      s          64000
     D ANTSND          s              5  0
     D SNR             s             17
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')

     D                 DS
     D  DSAVOP                 1     12
     D  DSAVD                  1      4
     D  Strek                  5      5
     D  DSOPD                  6     12
     D  DSOPD5                 6     10
     D                 DS
     D  DFS                    1     10    DIM(10)
     D  KFSODK                 1     10
     D                 DS
     D  KFTXT                  1     50
     D  KFTXTB                 5     49
     D  VisWEB                50     50
     D                 DS
     D  WDsÅMD                 1      8  0
     D  WDsÅMDÅ                1      4  0
     D  WDsÅMDM                5      6  0
     D  WDsÅMDD                7      8  0
     D                 DS
     D  WDsDMÅ                 1      8  0
     D  WDsDMÅD                1      2  0
     D  WDsDMÅM                3      4  0
     D  WDsDMÅÅ                5      8  0
     D                 DS
     D  HXSNDN                 1     20
     D  HXSNR17                4     20
     D                 DS
     D  HEGN                   1     15
     D  HEGN04                 1      4
     D  HEGN03                 5      7
     D  HEGN08                 8     15
     D  HEGN515                5     15
     D                 DS
     D  HERFA                  1     35
     D  HERFA03                1      3
     D  HERFA08                4     11
     D                 DS
     D  HEDTMO                 1      8
     D  HEDTMOD                1      2
     D  HEDTMOM                3      4
     D  HEDTMOÅ                5      6
     D  HEDTMO6                1      6
     D  HEDTMO2                7      8
     D                 DS
     D  WSATA                  1      8  0
     D  WSATAÅ                 1      4  0
     D  WSATAM                 5      6  0
     D  WSATAD                 7      8  0
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬lng¬ : ¬'+%trim(LNG)+'¬, '
        +'¬wsna¬ : ¬'+%trim(WSNA)+'¬, '
        +'¬kunde¬ : ¬'+%trim(%editc(BIKUNR:'Z'))+'¬, '
        +'¬sdato¬ : ¬'+%trim(%editc(BIDTV:'Z'))+'¬, '
        +'¬stid¬ : ¬'+%trim(%editc(BITIDV:'Z'))+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬wsopd¬ : ¬'+%trim(WSOPD)+'¬, '
        +'¬dir¬ : ¬'+%trim(DIR)+'¬, '
        +'¬extref¬ : ¬'+%trim(EXTREF)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Skriv JSON-LISTE Start
     C                   EVAL      JSONstring ='"dspoppdrag" : ['
     C                   CALLP     UPdHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Hent oppdrag
     C                   EXSR      DspPart
     C                   ADD       1             AntSnd
      /free
          JSONstring=*BLANKS;
       JSONstring=%trim(JSONstring)+'{'
        +'¬heur¬ : ¬'+%trim(HEUR)+'¬, '
        +'¬fitdvi¬ : ¬'+%trim(FITDVI)+'¬, '
        +'¬heavd¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬, '
        +'¬heopd¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬, '
        +'¬hesg¬ : ¬'+%trim(HESG)+'¬, '
        +'¬hedtop¬ : ¬'+%trim(%editc(HEDTOP:'Z'))+'¬, '
        +'¬hepro¬ : ¬'+%trim(%editc(HEPRO:'Z'))+'¬, '
        +'¬wsetd¬ : ¬'+%trim(%editc(WSETD:'Z'))+'¬, '
        +'¬wseta¬ : ¬'+%trim(%editc(WSETA:'Z'))+'¬, '
        +'¬wsata¬ : ¬'+%trim(%editc(WSATA:'Z'))+'¬, '
        +'¬tracking¬ : ¬'+%trim(XTXT)+'¬, '
        +'¬tracking2¬ : ¬'+%trim(XTXT)+'¬, '
        +'¬hegn¬ : ¬'+%trim(HEGN)+'¬, '
        +'¬herfa¬ : ¬'+%trim(HERFA)+'¬, '
        +'¬hehawb¬ : ¬'+%trim(HEHAWB)+'¬, '
        +'¬hetrcn¬ : ¬'+%trim(HETRCN)+'¬, '
        +'¬henas¬ : ¬'+%trim(HENAS)+'¬, '
        +'¬heads1¬ : ¬'+%trim(HEADS1)+'¬, '
        +'¬heads2¬ : ¬'+%trim(HEADS2)+'¬, '
        +'¬heads3¬ : ¬'+%trim(HEADS3)+'¬, '
        +'¬henak¬ : ¬'+%trim(HENAK)+'¬, '
        +'¬headk1¬ : ¬'+%trim(HEADK1)+'¬, '
        +'¬headk2¬ : ¬'+%trim(HEADK2)+'¬, '
        +'¬headk3¬ : ¬'+%trim(HEADK3)+'¬, '
        +'¬hesdff¬ : ¬'+%trim(HESDFF)+'¬, '
        +'¬hesdf¬ : ¬'+%trim(HESDF)+'¬, '
        +'¬hesdt¬ : ¬'+%trim(HESDT)+'¬, '
        +'¬hesdvt¬ : ¬'+%trim(HESDVT)+'¬, '
        +'¬hent¬ : ¬'+%trim(%editc(HENT:'Z'))+'¬, '
        +'¬clis¬ : ¬'+%trim(CLIDLINK)+'¬, '
        +'¬hedtmo¬ : ¬'+%trim(HEDTMO)+'¬, '
        +'¬hevkt¬ : ¬'+%trim(%editc(HEVKT:'4'))+'¬, '
        +'¬hefbv¬ : ¬'+%trim(%editc(HEFBV:'4'))+'¬, '
        +'¬hem3¬ : ¬'+%trim(%editc(HEM3:'4'))+'¬, '
        +'¬helm¬ : ¬'+%trim(%editc(HELM:'4'))+'¬, '
        +'¬hevs1¬ : ¬'+%trim(HEVS1)+'¬, '
        +'¬hevs2¬ : ¬'+%trim(HEVS2)+'¬, '
        +'¬hegm1¬ : ¬'+%trim(HEGM1)+'¬, '
        +'¬hegm2¬ : ¬'+%trim(HEGM2)+'¬, '
        +'¬hesgm¬ : ¬'+%trim(HESGM)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
     C                   EVAL      LNG     = zhbgetvar('LNG')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C     LC:UC         XLATE     WSUSER        WSUSER
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WSAVD   = zhbgetvar('HEAVD')
     C                   MOVE      *ZEROS        HEAVD
     C                   EVAL      HEAVD = c2n2(WSAVD)
     C                   EVAL      WSOPD   = zhbgetvar('HEOPD')
     C                   MOVE      *ZEROS        HEOPD
     C                   EVAL      HEOPD = c2n2(WSOPD)

     C                   ENDSR
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DSPPART       BEGSR
      *
     C     HEADFK        CHAIN     HEADF                              33
     C     *IN(33)       IFEQ      '0'
      * Test kundenr
     C                   IF        XINTERN <> 'J'
     C     1             DO        30            XN2
     C     KN(XN2)       CABEQ     0             SLDSPPART
     C     HEKNA         IFNE      KN(XN2)
     C     TRKNFA        ANDNE     KN(XN2)
     C     HEKNS         ANDNE     KN(XN2)
     C     HEKNSF        ANDNE     KN(XN2)
     C     HEKNK         ANDNE     KN(XN2)
     C     HEKNKF        ANDNE     KN(XN2)
     C                   ELSE
     C                   LEAVE
     C                   ENDIF
     C                   ENDDO
     C                   END

     C                   END
     C                   IF        HEUR = 'H'
     C                   EVAL      WSETD=TRSDFD
     C                   EVAL      WSETA=TRSDTD
     C                   ELSE
     C                   EVAL      WSETD=HEDTOP
     C                   EVAL      WSETA=HEDTOP
     C                   ENDIF

     C     HEADFK        CHAIN     TRACKT                             33
     C  N33              MOVE      TXATAD        WSATA
     C     WSATA         IFEQ      *ZEROS
     C                   IF        ((HEDTMO <> *ZEROS) AND (HEDTMO <> *BLANKS))
     C                   IF        HEDTMO2 = *BLANKS
     C                   MOVE      HEDTMOD       WSATAD
     C                   MOVE      HEDTMOM       WSATAM
     C                   MOVE      2000          WSATAÅ
     C                   MOVE      HEDTMOÅ       WSATAÅ
     C                   ELSE
     C                   MOVE      HEDTMO        WSATA
     C                   END
     C                   END
     C                   END
     C                   IF        HEUR = 'C'
     C                   MOVE      *BLANKS       KFKOD3
     C                   MOVEL     HERFA03       KFKOD3
     C     KOFASTK3      CHAIN     KOFAST                             33
     C   33KOFASTK5      CHAIN     KOFAST                             33
     C                   MOVE      *BLANKS       XTXT            300
     C                   MOVE      KFTXTE        XTXTA             1
     C                   MOVE      ' '           KFTXTE
     C                   MOVEL     KFTXT         XTXT
     C                   CAT       KFTXTE:0      XTXT
     C     KOFASTK4      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      KFTXTE        XTXTA             1
     C                   MOVE      ' '           KFTXTE
     C                   CAT       KFTXT:0       XTXT
     C                   CAT       KFTXTE:0      XTXT
     C                   ENDIF
     C                   IF        XTXTA = 'A'
     C                   CAT       HERFA08:0     XTXT
     C                   ENDIF

     C                   ELSE

     C                   MOVE      *BLANKS       XTXT
     C                   IF        (HEGN515 = '00000000000')
     C                             OR(HEGN515 = '           ')
     C                   MOVE      *BLANKS       HEGN
     C                   END
     C                   IF        HEGN04 = *BLANKS

      * 4 første av godnr ER IKKE fylt ut   - AWB
     C                   MOVE      *BLANKS       KFKOD3
     C                   MOVEL     HEGN03        KFKOD3
      * sjekk flyselskap / other ( med kofasttype 'WEBETRACK' )
     C     KOFASTK3      CHAIN     KOFAST                             33
     C   33KOFASTK5      CHAIN     KOFAST                             33
     C                   MOVE      *BLANKS       XTXT            300
     C                   MOVE      KFTXTE        XTXTA             1
     C                   MOVE      ' '           KFTXTE
     C                   MOVEL     KFTXT         XTXT
     C                   CAT       KFTXTE:0      XTXT
     C     KOFASTK4      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      KFTXTE        XTXTA             1
     C                   MOVE      ' '           KFTXTE
     C                   CAT       KFTXT:0       XTXT
     C                   CAT       KFTXTE:0      XTXT
     C                   ENDIF
     C                   IF        XTXTA = 'A'
     C                   CAT       HEGN08:0      XTXT
     C                   ENDIF
     C                   ENDIF

     C                   ENDIF
      * Skal link til kolli-IF vises
     C                   MOVE      *BLANKS       CLIDLINK        256
     C     DOKUFMKEY     CHAIN     DOKUFM                             33
     C     *IN33         IFEQ      *OFF
     C                   EVAL      ClidLink='/sycgip/syOPCL.pgm?user='
     C                             +%trim(BIBRID)
     C                             +'&avd='+%trim(%editc(HEAVD:'Z'))
     C                             +'&opd='+%trim(%editc(HEOPD:'Z'))+'" '
     C                             +'Target="ClIdWin">Se Kolli-ID/Godsmerke</A>'
     C                   ENDIF
      *
     C     SLDSPPART     TAG
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     HEADFK        klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     KOFASTK1      klist
     C                   kfld                    KFTYP1           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP1 = 'TRACKPREFI'
     C     KOFASTK2      klist
     C                   kfld                    KFTYP2           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP2 = 'TRACKSUFFI'
     C     KOFASTK3      klist
     C                   kfld                    KFTYP3           10
     C                   kfld                    KFKOD3            5
     C                   MOVEL     'WEBETRACK'   KFTYP3
     C     KOFASTK4      klist
     C                   kfld                    KFTYP3
     C                   kfld                    KFKOD4            5
     C                   MOVEL     '   **'       KFKOD4
     C     KOFASTK5      klist
     C                   kfld                    KFTYP3
     C                   kfld                    KFKOD5            5
     C                   MOVEL     'OTHER'       KFKOD5
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     ActKey        KLIST
     C                   KFLD                    KFTYPT           10
     C                   KFLD                    KFKOD
     C                   MOVEL     'TRACKT'      KFTYPT
     C     DokufmKey     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'F'           FMRI

     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * Ugyldig userID
     C     *IN50         IFEQ      '1'
     C                   close     BRIDF
     C                   EVAL      *INlr = *on
     C                   return
     C                   ENDIF
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
      * Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   OPEN      HEADF
     C                   OPEN      FIRM
     C                   OPEN      FRISOL01
     C                   OPEN      HEAD17
     C                   OPEN      TURER14
     C                   OPEN      KOFAST
     C                   OPEN      BRPARF
     C                   OPEN      GSSCGL02
     C                   OPEN      TRACKT
     C                   OPEN      DOKUFM

      * Initiere DS (obs - pass at ikke verdier tatt inn i parse  nulles)
     C                   MOVE      *ZEROS        WSATA
     C                   MOVE      *ZEROS        WDsÅMD
     C                   MOVE      *ZEROS        WDsDMÅ

     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID

     C                   EVAL      PABRID = WSUSER

     C                   MOVEL     'NOTES'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR
     C                             ((PAVRDA <> 'j') AND
     C                              (PAVRDA <> 'J')))
     C                   MOVE      'N'           XWRKNOTES         1
     C                   ELSE
     C                   MOVE      'J'           XWRKNOTES         1
     C                   END

     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END

     C                   MOVE      *ZEROS        KN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   MOVEL     'KUNGR'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA = *BLANKS))
     C                   MOVE      BIKUNR        KN(1)
     C                   ELSE
     C                   MOVEL     PAVRDA        CGCG
     C     CGCG          SETLL     GSSCGL02
     C     CGCG          READE     GSSCGL02                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN2
     C                   MOVE      CGCNO         KN(XN2)
     C     CGCG          READE     GSSCGL02                               33
     C                   ENDDO
     C                   END
      * LÅNER ARKTY-PARAMETEREN FOR Å AVGJØRE OM BRUKER KAN SE FAKTURALINK
      * sjekk om arkivtypene fa,fs,fx finnes(på førstelinjer) Hvis ikke, N i se fakturalink
      * (sjekk av arkivtyper for å se pdf-dok, sker generelt annet sted)
     C                   MOVE      'N'           OKFaktLink        1
     C                   MOVE      'ARKTY'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      '0'
     C     'fa'          scan      Pavrda                                 33
     C  N33'fs'          scan      Pavrda                                 33
     C  N33'fx'          scan      Pavrda                                 33
     C   33              MOVE      'J'           OKFaktLink
     C                   END
      * logikk ved eksternref i parameterstreng
     C     EXTREF        IFNE      *BLANKS
     C     HEAVD         ANDEQ     0
     C     HEOPD         ANDEQ     0
     C                   MOVE      'EXTRE'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     PAVRDA        FSKODE
     C                   EXSR      finnopd
     C                   ELSE
     C                   EXSR      finnopdh
     C                   END
     C                   END

     C     BIFIRM        CHAIN     FIRM                               33

     C                   ENDSR
      ****************************************************************************
     C     FINNOPD       BEGSR
      ****************************************************************************
     C     FRIHEAD       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FRIKEY0       KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   KFLD                    FSDTOP
     C     FRIKEY1       KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   EVAL      FSSOK  = EXTREF
     C                   EVAL      FSDTOP = 99999999
     C     FRIKEY0       SETGT     FRISOL01
     C                   setoff                                       33
     C     *IN33         doweq     '0'
     C     FRIKEY1       readpe    FRISOL01                               33
     C     *IN33         IFEQ      '0'
     C     FRIHEAD       CHAIN     HEADF                              34
     C     *IN34         IFEQ      '0'
     C     HEKNA         IFEQ      BIKUNR
     C     TRKNFA        OREQ      BIKUNR
     C                   MOVE      heavd         wsavd
     C                   MOVE      heopd         wsopd
     C                   GOTO      UTFRI
     C                   END
     C                   END
     C                   END
     C                   END
     C                   Z-ADD     0             heavd
     C                   Z-ADD     0             heopd
     C     UTFRI         ENDSR
      ****************************************************************************
     C     FINNOPDH      BEGSR
      ****************************************************************************
     C     H17KEY        KLIST
     C                   KFLD                    HERFA
     C                   EVAL      HERFA  = EXTREF
     C     H17KEY        SETGT     HEAD17
     C                   setoff                                       33
     C     *IN33         doweq     '0'
     C     H17KEY        readpe    HEAD17                                 33
     C     *IN33         IFEQ      '0'
     C     HEKNA         ANDEQ     BIKUNR
     C                   MOVE      heavd         wsavd
     C                   MOVE      heopd         wsopd
     C                   GOTO      UTH17
     C                   END
     C                   END
     C                   Z-ADD     0             heavd
     C                   Z-ADD     0             heopd
     C     UTH17         ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          BEGSR
     C                   close     BRIDF
     C                   close     HEADF
     C                   CLOSE     FIRM
     C                   CLOSE     FRISOL01
     C                   CLOSE     HEAD17
     C                   CLOSE     KOFAST
     C                   CLOSE     TURER14
     C                   close     BRPARF
     C                   CLOSE     GSSCGL02
     C                   CLOSE     TRACKT
     C                   CLOSE     DOKUFM
     C                   CALLP     wrtsection('*fini')
     C                   EVAL      *INlr = *on

     C                   ENDSR
