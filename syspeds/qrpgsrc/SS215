      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS215 MODULE(*LIBL/SS215 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FGSSTRF    IF   E           K DISK    USROPN
     FGSSORF    UF   E           K DISK    USROPN
     FGSSORL06  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR06)
     FGSSORL21  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR21)
     FGSSOWL01  UF   E           K DISK    USROPN
     F                                     PREFIX(OW_)
     FGSSPOF    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSTNO           s             11
     D WBTNO           s             11  0
     D WSONO           s              7
     D WBONO           s              7  0
     D WSFRAMES        s              1
     D WSREMOVE        s              1
     D WSPICKUP        s              1
     D WSCAN           s              1
     D WSCHG           s              1
     D WSSTART         s              1
     D WSOPDKL         s              1
     D XONOA           s              7A
     D XONO            s              7  0
     D XKLFA           s              3A
     D XKLF            s              3  0
     D XKLTA           s              3A
     D XKLT            s              3  0
     D TOTCOL          s              5  0
     D TOTVKT          s              7  1
     D TOTM3           s              7  2
     D Title           S            100
     D ItemCount       s             10i 0
     D i               s             10i 0
      *
     D OddEven         s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      * Definere  hex appostrof
     D Ap              c                   x'7D'
      *

     D IfsMultIndicators...
     D                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      WSOPDKL  = 'J'
     C                   EXSR      SROPDKL
     C                   WHEN      WSCAN    = 'J'
     C                   WHEN      WSREMOVE = 'Y'
     C                   EXSR      SRREMOVE
     C                   WHEN      WSPICKUP = 'Y'
     C                   EXSR      SRPICKUP
     C                   WHEN      WSPICKUP = 'A'
     C                   EXSR      SRPICKALL
     C                   ENDSL
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')
     C                   IF        WBTNO <> 0
      * HOVEDRUTINE
      * Vis ordre på trip.
     C                   callp     wrtsection('tablestr1')
     C     WBTNO         SETLL     GSSORL21
     C     WBTNO         READE     GSSORL21                               35
     C                   DOW       *IN35 = *OFF
     C                   ADD       ORCLI         TOTCOL
     C                   ADD       ORWT          TOTVKT
     C                   ADD       ORVOL         TOTM3
     C                   EXSR      SETROW
     C                   callp     wrtsection('tablerow1')
     C     WBTNO         READE     GSSORL21                               35
     C                   ENDDO
     C                   callp     updHTMLvar('TOTCOL':
     C                             %trim(%editc(TOTCOL:'Z')))
     C                   callp     updHTMLvar('TOTVKT':
     C                             %trim(%editc(TOTVKT:'3')))
     C                   callp     updHTMLvar('TOTM3':
     C                             %trim(%editc(TOTM3:'3')))
     C                   callp     wrtsection('tableend')
     C                   END
      * Vis ordre på lager.
     C                   callp     wrtsection('tablestr2')
      *
      *
     C     GSSORL06K     SETLL     GSSORL06
     C     GSSORL06K     READE     GSSORL06                               35
     C                   DOW       *IN35 = *OFF
     C                   EXSR      SRFILTER
     C                   IF        XVIS = 'J'
     C                   exsr      Setrow2
     C                   callp     wrtsection('tablerow2')
     C                   END
     C     GSSORL06K     READE     GSSORL06                               35
     C                   ENDDO
      *
     C                   callp     wrtsection('tableend2')
      *
     C                   IF        WBTNO <> 0
     C                   callp     wrtsection('PickupAll1')
     C                   END
     C                   callp     wrtsection('PickupAll2')
      *
      * Quit
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('botStd')                         Body/htmlavsl
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSTNO    = zhbgetvar('WSTNO')
     C                   EVAL      WBTNO    = c2n2(WSTNO)
     C                   eval      WSONO    = zhbgetvar('WSONO')
     C                   EVAL      WBONO    = c2n2(WSONO)
     C                   eval      WSFRAMES = zhbgetvar('FRAMES')
     C                   eval      WSREMOVE = zhbgetvar('WSREMOVE')
     C                   eval      WSPICKUP = zhbgetvar('WSPICKUP')
     C                   eval      WSCAN    = zhbgetvar('WSCAN')
     C                   eval      WSSTART  = zhbgetvar('WSSTART')
     C                   eval      WSOPDKL  = zhbgetvar('WSOPDKL')
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     GSSTRF
     C                   CLOSE     GSSORF
     C                   CLOSE     GSSORL06
     C                   CLOSE     GSSORL21
     C                   CLOSE     GSSOWL01
     C                   CLOSE     GSSPOF
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/eSpedTop.html -
     C                             /syspeddev/html/SS215.html -
     C                             /syspeddev/html/eSpedBot.html':
     C                             '/CGITAG/')
     C                   endsr
      *=====================================================================******
      *  FILTRERE POSTER  - SJØ
      *=====================================================================******
     C     SRFILTER      begsr
     C                   MOVE      'N'           XVIS              1
     C                   MOVE      'J'           XVIS              1
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  FILTRERE POSTER  - LAND
      *=====================================================================******
     C     SRFILTER2     begsr
     C                   MOVE      'N'           XVIS              1
     C                   MOVE      'J'           XVIS              1

     C                   ENDSR
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
     c                   eval      Title='POMS Purchase Order Management System'
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updhtmlvar('TITLE':Title)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSTNO':
     C                             %trim(%editc(WBTNO:'L')))
     C                   callp     updHTMLvar('FRAMES':WSFRAMES)
     C                   callp     updHTMLvar('WSCHG':WSCHG)
      *
     C                   IF        WBTNO <> 0
     C                   callp     updhtmlvar('VisPick':', "bVisible": true')
     c                   else
     C                   callp     updhtmlvar('VisPick':', "bVisible": false')
     c                   endif
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C     ORONO         CHAIN     GSSPOF                             33
     C   33              MOVE      *BLANKS       POCONO
     C                   callp     updHTMLvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   callp     updHTMLvar('POCONO':POCONO)
     C                   callp     updHTMLvar('ORBCD':ORBCD)
     C                   callp     updHTMLvar('WSCLNF':
     C                             %trim(%editc(ORCLNF:'Z')))
     C                   callp     updHTMLvar('WSCLNT':
     C                             %trim(%editc(ORCLNT:'Z')))
     C                   callp     updHTMLvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updHTMLvar('ORWT':
     C                             %trim(%editc(ORWT:'3')))
     C                   callp     updHTMLvar('ORVOL':
     C                             %trim(%editc(ORVOL:'3')))
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow2       begsr
      *
     C     ORONO         CHAIN     GSSPOF                             33
     C   33              MOVE      *BLANKS       POCONO
     C                   callp     updHTMLvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   callp     updHTMLvar('POCONO':POCONO)
     C                   callp     updHTMLvar('ORBCD':ORBCD)
     C                   callp     updHTMLvar('ORLC':ORLC)
     C                   callp     updHTMLvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updHTMLvar('ORWT':
     C                             %trim(%editc(ORWT:'3')))
     C                   callp     updHTMLvar('ORVOL':
     C                             %trim(%editc(ORVOL:'3')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  SRREMOVE
      *=====================================================================******
     C     SRREMOVE      begsr
      *
     C     WBONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      '0'           ORST
     C                   MOVE      *ZEROS        ORTNO
     C                   MOVE      *ZEROS        ORCLNF
     C                   MOVE      *ZEROS        ORCLNT
     C                   IF        ORLC = 'ONWAY'
     C                   MOVE      ORLC2         ORLC
     C                   MOVE      ORLC3         ORLC2
     C                   MOVE      *BLANKS       ORLC3
     C                   MOVE      ORDTL2        ORDTL
     C                   MOVE      ORDTL3        ORDTL2
     C                   MOVE      *ZEROS        ORDTL3
     C                   END
     C                   UPDATE    GSSORFR
     C     WBONO         CHAIN     GSSOWL01                           33
     C                   MOVE      ORST          OW_ORST
     C  N33              UPDATE    GSSOWFR
     C                   END
      *
     C                   EVAL      WSCHG = 'Y'
      *
     C                   endsr
      *
      *=====================================================================******
      *  SRPICKUP
      *=====================================================================******
     C     SRPICKUP      begsr
      *
     C     WBONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      TRST          ORST
     C                   MOVE      TRTNO         ORTNO
     C                   IF        ((TRST >= '2') AND (TRLOCT <> *BLANKS))
     C                   MOVE      ORLC2         ORLC3
     C                   MOVE      ORLC          ORLC2
     C                   MOVE      'ONWAY'       ORLC
     C                   MOVE      ORDTL2        ORDTL3
     C                   MOVE      ORDTL         ORDTL2
     C                   MOVE      TRDTL         ORDTL
     C                   END
     C                   UPDATE    GSSORFR
     C     WBONO         CHAIN     GSSOWL01                           33
     C                   MOVE      ORST          OW_ORST
     C  N33              UPDATE    GSSOWFR
     C                   END
      *
     C                   EVAL      WSCHG = 'Y'
      *
     C                   endsr
      *
      *=====================================================================******
      *  SRPICKALL
      *=====================================================================******
     C     SRPICKALL     begsr
      *
     C                   IF        TRBCD <> *BLANKS
     C     GSSORL06K2    SETLL     GSSORL06
     C     GSSORL06K2    READE     GSSORL06                               33
     C                   DOW       *IN33 = *OFF
     C     ORONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      TRST          ORST
     C                   MOVE      TRTNO         ORTNO
     C                   IF        ((TRST >= '2') AND (TRLOCT <> *BLANKS))
     C                   MOVE      ORLC2         ORLC3
     C                   MOVE      ORLC          ORLC2
     C                   MOVE      'ONWAY'       ORLC3
     C                   MOVE      ORDTL2        ORDTL3
     C                   MOVE      ORDTL         ORDTL2
     C                   MOVE      TRDTL         ORDTL
     C                   END
     C                   UPDATE    GSSORFR
     C     WBONO         CHAIN     GSSOWL01                           33
     C                   MOVE      ORST          OW_ORST
     C  N33              UPDATE    GSSOWFR
     C                   END
      *
     C     GSSORL06K2    READE     GSSORL06                               33
     C                   ENDDO
     C                   END
      *
     C                   endsr
      *
      *=====================================================================******
      *  SROPDKL
      *=====================================================================******
     C     SROPDKL       begsr
      *
     C                   eval      ItemCount = ZhbGetVarCnt('WSCLNF')
      *
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      XONOA = ZhbGetVar('WSONO':i)
     C                   EVAL      XONO  = c2n2(XONOA)
     C                   eval      XKLFA = ZhbGetVar('WSCLNF':i)
     C                   EVAL      XKLF  = c2n2(XKLFA)
     C                   eval      XKLTA = ZhbGetVar('WSCLNT':i)
     C                   EVAL      XKLT  = c2n2(XKLTA)
     C     XONO          CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      XKLF          ORCLNF
     C                   MOVE      XKLT          ORCLNT
     C                   UPDATE    GSSORFR
     C                   END
     C                   eval      i = i +1
     C                   ENDDO
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      GSSTRF
     C                   OPEN      GSSORF
     C                   OPEN      GSSORL06
     C                   OPEN      GSSORL21
     C                   OPEN      GSSOWL01
     C                   OPEN      GSSPOF
      *
     C     GSSORL06K     klist
     C                   kfld                    XST
     C                   kfld                    XTWLC
     C                   MOVE      '0'           XST               1
     C     GSSORL06K2    klist
     C                   kfld                    XST
     C                   kfld                    XTWLC
     C                   kfld                    TRBCD
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      *
      * Brukeren kan kun jobbe for følgende Attestasjonskode.
     C                   MOVE      'SSLOC'       PAID
     C     BrParKey      chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XTWLC             5
     C     WBTNO         CHAIN     GSSTRF                             33
      *
     C                   endsr
      *
