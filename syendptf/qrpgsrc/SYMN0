********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYMN0 MODULE(*LIBL/SYMN0 *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF12 Slettede linjer fjernet (se SYMN0.G)
********************************************************************
      *********************************************************************
      *  Hovedprogram for verifisering av pålogging                       *
      *  BLANK i PASSORD er anonym pålogging / 0 i kundenr                *
      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FBRPRF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D lstbrd          s              1
     D BRUKERID        S             10
     D PASSORD         S             10
     D PASSORDL        S             70
     D FRAFIR          S             10
     D XSPRÅK          s              2
     D XBPPRD          S            512
     D XXCMD           S            128
     D XPGM            S             50    DIM(10)
     D*FEILMELDING     S             70
     D FEILMELDING     S            150
      * State related variables
     D State           ds                  based(StateP)
     D  UBRID                        10
     D  UINNDT                        8
     D  UINNTM                        6
      * User space variables
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
      * Message ID for CrtUsrSpc
     D MsgId           s              7

     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  BIDTF                  1      8  0
     D  BIDTFÅ                 1      4  0
     D  BIDTFM                 5      6  0
     D  BIDTFD                 7      8  0
     D                 DS
     D  BIDTV                  1      8  0
     D  BIDTVÅ                 1      4  0
     D  BIDTVM                 5      6  0
     D  BIDTVD                 7      8  0
     D                 DS
     D  BPPRD2                 1    128
     D  BPPRD2A                1      6
     D  BPPRD2B                7    128
     D                 DS
     D  LLPAVRDA               1     50
     D  PAT                    1     50    DIM(50)
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA_01_10           1     10
     D  PAVRDA_11_50          11     50
     D                 DS
     D  P2                     1     14
     D  P2T                    1     14    DIM(14)
     D  P25                    1      1
     D  P26                    1      2
     D  P27                    1      3
     D  P28                    1      4
     D  P29                    1      5
     D  P210                   1      6
     D  P211                   1      7
     D  P212                   1      8
     D  P213                   1      9
     D                 DS
     D  XPTXT                  1     50
     D  XPT                    1     50    DIM(50)
     D  XPT01                  1      1
     D  XPT02                  1      2
     D  XPT03                  1      3
     D  XPT04                  1      4
     D  XPT05                  1      5
     D  XPT06                  1      6
     D  XPT07                  1      7
     D  XPT08                  1      8
     D  XPT09                  1      9
     D  XPT10                  1     10
     D  XPT11                  1     11
     D  XPT12                  1     12
      *
      *--------------------------------------------------------------------
      * Other variables
      *--------------------------------------------------------------------
     D NowTimeISO      s               z
     D TmpTimeISO      s               z
     D TmpDt20N        s             20  0
     D DifMin          s             11  0
     D WAITMIN         S             11  0
     D RESTMIN         S             11  0
     D                 DS
     D TmpTs                   1     14  0
     D   TmpDt                 1      8  0
     D   TmpTid                9     14  0

      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Override database files
     C                   EVAL      RC = DOCMD('OVRDBF FILE(BRIDF) +
     C                             TOFILE(*LIBL/BRIDF) SECURE(*YES)')
      * Åpne fil
     C                   OPEN      BRIDF
      * Get input
     C                   EVAL      NBRVARS =
     C                             zhbgetinput(savedquerystring:qusec)
      * Bruker-id
     C                   EVAL      BRUKERID = zhbgetvar('BRUKERID')
      * Passord
     C                   EVAL      PASSORD = zhbgetvar('PASSORD')
     C                   EVAL      PASSORDL= zhbgetvar('PASSORD')
      * visse ASP/FLER-firma har avvikende påloggingsbilde -
      *  første 'passive html' AxxSYMN/AxxSYMNEN og SYMNB/SYMNBEN vil da kunne ha spesiell layout
      *  I SÅ FALL hardkod HIDDEN param FRAFIR i AxxSYMN og AxxSYMNEN (der xx gjerne er firmakode)
     C                   EVAL      FRAFIR  = zhbgetvar('FRAFIR')
      * språk fra engelsk påloggingsside (kun ved feil pålogging)
      * ellers hentes det fra user-ID
     C                   EVAL      XSPRÅK  = zhbgetvar('SPR')
      * Convert to uppercase
     C                   eval      BRUKERID = uppify(BRUKERID)
     C                   eval      PASSORD  = uppify(PASSORD)
      * Test innlogging
     C     BRUKERID      CHAIN(N)  BRIDF                              30
      * BLANK I PASSORD - ANONYM PÅLOGGING - ALLTID OK..- blank input/forenkler videre tester
     C     *IN30         IFEQ      '0'
     C     BIPO          ANDEQ     *BLANKS
     C     BIPOL         ANDEQ     *BLANKS
     C                   MOVE      *BLANKS       PASSORD
     C                   END
      * dersom User inneholder -LOGON : dummy i starturl for å hente rett logo i flerfirmamiljø
     C     '-LOGON'      scan      BRUKERID                               35
     C   35              move      'Y'           Logo_LOGON        1
     C                   SELECT
     C                   WHEN      Logo_LOGON = 'Y'
     C                   MOVEL     '.'           FEILMELDING
     C                   WHEN      BRUKERID = 'AVSLUTT'
     C                   MOVEL     '.'           FEILMELDING
     C                   EVAL      BRUKERID = PASSORD
     C                   ENDSL

      * Userid/passord - IKKE avslør HVA som er feil
     c                   if        FEILMELDING=*blanks
      *
     c                   MOVE      'J'           UsrPwOK           1
     C                   if        *in30=*on                                    Ugyldig user
     c                   MOVE      'N'           UsrPwOK
     c                   else
      ** User ok / test passord
   TM * begge oppgitt - må stemme med ETT - midlretidig
   TMc                   if        BIPO<>*blanks and BIPOL<>*blanks
   TMc                   If        BIPO<>PASSORD and BIPOL<>PASSORDL            Ugyldig passord
   TMc                   MOVE      'N'           UsrPwOK
   TMc                   endif
   TMc                   else
      * kun ett oppgitt bruk det
     c                   if        BIPOL<>*blanks and BIPOL<>PASSORDL
     c                   MOVE      'N'           UsrPwOK
     c                   else
     c                   if        BIPO<>PASSORD
     c                   MOVE      'N'           UsrPwOK
     c                   endif
     c                   endif
   tmc                   endif
     c                   endif
      *
     c                   IF        UsrPwOK = 'N'
      *         User rett/passord feil - oppdater ant / tid
     c                   If        *in30=*off
     C                   EXSR      UpdateFeil
     c                   endif
     c                   If        XSPRÅK='EN'
     c                   eval      FEILMELDING='Incorrect userId or password'
     c                   else
     c                   eval      FEILMELDING='Feil brukerId eller passord'
     c                   endif
     c                   endif
      *
     c                   endif

      * VELLYKKET - MEN nylig DEAKTIVERT - Varsel om å vente xx min
      *             BIDTF / BITIDF = DATO/TID SISTE FEIL - HVIS MER ENN XX MIN SIDEN - ok
     c                   if        FEILMELDING=*blanks
     C                   if        BIANTF >  5
     c                   eval      Waitmin = 5
     c                   EVAL      TmpDt = BIDTF
     c                   EVAL      TmpTid= BITIDF
     c                   move      *zeros        TmpDt20N
     c                   movel     TmpTs         TmpDt20N
     c                   eval      TmpTimeISO = %timestamp(TmpDt20N)
     c                   time                    NowTimeISO
     c                   eval      DifMin=%diff(NowTimeISO : TmpTimeISO :
     c                                          *Minutes)
     c                   if        DifMin < Waitmin
     c                   eval      RestMin = Waitmin - DifMin
     c                   if        RestMin <= 0
     c                   eval      RestMin = 1
     c                   endif
     c                   If        XSPRÅK='EN'
     c                   eval      FEILMELDING='Too many inncorrect '
     c                             + 'logon attempts. Please waite '
     c                             + %trim(%editc(RestMin:'Z'))+ 'minutes.'
     c                   else
     c                   eval      FEILMELDING='For mange ugyldige '
     c                             + 'påloggingsforsøk. Vennligst vent '
     c                             + %trim(%editc(RestMin:'Z'))+ ' minutt.'
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Noe feil - vis feilmelding / nytt forsøk ELLERS last neste HTML..
     C     FEILMELDING   IFNE      *BLANKS
     C     FEILMELDING   IFEQ      '.'
     C                   MOVE      *blanks       FEILMELDING
     C                   END
     C                   EXSR      LoadHtmlFeil
     C                   ELSE
     C                   EXSR      LoadHtmlMnu
     C                   END
      *
     C     BRUKERID      CHAIN     BRIDF                              30
     C                   IF        FEILMELDING<>*blanks
     C                   MOVE      FeilAnt       BIANTF
     C                   MOVE      FeilDt        BIDTF
     C                   MOVE      FeilTid       BITIDF
     C                   MOVE      ' '           BIINN                          Brukes ikke lenger
     C                   else
      * Null ut antall misslykkede forsøk, ved ett riktig.
     C                   MOVE      *ZEROS        BIANTF
     C                   MOVE      *ZEROS        BIDTF
     C                   MOVE      *ZEROS        BITIDF
     C                   MOVE      'I'           BIINN                          Brukes ikke lenger
     C                   TIME                    WTIME
     C                   MOVE      WTID          BITIDV
     C                   MOVE      WÅÅ           BIDTVÅ
     C                   MOVE      WMM           BIDTVM
     C                   MOVE      WDD           BIDTVD
     C                   MOVE      WTID          BITIDF
     C                   MOVE      WÅÅ           BIDTFÅ
     C                   MOVE      WMM           BIDTFM
     C                   MOVE      WDD           BIDTFD
     C                   endif
     C  N30              UPDATE    BRIDFR
      *
      * Lukk fil
     C                   CLOSE     BRIDF
      * Quit
     C                   exsr      Exit
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *=====================================================================
      * (Aktiv) Bruker har gitt feil passord - add 1 til antall ganger
      * Mer en xx feil - må RESETTES av operatør
      *=====================================================================
     C     UpdateFeil    begsr
     C                   IF        BIANTF < 9
     C                   ADD       1             BIANTF
     C                   ENDIF
     C                   MOVE      BIANTF        FeilAnt           1 0
     C                   TIME                    WTIME
     C                   MOVE      WTID          BITIDF
     C                   MOVE      WÅÅ           BIDTFÅ
     C                   MOVE      WMM           BIDTFM
     C                   MOVE      WDD           BIDTFD
     C                   MOVE      BIDTF         FeilDt            8 0
     C                   MOVE      BITIDF        FeilTid           6 0
     C                   endsr
      *=====================================================================
      * Noe feil ved bruker ID
      *=====================================================================
     C     LoadHtmlFeil  begsr
     c* feil - men ikke feil USER-ID (f.eks ved AVSLUTT= hent libl/image
     c     *in30         ifeq      *off
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pg
     C                   call      BILIBL
     C                   end
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
     c     FRAFIR        ifne      *blanks
     c                   eval      FRAFIR = 'A'+BIFIRM
     c                   endif
     c                   endif
      ** VED FLERFIRMA - TA HØYDE FOR AT GITTE FIRMA KAN HA EGNE HTML-ER
     c                   select
      * Her legges spesialer for firma som har avvikende pålogging
     c     FRAFIR        wheneq    'Axx'
EXEMPC*    XSPRÅK        Ifeq      'EN'
EXEMPC*                  callp     gethtml('HTMLSRC':
EXEMPC*                            '*LIBL':
EXEMPC*                            'AxxSYMNBEN':'/CGITAG/')
EXEMPc*                  else
EXEMPC*                  callp     gethtml('HTMLSRC':
EXEMPC*                            '*LIBL':
EXEMPC*                            'AxxSYMNB':'/CGITAG/')
EXEMPc*                  end
     c                   other
     C     XSPRÅK        Ifeq      'EN'
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYMNBEN':'/CGITAG/')
     c                   else
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYMNB':'/CGITAG/')
     c                   end
     c                   endsl
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('FEILTXT':FEILMELDING)
     C                   callp     wrtsection('all')
     C                   endsr
      *=====================================================================
      * Bruker har oppgitt riktig bruker-id og passord
      *=====================================================================
     C     LoadHtmlMnu   begsr
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med param
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pg
     C                   call      BILIBL
     C                   end
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      BRPRF
     C                   OPEN      BRPARF
     C     BRPARFK       klist
     C                   kfld                    BIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK
     C                   END
      *
     C                   MOVEL     'KUNPG'       PAID
     C                   MOVE      *ZEROS        XN                3 0
     C     BRPARFK       chain     BRPARF                             33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C     LC:UC         XLATE     PAVRDA        PAVRDA
     C                   EVAL      XPGM(XN) = PAVRDA
     C     BRPARFK       READE     BRPARF                                 33
     C                   ENDDO
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
     C     XSPRÅK        Ifeq      'EN'
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYMN1EN':'/CGITAG/')
     C                   else
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYMN1':'/CGITAG/')
     C                   end
      *
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
      * Ta med variabler fra USER-ID
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('user':BIBRID)
     C                   callp     updHTMLvar('WSNA':BIBESK)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   eval      UsrSpcName= CrtUsrSpc(
     C                               UsrSpcLib : StateP : MsgID : '*ALL')
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   callp     wrtsection('top')
      *
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      * Bruker TIME - låner dato for vellykket innlogg for å formidle Userspace / UINNDT
     C                   TIME                    WTIME
     C                   MOVE      WÅÅ           BIDTVÅ
     C                   MOVE      WMM           BIDTVM
     C                   MOVE      WDD           BIDTVD
     C                   MOVE      BIDTV         UINNDT
     C                   MOVE      WTID          UINNTM
     C                   MOVE      BIBRID        UBRID
      *
     C                   MOVE      *ZEROS        XGMLLIN           3 0
     C     BIKUNR        SETLL     BRPRF
     C     STMNU01       TAG
     C     BIKUNR        READE     BRPRF                                  50
      *
     C  N50              IF        BPAKT = 'V'
     C                   EXSR      TESTPGM
     C   33              GOTO      STMNU01
     C                   END
      *
     C  N50              IF        XPGM(1) <> *BLANKS
     C                   EXSR      TESTPGM2
     C   33              GOTO      STMNU01
     C                   END
      *
     C     *IN50         IFEQ      '1'
      * Ingen menypkt på kundenivå / gruppe er ikke utfylt
     c     BIKNG         cabeq     *zeros        Ingenpkt
     C                   MOVE      'J'           XGRP              1
     C     BIKNG         SETLL     BRPRF
     C     STMNU02       TAG
     C     BIKNG         READE     BRPRF                                  50
      *
     C  N50              IF        BPAKT = 'V'
     C                   EXSR      TESTPGM
     C   33              GOTO      STMNU02
     C                   END
      *
     C  N50              IF        XPGM(1) <> *BLANKS
     C                   EXSR      TESTPGM2
     C   33              GOTO      STMNU02
     C                   END
      *
     C                   END
      *
     C     *IN50         DOWEQ     '0'
     C     BPAKT         IFEQ      'A'                                          A=Aktiv i Esped
     C     BPAKT         OREQ      'B'                                          B=begge Esped/Tomcat
     C     BIBRID        OREQ      'YBCOS'
     C     XGMLLIN       IFNE      0
     C     XGMLLIN       ANDNE     BPLIN
     C                   callp     wrtsection('tablerowny')
     C                   END
     C                   MOVE      BPLIN         XGMLLIN
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C                   END
     C     STMNU03       TAG
     C     XGRP          IFEQ      'J'
     C     BIKNG         READE     BRPRF                                  50
     C                   ELSE
     C     BIKUNR        READE     BRPRF                                  50
     C                   END
     C  N50              IF        BPAKT = 'V'
     C                   EXSR      TESTPGM
     C   33              GOTO      STMNU03
     C                   END
      *
     C  N50              IF        XPGM(1) <> *BLANKS
     C                   EXSR      TESTPGM2
     C   33              GOTO      STMNU03
     C                   END
      *
     C  N50              ENDDO                                                  *hival
      *
     C     IngenPkt      tag
     C                   callp     wrtsection('tableend')
      *
     C                   callp     wrtsection('foundsome')
     c                   exsr      switchSR
     C                   callp     wrtsection('foundsome2')
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
      *
     C                   CLOSE     BRPRF
     C                   CLOSE     BRPARF
     C                   endsr
      *****************************************************
     C     SWITCHSR      begsr
      *****************************************************
      * Hvis det finnes minst en Multi-ID-parameter (SwitchTo-user) så vis pulldown
      * (DERSOM SWITCH USER VELGES så utføres test,framhenting av passord mm av ESUSR -
      *  se javascript DoSwitch i html SYMN1 - ved uatorisert/uriktig kall på ESUSR
      *  resulterer det i en redusert visning av SYMN1 switcuser-pulldowne OG feilmelding.
      *  Ved OK kall på ESUSR vil variabel SWITCHOK=Y trigge javascript DoSwitch )
     c                   move      'MLTID'       PAID
     C     BRPARFK       setll     BRPARF
     C     BRPARFK       reade     BRPARF                                 40
     C     *in40         cabeq     '1'           UtSwitch
     C                   callp     wrtsection('switchtop')
     C     *in40         doweq     '0'
     C                   callp     updhtmlvar('ID':PAVRDA_01_10)
     C                   callp     updhtmlvar('IDTEXT':PAVRDA_11_50)
     C                   callp     wrtsection('switchrow')
     C     BRPARFK       reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     updhtmlvar('FEILTXT'  :' ')
     C                   callp     updhtmlvar('SWITCHTO' :' ')
     C                   callp     updhtmlvar('SWITCHPW' :' ')
      *
     C                   callp     wrtsection('switchbot')
     C     UtSwitch      endsr
      *=====================================================================******
      *  TEST OM PROGRAM SKAL BRUKES
      *=====================================================================******
     C     TESTPGM       begsr
     C                   MOVE      'LLPGM'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      LLPAVRDA = uppify(PAVRDA)
     C                   EVAL      BPPRD  = uppify(BPPRD)
     C                   EVAL      BPPRD2 = uppify(BPPRD2)
     C                   Z-ADD     1             XN                3 0
     C     STTEST01      TAG
     C                   DO        50
     C                   IF        PAT(XN) <> *BLANKS
     C                   LEAVE
     C                   END
     C                   ADD       1             XN
     C                   IF        XN > 50
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   IF        PAT(XN) = *BLANKS
     C                   MOVE      *ON           *IN33
     C                   GOTO      SLTEST
     C                   END
      *
      * HENT PROGRAMNAVN
     C                   Z-ADD     1             XN3               3 0
     C                   MOVE      *BLANKS       P2
     C                   DO        14
     C                   MOVE      PAT(XN)       P2T(XN3)
     C                   ADD       1             XN
     C                   IF        XN > 50
     C                   LEAVE
     C                   END
     C                   IF        PAT(XN) = *BLANKS
     C                   LEAVE
     C                   END
     C                   ADD       1             XN3
     C                   ENDDO
      *
      * TEST OM PROGRAM FINNES
     C                   SELECT
     C                   WHEN      XN3 < 5
     C                   MOVE      *OFF          *IN34
     C                   WHEN      XN3 = 5
     C     P25           SCAN      BPPRD                                  34
     C  N34P25           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 6
     C     P26           SCAN      BPPRD                                  34
     C  N34P26           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 7
     C     P27           SCAN      BPPRD                                  34
     C  N34P27           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 8
     C     P28           SCAN      BPPRD                                  34
     C  N34P28           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 9
     C     P29           SCAN      BPPRD                                  34
     C  N34P29           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 10
     C     P29           SCAN      BPPRD                                  34
     C  N34P29           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 11
     C     P29           SCAN      BPPRD                                  34
     C  N34P29           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 12
     C     P29           SCAN      BPPRD                                  34
     C  N34P29           SCAN      BPPRD2                                 34
     C                   WHEN      XN3 = 13
     C     P29           SCAN      BPPRD                                  34
     C  N34P29           SCAN      BPPRD2                                 34
     C                   OTHER
     C     P2            SCAN      BPPRD                                  34
     C  N34P2            SCAN      BPPRD2                                 34
     C                   ENDSL
     C  N34              GOTO      STTEST01
     C                   MOVE      'A'           BPAKT
      *
     C                   END
      *
     C     SLTEST        ENDSR
      *
      *=====================================================================******
      *  TEST OM PROGRAM SOM KUN BRUKES
      *=====================================================================******
     C     TESTPGM2      begsr
     C                   EVAL      *IN33 = *OFF
     C     1             DO        10            XN                             1<-B
     C                   IF        XPGM(XN) = *BLANKS                            2<-B
     C                   EVAL      *IN33 = *ON
     C                   GOTO      SLTEST2
     C                   ELSE                                                    2
      *
     C                   MOVE      XPGM(XN)      XPTXT
     C                   Z-ADD     50            XN2               3 0
     C                   DOW       XN2 > 0                                        3<-B
     C                   IF        XPT(XN2) <> *BLANKS                             4<-B
     C                   LEAVE
     C                   END                                                       4<-E
     C                   SUB       1             XN2
     C                   ENDDO                                                    3<-E
      *
      * TEST OM PROGRAM FINNES
     C     LC:UC         XLATE     BPPRD         BPPRD
     C                   SELECT                                                   3<-B
     C                   WHEN      XN2 = 1
     C     XPT01         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 2
     C     XPT02         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 3
     C     XPT03         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 4
     C     XPT04         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 5
     C     XPT05         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 6
     C     XPT06         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 7
     C     XPT07         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 8
     C     XPT08         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 9
     C     XPT09         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 10
     C     XPT10         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 11
     C     XPT11         SCAN      BPPRD                                  34
     C                   WHEN      XN2 = 12
     C     XPT12         SCAN      BPPRD                                  34
     C                   OTHER
     C                   EVAL      *IN34 = *OFF
     C                   ENDSL
     C   34              GOTO      SLTEST2                                        3<-E
      *
     C                   END                                                     2<-E
      *
     C                   ENDDO                                                  1<-E
      *
     C     SLTEST2       ENDSR
      *
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     c* Sequence of this Recd (numeric, converted to char)
     C*                  CAT       BIBRID:0      BPPRD
     C*                  callp     updhtmlvar('BPPRD':BPPRD)
      * TEST OM USER ER OPPGITT.
     C     *like         DEFINE    BPPRD         BPPRDTmp
     C                   EVAL      BPPRDTmp = uppify(BPPRD)
     C     'USER='       SCAN      BPPRDTmp                               45
      *
     C                   MOVE      *BLANKS       XBPPRD
     C                   MOVEL     BPPRD         XBPPRD
     C     *IN45         IFEQ      *ON
     C                   CAT       BIBRID:0      XBPPRD
     C                   CAT       '&UsrSpcNam':0XBPPRD
     C                   CAT       'e=':0        XBPPRD
     C                   CAT       UsrSpcName:0  XBPPRD
     C                   END
     C     BpPrd2A       Ifeq      '&Time='
     c     BpPrd2A       Oreq      '&TIME='
     c     BpPrd2A       Oreq      '&time='
     C                   CAT       BPPRD2A:0     XBPPRD
     C                   Movel     Timedata1     TimeStamp        26
     C                   CAT       TimeStamp:0   XBPPRD
     C                   CAT       BPPRD2B:0     XBPPRD
     c                   else
     C                   CAT       BPPRD2:0      XBPPRD
     c                   end
     C                   callp     updhtmlvar('BPPRD':XBPPRD)
     C                   callp     updhtmlvar('BPBILD':BPBILD)
     C                   IF        ((XSPRÅK = 'EN') AND (BPTXTE <> *BLANKS))
     C                   callp     updhtmlvar('BPTXT':BPTXTE)
     C                   ELSE
     C                   callp     updhtmlvar('BPTXT':BPTXT)
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Kun ved menykall fra switcheprog ESUSR vil SWITCHOK være Y , ALDRI FRA SYMN0 )
     C                   callp     updhtmlvar('SWITCHOK' :' ')
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
