********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6083 * 01 YBC PTF6 GAMMEL KURS BRUKES IKKE. MYE ENRING, INGEN MERKING
********************************************************************
NV5   * MYE ENDRINGER. INGEN MERKING
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 62 *NOMSGLIN)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '1')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            XVAKOD         3   O  4  5TEXT('VALUTAKODE')
     A            XVAXXX        27   O  4  9
     A            XVAKRS        11  3O  4 37TEXT('VALUTAKURS')
     A                                      EDTCDE(1)
     A            XVAOMR         3  0O  4 50TEXT('OMREGNINGSFAKTOR')
     A                                      EDTCDE(4)
     A            WVADT          6  0O  4 54TEXT('F.O.M.DATO')
     A                                      EDTCDE(Y)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 20'Søking av valutakoder'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(60 YBC1805 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'Søking')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(14)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F5=Frisk   F6=Lage   F8=Søk   F12=-
     A                                      Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'Søking')
     A                                      CF12(12 'Forrige')
     A                                  1 20'Søking av valutakoder'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(60 YBC1805 SYMSGF)
     A                                      COLOR(WHT)
     A                                 14  2'F5=Frisk   F6=Lage   F8=Søk   F12=-
     A                                      Avbryt'
     A                                      COLOR(BLU)
     A                                 15  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      WINDOW(WINDOWSZ)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(14)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'Hopp til valuta -->'
     A            W2AXXX        27A  I  1 22DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2'F12=Forrige'
     A                                      COLOR(BLU)
