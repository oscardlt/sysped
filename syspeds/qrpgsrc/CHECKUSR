      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D lng             s              2
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D MsgId           s              7
     D State           ds                  based(StateP)
     D  UBRID                        10
     D  UINNDT                        8
     D  UINNTM                        6
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D                 DS
     D  X1TID                  1      6  0
     D  X1HH                   1      2  0
     D  X1MM                   3      4  0
     D  X1SS                   5      6  0
     D                 DS
     D  X2TID                  1      6  0
     D  X2HH                   1      2  0
     D  X2MM                   3      4  0
     D  X2SS                   5      6  0
      */copy syspeds/qrpgsrcpy,prolog3
      ****************************************************************************
      * Mainline
      ****************************************************************************
      * Initialization
     C                   exsr      Init
      *
     C                   IF        BIBRID = *BLANKS
     C                   MOVE      'N'           UINN
     C                   ELSE
      * Get user's input, create or retrieve user space pointer.
     C                   exsr      UsrSpc
     C                   END
      *
     C                   IF        UINN = 'N'
     C                   exsr      SetSkl
     C                   END
      *
     C                   move      *off          *inlr
     C                   return
      ****************************************************************************
     C     UsrSpc        begsr
      ****************************************************************************
     C                   eval      StateP = RtvUsrSpcPtr(UsrSpcName:
     C                             UsrSpcLib)
      *
     C                   SELECT                                                 1<-B
     C                   WHEN      StateP = *null
     C                   move      'N'           UINN
     C                   WHEN      UBRID <> BIBRID
     C                   move      'N'           UINN
     C                   OTHER
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   OPEN      BRPARF
     C     BRPARFK       CHAIN     BRPARF                               50
     C                   CLOSE     BRPARF
     C                   IF        (*IN50 OR (PAVRDN = 0))
     C                   Z-ADD     100           XMIN              3 0
     C                   ELSE
     C                   EVAL      XMIN = PAVRDN
     C                   END
      *
     C                   MOVE      WTM           X1TID             6 0
     C                   MOVE      UINNTM        X2TID             6 0
     C                   IF        UINNDT <> WDT                                 2<-B
     C                   ADD       240000        X1TID
     C                   END                                                     2<-E
     C                   MOVE      *ZEROS        X1MIN             5 0
     C                   MOVE      *ZEROS        X2MIN             5 0
     C                   MOVE      *ZEROS        X3MIN             5 0
     C                   EVAL      X1MIN = X1HH * 60 + X1MM
     C                   EVAL      X2MIN = X2HH * 60 + X2MM
     C                   EVAL      X3MIN = X1MIN - X2MIN
      * ved L=direkte ingan FRA (MAIL-)LINK  - simuler 0 i inaktivtid = forny tidsspenn
     C                   IF        UINN = 'L'
     C                   EVAL      X3MIN = *zeros
     c                   end
     C                   IF        X3MIN > XMIN                                  2<-B
     C                   move      'N'           UINN
     C                   ELSE                                                    2
     C                   move      'J'           UINN
     C                   move      WDT           UINNDT
     C                   move      WTM           UINNTM
     C                   END                                                     2<-E
     C                   ENDSL                                                  1<-E
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'CHECKUSR'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('*fini')
      *
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
     C                   OPEN      BRIDF
     C     BIBRID        CHAIN     BRIDF                                50
     C                   CLOSE     BRIDF
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
      * Protocol
      * Div data for å fastslå hva som gikk galt...
      * aktuel passiv tid / max passiv-tid
     C                   callp     updHtmlVar('PASMIN':
     C                             %trim(%editc(X3MIN:'Z')))
     C                   callp     updHtmlVar('MAXMIN':
     C                             %trim(%editc(XMIN:'Z')))
     C                   callp     updhtmlvar('BIBRID':BIBRID)
     C                   callp     updhtmlvar('USRSPC':UsrSpcName)
     C                   IF        StateP = *null
     C                   callp     updhtmlvar('UBRID':' ')
     C                   ELSE
     C                   callp     updhtmlvar('UBRID':UBRID)
     C                   END
      *
     C                   endsr
      ****************************************************************************
     C     Init          begsr
      ****************************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    BIBRID           10
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
      *
     C     BRPARFK       klist
     C                   kfld                    BIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      'TIDUT'       PAID
      *
     C                   endsr
      *
