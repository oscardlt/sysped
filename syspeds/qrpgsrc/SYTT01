      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TERACK/TRACING            ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 TTMANU, Dft=S.  Etterhvert tatt i bruk for EDI/IFTSTA (se prog ESTSTT)
10XXX * 02 JOV PTF10 F10 er innlegg på TURnivå - foreslå Z.. ved manuell
10XXX * 03 JOV PTF10 Kan angi også tid på topplinja ved NY (4 lang)
10172 * 04 SH  PTF10 norsk text til Track and trace
10172 * 05 SH  PTF10 nye parametre turnivå indikator 60 = turnivå
10172 * 06 SH  PTF10 Hoppe til dupper hovedoppdrag
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
11xxx * 07 SH  PTF11 rettelser engelsk norsk text
11xxx * 08 JOV PTF11 Byttet NO/EN-tekst i VAREL (Nå Norsk Først ..)
11xxx * 09 SH  PTF11 full bredde 132
11xxx * 10 SH  PTF11 rettelse for å endre dupoppdrag status
********************************************************************
      *
      *
     FTRACKL01  IF   E           K DISK
     F                                     INFDS(TRACK1DS)
n05  FTRACKL09  IF   E           K DISK
     F                                     RENAME(TRACKFR:TRACKFR9)
n05  F                                     INFDS(TRACK9DS)
     FTRACKf    UF A E             DISK
     F                                     RENAME(TRACKFR:TRACKFRX)
     FKOFAST    IF   E           K DISK
N01  FHEADF     IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FDISP      UF   E           K DISK
     FSYTT01FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D TRACK1DS        DS
     D  RECNO                397    400B 0
n05  D TRACK9DS        DS
n05  D  RECNO9               397    400B 0
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZUSER                254    263
N06  D                 DS
N06  D  AV                     1     12  0
N06  D                                     DIM(3)
N06  D                 DS
N06  D  OP                     1     21  0
N06  D                                     DIM(3)
     D                 DS
     D  KO                     1     30
     D                                     DIM(10)
     D  WFKO                   1     30
     D  WFKO01                 1      3
     D  WFKO02                 4      6
     D  WFKO03                 7      9
     D  WFKO04                10     12
     D  WFKO05                13     15
     D  WFKO06                16     18
     D  WFKO07                19     21
     D  WFKO08                22     24
     D  WFKO09                25     27
     D  WFKO10                28     30
     D                 DS
     D  GR                     1      9
     D                                     DIM(3)
     D  WFGR                   1      9
     D  WFGR01                 1      3
     D  WFGR02                 4      6
     D  WFGR03                 7      9
     D                 DS
     D  SFTEXT                 1     70
     D  XDLT                   1      7
     D  SFTEXX                 1     70
     D                 DS
     D  SFTIME                 1      6  0
     D  SFTIMX                 1      4  0
     D                 DS
     D  W2DATE                 1      6  0
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D                 DS
     D  TTDATE                 1      8  0
     D  TTDTÅ                  1      4  0
     D  TTDTÅ2                 3      4  0
     D  TTDTM                  5      6  0
     D  TTDTD                  7      8  0
     D                 DS
     D  TTDATL                 1      8  0
     D  TTDTLÅ                 1      4  0
     D  TTDTLM                 5      6  0
     D  TTDTLD                 7      8  0
     D                 DS
     D  WFDATE                 1      6  0
     D  WFDED6                 1      2  0
     D  WFDEM6                 3      4  0
     D  WFDEÅ6                 5      6  0
     D                 DS
     D  WFDA8E                 1      8  0
     D  WFDEÅ8                 1      4  0
     D  WFDEM8                 5      6  0
     D  WFDED8                 7      8  0
     D                 DS
     D  WFDATL                 1      6  0
     D  WFDLD6                 1      2  0
     D  WFDLM6                 3      4  0
     D  WFDLÅ6                 5      6  0
     D                 DS
     D  WFDA8L                 1      8  0
     D  WFDLÅ8                 1      4  0
     D  WFDLM8                 5      6  0
     D  WFDLD8                 7      8  0
     D                 DS
     D  SFDATX                 1      6  0
     D  SFDTDX                 1      2  0
     D  SFDTMX                 3      4  0
     D  SFDTÅX                 5      6  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  DFBREF                 1     17
     D  DFBRTX                 1      5
     D  DFBRAV                 6      9
     D  DFBRST                10     10
     D  DFBROP                11     17
     C*
     C                   EXSR      INIT
      * DUMMY ETTER FJERNING I DDS...
     C     START         TAG
     C                   MOVE      WFFBNR        GMFBNR            3 0
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       W2ACTI
     C                   MOVE      *ZEROS        W2DATE
N03  C                   MOVE      *ZEROS        W2Time
      *_____________
      * Fyll Subfile
      *"""""""""""""
s05  C*    FSKEY         SETLL     TRACKL01
n05  C  N60FSKEY         SETLL     TRACKL01
n05  C   60FSKEYT        SETLL     TRACKL09
     C     START1        TAG
s05  C*    FSKEY         READE     TRACKL01                               94
n05  C  n60FSKEY         READE     TRACKL01                               94
n05  C   60FSKEYT        READE     TRACKL09                               94
      *
     C     START2        TAG
     C     *IN94         DOWEQ     '0'
     C                   EXSR      FILTER
     C     SKIPP         CABEQ     'J'           NEXT
     C                   ADD       1             ZRECNO
     C                   MOVE      TTAVD         SFAVD
     C                   MOVE      TTOPD         SFOPD
n05  C                   MOVE      TTtur         SFtur
     C                   MOVE      TTFBNR        SFFBNR
     C                   MOVE      TTACTI        SFACTI
     C                   MOVE      TTDTD         SFDTDX
     C                   MOVE      TTDTM         SFDTMX
     C                   MOVE      TTDTÅ         SFDTÅX
     C                   MOVE      TTDATE        SFDATE
     C                   MOVE      TTTIME        SFTIME
n09  C                   MOVE      TTDEPO        SFDEPO
n09  C                   MOVE      TTNAME        SFNAME
n09  C                   MOVE      TTMANU        SFMANU
s04  C*                  MOVE      TTTEXT        SFTEXT
n04  c     lang          ifeq      'E'
n04  C                   MOVE      TTTEXT        SFTEXT
n04  C                   else
n04  c                   move      tttexl        sftext
n04  c                   end
     C                   MOVEL     TTUSER        SFUSEX
s05  C*                  MOVE      recno         SFRECNO
n05  C  N60              MOVE      recno         SFRECNO
n05  c   60              move      recno9        sfrecno
     C                   WRITE     SUBFIL
     C     NEXT          TAG
s05  C*    FSKEY         READE     TRACKL01                               94
n05  C  N60FSKEY         READE     TRACKL01                               94
n05  C   60FSKEYT        READE     TRACKL09                               94
     C  N94              END
     C*
     C                   MOVE      'J'           FRTOPP            1
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C     FRTOPP        IFEQ      'J'
     C                   Z-ADD     1             ZRECNO
     C                   MOVE      'N'           FRTOPP            1
     C                   END
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
     C                   MOVEA     '00000'       *IN(86)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C*** F2 bytt språk
n04  C   02              do
n04  c     lang          ifeq      'E'
n04  c                   move      'N'           lang
n04  c                   else
n04  c                   move      'E'           lang
n04  c                   end
n04  c                   goto      start
n04  c                   enddo
     C*
     C*** F3 AVSLUTT
     C   03              GOTO      SLUTT
     C*** F12- OM EN HAR VÆRT INNOM F8 BETYR F12 NYTT SØK, ELLRS AVSLUT
     C     *IN12         IFEQ      '1'
     C     VIAF8         IFEQ      'J'
     C                   GOTO      VIAF8T
     C                   ELSE
     C                   GOTO      SLUTT
     C                   END
     C                   END
     C*** SØKING
     C   04              DO
N02  c                   movel     w2ACTI        xxACTI
N02  c     xxACTI        ifeq      *blanks
N02  c     W2Nr          andeq     '1'
N02  c                   movel     'Z'           xxACTI
N02  c                   endif
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    xxACTI            5
     c                   movel     xxacti        W2ACTI
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      START3
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C* NYTT UTVALG VIA HURTIGSØK...
     C     W2NR          IFEQ      ' '
     C     W2FBNR        IFNE      *BLANKS
     C     W2ACTI        ORNE      *BLANKS
     C     W2DATE        ORNE      *ZEROS
     C     W2FBNR        IFNE      *BLANKS
     C                   MOVE      W2FBNR        WFFBNR
     C                   MOVE      *BLANKS       W2FBNR
     C                   END
     C     W2ACTI        IFNE      *BLANKS
     C                   CLEAR                   WFKO
     C                   MOVE      W2ACTI        WFKO01
     C                   MOVE      *BLANKS       W2ACTI
     C                   END
     C     W2DATE        IFNE      *ZEROS
     C                   MOVE      W2DATE        WFDATE
     C                   MOVE      *ZEROS        W2DATE
     C                   EXSR      FIXDTA
     C                   end
     C                   GOTO      START
     C                   END
     C                   END
      * NYTT UTVALG VIA F8
     C     *IN08         IFEQ      '1'
     C     VIAF8T        TAG
     C                   MOVE      'J'           VIAF8             1
     C                   EXFMT     FILTERB
     C                   EXSR      FIXDTA
     C                   GOTO      START
     C                   END
     C*
N02   * F10 = På turnivå
N02  C     *IN10         IFEQ      '1'
N02  C     HEPRO         andne     *zeros
N02  C                   move      'Z  '         UTACTI
N02  C                   CALL      'SYTT05'
N02  C                   PARM                    HEPRO
N02  C                   PARM                    NULLTR            8 0
N02  C                   PARM                    UTACTI            3
N02  C                   GOTO      START
N02  C                   ENDIF
      *
     C*
N06   * F14 = bytt hovedoppdrag dup etc
      *
N06  C     *IN14         IFEQ      '1'
N06  C                   EXSR      BYTTOP
N06  C                   GOTO      START
N06  C                   END
     C     W2NR          IFEQ      '1'
     C                   EXSR      TESTB
     C   12              GOTO      START3
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFNE      0
S02  C*                  EXSR      HENT
N02  C                   EXSR      HENTSR
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     FIXDTA        BEGSR
     C***********************************************
      * FRAKTBREVSNR
     C     WFFBNR        IFNE      *BLANKS
     C                   TESTN                   WFFBNR               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      WFFBNR        WFFBNN            3 0
     C                   ELSE
     C                   MOVE      *ZEROS        WFFBNN            3 0
     C                   END
     C                   END
      * EVENTDATO
     C     WFDATE        IFEQ      *ZEROS
     C                   MOVE      *ZEROS        WFDA8E
     C                   ELSE
     C                   MOVE      WFDED6        WFDED8
     C                   MOVE      WFDEM6        WFDEM8
     C                   MOVE      WFDEÅ6        WFDEÅ8
     C     WFDEÅ6        IFGT      50
     C                   MOVEL     '19'          WFDEÅ8
     C                   ELSE
     C                   MOVEL     '20'          WFDEÅ8
     C                   END
     C                   END
      * LOGG-DATO
     C     WFDATL        IFEQ      *ZEROS
     C                   MOVE      *ZEROS        WFDA8L
     C                   ELSE
     C                   MOVE      WFDLD6        WFDLD8
     C                   MOVE      WFDLM6        WFDLM8
     C                   MOVE      WFDLÅ6        WFDLÅ8
     C     WFDLÅ6        IFGT      50
     C                   MOVEL     '19'          WFDLÅ8
     C                   ELSE
     C                   MOVEL     '20'          WFDLÅ8
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *
     C***********************************************
     C     FILTER        BEGSR
     C***********************************************
     C*
     C                   MOVE      'N'           SKIPP             1
     C* ACT KODE
     C     WFKO          IFNE      *BLANKS
     C     TTACTI        LOOKUP    KO                                     41
     C  N41              MOVE      'J'           SKIPP             1
     C  N41              GOTO      UTSKIP
     C                   END
     C* ACT GRUPPE
      * HENT GRUPPE/WEBKODE KUN OM DET ER TEST PÅ EN AV DE 2
     C     WFGR          IFNE      *BLANKS
     C     WFINEX        ORNE      *BLANKS
     C                   MOVEL     TTACTI        KFKOD
     C     KOKEY         CHAIN     KOFAST                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFTXT         KODGRP
     C                   MOVE      KFTXT         KODWEB
     C                   ELSE
     C                   MOVE      *BLANKS       KODGRP
     C                   MOVE      *BLANKS       KODWEB
     C                   END
     C                   END
     C     WFGR          IFNE      *BLANKS
     C     KODGRP        LOOKUP    GR                                     41
     C  N41              MOVE      'J'           SKIPP             1
     C  N41              GOTO      UTSKIP
     C                   END
      * FRAKTBREVSNR / ALLE / BARE OPPDRAGET
     C     WFFBNR        IFNE      *BLANKS
     C     TTFBNR        ANDNE     WFFBNN
     C                   MOVE      'J'           SKIPP             1
     C                   GOTO      UTSKIP
     C                   END
      * F.O.M EVENTDATO
     C     WFDA8E        IFNE      *ZEROS
     C     TTDATE        ANDLT     WFDA8E
     C                   MOVE      'J'           SKIPP             1
     C                   GOTO      UTSKIP
     C                   END
      * F.O.M LOGG-DATO
     C     WFDA8L        IFNE      *ZEROS
     C     TTDATL        ANDLT     WFDA8L
     C                   MOVE      'J'           SKIPP             1
     C                   GOTO      UTSKIP
     C                   END
      * INTERN/EKSTERNKODE (WEBKODE)
     C     WFINEX        IFEQ      'I'
     C     KODWEB        ANDEQ     'J'
     C                   MOVE      'J'           SKIPP             1
     C                   GOTO      UTSKIP
     C                   END
     C     WFINEX        IFEQ      'E'
     C     KODWEB        ANDNE     'J'
     C                   MOVE      'J'           SKIPP             1
     C                   GOTO      UTSKIP
     C                   END
     C*
     C     UTSKIP        ENDSR
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C* RUTINER ER HELT SKREVET OM UTEN MERKING (VAR IKKE I BRUK FØR..)
      *
     c                   CLEAR                   VARELI
      *
N08  C                   MOVE      '*LE'         TTEDEV                         Manu=Concat Local/EN
s01  C*                  MOVE      'J'           TTMANU
N04  c     uopd          ifne      0
N01  C                   MOVE      'S'           TTMANU
n04  c                   else
n04  c                   move      'J'           TTMANU
n04  c                   end
     C                   MOVE      UAVD          TTAVD
     C                   MOVE      UOPD          TTOPD
n05  C                   MOVE      Utur          TTtur
     C                   TESTN                   W2FBNR               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      W2FBNR        TTFBNR
     C                   END
n05  C   60              MOVE      *zeros        TTFBNR
     C                   MOVE      W2ACTI        TTACTI
     c     TTacti        ifne      *blanks
     C                   MOVEL     TTACTI        KFKOD
     C     KOKEY         CHAIN     KOFAST                             89
     C     *IN89         IFEQ      '0'
     C                   MOVEL     KFTXT         KODTXT
N07  C                   MOVEL     KFTXTE        KODTXTE          50
     C                   MOVEL     KFTXT         KODGRP
     C                   MOVE      KFTXT         KODWEB
     C                   ELSE
     C                   MOVE      *BLANKS       KODTXT
N07  C                   MOVE      *BLANKS       KODTXTE
     C                   MOVE      *BLANKS       KODGRP
     C                   MOVE      *BLANKS       KODWEB
     C                   END
     c                   end
     C                   MOVE      ZUSER         TTUSER
     C                   TIME                    XULTID
     C                   MOVE      XULDD         TTDTLD
     C                   MOVE      XULMM         TTDTLM
     C                   MOVE      XULÅÅ         TTDTLÅ
     C                   MOVE      XULTM         TTTIML
     c     w2date        ifeq      *zeros
     c                   move      TTDATL        TTDATE
     c                   else
     C                   MOVE      WSDTD         TTDTD
     C                   MOVE      WSDTM         TTDTM
     C                   MOVE      WSDTÅ         TTDTÅ
     C     WSDTÅ         IFGT      50
     C                   MOVEL     '19'          TTDTÅ
     C                   ELSE
     C                   MOVEL     '20'          TTDTÅ
     C                   END
     c                   end
N03  c     w2time        ifeq      *zeros
N03  c                   move      TTTIML        TTTIME
N03  c                   else
N03  C                   MOVE      *zeros        TTTIME
N03  C                   MOVEL     w2time        TTTIME
N03  c                   endif
      *cursor
     C     TTacti        ifeq      *blanks
     C     *IN89         OReq      '1'
S08  C*                  Z-ADD     15            LINNBR
N08  C                   Z-ADD     14            LINNBR
     C                   Z-ADD     20            POSNBR
N03  C     TTacti        ifeq      *blanks
N03  C                   movel     'Z'           TTACTI
N03  C                   Z-ADD     21            POSNBR
N03  c                   end
     c                   else
S03  C*                  Z-ADD     16            LINNBR
S03  C*                  Z-ADD     35            POSNBR
s04  C*                  Z-ADD     17            LINNBR
S08  C                   Z-ADD     16            LINNBR
N08  C                   Z-ADD     15            LINNBR
N03  C                   Z-ADD     5             POSNBR
     c                   end
      *
     c     feilTag       tag
     c                   exfmt     vareli
     c                   move      '0'           *IN30
     c                   movea     '00000'       *IN(85)
     C   12              GOTO      SLTB
     c     *in04         ifeq      '1'
N02  c                   movel     TTACTI        xxACTI
N02  c     xxACTI        ifeq      *blanks
N02  c                   movel     'Z'           xxACTI
N02  c                   endif
N03  c                   movel     xxACTI        gmACTI            3
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    xxACTI            5
     c                   movel     xxacti        TTACTI
N03  c     xxACTI        ifne      gmActi
S08  C*                  Z-ADD     17            LINNBR
N08  C                   Z-ADD     16            LINNBR
N03  C                   Z-ADD     5             POSNBR
N03  c                   endif
     c                   goto      FeilTag
     c                   end
      *
      * TID
     C     TTTIME        IFLT      000100
     C     TTTIME        ORGT      235959
S08  C*                  Z-ADD     16            LINNBR
N08  C                   Z-ADD     15            LINNBR
     C                   Z-ADD     35            POSNBR
     C                   SETON                                        8830
     C                   MOVE      'JOV1422'     MSGNR
     C                   END
      * DATO
     C                   MOVE      TTDTÅ2        UÅÅ               2 0
     C                   MOVE      TTDTM         UMM               2 0
     C                   MOVE      TTDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAG2            1 0
     C     UFLAG2        COMP      1                                      87
     C   87              MOVE      'SPD0001'     MSGNR
     C   87              SETON                                        30
s08  C*  87              Z-ADD     16            LINNBR
N08  C   87              Z-ADD     15            LINNBR
     C   87              Z-ADD     20            POSNBR
     C*
      * ACT-KOD
     C     TTACTI        IFEQ      *BLANKS
S08  C*                  Z-ADD     15            LINNBR
N08  C                   Z-ADD     14            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'JOV1421'     MSGNR
     C                   ELSE
     C                   MOVEL     TTACTI        KFKOD
     C     KOKEY         CHAIN     KOFAST                             89
     C     *IN89         IFEQ      '1'
s08  C*                  Z-ADD     15            LINNBR
N08  C                   Z-ADD     14            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'JOV1421'     MSGNR
     C                   END
     C                   END
     C     *IN89         IFEQ      '0'
     C                   MOVEL     KFTXT         KODTXT
N07  C                   MOVEL     KFTXTE        KODTXTE
     C                   MOVEL     KFTXT         KODGRP
     C                   MOVE      KFTXT         KODWEB
     C                   ELSE
     C                   MOVE      *BLANKS       KODTXT
N07  C                   MOVE      *BLANKS       KODTXTE
     C                   MOVE      *BLANKS       KODGRP
     C                   MOVE      *BLANKS       KODWEB
     C                   END
      *
      * FRAKTBR.NR
     C     TTFBNR        IFNE      *ZEROS
     C                   MOVE      ttFBNR        XXFBNR
     C     FBNRKE        CHAIN     DOKUF                              86
     C     *IN86         IFEQ      '1'
s08  C*                  Z-ADD     15            LINNBR
N08  C                   Z-ADD     14            LINNBR
     C                   Z-ADD     10            POSNBR
     C                   SETON                                        8630
     C                   MOVE      'SPF0027'     MSGNR
     C                   END
     C                   END
S08  C* N30TTTEXT        IFEQ      *BLANKS
N08  C  N30TTTEXL        IFEQ      *BLANKS
s07  C*                  MOVEL     KODTXT        TTTEXT
N07  C                   MOVEL     KODTXT        TTTEXL
N07  C                   MOVEL     KODTXTE       TTTEXT
s08  C*                  Z-ADD     17            LINNBR
N08  C                   Z-ADD     16            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'JOV1423'     MSGNR
     C                   SETON                                          30
     C                   END
      *
     C   30              GOTO      FEILTAG
      *
     C                   TIME                    XULTID
     C                   MOVE      XULDD         TTDTLD
     C                   MOVE      XULMM         TTDTLM
     C                   MOVE      XULÅÅ         TTDTLÅ
     C                   MOVE      XULTM         TTTIML
     C                   WRITE     TRACKFRX
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *Blanks       W2FBNR
     C                   MOVE      *BLANKS       W2ACTI
     C                   MOVE      *ZEROS        W2DATE
N03  C                   MOVE      *ZEROS        W2time
      * manuelt tillagt TEI -> fiks dispstat E på evt disp.oppdrag
     c     TTACTI        ifeq      'TEI'
S10  c*    utur          andne     *zeros
     c                   exsr      TEISR
     C                   end
     C*
     C     SLTB          ENDSR
     C***********************************************
     C     TEISR         BEGSR
     C***********************************************
     C     FBNRK         setll     DOKUF
     C     FBNRK         reade     DOKUF                                  33
     C     *in33         doweq     *off
     C     DFBRTX        IFEQ      'U.op:'
     C     DFBRAV        ANDGE     '0001'
     C     DFBRAV        ANDLE     '9999'
     C     DFBROP        ANDGE     '0000001'
     C     DFBROP        ANDLE     '9999999'
     C                   MOVE      DFBRAV        DIAVD
     C                   MOVE      DFBROP        DIOPD
     C     DISPKE        CHAIN     DISP                               34
     C  N34DISTA1        IFEQ      'E'
     C     DISTA1        OREQ      'V'
     C                   MOVE      ' '           DISTA1
     C                   UPDATE    DISPR
     C                   END
     C                   END
     C     FBNRK         reade     DOKUF                                  33
     C                   END
     c
     C                   ENDSR
     C*
     C***********************************************
S02  C*    HENT          BEGSR
N02  C     HENTSR        BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   Z-ADD     2             POSNBR
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C     LINNBR        IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   END
     C     WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     SFRECNO       CHAIN     TRACKF                             33
     C     *IN33         IFEQ      '0'
     C     TTMANU        IFNE      'J'
     C                   MOVE      'JOV1424'     MSGNR
     C                   SETON                                            30
     C                   update    TRACKFRX
     C                   ELSE
     C                   DELETE    TRACKFRX
     C                   MOVE      *ZEROS        TTFBNR
     C                   MOVE      *BLANKS       TTACTI
     C                   MOVE      *BLANKS       TTTEXT
n04  C                   MOVE      *BLANKS       TTTEXL
     C                   MOVE      *ZEROS        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVE      *BLANKS       SFACTI
     C                   MOVE      *BLANKS       SFTEXX
     C                   MOVE      *BLANKS       SFUSEX
     C                   MOVE      *ZEROS        SFDATX
     C                   MOVE      *ZEROS        SFTIMX
     C                   MOVE      '*DELETE'     XDLT
     C                   END
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C     *IN30         CABEQ     '1'           SLHENT
     C                   END
     C                   END
     C     WSNR          IFEQ      '5'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     SFRECNO       CHAIN(N)  TRACKF                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     TTACTI        KFKOD
     C     KOKEY         CHAIN     KOFAST                             33
     C     *IN33         IFEQ      '0'
s07  C*                  MOVEL     KFTXT         KODTXT
n07  C                   MOVEL     KFTXT         KODTXT
n07  C                   MOVEL     KFTXTE        KODTXTE
     C                   MOVEL     KFTXT         KODGRP
     C                   MOVE      KFTXT         KODWEB
     C                   ELSE
     C                   MOVE      *BLANKS       KODTXT
N07  C                   MOVE      *BLANKS       KODTXTE
     C                   MOVE      *BLANKS       KODGRP
     C                   MOVE      *BLANKS       KODWEB
     C                   END
     C                   EXFMT     VARELI
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   SETON                                        98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     FSKEY         KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
n05  C     FSKEYT        KLIST
n05  C                   KFLD                    TTTUR
     C     TRA01K        KLIST
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C                   KFLD                    SFDATE
     C                   KFLD                    SFTIME
     C     KOKEY         KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'TRACKT'      KFTYP
     C     DISPKE        KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C     FBNRK         KLIST
     C                   KFLD                    YRI
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C     FBNRKE        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C                   KFLD                    XXFBNR            3 0
     C                   MOVE      'F'           YRI               1
      *
     C                   MOVE      'N'           UUPDAT            1
N04  C                   MOVE      'N'           LANG              1
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
n05  C                   PARM                    Utur              8 0
n05  C                   PARM                    Uflagg            1 0
n05  C                   move      utur          TTTUR
     C                   move      UAVD          TTAVD
     C                   move      UOPD          TTOPD
n05  c     tttur         comp      0                                  60
n05  c     *in60         ifeq      '0'
n02  c     FSkey         chain     Headf
N02  c     HEPRO         comp      *zeros                             5050
n05  c                   end
N06  C                   MOVEA     *ZEROS        AV
N06  C                   MOVEA     *ZEROS        OP
      *
n06  C                   move      UAVD          uavd0
n06  C                   move      UOPD          uopd0
n06  c                   move      uavd          av(1)
n06  c                   move      uopd          op(1)
n06  c                   z-add     1             opvis             1 0
n06  c     tropd0        ifne      *zeros
n06  c                   move      'HO'          op1
n06  c                   move      travd0        op1av
n06  c                   move      tropd0        op1op
n06  c                   move      travd0        av(2)
n06  c                   move      tropd0        op(2)
n06  c                   else
n06  c     tropd1        ifne      *zeros
n06  c                   move      'FF'          op1
n06  c                   MOVE      TRAVD1        OP1AV
n06  c                   MOVE      TROPD1        OP1OP
n06  c                   MOVE      TRAVD1        AV(2)
n06  c                   MOVE      TROPD1        OP(2)
n06  c                   end
n06  c     tropd2        ifne      *zeros
n06  c                   move      'VF'          op2
n06  c                   MOVE      TRAVD2        OP2AV
n06  c                   MOVE      TROPD2        OP2OP
n06  c                   MOVE      TRAVD2        AV(3)
n06  c                   MOVE      TROPD2        OP(3)
n06  c                   end
n06  c                   end
     C                   MOVE      *BLANKS       W2FBNR
     C                   MOVE      *ZEROS        W2DATE
N03  C                   MOVE      *ZEROS        W2time
     C                   MOVE      *ZEROS        TTDATE
     C                   MOVE      *ZEROS        WFDATE
     C                   MOVE      *ZEROS        WFDA8E
     C                   MOVE      *ZEROS        WFDATL
     C                   MOVE      *ZEROS        WFDA8L
      *
s09  C*                  WRITE     WINDOWSZ
      *
     C                   ENDSR
     C*
     C*
N06  C***********************************************
N06  C     BYTTOP        BEGSR
N06  C***********************************************
N06  c                   add       1             opvis
N06  c     opvis         ifeq      4
n06  c                   z-add     1             opvis
n06  c                   end
n06  c     op(opvis)     ifeq      *zeros
n06  c                   add       1             opvis
n06  c                   end
N06  c     opvis         ifeq      4
n06  c                   z-add     1             opvis
n06  c                   end
n06  c     op(opvis)     ifeq      *zeros
n06  c                   add       1             opvis
n06  c                   end
N06  c     opvis         ifeq      4
n06  c                   z-add     1             opvis
n06  c                   end
n06  C                   move      av(opvis)     TTAVD
n06  C                   move      op(opvis)     TTOPD
n06  C                   move      ttavd         uavd0
n06  C                   move      ttopd         uopd0
n06  c                   endsr
