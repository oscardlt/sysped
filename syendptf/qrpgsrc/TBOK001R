      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TBOK001R MODULE(*LIBL/TBOK001R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Innført fra/til dator - klokkelsett
      * 02 JOV PTF12 AVVIKENDE BRUKERID / MAL FOR HEURx -parametre
      * 03 JOV PTF12 feil på avd
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FZEADL05   IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D ZB              S              1    DIM(35)
     D ZC              S              1    DIM(17)
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     D xFakBet         S              1

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬kundnr¬ : ¬'+%trim(%editc(KUNDNR:'Z'))+'¬, '
        +'¬knavn¬ : ¬'+%trim(KNAVN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE ordretyper ved NY
      * ............................................................................................
     c                   eval      JSONstring ='"orderTypesNew" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C                   MOVE      'HEUR '       PAID
N02  c     Heur2         tag
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     c                   z-add     PAVRDN        XXAVD             4 0
     c                   move      XXAVD         XXAVDA            4
     C                   Add       1             AntLest
     c                   eval      xFakBet = 'S'                                Sender
     c                   if        %subst(PAVRDA:5:1)='K'
     c                   eval      xFakBet = 'M'                                Mottaker
     c                   endif
      /free
       if AntLest=1;
          JSONstring=*blanks;
       else;
          JSONstring=', ';
       endif;
       JSONstring=%trim(JSONstring)+'{'
S03    // +'¬newAvd¬ : ¬'+%trim(%editc(PAVRDN:'Z'))+'¬, '
N03       +'¬newAvd¬ : ¬'+%trim(%editc(XXAVD:'Z'))+'¬, '
          +'¬newModul¬ : ¬'+%trim(%subst(PAVRDA:1:1))+'¬, '
          +'¬newModul2¬ : ¬'+%trim(%subst(PAVRDA:2:1))+'¬, '
          +'¬newLandKode¬ : ¬'+%trim(%subst(PAVRDA:3:2))+'¬, '
          +'¬newSideSK¬ : ¬'+%trim(%subst(PAVRDA:5:1))+'¬, '
          +'¬xfakBet¬ : ¬'+%trim(xFakBet)+'¬, '
          +'¬newText¬ : ¬'+%trim(%subst(PAVRDA:6))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
N02   * hvis ingen typer på useren - sjekk om det finnes på dummy/mal-user HEURXUSR
N02  C                   if        AntLest=0  and
N02  C                             HEURxUSR<>*blanks and
N02  C                             PABRID  <>HEURxUSR
N02  C                   eval      PABRID  = HEURxUSR
N02  C                   goto      HEUR2
N02  C                   endif
      * ...slutt Ordretypeliste
     c                   eval      JSONstring ='], '
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"orderList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Angivelser
      *
     c     Error         ifeq      *blanks

      * HENT KUNDENAVN
     C     CUNDL01K      CHAIN     CUNDL01                            33
      * HENT OPPDRAGSGIVER
N02  C                   MOVE      BIBRID        PABRID
     C                   MOVEL     'HEKN1'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C   33              MOVE      BIKUNR        XKNGIVER          8 0
     C  N33              EVAL      XKNGIVER = PAVRDN

     C                   move      *zeros        AntLest
     C     XKNGIVER      SETLL     ZEADL05
     C     XKNGIVER      READE     ZEADL05                                55
     C     *IN55         DOWEQ     '0'

     C*                  IF        (((XKNGIVER <> BIKUNR) AND
     C*                              (HEKNS <> BIKUNR)) AND
     C*                              (HEKNK <> BIKUNR))
     C*                  GOTO      HOPP
     C*                  END

     C                   select
     C     HEUR          wheneq    'H'
     C                   EVAL      XFRALK = HELKA
     C                   EVAL      XTILLK = HETRI
     C     HEUR          wheneq    'A'
     C     HEUR          oreq      'B'
     C                   EVAL      XFRALK = HELKS
     C                   EVAL      XTILLK = HELKK
     C                   OTHER
     C                   MOVE      *BLANKS       XFRALK            2
     C                   MOVE      *BLANKS       XTILLK            2
     c                   endsl

     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬unik¬ : ¬'+%trim(%editc(HEUNIK:'Z'))+'¬, '
          +'¬hereff¬ : ¬'+%trim(HEREFF)+'¬, '
          +'¬status¬ : ¬'+%trim(HEST)+'¬, '
          +'¬hedtop¬ : ¬'+%trim(%editc(HEDTOP:'Z'))+'¬, '
N01       +'¬trsdfd¬ : ¬'+%trim(%editc(TRSDFD:'Z'))+'¬, '
N01       +'¬trsdfk¬ : ¬'+%trim(%editc(TRSDFK:'Z'))+'¬, '
N01       +'¬trsdtd¬ : ¬'+%trim(%editc(TRSDTD:'Z'))+'¬, '
N01       +'¬trsdtk¬ : ¬'+%trim(%editc(TRSDTK:'Z'))+'¬, '
          +'¬hegn¬ : ¬'+%trim(HEGN)+'¬, '
          +'¬henas¬ : ¬'+%trim(henas)+'¬, '
          +'¬henak¬ : ¬'+%trim(henak)+'¬, '
          +'¬hefr¬ : ¬'+%trim(HEFR)+'¬, '
          +'¬hent¬ : ¬'+%trim(%editc(HENT:'Z'))+'¬, '
          +'¬hevkt¬ : ¬'+%trim(%editc(HEVKT:'Z'))+'¬, '
          +'¬hem3¬ : ¬'+%trim(%editc(HEM3:'4'))+'¬, '
          +'¬hesdf¬ : ¬'+%trim(HESDF)+'¬, '
          +'¬hesdt¬ : ¬'+%trim(HESDT)+'¬, '
          +'¬xfralk¬ : ¬'+%trim(XFRALK)+'¬, '
          +'¬xtillk¬ : ¬'+%trim(XTILLK)+'¬, '
          +'¬hepk1¬ : ¬'+%trim(HEPK1)+'¬, '
          +'¬hepk2¬ : ¬'+%trim(HEPK2)+'¬, '
          +'¬hepk3¬ : ¬'+%trim(HEPK3)+'¬, '
          +'¬hepk4¬ : ¬'+%trim(HEPK4)+'¬, '
          +'¬hepk5¬ : ¬'+%trim(HEPK5)+'¬, '
          +'¬herfa¬ : ¬'+%trim(HERFA)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     HOPP          TAG

     C     XKNGIVER      READE     ZEADL05                                55
     C                   ENDDO
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C  N50              CLOSE     ZEADL05
     C  N50              CLOSE     CUNDL01

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C**                 EVAL      WSSTAT  = zhbgetvar('STATUS')
      *
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      ZEADL05
     C  N50              OPEN      CUNDL01
     C  N50              OPEN      BRPARF

     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
N02   * AVVIKENDE BRUKERID / MAL FOR HEURx -parametre
N02  c                   move      'HEURU'       PAID
N02  c     BrParKey      chain     BRPARF
N02  c                   if        %found
N02  c                   movel     PAVRDA        HEURxUsr         10
N02  c                   endif
N02
     c                   endsr

