      *
      *** PROGRAM SKAL SKRIVE UT LASTELISTE (SAD400)            ***
      *
     FKODTFM    IF   E           K DISK
     FSTANDT    IF   E           K DISK
     FSADTRH    UF   E           K DISK
     FSADTRH08  IF   E           K DISK
     F                                     RENAME(SADTRHR:SADTRHR8)
     FKODTSI    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTP     IF   E           K DISK
     FSAD025FM  CF   E             WORKSTN
     F                                     SLN(LINJE)
     FDATALIST  O    F  130        PRINTER OFLIND(*INOF)
     F                                     USROPN
     F                                     FORMLEN(72)
     F                                     FORMOFL(72)
     D TD              S              7  0 DIM(10)
     D UCMD            DS          1024
     D                 DS
     D XCMD1           C                   CONST('OVRPRTF -
     D                                     FILE(DATALIST) -
     D                                     OUTQ(')
     D XCMD2           C                   CONST('PAGESIZE(72 130) -
     D                                     CPI(15) -
     D                                     OVRFLW(72) -
     D                                     FORMTYPE(LASTELISTE)')
     D                 DS
     D  SETXT1                 1     17
     D  SETXT2                18     47
     D  SETXT3                48    102
     D  WSTXT1                 1     17
     D  WSTXT2                18     47
     D  WSTXT3                48    102
     D  WSTXTX                48     94
NV5  D                 DS
NV5  D  SEDTÅ                  1      4  0
NV5  D  SEDTM                  5      6  0
NV5  D  SEDTD                  7      8  0
NV5  D  SEDT                   1      8  0
NV5  D                 DS
NV5  D  WSDTD                  1      2  0
NV5  D  WSDTM                  3      4  0
NV5  D  WSDTÅ                  5      6  0
NV5  D  WSDT                   1      6  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
      *
     C                   EXSR      INIT
      *
     C     START0        TAG
     C     SATHK         SETGT     SADTRH
     C                   EXSR      LES
      *
     C     START         TAG
     C                   EXFMT     BILDE01
     C   03              GOTO      SLUTT
     C   07              DO
     C     KODSIK        CHAIN     KODTSI                             98
     C  N98              EXSR      PRINT
     C                   GOTO      START
     C                   END
     C   25              DO
     C                   MOVE      XTDN          WSTDN
     C     SATHK         SETGT     SADTRH
     C                   EXSR      LES
     C                   GOTO      START
     C                   END
     C   26              DO
     C                   MOVE      XTDN          WSTDN
     C     SATHK         SETLL     SADTRH
     C                   EXSR      DOWN
     C                   GOTO      START
     C                   END
      *
     C     WSTDN         IFEQ      0
     C     WSN           COMP      0                                      99
     C  N99WSN           COMP      8                                  99
     C  N99TD(WSN)       COMP      0                                      99
     C  N99
     CAN 04              EXSR      VISSAD
     C  N99
     CAN 08              EXSR      PLUKK
     C  N99
     CAN 09              EXSR      FJERN
     C                   GOTO      START
     C                   ELSE
     C                   GOTO      START0
     C                   END
      *
     C     SLUTT         TAG
     C                   MOVE      XANT          UANT
     C                   CLOSE     DATALIST
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C     WSLNR         COMP      0                                  4040
     C     SATH8K        SETLL     SADTRH08
     C                   SETOFF                                       33
     C     SATH8K        READE     SADTRH08                               33
     C   33              GOTO      SLPRI
      *
     C                   Z-ADD     1             XNR               3 0
     C                   Z-ADD     21            XANTL             2 0
     C                   Z-ADD     1             XSIDE             2 0
      *
      *** UIN01 = 0 BETYR SKAL HA LINJESKILLE PÅ UTSKRIFT
     C     UIN01         COMP      '0'                                    27
     C                   MOVE      SEDK          XDK               2
     C                   EXCEPT    DLHEAD
      *
     C     STPRI         TAG
     C                   MOVE      SETDN         WSTDN
     C     SATHK         CHAIN     SADTRH                             33
     C                   MOVE      ' '           SESTL
     C                   UPDATE    SADTRHR
      *
     C     WSTXT3        COMP      *BLANKS                                42
     C   42              ADD       1             XANTL
     C  N42              ADD       2             XANTL
     C     XANTL         IFGE      64
     C  N42
     CAN 27XANTL         IFEQ      64
     C                   EXCEPT    DLLIN
     C                   END
     C                   EXCEPT    DLBUNN
     C                   ADD       1             XSIDE
     C                   EXCEPT    DLHEAD
     C                   Z-ADD     22            XANTL
     C  N42              ADD       1             XANTL
     C                   END
     C                   EXCEPT    DLDET
     C  N42              EXCEPT    DLDET2
     C                   ADD       1             XNR
      *
     C     SATH8K        READE     SADTRH08                               33
     C  N33              GOTO      STPRI
      *
     C   27XANTL         DOWLE     64
     C                   EXCEPT    DLLIN
     C                   ADD       1             XANTL
     C                   END
     C                   EXCEPT    DLBUNN
     C                   ADD       1             XANT
      *
     C     SLPRI         TAG
     C                   EXSR      BYGGL
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISSAD        BEGSR
     C***********************************************
     C                   MOVE      TD(WSN)       WSTDN
     C     SATHK         CHAIN     SADTRH                             33
NV5  C                   MOVE      SEDTD         WSDTD
NV5  C                   MOVE      SEDTM         WSDTM
NV5  C                   MOVE      SEDTÅ         WSDTÅ
     C     STVIS         TAG
     C                   EXFMT     TOLLDEK
     C  N12              GOTO      STVIS
     C                   EXSR      BYGGL
      *
     C                   ENDSR
      *
     C***********************************************
     C     PLUKK         BEGSR
     C***********************************************
     C                   MOVE      TD(WSN)       WSTDN
     C     SATHK         CHAIN     SADTRH                             33
     C     SEST          COMP      ' '                                9797
     C   97SEST          COMP      'P'                                9797
     C   97              GOTO      SLPLUK
      *
     C     STPLUK        TAG
     C                   EXFMT     BILDE02
     C     WSTXT1        COMP      *BLANKS                                99
     C   99WSTXT2        COMP      *BLANKS                                99
     C   99WSTXT3        COMP      *BLANKS                                99
     C   99              GOTO      STPLUK
      *
     C                   MOVE      'L'           SESTL
     C                   MOVE      UWS           SEWS
     C                   UPDATE    SADTRHR
     C                   EXSR      BYGGL
      *
     C     SLPLUK        ENDSR
      *
     C***********************************************
     C     FJERN         BEGSR
     C***********************************************
     C                   MOVE      TD(WSN)       WSTDN
     C     SATHK         CHAIN     SADTRH                             33
     C     SEST          COMP      ' '                                3333
     C   33SEST          COMP      'P'                                3333
     C  N33SESTL         COMP      'L'                                3333
     C  N33SEWS          COMP      UWS                                3333
     C   33              GOTO      SLFJER
      *
     C                   MOVE      ' '           SESTL
     C                   MOVE      *BLANKS       SEWS
     C                   UPDATE    SADTRHR
     C                   EXSR      BYGGL
      *
     C     SLFJER        ENDSR
      *
     C***********************************************
     C     BYGGL         BEGSR
     C***********************************************
     C     TD(1)         IFNE      0
     C                   MOVE      TD(1)         WSTDN
     C     SATHK         SETGT     SADTRHR
     C                   MOVE      *ZEROS        RULB
     C                   EXSR      LES
     C                   ELSE
     C                   MOVE      TD(8)         WSTDN
     C     SATHK         SETLL     SADTRHR
     C                   MOVE      *ZEROS        RULF
     C                   EXSR      DOWN
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     LES           BEGSR
     C***********************************************
     C                   Z-ADD     5             LINJE
     C                   SETOFF                                       60
     C                   MOVE      *ZEROS        RULF              2 0
     C                   MOVE      *ZEROS        XTDN              7 0
     C                   MOVE      *ZEROS        WSN
     C                   MOVE      *ZEROS        TD
     C                   WRITE     HEADER
      *
     C     STLES         TAG
     C     UAVD          READPE(N) SADTRH                                 60
     C  N60              SUB       1             RULB                 59
     C  N60
     CAN 59              GOTO      STLES
     C  N60              DO
     C                   ADD       1             WSN
     C                   MOVE      SETDN         TD(WSN)
     C                   MOVE      SETDN         XTDN
     C     SESTL         COMP      'L'                                    59
     C                   WRITE     SUBFIL
     C                   ADD       1             RULF
     C                   ADD       2             LINJE
     C     LINJE         COMP      19                                 60
     C  N60              GOTO      STLES
     C                   END
     C                   MOVE      *ZEROS        WSN
      *
     C                   ENDSR
      *
     C***********************************************
     C     DOWN          BEGSR
     C***********************************************
     C                   Z-ADD     19            LINJE
     C                   SETOFF                                       60
     C                   MOVE      *ZEROS        RULB              2 0
     C                   MOVE      '99999'       XTDN
     C                   Z-ADD     8             WSN
     C                   MOVE      *ZEROS        TD
     C                   WRITE     HEADER
      *
     C     STDOW         TAG
     C     UAVD          READE(N)  SADTRH                                 60
     C  N60              SUB       1             RULF                 59
     C  N60
     CAN 59              GOTO      STDOW
     C  N60              DO
     C                   SUB       1             WSN
     C                   MOVE      SETDN         TD(WSN)
     C                   MOVE      SETDN         XTDN
     C     SESTL         COMP      'L'                                    59
     C                   WRITE     SUBFIL
     C                   ADD       1             RULB
     C                   SUB       2             LINJE
     C     LINJE         COMP      5                                    60
     C  N60              GOTO      STDOW
     C                   END
     C                   MOVE      *ZEROS        WSN
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SATHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    WSTDN
     C     SATH8K        KLIST
     C                   KFLD                    XSTL
     C                   KFLD                    UWS
     C     KODTAK        KLIST
     C                   KFLD                    XAUNI
     C                   KFLD                    UAVD
     C     KODSIK        KLIST
     C                   KFLD                    XSIUNI
     C                   KFLD                    WSSG
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KOAKNR
     C     KODKEY        KLIST
     C                   KFLD                    WOPUNI
     C                   KFLD                    WOPAVD
     C                   KFLD                    WOPLNR
      *
     C                   MOVE      'L'           XSTL              1
     C                   MOVE      'A'           XAUNI             1
     C                   MOVE      'SI'          XSIUNI            2
     C                   MOVE      *BLANKS       WSTXT1
     C                   MOVE      *BLANKS       WSTXT2
     C                   MOVE      *BLANKS       WSTXT3
     C                   MOVE      '99999'       WSTDN
     C                   MOVE      *ZEROS        XANT              1 0
     C     *LIKE         DEFINE    KOPAVD        WOPAVD
     C     *LIKE         DEFINE    KOPUNI        WOPUNI
     C     *LIKE         DEFINE    KOPLNR        WOPLNR
     C                   Z-ADD     0             WOPAVD
     C                   MOVE      'P'           WOPUNI
     C                   Z-ADD     0             WOPLNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
     C                   PARM                    UWS              10
     C                   PARM                    UANT              1
      *
     C     UAVD          CHAIN     STANDT                             33
     C                   Z-ADD     UAVD          WOPAVD
     C                   Z-ADD     95            WOPLNR
     C     KODKEY        CHAIN     KODTP                              33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       KOPNVN
     C                   MOVEL     '*JOB'        KOPNVN
     C                   ENDIF
     C     XCMD1         CAT       KOPNVN:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD2:1       UCMD
     C                   Z-ADD     1024          UCMDL            15 5
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      DATALIST
      *
     C     KODTAK        CHAIN     KODTA                              33
     C                   MOVE      KOANVN        UAVDNV
     C     KUND1K        CHAIN     CUNDL01                            33
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          XADR3            30
     C                   ELSE
     C                   MOVEL     POSTNR        XADR3
     C                   MOVE      ADR3          XADR3
     C                   END
      *
     C*
      * HENT STRINGKODE
     C                   Z-ADD     95            WTSKRN            3 0
     C                   MOVE      UAVD          LOSAV2            4 0
     C                   CALL      'SYGE01'
     C                   PARM                    LOSAV2
     C                   PARM                    WTSKRN
     C                   PARM                    WOPNVN           10
     C                   PARM                    WOPTY            10
     C                   PARM                    WOPHEA            1
     C                   PARM                    WOPFM             5
     C     WOPFM         IFNE      *BLANKS
     C     WOPFM         CHAIN     KODTFM                             33
     C                   END
     C*
     C                   ENDSR
      *
      *
     ODATALIST  E            DLHEAD           01
     O                       KFMT1               75
     ODATALIST  E    27      DLHEAD           04
     O                                           60 'L A S T E L I S T E'
     O          E    27      DLHEAD           05
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            DLHEAD           06
     O               27                          73 '!'
     O               40                          83 'LISTE NR.'
     O               40      WSLNR               85
     O          E            DLHEAD           07
     O               27                          73 '!'
     O                       KNAVN               65
     O          E            DLHEAD           08
     O               27                          73 '!'
     O                       ADR1                65
     O          E            DLHEAD           09
     O               27                          96 '------------------------'
     O               27                         120 '------------------------'
     O                       ADR2                65
     O          E            DLHEAD           10
     O               27                          73 '!'
     O                       XADR3               65
     O          E    27      DLHEAD           11
     O                                           73 '!'
     O          E    27      DLHEAD           12
     O                                           73 '!'
     O          E    27      DLHEAD           13
     O                                           73 '!'
     O          E    27      DLHEAD           14
     O                                           73 '!'
     O          E    27      DLHEAD           15
     O                                           73 '!'
     O          E    27      DLHEAD           16
     O                                           73 '!'
     O          E    27      DLHEAD           17
     O                                           73 '!'
     O          E    27      DLHEAD           18
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E    27      DLHEAD           19
     O                                           16 'VAREPOST   !'
     O                                           96 '!AVS./UTF.! BRUTTOVEKT !'
     O          E    27      DLHEAD           20
     O                                           16 '   NR      !'
     O                                           45 'MERKER & NR ANTALL KOLLI'
     O                                           70 'OG SLAG; VAREBESKRIVELSE'
     O                                           96 '!  LAND   !    (KG)    !'
     O                                          114 'FOR TOLLVESENET'
     O          E    27      DLHEAD           21
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E   N27      DLHEAD           21
     O          E            DLDET       1
     O                       XNR           Z     10
     O                       WSTXT1              33
     O                       SENTK         Z     40
     O                       WSTXT2              71
     O                                           79 'NO'
     O                       SEVKB         Z     92
     O                       SETLE              104
     O                                          106 '-'
     O                       SETLL              117
     O               27                          16 '!'
     O               27                          73 '!'
     O               27                          83 '!'
     O               27                          96 '!'
     O          E            DLDET2      1
     O                       WSTXT3              71
     O               27                          16 '!'
     O               27                          73 '!'
     O               27                          83 '!'
     O               27                          96 '!'
     O          E            DLLIN       1
     O               27                          16 '!'
     O               27                          73 '!'
     O               27                          83 '!'
     O               27                          96 '!'
     O          E    27      DLBUNN           66
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            DLBUNN           69
     O               27                          15 'RG-0139'
     O               27                         120 '........................'
     O                       KSINAV             116
     O          E    27      DLBUNN           70
     O                                          120 '     (UNDERSKRIFT)      '
     ODATALIST  E            DLBUNN           71
     O                       KFMT2               75
