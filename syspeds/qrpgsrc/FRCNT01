      *********************************************************************
      *  RPG ILE MODULE SYSPED/FRCNT01
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/FRCNT01 MODULE(*LIBL/FRCNT01
CRTPGM*         *LIBL/ASGORDNBR) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Bookshelves by description
     FKODTA     IF   e           K DISK    USROPN
      * Books on the bookshelves
     FDISPL04   IF   e           K DISK    USROPN
      *=====================================================================
      * Includes to be used in all CGIs
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      *  Fields used for parsing the input string
     D action          s             10a
     D subaction       s             10a
     D lng             s              2a
      * aktiv tur...
     D Tur             s              8a
     D Turnr           s              8  0
      * aktiv avd...
     D Avd             s              4a
     D Avdnr           s              4  0
      *
     D htmlsrclib      s             10a
     D htmlsrcfil      s             10a
     D htmlsrcmbr      s             10a
      *--------------------------------------------------------------------
      * "InitSW"- is blank only the first time pgm is called
     DInitSW           S              1a
      *
     Dlinenbr          S              5s 0
      *=====================================================================
     C                   exsr      SetTimeStr
      *=====================================================================
      * Read remote browser input string and parse it
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prolog3
     C                   exsr      Parse
      *=====================================================================
      * Main line
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
     C                   exsr      LoadHtml
      *------------------
      * Start html
     C                   callp     wrtsection('top')
     C                   eval      wrotetop = *on                               For pssr
      * Start left leg
     C                   callp     wrtsection('leg1')
      * Fill left leg
      *  Open file
     C                   exsr      OpenDbf
     c     Disp4Key      KLIST
     C                   kfld                    DISTA1
     C                   kfld                    DITUR
     C                   kfld                    KOAAVD
      *
      * Gjeldende tur
     C                   callp     updHTMLvar('TUR':
     C                             %trim(%editc(TurNr:'Z')))
      *
     C     *loval        setll     Kodta
     C                   eval      linenbr = 0
      *
     C                   read      kodta
     C                   dow       not%eof
     c     KOAIE         ifne      '1'
     c     KOAIE         andne     '2'
     c     KOAIE         andne     '3'
     c                   goto      NextAvd
     c                   end
     C                   eval      linenbr = linenbr +1
     C                   exsr      SetLeg1Row
     C     Disp4Key      chain     Displ04
     C                   if        %found
     C                   callp     wrtsection('leg1rowa')
     C                   else
     C                   callp     wrtsection('leg1rowb')
     C                   endif
     C     NextAvd       Tag
     C                   read      kodta
     C                   enddo
      *
     C                   exsr      CloseDbf
      * End left leg
     C                   callp     wrtsection('leg1end')
      * Flush the html buffer and exit
     C                   exsr      Exit
      *=====================================================================
      * Set variables in html section "leg1row"
      *=====================================================================
     C     SetLeg1Row    begsr
      * Line number
     C                   callp     updHTMLvar('linenbr':
     C                             %trim(%editc(linenbr:'Z')))
      * Aktivt turnr
     C                   callp     updHTMLvar('TUR':Tur)
      * Bookshelf number (unedited)
     C                   move      KOAAVD        AVD               4
     C                   callp     updHTMLvar('AVD':AVD)
      * Bookshelf description
     C                   callp     updHTMLvar('AVDDES':KOANVN)
      *
     C                   endsr
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
      *=====================================================================
     C     LoadHtml      begsr
      * Read externally defined output html
     C                   eval      HtmlSrcLib = '*LIBL'
     C                   eval      HtmlSrcFil = 'HTMLSRC' + lng
     C                   eval      HtmlSrcMbr = 'FRCNT01'
     C                   callp     gethtml(HtmlSrcFil:HtmlSrcLib:HtmlSrcMbr:
     C                             '/CGITAG/')
      *
     C     *like         define    lng           prvlng
     C                   eval      prvlng = lng
     C                   endsr
      *=====================================================================
      * Open database files
      *=====================================================================
     C     OpenDbf       begsr
      *
     C                   call      'INCGIFULL'
     C                   open      KODTA
     C                   open      DISPL04
     C                   endsr
      *=====================================================================
      * Close database files
      *=====================================================================
     C     CloseDbf      begsr
     C                   close     KODTA
     C                   close     DISPL04
     C                   endsr
      *=====================================================================
      *  Initialization
      *=====================================================================
     C     Init          begsr
     C                   exsr      SetTimeStr
     C                   endsr
      *=====================================================================
      *  Time started
      *=====================================================================
     C     SetTimeStr    begsr
     C                   time                    TimeStart
     C                   endsr
      *=====================================================================
      *  Time ended and time elapsed elapsed
      *=====================================================================
     C     SetTimeEnd    begsr
     C                   time                    TimeEnd
     C     TimeEnd       subdur    TimeStart     MsElaps:*ms
     C                   eval      SecElaps = MsElaps / 1000000
     C                   endsr
      *=====================================================================
      * Send response html and quit
      *=====================================================================
     C     Exit          begsr
     C                   exsr      SetTimeEnd
     C                   callp     updhtmlvar('RUNTIME':
     C                             %trim(%editw(SecElaps:'     0 .   ')))
     C                   callp     wrtsection('runtime')
     C                   callp     wrtsection('end')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   return
     C                   endsr
      *=====================================================================
      * Parse the input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng        = zhbgetvar('lng       ')
     C                   eval      Tur        = zhbgetvar('TUR       ')
     C                   eval      Turnr      = c2n2(tur)
     C                   endsr
