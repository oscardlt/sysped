     A*****************************************************************
     A* UTSKRIFT AV LAGERTELLELISTE                                  *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80 OVERFLOW=64       *
     A*****************************************************************
     A* HEADING
     A          R HEAD
     A                                  2  2'LL046R'
     A                                  2 12MSGCON(40 YBC1535 SYMSGF)
     A                                      HIGHLIGHT
     A                                  2 68MSGCON(05 YBC1536 SYMSGF)
     A                                  2 74PAGNBR EDTCDE(Z)
     A                                  3  2MSGCON(14 YBC1537 SYMSGF)
     A            NVNGRU        30A     3 17
     A                                      HIGHLIGHT
     A                                  4  2MSGCON(14 YBC1538 SYMSGF)
     A            NVNKJE        30A     4 17
     A                                      HIGHLIGHT
     A                                  4 41MSGCON(08 YBC1539 SYMSGF)
     A            LTLNR          7S 0   4 60EDTCDE(Z)
     A                                  5  2MSGCON(16 YBC1540 SYMSGF)
     A            XDT            8S 0   5 20EDTWRD('    .  .  ')
     A                                      HIGHLIGHT
     A                                  5 42MSGCON(14 YBC1541 SYMSGF)
     A            XIDAG          8S 0   5 60EDTWRD('    .  .  ')
     A                                  6  2MSGCON(30 YBC1542 SYMSGF)
     A                                  7  2MSGCON(08 YBC1543 SYMSGF)
     A            WSARTF        15A     7 11
     A                                      HIGHLIGHT
     A                                  7 27'->'
     A            WSARTT        15A     7 30
     A                                      HIGHLIGHT
     A                                  8  2MSGCON(08 YBC1544 SYMSGF)
     A            WSLAGF        25A     8 11
     A                                      HIGHLIGHT
     A                                  8 37'->'
     A            WSLAGT        25A     8 40
     A                                      HIGHLIGHT
     A                                  9  2MSGCON(08 YBC1545 SYMSGF)
     A            WSDATF         6S 0   9 11EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                  9 20'->'
     A            WSDATT         6S 0   9 23EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                 11  2MSGCON(20 YBC1546 SYMSGF)
     A  52                                   HIGHLIGHT
     A                                 11 29MSGCON(15 YBC1547 SYMSGF)
     A  51                                   HIGHLIGHT
     A                                 11 44MSGCON(15 YBC1548 SYMSGF)
     A                                 11 75'BATCHNUMMER'
     A                                 11 91MSGCON(10 YBC1549 SYMSGF)
     A  53                                   HIGHLIGHT
     A                                 11102MSGCON(03 YBC1550 SYMSGF)
     A                                 12  2'-------------------------'
     A                                 12 27'-------------------------'
     A                                 12 52'-------------------------'
     A                                 12 77'-------------------------'
     A                                 12102'-------------'
     A                                 13  2' '
      *DETALJER (Dette gods/artikkelnr)
     A          R DETALJ                    SPACEB(1)
     A            LTLOK1         5A        2
     A  52                                   HIGHLIGHT
     A            LTLOK2        20A        8
     A  52                                   HIGHLIGHT
     A            LTANR         15A       29
     A  51                                   HIGHLIGHT
     A            LTVARX        30A       44
     A            LTBATX        15A       75
     A            LTDATO         8S 0     91EDTWRD('    .  .  ')
     A  53                                   HIGHLIGHT
     A                                   102'___'
      *
     A          R BUNN                      SPACEB(2)
     A                                     2MSGCON(122 YBC1921 SYMSGF)
