CB1   * Lagt til kostander innen EU (Egentlig terminalkostnader) til Øvriga kostnader
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 CB  PTF11 Extern TULLID TDS SE
********************************************************************
     FHEADF     IF   E           K DISK
     FLLHEAD07  IF   E           K DISK
     FLLLINE    UF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLAGJFEU  IF   E           K DISK
     FLLSERL03  IF   E           K DISK
     FLLTARF    IF   E           K DISK
     FLLMRF     IF   E           K DISK
     FSVIA      IF   E           K DISK
     FSVIH      UF A E           K DISK
     FSVIV      O  A E           K DISK
     FSVTHA     IF   E           K DISK
     FSVTVK     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKUNKO2    IF   E           K DISK
     D SN              S             17    DIM(9)
      *
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UDSAVD                15     18  0
     D  UDSSG                 19     21
     D                 DS
     D  VAFAR                  1     10
     D  VAVAL                  1      3
     D                 DS
     D  SVIH_SYAV              1      4  0
     D  SVIH_SYOP              5     11  0
     D  X_SYSREF               1     11
      *
     C                   EXSR      INIT
      *
     C     HEADFK        CHAIN(N)  SVIH                               33
     C     *IN33         CABEQ     *OFF          SLUTT
     C     HEADFK        CHAIN     HEADF                              33
     C  N33HEADFK        CHAIN     LLHEAD07                           33
     C     *IN33         CABEQ     *ON           SLUTT
     C                   EVAL      KUNDNR = LHPNR
     C     CUNDL01K      CHAIN     CUNDL01                            33
      *
     C     LLAGJD02K2    CHAIN(N)  LLAGJD02                           33
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C     LLMRFK        CHAIN     LLMRF                              33
      *
     C     SVIH_SYAV     CHAIN     SVIA                               33
      *
      * FORTOLLING HEADING
     c                   MOVE      *ZEROS        XLI
     C                   EVAL      SVIH_SYSG = HESG
     C                   EVAL      SVIH_SYDT = WDT08
     C                   EVAL      SVIH_MTYP = 'DNK'
N01  C                   IF        UTUID = *BLANKS
     C                   CALL      'SVG001R'
     C                   PARM                    SVIH_TUID
N01  C                   ELSE
N01  C                   EVAL      SVIH_TUID=UTUID
N01  C                   EVAL      SVIH_SYST='G'
N01  C                   ENDIF
     C     KUNKO2KEY     CHAIN     KUNKO2                             33
     C                   IF        *IN33=*OFF
     C                   EVAL      KUNDNR    = KUKUN2
     C                   ELSE
     C                   IF        LHPNR     = 3022
     C                   EVAL      KUNDNR    = 1433
     C                   ENDIF
     C                   ENDIF
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      SVIH_AVKN = KUNDNR
     C                   EVAL      SVIH_AVNA = KNAVN
     C                   EVAL      SVIH_AVA1 = ADR1
     C                   EVAL      SVIH_AVA2 = ADR2
     C                   EVAL      SVIH_AVPN = SYPOGE
     C                   EVAL      SVIH_AVPA = ADR3
     C                   EVAL      SVIH_AVLK = SYLAND
     C                   EVAL      SVIH_AVUT = SYLAND
     C                   EVAL      SVIH_KOTA = LHKLL
     C                   EVAL      SVIH_RFAB = X_SYSREF
     C                   EVAL      SVIH_AVLK = SYLAND
     C                   EVAL      SVIH_DEK1 = 'IM'
     C                   EVAL      SVIH_DEK2 = 'A'
     C                   EVAL      SVIH_TART = '1'
     C                   EVAL      KUNDNR    = LHPNR
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      SVIH_MOKN = KUNDNR
     C                   EVAL      SVIH_MOEO = EORI
     C                   EVAL      SVIH_MONA = KNAVN
     C                   EVAL      SVIH_MOA1 = ADR1
     C                   EVAL      SVIH_MOA2 = ADR2
     C                   EVAL      SVIH_MOPN = LHBPU
     C                   EVAL      SVIH_MOPA = LHBA3
     C                   EVAL      SVIH_MOLK = LHBLK
     C                   EVAL      SVIH_MOHA = KPERS
     C                   EVAL      SVIH_MOTL = TLF
     C                   EVAL      SVIH_DKKN = 1
     C                   EVAL      KUNDNR    = 1
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   IF        EORI = *BLANKS                               1<-B
     C                   MOVEL(P)  SYLAND        SVIH_DKEO
     C                   CAT       SYRG:0        SVIH_DKEO
     C                   CAT       '00':0        SVIH_DKEO
     C                   IF        SYRG=*BLANKS                                  2<-B
     C                   MOVEL(P)  SYLAND        SVIH_DKEO
     C                   CAT       VATKKU:0      SVIH_DKEO
     C                   ENDIF                                                   2<-E
     C                   ELSE                                                   1
     C                   EVAL      SVIH_DKEO = EORI
     C                   END                                                    1<-E
     C                   EVAL      SVIH_DKLK = SYLAND
     C                   EVAL      SVIH_DKNA = KNAVN
     C                   EVAL      SVIH_DKA1 = ADR1
     C                   EVAL      SVIH_DKA2 = ADR2
     C                   IF        POSTNR = 0                                   1<-B
      *
     C                   EVAL      P = %SCAN ('-' : SYPOGE)
     C     9             SUB       P             Q                 1 0
     C                   ADD       1             P                 3 0
     C                   EVAL      SVIH_DKPN = %SUBST(SYPOGE:P:Q)
      *
     C                   ELSE                                                   1
     C                   EVAL      SVIH_DKPN = *BLANKS
     C                   MOVEL     POSTNR        SVIH_DKPN
     C                   ENDIF                                                  1<-E
     C                   EVAL      SVIH_DKPA = ADR3
     C                   IF        SYLAND <> *BLANKS                            1<-B
     C                   EVAL      SVIH_DKLK = SYLAND
     C                   ENDIF                                                  1<-E
     C     SVIH_SYSG     CHAIN     SVTHA                              33
     C                   IF        *IN33=*OFF
     C                   EVAL      SVIH_DKHA = SVTH_NAMN
     C                   EVAL      SVIH_DKTL = SVTH_TLF
     C                   EVAL      SVIH_OMHA = SVTH_NAMN
     C                   EVAL      SVIH_OMTL = SVTH_TLF
     C                   ENDIF
     C                   EVAL      SVIH_OMEO = SVIA_OMEO
     C                   EVAL      SVIH_OMTY = SVIA_OMTY
     C                   IF        LHPNR     = 3022
     C                   EVAL      SVIH_OMTY = '3'
     C                   ENDIF
     C                   EVAL      SVIH_CONT = '0'
     C                   EVAL      SVIH_FATY = 'N380'
     C                   EVAL      SVIH_FATX = LHPSD
     C                   EVAL      SVIH_TRGR = '1'
     C                   EVAL      SVIH_TRIN = '3'
     C                   EVAL      SVIH_GOLK = %SUBST(MRGN:1:3)
     C                   EVAL      SVIH_BRUT = LHWVEK
     C                   EVAL      SVIH_EUP1 = '4071'
     C                   IF        LHPNR     = 3022
     C                   EVAL      SVIH_UPPS = 'O'
     C                   ELSE
     C                   EVAL      SVIH_UPPS = *BLANKS
     C                   END
     C                   EVAL      SVIH_GODN = MRGN
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        LLWNT0 <> 0                                   2<-B
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   SELECT                                                    4<-B
     C                   WHEN      SVIH_VAKD = *BLANKS                             4
     C                   IF        VAVAL = *BLANKS                                  5<-B
     C                   EVAL      SVIH_VAKD = 'SEK'
     C                   ELSE                                                       5
     C                   EVAL      SVIH_VAKD = VAVAL
     C                   END                                                        5<-E
     C                   WHEN      SVIH_VAKD <> VAVAL AND VAVAL <> *BLANKS         4
     C                   EVAL      SVIH_VAKD = 'SEK'
     C                   ENDSL                                                     4<-E
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   ENDDO                                                    3<-E
     C                   END                                                     2<-E
     C     LHMN          READE(N)  LLLINE                                 33
     C                   ENDDO                                                  1<-E
     C     SVTVKK        SETGT     SVTVK
     C     SVIH_VAKD     READPE    SVTVK                                  33
     C                   EVAL      SVIH_VAKU = SVVK_KRS

     C                   WRITE     SVIHR

     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        LLWNT0 <> 0                                   2<-B
     C                   EXSR      OPPDLIN
     C                   MOVE      'SAD'         LLHK
     C                   END                                                     2<-E
     C                   UPDATE    LLLINER
     C     LHMN          READE     LLLINE                                 33
     C                   ENDDO                                                  1<-E
      *
     C     HEADFK        CHAIN     SVIH                               33
     C                   EVAL      SVIH_VUFR = X_VUFR
CB1  C                   EVAL      SVIH_OVKO = X_OVKO
     C                   EVAL      SVIH_VUVA = X_VUVA
     C                   EVAL      SVIH_FABL = X_FABL
     C                   UPDATE    SVIHR
      *
N01  C                   IF        UTUID = *BLANKS
     C                   CALL      'SVI001R'
     C                   PARM      'E'           MODE              1            E=Endre N=Ny D=Vis
     C                   PARM      SVIH_SYAV     UVIH_SYAV
     C                   PARM      SVIH_SYOP     UVIH_SYOP
     C                   PARM      SVIH_SYSG     UVIH_SYSG
N01  C                   ENDIF
      *
     C     SLUTT         TAG
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     OPPDLIN       BEGSR
      *////////////////////////////////////////////////////////////
     C                   CLEAR                   SVIVR
     C                   EVAL      SVIV_SYAV = SVIH_SYAV
     C                   EVAL      SVIV_SYOP = SVIH_SYOP
      *
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   IF        %SUBST(VALLOK:1:5) = SVIH_GOLK
      *
     C     LLAGJFEUK     CHAIN     LLAGJFEU                           33
     C                   IF        *IN33
     C                   EVAL      VAOPLK = *blanks
     C                   EVAL      VAFVUE = *BLANKS
     C                   EVAL      VAFRUE = 0
     C                   EVAL      VAFVIE = *BLANKS
     C                   EVAL      VAFRIE = 0
     C                   END
      *
     C                   EVAL      XLI       = XLI + 1
     C                   EVAL      SVIV_SYLI = XLI
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      SVIH_SYAV     VDAVD
     C                   MOVE      SVIH_SYOP     VDOPD
     C                   EVAL      VDTEXT = LHBNA
     C                   EVAL      VDST   = 'R'
     C                   EVAL      VDBUSE = 'SV-IMPORT'
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDST   = 'R'
     C                   UPDATE    LLAGJDR
      *
     C                   IF        VAFVUE <> *BLANKS
     C                   EVAL      X_VUVA = VAFVUE
     C                   END
     C                   EVAL      X_VUFR = X_VUFR + VAFRUE * VDLANT
CB1  C     SVTVKK2       SETGT     SVTVK
CB1  C     VAFVIE        READPE    SVTVK                                  33
CB1  C                   EVAL      X_OVKO = X_OVKO +
CB1  C                             VAFRIE * SVVK_KRS / SVVK_OMR * VDLANT
     C                   EVAL      SVIV_KOTA = 1
     C                   EVAL      SVIV_KOSL = 'NE'
     C                   EVAL      SVIV_VASL = LLVB
      * SERIENR
     C                   EVAL      SN        = *BLANKS
     C                   Z-ADD     0             XN01              3 0
     C     LLSERL03K     SETLL     LLSERL03
     C     LLSERL03K     READE     LLSERL03                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        SNST = 'P'
     C                   ADD       1             XN01
     C                   EVAL      SN(XN01) = SNSN
     C                   END
     C     LLSERL03K     READE     LLSERL03                               33
     C                   ENDDO
     C     1             DO        9             XN01
     C                   IF        SN(XN01) = *BLANKS
     C                   LEAVE
     C                   END
     C                   SELECT
     C                   WHEN      XN01 = 1
     C                   EVAL      SVIV_VAS2 = SNSN
     C                   WHEN      XN01 < 4
     C                   EVAL      SVIV_VAS2 = %TRIM(SVIV_VAS2) + ', ' + SNSN
     C                   WHEN      XN01 = 4
     C                   EVAL      SVIV_VAS3 = SNSN
     C                   WHEN      XN01 < 7
     C                   EVAL      SVIV_VAS3 = %TRIM(SVIV_VAS3) + ', ' + SNSN
     C                   WHEN      XN01 = 7
     C                   EVAL      SVIV_VAS3 = SNSN
     C                   OTHER
     C                   EVAL      SVIV_VAS3 = %TRIM(SVIV_VAS3) + ', ' + SNSN
     C                   ENDSL
     C                   ENDDO
      * TARIC NR
     C     LLTARFK       CHAIN     LLTARF                             33
     C  N33              EVAL      SVIV_VATA = LTTANR
     C  N33              EVAL      SVIV_ULKD = LTLKOP
      *
     C                   EVAL      SVIV_VANO = SVIV_SYLI
     C                   EVAL      SVIV_VATI = '4999'
     C                   EVAL      SVIV_BRUT = VDLANT * VAVKT0
     C                   EVAL      SVIV_FOKD = '100'
     C                   EVAL      SVIV_EUP1 = '4071'
     C                   EVAL      SVIV_NETO = SVIV_BRUT * 0,9
     C                   EVAL      SVIV_ANKV = VDLANT
     C                   EVAL      SVIV_FABL = VAVERD * VDLANT
     C                   EVAL      X_FABL    = X_FABL + VAVERD * VDLANT
     C                   EVAL      SVIV_BIT1 = 'N380'
     C                   EVAL      SVIV_BII1 = LHPSD
     C                   IF        SVIH_MOKN = 3022
     C                   EVAL      SVIV_BIT2 = '3MSE'
     C                   EVAL      SVIV_BII2 = 'SE502071988501'
     C                   EVAL      SVIV_SUTX = 'COLLICARE AGERAR SOM INDIREKT '
     C                             + 'OMBUD ÅT POLARIS SALES EUROPE SARL'
     C                   END
     C                   EVAL      SVIV_LAGI = MRGN
     C                   EVAL      SVIV_LAGT = 'C'
     C                   EVAL      SVIV_LAGL = 'SE'
     C                   EVAL      SVIV_CALL = '00'
      *
     C                   WRITE     SVIVR
      *
     C                   END
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
N01  C                   PARM                    UTUID            10
      *
     C     HEADFK        KLIST
     C                   KFLD                    SVIH_SYAV
     C                   KFLD                    SVIH_SYOP
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     SVIHK         KLIST
     C                   KFLD                    SVIH_SYAV
     C                   KFLD                    SVIH_SYOP
     C     SVTVKK        KLIST
     C                   KFLD                    SVIH_VAKD
     C                   KFLD                    XX_DTS
CB1  C     SVTVKK2       KLIST
CB1  C                   KFLD                    VAFVIE
CB1  C                   KFLD                    XX_DTS
     C     LLAGJL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLAGJFEUK     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMN
     C     LLAGJD02K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGJD02K2    KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHMN
     C     LLMRFK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMRNR
     C     LLSERL03K     KLIST
     C                   KFLD                    VDMN
     C                   KFLD                    VDVN
     C     LLTARFK       KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C                   KFLD                    LTLK
     C     KUNKO2KEY     KLIST
     C                   KFLD                    LHPNR
     C                   KFLD                    LHBNR
      *
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C     *LIKE         DEFINE    SVIV_SYLI     XLI
     C     *LIKE         DEFINE    SVIH_SYAV     UVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     UVIH_SYOP
     C     *LIKE         DEFINE    SVIH_SYSG     UVIH_SYSG
     C     *LIKE         DEFINE    SVIH_VUFR     X_VUFR
     C     *LIKE         DEFINE    SVIH_VUVA     X_VUVA
     C     *LIKE         DEFINE    SVIH_FABL     X_FABL
     C     *LIKE         DEFINE    SVVK_DTS      XX_DTS
CB1  C     *LIKE         DEFINE    SVIH_OVKO     X_OVKO
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      UAVD          UDSAVD
     C                   MOVE      UAVD          SVIH_SYAV
     C                   MOVE      UOPD          SVIH_SYOP
     C                   MOVE      'SE'          LTLK
     C                   EVAL      XX_DTS = 99999999
     C                   OUT       SAVUDS
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               8
     C                   MOVE      WDT           WDT08             8 0
      *
     C                   ENDSR
      *
