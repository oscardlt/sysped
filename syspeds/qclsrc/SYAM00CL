             PGM
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0')
                       /* Hvis &in01 på, AVBRYT PROGRAM */
             DCL        VAR(&IN10) TYPE(*LGL) LEN(1) VALUE('0')
                       /* Hvis &in10 på, AMETA FERDIGMELDT */
             DCL        VAR(&P1) TYPE(*CHAR) LEN(2) VALUE('00')
             DCL        VAR(&P2) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&P3) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P4) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P5) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P6) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P7) TYPE(*CHAR) LEN(1) VALUE('J')
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til fakturaer. */
             DCL        VAR(&PT) TYPE(*CHAR) LEN(10) /* Papirtype til +
                          fakturaer. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7) /* Unikkode +
                          til register SORT. */
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&GIRO) TYPE(*DEC) LEN(3 0) VALUE(027)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&GIRON) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GIROP) TYPE(*CHAR) LEN(10)
    /* */
             DCL        VAR(&TURNR) TYPE(*DEC) LEN(8 0) VALUE(0)
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) /* ARB.STASJ*/
    /* */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* */
    /* KJØRE AMETAAVREGNING                  */
    /* */
             RTVJOBA    JOB(&WS) /* Hente arbeidstsjonskode WS */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* */
    /* BLANK ARBEIDSFILER ELLER                         */
    /* LEGG TIL NYTT MEMBER (WORKSTATION)               */
    /* */
             CLRPFM     FILE(AMDEWRK) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(AMDEWRK) +
                          MBR(&WS))
    /* */
             CLRPFM     FILE(AMGEWRK) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(AMGEWRK) +
                          MBR(&WS))
    /* */
    /* OVERRIDE FILE TIL MEMBER (=DSPSTN NAVN.) */
    /* */
             OVRDBF     FILE(AMDEWRK) TOFILE(AMDEWRK) MBR(&WS)
             OVRDBF     FILE(AMGEWRK) TOFILE(AMGEWRK) MBR(&WS)
             CALL       PGM(SYAM00) PARM(&IN10 &TURNR)
             IF         COND(&IN10 = '0') THEN(GOTO CMDLBL(SLUTT)) +
                          /* Hvis &IN10 av, avslutter program. */
    /* */
    /*       GOTO CMDLBL(SLUTT)    */
    /* */
    /* HENTE OPPLYSNINGER OM HVOR UTSKRIFTEN ØNSKES */
    /* */
             CALL       PGM(SYAMBK) PARM(&IN01 &P3)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* */
    /* SKRIVER UT FAKTURA */
    /* */
             RTVDTAARA  DTAARA(*LDA (681 10)) RTNVAR(&PRT)
             RTVDTAARA  DTAARA(*LDA (691 10)) RTNVAR(&PT)
    /* */
    /* RETRIVE DEVICEDESCRIPTION */
    /* */
             RTVDTAARA  DTAARA(*LDA (48 1)) RTNVAR(&TYPE)
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYAM20P) OUTQ(&PRT) DEVTYPE(*IPDS) +
                          PAGESIZE(72 80) CPI(10) OVRFLW(72) +
                          FONT(011) CHRID(590 340) FORMTYPE(&PT))
             ELSE       CMD(OVRPRTF FILE(SYAM20P) OUTQ(&PRT) +
                          DEVTYPE(*SCS) PAGESIZE(72 80) CPI(10) +
                          OVRFLW(72) FORMTYPE(&PT))
    /* */
    /* FIX SEPARATE GIRO */
    /* */
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             CALL       PGM(GETPRT) PARM(&AVD &GIRO &GIROP &GIRON)
    /* */
    /* RETRIVE DEVICEDESCRIPTION */
    /* */
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYAM20PG) OUTQ(&GIROP) DEVTYPE(*IPDS) +
                          PAGESIZE(24 80) CPI(10) OVRFLW(24) +
                          FONT(011) CHRID(590 340) FORMTYPE(&GIRON))
             ELSE       CMD(OVRPRTF FILE(SYAM20PG) OUTQ(&GIROP) +
                          DEVTYPE(*SCS) PAGESIZE(24 80) CPI(10) +
                          OVRFLW(24) FORMTYPE(&GIRON))
    /* */
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
    /*                                    */
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)
    /*                                    */
/* SORT OGSÅ I LØSNING 1 - INLIBL FOR Å SIKRE BIBLIOTEKSLISTE */
             CALL INLIBL
             RMVM       FILE(SORT) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(SORT) MBR(&UNIK)
             OVRDBF     FILE(SORT) TOFILE(SORT) MBR(&UNIK)
    /* */
             CALL       PGM(SYAM20) PARM(&P1 &P2 &P3 &P4 &P5 &P6 &P7)
             SNDPGMMSG  MSG(' ' *BCAT &P1 *BCAT 'ametafakturaer er +
                          klare til utskrift på printer ' *BCAT &PRT +
                          *BCAT '.') MSGTYPE(*INFO)
             RMVM       FILE(SORT) MBR(&UNIK)
    /* */
    /* UTSKRIFT AV AMETASKJEMA */
    /* */
             OVRPRTF    FILE(SYAM02PF) OUTQ(PRT06) PAGESIZE(72 132) +
                          CPI(15) OVRFLW(70) FORMTYPE(*STD)
    /* */
             CALL       PGM(SYAM02) PARM(&TURNR)
    /* */
 SLUTT:      ENDPGM
