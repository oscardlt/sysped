      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE09R MODULE(*LIBL/TJGE09R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF12 kall Med UNIK/REFF = kall bra booking/Z-filer
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFREETXT1  IF   E           K DISK    USROPN
N01  FZREETXT2  IF   E           K DISK    USROPN
N01  F                                     RENAME(FREETXTR:ZREETXTR)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
N01  D WA              S             15
N01  D UNIK            S              9S 0
N01  D REFF            S             17
     D DAT             S              8
     D DATN            S              8  0
     D LIN             S              2
     D LINN            S              2  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
N01     +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
N01     +'¬unik¬ : ¬'+%trim(%editc(UNIK:'Z'))+'¬, '
        +'¬dat¬ : ¬'+%trim(%editc(DATN:'Z'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(LINN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"freetxtGet" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Hæmta detta ena ærendet.

     C     Error         IFEQ      *blanks
N01  c                   IF        OPDN<>0
     C     FREEKEY       CHAIN     FREETXT1                           55
N01  c                   ELSE
N01  C     ZREEKEY       CHAIN     ZREETXT2                           55
N01  c                   ENDIF
     C     *IN55         IFEQ      '0'
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬frtavd¬ : ¬'+%trim(%editc(frtavd:'Z'))+'¬, '
          +'¬frtopd¬ : ¬'+%trim(%editc(frtopd:'Z'))+'¬, '
N01       +'¬frreff¬ : ¬'+%trim(FRREFF)+'¬, '
N01       +'¬frunik¬ : ¬'+%trim(%editc(FRUNIK:'Z'))+'¬, '
          +'¬frtdt¬ : ¬'+%trim(%editc(frtdt:'Z'))+'¬, '
          +'¬frtli¬ : ¬'+%trim(%editc(frtli:'Z'))+'¬, '
          +'¬frttxt¬ : ¬'+%trim(frttxt)+'¬, '
          +'¬frtkod¬ : ¬'+%trim(frtkod)+'¬, '
          +'¬frtfak¬ : ¬'+%trim(%editc(frtfak:'Z'))+'¬, '
          +'¬frtles¬ : ¬'+%trim(frtles)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONstring =']}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     FREETXT1
N01  C  N50              CLOSE     ZREETXT2

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   EVAL      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
N01  C                   eval      REFF    = zhbgetvar('REFF')
N01  C                   eval      WA      = zhbgetvar('UNIK')
N01  C                   eval      UNIK  = c2n2(WA)
     C                   EVAL      DAT     = zhbgetvar('DAT')
     C                   EVAL      DATN    = c2n2(DAT)
     C                   EVAL      LIN     = zhbgetvar('LIN')
     C                   EVAL      LINN    = c2n2(LIN)
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     C     FREEKEY       klist
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    DATN
     C                   KFLD                    LINN
N01  C     ZREEKEY       KLIST
N01  C                   KFLD                    UNIK
N01  C                   KFLD                    DATN
N01  C                   KFLD                    LINN

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      FREETXT1
N01  C  N50              OPEN      ZREETXT2

     C                   ENDSR
