      *********************************************************************
      * SLETT MERKET SLETT-LINJER
      * PROGRAM BEHANDLER INPUT FRA EISO13
      *
CRTPG+*  CRTPGM SYSPED/ESOVERS9 MODULE(*LIBL/ESOVERS9 *LIBL/ESOVERS8B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KOLLI PÅ CONTAINER
10XXX * 02 YBC PTF10 SORTERING
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FHEADF     UF   E           K DISK    USROPN
     FBLV1      UF   E           K DISK    USROPN
N02  FBLV1LF    UF   E           K DISK    USROPN
N02  F                                     RENAME(BLV1R:BLV1RLF)
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10

     D WSUSER          S             10
N02  D WSAVD           S              4
     D BSAVD           S              4  0
N02  D WSOPD           S              7
     D BSOPD           S              7  0
     D BSLI            S              3  0
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D SernoC          s            105a
N02  D WSSORT          S              3
N02  D BSSORT          S              3  0
N02  D SL              S              3  0 DIM(900)
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/ESOVERS8B')
     D                 DS
     D XXREF                   1     24
     D XXAVD                   1      4
     D XXOPD                   5     11
     D XXLI                   12     14
     D XXUSER                 15     24
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
N02   * Oppdater sortering
N02  C                   exsr      SORT
      * Slett plukklinje
     C                   exsr      SLETT
      *
     C                   EXSR      VISLINJE
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
     C                   CLOSE     HEADF
     C                   CLOSE     BLV1
N02  C                   CLOSE     BLV1LF
     C                   CLOSE     BRIDF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')

     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   open      HEADF                                50
     C                   open      BLV1                                 50
N02  C                   open      BLV1LF                               50
     c     HEADFK        klist
     c                   kfld                    BSAVD
     c                   kfld                    BSOPD
     c     BLV1K         klist
     c                   kfld                    BSAVD
     c                   kfld                    BSOPD
     c                   kfld                    BSLI
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================
      *  Slett merket linje
      *=====================================================================
     C     SLETT         begsr
      *
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
     C                   IF        ItemCount <> 0
     C                   DOW       i < ItemCount
     C                   eval      i = i + 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   MOVEL     SERNOC        XXREF
     C                   MOVE      XXAVD         BSAVD
     C                   MOVE      XXOPD         BSOPD
     C                   MOVE      XXLI          BSLI
      *
     C     BLV1K         CHAIN     BLV1                               33
     C                   IF        *IN33 = *OFF
     C                   DELETE    BLV1R
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF
     C                   EVAL      HEVKT = HEVKT - BVVKT
     C                   EVAL      HEM3  = HEM3  - BVM3
     C                   EVAL      HENT  = HENT  - 1
     C                   UPDATE    HEADFR
     C                   END
     C                   END
      *
     C                   ENDDO
     C                   END
      *
     C                   MOVE      *ZEROS        BSLI
      *
     C                   endsr
      *=====================================================================
      * VIS LINJER
      *=====================================================================
     C     VISLINJE      begsr
      *
     C                   CALLB     PGM2
     C                   PARM                    BSAVD
     C                   PARM                    BSOPD
     C                   PARM                    BSLI
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFARG
     C                   PARM                    WSCN             17
     C                   PARM                    WSCT              2
     C                   PARM                    WSCTT            30
N01  C                   PARM                    BSNT              5 0
     C                   PARM                    BSVKT             9 0
     C                   PARM                    BSM3              9 3
     C                   PARM                    FMELDNR           2
     C                   PARM                    FEILMELDING      80
      *
     C                   endsr
N02   ****************************************
N02  C     SORT          BEGSR
N02   ****************************************
N02   *
N02  C                   eval      WSAVD   = zhbgetvar('WSAVD')
N02  C                   eval      BSAVD   = c2n2(WSAVD)
N02  C                   eval      WSOPD   = zhbgetvar('WSOPD')
N02  C                   eval      BSOPD   = c2n2(WSOPD)
N02  C                   MOVE      *ZEROS        SL
N02   *
N02  C     HEADFK        SETLL     BLV1LF
N02  C     HEADFK        READE     BLV1LF                                 33
N02  C                   eval      i = 0
N02  C                   DOW       *IN33 = *OFF
N02  C     1             DO        99            XN                3 0
N02  C                   SELECT
N02  C                   WHEN      SL(XN) = BVLI
N02  C                   UPDATE    BLV1RLF
N02  C                   GOTO      XLESNESTE
N02  C                   WHEN      SL(XN) = 0
N02  C                   EVAL      SL(XN) = BVLI
N02  C                   LEAVE
N02  C                   END
N02  C                   ENDDO
N02  C                   eval      i = I + 1
N02  C                   eval      WSSORT = ZhbGetVar('WSSORT':i)
N02  C                   eval      BSSORT = c2n2(WSSORT)
N02  C                   EVAL      BVSORT = BSSORT
N02  C                   UPDATE    BLV1RLF
N02   *
N02  C     XLESNESTE     TAG
N02  C     HEADFK        READE     BLV1LF                                 33
N02  C                   ENDDO
N02  C                   eval      i = 0
N02   *
N02  C                   ENDSR
N02   *
