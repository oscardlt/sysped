     A*____________________________
     A* SYSPED / TRANSPORTØRS FERGE AVREGNING - HODE
     A*""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R TFAVHR
     A            FHST           1          COLHDG('STATUS')
     A            FHTNR          8S 0       COLHDG('TRANSPORTØRNR.')
     A            FHDTC     R               REFFLD(DTC)
     A            FHDTÅ     R               REFFLD(DTÅ)
     A            FHDTM     R               REFFLD(DTM)
     A            FHRTNR    R               REFFLD(PRO)
     A                                      COLHDG('RUDNTURSNR')
     A            FHTAVD    R               REFFLD(AVD)
     A                                      COLHDG('AVDELING TUR')
     A            FHTUNR    R               REFFLD(PRO)
     A                                      COLHDG('TUR (DELTUR)')
     A            FHBRT     R               REFFLD(BEL11)
     A                                      COLHDG('SALGSPRIS     (A)')
     A            FHRKV     R               REFFLD(BEL11)
     A                                      COLHDG('REKV. KOSTN.  (B)')
     A            FHBUD     R               REFFLD(BEL11)
     A                                      COLHDG('BUDSJ/I.PÅLØ. (C)')
     A            FHJUS     R               REFFLD(BEL11)
     A                                      COLHDG('EKSTRA JUST.  (D)')
     A            FHTOT     R               REFFLD(BEL11)
     A                                      COLHDG('TOTALT AVR.BELØP')
     A* ANTALL POSTER AV TYPE (A-D) I SUBREGISTER (TRAVV)
     A            FHBRTA         3P 0       COLHDG('ANT. BRU.-PRO.(A)')
     A            FHRKVA         3P 0       COLHDG('ANT. REKV.KOST(B)')
     A            FHBUDA         3P 0       COLHDG('ANT. BUDS.KOST(C)')
     A            FHJUSA         3P 0       COLHDG('ANT. EKSTR.JUS(D)')
     A            FHAVNR         7P 0       COLHDG('AVREGNINGSNR')
     A*
     A          K FHST
     A          K FHTNR
     A          K FHDTC
     A          K FHDTÅ
     A          K FHDTM
     A          K FHRTNR
     A          K FHTAVD
     A          K FHTUNR
