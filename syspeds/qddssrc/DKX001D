     A*%%TS  SD  20140814  113212  CB          REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9041A* 01 YBC PTF9 FORHÅNDSVARSLING
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A*
     A          R DK001D01
     A*%%TS  SD  20140704  135202  CB          REL-V7R1M0  5770-WDS
     A                                      CA03(03 'Avslutt')
     A                                  1  2'SYSPED/DKX001R'
     A                                      COLOR(BLU)
     A                                  1 19'Underhold af koder NCTS'
     A                                      COLOR(WHT)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2 69DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  3 29'012=Språkkode'
     A                                      COLOR(BLU)
     A                                  4 29'013=Dokumentkode'
     A                                      COLOR(BLU)
     A                                  5 29'014=Tidigare dokument'
     A                                      COLOR(BLU)
     A                                  6 29'017=Kollityp'
     A                                      COLOR(BLU)
     A                                  7  2'Vælg typ av kode:'
     A            WSUNIK         3Y 0B  7 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  7 29'031=Deklarations typ'
     A                                      COLOR(BLU)
     A                                  8 29'039=Tillæggsupplysning'
     A                                      COLOR(BLU)
     A                                  9 29'047=Kontrollresultat '
     A                                      COLOR(BLU)
     A                                 10 29'064=Kænslig vara'
     A                                      COLOR(BLU)
     A                                 11 29'096=Speciella omstændigheter'
     A                                      COLOR(BLU)
     A                                 12 29'105=Tillgångskod før garanti'
     A                                      COLOR(BLU)
     A                                 13 29'106=Tullkontor referansenr'
     A                                      COLOR(BLU)
     A                                 14 29'116=Betalningssætt transportkostna-
     A                                      d'
     A                                      COLOR(BLU)
     A                                 17 29'023=Fejlkoder i CONTROL'
     A                                      COLOR(BLU)
     A                                 18 29'041=Kontrollindikator varepost'
     A                                      COLOR(BLU)
     A                                 19 29'042=Kontrollindikator Transit'
     A                                      COLOR(BLU)
     A                                 20 29'049=Fejlkoder i CUSRES'
     A                                      COLOR(BLU)
     A                                 21 29'149=Fejlkoder for datagrupp i CUSR-
     A                                      ES'
     A                                      COLOR(BLU)
     A                                 22 29'209=Fejlkoder for garanti'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Afslut '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 15 29'108=Transportmåde'
     A                                      COLOR(BLU)
     A                                  3 58'301=Statuskoder import'
     A                                      COLOR(BLU)
     A                                  4 58'302=Statuskoder eksport'
     A                                      COLOR(BLU)
      *
     A          R DK001D11                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            TKKODE        10   O  5  5
     A            TKTXTN        35A  O  5 16
     A            TKTXTE        35A  H
     A            TKAVG          1A  O  5 52
     A            TKANK          1A  O  5 56
     A            TKTRS          1A  O  5 60
     A*
     A          R DK001D12                  SFLCTL(DK001D11)
     A*%%TS  SD  20140625  093650  CB          REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0015)
     A  *DS4                                SFLSIZ(0015)
     A  *DS3                                SFLPAG(0014)
     A  *DS4                                SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CA03(03 'Avslutt program')
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A  91N93                               CA12(12 'Forrige')
     A            MSGNRW         7A  P
     A                                  1  2'SYSPED/DKX001R'
     A                                      COLOR(BLU)
     A            TR1MSG        40A  O  1 22MSGID(&MSGNRW SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  3'2=Ændre   4=Slette'
     A                                      COLOR(BLU)
     A N28                              4  2'Op Kode       Text       '
     A                                      COLOR(WHT)
     A  28                              4  2'Op Kode       Beskrivning         -
     A                                                      Avg Ank Trs'
     A                                      COLOR(WHT)
     A                                  2  3'Vælg og tryk Enter'
     A                                      COLOR(BLU)
     A*
     A          R DK001D13
     A*%%TS  SD  20140625  093650  CB          REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                 23  2'F3=Afslut    F5=Opdater     F6=Opr-
     A                                      ette    F8=søg   F12=Annule'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R DK001D14
     A*%%TS  SD  20140814  113212  CB          REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A N28                              1  2'   Kode       Beskrivelse (Dansk/E-
     A                                      ngelsk)'
     A                                      COLOR(WHT)
     A  28                              1  2'   Kod        Beskrivning (Dansk/E-
     A                                      ngelsk)'
     A                                      COLOR(WHT)
     A            WSKODE        10A  B  2  5TEXT('KODE')
     A N30N40
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WSTXTN        35A  B  2 16TEXT('FRITEKST')
     A N30 40
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A  28        WSAVG          1A  B  2 52TEXT('AVGANG')
     A                                      DSPATR(UL)
     A                                      VALUES('J' 'N')
     A  28        WSANK          1A  B  2 56TEXT('ANKOMST')
     A                                      DSPATR(UL)
     A                                      VALUES('J' 'N')
     A  28        WSTRS          1A  B  2 60TEXT('TRANSITT')
     A                                      DSPATR(UL)
     A                                      VALUES('J' 'N')
     A            WSTXTE        35A  B  3 16TEXT('FRITEKST')
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R DK001D14A
     A*%%TS  SD  20140814  113212  CB          REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A N28                              1  2'   Kode       Beskrivelse (Dansk/E-
     A                                      ngelsk)'
     A                                      COLOR(WHT)
     A  28                              1  2'   Kod        Beskrivning (Dansk/E-
     A                                      ngelsk)'
     A                                      COLOR(WHT)
     A            WSKODE        10A  B  2  5TEXT('KODE')
     A N30N40
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WSTXTN22      22A  B  2 16TEXT('FRITEKST')
     A N30 40
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            WSTXTE22      22A  B  3 16TEXT('FRITEKST')
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R DK001D15
     A*%%TS  SD  20140625  093650  CB          REL-V7R1M0  5770-WDS
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A N28                              1  2'   Kode       Beskrivelse (Dansk/E-
     A                                      ngelsk)'
     A                                      COLOR(WHT)
     A  28                              1  2'   Kod        Beskrivning (Dansk/E-
     A                                      ngelsk)        Avg Ank Trs'
     A                                      COLOR(WHT)
     A            TKKODE        10A  O  2  5TEXT('KODE')
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            TKTXTN        35A  O  2 16TEXT('FRITEKST')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  28        TKAVG          1A  O  2 52TEXT('AVGANG')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  28        TKANK          1A  O  2 56TEXT('ANKOMST')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  28        TKTRS          1A  O  2 60TEXT('TRANSITT')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            TKTXTE        35A  O  3 16TEXT('FRITEKST')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A*
     A          R DK001D16
     A*%%TS  SD  20140814  113212  CB          REL-V7R1M0  5770-WDS
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  2  2'Søk fra kode  --->'
     A            W2KODE        10A  B  2 22TEXT('KODE')
     A                                      DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  3  2'Søk fra tekst --->'
     A            W2TXTN        35A  B  3 22TEXT('FRITEKST')
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R DK001D17
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/DKX001R'
     A                                      COLOR(BLU)
     A            TR1MSG        40   O  1 22MSGID(&MSGNRW SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNRW         7A  P
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  2  3MSGCON(030 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  3MSGCON(030 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A N28                              4  2'Op Kod        Beskrivning'
     A                                      COLOR(WHT)
     A  28                              4  2'Op Kod        Beskrivning         -
     A                                                      Avg Ank Trs'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Avsluta   F5=Uppdatera   F6=-
     A                                       Skapa ny   F8=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
