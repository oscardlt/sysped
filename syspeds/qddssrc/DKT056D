     A*%%TS  SD  20131101  140208  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R DKT056DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            DKTH_SYSG      3A  O  8  4
     A            DKTH_NAMN     35A  O  8  8
     A            DKTH_USID     10   O  8 44
     A*
     A          R DKT056DB                  SFLCTL(DKT056DA)
     A*%%TS  SD  20131101  140208  CB          REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0011)
     A  *DS4                                SFLSIZ(0011)
     A  *DS3                                SFLPAG(0010)
     A  *DS4                                SFLPAG(0010)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/DKT056'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 27'Underhold af TOLD sagsbehandlere'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælg og tryk Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændre   4=Slette'
     A                                      COLOR(BLU)
     A                                  6 44'IBM i'
     A                                      COLOR(WHT)
     A                                  7  4'Sig'
     A                                      COLOR(WHT)
     A                                  7  8'Navn'
     A                                      COLOR(WHT)
     A                                  7 44'Userid'
     A                                      COLOR(WHT)
     A*
     A          R DKT056DC
     A*%%TS  SD  20131101  140208  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                 23  2'F3=Afslut'
     A                                      COLOR(BLU)
     A                                 23 14'F4=Søg'
     A                                      COLOR(BLU)
     A                                 23 23'F6=Oprette ny'
     A                                      COLOR(BLU)
     A                                 23 39'F12=Annuler'
     A                                      COLOR(BLU)
     A*
     A          R DKT056DD
     A*%%TS  SD  20131101  140208  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Forrige')
     A            ZKTH_SYSG      3A  B 19  4
     A  31                                  DSPATR(RI)
     A N30                                  DSPATR(PC)
     A            ZKTH_NAMN     35A  B 19  8
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            ZKTH_USID     10A  B 19 44
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 23 24'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R DKT056DE
     A                                 20  2' '
     A                                 21  2' '
      *
     A          R DKT056DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
