      *********************************************************************
      * Koble scannede dokument
      * PROGRAM BEHANDLER INPUT FRA SYMN1
CRTPG+*  CRTPGM SYSPED/ESARKSC MODULE(*LIBL/ESARKSC *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D FORMS           s              2
     D FRAMES          s              1
      *
     D KAT             s             50
     D KAT2            s             50
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Blank passord=anonym, OK, Ellers må være "Inne"
     C     BIPO          IFNE      *BLANKS
     C*                  IF        ((BIBRID = *BLANKS) OR
     C                   IF        ((*IN50 = *ON) OR
     C                              (BIINN = *BLANKS))
     C                   exsr      SetSkl01B
     C                   GOTO      SLUTT
     C                   END
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1.KALL FRA MENY, Frames ULIK ' '   VIS HTML SOM KONTROLLERER FORMS
     C     FRAMES        IFNE      ' '
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
     c                   goto      slutt
     C                   END
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * HOVEDRUTINE
      * Vis liste over brukerens mulige katalogbaner
     C                   EXSR      LESKAT
      * Compute run time
     C                   TIME                    timedata2
     C     timedata2     SUBDUR    timedata1     ms:*ms
     C                   EVAL      sec = ms / 1000000
     C                   CALLP     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))

     C                   callp     updHtmlVar('ENBANE':ENBANE)
     C                   CALLP     wrtsection('bot')

      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      FORMS   = zhbgetvar('FORMS')
     C                   eval      FRAMES  = zhbgetvar('FRAMES')
     c     Frames        ifeq      *Blanks
     C                   eval      FRAMES  = FORMS
     C                   end
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     updHTMLvar('FEILTXT':'.')
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
      * Hent dato fra og med. (legg ut også som hidden imput)
     C                   callp     updHTMLvar('KAT':KAT)
     C                   callp     updHTMLvar('KAT2':KAT2)
     C                   callp     updHTMLvar('FEILTEXT':Spaces)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'ESARKSC'
      * 1.KALL FRA MENY, FORMS ULIK ' ' - VIS HTML SOM KONTROLLERER FORMS
     C     FRAMES        IFNE      ' '
     C                   eval      Mbr = 'ESARKSCF'
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   OPEN      Cundl01
      * Key for CUNDL01
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     KunKey        CHAIN     Cundl01                            33
      * Key for BRPARF
     C     PARKEY        KLIST
     C                   KFLD                    WSUSER
     C                   KFLD                    WPAKUNR
     C                   KFLD                    WPAID
     C     *LIKE         DEFINE    PAKUNR        WPAKUNR
     C     *LIKE         DEFINE    PAID          WPAID
     C                   EVAL      WPAKUNR=0
     C                   EVAL      WPAID='ARTMP'
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     WsUSER        tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   CLOSE     cundl01
     C                   CLOSE     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
      *////////////////////////////////////////////////////////////
      *  Les fra BRPARF og legg ut oversikt                LESKAT /
      *////////////////////////////////////////////////////////////
     C     LESKAT        BEGSR
     C                   MOVE      *ZEROS        XANTBANE          3 0
     C                   OPEN      BRPARF
     C     STLESKAT      TAG
     C     PARKEY        SETLL     BRPARF                                 51
     C                   IF        *IN51
     C     PARKEY        READE     BRPARF                                 52
     C     *IN52         DOWEQ     *OFF

     C                   IF        ((WPAID = 'ARTMP') OR
     C                             ((WPAID = 'ARTSL') AND (PAVRDN = 9)))
     C                   EVAL      KAT=*BLANKS
     C                   EVAL      KAT2=*BLANKS
     C                   EVAL      KAT= PAVRDA
     C                   EVAL      KAT2= PAVRDA
     C                   ADD       1             XANTBANE
     C                   EXSR      Setrow
     C                   CALLP     wrtsection('row')
     C                   END

     C     PARKEY        READE     BRPARF                                 52
     C                   ENDDO
     C                   ENDIF

     C                   IF        WPAID='ARTMP'
     C                   EVAL      WPAID='ARTSL'
     C                   GOTO      STLESKAT
     C                   END

     C                   IF        XANTBANE = 1
     C                   MOVE      'J'           ENBANE            1
     C                   ELSE
     C                   MOVE      'N'           ENBANE
     C                   END

     C                   ENDSR
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        BEGSR
     C                   CALLP     updHTMLvar('KAT':KAT)
     C                   CALLP     updHTMLvar('KAT2':KAT2)
     C                   ENDSR
