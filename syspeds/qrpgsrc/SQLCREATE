          D cmdlen          S             15  5
          D command         S            512
          D filename        S             10
          D library         S             10
          D newfile         S                   Like(filename)
          D newlibrary      S                   Like(library)
          D sqlstmt         S            512
          D tabletype       S              1
          C     *Entry        PList
          C                   Parm                    filename
          C                   Parm                    library
          C                   Parm                    newfile
          C                   Parm                    newlibrary
           * Retrieve the table type from the system table
          C                   Clear                   tabletype
          C/Exec SQL
          C+ SELECT
          C+ TABLE_TYPE into :tabletype
          C+ FROM systables
          C+ Where TABLE_SCHEMA = :library and
          C+       TABLE_NAME = :filename
          C/End-Exec
          C                   Select
           * Process tables & views created using SQL
          C                   When      tabletype = 'T'
          C                   Eval      sqlstmt = 'Create Table '
          C                                       + %Trim(newlibrary)
          C                                       + '/'
          C                                       + %Trim(newfile)
          C                                       + ' Like '
          C                                       + %Trim(library)
          C                                       + '/'
          C                                       + %Trim(filename)
          C/Exec SQL
          C+ Execute Immediate :sqlstmt
          C/End-Exec
           * Process physical and logical files created with DDS
          C                   When      tabletype = 'P'
          C                   Eval      command = 'CRTDUPOBJ '
          C                                     + 'OBJ(' + %Trim(FILENAME) + ') '
          C                                     + 'FROMLIB(' + %Trim(LIBRARY) + ') '
          C                                     + 'OBJTYPE(*FILE) '
          C                                     + 'TOLIB(' + %Trim(NEWLIBRARY) + ') '
          C                                     + 'NEWOBJ(' + %Trim(NEWFILE) + ') '
          C                   Eval      cmdlen = %Len(%Trim(command))
          C                   Call      'QCMDEXC'
          C                   Parm                    command
          C                   Parm                    cmdlen
          C                   EndSL
           * End the Program
          C                   Eval      *INLR = *On
