     FSVIHH     IF   E           K DISK
     FSVIH      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSVI012P   O    E             PRINTER OFLIND(*IN27) USROPN
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVIH_SYAV     UVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     UVIH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
     C     *LIKE         DEFINE    SVIHH_DTH     UVIHH_DTH
     C     *LIKE         DEFINE    SVIHH_TMH     UVIHH_TMH
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVIH_SYAV
     C                   KFLD                    UVIH_SYOP
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UVIH_SYAV
     C                   EVAL      UOPLNR=253
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIH_SYAV
     C                   PARM                    UVIH_SYOP
     C                   PARM                    UVIHH_DTH
     C                   PARM                    UVIHH_TMH
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C                   EVAL      UCMD='OVRPRTF FILE(SVI012P) OUTQ('
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      SVI012P
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs fakturor                                         SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVIH
     C                   WRITE     SVI012PA

     C     KEYA          SETLL     SVIHH
     C     KEYA          READE     SVIHH                                  51
     C     *IN51         DOWEQ     *OFF
     C     UVIHH_DTH     IFEQ      SVIHH_DTH
     C     UVIHH_TMH     ANDEQ     SVIHH_TMH

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SVI012PA
     C                   ENDIF
     C                   WRITE     SVI012PB

     C                   ENDIF
     C     KEYA          READE     SVIHH                                  51
     C                   ENDDO

     C                   WRITE     SVI012PZ
     C                   ENDSR
