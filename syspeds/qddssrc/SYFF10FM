     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('OPDF0001' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Endre oppdrag')
     A                                      CF10(10 'Frigjør ett ferdigmeldt op-
     A                                      pdrag')
     A                                  1  2'SYSPED/SYFF10'
     A                                      COLOR(BLU)
     A            MSG001        50A  O  1 19MSGID(DIV 1534 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            MSG002        50A  O  2 19MSGID(DIV 1535 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A            MSG003        16A  O  8  2MSGID(DIV 1536 SYMSGF)
     A            UKODE          1A  O  8 21COLOR(WHT)
     A            MSG004        35A  O  8 30MSGID(DIV 1537 SYMSGF)
     A            MSG005        16A  O 11  2MSGID(DIV 1538 SYMSGF)
     A            WSSG           3A  B 11 21DSPATR(UL)
     A  99                                  DSPATR(RI)
     A            MSG006        16A  O 13  2MSGID(DIV 1539 SYMSGF)
     A            WSAVD          4Y 0B 13 21DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A            MSG007        16A  O 15  2MSGID(DIV 1540 SYMSGF)
     A            WSOPD          7Y 0B 15 21DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A            MSG008        18A  O 22  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG009        18A  O 22 30MSGID(DIV 1541 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG010        25A  O 23  2MSGID(DIV 1542 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG011        25A  O 23 30MSGID(DIV 1543 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
