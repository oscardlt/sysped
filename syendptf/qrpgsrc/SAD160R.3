      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE EKSPRESSFORTOLL. MANIFEST ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEXML1  IF   E           K DISK    RENAME(SADEXMFR:SADEXMFR1)
     FSADEXML2  IF   E           K DISK    RENAME(SADEXMFR:SADEXMFR2)
     FSADEXML3  IF   E           K DISK    RENAME(SADEXMFR:SADEXMFR3)
     FSADEXML4  IF   E           K DISK    RENAME(SADEXMFR:SADEXMFR4)
     FSADEXML5  IF   E           K DISK    RENAME(SADEXMFR:SADEXMFR5)
     FSADEXML6  IF   E           K DISK    RENAME(SADEXMFR:SADEXMFR6)
     FSADEXMOPD UF   E           K DISK
     FSADEXHL1  IF   E           K DISK
     FTURER14   IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FFIRM      IF   E           K DISK
     FSAD160D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  XIDAG_N                1      8  0
     D  XIDAG_A                1      8
     D                 DS
     D  XTID_N                 1      6  0
     D  XTID_A                 1      6
     D                 DS
     D  WSETADD                1      2  0
     D  WSETADM                3      4  0
     D  WSETADA                5      6  0
     D  WSETAD                 1      6  0
     D                 DS
     D  XSETADC                1      2  0
     D  XSETADA                3      4  0
     D  XSETADM                5      6  0
     D  XSETADD                7      8  0
     D  XSETAD                 1      8  0
     D                 DS
     D  W2ETADD                1      2  0
     D  W2ETADM                3      4  0
     D  W2ETADA                5      6  0
     D  W2ETAD                 1      6  0
     D                 DS
     D  X2ETADC                1      2  0
     D  X2ETADA                3      4  0
     D  X2ETADM                5      6  0
     D  X2ETADD                7      8  0
     D  X2ETAD                 1      8  0
     D                 DS
     D  EMETADC                1      2  0
     D  EMETADA                3      4  0
     D  EMETADM                5      6  0
     D  EMETADD                7      8  0
     D  EMETAD                 1      8  0
     D                 DS
     D  X_EMETADD              1      2  0
     D  X_EMETADM              3      4  0
     D  X_EMETADA              5      6  0
     D  X_EMETAD               1      6  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  XSDTRC                 1      2  0
     D  XSDTRA                 3      4  0
     D  XSDTRM                 5      6  0
     D  XSDTRD                 7      8  0
     D  XSDTR                  1      8  0
     D                 DS
     D  W2DTRD                 1      2  0
     D  W2DTRM                 3      4  0
     D  W2DTRA                 5      6  0
     D  W2DTR                  1      6  0
     D                 DS
     D  X2DTRC                 1      2  0
     D  X2DTRA                 3      4  0
     D  X2DTRM                 5      6  0
     D  X2DTRD                 7      8  0
     D  X2DTR                  1      8  0
     D                 DS
     D  EMDTRC                 1      2  0
     D  EMDTRA                 3      4  0
     D  EMDTRM                 5      6  0
     D  EMDTRD                 7      8  0
     D  EMDTR                  1      8  0
     D                 DS
     D  X_EMDTRD               1      2  0
     D  X_EMDTRM               3      4  0
     D  X_EMDTRA               5      6  0
     D  X_EMDTR                1      6  0
     D                 DS
     D  xnr11                  1     11  0
     D  xnr11_avd              1      4  0
     D  xnr11_tdn              5     11  0
     D  xnr11_pro              4     11  0
     D                 DS
     D  u_pro_a                1      8
     D  u_pro_n                1      8  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
      *  Prototype for 'SYTT01CL'
     d SYTT01CL        pr                  extpgm('SYTT01CL')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d u_pro_                              like(u_pro)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'SAD003'
     d SAD003          pr                  extpgm('SAD003')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d win03_                              like(win03)
      *  Prototype for 'SADEXJSON'
     d*SADEXJSON       pr                  extpgm('SADEXJSON')
     d*u_pro_a_                            like(u_pro_a)
      *  Prototype for 'SAD160SND'
     d SAD160SND       pr                  extpgm('SAD160SND')
     d ujson_type_                         like(ujson_type)
     d ujson_pro_                          like(ujson_pro)
     d ujson_avd_                          like(ujson_avd)
     d ujson_opd_                          like(ujson_opd)
     d ujson_mrn_                          like(ujson_mrn)
     d ujson_lrn_                          like(ujson_lrn)
     d ujson_fil_                          like(ujson_fil)
      *  Prototype for 'SAD161R'
     d SAD161R         pr                  extpgm('SAD161R')
     d u_avd_                              like(u_avd)
     d u_pro_                              like(u_pro)
     d usg_                                like(usg)
     d xtype_                              like(xtype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD162R'
     d SAD162R         pr                  extpgm('SAD162R')
     d emavd_                              like(emavd)
     d empro_                              like(empro)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for SAD160R
     d sad160r         pr
      *  *ENTRY Interface for Main Procedure
     d sad160r         pi
      *
     d i               s              3s 0
     d xrecno          s              5s 0
     d xrecn2          s              5s 0
     d xtype           s              3a
     d x_emst          s                   like(emst)
     d x_fortsett      s              1a
     d x_sokok         s              1a
     d x_nysok         s              1a
     d x_sndok         s              1a
     d wdt             s              8a
     d wtm             s              6a
     d win03           s              1a
     d wsjn            s              1a
     d u_avd           s              4  0
     d u_pro           s              8  0
     d u_tdn           s              7  0
     d uflagg          s              1  0
     d utype           s              3a
     d ujson_type      s             30a
     d ujson_pro       s              8a
     d ujson_avd       s              4a
     d ujson_opd       s              7a
     d ujson_mrn       s             18a
     d ujson_lrn       s             36a
     d ujson_fil       s             45a
      *
      /free

         exsr init;

         dow *in03 = *off;
           exsr srvistur;
         enddo;

         if wsavd <> 0;
           uavd = wsavd;
         endif;

         *inlr = *on;
         return;

      // ********************************************
       begsr srvistur;

       *in93 = *on;
       write subctl;
       *in93 = *off;
       *in94 = *off;
       zrecno = 0;
       x_fortsett = 'J';
       x_nysok = 'N';

       dow x_fortsett = 'J' and x_nysok = 'N';    // START                     // 1<b

         exsr setkey;

         dow *in03 = *off and x_nysok = 'N';                                   // 2<b
           if *in94 = *off;          // START1                                 // 3<b
           // les post til max 19 poster pr gang.
             for i = 1 to 19;                                                  // 4<b
               exsr lesrec;
               if *in94 = *off;                                                // 5<b
                 X_EMETADD = EMETADD;
                 X_EMETADM = EMETADM;
                 X_EMETADA = EMETADA;
                 X_EMDTRD  = EMDTRD;
                 X_EMDTRM  = EMDTRM;
                 X_EMDTRA  = EMDTRA;
                 zrecno = zrecno + 1;
                 write subfil;
                 xrecn2 = zrecno;
               endif;                                                          // 5<e
             endfor;                                                           // 4<e
             xrecno = zrecno;
           endif;                                                              // 3<e

           *in25 = *off;
           dow *in03 = *off and *in25 = *off;     // start3                    // 3<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                    // 4<b
               if *in27 = *off;                                                // 5<b
                 *in27 = *on;
                 x_fortsett = 'N';
                 leave;
               endif;                                                          // 5<e
               exfmt nyvare;
             else;                                                             // 4
               exfmt subctl;
             endif;                                                            // 4<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;

             select;                                                           // 4<b
             when *in03;                                                       // 4
               *inlr = *on;
               return;
             when *in05;                                                       // 4
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               exsr setkey;
               leave;
             when *in06;                                                       // 4
               if wsavd = 0;
                 u_avd = uavd;
               else;
                 u_avd = wsavd;
               endif;
               u_pro = 0;
               exsr crttur;
             when *in08 or *in12;                                              // 4
               exsr srsok;      // handle om START0
               leave;           // til start
             when *in25;                                                       // 4
               zrecno = xrecno;
             other;                                                            // 4
               if w2nr <> 0 or w2pro <> 0 or wsetad <> 0                       // 5<b
                  or w2st <> *blanks or w2dtr <> 0;
                 if w2nr = 0;                                                  // 6<b
                   exsr nysok;
                   x_nysok = 'J';
                   leave;          // til START
                 else;                                                         // 6
                   if w2nr = 1;                                                // 7<b
                     *in06 = *on;
                   endif;                                                      // 7<e
                   if *in06;                                                   // 7<b
                     if w2avd <> 0;                                            // 8<b
                       wsavd = w2avd;
                     endif;                                                    // 8<e
                     wsnr = 0;
                     wsavd = 0;
                     if w2pro <> 0;                                            // 8<b
                       chain w2pro sadexml2;
                       if %found;                                              // 9<b
                       // Tur funnet og start endring
                       //if (emst = ' ' or emst = 'C' or emst = 'M')           // 10<b
                         if (emst = ' ' or emst = 'F')                         // 10<b
                            and (emetad > xidag_n
                            or emetad = xidag_n and emetat > xtid_n);
                           *in90 = *on;
                           exsr chgtur;
                         endif;                                                // 10<e
                         w2nr = 0;
                         w2avd = 0;
                         w2pro = 0;
                         leave;
                       else;                                                   // 9
                         chain w2pro turer14;
                         if %found;                                            // 10<b
                         // Tur funnet i turregister og start lage ny
                           u_avd = tuavd;
                           u_pro = tupro;
                           exsr crttur;
                           w2nr = 0;
                           w2avd = 0;
                           w2pro = 0;
                         endif;                                                // 10<e
                         leave;
                       endif;                                                  // 9 <e
                       w2pro = 0;
                     else;                                                     // 8
                       exsr crttur;
                       w2nr = 0;
                       w2avd = 0;
                       w2pro = 0;
                       leave;
                     endif;                                                    // 8<e
                   endif;                                                      // 7<e
                   if w2pro = 0;                                               // 7<b
                     *in98 = *on;
                     *in30 = *on;
                     msgnr = 'YBC2034';
                   else;                                                       // 7
                     exsr nytur;
                     w2pro = 0;
                     w2etad = 0;
                     w2st = *blanks;
                     w2dtr = 0;
                     leave;
                   endif;                                                      // 7<e

                 endif;                                                        // 6<e
               endif;                                                          // 5<e

               if zrecno <> 0;                                                 // 5<b
                 exsr hent;
               endif;                                                          // 5<e

             endsl;                                                            // 4<e

           enddo;                                                              // 3<e

         enddo;                                                                // 2<e
       enddo;                                                                  // 1<e

       endsr;

      // ********************************************
       begsr srsok;

       x_sokok = 'N';
       dow x_sokok = 'N';
         exfmt bilde01;
         *in98 = *off;
         *in99 = *off;
         *in30 = *off;
         select;
           when *in03 = *on;
             *inlr = *on;
             return;
           //when *in04 = *on;
           //  exsr sokkun;
           other;
           exsr testb1;
         endsl;
       enddo;

       *in93 = *on;
       *in94 = *off;
       write subctl;
       *in93 = *off;
       *in94 = *off;
       zrecno = 0;
       exsr setkey;

       endsr;

      // ********************************************
       begsr nysok;

       clear bilde01;
       syge15c (wdt: wtm);
       if w2avd = 0;
         wsavd = uavd;
       else;
         wsavd = w2avd;
       endif;
       select;
       when w2pro <> 0;
         wspro = w2pro;
       when w2etad <> 0;
         wsetad = w2etad;
       when w2st <> *blanks;
         wsst = w2st;
       when w2dtr <> 0;
         wsdtr = w2dtr;
       endsl;

       w2nr = 0;
       w2avd = 0;
       w2pro = 0;
       w2etad = 0;
       w2st = *blanks;
       w2dtr = 0;

       *in98 = *off;
       *in99 = *off;
       exsr testb1;

       endsr;

      // ********************************************
       begsr nytur;

       if w2avd = 0;
         w2avd = uavd;
       endif;
       chain (wsavd: w2pro) sadexml1;
       if not %found;
         msgnr = 'YBC2035';
         *in98 = *on;
       else;
         *in90 = *on;
         select;
         when w2nr = 2;
          // kan ikke gå inn på normale oppdrag fra dummy
           syge15c (wdt: wtm);
           xtid_a = wtm;
           //if emst <> ' ' and emst <> 'C' and emst <> 'M'
           if emst <> ' ' and emst <> 'F' or emst2 = 'D' or emst2 = 'I'
              or (emetad <> 0 and emetad <> 20000000
              and (emetad < xidag_n or emetad = xidag_n and emetat < xtid_n));
             *in30 = *on;
             msgnr = 'YBC2036';
           else;
             exsr chgtur;
           endif;
         when w2nr = 3;
           exsr cpytur;
         when w2nr = 5;
           exsr dsptur;
         when w2nr = 9;
           exsr dsplog;
         endsl;
       endif;

       if *in98;
         *in30 = *on;
       else;
         w2avd = 0;
         w2pro = 0;
       endif;
       *in90 = *off;
       w2nr = 0;

       endsr;

      // ********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in99 = *off;
       readc subfil;
       dow not %eof;
         select;
         when wsnr = 2;
           *in99 = *on;
           syge15c (wdt: wtm);
           xtid_a = wtm;
           //if emst <> ' ' and emst <> 'C' and emst <> 'M' and emst <> 'F'
           if emst <> ' ' and emst <> 'F' or emst2 = 'D' or emst2 = 'I'
              or (emetad <> 0 and emetad <> 20000000
              and (emetad < xidag_n or emetad = xidag_n and emetat < xtid_n));
             *in30 = *on;
             msgnr = 'YBC2103';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           exsr chgtur;
         when wsnr = 3;
           *in99 = *on;
           exsr cpytur;
         when wsnr = 4;
           *in99 = *on;
           exsr dlttur;
         when wsnr = 5;
           *in99 = *on;
           exsr dsptur;
         when wsnr = 9;
           *in99 = *on;
           exsr dsplog;
         when wsnr = 25;
           *in99 = *on;
           exsr dspTT;
         when wsnr = 90;
           // if emst <> ' ' and emst <> 'C' and emst <> 'M';
           if emst <> ' ' and emst <> 'F';
             *in30 = *on;
             msgnr = 'YBC2103';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           *in99 = *on;
           exsr sndtur;
         //when wsnr = 99;
         //  select;
         //  when emst = 'P' and emmid <> *blanks;
         //    *in33 = *off;
         //  other;
         //    *in33 = *on;
         //  endsl;
         //  if *in33;
         //    *in30 = *on;
         //    msgnr = 'YBC2102';
         //    wsnr = 0;
         //    update subfil;
         //    leave;
         //  endif;
         //  *in99 = *on;
         //  exsr avsltur;
         endsl;
         readc subfil;
       enddo;

       if *in99 = *off and *in30 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      // ********************************************
       begsr setcur;

       posnbr = 35;
       select;
       when wspro <> 0;
         linnbr = 8;
       when wsmid <> *blanks;
         linnbr = 9;
       when wsetad <> 0;
         linnbr = 10;
       when wsdtr <> 0;
         linnbr = 11;
       when wsst <> *blanks;
         linnbr = 12;
       other;
         linnbr = 8;
       endsl;

       endsr;

      // ********************************************
       begsr dsptur;

       xtype = 'DSP';            // SPØRRING
       u_avd = emavd;
       u_pro = empro;
       sad161r (u_avd: u_pro: usg: xtype: uflagg);
       select;
       when uflagg = 3;
         *inlr = *on;
         return;
       when *in90 = *off;
         wsnr = 0;
         update subfil;
       endsl;

       endsr;

      // ********************************************
       begsr chgtur;

       xtype = 'CHG';                // ENDRING
       u_avd = emavd;
       u_pro = empro;
       sad161r (u_avd: u_pro: usg: xtype: uflagg);

       x_emst = emst;
       //chain (u_avd: u_pro) sadexmopd;
       //emst = x_emst;
       //update sadexmfr;
       chain(n) (u_avd: u_pro) sadexmopd;
       X_EMETADD = EMETADD;
       X_EMETADM = EMETADM;
       X_EMETADA = EMETADA;
       X_EMDTRD  = EMDTRD;
       X_EMDTRM  = EMDTRM;
       X_EMDTRA  = EMDTRA;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr cpytur;

       xtype = 'CPY';                // KOPIERING
       u_avd = emavd;
       u_pro = empro;
       sad161r (u_avd: u_pro: usg: xtype: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       if u_pro <> 0 and u_pro <> empro;
         chain(n) (u_avd: u_pro) sadexmopd;
         X_EMETADD = EMETADD;
         X_EMETADM = EMETADM;
         X_EMETADA = EMETADA;
         X_EMDTRD  = EMDTRD;
         X_EMDTRM  = EMDTRM;
         X_EMDTRA  = EMDTRA;
         xrecn2 = xrecn2 + 1;
         zrecno = xrecn2;
         write subfil;
       endif;

       endsr;

      // ********************************************
       begsr dlttur;

       if emmid = *blanks;
         wsjn = 'J';
         *in33 = *off;
         dow *in33 = *off;
           exfmt comptur;
           if wsjn <> ' ';
             leave;
           endif;
         enddo;
         if wsjn = 'J';
           chain (emavd: empro) sadexmopd;
           emst2 = 'D';
           update sadexmfr;
         endif;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr crttur;

       xtype = 'CRT';                // LAGE NY TUR
       sad161r (u_avd: u_pro: usg: xtype: uflagg);

       if u_pro > 0;
         chain(n) (u_avd: u_pro) sadexmopd;
         if %found;
           if u_avd = 0;
             wsavd = u_avd;
           else;
             uavd = u_avd;
           endif;
           X_EMETADD = EMETADD;
           X_EMETADM = EMETADM;
           X_EMETADA = EMETADA;
           X_EMDTRD  = EMDTRD;
           X_EMDTRM  = EMDTRM;
           X_EMDTRA  = EMDTRA;
           xrecn2 = xrecn2 + 1;
           zrecno = xrecn2;
           write subfil;
         endif;
       endif;

       endsr;

      // ********************************************
       begsr dsplog;

       win03 = '8';                // Vis edi logg
       xnr11 = 0;
       xnr11_pro = empro;
       u_avd = xnr11_avd;
       u_tdn = xnr11_tdn;
       sad003 (u_avd: u_tdn: win03);
       if win03 = '1';
         *inlr = *on;
         return;
       endif;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr dspTT;

       u_pro = empro;
       u_avd = emavd;
       u_tdn = 0;
       uflagg = 1;
       sytt01cl (u_avd: u_tdn: u_pro: uflagg);
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr sndtur;

       // Oscar lager og sender Json string.
       //if *in33 and *in33 = *off;

       exsr testtur;

       if x_sndok = 'J';
         if emmid = *blanks;
           wsst2 = 'S';
           *in51 = *on;
         else;
           wsst2 = 'R';
           *in51 = *off;
         endif;
         dow *in03 = *off and *in12 = *off;
           exfmt sendtur;
           if wsst2 <> ' ';
             *in03 = *on;
           endif;
         enddo;
         *in03 = *off;
         if *in12 = *off;
           if wsst2 = 'D' and emmid = *blanks;
           // Merk sletting på manifest som aldri sendt til Tollvesenet.
             chain (emavd: empro) sadexmopd;
             if %found;
               //emst  = 'D';
               //emst2 = 'D';
               update sadexmfr;
             endif;
           else;
             // Send manifest til Tollvesenet.
             u_pro_n = empro;
             chain (emavd: empro) sadexmopd;
JOV          //emst  = 'C';
             //emst2 = wsst2;
             update sadexmfr;
      /end-free
JOV  C                   MOVE      emavd         ujson_avd
     C                   MOVE      empro         ujson_pro
      /free
             ujson_mrn = emmid;
             ujson_lrn = emuuid;
             select;
             when wsst2 = 'S';
              ujson_type = 'postMasterConsignment.do';
             when wsst2 = 'R';
              ujson_type = 'putMasterConsignment.do';
             when wsst2 = 'D';
              ujson_type = 'deleteMasterConsignment.do';
             when wsst2 = 'K';
              ujson_type = 'getStatusMasterConsignment.do';
             // when wsst2 = 'I';   // er masteren innpassert ???
             //  ujson_type = 'getMasterConsignment.do';
             endsl;
             sad160snd (ujson_type: ujson_pro: ujson_avd: ujson_opd:
                        ujson_mrn: ujson_lrn: ujson_fil);
             chain(n) (emavd: empro) sadexmopd;
             exsr sredis;
           endif;
           *in03 = *off;
         else;
       //  exfmt errmsg;
         endif;
       else;
         exfmt errsnd;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr testtur;

       x_sndok = 'J';

       endsr;

      // ********************************************
       begsr avsltur;

       wsjn = 'J';
       *in33 = *off;
       dow *in33 = *off;
         exfmt comptur;
         if wsjn <> ' ';
           leave;
         endif;
       enddo;
       if wsjn = 'J';
         chain (emavd: empro) sadexmopd;
         emst2 = 'D';
         update sadexmfr;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr sredis;

        chain ('S': 'EXPREM') ecmsg1;
        chain 1 edic;
        csn = csn + 1;
        update edicr;
        s0004 = ECIIDI;
        s0010 = ECIIDC;
        u_pro_n = empro;
        s0020 = 'EF' + %TRIM(u_pro_a);
        s0026 = 'SADEXJSON';
        ssn   = CSN;
        ssr   = 'S';
        sst   = 'B';
        select;
        when wsst2 = 'S';
          s0036 = 1;
        when wsst2 = 'D';
          s0036 = 2;
        when wsst2 = 'R';
          s0036 = 4;
        endsl;
        sdt   = %dec(wdt:8:0);
        stm   = %dec(wtm:6:0);
        slib  = '*LIB';
        sfile = 'EDISE1K';
        smbr  = *BLANKS;
        sifs  = *blanks;
        write edisr;

       endsr;

      // ********************************************
       begsr testb1;

       koanvn = *blanks;
       if wsavd > 0;
         chain ('A': wsavd) kodta;
         if not %found;
           linnbr = 4;
           posnbr = 26;
           msgnr = 'SPA0001';
           *in30 = *on;
           *in99 = *on;
         else;
           uavd = wsavd;
           chain (fifirm: koaknr) cundl01;
         endif;
       endif;

       *in41 = *off;
       *in42 = *off;
       *in43 = *off;
       *in44 = *off;
       *in45 = *off;
       x_sokok = 'J';
       select;
       when wspro > 0;
         *in41 = *on;
       when wsmid <> *blanks;
         *in42 = *on;
       when wsetad > 0;
         *in43 = *on;
       when wsdtr > 0;
         *in44 = *on;
       when wsst <> *blanks;
         *in45 = *on;
       other;
         x_sokok = 'N';
         msgnr = 'DIV0064';
         *in30 = *on;
         *in98 = *on;
         linnbr = 8;
         posnbr = 35;
       endsl;

       endsr;

      // ********************************************
       begsr setkey;

       select;
       when *in41;
       when *in42;
       when *in43;
         if wsetad = 999999;
           xsetad = 99999999;
         else;
           xsetadd = wsetadd;
           xsetadm = wsetadm;
           xsetada = wsetada;
           xsetadc = 20;
         endif;
       when *in44;
         if wsdtr = 999999;
           xsdtr = 99999999;
         else;
           xsdtrd = wsdtrd;
           xsdtrm = wsdtrm;
           xsdtra = wsdtra;
           xsdtrc = 20;
         endif;
       when *in45;
       endsl;

       exsr mintur;

       endsr;

      // ********************************************
       begsr mintur;

       select;
       when *in41;
         if wsavd = 0;
           setll wspro sadexml2;
         else;
           setll (wsavd: wspro) sadexml1;
         endif;
       when *in42;
         setll wsmid sadexml3;
       when *in43;
         setll xsetad sadexml4;
       when *in44;
         setll xsdtr sadexml5;
       when *in45;
         setll (wsavd: wsst) sadexml6;
       endsl;

       endsr;

      // ********************************************
       begsr lesrec;

       select;
       when *in41;
         if wsavd = 0;
           read sadexml2;
         else;
           reade wsavd sadexml1;
         endif;
       when *in42;
         read sadexml3;
       when *in43;
         if xsetad = 99999999;
           readp sadexml4;
         else;
           read sadexml4;
         endif;
       when *in44;
         if xsdtr = 99999999;
           readp sadexml5;
         else;
           read sadexml5;
         endif;
       when *in45;
         if wsavd = 0;
           //read wsst sadexml6;
         else;
           reade (wsavd: wsst) sadexml6;
         endif;
       endsl;

       if %eof;
         *in94 = *on;
         i = 50;
       endif;

       endsr;

      // ********************************************
       begsr init;

       wspro = 0;
       wsavd = uavd;
       wsetad = 0;
       wsdtr = 999999;
       w2etad = 0;
       w2dtr = 0;
       x_emetad = 0;
       *in41 = *off;
       *in42 = *off;
       *in43 = *off;
       *in44 = *on;
       *in45 = *off;

       setll *loval firm;
       read firm;
       chain ('A': wsavd) kodta;
       chain (fifirm: koaknr) cundl01;

       syge15c (wdt: wtm);
       xidag_a = wdt;

       endsr;

       /end-free
