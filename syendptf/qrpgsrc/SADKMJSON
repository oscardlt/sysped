     H*debug(*yes)
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
      *
     FSADKML02  IF   E           K DISK                                         Turheader
     FSADKMCL1  IF   E           K DISK                                         House/SAD-X-ref
     FSADKMVL1  IF   E           K DISK                                         House/SAD-X-ref
     FSADH      IF   E           K DISK
     FSADV      IF   E           K DISK
     FSADF      IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E           K DISK
     FEDIS2     IF   E           K DISK    RENAME(EDISR:EDISR2)
     F                                     PREFIX(S2_)
     D                 DS
     D  TS                     1     26
     D  TS_N                   1     26  0
     D                 DS
     D  KMPRO                  1      8  0
     D  KMPRO_A                1      8
      * TRANSP.MÅTE bør nok gjøre om til 1 alfa
     D                 DS
     D  KMTM                   1      2  0
     D  KMTM1                  2      2  0
     D                 DS
     D  SVVNT                  1      8  0
     D  SVVNT_A                1      6
     D  SVVNT_B                7      8
     D                 DS
     D  SVCRE1                 1      3
     D  SVCRE2                 4      6
     D  SVCRE3                 7      9
     D  SVCRE4                10     12
     D  SVCRE5                13     15
     D  SVCRE6                16     18
     D  SVCRE7                19     21
     D  SVCRE8                22     24
     D  SVCRE9                25     27
     D  SVCRE10               28     30
     D  CRE                    1     30    DIM(10)
     D                 DS
     D  SVTOP1                 1     17
     D  SVTOP2                18     34
     D  SVTOP3                35     51
     D  SVTOP4                52     68
     D  SVTOP5                69     85
     D  SVTOP6                86    102
     D  SVTOP7               103    119
     D  SVTOP8               120    136
     D  SVTOP9               137    153
     D  SVTOP10              154    170
     D  TOP                    1    187    DIM(11)
     D                 DS
     D  MCAVD                  1      4  0
     D  XSTREKK                5      5
     D  MCTDN                  6     12  0
     D  SYSPEDREF              1     12
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
      *
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Data            s             80a   Varying
     D codepage        s              5a   Varying
     D CmdLine         S            512
     D CmdLen          S             15  5
     D LF              c                   x'25'
      *
     D OrgNr           s             17
     D TypOper         s              1
     D Name            s             70
     D Adr1            s             70
     D Adr2            s             70
     D PoNr            s             17
     D PoSted          s             35
     D LandKo          s              2
     D Number          s             35
     D PoBoAd          s             70
     D COM             s             50
     D COMT            s              3
     D VOECno          s             17
     D Role            s              6
     D RefType         s              6
     D TypeOfGoods     s              2
     D HouseNo         s              5i 0
     D ItemNo          s              5i 0
      * Status = Pre-alert  / On-Arrival / Cancelled
     D Status          s             20
     D JSONstring      s          32000
     D WFILNAM         S            200
     D WFILNAM1        S            200
     D WFILNAM2        S            200
     D WFILNAM3        S            200
     D WFILNAM4        S            200
     D WMAPPE          S            100
     D WMAPPE2         S            100
     D XLIB            S             11
     D WFilback        S            512
     D WFIL            S            512
     D timeData        S               Z                                        Z=Timestamfield
     D Fldr            S             60
     d wdt             s              8a
     d wtm             s              6a
     d xn              s              3  0
      *
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )
     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value
     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
      *   EDIDTA data area
     D EDIDTA          DS           256    DTAARA('EDIDTA')
     D  UELIBF               171    180
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'ARC040C1'
     d ARC040C1        pr                  extpgm('ARC040C1')
     d Fldr_                               like(Fldr)
      *  Prototype for 'SADKMJSONC'
     d SADKMJSONC      pr                  extpgm('SADKMJSONC')
     d wfilnam_                            like(wfilnam)
     d wfilnam1_                           like(wfilnam1)
     d wfilnam2_                           like(wfilnam2)
     d wfilnam3_                           like(wfilnam3)
     d wfilnam4_                           like(wfilnam4)
     d wmappe_                             like(wmappe)
     d wmappe2_                            like(wmappe2)
      *  Prototype for SADKMJSON
     d SADKMJSON       pr
     d upro_                               like(upro)
      *  *ENTRY Interface for Main Procedure
     d SADKMJSON       pi
     d upro                           8
      *
      /free

         exsr init;
         // tEMPORÆRE HARDKODEDE VERDIER

         exsr LagFil;

         exsr OpdEDIS;

         *inlr = *on;
         return;

        //*********************
        Begsr LagFil;
        //*********************
        //time (Timedata);
        //ts = Timedata;
      /end-free
     C                   time                    Timedata
     C                   movel     timedata      TS
      /free

        //Lag fil og set codepage (pc)

        WFil = KMUUID;                       // 1. GANG SENDING
        S0020 = 'KM' + KMPRO_A;
        setll s0020 edis2;
        reade s0020 edis2;
        dow not %eof;
          if s2_ssr = 'S' and s2_s0026 = 'SADKMJSON' and s2_sst = 'C';
            WFil = 'U_' + KMUUID;            // UPDAET
            leave;
          endif;
          reade s0020 edis2;
        enddo;

        XLIB = '/' + UELIBF;
        // ByggFlr kan gjerne flyttes til annet prog. men det er gjort på millisekunder..
        exsr ByggFlr;
        // Fil i backup med stempel
        WFilNam  = %TRIM(XLIB) + '/KURERMANIFEST/json_Ut/Backup/'
                  + %TRIM(TS) + %TRIM(Wfil) + '.json';
        // Fil i temp med stempel
        WFilNam1 = %TRIM(XLIB) + '/KURERMANIFEST/' + 'json_Ut/Temp/'
                  + %TRIM(TS) + %TRIM(Wfil) + '.json';
        // Fil i temp uten stempel
        WFilNam2 = %TRIM(XLIB) + '/KURERMANIFEST/' + 'json_Ut/Temp/'
                  + %TRIM(Wfil) + '.json';
        // Filnavn uten stempel
        WFilNam3 = %TRIM(Wfil) + '.json';
        // Sjekk tidligere versjon
        WFilNam4 = %TRIM(XLIB) + '/KURERMANIFEST/' + 'json_Ut/'
                  + %TRIM(Wfil) + '.json';

        WMAPPE   = %TRIM(XLIB) + '/KURERMANIFEST/' + 'json_Ut/Temp';
        WMAPPE2  = %TRIM(XLIB) + '/KURERMANIFEST/' + 'json_Ut';

        FILE_o = open( %TrimR( WFilNam ) : 'w, codepage=865' );
        rc = close( FILE_o );
        FILE_o = open( %TrimR( WFilNam ) : 'w' );

        exsr LagJson;

        rc = close( FILE_o );

        sadkmjsonc (wfilnam: wfilnam1: wfilnam2: wfilnam3: wfilnam4:
                    wmappe: wmappe2);
        // Kopiert til endelig mappe ..

        endsr;

        //*********************
        Begsr LagJson;
        //*********************

        // Start JSON
        jsonstring ='{';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Message / UUID
        jsonstring ='¬id¬:¬'+%trim(KMUUID)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Declarant (Mandatory)
        exsr Declarant;

        // Consignment Master
        exsr ConsMaster;

        jsonstring = '}';                                    // slutt Hele meldingen
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr ConsMaster;
        //*********************

        // Consignment Master start
        jsonstring = '¬consignmentMaster¬:{';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Carrier er Mandatory (hvem du sender MED , men ikke detaljer om reg-nr mm )
        exsr carrier;

          // Transport Means- kun hvis bil/registreringsnummer ulik blank
        if KMTRID <> *blanks;
          exsr transpMeans;
        endif;

        // Entry office sendes kun hvis oppgitt
        if KMTD1 <> *blanks;
          exsr entryOffice;
        endif;

        // Leseloop for oppdrag/SADer...= consignmentHouse
        //
        exsr ReadHouse;
        //

        jsonstring = '}';                                    // slutt Consignment Master
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr Declarant;
        //*********************
        OrgNr  = KMRGD;
        TypOper= ' ';      // type of preson (kun consignor/vonsignee) er IKKE mandatory
        Name   = KMNAD;
        Adr1   = KMADD1;
        Adr2   = KMADD2;
        PoNr   = KMPND;
        PoSted = KMADD3;
        LandKo = KMLKD;
        //Number = XXXXX;  // husnummer - brukes IKKE
        //PoBoAd = YYYYY;  // postboks adresselinje - Postboks 10, Sentrum - beukes IKKE
        COM    = KMCMD;
        COMT   = KMCMDT;

        jsonstring =
           '¬declarant¬:{';

        exsr PartySr;

        jsonstring =%trim(jsonstring)+'},';

        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr Carrier;
        //*********************
        OrgNr  = KMRGC;
        TypOper= ' ';      // type of preson (kun consignor/vonsignee) er IKKE mandatory
        Name   = KMNAC;
        Adr1   = KMADC1;
        Adr2   = KMADC2;
        PoNr   = KMPNC;
        PoSted = KMADC3;
        LandKo = KMLKC;
        //Number = XXXXX;  // husnummer - brukes IKKE
        //PoBoAd = YYYYY;  // postboks adresselinje - Postboks 10, Sentrum - beukes IKKE
        COM    = KMCMC;
        COMT   = KMCMCT;

        jsonstring =
           '¬carrier¬:{';

        exsr PartySr;

        jsonstring =%trim(jsonstring)+'},';

        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr Consignee;
        //*********************
        OrgNr  = MCRGK;
        TypOper= ' ';      // type of preson (kun consignor/vonsignee) er IKKE mandatory
        Name   = MCNAK;
        Adr1   = MCADK1;
        Adr2   = MCADK2;
        PoNr   = MCPNK;
        PoSted = MCADK3;
        LandKo = MCLKK;
        //Number = XXXXX;  // husnummer - brukes IKKE
        //PoBoAd = YYYYY;  // postboks adresselinje - Postboks 10, Sentrum - beukes IKKE
        COM    = MCCMK;
        COMT   = MCCMKT;

        jsonstring =
           '¬consignee¬:{';

        exsr PartySr;

        jsonstring =%trim(jsonstring)+'},';

        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr Consignor;
        //*********************
        OrgNr  = MCRGS;
        TypOper= ' ';      // type of preson (kun consignor/vonsignee) er IKKE mandatory
        Name   = MCNAS;
        Adr1   = MCADS1;
        Adr2   = MCADS2;
        PoNr   = MCPNS;
        PoSted = MCADS3;
        LandKo = MCLKS;
        //Number = XXXXX;  // husnummer - brukes IKKE
        //PoBoAd = YYYYY;  // postboks adresselinje - Postboks 10, Sentrum - beukes IKKE
        COM    = MCCMS;
        COMT   = MCCMST;

        jsonstring =
           '¬consignor¬:{';

        exsr PartySr;

        jsonstring =%trim(jsonstring)+'},';

        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;


        //*********************
        Begsr PartySr;
        //*********************
        // deler som er blank - heller ikke ledeteksetr skrives (ikke tomme tager)
        jsonstring = %trim(jsonstring)
            +'¬name¬:¬'+%trim(Name)+'¬,';
        if OrgNr <> *blanks;
          jsonstring = %trim(jsonstring)
            +'¬identificationNumber¬:¬'+%trim(OrgNr)+'¬,';
        endif;
        // country/city/postcode er mandatory..
        jsonstring = %trim(jsonstring)
            +'¬address¬:{'
              +'¬country¬:¬'+%trim(LandKo)+'¬,'
              +'¬city¬:¬'+%trim(PoSted)+'¬,'
              +'¬postcode¬:¬'+%trim(PoNr)+'¬';
           // Address 1  kun hvis ulik blank
        if Adr1  <> *blanks;
          jsonstring = %trim(jsonstring) +', '
              +'¬streetNameLine1¬:¬'+%trim(Adr1)+'¬';
        endif;
           // Address 2  kun hvis ulik blank
        if Adr2  <> *blanks;
          jsonstring = %trim(jsonstring) +', '
              +'¬streetNameLine2¬:¬'+%trim(Adr2)+'¬';
        endif;
           // '¬number¬ og ¬POBox¬ benyttes IKKE
        // xxxx
        jsonstring = %trim(jsonstring) +'} ';                // slutt adresse
           // Communication kun hvis ulik blank
        if Com <> *blanks;
          jsonstring = %trim(jsonstring) +', '
            +'¬communication¬:['
            +'{¬identifier¬:¬'+%trim(Com)+'¬,'
                  +'¬type¬:¬'+%trim(ComT)+'¬}]';
        endif;

        endsr;

        //*********************
        Begsr TranspMeans;
        //*********************

        jsonstring =
           '¬activeBorderTransportMeans¬:{'
            +'¬modeOfTransport¬:¬'+%trim(%editc(KMTM1:'X'))+'¬,'
            +'¬transportRegistrationNumber¬:¬'+%trim(KMTRID)+'¬,';
          // ATD kun hvis ikke blank
        if KMATDD <> *zeros;
          jsonstring =%trim(jsonstring)
              +'¬actualDateAndTimeOfDeparture¬:¬'
                +%trim(%editc(KMATDD:'X'))+'T'+%trim(%editc(KMATDT:'X'))+'¬,';
        endif;
          // conveyance ref.no er mandatory (dersom ..Tranp.means-gruppe oppgitt)
        jsonstring =%trim(jsonstring)
              +'¬conveyanceReferenceNumber¬:¬'+%trim(KMTRRF)+'¬'
            +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr entryOffice;
        //*********************

        jsonstring =
           '¬entryOffice¬:{'
            +'¬customsOfficeOfFirstEntryCode¬:¬'+%trim(KMTD1)+'¬,'
            +'¬actualCustomsOfficeOfFirstEntryCode¬:¬'+%trim(KMTDA)+'¬'
          +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr readhouse;
        //*********************

        jsonstring = '¬consignmentHouse¬:[';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        setll mcpro SADKMCL1;
        reade mcpro SADKMCL1;
        dow not %eof;
          if MCCN = 0;
            chain (mcavd: mctdn) SADH;
            if %found;
              exsr OneHouseHead;
            endif;
          else;
            SILKA  = MCLKA;
            SIVKB  = 0;
            SIBEL3 = 0;
            SIVAL3 = MCVAL;
            setll (kmpro: mccn) SADKMVL1;
            reade (kmpro: mccn) SADKMVL1;
            DOW NOT %EOF;
              SIVKB  = SIVKB + MVVKTB;
              SIBEL3 = SIBEL3 + MVBL;
              reade (kmpro: mccn) SADKMVL1;
            enddo;
            if SIVKB > 0;
              exsr OneHouseHead;
            endif;
          endif;
          reade mcpro SADKMCL1;
        enddo;

        jsonstring = ']';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;
      *

        //*********************
        Begsr OneHouseHead;
        //*********************

        HouseNo = HouseNo+1;
        // Start House-tager
        if HouseNo = 1;
          jsonstring ='{';
        else;
          jsonstring =',{';
        endif;
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        select;
        when mcst = 'S';   // kansellert
        Status='Cancelled';
        when kmtda = ' ';  // gods har ikke passert grense
        Status='Pre-alert';
        other;             // gods har passert grense
        Status='On-arrival';
        endsl;

        jsonstring =
             '¬countryOfOrigin¬:¬'+%trim(SILKA)+'¬,'
            +'¬totalGrossMass¬:'+%trim(%editc(SIVKB:'3'))+','
            +'¬status¬:¬'+%trim(STATUS)+'¬,'
            +'¬totalAmountInvoiced¬:{'
              +'¬value¬:'+%trim(%editw(SIBEL3:'           .  '))+','         //13,2
              +'¬currency¬:¬'+%trim(SIVAL3)+'¬},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Consignee / mottaker
        exsr consignee;

        // Consignor / sender
        exsr consignor;

        // Transport document
        jsonstring =
             '¬transportDocument¬:{'
            +'¬referenceNumber¬:¬'+%trim(MCTRF)+'¬,'
            +'¬type¬:¬'+%trim(MCTRFT)+'¬},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        exsr      GoodsItems;

        // Slutt House-tager
        jsonstring ='}';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr GoodsItems;
        //*********************

        ItemNo = *zeros;
        // item list start
        jsonstring ='¬goodsItem¬:[';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        //les  SADV..
        if MCCN = 0;
          setll (mcavd: mctdn) SADV;
          reade (mcavd: mctdn) SADV;
          dow not %eof;
            VOECno = *blanks;
            for xn=1 to 10;
              if cre(xn) = 'VEC';
                VOECno = top(xn);
                leave;
              endif;
            endfor;
            exsr OneItem;
            reade (mcavd: mctdn) SADV;
          enddo;
        else;
          setll (kmpro: mccn) SADKMVL1;
          reade (kmpro: mccn) SADKMVL1;
          dow not %eof;
            exsr OneItem2;
            reade (kmpro: mccn) SADKMVL1;
          enddo;
        endif;

        // item list slutt
        jsonstring = ']';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr OneItem;
        //*********************

        ItemNo = ItemNo+1;

        // Start Item line tager
        if itemNo = 1;
          jsonstring ='{';
        else;
          jsonstring =',{';
        endif;
        rc = fputs(%trimr(jsonstring) + LF: File_o);

         //  Type of goods
               //  11 - Sale of goods
               //  21 - Returned goods
               //  31 - Gift
               //  32 - Commercial sample
               //  91 - Documents
        //rent teknisk er både type of goods og Itemamount valgfrie men i praksis???
        // går dette på tidlig-varsling ??? FØR en vet mer ... er ureslistisk/uønsket i så fall
        // Hvis det skal tas høyde for det så må JSON her deles opp og bettinges med if..
        jsonstring =
             '¬goodsItemNumber¬:'+%trim(%editc(ItemNo:'3'))+','
            +'¬typeOfGoods¬:¬'+%trim(mctype)+'¬,'
            +'¬itemAmountInvoiced¬:{'
              +'¬value¬:'+%trim(%editw(SVBELT:'           .  '))+','         //13,2
              +'¬currency¬:¬'+%trim(SIVAL3)+'¬},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        If VOECno <> *blanks;
          Role='FR5';
          jsonstring =
               '¬additionalFiscalReferences¬:{'
              +'¬VATIdentificationNumber¬:¬'+%trim(VOECno)+'¬,'
              +'¬role¬:¬'+%trim(Role)+'¬'
              +'},';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
        endif;

        jsonstring =
             '¬commodity¬:{'
            +'¬descriptionOfGoods¬:¬'+%trim(SVVT01)+'¬,'
            +'¬commodityCode¬:{'
              +'¬harmonizedSystemSubHeadingCode¬:¬'+%trim(SVVNT_A)+'¬,'
              +'¬combinedNomenclatureCode¬:¬'+%trim(SVVNT_B)+'¬}'
              +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
             '¬weight¬:{'
              +'¬grossMass¬:'+%trim(%editw(SVVKTB:'         .   '))+','         //12,3
              +'¬netMass¬:'+%trim(%editw(SVVKTN:'         .   '))             //12,3
              +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // tmp - Numb o packages = antall pakker i FORSENDELSEN (fra sadh ?? SVNTM -> SINTK.)
        jsonstring =
             '¬packages¬:{'
              +'¬numberOfPackages¬:'+%trim(%editc(SINTK:'3'))
              +'}';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Slutt item line tager
        jsonstring ='}';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr OneItem2;
        //*********************

        svvnt = mvvnt;

        ItemNo = ItemNo+1;

        // Start Item line tager
        if itemNo = 1;
          jsonstring ='{';
        else;
          jsonstring =',{';
        endif;
        rc = fputs(%trimr(jsonstring) + LF: File_o);

         //  Type of goods
               //  11 - Sale of goods
               //  21 - Returned goods
               //  31 - Gift
               //  32 - Commercial sample
               //  91 - Documents
        //rent teknisk er både type of goods og Itemamount valgfrie men i praksis???
        // går dette på tidlig-varsling ??? FØR en vet mer ... er ureslistisk/uønsket i så fall
        // Hvis det skal tas høyde for det så må JSON her deles opp og bettinges med if..
        jsonstring =
             '¬goodsItemNumber¬:'+%trim(%editc(ItemNo:'3'))+','
            +'¬typeOfGoods¬:¬'+%trim(mctype)+'¬,'
            +'¬itemAmountInvoiced¬:{'
              +'¬value¬:'+%trim(%editw(MVBL:'           .  '))+','         //13,2
              +'¬currency¬:¬'+%trim(MCVAL)+'¬},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        //If VOECno <> *blanks;
          Role='FR5';
          jsonstring =
               '¬additionalFiscalReferences¬:{'
              +'¬VATIdentificationNumber¬:¬'+%trim(MVVOEC)+'¬,'
              +'¬role¬:¬'+%trim(Role)+'¬'
              +'},';
          jsonfix ( jsonstring );
          rc = fputs(%trimr(jsonstring) + LF: File_o);
        // endif;

        jsonstring =
             '¬commodity¬:{'
            +'¬descriptionOfGoods¬:¬'+%trim(MVVS)+'¬,'
            +'¬commodityCode¬:{'
              +'¬harmonizedSystemSubHeadingCode¬:¬'+%trim(SVVNT_A)+'¬,'
              +'¬combinedNomenclatureCode¬:¬'+%trim(SVVNT_B)+'¬}'
              +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
             '¬weight¬:{'
              +'¬grossMass¬:'+%trim(%editw(MVVKTB:'      .   '))+','         //9,3
              +'¬netMass¬:'+%trim(%editw(MVVKTN:'      .   '))             //9,3
              +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // tmp - Numb o packages = antall pakker i FORSENDELSEN (fra sadh ?? SVNTM -> SINTK.)
        jsonstring =
             '¬packages¬:{'
              +'¬numberOfPackages¬:'+%trim(%editc(MVNT:'3'))
              +'}';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Slutt item line tager
        jsonstring ='}';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr OpdEDIS;
        //*********************

        chain ('S': 'KURERM') ECMSG1;
        chain 1 EDIC;
        csn = csn + 1;
        update edicr;

        s0004 = ECIIDI;
        s0010 = ECIIDC;
        s0020 = 'KM' + %TRIM(UPRO);
        s0026 = 'SADKMJSON';
        ssn   = CSN;
        ssr   = 'S';
        sst   = 'B';
        sdt   = %dec(wdt:8:0);
        stm   = %dec(wtm:6:0);
        slib  = '*LIB';
        sfile = '*IFS';
        smbr  = *BLANKS;
        sifs  = WFilNam;
        write edisr;

        endsr;

        //*********************
        Begsr init;
        //*********************

        mcpro = %dec(upro:8:0);

        // hent turheader
        chain mcpro SADKML02;

        // hent  consingment / xref   - MINST 1 må finnes
        chain mcpro SADKMCL1;
        if not %found;
          *INLR = *on;
          return;
        endif;
        // hent  sad header-data
        if MCavd <> 0;
          chain (mcavd: mctdn) SADH;
          if not %found;
            *INLR = *on;
            return;
          endif;
        endif;

        syge15c (wdt: wtm);

        XSTREKK = '/';

        // Åpne dataarea
        IN EDIDTA;

        endsr;

        //*********************
        Begsr ByggFlr;
        //*********************

        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/json_Ut';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/json_Ut/Temp';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/json_Ut/Backup';
        ARC040C1 (Fldr);

        // for Oscar/Java-bruk
        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/json_Ut/error';
        ARC040C1 (Fldr);

        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/json_Ut/sent';
        ARC040C1 (Fldr);

        // BAT = Batch / for Java sende motoren
        Fldr = %TRIM(XLIB) + '/KURERMANIFEST/BAT';
        ARC040C1 (Fldr);

        // automatistere også denne (lage egen CL ?)
        // også kopiere  /apache/kurer-client/upload-engine-kurermanif-client.jar
        //          til  .../BAT/upload-engine-kurermanif-client.jar

        endsr;

      /end-free
