     H debug(*yes)
      *********************************************************************
      *  RPG ILE MODULE SYSPED/FRL1O01
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/FRL1O01 MODULE(*LIBL/FRL1O01)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Bookshelves by description
     FWOTRAHF   If   e           K DISK    USROPN
     FPRISLD2   IF   e           K DISK    USROPN
     FPRISLH    IF   e           K DISK    USROPN
     F*ODQL01   IF   e           K DISK    USROPN
     FWEBORD    UF   e           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPRF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *=====================================================================
      * Includes to be used in all CGIs
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      *  Fields used for parsing the input string
     D action          s             10a
     D subaction       s             10a
     D lng             s              2a
     D PASSORD         S             10
     D WSUSER          S             10
     D KNAVN           S             30
     D LOGO            S             50
      * lev datod...
     D LDATO           s              4a
      * aktiv Ord...
     D Ord             s              8a
     D Ordnr           s              8  0
      * prisliste ..
     D Prinr           s              5a
     D Prinrn          s              5  0
      * aktiv ptoduktgruppe
     D PRDGRP          s             35
     D KODTXT          s             30
      * firmanummer
     D Firma           s              2
      *
      * kundnummer
     D Kundnr          s              8  0
     D KUNDna          s              8a
      *
     D htmlsrclib      s             10a
     D htmlsrcfil      s             10a
     D htmlsrcmbr      s             10a
      *--------------------------------------------------------------------
      * "InitSW"- is blank only the first time pgm is called
     DInitSW           S              1a
      *
     Dlinenbr          S              5s 0
     D XSPRÅK          s              2
     D WSANR           s             15a
     D XBPPRD          S            512
     D XXCMD           S            128
     D FEILMELDING     S            150
      * State related variables
     D State           ds                  based(StateP)
     D  UBRID                        10
     D  UINNDT                        8
     D  UINNTM                        6
      * User space variables
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
      * Message ID for CrtUsrSpc
     D MsgId           s              7

     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  IDAG                   1      8  0
     D  IDAGA                  1      4  0
     D  IDAGM                  5      6  0
     D  IDAGD                  7      8  0
     D                 DS
     D  BIDTF                  1      8  0
     D  BIDTFÅ                 1      4  0
     D  BIDTFM                 5      6  0
     D  BIDTFD                 7      8  0
     D                 DS
     D  BIDTV                  1      8  0
     D  BIDTVÅ                 1      4  0
     D  BIDTVM                 5      6  0
     D  BIDTVD                 7      8  0
     D                 DS
     D  BPPRD2                 1    128
     D  BPPRD2A                1      6
     D  BPPRD2B                7    128
     D                 DS
     D  WSDATOT                1      8  0
     D  WSAAT                  1      4  0
     D  WSMMT                  5      6  0
     D  WSDDT                  7      8  0
     D                 DS
     D  WSDATO                 1      6  0
     D  WSDD                   1      2  0
     D  WSMM                   3      4  0
     D  WSAA                   5      6  0
     D                 DS
     D  LLPAVRDA               1     50
     D  PAT                    1     50    DIM(50)
     D                 DS
     D  P2                     1     14
     D  P2T                    1     14    DIM(14)
     D  P25                    1      1
     D  P26                    1      2
     D  P27                    1      3
     D  P28                    1      4
     D  P29                    1      5
     D  P210                   1      6
     D  P211                   1      7
     D  P212                   1      8
     D  P213                   1      9
      *
      *--------------------------------------------------------------------
      * Other variables
      *--------------------------------------------------------------------
      * Konstanter  Norsk (engelsk lenger nede..)
     D FMELDING1       C                   CONST('Tastet bruker-id fin-
     D                                     nes ikke.')
     D FMELDING2       C                   CONST('Tastet bruker-id er -
     D                                     deaktivert av systemoperatø-
     D                                     r ')
     D FMELDING3       C                   CONST('Tastet bruker-id er -
     D                                     deaktivert p.g.a. for mang -
     D                                     e feilaktige på loggingsfor-
     D                                     søk. Kontakt systemoperatør-
     D                                     ')
     D FMELDING4       C                   CONST('Tastet passord er fe-
     D                                     il. Prøv igjen (max 4 feil -
     D                                     på rad tillates!)')
     D FMELDING5       C                   CONST('Tast en gyldig ønske-
     D                                     t levringsdato(DDMMÅÅ)')
     D FMELDING6       C                   CONST('Leveringsdato må vær-
     D                                     e senere enn dagens dato(DDMMÅÅ)')
      * Konstanter  engelsk  (FMELDING.. -> FMELDeNG..)
     D FMELDeNG1       C                   CONST('Keyed USERID does no-
     D                                     t exist.')
     D FMELDeNG2       C                   CONST('Keyed USERID is deac-
     D                                     tivated by system operator')
     D FMELDeNG3       C                   CONST('Keyed USERID is deac-
     D                                     tivated due to too many err-
     D                                     onous logon attempts. Conta-
     D                                     ct system administrator')
     D FMELDeNG4       C                   CONST('Keyed PASSWORD is wr-
     D                                     ong. Try again (maximum 4 c-
     D                                     onsecutive errors allowed!)')
     D FMELDeNG5       C                   CONST('Enter valid delivery-
     D                                     date (DDMMYY)')
     D FMELDeNG6       C                   CONST('Deliverydate must be-
     D                                     higher tahn date today(DDMMYY)')
      *=====================================================================
     C                   exsr      SetTimeStr
      *=====================================================================
      * Read remote browser input string and parse it
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prolog3
     C                   exsr      Parse
      *=====================================================================
      * Main line
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
     C                   exsr      LoadHtml
      *------------------
      * Start html
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':WSUSER)
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('LOGO':LOGO)
      * Prod-gruppe number
     C                   callp     updHTMLvar('PRDGRP':PRDGRP)
      * Prod-gruppe description
     C                   callp     updHTMLvar('KODTXT':KODTXT)
     C                   callp     updHTMLvar('PRINR':
     C                             %trim(%editc(Prinrn:'Z')))
     C                   callp     updHTMLvar('PRH_KNK':PRH_KNK)
     C                   callp     updHTMLvar('KUNDNA':
     C                             %trim(%editc(Kundnr:'Z')))
     C                   callp     updHTMLvar('FIRMA':Firma)
     C                   callp     updHTMLvar('LDATO':LDATO)
     C                   callp     updHTMLvar('ORD':
     C                             %trim(%editc(Ordnr:'Z')))
      *
     C                   callp     wrtsection('top')
     C                   eval      wrotetop = *on                               For pssr
      *
      * PASSORDKONTOLL HVIS USER MANGLER
      *
     C     wsuser        ifeq      *blanks
     C                   callp     wrtsection('pw')
     c     FEILMELDING   IFNE      *BLANKS
     C                   callp     wrtsection('pwf')
     C                   end
     c                   else
      * Fill left leg
      *  Open file
     C                   exsr      OpenDbf
     c* henter neste ordrenummer
      *
     c                   move      'N'           hode              1
     c     ORDNR         IFEQ      *ZEROS
     c                   exsr      hentor
     c                   else
     c     linkey2       chain     wotrahf                            33
     c  N33              move      'J'           hode
     c                   end
      *
      * Gjeldende ORDRENR
     C                   callp     updHTMLvar('USER':WSUSER)
     C                   callp     updHTMLvar('LDATO':LDATO)
     C                   callp     updHTMLvar('ORD':
     C                             %trim(%editc(Ordnr:'Z')))
     C                   callp     updHTMLvar('PRINR':
     C                             %trim(%editc(Prinrn:'Z')))
     C                   callp     updHTMLvar('PRH_KNK':PRH_KNK)
     C                   callp     updHTMLvar('KUNDNA':
     C                             %trim(%editc(Kundnr:'Z')))
     C                   callp     updHTMLvar('FIRMA':Firma)
     C                   callp     updHTMLvar('WSANR':WSANR)
      *
      * Start left leg
     c     hode          ifeq      'J'
     C                   callp     wrtsection('leg1')
     C                   eval      linenbr = 0
     C     PRIKEY        setll     PRISLD2
      *
     C     PRIKEY        readE     PRISLD2
     C                   dow       not%eof
     C     PRD_PGR       IFNE      GML_PGR
     C                   eval      linenbr = linenbr +1
     C*                  MOVEL     PRD_PGR       GRGRGR
     C*    GRUKEY        CHAIN     KODQ5A                             34
     c*  34              EVAL      KODTXT='NN'
     C* N34              MOVEL     KODTTQ        KODTXT           30
     C                   exsr      SetLeg1Row
     C                   callp     wrtsection('leg1rowa')
     C                   MOVE      PRD_PGR       GML_PGR          35
     C                   END
     C     PRIKEY        readE     PRISLD2
     C                   enddo
     c                   else
     C                   callp     wrtsection('SOKING')
     c     action        ifeq      'SOKING'
     C                   callp     wrtsection('SOKINGFEIL')
     c                   end
      *
     c                   end
      *
     C                   exsr      CloseDbf
     c                   end
      * End left leg
     C                   callp     wrtsection('leg1end')
      * Flush the html buffer and exit
     C                   exsr      Exit
      *=====================================================================
      * Set variables in html section "leg1row"
      *=====================================================================
      **********************************************************************
     C     SetLeg1Row    begsr
      **********************************************************************
      * Line number
     C                   callp     updHTMLvar('linenbr':
     C                             %trim(%editc(linenbr:'Z')))
      * Aktivt Ordrenr
     C                   callp     updHTMLvar('ORD':
     C                             %trim(%editc(Ordnr:'Z')))
      * Prisliste-nr
     C                   callp     updHTMLvar('PriNr':
     C                             %trim(%editc(Prinrn:'Z')))
      * Prisliste-nAVN
     C                   callp     updHTMLvar('PRH_KNK':PRH_KNK)
      * Kundenummer
     C                   callp     updHTMLvar('KUNDNA':
     C                             %trim(%editc(Kundnr:'Z')))
      * Levdato
     C                   callp     updHTMLvar('LDATO':LDATO)
      * Firma
     C                   callp     updHTMLvar('FIRMA':Firma)
      * Prod-gruppe number
     C                   callp     updHTMLvar('PRDGRP':PRD_PGR)
      * Prod-gruppe description
     C                   callp     updHTMLvar('KODTXT':KODTXT)
      *
     C                   endsr
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
      *=====================================================================
      **********************************************************************
     C     LoadHtml      begsr
      **********************************************************************
      * Read externally defined output html
     C                   eval      HtmlSrcLib = '*LIBL'
     C                   eval      HtmlSrcFil = 'HTMLSRC' + lng
     C                   eval      HtmlSrcMbr = 'FRL1O01'
     C                   callp     gethtml(HtmlSrcFil:HtmlSrcLib:HtmlSrcMbr:
     C                             '/CGITAG/')
      *
     C     *like         define    lng           prvlng
     C                   eval      prvlng = lng
     C                   endsr
      *=====================================================================
      * Open database files
      *=====================================================================
      **********************************************************************
     C     OpenDbf       begsr
      **********************************************************************
      *
     C                   open      PRISLD2
     C                   open      WOTRAHF
     C                   open      BRPRF
     C     linkey2       Klist
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    ORDNOT
     C     linkey3       Klist
     C                   KFLD                    FIRMA
     C                   KFLD                    ORDNOT
     C     grukey        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    GRGRGR            5
     C     PRIKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    Prinrn
     C     PARKEY        KLIST
     C                   KFLD                    WSUSER
     C                   KFLD                    KUNULL            8 0
     C                   KFLD                    PAID              5
     C                   endsr
      *=====================================================================
      * Close database files
      *=====================================================================
      **********************************************************************
     C     CloseDbf      begsr
      **********************************************************************
     C                   close     PRISLD2
     C                   close     WOTRAHF
     C                   close     BRPRF
     C                   endsr
      *=====================================================================
      *  Time started
      *=====================================================================
      **********************************************************************
     C     SetTimeStr    begsr
      **********************************************************************
     C                   TIME                    WTIME
     C                   MOVE      WÅÅ           IDAGA
     C                   MOVE      WMM           IDAGM
     C                   MOVE      WDD           IDAGD
     C                   time                    TimeStart
     C                   call      'INCGITALG'
     C                   endsr
      *=====================================================================
      *  Time ended and time elapsed elapsed
      *=====================================================================
      **********************************************************************
     C     SetTimeEnd    begsr
      **********************************************************************
     C                   time                    TimeEnd
     C     TimeEnd       subdur    TimeStart     MsElaps:*ms
     C                   eval      SecElaps = MsElaps / 1000000
     C                   endsr
      *=====================================================================
      * Send response html and quit
      *=====================================================================
      **********************************************************************
     C     Exit          begsr
      **********************************************************************
     C                   exsr      SetTimeEnd
     C                   callp     updhtmlvar('RUNTIME':
     C                             %trim(%editw(SecElaps:'     0 .   ')))
     C                   callp     wrtsection('runtime')
     C     wsuser        ifeq      *blanks
     C                   callp     wrtsection('end1')
     c                   else
     C                   callp     wrtsection('end2')
     c                   end
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   move      *on           *INLR
     C                   return
     C                   endsr
      *=====================================================================
      * Parse the input string
      *=====================================================================
      **********************************************************************
     C     Parse         begsr
      **********************************************************************
      *
      * Bruker-id
     C                   eval      WSUSER     = zhbgetvar('USER      ')
      * Passord
     C                   EVAL      PASSORD = zhbgetvar('PASSORD')
      *
      * Convert to uppercase
     C                   eval      WSUSER   = uppify(WSUSER)
     C                   eval      PASSORD  = uppify(PASSORD)
      *
      * behandling når brukerid er tastet
      *
     c     wsuser        ifne      *blanks
     C                   eval      action     = zhbgetvar('ACTION    ')
     C                   eval      KNAVN      = zhbgetvar('KNAVN     ')
     C                   eval      LOGO       = zhbgetvar('LOGO      ')
     C                   eval      lng        = zhbgetvar('lng       ')
     C                   eval      Ord        = zhbgetvar('ORD       ')
     C                   eval      LDATO      = zhbgetvar('LDATO     ')
     C                   eval      Ordnr      = c2n2(Ord)
     c                   z-add     ORDNR         ORDNOT
     C                   eval      Prinr      = zhbgetvar('PRINR     ')
     C                   eval      Prinrn     = c2n2(prinr)
     C                   eval      PRH_KNK    = zhbgetvar('PRH_KNK   ')
     C                   eval      FIRMA      = zhbgetvar('FIRMA     ')
     C                   eval      KUNDNA     = zhbgetvar('KUNDNA    ')
     C                   eval      Kundnr     = c2n2(KUNDNA)
     C                   eval      PRDGRP     = zhbgetvar('PRDGRP    ')
     C                   eval      KODTXT     = zhbgetvar('KODTXT    ')
      *
      * test bruker hvis ikke nyordre fra avslutning av midtbildet
      *
     c     action        ifne      'NYORDRE'
     c     action        andne     'SOKING'
     c                   exsr      testuser
     c                   else
     c                   open      bridf
     C     WSUSER        CHAIN     BRIDF                              30
     c                   close     bridf
      * nyordre start
     c     action        ifeq      'NYORDRE'
     c                   exsr      nyordre
     c                   z-add     ORDNR         ORDNOT
     c                   end
      * nyordre slutt
     c                   end
     c***                end
      * behandling ved blank bruker
     c                   else
     C                   eval      action     = *blanks
     C                   eval      KNAVN      = *blanks
     C                   eval      LOGO       = *blanks
     C                   eval      lng        = *blanks
     C                   eval      Ord        = *blanks
     C                   eval      LDATO      = *blanks
     C                   eval      Ordnr      = 0
     C                   eval      Prinr      = *blanks
     C                   eval      Prinrn     = 0
     C                   eval      FIRMA      = *blanks
     C                   eval      KUNDNA     = *blanks
     C                   eval      Kundnr     = 0
     C                   eval      PRDGRP     = *blanks
     C                   eval      KODTXT     = *blanks
     c                   end
     C                   endsr
     c********************************************************************
     c     hentor        begsr
     c********************************************************************
      * henter neste ordrenummer
      *
     C                   open      WEBORD
     C                   open      BRPARF
     C                   open      PRISLH
      * henter Logo
     c                   move      'LOGO '       paID
     c     parkey        chain     brparf                             33
     c  n33              movel     PAVRDA        LOGO
     c                   move      'L1PRL'       paID
     c     parkey        chain     brparf                             33
     c                   move      bieier        bieiern           5 0
     c                   z-add     bieiern       prinrn            5 0
     C     PRIKEY        CHAIN     PRISLH                             33
     C     NUMMKY        KLIST
     C                   KFLD                    FIRMA
     C     NUMMKY        CHAIN     webord                             33
     C  n33              ADD       1             weordn
     C  n33              UPDATE    webordr
     c  n33              z-add     weORDN        ORDNR
     C                   close     WEBORD
     C                   close     BRPARF
     C                   close     PRISLH
     C                   endsr
      **********************************************************************
     C     testuser      begsr
      **********************************************************************
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Override database files
     C                   EVAL      RC = DOCMD('OVRDBF FILE(BRIDF) +
     C                             TOFILE(*LIBL/BRIDF) SECURE(*YES)')
      * Åpne fil
     C                   OPEN      BRIDF
      * språk fra engelsk påloggingsside (kun ved feil pålogging)
      * ellers hentes det fra user-ID
     C                   EVAL      XSPRÅK  = zhbgetvar('SPR')
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              30
      *
      * BLANK I PASSORD - ANONYM PÅLOGGING - ALLTID OK..
      *
     C     *IN30         IFEQ      '0'
     C     BIPO          ANDEQ     *BLANKS
     C                   MOVE      *BLANKS       PASSORD
     C                   END
     c                   movel     bibesk        knavn
     c                   move      bifirm        firma
     c                   move      bikunr        kundnr
     C                   MOVE      *BLANKS       FEILMELDING
     C                   SELECT
      * avslutt
     C                   WHEN      WSUSER   = 'AVSLUTT'
     C                   MOVEL     '.'           FEILMELDING
     C                   EVAL      WSUSER   = PASSORD
     C     WSUSER        CHAIN     BRIDF                              30
     C                   MOVE      *ZEROS        BIANTF
     C                   MOVE      ' '           BIINN
     C                   WHEN      *IN30 = *ON
     C     XSPRÅK        Ifeq      'EN'
     C                   MOVEL     FMELDeNG1     FEILMELDING
     c                   else
     C                   MOVEL     FMELDING1     FEILMELDING
     c                   end
     C                   WHEN      ((BIAKT <> 'A') AND (BIAKT <> 'L'))
     C     XSPRÅK        Ifeq      'EN'
     C                   MOVEL     FMELDeNG2     FEILMELDING
     c                   else
     C                   MOVEL     FMELDING2     FEILMELDING
     c                   end
     C                   MOVE      ' '           BIINN
     C                   WHEN      BIANTF >  4
     C     XSPRÅK        Ifeq      'EN'
     C                   MOVEL     FMELDeNG3     FEILMELDING
     c                   else
     C                   MOVEL     FMELDING3     FEILMELDING
     c                   end
     C                   MOVE      ' '           BIINN
     C                   WHEN      BIPO   <> PASSORD
     C     XSPRÅK        Ifeq      'EN'
     C                   MOVEL     FMELDeNG4     FEILMELDING
     c                   else
     C                   MOVEL     FMELDING4     FEILMELDING
     c                   end
     c                   other
     C                   MOVE      *BLANKS       FEILMELDING
     C                   ENDSL
     C                   EXSR      UpdateFeil
      *
      * Noe feil - vis feilmelding / nytt forsøk ELLERS last neste HTML..
     C     FEILMELDING   IFNE      *BLANKS
     C                   EXSR      LoadHtmlFeil
     C*                  ELSE
     C*                  EXSR      LoadHtmlMnu
     C                   END
      *
     C  N30              UPDATE    BRIDFR
     C  N30              exsr      nyordre
     C                   ENDSR
      *=====================================================================
      * Noe feil ved bruker ID
      *=====================================================================
     C     LoadHtmlFeil  begsr
     C                   MOVE      *BLANKS       WSUSER
     C                   callp     updHTMLvar('FEILTXT':FEILMELDING)
     C                   callp     wrtsection('all')
     C                   endsr
      *=====================================================================
      * (Aktiv) Bruker har gitt feil passord - add 1 til antall ganger
      * Mer en xx feil - må RESETTES av operatør
      *=====================================================================
     C     UpdateFeil    begsr
     C   30              ADD       1             BIANTF
     C                   TIME                    WTIME
     C                   MOVE      WTID          BITIDF
     C                   MOVE      WÅÅ           BIDTFÅ
     C                   MOVE      WMM           BIDTFM
     C                   MOVE      WDD           BIDTFD
     C                   endsr
     c********************************************************************
     c     nyordre       begsr
     c********************************************************************
      * henter neste ordrenummer
      *
     C                   EXSR      HENTOR
     C                   callp     updHTMLvar('ORD':
     C                             %trim(%editc(Ordnr:'Z')))
      * Prod-gruppe number
     C*                  callp     updHTMLvar('PRDGRP':PRDGRP)
     C                   eval      PRDGRP=*blanks
      * Prod-gruppe description
     C                   callp     updHTMLvar('KODTXT':KODTXT)
     C                   eval      KODTXT=*blanks
      *
     C                   ENDSR
