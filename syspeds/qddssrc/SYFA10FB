     A*%%TS  SD  20151216  081148  SVEIN       REL-V7R1M0  5770-WDS
     A*                                     DSPSIZ(24 80 *DS3 27 132 *DS4)
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R FA10BB10
     A*%%TS  SD  20151211  134719  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A  89                                  CF11(11 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(01)
     A                                      CLRL(24)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYFA10B'
     A                                  1 20MSGCON(017 TRO0039 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 63TIME
     A            WMODE          7A  O  4 27COLOR(WHT)
     A                                  5 11MSGCON(013 TRO0025 *LIBL/SYMSGF)
     A            FMAVD          4Y 0O  5 27EDTCDE(4)
     A                                  6 11MSGCON(013 TRO0026 *LIBL/SYMSGF)
     A            FMOPD          7Y 0O  6 27EDTCDE(4)
     A                                  7 11MSGCON(013 TRO0027 *LIBL/SYMSGF)
     A            FMFBNR         3Y 0O  7 27EDTCDE(4)
     A                                  8 11MSGCON(013 TRO0028 *LIBL/SYMSGF)
     A            FMLINR         2Y 0O  8 27EDTCDE(4)
     A                                  9 11MSGCON(013 TRO0043 *LIBL/SYMSGF)
     A            FMMKNR         5Y 0O  9 27EDTCDE(4)
     A                                 10 11MSGCON(013 TRO0044 *LIBL/SYMSGF)
     A            FMMRK1        35A  B 10 27
     A  88                                  DSPATR(PR)
     A  89                             23  3MSGCON(020 TRO0003 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 23 23MSGCON(012 TRO0004 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            WMODE2        35A  O 15 27COLOR(RED)
     A                                 11 11MSGCON(013 SHU1618 *LIBL/SYMSGF)
     A                                 12 11MSGCON(013 SHU1619 *LIBL/SYMSGF)
     A            FMVKT          9  0B 11 27
     A            FMLEN          5  0B 12 27
     A          R FA10BB20
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYFA10B'
     A                                  1 20MSGCON(017 TRO0039 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 63TIME
     A                                  3  3MSGCON(009 TRO0012 *LIBL/SYMSGF)
     A            UAVD           4  0O  3 13EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 20MSGCON(006 TRO0013 *LIBL/SYMSGF)
     A            UOPD           7  0O  3 27EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 36MSGCON(012 TRO0014 *LIBL/SYMSGF)
     A            UFBNR          3  0O  3 49EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 54MSGCON(009 TRO0038 *LIBL/SYMSGF)
     A            ULINR          2  0O  3 64DSPATR(HI)
     A                                  4  3MSGCON(025 TRO0006 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10 25MSGCON(013 TRO0042 *LIBL/SYMSGF)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R FA10BB40
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'EXIT')
     A N31                                  CF05(05 'REFRESH')
     A N69                                  CF06(06 'NEW')
N01  A                                      CF10(10 'SE DUP')
N01  A                                      CF11(11 'MERK O')
     A                                      CF12(03)
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3MSGCON(008 TRO0002 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              1 13MSGCON(012 TRO0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N69                              1 26MSGCON(018 TRO0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 69MSGCON(012 TRO0004 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      DSPATR(HI)
     A            MSGNR          7A  P
     A                                  1 45'F11=Merk E->O=Original'
     A                                      COLOR(BLU)
     A          R FA10BH50                  SFL
     A*%%TS  SD  20151216  081148  SVEIN       REL-V7R1M0  5770-WDS
     A            SFMLNR         2Y 0H
     A            WSNR           1A  I  9  3VALUES(' ' '2' '3' '4' '5' 'L')
     A            SFMKNR         5Y 0O  9  5EDTCDE(4)
     A            SFST           1A  O  9 11COLOR(RED)
     A            SFMRK         35A  O  9 13
     A            SFITER         1A  O  9 52
     A            SFUTER         1A  O  9 57
     A            SFLEVE         1A  O  9 62
     A            SFVKT          9Y 0O  9 64EDTCDE(Z)
     A            SFLEN          5  0O  9 74EDTCDE(Z)
     A            SFPAID        17   O  9 80
     A          R FA10BK60                  SFLCTL(FA10BH50)
     A*%%TS  SD  20151216  081148  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  3'SYFA10B'
     A                                  1 20MSGCON(017 TRO0039 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 63TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  3MSGCON(009 TRO0012 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  3 13EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 20MSGCON(006 TRO0013 *LIBL/SYMSGF)
     A            UOPD           7Y 0O  3 27EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 36MSGCON(012 TRO0014 *LIBL/SYMSGF)
     A            UFBNR          3Y 0O  3 49EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 54MSGCON(009 TRO0038 *LIBL/SYMSGF)
     A            ULINR          2S 0O  3 64DSPATR(HI)
     A                                  4  4MSGCON(025 TRO0006 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N69                              5  6MSGCON(007 TRO0007 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N69                              5 15MSGCON(008 TRO0008 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N69                              5 25MSGCON(008 TRO0009 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 35MSGCON(006 TRO0010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 51MSGCON(018 JOV1389 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 42'L=Log'
     A                                      COLOR(BLU)
     A                                  7  3MSGCON(100 SHU1617 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
