********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 LOGIKK RUNDT HEKNT  ....
10XXX * 02 JOV PTF10 LOGIKK RUNDT HEKNT  - skal være TRANSP-nr
10XXX * 03 SH  PTF10 A=Arkiv
10XXX * 04 SH  PTF10 nye parametre sytt01
10XXX * 05 sh  PTF11 bringtms summere helmla/hepoen   BRINGTMS-prosjekt.
10XXX * 06 JOV PTF11 test mot at gitte typer farlig gods ikke kan mikses
********************************************************************
      *
      *
      ****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TUR (PLUKKE OPPDRAG)           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 02 04-11 13-24
      *** LEDIGE INDIKATORER 31 35-54 56-90 95-97
      ****************************************************************
      *
s02  F*RAN      IF   E           K DISK
     FTELL      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHESTED    IF   E           K DISK
     FTURER     UF   E           K DISK
     FHEADF     UF   E           K DISK
     FKODTA     IF   E           K DISK
     FDISP      UF   E           K DISK
     FDISPL01   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR1)
     FDISPL02   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR2)
     FDISPL03   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR3)
     FDISPL04   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR4)
     FKOSBU9    UF   E           K DISK
     FTRACKL02  IF   E           K DISK
     FSYTR88FC  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HEVKT                  1      9  0
     D  HEVKTW                 5      9  0
     D                 DS
     D  YSTEF2                 1      5
     D  AM                     1      5
     D                                     DIM(5)
     D                 DS
     D  YSTET2                 1      5
     D  AM2                    1      5
     D                                     DIM(5)
     D* Sted til utfylt - test mot den del som er utfylt
     D                 DS
     D  DITSTT                 1      5
     D  AD2                    1      5
     D                                     DIM(5)
     D* Sone til utfylt - test mot den del som er utfylt
     D                 DS
     D  DITSTS                 1      5
     D  AS2                    1      5
     D                                     DIM(5)
     D* De 2 første av SONE er LK dersom fylt ut i kombinasjon med sted
     D* betyr det at landode må stemme i TILLEG til sted (postnummer)
     D                 DS
     D  WSSONF                 1      5
     D  WSLKF                  1      2
     D                 DS
     D  DISONF                 1      5
     D  DILKF                  1      2
     D                 DS
     D  WSSONT                 1      5
     D  WSLKT                  1      2
     D                 DS
     D  DISONT                 1      5
     D  DILKT                  1      2
     D                 DS
     D  YSONF2                 1      5
     D  AL                     1      5
     D                                     DIM(5)
     D                 DS
     D  YSONT2                 1      5
     D  AL2                    1      5
     D                                     DIM(5)
     D                 DS
     D  HEVS1                  1     25
     D  HEVS10                 1     10
     D                 DS
     D  HENT                   1      7  0
     D  HENT4                  4      7  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   34DISP4K        SETLL     DISPL04
     C   27DISP3K        SETLL     DISPL03
     C   28DISP1K        SETLL     DISPL01
     C   29DISP2K        SETLL     DISPL02
     C     START1        TAG
     C                   EXSR      LESFIL
      *
     C     START2        TAG
     C                   Move      'N'           PDAFarge          1
      *
     C  N94              DO                                                     1<-B
     C                   DO        6                                             2<-B
     C                   ADD       1             ZRECNO
     C                   MOVE      *BLANKS       IMCO
     C                   MOVE      *BLANKS       ETTE
     C     TRFA21        IFNE      *BLANKS
     C                   MOVE      'A'           IMCO                            *ADR.gods
     C                   END
     C     TRFA11        IFNE      *BLANKS
     C                   MOVE      'I'           IMCO                            *IMCO gods
     C                   END
     C     TRETTB        IFNE      *ZEROS
     C                   MOVE      'E'           ETTE
     C                   END
     C*
     C* FIKSER DATOFORMAT PÅ BILDET
     C     FIDTFM        IFNE      3
     C                   MOVE      TRSDFD        WRSDFD
     C                   MOVE      TRSDTD        WRSDTD
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WRSDFD
     C                   PARM                    WRSDTD
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C*
     C                   Z-ADD(H)  HEM3          WSM3SU
     C     HEADFK        CHAIN     HESTED                             33
     C  N33              DO
     C                   MOVEL     HSFROM        HESDF
     C                   MOVEL     HSTO          HESDT
     C                   END
     C     TROPD0        COMP      *ZEROS                             5959
     C                   exsr      TSTPDA
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C                   EXSR      LESFIL
     C  N94              END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C* Sist leste post (hvis ikke EOF) er 1. post for neste skjermside
     C* (det leses "en for langt" for at en skal kunne vise '+' i bunn)
     C* Bevar alle subfildata i arbeidsvariable slik at de kan hentes
     C* tilbake uten disk-read ved roll videre (forenkler + sparer tid)
     C*
     C     *IN94         IFEQ      '0'
     C                   EXSR      MOVSUX
     C                   END
     C     *in30         IFEQ      '0'
     C     PDAFarge      andeq     'J'
     C                   MOVE      'JOV1375'     MSGNR
     C                   SETON                                            30
     C                   end
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   EXFMT     SUBCTL
     C     TUTVKT        ADD       TUTARA        WSBER1
     C                   END
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C                   SETOFF                                       9830
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
      * F05= REFRESH
     C   05              GOTO      START
     C   12              Z-ADD     9             UFLAGG
     C   12              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   EXSR      MOVXSU
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      ' '                                          1<-B
     C     W2AVD         ORNE      WSAVD
     C     W2OPDT        ORNE      WSOPDT
     C     W2SDF         ORNE      WSSTEF
     C     W2SDFD        ORNE      WSDATO
     C     W2SDT         ORNE      WSSTET
     C     W2SONF        ORNE      WSSONF
     C     W2SONT        ORNE      WSSONT
     C     W2NR          IFEQ      ' '                                           2<-B
     C                   EXSR      NYSØK
     C   30              GOTO      START3
     C  N30              GOTO      START
     C                   ELSE                                                    2
      *
     C     W2OPD         IFEQ      0                                              3<-B
     C                   SETON                                        9830
     C                   MOVE      'YBC0039'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYOPT
     C                   END                                                      3<-E
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      TESTIN
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C     W2AVD         IFNE      WSAVD
     C                   MOVE      W2AVD         WSAVD             4 0
     C                   END
     C     W2OPDT        IFNE      *BLANKS                                      1<-B
     C                   MOVE      W2OPDT        WSOPDT            2
     C                   END                                                    1<-E
     C     W2SDF         IFNE      WSSTEF                                       1<-B
     C                   MOVE      W2SDF         WSSTEF            5
     C                   END                                                    1<-E
     C     W2SDFD        IFNE      WSDATO                                       1<-B
     C                   MOVE      W2SDFD        WSDATO            6 0
     C                   END                                                    1<-E
     C     W2SDT         IFNE      WSSTET                                       1<-B
     C                   MOVE      W2SDT         WSSTET            5
     C                   END                                                    1<-E
     C     W2SONF        IFNE      WSSONF                                       1<-B
     C                   MOVE      W2SONF        WSSONF
     C                   END                                                    1<-E
     C     W2SONT        IFNE      WSSONT                                       1<-B
     C                   MOVE      W2SONT        WSSONT
     C                   END                                                    1<-E
      *
     C                   EXSR      TSTSØK
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYOPT         BEGSR
     C***********************************************
     C     W2AVD         IFEQ      0
     C                   MOVE      UAVD          W2AVD
     C                   END
     C     WRK2K         CHAIN(N)  DISP                               98
     C  N98DISTA1        COMP      STATDI                             9898
     C  N98WRK2K         CHAIN(N)  HEADF                              98
     C   98              DO
     C                   MOVE      'SPO0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLNYO
     C                   END
      *
     C                   Z-ADD(H)  HEM3          WSM3SU
     C     W2NR          IFEQ      '1'
     C     HEPRO         IFNE      0
     C                   MOVE      'SPO0010'     MSGNR
     C                   SETON                                        983032
     C                   GOTO      SLNYO
     C                   END
     C                   EXSR      PLUKK
     C                   ELSE
     C     W2NR          IFEQ      '5'
     C                   EXSR      VISOPD
     C                   END
     C                   END
     C                   MOVE      ' '           W2NR
     C                   MOVE      WSAVD         W2AVD
     C                   MOVE      *ZEROS        W2OPD
      *
     C     SLNYO         TAG
     C                   MOVE      WSOPDT        W2OPDT
     C                   MOVE      WSSTEF        W2SDF
     C                   MOVE      WSDATO        W2SDFD
     C                   MOVE      WSSTET        W2SDT
     C                   MOVE      WSSONF        W2SONF
     C                   MOVE      WSSONT        W2SONT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSUX        BEGSR
     C***********************************************
     C* Post er lest, men skal først senere skrives til subfil.
     C* Flytter de nødvendige subfilbegrep
     C* (KUN HEADF-FELT TRENGS BEVARES-HEADTR CHAINES FØR WRITESUBFIL)
     C* Felt i subfila som er hentet opp FØR SETSUB
     C                   MOVE      HEAVD         XXAVD
     C                   MOVE      HEOPD         XXOPD
     C                   MOVE      HENAS         XXNAS
     C                   MOVE      HENAK         XXNAK
     C                   MOVE      HESDF         XXSDF
     C                   MOVE      HESDT         XXSDT
     C                   MOVE      HENT4         XXNT4
     C                   MOVE      HEVS10        XXVS10
     C                   MOVE      HEVKTW        XXVKTW
     C                   MOVE      HELM          XXLM
N05  C                   MOVE      HELMLA        XXLMLA
N05  C                   MOVE      HEPOEN        XXPOEN
     C                   MOVE      HEOT          XXOT
     C                   MOVE      HEFR          XXFR
     C                   MOVE      HEST          XXST
     C                   MOVE      TROPD0        XXOPD0
     C                   MOVE      WSM3SU        XXM3SU
     C                   MOVE      WRSDFD        XXSDFD
     C                   MOVE      WRSDTD        XXSDTD
     C                   MOVE      IMCO          XXIMCO
     C                   MOVE      ETTE          XXETTE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVXSU        BEGSR
     C***********************************************
     C*
     C                   MOVE      XXAVD         HEAVD
     C                   MOVE      XXOPD         HEOPD
     C                   MOVE      XXNAS         HENAS
     C                   MOVE      XXNAK         HENAK
     C                   MOVE      XXSDF         HESDF
     C                   MOVE      XXSDT         HESDT
     C                   MOVE      XXNT4         HENT4
     C                   MOVE      XXVS10        HEVS10
     C                   MOVE      XXVKTW        HEVKTW
     C                   MOVE      XXLM          HELM
N05  C                   MOVE      XXLMLA        HELMLA
N05  C                   MOVE      XXPOEN        HEPOEN
     C                   MOVE      XXOT          HEOT
     C                   MOVE      XXFR          HEFR
     C                   MOVE      XXST          HEST
     C                   MOVE      XXOPD0        TROPD0
     C                   MOVE      XXM3SU        WSM3SU
     C                   MOVE      XXSDFD        WRSDFD
     C                   MOVE      XXSDTD        WRSDTD
     C                   MOVE      XXIMCO        IMCO
     C                   MOVE      XXETTE        ETTE
     C                   ENDSR
     C*
     C***********************************************
     C     MAX           BEGSR
     C***********************************************
     C                   MOVE      WSSTEF        YSTEF             5
     C                   MOVE      WSSTEF        YSTEF2            5
     C                   MOVE      WSSTET        YSTET             5
     C                   MOVE      WSSTET        YSTET2            5
     C                   MOVE      WSSONF        YSONF             5
     C                   MOVE      WSSONF        YSONF2            5
     C                   MOVE      WSSONT        YSONT             5
     C                   MOVE      WSSONT        YSONT2            5
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSTEF2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSTEF2
     C                   ELSE
     C     STMBI3        TAG
     C     AM(XN)        IFEQ      ' '
     C                   MOVE      '9'           AM(XN)
     C                   SUB       1             XN
     C     XN            IFGE      1
     C                   GOTO      STMBI3
     C                   END
     C                   END
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSTET2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSTET2
     C                   ELSE
     C     STMBI4        TAG
     C     AM2(XN)       IFEQ      ' '
     C                   MOVE      '9'           AM2(XN)
     C                   SUB       1             XN
     C     XN            IFGE      1
     C                   GOTO      STMBI4
     C                   END
     C                   END
     C** XN=ant.utfylte char / TN= blank fra og med TN for test
     C     XN            IFLT      5
     C     XN            ADD       1             TN                2 0
     C                   END
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSONF2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSONF2
     C                   ELSE
     C     STMBIL        TAG
     C     AL(XN)        IFEQ      ' '
     C                   MOVE      '9'           AL(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBIL
     C                   END
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSONT2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSONT2
     C                   ELSE
     C     STMBI2        TAG
     C     AL2(XN)       IFEQ      ' '
     C                   MOVE      '9'           AL2(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBI2
     C                   END
     C** XN=ant.utfylte char / SN= blank fra og med SN for test
     C     XN            IFLT      5
     C     XN            ADD       1             SN                2 0
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TSTSØK        BEGSR
     C***********************************************
     C                   SETOFF                                       99
     C     WSAVD         IFNE      0
     C     WSAVD         CHAIN     TELL                               99
     C   99AVDZER        CHAIN     TELL                               99
     C   99              MOVE      'SPA0009'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLTST
     C                   END
     C*
     C* LEGGER OVER MAKSVERDIER FOR STEDSNAVN
     C*
     C                   EXSR      MAX
     C*
     C* FIXER DATOBEGREP
     C*
     C                   MOVE      WSDATO        LLDAT6            6 0
     C     WSDATO        IFNE      *ZEROS
     C     FIDTFM        ANDNE     3
     C                   CALL      'DATFM5'
     C                   PARM      FIDTFM        FIDTFM
     C                   PARM                    LLDAT6
     C                   END
     C     LLDAT6        IFEQ      0
     C                   MOVE      *ZEROS        LLDATO            8 0
     C                   ELSE
     C     LLDAT6        IFGT      501231
     C     LLDAT6        ADD       19000000      LLDATO
     C                   ELSE
     C     LLDAT6        ADD       20000000      LLDATO
     C                   END
     C                   END
     C* AVD. BRUKES SOK KEY KUN NÅR DE 3 ANDRE ALT. ER BLANKE...
     C     WSSTEF        IFEQ      *BLANKS
     C     WSSONF        ANDEQ     *BLANKS
     C     WSOPDT        ANDEQ     *BLANKS
     C     WSAVD         ANDNE     *ZEROS
     C                   MOVEA     '000'         *IN(27)
     C                   MOVE      '1'           *IN34
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSSTEF        IFNE      *BLANKS
     C                   MOVEA     '010'         *IN(27)
     C                   MOVE      '0'           *IN34
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSSONF        IFNE      *BLANKS
     C                   MOVEA     '001'         *IN(27)
     C                   MOVE      '0'           *IN34
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSOPDT        IFNE      *BLANKS
     C                   MOVEA     '100'         *IN(27)
     C                   MOVE      '0'           *IN34
     C                   GOTO      SLTST
     C                   END
     C*
      * INGEN KEYBEGREP -> ALLE ÅPNE OPPDRAG/ALLE AVD
      *                 -> EVT MED BEGRENSNING TILSTED/TILSONE
     C                   SETON                                        34
     C     *LOVAL        SETLL     KODTA
     C                   READ      KODTA                                  94
     C  N94              MOVE      KOAAVD        WSAVD
     C*
     C     SLTST         ENDSR
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C* Hvis en leser på tvers av avdeling - sett key før les
     C* blank etter les (WSAVD er også kontrollfelt for ENDRET SØKEVEI)
     C     STLES         TAG
     C     W2AVD         IFEQ      *ZEROS
     C                   MOVE      KOAAVD        WSAVD
     C                   END
     C   34K4EQ          READE     DISPL04                                94
     C   27K3EQ          READE     DISPL03                                94
     C   28LESNES        READE     DISPL01                                94
     C   29LESNES        READE     DISPL02                                94
     C     W2AVD         IFEQ      *ZEROS
     C                   MOVE      *ZEROS        WSAVD
     C                   END
     C*
     C*__________________________________________________________
     C* På tvers av avd.(VIA AVD-KEY) - hente neste avdeling?
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *IN94         IFEQ      '1'
     C     *IN34         ANDEQ     '1'
     C     W2AVD         ANDEQ     *ZEROS
     C                   MOVE      '0'           *IN94
     C                   READ      KODTA                                  94
     C*   *IN94 ON = det fantes ikke flere avdelinger
     C     *IN94         IFEQ      '0'
     C                   MOVE      KOAAVD        WSAVD
     C     DISP4K        SETLL     DISPL04
     C                   GOTO      STLES
     C                   ENDIF
     C                   MOVE      *ZEROS        WSAVD
     C                   ENDIF
     C*
     C   94              GOTO      SLXY
     C*
     C* AVDELING UTFYLLT MEN IKKE KEY - TESTES PÅ DETTE
     C*
     C     WSAVD         IFNE      *ZEROS
     C     WSAVD         ANDNE     DIAVD
     C     *IN34         ANDNE     '1'
     C                   GOTO      STLES
     C                   END
     C*
     C* OPPDRAGSTYPE FYLT UT MEN IKKE KEY - TEST PÅ DETTE
     C*
     C     WSOPDT        IFNE      *BLANKS
     C     DIOT          ANDNE     WSOPDT
     C     *IN27         ANDNE     '1'
     C                   GOTO      STLES
     C                   END
     C*
     C*
     C* HVIS TILSTED ER PUNCHET MÅ DET VÆRE RIKTIG(uavhengig av key)
     C* (men gjør compare kun på utfylt del av feltet)
     C     WSSTET        IFNE      *BLANKS
     C                   MOVE      DISTET        DITSTT
     C                   MOVEA     *BLANK        AD2(TN)
     C     DITSTT        IFNE      WSSTET
     C                   GOTO      STLES
     C                   END
     C                   END
     C* HVIS TILSONE ER PUNCHET MÅ DET VÆRE RIKTIG(uavhengig av key)
     C* (men gjør compare kun på utfylt del av feltet)
     C* (Dette takler landkode i forb. med tastet Tilsted(postnr) også)
     C     WSSONT        IFNE      *BLANKS
     C                   MOVE      DISONT        DITSTS
     C                   MOVEA     *BLANK        AS2(SN)
     C     DITSTS        IFNE      WSSONT
     C                   GOTO      STLES
     C                   END
     C                   END
     C* DATO UTFYLT - TEST ALLTID
     C     WSDATO        IFNE      *ZEROS
     C     DIDATF        ANDLT     LLDATO
     C                   GOTO      STLES
     C                   END
     C*
     C* SØKING VED STED FRA UTFYLLT
     C*
     C   28
     CANN94DISTEF        COMP      YSTEF2                             94
     C   94              GOTO      SLXY
     C   28              DO
     C* Når lankode er fylt ut kombinert med sted (LK + postnr) test
     C     WSLKF         IFNE      *BLANKS
     C     WSLKF         ANDNE     DILKF
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C* SLUTT PÅ TEST VED 28   STTD FRA ER UTFYLLT
     C*
     C*
     C* SØKING VED SONE FRA UTFYLLT  (MEN IKKE STED FRA)
     C*
     C   29
     CANN94DISONF        COMP      YSONF2                             94
     C   94              GOTO      SLXY
     C*
     C* SLUTT SONETEST
     C*
     C     *IN94         IFEQ      '0'
     C     DIKEY         CHAIN(N)  HEADF                              33
     C   33              GOTO      STLES
     C                   END
     C*
     C     SLXY          ENDSR
     C***********************************************
     C     TSTPDA        BEGSR
     C***********************************************
      * ROSA(lilla?) AVSENDER hvis udisponert og avvist fra PDA
     C                   move      *off          *in71
     C     HEPRO         IFEQ      *zeros
     C     TTPDAK        Chain     TRACKL02                           33
     C  N33              move      *on           *in71
     C  N33              move      'J'           PDAFarge
     C                   end
     C*
     C                   ENDSR
     C***********************************************
     C     TESTIN        BEGSR
     C***********************************************
     C                   SETON                                        32
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'                                           2<-B
     C     HENAS         IFEQ      XPLUKK                                         3<-B
     C                   MOVE      'SPO0010'     MSGNR
     C                   SETON                                        983032
     C                   END                                                      3<-E
     C  N98              EXSR      PLUKK
     C                   SETON                                        55
     C                   UPDATE    SUBFIL
     C                   SETOFF                                       55
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST2
     C                   END                                                     2<-E
     C*
     C  N98WSNR          IFEQ      '5'                                           2<-B
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C  N98              EXSR      VISOPD
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST2
     C                   END                                                     2<-E
     C*
N03   * OPT A = Arkiv
N03  C  N98WSNR          IFEQ      'A'                                           2<-B
N03  C                   CALL      'ARC003'
N03  C                   PARM                    HEAVD
N03  C                   PARM                    HEOPD
N03  C                   PARM                    URUNDE           10
N03  C                   exsr      TSTPDA
N03  C                   MOVE      ' '           WSNR
N03  C                   UPDATE    SUBFIL
N03  C                   GOTO      SLTST2
N03  C                   END                                                     2<-E
     C  N98WSNR          IFEQ      'L'                                           2<-B
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C                   Z-ADD     1             TTFLAG            1 0
N04  C                   Z-ADD     0             TTTUR             8 0
     C                   CALL      'SYTT01CL'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
N04  C                   PARM                    TTTUR
     C                   PARM                    TTFLAG
     C                   GOTO      SLTST2
     C                   END                                                     2<-E
     C*
     C                   END                                                    1<-E
     C*
     C  N32              SETOFF                                       98
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C     SLTST2        ENDSR
     C*
     C***********************************************
     C     PLUKK         BEGSR
     C***********************************************
     C* OPPDATERER PLUKKET ORDRE
     C*
N06   * SJEKK OM FARLIG GODS SOM IKKE KAN SAMLASTES
N06  C                   CALL      'SYGE84'
N06  C                   PARM      HEAVD         UFARAVD           4 0
N06  C                   PARM      HEOPD         UFAROPD           7 0
N06  C                   PARM      UPRO          UPRO              8 0
N06  C                   PARM                    UFARSAM           1
N06  C     UFARSAM       IFEQ      '1'
N06  C                   MOVE      'JOV1673'     MSGNR
N06  C                   SETON                                        983032
N06  C                   GOTO      UTPLUK
N06  C                   ENDIF
      *
     C     HEADFK        CHAIN     HEADF                              33
     C     HEPRO         IFNE      0                                            1<-B
     C                   UPDATE    HEADFR
     C                   MOVE      'JOV0427'     MSGNR
     C                   SETON                                        983032
     C                   ELSE                                                   1
     C     HEST          IFEQ      'X'                                           2<-B
     C                   UPDATE    HEADFR
     C                   MOVE      'JOV0428'     MSGNR
     C                   SETON                                        983032
     C                   GOTO      UTPLUK
     C                   END                                                     2<-E
     C                   Z-ADD     UPRO          HEPRO
     C                   MOVE      TURUND        TRRUND
     C                   MOVEL     TUBILN        HEHAWB
s02  C*    TRANKE        CHAIN     TRAN                               33
s02  C*                  MOVE      VMTRKU        HEKNT
N02  C                   MOVE      TUKNT2        HEKNT
     C                   MOVE      TUTRMA        HETRM
     C     TUCON1        IFNE      *BLANKS                                       2<-B
     C                   MOVE      1             HETRC
     C                   MOVEL     TUCON1        HETRCN
     C                   END                                                     2<-E
     C                   UPDATE    HEADFR
     C     HEPRO         IFEQ      UPRO                                          2<-B
     C     TURERK        CHAIN     TURER                              33
     C                   MOVE      HEOT          TUOPDT
     C                   ADD       HENT          TUTS
     C                   ADD       HEVKT         TUTVKT
     C                   ADD       HEM3          TUTM3
     C                   ADD       HELM          TUTLM
N05  C                   ADD       HELMLA        TUTLM2
N05  C                   ADD       HEPOEN        TUPOEN
     C                   ADD       1             TUAO
     C                   UPDATE    TURERR
     C     HEADFK        CHAIN     DISP                               33
     C  N33              MOVE      HEPRO         DITUR
     C  N33              MOVE      TURUND        DIRUND
     C  N33              MOVE      TUAVD         DITUAV
     C  N33              MOVE      ' '           DISTA1
     C  N33              UPDATE    DISPR
     C                   EXSR      BUTADD
     C                   END                                                      2<-E
     C                   END                                                     1<-E
      *
     C                   MOVE      XPLUKK        HENAS
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      *ZEROS        HENT
     C                   MOVE      *BLANKS       HEVS1
     C                   MOVE      *BLANKS       HESDF
     C                   MOVE      *BLANKS       HESDT
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
     C                   MOVE      *ZEROS        WSM3SU
     C                   MOVE      *ZEROS        HELM
N05  C                   MOVE      *ZEROS        HELMLA
N05  C                   MOVE      *ZEROS        HEPOEN
     C                   MOVE      *BLANKS       HEOT
     C                   MOVE      *BLANKS       HEFR
     C                   MOVE      *BLANKS       HEST
     C                   MOVE      *BLANKS       IMCO
     C                   MOVE      *BLANKS       ETTE
     C                   MOVE      *ZEROS        TRSDFD
     C                   MOVE      *ZEROS        WRSDFD
     C                   MOVE      *BLANKS       TRSTAF
     C                   MOVE      *ZEROS        TRSDTD
     C                   MOVE      *ZEROS        WRSDTD
     C                   MOVE      *BLANKS       TRSTAE
     C*
     C     UTPLUK        ENDSR
     C*
     C*
     C***********************************************
     C     BUTADD        BEGSR
     C***********************************************
     C     HEOPD         CABEQ     *ZEROS        UTBUTA
     C* GÅR IGJENNOM EVT. REKVISISJONER PÅ DETTE OPPDRAG OG LEGGER INN
     C* TURNUMMER (FOR EVT Å KUNNE INNGÅ I TRANSPORTØRAVREGNING )
     C                   MOVE      ' '           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      TURMND        BUPMN                          *PERIODE FØLGER
     C                   MOVE      TURAAR        BUPÅR                           ALLTID TUREN...
     C                   MOVE      TURACC        BUPCC                           ALLTID TUREN...
     C     BUBILN        IFEQ      *BLANKS
     C                   MOVE      TUBILN        BUBILN
     C                   MOVE      TUBILK        BUBILK
     C                   MOVE      TUDT          BUFEDT
     C                   END
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C* OPPDATER TUR OGSÅ PÅ EVT. DUMMY/DUPREKV. PÅ OPPDRAGET
     C                   EXSR      BUTAD2
     C*
     C     UTBUTA        ENDSR
     C***********************************************
     C     BUTAD2        BEGSR
     C***********************************************
     C* EVT. DUMMYREKVISISJONER (DUP)PÅ DETTE OPPDRAG-LEGGER INN TURNR
     C* (FOR Å FÅ "TOTALBILDE" VED SPØRRING PÅ REKV - STATUS "*")
     C                   MOVE      'D'           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      TURMND        BUPMN                          *PERIODE FØLGER
     C                   MOVE      TURAAR        BUPÅR                           ALLTID TUREN...
     C                   MOVE      TURACC        BUPCC                           ALLTID TUREN...
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C                   ENDSR
     C*
     C***********************************************
     C     VISOPD        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C                   MOVE      *ZEROS        UFLAGG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
s02  C*    TRANKE        KLIST
s02  C*                  KFLD                    FIFIRM
s02  C*                  KFLD                    TUKNT2
     C     WRK2K         KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    W2OPD
     C     DIKEY         KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C     DISP1K        KLIST
     C                   KFLD                    STATDI            1
     C                   KFLD                    NULPRO
     C                   KFLD                    YSTEF
     C                   KFLD                    LLDATO
     C                   KFLD                    YSTET
     C     DISP2K        KLIST
     C                   KFLD                    STATDI
     C                   KFLD                    NULPRO
     C                   KFLD                    YSONF
     C                   KFLD                    LLDATO
     C                   KFLD                    YSONT
     C     DISP3K        KLIST
     C                   KFLD                    STATDI
     C                   KFLD                    NULPRO
     C                   KFLD                    WSOPDT
     C                   KFLD                    LLDATO
     C     DISP4K        KLIST
     C                   KFLD                    STATDI
     C                   KFLD                    NULPRO
     C                   KFLD                    WSAVD
     C                   KFLD                    LLDATO
     C     LESNES        KLIST
     C                   KFLD                    STATDI            1
     C                   KFLD                    NULPRO
     C     K3EQ          KLIST
     C                   KFLD                    STATDI
     C                   KFLD                    NULPRO
     C                   KFLD                    WSOPDT
     C     K4EQ          KLIST
     C                   KFLD                    STATDI
     C                   KFLD                    NULPRO
     C                   KFLD                    WSAVD
     C*
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KODTAK        KLIST
     C                   KFLD                    YUNI
     C                   KFLD                    WSAVD
     C     KOSB9K        KLIST
     C                   KFLD                    YYST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      * T&T NEI - ordre avvist fra PDA.
     C     TTPDAK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   MOVE      'NEI'         TTACTI
     C*
     C                   MOVE      ' '           YYST              1
     C                   READ      FIRM                                   33
     C     *LIKE         DEFINE    DITUR         NULPRO
     C                   MOVE      *ZEROS        AVDZER            4 0
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    URUND             8 0
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *ZEROS        UFLAGG
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      UAVD          W2AVD
     C                   MOVE      *ZEROS        YPRO              8 0
     C                   MOVE      *ZEROS        YKNA              8 0
     C                   MOVE      'A'           YUNI              1
     C                   MOVE      *BLANKS       YSONF
     C                   MOVE      *BLANKS       YSONF2
     C                   MOVE      *BLANKS       YSONT
     C                   MOVE      *BLANKS       YSONT2
     C                   MOVE      *BLANKS       YSTEF
     C                   MOVE      *BLANKS       YSTEF2
     C                   MOVE      *BLANKS       YSTET
     C                   MOVE      *BLANKS       YSTET2
     C*
     C                   MOVE      'DIV0702'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XPLUKK           30
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C     TURERK        CHAIN(N)  TURER                              33
     C     TUKVKT        ADD       TUTARA        TUBER2
     C                   MOVE      *ZEROS        WSDATO
     C* FIKSER DATO
     C*
     C                   MOVE      TUDT          WSDAFR
     C                   MOVE      TUDTT         WSDATI
     C     FIDTFM        IFNE      1
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WSDAFR
     C                   PARM                    WSDATI
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C* Definer hjelpevariable for å bevare subfildata
     C     *LIKE         DEFINE    HEAVD         XXAVD
     C     *LIKE         DEFINE    HEOPD         XXOPD
     C     *LIKE         DEFINE    HENAS         XXNAS
     C     *LIKE         DEFINE    HENAK         XXNAK
     C     *LIKE         DEFINE    HESDF         XXSDF
     C     *LIKE         DEFINE    HESDT         XXSDT
     C     *LIKE         DEFINE    HENT4         XXNT4
     C     *LIKE         DEFINE    HEVS10        XXVS10
     C     *LIKE         DEFINE    HEVKTW        XXVKTW
     C     *LIKE         DEFINE    HELM          XXLM
N05  C     *LIKE         DEFINE    HELMLA        XXLMLA
N05  C     *LIKE         DEFINE    HEPOEN        XXPOEN
     C     *LIKE         DEFINE    HEOT          XXOT
     C     *LIKE         DEFINE    HEFR          XXFR
     C     *LIKE         DEFINE    HEST          XXST
     C     *LIKE         DEFINE    TROPD0        XXOPD0
     C     *LIKE         DEFINE    TRSDFD        XXSDFD
     C     *LIKE         DEFINE    TRSDTD        XXSDTD
     C     *LIKE         DEFINE    TRSTAF        XXSTAF
     C     *LIKE         DEFINE    TRSTAE        XXSTAE
     C     *LIKE         DEFINE    WSM3SU        XXM3SU
     C     *LIKE         DEFINE    IMCO          XXIMCO
     C     *LIKE         DEFINE    ETTE          XXETTE
     C*
     C*
     C                   EXSR      TSTSØK
     C                   ENDSR
     C*
