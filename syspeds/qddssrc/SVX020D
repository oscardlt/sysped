********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10311 * 02 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 73 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5')
S01  A*           TVDTY          3A  O  4  5
N01  A            TVDTY          4A  O  4  5
E01  A            TVDREF        20A  O  4 10
E01  A            TVDSK          2A  O  4 31
E01  A            TVDO          26A  O  4 34
E01  A            TVDOSK         2A  O  4 61
     A            TVMTXT        48A  H
     A            TVMSK          2A  H
E01  A            TVTLO          5A  O  4 64
E01  A            TVEXKD         1  0O  4 70EDTCDE(4)
E01  A            TVEXLK         2A  O  4 72
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lag ny')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX020R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll dokumenter  (rubr. 44)'
     A                                      COLOR(WHT)
     A N27                              2  2'2=Ændra 4=Radera 5=Visa'
     A                                      COLOR(BLU)
     A  27                              2  2'5=Visa'
     A                                      COLOR(BLU)
E01  A                                  3  2'Op Dokument typ/referans/SK     Up-
E01  A                                      plysningar               SK T.O. ID'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                  9  2'   '
     A                                 11  2'F3/12=Avbryt F5=Uppdatera F6=Skapa-
     A                                       ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(09)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
E01  A                                  1  2'   Dokument typ/referans/SK     Up-
E01  A                                      plysningar               SK T.O. ID'
     A                                      COLOR(WHT)
S01  A*           WSDTY          3A  B  2  5DSPATR(UL)
N01  A            WSDTY          4A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
E01  A            WSDREF        20A  B  2 10DSPATR(UL)
     A  89                                  DSPATR(RI)
E01  A            WSDSK          2A  B  2 31DSPATR(UL)
     A  88                                  DSPATR(RI)
E01  A            WSDO          26A  B  2 34DSPATR(UL)
E01  A            WSDOSK         2A  B  2 61DSPATR(UL)
     A  87                                  DSPATR(RI)
E01  A            WSTLO          5A  B  2 64DSPATR(UL)
     A  86                                  DSPATR(RI)
E01  A            WSEXKD         1  0B  2 70DSPATR(UL)
     A                                      EDTCDE(4)
E01  A            WSEXLK         2A  B  2 72DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                  3  2'Mærknad:'
     A            WSMTXT        48A  B  3 11DSPATR(UL)
     A            WSMSK          2A  B  3 60DSPATR(UL)
     A  84                                  DSPATR(RI)
     A N30                              4  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(09)
     A                                      CLRL(04)
     A                                      OVERLAY
E01  A                                  1  2'   Dokument type/referanse/SK   Up-
E01  A                                      plysninger               SK T.O. ID'
     A                                      COLOR(WHT)
S01  A*           TVDTY          3A  O  2  5DSPATR(UL)
N01  A            TVDTY          4A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
E01  A            TVDREF        20A  O  2 10DSPATR(UL)
     A                                      DSPATR(HI)
E01  A            TVDSK          2A  O  2 31DSPATR(UL)
     A                                      DSPATR(HI)
E01  A            TVDO          26A  O  2 34DSPATR(UL)
     A                                      DSPATR(HI)
E01  A            TVDOSK         2A  O  2 61DSPATR(UL)
     A                                      DSPATR(HI)
E01  A            TVTLO          5A  O  2 64DSPATR(UL)
     A                                      DSPATR(HI)
E01  A            TVEXKD         1  0O  2 70DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
E01  A            TVEXLK         2A  O  2 72DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'Mærknad:'
     A            TVMTXT        48A  O  3 11DSPATR(UL)
     A                                      DSPATR(HI)
     A            TVMSK          2A  O  3 60DSPATR(UL)
     A                                      DSPATR(HI)
     A N30                              4  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX020R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll domumenter  (rubr. 44)'
     A                                      COLOR(WHT)
     A N27                              2  2'2=Ændra 4=Radera 5=Visa'
     A                                      COLOR(BLU)
     A  27                              2  2'5=Visa'
     A                                      COLOR(BLU)
E01  A                                  3  2'Op Dokument typ/referans/SK     Up-
E01  A                                      plysningar               SK T.O. ID'
     A                                      COLOR(WHT)
     A N30                             11  2'F3/12=Avbryt F5=Uppdatera F6=Skapa-
     A                                       ny'
     A                                      COLOR(BLU)
