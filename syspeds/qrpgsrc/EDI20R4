      *****************************************************************
      * Corrections in this program
      * Internal Chg.Ord.
      * number   Number    Description...................
      * """"""""""""""""""""""""""""""""""""""""""""""""""""""
      * New program replaces SM200 in version 2, release 1.
     H DATEDIT(*YMD)
      ***********************************************************
      ***********************************************************
      *
     FFSR00F    IF A E             DISK
     FLOGF      IF A E             DISK
     FCNFF      UF   E             DISK
     FLOCL01    IF   E           K DISK
     FISUL01    IF   E           K DISK
     FPARL01    IF   E           K DISK
      ***********************************************************
      *____________________________________________________________
      * Definition of the parameter AUTINF
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  AUTTRM                 1      1
     D  AUTPID                 2     18
     D  AUTALN                19     21
     D  AUTAWB                22     29
     D  AUTINF                 1     29
      *____________________________________________________________
      * Dobbel Definition of AWBNO
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  AWBNO                  1     11  0
     D  S0211                  1      3  0
     D  S0213                  4     11  0
      *____________________________________________________________
      * Parameter list
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WWAUTI           29
     C                   PARM                    WWUSER           17
      *____________________________________________________________
      * Initialize subroutine
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   EXSR      INIT
      *____________________________________________________________
      * Set autotransmit values
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     WWAUTI        AUTINF
     C     AUTPID        IFNE      *BLANKS
     C                   MOVEL     AUTPID        DEFADR           17
     C                   ENDIF
     C                   MOVEL     AUTALN        S0211
     C                   MOVEL     AUTAWB        S0213
     C                   EXSR      VALSR
     C                   EXSR      SNDRQS
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *
      *****************************************************************
     C     INIT          BEGSR
      *****************************************************************
     C                   Z-ADD     5             A                 2 0
     C                   Z-ADD     3             B                 3 0
     C                   Z-ADD     0             RRECNR            9 0
     C                   MOVEL     'SMADTATQ'    QTNAME           10
     C                   MOVEL     'SMADTARQ'    QRNAME           10
     C                   MOVEL     'SMALIBF'     QTLIB            10
     C                   MOVEL     'SMALIBF'     QRLIB            10
     C                   MOVEL     *BLANKS       QRFIEL            9
     C                   Z-ADD     9             QTLEN             5 0
     C                   Z-ADD     9             QRLEN             5 0
     C     1             CHAIN(N)  CNFF                               90
     C     *IN90         IFEQ      '0'
     C                   Z-ADD     CNTFSR        QWAIT             5 0
     C                   END
     C     WWUSER        CHAIN     LOCL01                             90
     C                   MOVE      'EQ'          QORDER            2
     C                   Z-ADD     9             QKILEN            3 0
     C                   MOVEL     *BLANKS       QKEY              9
     C                   MOVEL     *BLANKS       QBACK             9
     C                   MOVEL     *BLANKS       QTFIEL            9
     C                   Z-ADD     10            QSNDRL            3 0
     C                   MOVEL     *BLANKS       QSNDR            10
     C                   Z-ADD     0             AWBNO
      *____________________________________________________________
      * Dummy read of FSR00F, FSR03F and LOGF
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *ON           *IN99
     C  N99
     CAN 99              READ      FSR00F                                 90
     C  N99
     CAN 99              READ      LOGF                                   90
      *
     C                   ENDSR
      *
      *
      *
      *****************************************************************
     C     SNDRQS        BEGSR
      * Send FSR status request
      *****************************************************************
      *____________________________________________________________
      * Retrieve last used recordnumber from CNFF and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     CNFF                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CNRECN
     C                   UPDATE    CNFR
     C                   CLEAR                   LOGR
     C                   Z-ADD     CNRECN        S00REC
     C                   Z-ADD     CNRECN        LGRECN                         <LOGF
     C                   Z-ADD     CNRECN        LGTRID                         <LOGF
      *____________________________________________________________
      * Retreive data from local file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     LOID          LGSID                          <LOGF
     C                   MOVEL     DEFADR        LGID
      *____________________________________________________________
      * Write record to FSR master file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   WRITE     FSR00R
      *
      *____________________________________________________________
      * Write record to LOG file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     *BLANKS       LGIDNE
     C                   MOVEL     'FSR'         LGMSID
     C                   MOVEL     *BLANKS       LGSNET
     C                   MOVEL     'T'           LGTR
     C                   MOVEL     'A'           LGSTAT
     C                   MOVEL     UDATE         LGDATE
     C                   TIME                    LGTIME
      *
     C                   WRITE     LOGR
      *_____________________________________________________________
      * Send record to dataq SMADTATQ and wait for reply in SMADTARQ
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     LGRECN        QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QTNAME
     C                   PARM                    QTLIB
     C                   PARM                    QTLEN
     C                   PARM                    QTFIEL
      *
     C                   END                                                    <*IN90
      *
     C                   ENDSR
      *
      *****************************************************************
     C     VALSR         BEGSR
      * Validate fields in control format
      *****************************************************************
      *____________________________________________________________
      * Find Participant ID based on airline prefix
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     DEFADR        IFEQ      *BLANKS
     C     S0211         CHAIN     ISUL01                             90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     ISPART        DEFADR
     C                   ENDIF                                                  <*IN90
     C                   ENDIF                                                  <WDEFAD
      *____________________________________________________________
      *____________________________________________________________
      * Check that participant is valid
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     DEFADR        SETLL     PARL01                                 90
      *
     C                   ENDSR
      *
