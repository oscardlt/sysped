********************************************************************
      * GENERERE FAX  FOR VARSLING OM HENDELSE (T&T) PÅ SENDING
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 VED INNLAND-OGSÅ SENDINGSNUMMER, GODSNR KUN HVIS UTFYLT
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 02 JOV PTF11 Send NORSK/Local Lang tekst, Ved TTEDEV='*LE' send Local + Engelsk
      *              OBS! Alle ESTSTT.. + SYTT01 (+ SYTT05)/SYSO02  bør oppdateres samtidig
********************************************************************
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FESTSTTFP  O    E             PRINTER
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
     D EMNE            S             70
     D POS             S             15
     C                   exsr      INIT
      *
     c     HeaKey        chain     HEADF                              35
     c     *in35         ifeq      *off
     c                   exsr      SndMail
     c                   endif
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *
      ******************************************************************************
     C     SndMail       BEGSR
      ******************************************************************************
      *  Prtf-definisjon har samme rcd/felt-navn som  ARKSRC for å få likhet med mail-prog
      *
     C                   WRITE     Header
      *
     C                   eval      SRCDTA = 'MELDING FRA ' + %TRIM(FIFT)
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'OM HENDELSE PÅ DIN '
     C                             + 'SENDING MED SENDERS/OPPDR.GIVERS REF: '
     C                             + HERFA
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'På din sending (se under) er det '+
     C                             'nylig registrert følgende hendelse/info:'
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '----------------------------------'
     C                                    + '----------------------------------'
     C                                    + '-----'
     C                   WRITE     arcdat
      *
N02  C                   eval      SRCDTA = TTTEXL
N02  C                   WRITE     arcdat
      *
N02  c                   if        TTEDEV = '*LE' and TTTEXT <> *blanks
     C                   eval      SRCDTA = TTTEXT
     C                   WRITE     arcdat
N02  c                   endif
      *
     C                   eval      SRCDTA = '----------------------------------'
     C                                    + '----------------------------------'
     C                                    + '-----'
     C                   WRITE     arcdat
      *
     C                   movel     TTTIME        TTTIM4            4 0
     C                   eval      SRCDTA = 'Hendelses-Dato/Tid: '
     C                             + %trim(%editw(TTDATE:'    .  .  '))
     C                             + '   Kl: ' + %trim(%editw(TTTIM4:'  :  '))
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '*** Sendingsdetaljer *** : '
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Vår ref:  '+
     C                             %trim(%editc(HEAVD:'Z'))+'/'+
     C                             %trim(%editc(HEOPD:'Z'))+'/'+HESG
     C                   WRITE     arcdat
      *
N01   *
N01  c     HXSNDN        ifne      *blanks
N01  C                   move      *blanks       SNDN17           17
N01  C                   IF        (%SUBST(HXSNDN:1:3)='401') AND
N01  C                             (%SUBST(HXSNDN:20:1)<>' ')
N01  C                   eval      SNDN17=%SUBST(HXSNDN:4:17)
N01  c                   else
N01  C                   eval      SNDN17=HXSNDN
N01  c                   endif
N01  C                   eval      SRCDTA = 'Send.nr:  ' + SNDN17
N01  C                   WRITE     arcdat
N01  c                   endif
N01   *
N01  C                   IF        %SUBST(HEGN:5:1) <> *BLANKS
     C     HEPOS2        IFEQ      *ZEROS
     C                   EVAL      POS = HEPOS1
     C                   ELSE
     C                   EVAL      POS = %trim(HEPOS1) +'/' +
     C                             %trim(%editc(HEPOS2:'Z'))
     C                   END
      * ...
     C                   eval      SRCDTA = 'Godsnummer: ' + HEGN +
     C                             '  Posisjon: ' + POS
     C                   WRITE     arcdat
N01  c                   endif
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Avs.: ' + HENAS
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADS1
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADS2
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADS3
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Mot.: ' + HENAK
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADK1
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADK2
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADK3
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Varebeskrivelse: ' +
     C                             %trim(%editc(HENT:'Z')) + ' ' +
     c                             %trim(HEVS1) +' '+ %trim(HEVS2)
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Vekt: ' +
     C                             %trim(%editc(HEVKT:'Z'))
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     C                   WRITE     arcdat
      *
      *
     C                   eval      SRCDTA = 'Denne melding er maskinelt/'
     C                             +'automatisk sendt ut fra '
     C                             + FIFT
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Evt svar på faksen blir '
     C                             +'IKKE lest.'
     C                   WRITE     arcdat
      *
     C                   ENDSR
      *
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *entry        PLIST
     c                   parm                    UUAVD             4
     c                   parm                    UUOPD             7
     c                   parm                    TTACTI            3
     c                   parm                    UUDATE            8
     c                   parm                    UUTIME            6
     c                   parm                    UUDATL            8
     c                   parm                    UUTIML            6
N02  c                   parm                    TTEDEV            3
N02  c                   parm                    TTTEXL           70
     c                   parm                    TTTEXT           70
     c                   parm                    TTUSER           10
     c                   parm                    CCONTA           30
     c                   parm                    FAX              21
      *
     c                   move      UUAVD         HEAVD
     c                   move      UUOPD         HEOPD
     c                   move      UUDATE        TTDATE            8 0
     c                   move      UUTIME        TTTIME            6 0
     c                   move      UUDATL        TTDATL            8 0
     c                   move      UUTIML        TTTIML            6 0
      *
     c     HeaKey        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
      *
     c     *loval        setll     FIRM
     c                   read      FIRM
     C                   ENDSR
