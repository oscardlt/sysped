      **
      **    OPPDATERE TRANSPORTØRKREDITNOTA
      **    I STATISTIKK / LEGGE KOSTN.ANDEL PÅ OPPDRAG
      **
*****F**************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnrF*Sek Sig Vers Beskrivelse...........................
"""""F*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0607F* 01 JOV PTF6 KRAFTIG OMSKRIVING, DIREKTE SLETTING/ENDRING UTEN
      *             MERKING (SE V6...).    LEGGER NÅ UT TALL SOM ER
      *             BRUTT NED PÅ AVD/OPD BASERT PÅ TRAVV-INFO(%BASERT)
      *             KUN NÅR DETTE IKKE SAMSVARER MED TURTOTAL FORDELES
      *             DIFFEERANSEN PÅ OPPDRAGENE (OG DA E. SAMME NØKKEL)
      *             GODSKRIVING/TREKK STAT.FORDELES OGSÅ.
      *             ENTEN RETT PÅ AVD/OPPD ELLER VIA FVEKT PÅ TUR..
      *             SENERE JUSTERINGER FANGES OGSÅ INN PÅ DETTE VIS
*****F**************************************************************
     FTRAN      IF   E           K DISK
     FTRAVH3    IF   E           K DISK
     FTRAVV5    IF   E           K DISK
     FWFAKTOR   UF A E           K DISK
     FFAKT      IF A E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEA11R)
     FKOSBU     IF   E           K DISK
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D AVRTXD          C                   CONST('*AVR*DIREKTE KOSTN. ')
     D AVRTXF          C                   CONST('*AVF*FORDELT KOSTN. ')
     D AVRTGD          C                   CONST('*AGD*GODSKR DIREKTE ')
     D AVRTGF          C                   CONST('*AGF*GODSKR FORDELT ')
     C                   EXSR      INIT
     C*
     C     'O'           SETLL     TRAVH3
     C     'O'           READE     TRAVH3                                 30
     C     *IN30         DOWEQ     '0'
     C     THTAVD        IFNE      *ZEROS
     C     THFAKT        ANDNE     *ZEROS                                       IKKE KODE 3!
     C                   EXSR      HOVED
     C                   END
     C* FLERE ??
     C     'O'           READE     TRAVH3                                 30
     C  N30              END
     C*
     C* REGENERER TREKK/GODSKRIVINGER....
     C     *LOVAL        SETLL     TRAN
     C                   READ      TRAN                                   30
     C     *IN30         DOWEQ     '0'
     C*ALLE      VMINCR    IFEQ 0
     C                   MOVE      *ZEROS        THTAVD
     C                   MOVE      VMTRAN        THTUNR
     C                   MOVE      'O'           XXTVST
     C                   EXSR      GODSKR
     C*ALLE                END
     C                   READ      TRAN                                   30
     C  N30              END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     HOVED         BEGSR
     C******************************************
     C                   MOVE      ' '           XXTVST
     C                   MOVE      'N'           DIFFJN            1
     C                   MOVE      *ZEROS        TOTBEL           13 2
     C                   MOVE      *ZEROS        OPDBEL           13 2
      * FLAGG OM AT BELØP ER AVTALT PÅ TUR (DIREKTE BELASTN. ER DA
      * IKKE HELT DIREKTE LIKEVEL, MEN "SLIK DET VILLE BLITT VED AVR.")
     C                   MOVE      ' '           TURAVT            1
     C     THTOT         IFEQ      THAVT
     C                   MOVE      'A'           TURAVT
     C                   END
      * LES DETALJ-LINJER, LEGG UT BRUDD PR. AVD/OPPD
     C     TRAV5K        SETLL     TRAVV5
     C     TRAV5E        READE     TRAVV5                                 94
     C                   MOVE      TVOAVD        GMOAVD
     C                   MOVE      TVOPD         GMOPD
     C     *IN94         DOWEQ     '0'
      * VED BRUDD. LEGG UT LINJE
     C     TVOAVD        IFNE      GMOAVD
     C     TVOPD         ORNE      GMOPD
     C                   EXSR      DETLIN
     C                   END
     C                   ADD       TVBLL         OPDBEL
     C                   MOVE      TVOAVD        GMOAVD
     C                   MOVE      TVOPD         GMOPD
     C     TRAV5E        READE     TRAVV5                                 94
     C                   END
      * VED AVSLUTTET LESELOOP, LEGG UT SISTE BRUDD-SUM
     C     OPDBEL        IFNE      *ZEROS
     C                   EXSR      DETLIN
     C                   END
      *
      * LEGG UT EVT DIFF MELLOM FORDELT, THTOT
     C                   EXSR      DETLI2
      *
     C                   ENDSR
      *
     C***********************************************
     C     DETLIN        BEGSR
     C***********************************************
      * HENT KUNDE SOM STATISTISK SKAL BELASTES (PRIMÆRT FAKT-LIN 00)
     C     FAKKEY        CHAIN     FAKT                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      FASK          XXSK              1
     C                   MOVE      FAKUNR        XXKUND           80
     C                   ELSE
     C     HEAKEY        CHAIN     HEADF                              33
     C     HEKNSF        IFNE      *ZEROS
     C                   MOVE      'S'           XXSK              1
     C                   MOVE      HEKNSF        XXKUND           80
     C                   ELSE
     C                   MOVE      'K'           XXSK              1
     C                   MOVE      HEKNKF        XXKUND           80
     C                   END
     C     TRSLAG        IFNE      'HO'
     C                   MOVE      'I'           XXSK              1
     C                   END
     C                   END
      *
     C                   CLEAR                   FAKTR
     C                   MOVE      GMOAVD        FAAVD
     C                   MOVE      GMOPD         FAOPD
     C                   MOVE      *ZEROS        FALI
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   MOVE      'FRA'         FAVK
     C     THTAVD        IFEQ      *ZEROS                                       GODSKR..
     C     DIFFJN        IFEQ      'N'
     C                   MOVE      AVRTGD        FAVT
     C                   ELSE
     C                   MOVE      AVRTGF        FAVT
     C                   END
     C                   ELSE                                                   HOVEDTUR..
     C     DIFFJN        IFEQ      'N'
     C                   MOVE      AVRTXD        FAVT
     C                   ELSE
     C                   MOVE      AVRTXF        FAVT
     C                   END
     C                   MOVE      TURAVT        FAVT
     C                   END
     C                   MOVE      XXSK          FASK
     C                   MOVE      XXKUND        FAKUNR
     C                   MOVE      'K'           FAKDA
     C                   MOVE      'NOK'         FAVAL
     C                   Z-SUB     OPDBEL        FABELN
     C                   Z-SUB     OPDBEL        FABELV
     C                   MOVE      THKDM         FAKDM
     C                   Z-ADD     THFAKT        FAFAKT
     C                   Z-ADD     THDATO        FADATO
     C                   Z-ADD     THDAFF        FADAFF
     C                   MOVE      'O'           FAOPKO
     C                   Z-ADD     THCC          FACC
     C                   Z-ADD     THÅR          FAÅR
     C                   Z-ADD     THMND         FAMND
     C                   Z-ADD     THJOUR        FAJOUR
      *
     C                   WRITE     FAKTR
      *
      * LEGG UT FAKTOR (ANDEL AV TOTAL I W-FILE...)
      * (MEN KUN VED "HOVED-KJØRING" FOR TUREN, IKKE VED DIFF...)
     C     DIFFJN        IFEQ      'N'
     C     THTAVD        ANDNE     *ZEROS
     C                   MOVE      FAAVD         WFAVD
     C                   MOVE      FAOPD         WFOPD
     C*          OPDBEL    DIV  THTOT     WFFAKT           ( / THTOT!!)
     C                   Z-ADD     OPDBEL        WFBELØ
     C                   WRITE     WFAKTORR
     C                   ADD       OPDBEL        TOTBEL           13 2
     C                   END
      *
     C                   MOVE      *ZEROS        OPDBEL           13 2
      *
     C                   ENDSR
     C*
     C***********************************************
     C     DETLI2        BEGSR
     C***********************************************
     C* DIFF MELLOM FORDELT/THTOT - REFORDEL ETTER SAMME FAKTOR...
     C                   MOVE      'J'           DIFFJN            1
     C     THTOT         SUB       TOTBEL        DIFF             13 2
     C     THTOT         SUB       TOTBEL        DIFF2            13 2
     C     DIFF          IFNE      *ZEROS
      *
     C     WFUNIK        SETLL     WFAKTOR
     C     WFUNIK        READE     WFAKTOR                                33
     C     *IN33         DOWEQ     '0'
     C     WFBELØ        DIV       TOTBEL        FAKTOR           19 9
     C     DIFF          MULT      FAKTOR        OPDBEL
     C                   SUB       OPDBEL        DIFF2
     C     DIFF2         IFLE      1
     C     DIFF2         ANDGE     -1
     C                   ADD       DIFF2         OPDBEL
     C                   MOVE      *ZEROS        DIFF2
     C                   END
     C                   MOVE      WFAVD         GMOAVD
     C                   MOVE      WFOPD         GMOPD
     C                   EXSR      DETLIN
     C                   DELETE    WFAKTORR
     C     DIFF2         CABEQ     *ZEROS        UTDIFF
     C     WFUNIK        READE     WFAKTOR                                33
     C                   END
     C                   END
     C*
     C     UTDIFF        TAG
     C                   EXSR      DELWF
     C                   ENDSR
     C***********************************************
     C     DELWF         BEGSR
     C***********************************************
     C     WFUNIK        SETLL     WFAKTOR
     C     WFUNIK        READE     WFAKTOR                                33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    WFAKTORR
     C     WFUNIK        READE     WFAKTOR                                33
     C                   END
     C                   ENDSR
     C******************************************
     C     GODSKR        BEGSR
     C******************************************
      * LES DETALJ-LINJER, LEGG UT BRUDD PR. AVD/OPPD
     C     TRAV5E        SETLL     TRAVV5
     C     TRAV5E        READE     TRAVV5                                 94
     C     *IN94         DOWEQ     '0'
     C     TVFAKT        IFNE      *ZEROS
     C                   Z-ADD     TVBLL         OPDBEL
      * FINN MOMSKODE M.M. VIA KOSBU
     C     TVBNRB        CHAIN     KOSBU                              33
     C                   MOVE      BUKDM         THKDM
     C     THKDM         IFNE      'J'
     C     THKDM         ANDNE     'N'
     C     BUTUNR        IFEQ      *ZEROS
     C     BUOPD         ANDEQ     *ZEROS
     C                   MOVE      'N'           THKDM
     C                   ELSE
     C                   MOVE      'J'           THKDM
     C                   END
     C                   END
     C     TVOPD         IFNE      *ZEROS
     C                   MOVE      TVOAVD        GMOAVD
     C                   MOVE      TVOPD         GMOPD
     C                   MOVE      'N'           DIFFJN            1
     C                   EXSR      DETLIN
     C                   ELSE
      * JUSTERING HENFØRT TIL TUR...
     C     BUTUNR        IFNE      *ZEROS
     C                   EXSR      TURLIN
     C                   END
     C                   END
     C                   END
     C     TRAV5E        READE     TRAVV5                                 94
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     TURLIN        BEGSR
     C***********************************************
      * SIMULERER EN DIFF ETTER NORMALFORDELING....
      * BRUK WFIL "WFAKTOR" FOR Å FORDELE ETTER F.VEKT.
     C                   MOVE      *ZEROS        TOTBEL
     C     BUTUNR        SETLL     HEAD11
     C     BUTUNR        READE     HEAD11                                 33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      HEAVD         WFAVD
     C                   MOVE      HEOPD         WFOPD
     C                   Z-ADD     HEFBV         WFBELØ
     C                   WRITE     WFAKTORR
     C                   ADD       HEFBV         TOTBEL
     C     BUTUNR        READE     HEAD11                                 33
     C                   END
      *
      * "TOTBEL" ER NÅ SAMLET VEKT.
      * VED Å TRIKSE INN AKTUELT BELØP PÅ TOPPEN VIL SR "DETLI2" KUNNE
      * KJØRES UFOREANDRET (DENNE FINNER DIFF VED THTOT MINUS TOTBEL!)
     C     TOTBEL        IFNE      *ZEROS
     C     TOTBEL        ADD       TVBLL         THTOT
     C                   EXSR      DETLI2
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      * TRAVV M/NULL I AVD/OPD SKAL IKKE LESES (REFORDELES SOM "DIFF")
     C     TRAV5K        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    XXTVST            1
     C                   KFLD                    XXOAVD
     C     *LIKE         DEFINE    TVOAVD        XXOAVD
     C                   Z-ADD     1             XXOAVD
     C     TRAV5E        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    XXTVST            1
     C     FAKKEY        KLIST
     C                   KFLD                    GMOAVD
     C                   KFLD                    GMOPD
     C                   KFLD                    XXLI
     C     *LIKE         DEFINE    FALI          XXLI
     C     HEAKEY        KLIST
     C                   KFLD                    GMOAVD
     C                   KFLD                    GMOPD
     C     *LIKE         DEFINE    FAAVD         GMOAVD
     C     *LIKE         DEFINE    FAOPD         GMOPD
     C                   MOVE      ZJOBNR        WFUNIK
     C                   EXSR      DELWF
     C                   ENDSR
