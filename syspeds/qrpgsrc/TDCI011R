      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCI011R MODULE(*LIBL/TDCI011R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKNV      IF   E           K DISK    USROPN
     FDKNVCN    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D LIN             S              5
     D LINN            S              5  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D                 DS
     D  TVCNR1                 1     17
     D  TVCNR2                18     34
     D  TVCNR3                35     51
     D  TVCNR4                52     68
     D  TVCNR5                69     85
     D  CN                     1     85    DIM(5)
     D                 DS
     D  TVST                   1     35
     D  TVSTSK                36     37
     D  TVSTLK                38     39
     D  TVCTL                 40     40  0
     D  TVFLG                 41     41  0
     D  TVINF1                42    111
     D  TVINF2               112    181
     D  TVINF3               182    251
     D  TVINF4               252    321
     D  TVINSK               322    323
     D  TVGDT                324    331  0
     D  TVGM                 332    366
     D  TVGMSK               367    368
     D  TVGMST               369    403
     D  TVGMSS               404    405
     D  TVGMLK               406    407
     D  TVDANT               408    410  0
     D  TVDFKD               411    430
     D  TVDFSK               431    432
     D  TVTAID               433    459
     D  TVTASK               460    461
     D  TVTALK               462    463
     D  TVGODT               464    471  0
     D  TVOM                 472    506
     D  TVOMSK               507    508
     D  TVOMST               509    543
     D  TVOMSS               544    545
     D  TVOMLK               546    547

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(LINN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ¬oneline¬ : [';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Hæmta detta ena ærendet.

     C     Error         IFEQ      *blanks
     C                   MOVE      *BLANKS       CN
     C     DKNVKey       CHAIN     DKNV                               55
     C     *IN55         IFEQ      '0'
     C                   MOVE      TVCNR         CN(1)
      *
     C                   Z-ADD     1             XN                3 0
     C     DKNVKey       SETLL     DKNVCN
     C     DKNVKey       READE     DKNVCN                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN                3 0
     C                   MOVE      TVCNR         CN(XN)
     C     DKNVKey       READE     DKNVCN                                 33
     C                   ENDDO
      *
      *
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tvavd¬ : ¬'+%trim(%editc(TVAVD:'Z'))+'¬, '
          +'¬tvtdn¬ : ¬'+%trim(%editc(TVTDN:'Z'))+'¬, '
          +'¬tvli¬ : ¬'+%trim(%editc(TVLI:'Z'))+'¬, '
          +'¬tvst¬ : ¬'+%trim(TVST)+'¬, '
          +'¬tvstsk¬ : ¬'+%trim(TVSTSK)+'¬, '
          +'¬tvstlk¬ : ¬'+%trim(TVSTLK)+'¬, '
          +'¬tvctl¬ : ¬'+%trim(%editc(TVCTL:'4'))+'¬, '
          +'¬tvflg¬ : ¬'+%trim(%editc(TVFLG:'4'))+'¬, '
          +'¬tvinf1¬ : ¬'+%trim(TVINF1)+'¬, '
          +'¬tvinf2¬ : ¬'+%trim(TVINF2)+'¬, '
          +'¬tvinf3¬ : ¬'+%trim(TVINF3)+'¬, '
          +'¬tvinf4¬ : ¬'+%trim(TVINF4)+'¬, '
          +'¬tvinsk¬ : ¬'+%trim(TVINSK)+'¬, '
          +'¬tvgdt¬ : ¬'+%trim(%editc(TVGDT:'4'))+'¬, '
          +'¬tvgm¬ : ¬'+%trim(TVGM)+'¬, '
          +'¬tvgmsk¬ : ¬'+%trim(TVGMSK)+'¬, '
          +'¬tvgmst¬ : ¬'+%trim(TVGMST)+'¬, '
          +'¬tvgmss¬ : ¬'+%trim(TVGMSS)+'¬, '
          +'¬tvgmlk¬ : ¬'+%trim(TVGMLK)+'¬, '
          +'¬tvdant¬ : ¬'+%trim(%editc(TVDANT:'4'))+'¬, '
          +'¬tvdfkd¬ : ¬'+%trim(TVDFKD)+'¬, '
          +'¬tvdfsk¬ : ¬'+%trim(TVDFSK)+'¬, '
          +'¬tvtaid¬ : ¬'+%trim(TVTAID)+'¬, '
          +'¬tvtask¬ : ¬'+%trim(TVTASK)+'¬, '
          +'¬tvtalk¬ : ¬'+%trim(TVTALK)+'¬, '
          +'¬tvgodt¬ : ¬'+%trim(%editc(TVGODT:'4'))+'¬, '
          +'¬tvom¬ : ¬'+%trim(TVOM)+'¬, '
          +'¬tvomsk¬ : ¬'+%trim(TVOMSK)+'¬, '
          +'¬tvomst¬ : ¬'+%trim(TVOMST)+'¬, '
          +'¬tvomss¬ : ¬'+%trim(TVOMSS)+'¬, '
          +'¬tvomlk¬ : ¬'+%trim(TVOMLK)+'¬, '
          +'¬tvcnr¬ : ¬'+%trim(TVCNR1)+'¬, '
          +'¬tvcnr2¬ : ¬'+%trim(TVCNR2)+'¬, '
          +'¬tvcnr3¬ : ¬'+%trim(TVCNR3)+'¬, '
          +'¬tvcnr4¬ : ¬'+%trim(TVCNR4)+'¬, '
          +'¬tvcnr5¬ : ¬'+%trim(TVCNR5)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONstring =']}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKNV
     C  N50              CLOSE     DKNVCN

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   EVAL      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
     C                   EVAL      LIN     = zhbgetvar('LIN')
     C                   EVAL      LINN    = c2n2(LIN)
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     C     DKNVKey       klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
     C                   kfld                    LINN

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      DKNV
     C  N50              OPEN      DKNVCN

     C                   ENDSR
