********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 CB  PTF11 LES FORBI LF
11XXX * 02 CB  PTF11 Tillat lengre ref i UNH
********************************************************************
     FEDI85F1K  IF   E             DISK
     FEDIM5     IF   E           K DISK
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
     D  AA                     1   1024
     D                                     DIM(1024)
     D  AADS                   1   1024
     D                 DS
     D  AB                     1   1024
     D                                     DIM(1024)
     D  ABDS                   1   1024
     D                 DS
     D  M0065                  1      6
     D  X0065                  1      3
     C*___________
     C* HODELOGIKK
     C*"""""""""""
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  NLR              EXSR      GETSEG
     C   LR              GOTO      SLUTT
     C     SEGNAM        CASEQ     'UNH'         SRUNH
     C                   END
     C                   GOTO      SEGSTA
     C     SLUTT         TAG
     C*
     C*__________________________________________________________
     C* SR for initiering                                  SRINIT
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C* Parametre fra CL= MESSAGE TYPE OG MESSAGE NA,E
     C     *ENTRY        PLIST
     C                   PARM      W0065         W0065
      * INIT AV ARBEIDSFELT
     C                   MOVE      'N'           OK                1            UNB--> J    elt.
     C                   MOVE      *ZEROS        NUMVRD           15 0          Gener. num felt.
     C                   MOVE      *ZEROS        NUTEGN            1 0          "TEGN" numerisk rep.
     C                   MOVE      *BLANKS       SEGNAM            3            Segment navn
     C                   MOVE      *BLANKS       W0065             6            MSG TYPE
N01  C                   MOVE      X'25'         XLF               1
     C                   READ      EDI85F1K                               LR
     C   LR              GOTO      ENDINI
     C                   MOVE      RDATA         ABDS
     C     AB(1)         IFEQ      'ø'
     C     1             DO        2
     C                   READ      EDI85F1K                               LR
     C   LR              GOTO      ENDINI
     C                   END
     C                   END
     C*
     C                   MOVE      RDATA         ABDS
     C                   Z-ADD     1025          TN                4 0
     C                   MOVE      'NO '         EOF               3             * FLAGG FOR EOF
     C                   MOVE      ' '           TEGN              1
     C                   MOVE      ' '           LOKAHE            1
     C*
     C* Spesielt reserverte tegn defineres her med både grafisk verdi og
     C* med en logisk verdi. Den grafiske verdien testes det mot i rutinen
     C* som henter tegn. Finner denne rutinen at at tegnet er ment som en
     C* spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
     C* (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
     C* innkommende datastroemmen). '?' når brukt som opphevelse foran annet
     C* spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
     C* kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.
     C*
     C* Ved andre standarder for spesialtegn er det bare å bytte verdiene til
     C* de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
     C* Logiske verdier for spesialtegn
     C* LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
     C* HVIS KOMMUNEDATA, INITIERE MED LOGAPP (GÅ RETT PÅ UNB)
     C                   MOVE      LOGAPP        TEGN
     C* LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   BITON     '16'          LOGPLU            1
     C                   BITOFF    '023457'      LOGPLU
     C* LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   BITON     '167'         LOGKOL            1
     C                   BITOFF    '02345'       LOGKOL
     C*
     C     ENDINI        ENDSR
     C*
     C*__________________________________________________________
     C* SR for å hente neste tegn i datastroem             GETCH
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETCH         BEGSR
     C* Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
     C* i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
     C* LR settes når en ved "Toemming" finner at AB-arrayet er tomt.
     C*
     C* "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
     C*Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
     C*    98 ON -> kun grafisk betydning (uforandret)
     C*    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   SETOFF                                           98
     C     STACH         TAG
     C* Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      1025
     C     EOF           IFEQ      'YES'
     C                   SETON                                            LR
     C                   GOTO      ENDCH
     C                   END
     C                   MOVE      ABDS          AADS
     C  N99              READ      EDI85F1K                               99     *hent ny til AB
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF                             * FLAGG FOR EOF
     C                   Z-ADD     1             TN                              * Tegn-index-nr
     C                   END
     C* Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      1024
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   END
     C* Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
     C* Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
     C*        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
     C* Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
     C* foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   END
     C                   END
     C                   END
     C*
     C     ENDCH         ENDSR
     C*__________________________________________________________
     C* SR for å hente neste Segment                       GETSEG
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETSEG        BEGSR
     C* "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   ENDDO
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
N01  C     TEGN          OREQ      XLF
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
     C* Har nå lest appostrof og kansje blank- de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   LR              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C                   MOVEL     FELT          SEGNAM
     C* La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
     C* Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
     C*__________________________________________________________
     C* SR for å behandle UNH-segmentet                    SRUNH
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUNH         BEGSR
     C                   MOVEA     *BLANKS       FE
S02  C*    1             DO        15            NR
N02  C     1             DO        35            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          IFEQ      LOGPLU
     C                   GOTO      UNH010
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     UNH010        TAG
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        6             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          W0065
      *
     C     W0065         IFEQ      'CUSDEC'
     C     W0065         OREQ      'CUSRES'
      *
      * Lese forbi ':1:902:UN:'
     C     1             DO        10
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C                   END
      *
      * Lese NEP-I eller NODI
     C                   MOVE      *BLANKS       FE
     C     1             DO        5             NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDUNH
     C     TEGN          IFEQ      LOGPLU
     C                   GOTO      UNH011
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     UNH011        TAG
     C                   MOVEL     FELT          X0057             5
      *
     C     X0057         IFEQ      'NEP-I'
     C                   MOVE      'IMPORT'      W0065
     C                   ELSE
      *
     C     X0057         IFEQ      'NEP  '
     C                   MOVE      'EXPORT'      W0065
     C                   ELSE
     C                   EXSR      TSTVID
     C                   END
      *
     C                   END
      *
     C                   END
      *
     C     ENDUNH        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C                   ENDSR
     C*__________________________________________________________
     C* MIDLETIG SUBRUTINE
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TSTVID        BEGSR
     C     TVIM5K        KLIST
     C                   KFLD                    M0068
      *
     C                   MOVE      *BLANKS       FE
     C     1             DO        22            NR
     C                   EXSR      GETCH
     C   LR              GOTO      ENDTST
     C     TEGN          IFEQ      LOGAPP
     C     TEGN          OREQ      LOGPLU
     C                   GOTO      STTST0
     C                   END
     C                   MOVE      TEGN          FE(NR)
     C                   END
     C     STTST0        TAG
     C                   MOVEL     FELT          M0068
      *
     C     TVIM5K        SETLL     EDIM5                              33
     C     STTST         TAG
     C     TVIM5K        READE     EDIM5                                  33
     C  N33MSR           IFEQ      'S'                                          1<-B
     C     M1001         IFEQ      '830'                                         2<-B
     C                   MOVE      'EXPORT'      W0065
     C                   ELSE                                                    2
     C     M1001         IFEQ      '929'                                          3<-B
     C                   MOVE      'IMPORT'      W0065
     C                   ELSE                                                     3
     C     X0065         IFEQ      'CUS'                                           4<-B
     C                   MOVE      'EXPORT'      W0065
     C                   ELSE                                                      4
     C     X0065         IFNE      'IFT'                                            5<-B
     C                   MOVE      'IMPORT'      W0065
     C                   ELSE                                                       5
     C                   GOTO      STTST
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   ELSE                                                   1
     C                   GOTO      STTST
     C                   END                                                    1<-E
      *
     C     ENDTST        ENDSR
