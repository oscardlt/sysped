     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EUNHJ1)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØK')
     A                                      HELP
      *
     A          R EDI66DA                   SFL
     A            SFILE     R        H
     A            SMBR      R        H
     A            MSN       R        H
     A            MMN       R        H
     A            M0004     R        H
     A            M0010     R        H
     A            M0068     R        H
     A            M1004     R        H
     A            ZOP            1A  I 11  2VALUES(' ' '5')
     A            M0065     R        O 11  5
     A            U0054     R        O 11 13
     A            M1001     R        O 11 18
     A            M0062     R        O 11 23
     A            W0068         20A  O 11 39
     A            W1004         20A  O 11 61
      *
     A          R EDI66DB                   SFLCTL(EDI66DA)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      TEXT('EDI Meldingslogg, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYEDI'
     A                                      COLOR(BLU)
     A                                  1 28MSGCON(025 EDI0232 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 69'EDI66D A'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(009 EDI0233 *LIBL/SYMSGF)
     A            S0004     R        O  3 12REFFLD(S0004 EDIS)
     A                                  3 50MSGCON(013 EDI0631 *LIBL/SYMSGF)
     A            S0020     R        O  3 64REFFLD(S0020 EDIS)
     A                                  4  2MSGCON(009 EDI0234 *LIBL/SYMSGF)
     A            S0010     R        O  4 12REFFLD(S0010 EDIS)
     A                                  5  2'Testind :'
     A            S0035     R        O  5 12REFFLD(S0035 EDIS)
     A                                  5 23MSGCON(009 EDI0237 *LIBL/SYMSGF)
     A            S0036     R        O  5 33REFFLD(S0036 EDIS)
     A                                      EDTCDE(4)
     A                                  5 44MSGCON(019 EDI0238 *LIBL/SYMSGF)
     A            SSR       R        O  5 64REFFLD(SSR EDIS)
     A                                  6  2'Status  :'
     A            SST       R        O  6 12REFFLD(MST)
     A                                  6 23MSGCON(009 EDI0240 *LIBL/SYMSGF)
     A            SDT       R        O  6 33REFFLD(SDT EDIS)
     A                                  6 44MSGCON(019 EDI0241 *LIBL/SYMSGF)
     A            STM       R        O  6 64REFFLD(STM EDIS)
     A                                      EDTWRD('0 :  :  ')
     A                                  7  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  7 69SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  8  4MSGCON(007 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10  1MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  5MSGCON(004 EDI0626 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 13MSGCON(003 EDI0627 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 18MSGCON(003 EDI0628 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 23MSGCON(009 EDI0191 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 39MSGCON(020 EDI0629 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 61MSGCON(015 EDI0630 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R EDI66DC
     A                                      OVERLAY
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 28MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI66DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
