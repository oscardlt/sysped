     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/SYPROD)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A* MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(*DFT 15 72 *NOMSGLIN)
     A          R BILDE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                  1  2'OBS!!  '
     A                                      COLOR(BLU)
     A                                  1 10'MODUL / PRODUKT ER IKKE INSTALLERT'
     A                                      COLOR(WHT)
     A                                  3  2'Takk for din interesse for modulen-
     A                                      /produktet '
     A            PRKODE    R        O  3 48REFFLD(SYPRODR/PRKODE *LIBL/SYPROD)
     A                                      COLOR(WHT)
     A                                  5  2'Fullstendig produktnavn '
     A            PRTX1     R        O  5 30REFFLD(SYPRODR/PRTX1 *LIBL/SYPROD)
     A                                      COLOR(WHT)
     A                                  7  2'Modul/produktbeskrivelse '
     A            WSTXA         70   O  8  2COLOR(WHT)
     A            WSTXB         70   O  9  2COLOR(WHT)
     A            WSTXC         70   O 10  2COLOR(WHT)
     A            WSTXD         70   O 11  2COLOR(WHT)
     A                                 13  2'Ta kontakt med SYSTEMA A/S dersom -
     A                                      du er interessert i en demonstrasjo-
     A                                      n,'
     A                                 14  2'ønsker å få gjeldende priser eller-
     A                                       få annen informajon om produktet. -
     A                                        '
     A                                 15 30'Tast ENTER for å komme ut av dette-
     A                                       bildet'
     A                                      COLOR(WHT)
