********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 xxx PTF11 xxxxxxxx
********************************************************************
     FTRACKL02  UF A E           K DISK
     FFRISOK    UF A E           K DISK
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UrgStat           1
      *
     c     URGKEYTT      KLIST
     c                   kfld                    TTAVD
     c                   kfld                    TTOPD
     c                   kfld                    TTFBNR
     c                   kfld                    TTACTI
     C                   MOVE      UAVD          TTAVD
     C                   MOVE      UOPD          TTOPD
     c                   move      'URG'         TTACTI
     c     URGKEYFRI     KLIST
     c                   kfld                    FSAVD
     c                   kfld                    FSOPD
     c                   kfld                    FSKODE
     C                   MOVE      UAVD          FSAVD
     C                   MOVE      UOPD          FSOPD
     c                   move      'URG'         FSKODE
      *
      * Loggfør alle endringer i T&T:
     c     URGKEYTT      Chain     TRACKL02                           50
     c                   move      'U'           UrgStat                        Ny stat = URGENT
     c                   If        *in50=*off and %subst(TTTEXT:1:8)='*URGENT*'
     c                   move      'N'           UrgStat                        Ny stat = IKKE URG
     c                   endif
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      ZUSER         TTUSER
     c                   if        UrgStat='U'
     c                   eval      TTTEXT='*URGENT*  Prioritized goods!'
     c                   eval      TTTEXL='*URGENT*  Prioritert gods!'
     c                   else
     c                   eval      TTTEXT='NOT urgent any more, '
     c                             +'Urgent indicator switched off.'
     c                   eval      TTTEXL='IKKE urgent lenger, '
     c                             +'Prioritetsmerke slått av.'
     c                   endif
     c                   eval      TTMANU =' '
     C  N50              UPDATE    TRACKFR
     C   50              WRITE     TRACKFR
      *
      * Fri Søkevei URG - skal FINNES når URG er slått på / fjernes om den slås AV
     c     URGKEYFRI     Chain     FRISOK                             50
     c                   if        UrgStat='U'
     c                   eval      FSSOK ='*URGENT*  Prioritert gods!'
     c                   eval      FSDOKK='GL'
     C   50              WRITE     FRISOKR
     c                   else
     C  N50              DELETE    FRISOKR
     c                   endif
      *
     C                   SETON                                        LR
     C                   RETURN
