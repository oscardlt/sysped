     ftrih09    if   e           k disk
     fedim2     if   e           k disk
     fkalender  if   e           k disk
     fkalender1 if   e           k disk    rename(kalenderr:kalenderr1)
     fkalender2 if   e           k disk    rename(kalenderr:kalenderr2)
     ftr077d    cf   e             workstn
     f                                     sfile(subfil:zrecno)
     d                 ds
     d tign                          35
     d  tign15                       15    overlay(tign)
     d   tign1                        4    overlay(tign15)
     d   tign2                        3    overlay(tign15:10)
     d   tign3                        1    overlay(tign15:15)
     ‚*  Prototype for 'TR061C'
     d tr061c          pr                  extpgm('TR061C')
     d tiavd_                              like(tiavd)
     d titdn_                              like(titdn)
     d tisg_                               like(tisg)
     d xtype_                              like(xtype)
     d uflagg_                             like(uflagg)
     ‚*  Prototype for 'SAD003'
     d sad003          pr                  extpgm('SAD003')
     d tiavd_                              like(tiavd)
     d titdn_                              like(titdn)
     d win03_                              like(win03)
     ‚*  Prototype for 'TR064C'
     d tr064c          pr                  extpgm('TR064C')
     d tiavd_                              like(tiavd)
     d titdn_                              like(titdn)
     d xtype_                              like(xtype)
     d uflagg_                             like(uflagg)
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d do_x            s              7  0
     d x               s              3  0
     d gmrec           s              5  0
     d uflagg          s              1  0
     d win03           s              1
     d wsgdt1          s              8  0
     d x_adt           s              8  0
     d x_antd          s              3  0
     d x_avd           s              4  0
     d x_gdt           s              8  0
     d x_gn            s             35
     d x_tdn           s              7  0
     d x_trnr          s             18
     d xok             s              1
     d xtype           s              3
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------
     ‚*//////////////
     ‚*  Hodelogikk /
     ‚*//////////////
      /free
       exsr sra;

       dow *in03 = *off;
         exfmt bilde;
         *in97 = *off;
         *in98 = *off;
         *in99 = *off;
         if *in03 = *off;
           chain wsdtf kalender;     // test fra-dato
           if %found;
             chain wsdtt kalender;   // test til-dato
             if %found;
               if wsdtf <= wsdtt;    // test fra og til dato
                 exsr srb;           // vis NCTS
               else;
                 *in97 = *on;
               endif;
             else;
               *in98 = *on;
             endif;
           else;
             *in99 = *on;
           endif;
         endif;
       enddo;

       *inlr = *on;
       return;

       //////////////////////////////////////////////////////////////
       //  Initiering                                           SRA /
       //////////////////////////////////////////////////////////////
       begsr sra;
         wsdtf = *zeros;
         wsdtt = *zeros;
         wsadjn = 'N';

       endsr;

       //////////////////////////////////////////////////////////////
       //  Bearbeide Formater                                   SRB /
       //////////////////////////////////////////////////////////////
       begsr srb;
         //______________
         // Tømme Subfile
         //""""""""""""""
         *in05 = *on;
         dow *in05 = *on;
           *in93 = *on;
           write subctl;
           *in93 = *off;
           *in94 = *off;
           zrecno = *zero;
           //_____________
           // Fyll Subfile
           //"""""""""""""

           // Finn først post.
           setll wsdtf trih09;
           xok = 'N';
           dow xok = 'N';
             read trih09;
             *in94 = %eof;
             if *in94 = *off;
               *in94 = (wsdtt < tidt);
             endif;
             if *in94;
               *in12 = '1';
               leavesr;
             endif;
             exsr srsjdt;
           enddo;

           if *in94 = *off;
             x = 0;
             dow x < 16;
               x = x + 1;
               zrecno = zrecno + 1;
               write subfil;
               xok = 'N';
               dow xok = 'N';
                 read trih09;
                 *in94 = %eof;
                 if *in94 = *off;
                   *in94 = (wsdtt < tidt);
                 endif;
                 if *in94 = *off;
                   exsr srsjdt;
                 else;
                   xok = 'X';
                   x = 16;
                 endif;
               enddo;
             enddo;
           endif;

           if *in94 = *off;
             x_avd = tiavd;
             x_tdn = titdn;
             x_gn = tign;
             x_trnr = titrnr;
             x_gdt = wsgdt;
             x_adt = wsadt;
             x_antd = wsantd;
           endif;
           //_______________
           // Behandle bilde
           //"""""""""""""""
           dow *in03 = *off and *in12 = *off;
             *in91 = *on;
             *in92 = *on;
             write bunn;
             exfmt subctl;
             if *in03 = *on or *in12 = *on;
               leavesr;
             endif;

             if *in05 = *on;
               leave;
             endif;
             *in91 = *off;
             *in92 = *off;
             exsr hent;
           enddo;

         enddo;

       endsr;

        //***********************************************
       begsr hent;
         //**********************************************
         gmrec = zrecno;
         *in98 = *off;
         dow *in98 = *off;
           readc subfil;
           *in98 = %eof;
           if *in98 = *off;

             select;
             when wsnr = 5;
               exsr dsptrh;
             when wsnr = 9;
               exsr dsptvi;
             when wsnr = 45;
               exsr dlstrh;
             endsl;
           endif;
         enddo;

         tiavd = x_avd;
         titdn = x_tdn;
         tign = x_gn;
         titrnr = x_trnr;
         wsgdt = x_gdt;
         wsadt = x_adt;
         wsantd = x_antd;
         zrecno = gmrec;

       endsr;

       //**********************************************
       begsr srsjdt;
         //**********************************************
         xok = 'N';
         if tign3 <> ' ';
           // HENT GODSDATO
           kayear = %dec(tign1:4:0);
           kadgnr = %dec(tign2:3:0);
           chain ( kayear : kadgnr ) kalender2;
           wsgdt = kadate;

           if wsadjn = 'J';
             // HENT GODSDATO + 1 ARBEIDSDAG
             setgt wsgdt kalender1;
             read kalender1;
           else;
             // HENT GODSDATO + 1 DAG
             setgt wsgdt kalender;
             read kalender;
           endif;
           wsgdt1 = kadate;
           // HENT IE44-DATO
           wsadt = *zeros;
           setll ( tiavd : titdn ) edim2;
           reade ( tiavd : titdn ) edim2;
           dow not %eof;
             if m1n07 = '044';
               wsadt = mdt;
             endif;
             reade ( tiavd : titdn ) edim2;
           enddo;

           if wsgdt1 < wsadt or wsadt = 0;
             xok = 'J';
             // BEREGN ANTALL DAGER
             if wsadt = 0;
               wsantd = 999;
             else;
               chain wsgdt kalender;
               *in33 = not %found;
               wsantd = 0;
               dow *in33 = *off;
                 read kalender;
                 *in33 = %eof;
                 if *in33 = *off;
                   wsantd = wsantd + 1;
                   *in33 = (wsadt <= kadate);
                 endif;
               enddo;
             endif;
           endif;

         endif;

       endsr;

       //**********************************************
       begsr dsptrh;
         //**********************************************
         xtype = 'DSP';
         tr061c ( tiavd : titdn : tisg : xtype :
         uflagg );
         if uflagg = 3;
           *inlr = *on;
           return;
         endif;
         wsnr = *zeros;
         update subfil;

       endsr;

       //**********************************************
       begsr dsptvi;
         //**********************************************
         win03 = '3';
         tiavd = tiavd;
         titdn = titdn;
         sad003 ( tiavd : titdn : win03 );
         if win03 = '1';
           *inlr = *on;
           return;
         endif;
         wsnr = *zeros;
         update subfil;

       endsr;

       //**********************************************
       begsr dlstrh;
         //**********************************************
         xtype = 'DSP';
         tr064c ( tiavd : titdn : xtype : uflagg );
         wsnr = *zeros;
         update subfil;

       endsr;

      /end-free
