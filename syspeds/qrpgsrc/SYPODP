      *
CRTPG+*  CRTPGM SYSPED/SYPODP MODULE(*LIBL/SYPODP *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
      *
      *********************************************************************
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FWRKCGI    UF   E           K DISK    usropn
     FTELLGE    UF A E           K DISK    usropn
     FHeadf     UF   E           K DISK    usropn
     FHead39    UF   E           K DISK    usropn
     F                                     RENAME(HEADFR:HEADFR39)
     FDokuf7    UF   E           K DISK    usropn
     FDokuf     IF   E           K DISK    usropn
     F                                     RENAME(DOKUFR:DOKUFRx)
     FTRACKF    O  A E           K DISK    Usropn
     FTRACKT    UF A E           K DISK    Usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D user            s             10
     D WINT            s              6
     D SJNAVN          s             15
      *
     D ANTKLLN         s              4  0
     D DATON           s              6  0
     D KLOKKN          s              4  0
     D BUNTNR          s              7  0
     D ANTSND          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILTXT         s             80
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  Wkey2                  1     30
     D  SNDNR                  1     20
     D  WSSNEA                 1      3
     D  WSSN17                 4     20
     D                 DS
     D  DataDS                 1    220
     D  WDsANT                 1      4
     D  WDsSign                5     34
     D  WDsSnav               35     49
     D  WDsAvvt               50     69
     D  WDsMerk               70    114
     D  WDsDato              115    120
     D  WDsDatoD             115    116
     D  WDsDatoM             117    118
     D  WDsDatoÅ             119    120
     D  WDsKlokke            121    124
     D                 DS
     D  WDsDÅMD                1      8
     D  WDsDÅ                  1      4
     D  WDsDM                  5      6
     D  WDsDD                  7      8
     D                 DS
     D  DFMÅL                  1     17
     D  DFDTMO                 1      8
     D  DFKLMO                 9     12
     D  DFSGM                 13     17
     D                 DS
     D  TimeTmp                1     26
     D  TimeTmpÅ               1      4
     D  TimeTmpM               6      7
     D  TimeTmpD               9     10
     D  TimeTmpT              12     16
     D  TimeTmpTT             12     13
     D  TimeTmpTM             15     16
     D  TimeTmpTS             18     18
     D                 DS
     D  Idag                   1      8
     D  IdagTmpÅ               1      4
     D  IdagTmpM               5      6
     D  IdagTmpD               7      8
     D                 DS
     D  IdagTID                1      6
     D  IdagTT                 1      2
     D  IdagTM                 3      4
     D  IdagTTM                3      4
     D  IdagTS                 5      6
     D                 DS
     D  TID                    1     16
     D  Time1D                 1      2
     D  Time1P1                3      3
     D  Time1M                 4      5
     D  Time1P2                6      6
     D  Time1Å                 7     10
     D  Time1B                11     11
     D  Time1T                12     16
     D  Time1P3               14     14
     D                 DS
     D  TTTXTA                 1     70
     D  TXBY                   1     10
     D  XXSGM                 11     40
     D  TXWEBU                41     44
     D  XXWEBU                46     70
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      * Vis liste over registrete leveringer
     C                   Move      *zeros        AntSnd
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     c     *IN35         doweq     '0'
     c     Buntnr        ifne      *zeros
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     c                   exsr      UPDPOD
     c                   end
     C                   delete    WRKCGIR
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'Z')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      WINT     = zhbgetvar('WINT')
     C                   eval      WINTEG   = c2n2(WINT)
     C                   eval      SJNAVN   = zhbgetvar('SJNAVN')
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     WRKCGI
     C                   CLOSE     TELLGE
     C                   CLOSE     HEADF
     C                   CLOSE     HEAD39
     C                   CLOSE     DOKUF
     C                   CLOSE     DOKUF7
     C                   CLOSE     FIRM
     C                   CLOSE     TRACKF
     C                   CLOSE     TRACKT
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYPODP':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHTMLvar('WINT':WINT)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHTMLvar('SJNAVN':SJNAVN)
     C                   callp     updHTMLvar('BUNTNR':
     C                             %trim(%editc(BUNTNR:'Z')))
     C                   movel     timedata1     TimeTmp
     c                   movel     TimeTmpÅ      Time1Å
     c                   movel     TimeTmpM      Time1M
     c                   movel     TimeTmpD      Time1D
     c                   movel     TimeTmpT      Time1T
     c                   move      '.'           Time1P1
     c                   move      '.'           Time1P2
     c                   move      ' '           Time1B
     c                   move      ':'           Time1P3
     C                   callp     updHTMLvar('TID':TID)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     c                   movel     WData         DataDS
     C                   callp     updHTMLvar('SNDNRL':WKEY2)
     C                   move      WDsANT        ANTKLLN
     C                   callp     updHTMLvar('ANTKLLL':
     C                             %trim(%editc(ANTKLLN:'Z')))
     C                   callp     updHTMLvar('SIGNL':WDsSign)
     C                   move      WDsDato       DatoN
     C                   callp     updHTMLvar('DATOL':
     C                             %trim(%editw(DatoN:'  .  .  ')))
     C                   move      WDsKlokke     KlokkN
     C                   callp     updHTMLvar('KLOKKL':
     C                             %trim(%editw(KLOKKN:'  :  ')))
     C                   callp     updHTMLvar('AVVIKTXTL':WDsAvvt)
     C                   callp     updHTMLvar('MERKNADL':WDsMerk)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      WRKCGI
     C                   OPEN      TELLGE
     C                   OPEN      HEADF
     C                   OPEN      HEAD39
     C                   OPEN      DOKUF
     C                   OPEN      DOKUF7
     C                   OPEN      FIRM
     C                   OPEN      TRACKF
     C                   OPEN      TRACKT
      *
     C     HeadKey       KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
      *
     C     DokuKey       KLIST
     C                   KFLD                    XXRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   MOVE      'F'           XXRI              1
      *
     c     *loval        setll     firm
     c                   read      firm
      * hent buntnr (men ikke hvis sjåfør er blank / inn-ut)
     C                   MOVE      *zeros        BUNTNR
     C     SjNavn        Ifne      *blanks
     C                   Movel     'PODBUNT '    GECO
     C     GECO          Chain     TellGE                             33
     C   33              Z-add     1             GENO
     C                   MOVE      GENO          BUNTNR
     C                   add       1             GENO
     C   33              Write     TELLGER
     C  N33              Update    TELLGER
     C                   end
      *
     C                   endsr
      *
      *=====================================================================******
      *  UPDPOD
      *=====================================================================******
     C     UPDPOD        begsr
      *
      * konv. dato til ÅÅÅÅMMDD
     C                   movel     '20'          WDsDÅ
     C                   move      WDsDatoÅ      WDsDÅ
     C                   move      WDsDatoM      WDsDM
     C                   move      WDsDatoD      WDsDD
      *
      * Ren innland - oppdater på HEADF-nivå (ikke sikker det finnes f.br)
      * goto / tag erstattet med leseloop..
     c     SNDNR         Setll     HEAD39
     c     SNDNR         reade     HEAD39                                 30
     C     *in30         doweq     '0'
     C                   movel     WDsSign       HESGM
     C                   movel     WDsDÅMD       HEDTMO
     C                   movel     WDsKlokke     HEKLMO
     C                   UPDATE    HEADFR39
     C     FIURBL        IFEQ      'J'
     c                   MOVE      HEAVD         DFAVD
     c                   MOVE      HEOPD         DFOPD
     c                   MOVE      *zeros        DFFBNR
     C                   EXSR      TTSR
     C                   END                                                    1<-E
     c     SNDNR         reade     HEAD39                                 30
     c                   end
     c
     c
      * oppdater på fraktbrevsnivå.
      * (det KAN finnes flere.. Utland som bestiller av innl. tr.modul)
     c     WsSnea        IFEQ      '401'
     C                   eval      DF1004  = WSSN17
     c                   else
     C                   eval      DF1004  = SNDNR
     c                   end
     c     DF1004        setll     dokuf7
     c     DF1004        reade     dokuf7                                 30
     c     *in30         doweq     *off
     C                   movel     WDsSign       DFSGM
     C                   movel     WDsDÅMD       DFDTMO
     C                   movel     WDsKlokke     DFKLMO
     C                   UPDATE    DOKUFR
     C     FIURBL        IFEQ      'J'
     C                   movel     WDsSign       HESGM
     C                   EXSR      TTSR
     C                   END                                                    1<-E
      * sjekk om alle fraktbrev på oppdraget er utkvittert
     C                   EXSR      SjekkOpd
     c     DF1004        reade     dokuf7                                 30
     c                   end
      *
     C                   endsr
      *
     C***********************************************
     C     SjekkOpd      BEGSR
     C***********************************************
      * hvis alle fraktbrev er kvitteret (spedisjon/spredning) skal også
      * oppdrag kvitteres ut (om det ikke alt er gjort - som ved ren innl.)
     C                   MOVE      'J'           FERDIG            1
     C                   MOVE      *blanks       OPDSGM            5
     c     DokuKey       setll     DOKUF
     c     DokuKey       reade     DOKUF                                  30
     c     *in30         doweq     *off
     c     DFSGM         IFEQ      *BLANKS
     C                   MOVE      'N'           FERDIG
     c                   Goto      SluttOp
     C                   end
     c     OPDSGM        IFEQ      *BLANKS
     C                   MOVE      DFSGM         OPDSGM
     C                   end
     c     OPDSGM        IFNE      DFSGM
     C                   MOVE      '*MIX*'       OPDSGM
     C                   end
      *
     c     DokuKey       reade     DOKUF                                  30
     C                   end
      *
     C     SluttOp       Tag
     c     Ferdig        IFEQ      'J'
     c     HeadKey       Chain     Headf                              30
     c     *in30         ifeq      '0'
     c     Hesgm         IFEQ      *blanks
     C                   movel     OPDSGM        HESGM
     C                   movel     WDsDÅMD       HEDTMO
     C                   movel     WDsKlokke     HEKLMO
      * T&T på oppdragsnivå
     C     FIURBL        IFEQ      'J'
     C                   MOVE      *ZEROS        DFFBNR
     C                   EXSR      TTSR
     C                   END
     C                   end
     C                   update    headfr
     C                   end
     C                   end
     C                   endsr
      *
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C                   MOVE      DFAVD         TTAVD
     C                   MOVE      DFOPD         TTOPD
     C                   MOVE      DFFBNR        TTFBNR
     C                   MOVE      'POD'         TTACTI
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      Idag          TTDATL
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
     C                   MOVE      IdagTID       TTTIML
     C                   MOVE      WDsDÅMD       TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     WDsKlokke     TTTIME
     C                   MOVEL     'WEB'         TTUSER
     C                   MOVEL     'Signed by '  TXBY
     C                   MOVEL     WDsSign       XXSGM
     C                   MOVEL     HESGM         TTNAME
     C                   MOVEL     'WEB:'        TXWEBU
     C                   MOVE      BUNTNR        BUNTNRA           7
     C                   MOVE      *blanks       TMPWEBU
     C     BUNTNRA       CAT       '/':0         TMPWEBU          25
     C                   CAT       USER:0        TMPWEBU
     C                   CAT       '-':0         TMPWEBU
     C                   CAT       WDsSnav:0     TMPWEBU
     C                   MOVEL     TMPWEBU       XXWEBU
     C                   MOVEL     TTTXTA        TTTEXT
n01  C                   MOVEL     'Signatur: '  TXBY
n01  C                   MOVEL     TTTXTA        TTTEXL
     C                   WRITE     TRACKFR
      * Actual time of delivery i trackt
     C     DFFBNR        IFEQ      0
     c     HeadKey       chain     Trackt                             33
     C     *in33         Ifeq      *OFF
     c                   move      WDsDÅMD       TXATAD
     c                   move      WDsKlokke     TXATAK
     c                   update    tracktr
     C                   end
     C                   end
      * Avvik/merknad...
     C     WDsAvvt       Ifne      *blanks
     C     WDsMerk       Orne      *blanks
     C                   exsr      ttsr2
     c                   end
     C                   ENDSR
     C***********************************************
     C     TTSR2         BEGSR
     C***********************************************
     C                   MOVE      DFAVD         TTAVD
     C                   MOVE      DFOPD         TTOPD
     C                   MOVE      DFFBNR        TTFBNR
     C                   MOVE      'AVV'         TTACTI
     C                   MOVEL     'WEB'         TTUSER
     C                   MOVE      *blanks       TTTEXT
     C     WDsAvvt       Ifne      *blanks
     C     WDsAvvt       CAT       '/':1         TTTEXT
     C                   CAT       WDsMerk:1     TTTEXT
     c                   else
     C                   Movel     WDsMerk       TTTEXT
     c                   end
n01  C                   Movel     WDsMerk       TTTEXL
     c
     C                   WRITE     TRACKFR
     C                   ENDSR
