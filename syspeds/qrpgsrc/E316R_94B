      * SYSPED-SYEDI For WILTRACK INVOIC D.94B
      *
      * 1. SYSPED SYKS37 legger ut poster i INTFAS
      *
      * 2. Dette programmet leser fra INTFAS
      *
      * 3. Dette programmet legger ut til EDI-filer i SYEDI.
      *
      * 4. SYEDI lager automatisk EDIFACT utifra EDI-filene.
      *
     FINTFAS    UF   E           K DISK
     FEDIC      UF   E             DISK
     FCUNDL01   IF   E           K DISK
     FFAKT      IF   E           K DISK
     FGSSORL21  IF   E           K DISK
     FGSSPOF    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FBILLOF    IF   E           K DISK
     FWTTA      IF   E           K DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FEUNS      O  A E             DISK
     FECNT94B   O  A E             DISK
     FECOM94B   O  A E             DISK
     FECTA94B   O  A E             DISK
     FECUX94B   O  A E             DISK
     FEDTM94B   O  A E             DISK
     FEFII94B   O  A E             DISK
     FELIN94B   O  A E             DISK
     FELOC94B   O  A E             DISK
     FEMEA94B   O  A E             DISK
     FEMOA94B   O  A E             DISK
     FENAD94B   O  A E             DISK
     FEPAI94B   O  A E             DISK
     FEPAT94B   O  A E             DISK
     FEPRI94B   O  A E             DISK
     FEQTY94B   O  A E             DISK
     FERFF94B   O  A E             DISK
     FETAX94B   O  A E             DISK
     FETDT94B   O  A E             DISK
     FETOD94B   O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTID                   1      6  0
     D  WDATO                  7     14  0
     D                 DS
     D  WKID                   1     19
     D  WKID1                  1      8  0
     D  WKID2                  9     15  0
     D  WKID3                 16     19  0
     D                 DS
     D  XKID                   1     20
     D  XKID1                  1     19
     D  XKID2                 20     20  0
     D                 DS
     D  WSIREF                 1     12
     D  WSIREF1                1      4S 0
     D  WSIREF2                5      5    INZ('/')
     D  WSIREF3                6     12S 0
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR                                                  COM05 /
      *////////////////////////////////////////////////////////////
     C     COM05         BEGSR
     C                   Z-ADD     0             WREP2
      *_________________________________
      * Communication contact, Telephone
      *"""""""""""""""""""""""""""""""""
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         CREP2
     C                   EVAL      C3155='TE'
     C                   EVAL      C3148='+4723194800'
     C                   WRITE     COMR
      *___________________________
      * Communication contact, Fax
      *"""""""""""""""""""""""""""
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         CREP2
     C                   EVAL      C3155='FX'
     C                   EVAL      C3148='+4723194801'
     C                   WRITE     COMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CTA05 /
      *////////////////////////////////////////////////////////////
     C     CTA05         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
      *___________________
      * Purchasing contact
      *"""""""""""""""""""
     C                   MOVEL     'PD'          C3139
     C                   EVAL      C3412='Arnulf Eriksen'
     C                   WRITE     CTAR
     C                   EXSR      COM05
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     0             WREP0
      *_____________________
      * Number of line items
      *"""""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '2'           C6069
     C                   MOVEL     L1082         C6066
     C                   WRITE     CNTR
      *___________________
      * Total gross weight
      *"""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '7'           C6069
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CUX07 /
      *////////////////////////////////////////////////////////////
     C     CUX07         BEGSR
      *_________
      * Currency
      *"""""""""
     C                   CLEAR                   CUXR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     7             CGN
     C                   Z-ADD     WREP0         CREP0

     C                   EVAL      C6347A='2'
     C                   EVAL      C6345A='NOK'
     C     INVAL         IFNE      *BLANKS
     C                   EVAL      C6345A=INVAL
     C                   ENDIF
     C                   WRITE     CUXR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
      *____________
      * Invoic date
      *""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   MOVEL     '3'           D2005
     C                   MOVEL     INDATO        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
      *____________
      * Invoic date
      *""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     2             WREP0
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   MOVEL     '10'          D2005
     C                   MOVEL     HEDTOP        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM08 /
      *////////////////////////////////////////////////////////////
     C     DTM08         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_____________
      * Forfallsdato
      *"""""""""""""
     C                   MOVEL     '13'          D2005
     C                   MOVEL     INDAFF        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * EDIM0                                                EDIM0/
      *////////////////////////////////////////////////////////////
     C     EDIM0         BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     INSEID        M0004
     C                   MOVEL     INMOID        M0010
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   WRITE     EDIMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FII02 /
      *////////////////////////////////////////////////////////////
     C     FII02         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   FIIR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     2             FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
      *_______________
      * Receiving Bank
      *"""""""""""""""
     C                   EVAL      F3035='RB'
     C                   EVAL      F3194='NO9990010685388'
     C                   EVAL      F3192A='Wilson Logistics (Norway) AS'
     C                   EVAL      F3433='Sparebank 1'
     C                   EVAL      F3434='SWIFT LABANOKK'
     C                   EVAL      F3207='NO'
     C                   WRITE     FIIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LIN25 /
      *////////////////////////////////////////////////////////////
     C     LIN25         BEGSR

     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LINR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     25            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP0         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082
     C                   EVAL      L7140=INVK
     C                   EVAL      L7143='VP'
     C                   WRITE     LINR

     C                   EXSR      QTY25
     C                   EXSR      MOA26
     C                   EXSR      PRI28
     C                   EXSR      RFF29

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LOC10 /
      *////////////////////////////////////////////////////////////
     C     LOC10         BEGSR
     C                   Z-ADD     0             WREP1
      *___________________
      * Place of departure
      *"""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     10            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   EVAL      L3227='5'
     C                   EVAL      L3225='OSL'
     C                   WRITE     LOCR
      *__________________
      * Place of delivery
      *""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     10            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   EVAL      L3227='7'
     C                   EVAL      L3225='ANYWHERE'
     C                   WRITE     LOCR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LOC12 /
      *////////////////////////////////////////////////////////////
     C     LOC12         BEGSR
     C                   Z-ADD     0             WREP1
      *___________________
      * Place of departure
      *"""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     12            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   EVAL      L3227='1'
     C                   EVAL      L3225='OSL'
     C                   WRITE     LOCR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA26 /
      *////////////////////////////////////////////////////////////
     C     MOA26         BEGSR
     C                   Z-ADD     0             WREP1
      *____________
??    * Bruttofrakt
      *""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     26            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '38'          M5025
     C     INVAL         IFEQ      *BLANKS
     C     INVAL         OREQ      'NOK'
     C                   EVAL      ØN153=INBELN
     C                   ELSE
     C                   EVAL      ØN153=INBELV
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ADD       INBELN        WINBELN
     C                   ADD       INBELV        WINBELV
     C     INMVA         IFEQ      'J'
     C                   ADD       INBELN        WINBELNM
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA48 /
      *////////////////////////////////////////////////////////////
     C     MOA48         BEGSR
      *__________
      * Momsbelöp
      *""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     48            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '176'         M5025
     C                   EVAL      ØN153= WINBELNM * 0.24
     C                   EVAL      WMOMS=ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *_____________
      * Fakturabeløp
      *"""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     48            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '39'          M5025
     C                   IF        C6345A='NOK'
     C                   Z-ADD     WINBELN       ØN153
     C                   ADD       WMOMS         ØN153
     C                   ELSE
     C                   Z-ADD     WINBELV       ØN153
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD02 /
      *////////////////////////////////////////////////////////////
     C     NAD02         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *________________
      * Vendor/Supplier
      *""""""""""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'VN'          N3035
     C                   EVAL      N3036A='Wilson Logistics (Norway) AS'
     C                   EVAL      N3042A='Ulvenveien 90 b'
     C                   EVAL      N3042B='Postboks 249'
     C                   EVAL      N3164='Oslo'
     C                   EVAL      N3251='N-0152'
     C                   EVAL      N3207='NO'
     C                   WRITE     NADR

     C                   EXSR      FII02
     C                   EXSR      RFF03
     C                   EXSR      CTA05
      *______
      * Buyer
      *""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'BY'          N3035
      * hente navn og adresse fra kunderegister.
     C                   EVAL      WUNDNR=INKUNR
     C     CUNKEY        CHAIN     CUNDL01                            61
     C                   EVAL      N3036A=KNAVN
     C                   EVAL      N3042A=ADR1
     C                   EVAL      N3042B=ADR2
     C                   EVAL      N3164=ADR3
     C                   MOVEL     POSTNR        N3251
     C                   EVAL      N3207=SYLAND
     C                   WRITE     NADR

     C                   EXSR      RFF03BY
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PAI00 /
      *////////////////////////////////////////////////////////////
     C     PAI00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   PAIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     0             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   EVAL      P4439='1'
     C                   EVAL      P4431='14'
     C                   EVAL      P4461='ZZZ'
     C                   WRITE     PAIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PAT08 /
      *////////////////////////////////////////////////////////////
     C     PAT08         BEGSR
      *______________
      * Payment terms
      *""""""""""""""
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '1'           P4279
     C                   WRITE     PATR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PRI28 /
      *////////////////////////////////////////////////////////////
     C     PRI28         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   PRIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     28            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     'INF'         P5125
     C                   MOVEL     '1'           P5118
     C                   MOVEL     'PE'          P5375
     C                   WRITE     PRIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  QTY25 /
      *////////////////////////////////////////////////////////////
     C     QTY25         BEGSR
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     25            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   MOVEL     '21'          Q6063
     C                   MOVEL     '1'           Q6060
     C                   MOVEL     'EA'          Q6411
     C                   WRITE     QTYR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   Z-ADD     0             WREP0
      *_________________________
      * Commodity control number
      *"""""""""""""""""""""""""
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   EXSR      MOD10
     C                   MOVEL     'HS'          R1153
     C                   MOVEL     INKID         R1154
     C                   WRITE     RFFR
      *___________________
      * Shippers Reference
      *"""""""""""""""""""
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'SI'          R1153
     C                   EVAL      WSIREF1=INUAVD
     C                   EVAL      WSIREF3=INUOPD
     C                   EVAL      R1154=WSIREF
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
      *___________
      * VAT number
      *"""""""""""
     C                   EVAL      R1153='VA'
     C                   EVAL      R1154='934667743'
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                RFF03BY /
      *////////////////////////////////////////////////////////////
     C     RFF03BY       BEGSR
      *_____________
      * Order number
      *"""""""""""""
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'ON'          R1153
      *
     C                   EVAL      WORTNO=INUAVD * 10000000
     C                   ADD       INUOPD        WORTNO
     C                   EVAL      *IN21=*OFF
     C     WORTNO        SETLL     GSSORL21
     C     WORTNO        READE     GSSORL21                               21
     C     *IN21         DOWEQ     *OFF
      *
     C                   EVAL      *IN22=*OFF
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 22
     C     *IN22         DOWEQ     *OFF
      *
     C     POCONO        IFNE      *BLANKS
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         RREP1
     C                   MOVE      *BLANKS       R1154
     C                   MOVEL     POCONO        R1154
     C                   WRITE     RFFR
     C                   ENDIF
      *
     C
     C     ORONO         READE     GSSPOF                                 22
     C                   ENDDO
     C     WORTNO        READE     GSSORL21                               21
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     29            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'AED'         R1153
     C                   MOVEL     INVK          R1154
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    WINSEID
     C                   PARM                    WINMOID
     C                   PARM                    UUFANR            6
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO
     C                   PARM                    WTID
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    DREP3         WREP3
     C     *LIKE         DEFINE    UMN           WMN
     C     *LIKE         DEFINE    FIRMA         WFIRM
     C     *LIKE         DEFINE    KUNDNR        WUNDNR
     C     *LIKE         DEFINE    INBELN        WINBELN
     C     *LIKE         DEFINE    INBELV        WINBELV
     C     *LIKE         DEFINE    INBELN        WINBELNM
     C     *LIKE         DEFINE    INBELN        WMOMS
     C     *LIKE         DEFINE    INST          WINST
     C     *LIKE         DEFINE    INSEID        WINSEID
     C     *LIKE         DEFINE    INMOID        WINMOID
     C     *LIKE         DEFINE    INFANR        WINFANR
     C     *LIKE         DEFINE    ORTNO         WORTNO
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W_ØN153          15 3
     C                   EVAL      WFIRM='01'
     C                   MOVE      UUFANR        WINFANR
      * DEF AV KEYS
     C     CUNKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    WUNDNR
     C     INTKEY        KLIST
     C                   KFLD                    WINST
     C                   KFLD                    WINSEID
     C                   KFLD                    WINMOID
     C                   KFLD                    WINFANR
     C     FAKKEY        KLIST
     C                   KFLD                    INUAVD
     C                   KFLD                    INUOPD
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   MOVE      '1'           *IN41
      *_____________________
      * Les oppdragsregister
      *"""""""""""""""""""""
     C     FAKKEY        CHAIN     HEADF                              52
     C     FAKKEY        CHAIN     BILLOF                             52
      *_________________________
      * Les fakturatransaksjoner
      *"""""""""""""""""""""""""
     C     INTKEY        SETLL     INTFAS
     C     INTKEY        READE     INTFAS                                 51
     C     *IN51         DOWEQ     '0'
      *____________________________
      * Slett transkasjonspost
      *""""""""""""""""""""""""""""
     C                   EVAL      INST='X'
     C                   UPDATE    INTFASR
      *_____________________
      * Les oppdragsregister
      *"""""""""""""""""""""
     C     FAKKEY        CHAIN     HEADF                              52
     C     FAKKEY        CHAIN     BILLOF                             52
      *_________________
      * EDIFACT Detaljer
      *"""""""""""""""""
     C                   EXSR      LIN25
      *__________________
      * Les neste faktura
      *""""""""""""""""""
     C     INTKEY        READE     INTFAS                                 51
     C                   ENDDO
     C     INTKEY        SETLL     INTFAS
     C     INTKEY        READE(N)  INTFAS                                 51
      *_________________
      * Hoveopplysninger
      *"""""""""""""""""
     C                   EXSR      SRB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      DTM00
     C                   EXSR      PAI00
     C*                  EXSR      FTX00
     C                   EXSR      RFF01
     C                   EXSR      NAD02
     C                   EXSR      TAX06
     C                   EXSR      CUX07
     C                   EXSR      PAT08
     C                   EXSR      TDT09
     C                   EXSR      TOD12
     C                   EXSR      UNS00
     C                   EXSR      CNT00
     C                   EXSR      MOA48
      *_____________
      * WRITE TO UNH
      *"""""""""""""
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      EDIM0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Remove neg sign.
     C                   IF        ØN153 < 0
     C                   Z-SUB     ØN153         ØN153
     C                   ENDIF
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'
     C                   MOVE      ØA153H        ØA12
     C                   ELSE
     C                   MOVEL     ØA153X        ØA12
     C                   MOVE      '.   '        ØA12
     C                   MOVE      ØA153D        ØA12
     C                   END
      * search from left and find first sign over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'
     C     ØA153X        IFEQ      '00000000'
     C                   SUB       1             ØX
     C                   ENDIF
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX06 /
      *////////////////////////////////////////////////////////////
     C     TAX06         BEGSR
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     6             TGN
     C                   Z-ADD     1             TREP0
     C                   MOVEL     '9'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   EVAL      T5305='S'
     C                   WRITE     TAXR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TDT09 /
      *////////////////////////////////////////////////////////////
     C     TDT09         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *_________
      * Main leg
      *"""""""""
     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     9             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   EVAL      T8051='20'
      * C222 Sea only
     C                   EVAL      T8067='1'
      * Hent rederi kode (SCAC)
     C                   MOVEL     'W066'        T8213

     C                   WRITE     TDTR

     C*                  EXSR      LOC10

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TOD12 /
      *////////////////////////////////////////////////////////////
     C     TOD12         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *_________
      * Main leg
      *"""""""""
     C                   CLEAR                   TODR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     12            TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   EVAL      T4055='6'
     C                   EVAL      T4053='FOB'
     C                   WRITE     TODR

     C                   EXSR      LOC12

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'INVOIC'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '94B'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'GEIINV'      U0057
     C                   MOVEL     '380'         U1001
     C                   MOVEL     INFANR        U1004
     C                   MOVEL     '9'           U1225
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  UNS00 /
      *////////////////////////////////////////////////////////////
     C     UNS00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   UNSR
     C                   Z-ADD     WMN           UMN
     C                   Z-ADD     60            UGN
     C                   Z-ADD     WREP0         UREP0
     C                   MOVEL     'S'           U0081
     C                   WRITE     UNSR
     C                   ENDSR
      *_________________________________________
      *  MOD10     -    UTREGNING AV MOD-10 KONTROLLFELT (WBAK)
      *""""""""""""""""""""""""""""""""""""""""""
     C     MOD10         BEGSR
     C*                  Z-ADD     WB07          WKID1
     C*                  Z-ADD     WB06          WKID2
     C*                  Z-ADD     0             WKID3
     C*                  MOVE      *ZEROS        WKID4            21
     C*                  MOVE      WKID          WKID4
     C*                  CALL      'SYGE09'
     C*                  PARM                    WKID4
     C*                  PARM                    WKID5             1 0
     C*                  MOVE      WKID5         WKID6             1 0
      *
     C*                  MOVEL     WKID          XKID1
     C*                  Z-ADD     WKID6         XKID2
     C                   ENDSR
