      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDKLKNR' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(12 'Gå til forrige bilde')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL009R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(50 YBC1706 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 11  2MSGCON(12 DIV0199 SYMSGF)
     A            WSKUNR         8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            VADRNA        30A  H
     A            VADRN1        30A  H
     A            VADRN3        30A  H
     A            VADRN2        30A  H
     A            VALAND         2A  H
     A            VATLF         15A  H
     A            VAFAX         15A  H
     A            VAMAIL        70A  H
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            VADRNR         5Y 0O  5  4TEXT('Vareadressenr.')
     A                                      EDTCDE(4)
     A            VAADNX        25A  O  5 10
     A            VAAD1X        25A  O  5 36
     A            VAAD3X        18A  O  5 62
     A            VAKURE        17A  O  6 10
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/LL009R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(50 YBC1706 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(12 DIV0199 SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(WHT)
     A                                  2 70TIME
     A            ZRECNO         4S 0O  2 63SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(20 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(79 DIV1589 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              5  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              5 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A N40                                  CF09(09 'NESTE LEDIG NR.')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  1  4MSGCON(75 DIV1588 SYMSGF)
     A                                      COLOR(WHT)
     A            WSADNR         5Y 0B  2  4TEXT('Vareadressenr.')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADNA        30A  B  2 15DSPATR(UL)
     A  40N30
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A            WSAD1         30A  B  2 46DSPATR(UL)
     A                                      COLOR(GRN)
     A            WSAD2         30A  B  3 15DSPATR(UL)
     A                                      COLOR(GRN)
     A            WSAD3         30A  B  3 46DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A            WSLAND         2A  B  3 77DSPATR(UL)
     A                                      COLOR(GRN)
     A                                  4  2MSGCON(11 SHU0521 SYMSGF)
     A            WSKURE        17A  B  4 15
     A                                  4 33MSGCON(05 YBC1707 SYMSGF)
     A            WSTLF         15A  B  4 39DSPATR(UL)
     A                                  4 55MSGCON(05 YBC1708 SYMSGF)
     A            WSFAX         15A  B  4 61DSPATR(UL)
     A                                  5  2MSGCON(07 YBC0790 SYMSGF)
     A            WSMAIL        70A  B  5 10DSPATR(UL)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A N30N40                           6 21MSGCON(18 YBC0677 SYMSGF)
     A                                      COLOR(BLU)
     A N30                              6 40MSGCON(40 JOV1152 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(025 YBC0791 *LIBL/SYMSGF)
     A            W2ADNR         5Y 0I  1 28DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2MSGCON(025 YBC0792 *LIBL/SYMSGF)
     A            W2ADNA        30A  I  2 28DSPATR(UL)
N01  A                                  3  2MSGCON(025 JOV1192 *LIBL/SYMSGF)
N01  A            W2PNR          4A  I  3 28DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  1  4MSGCON(075 DIV1588 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            VADRNR         5Y 0O  2  4TEXT('Vareadressenr.')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VADRNA        30A  O  2 15COLOR(WHT)
     A            VADRN1        30A  O  2 46COLOR(WHT)
     A            VADRN2        30A  O  3 15COLOR(WHT)
     A            VADRN3        30A  O  3 46COLOR(WHT)
     A            VALAND         2A  O  3 77COLOR(WHT)
     A                                  4  2MSGCON(11 SHU0521 SYMSGF)
     A            VAKURE        17A  O  4 15
     A                                  4 33MSGCON(05 YBC1707 SYMSGF)
     A            VATLF         15A  O  4 39COLOR(WHT)
     A                                  4 55MSGCON(05 YBC1708 SYMSGF)
     A            VAFAX         15A  O  4 61COLOR(WHT)
     A                                  5  2MSGCON(07 YBC0790 SYMSGF)
     A            VAMAIL        70A  O  5 10COLOR(WHT)
     A                                  6  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/LL009R'
     A                                      COLOR(BLU)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  1 19MSGCON(50 YBC1706 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(12 DIV0199 SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(BLU)
     A            KNAVN         30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(79 DIV1589 SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
