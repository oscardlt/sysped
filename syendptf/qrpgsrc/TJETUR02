      *********************************************************************
      *  HENT en turheader (eller ved 0 i turnr - hent defaulter)
      *********************************************************************
CB    * Fjernet kall på SYTR12A etter diskusjon med Svein
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR02 MODULE(*LIBL/TJETUR02 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Parameter kopiRund - når enm skal nete input til koiering/forlenge rundtur
12XXX * 02 JOV PTF12 Vis type transportør i turheader
12XXX * 03 JOV PTF12 ErrMsg ved ugyldig tur / 0 i turnr = hent defaulter
12XXX * 04 JOV PTF12 Feil i PTF03
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
S01  F*URER     IF   E           K DISK    USROPN
N01  FTURER     IF A E           K DISK    USROPN
     FHEAD11    IF   E           K DISK    USROPN
     FFAK2      IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FKODTV     IF   E           K DISK    USROPN
     FVALUL01   IF   E           K DISK    USROPN
     FKOSBU2    IF   E           K DISK    USROPN
     FKODTGE    IF   E           K DISK    USROPN
     FTRAVH1    IF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
N03  FKodta     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
N01  D kopiRund        S              1
     D WA              S             15
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
N02  D typKod          s              1
N02  D typTxt          s             20
      *
     D AVRTXT          C                   CONST('AVREGNING HOVEDTRANS')
     D AVRTXD          C                   CONST('*AVR*DIREKTE KOSTN. ')
     D AVRTXF          C                   CONST('*AVF*FORDELT KOSTN. ')
     D AVRTGD          C                   CONST('*AGD*GODSKR DIREKTE ')
     D AVRTGF          C                   CONST('*AGF*GODSKR FORDELT ')
     D                 DS
     D  FAVT                   1     20
     D  FAVTX                  1     19
     D  FAVTXX                13     15
n03  D                 DS
n03  D DEFperi                 1      8  0
n03  D  DEFACC                 1      2  0
n03  D  DEFAAR                 3      4  0
n03  D  DEFMND                 5      6  0
n03  D  Dag                    7      8  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬tuavd¬ : ¬'+%trim(%editc(WTUAVD:'Z'))+'¬, '
        +'¬tupro¬ : ¬'+%trim(%editc(WTUPRO:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ¬getonetrip¬ : [';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Hæmta detta ena ærendet.

     C     Error         IFEQ      *blanks
S04  C*                  if        TUPRO = *zeros
N04  C                   if        WTUPRO = *zeros
N03  C                   setoff                                       55
N03  C                   exsr      GetDft
N03  C                   else
     C     TURKEY        CHAIN     TURER                              55
N03  C                   endif
     C     *IN55         IFEQ      '0'
     C                   if        TUPRO <> *zeros
     C                   EXSR      FIXVAR
     C                   endif
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tust¬ : ¬'+%trim(TUST)+'¬, '
          +'¬tuavd¬ : ¬'+%trim(%editc(TUAVD:'Z'))+'¬, '
          +'¬tupro¬ : ¬'+%trim(%editc(TUPRO:'Z'))+'¬, '
          +'¬tudt¬ : ¬'+%trim(%editc(TUDT:'Z'))+'¬, '
          +'¬tukna¬ : ¬'+%trim(%editc(TUKNA:'Z'))+'¬, '
          +'¬tuknt¬ : ¬'+%trim(%editc(TUKNT:'Z'))+'¬, '
               +'¬tunat¬ : ¬'+%trim(TUNAT)+'¬, '
               +'¬tuad1t¬ : ¬'+%trim(TUAD1T)+'¬, '
               +'¬tuad2t¬ : ¬'+%trim(TUAD2T)+'¬, '
               +'¬tuad3t¬ : ¬'+%trim(TUAD3T)+'¬, '
N02            +'¬typKod¬ : ¬'+%trim(typKod)+'¬, '
N02            +'¬typTxt¬ : ¬'+%trim(typTxt)+'¬, '
               +'¬tubiln¬ : ¬'+%trim(TUBILN)+'¬, '
               +'¬tutarf¬ : ¬'+%trim(TUTARF)+'¬, '
               +'¬tulk¬ : ¬'+%trim(TULK)+'¬, '
               +'¬tuxxx1¬ : ¬'+%trim(TUXXX1)+'¬, '
               +'¬tusdf¬ : ¬'+%trim(TUSDF)+'¬, '
               +'¬tusdt¬ : ¬'+%trim(TUSDT)+'¬, '
               +'¬tuao¬ : ¬'+%trim(%editc(TUAO:'Z'))+'¬, '
N01            +'¬tuts¬ : ¬'+%trim(%editc(TUTS:'Z'))+'¬, '
N01            +'¬tutvkt¬ : ¬'+%trim(%editc(TUTVKT:'Z'))+'¬, '
               +'¬tutlm¬ : ¬'+%trim(%editc(TUTLM:'4'))+'¬, '
N01            +'¬tutm3¬ : ¬'+%trim(%editc(TUTM3:'4'))+'¬, '
               +'¬tulast¬ : ¬'+%trim(%editc(TULAST:'Z'))+'¬, '
               +'¬tubusj¬ : ¬'+%trim(%editc(TUBUSJ:'Z'))+'¬, '
               +'¬tuxxx2¬ : ¬'+%trim(TUXXX2)+'¬, '
               +'¬tutst1¬ : ¬'+%trim(TUTST1)+'¬, '
               +'¬tutst2¬ : ¬'+%trim(TUTST2)+'¬, '
               +'¬tutst3¬ : ¬'+%trim(TUTST3)+'¬, '
               +'¬tutst4¬ : ¬'+%trim(TUTST4)+'¬, '
               +'¬tutst5¬ : ¬'+%trim(TUTST5)+'¬, '
               +'¬tuavre¬ : ¬'+%trim(TUAVRE)+'¬, '
               +'¬tuavnr¬ : ¬'+%trim(%editc(TUAVNR:'Z'))+'¬, '
               +'¬turund¬ : ¬'+%trim(%editc(TURUND:'Z'))+'¬, '
               +'¬turacc¬ : ¬'+%trim(%editc(TURACC:'Z'))+'¬, '
               +'¬turaar¬ : ¬'+%trim(%editc(TURAAR:'Z'))+'¬, '
               +'¬turmnd¬ : ¬'+%trim(%editc(TURMND:'Z'))+'¬, '
               +'¬tuopdt¬ : ¬'+%trim(TUOPDT)+'¬, '
               +'¬tutrma¬ : ¬'+%trim(%editc(TUTRMA:'Z'))+'¬, '
               +'¬tuheng¬ : ¬'+%trim(TUHENG)+'¬, '
               +'¬tulkh¬ : ¬'+%trim(TULKH)+'¬, '
               +'¬tucon1¬ : ¬'+%trim(TUCON1)+'¬, '
               +'¬tulkc1¬ : ¬'+%trim(TULKC1)+'¬, '
               +'¬tucon2¬ : ¬'+%trim(TUCON2)+'¬, '
               +'¬tulkc2¬ : ¬'+%trim(TULKC2)+'¬, '
               +'¬tutara¬ : ¬'+%trim(%editc(TUTARA:'Z'))+'¬, '
               +'¬tubilk¬ : ¬'+%trim(TUBILK)+'¬, '
               +'¬tuknt2¬ : ¬'+%trim(%editc(TUKNT2:'Z'))+'¬, '
               +'¬tusja1¬ : ¬'+%trim(%editc(TUSJA1:'Z'))+'¬, '
               +'¬tusjn1¬ : ¬'+%trim(TUSJN1)+'¬, '
               +'¬tusja2¬ : ¬'+%trim(%editc(TUSJA2:'Z'))+'¬, '
               +'¬tusjn2¬ : ¬'+%trim(TUSJN2)+'¬, '
               +'¬tustef¬ : ¬'+%trim(TUSTEF)+'¬, '
               +'¬tusonf¬ : ¬'+%trim(TUSONF)+'¬, '
               +'¬tudtt¬ : ¬'+%trim(%editc(TUDTT:'Z'))+'¬, '
              // +'¬tutm¬ : ¬'+%trim(%editc(TUTM:'Z'))+'¬, '
               +'¬tutm¬ : ¬'+%trim(TUTMA)+'¬, '
              // +'¬tutmt¬ : ¬'+%trim(%editc(TUTMT:'Z'))+'¬, '
               +'¬tutmt¬ : ¬'+%trim(TUTMTA)+'¬, '
               +'¬tustet¬ : ¬'+%trim(TUSTET)+'¬, '
               +'¬tusont¬ : ¬'+%trim(TUSONT)+'¬, '
N01            +'¬tukvkt¬ : ¬'+%trim(%editc(TUKVKT:'Z'))+'¬, '
N01            +'¬tukam3¬ : ¬'+%trim(%editc(TUKAM3:'4'))+'¬, '
               +'¬tukalM¬ : ¬'+%trim(%editc(TUKALM:'4'))+'¬, '
               +'¬tuto1a¬ : ¬'+%trim(TUTO1A)+'¬, '
               +'¬tuto1b¬ : ¬'+%trim(TUTO1B)+'¬, '
               +'¬tutval¬ : ¬'+%trim(TUTVAL)+'¬, '
               +'¬tutako¬ : ¬'+%trim(TUTAKO)+'¬, '
               +'¬tutbel¬ : ¬'+%trim(%editc(TUTBEL:'4'))+'¬, '
               +'¬tutref¬ : ¬'+%trim(%editc(TUTREF:'Z'))+'¬, '
               +'¬tuhoyb¬ : ¬'+%trim(%editc(TUHØYB:'4'))+'¬, '
               +'¬tuhoyh¬ : ¬'+%trim(%editc(TUHØYH:'4'))+'¬, '
               +'¬tusg¬ : ¬'+%trim(TUSG)+'¬, '
               +'¬tuant1¬ : ¬'+%trim(%editc(TUANT1:'Z'))+'¬, '
               +'¬tuenh1¬ : ¬'+%trim(TUENH1)+'¬, '
               +'¬tuant2¬ : ¬'+%trim(%editc(TUANT2:'4'))+'¬, '
               +'¬tuenh2¬ : ¬'+%trim(TUENH2)+'¬, '
               +'¬tukm¬ : ¬'+%trim(%editc(TUKM:'Z'))+'¬, '
               +'¬tublnr¬ : ¬'+%trim(TUBLNR)+'¬, '
               +'¬tubkd¬ : ¬'+%trim(TUBKD)+'¬, '
               +'¬tubnv¬ : ¬'+%trim(TUBNV)+'¬, '
               +'¬tuetd¬ : ¬'+%trim(%editc(TUETD:'Z'))+'¬, '
               +'¬tueta¬ : ¬'+%trim(%editc(TUETA:'Z'))+'¬, '
               +'¬tuatd¬ : ¬'+%trim(%editc(TUATD:'Z'))+'¬, '
               +'¬tuata¬ : ¬'+%trim(%editc(TUATA:'Z'))+'¬, '
               +'¬tuckd1¬ : ¬'+%trim(TUCKD1)+'¬, '
               +'¬tuckd2¬ : ¬'+%trim(TUCKD2)+'¬, '
TMS            +'¬tupoen¬ : ¬'+%trim(%editc(TUPOEN:'Z'))+'¬, '
TMS            +'¬tutlm2¬ : ¬'+%trim(%editc(TUTLM2:'4'))+'¬, '
TMS            +'¬tustn1¬ : ¬'+%trim(TUSTN1)+'¬, '
TMS            +'¬tustn2¬ : ¬'+%trim(TUSTN2)+'¬, '
TMS            +'¬tustn3¬ : ¬'+%trim(TUSTN3)+'¬, '
TMS            +'¬tustn4¬ : ¬'+%trim(TUSTN4)+'¬, '
TMS            +'¬tustn5¬ : ¬'+%trim(TUSTN5)+'¬, '
TMS            +'¬tustn6¬ : ¬'+%trim(TUSTN6)+'¬, '
TMS            +'¬tustn7¬ : ¬'+%trim(TUSTN7)+'¬, '
TMS            +'¬tustn8¬ : ¬'+%trim(TUSTN8)+'¬, '
               +'¬tustn9¬ : ¬'+%trim(TUSTN9)+'¬, '
               +'¬tulkf¬ : ¬'+%trim(TULKF)+'¬, '
               +'¬tulkt¬ : ¬'+%trim(TULKT)+'¬, '
               +'¬simlm¬ : ¬'+%trim(%editc(SIMLM:'4'))+'¬, '
               +'¬simm3¬ : ¬'+%trim(%editc(SIMM3:'4'))+'¬, '
               +'¬berbud¬ : ¬'+%trim(%editc(BERBUD:'Q'))+'¬, '
               +'¬totiaa¬ : ¬'+%trim(%editc(TOXIÅA:'Q'))+'¬, '
               +'¬totioa¬ : ¬'+%trim(%editc(TOXIOA:'Q'))+'¬, '
               +'¬totisa¬ : ¬'+%trim(%editc(TOXISA:'Q'))+'¬, '
               +'¬totiag¬ : ¬'+%trim(%editc(TOXIÅG:'Q'))+'¬, '
               +'¬totiog¬ : ¬'+%trim(%editc(TOXIOG:'Q'))+'¬, '
               +'¬totisg¬ : ¬'+%trim(%editc(TOXISG:'Q'))+'¬, '
               +'¬totkaa¬ : ¬'+%trim(%editc(TOXKÅA:'Q'))+'¬, '
               +'¬totkoa¬ : ¬'+%trim(%editc(TOXKOA:'Q'))+'¬, '
               +'¬totksa¬ : ¬'+%trim(%editc(TOXKSA:'Q'))+'¬, '
               +'¬totkao¬ : ¬'+%trim(%editc(TOXKÅØ:'Q'))+'¬, '
               +'¬totkoo¬ : ¬'+%trim(%editc(TOXKOØ:'Q'))+'¬, '
               +'¬totkso¬ : ¬'+%trim(%editc(TOXKSØ:'Q'))+'¬, '
               +'¬totopn¬ : ¬'+%trim(%editc(TOXÅ:'Q'))+'¬, '
               +'¬totovf¬ : ¬'+%trim(%editc(TOXO:'Q'))+'¬, '
               +'¬totsum¬ : ¬'+%trim(%editc(TOXS:'Q'))+'¬, '
               +'¬tures¬ : ¬'+%trim(%editc(TURES:'3'))+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONstring =']}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     TURER
     C  N50              CLOSE     HEAD11
     C  N50              CLOSE     FAK2
     C  N50              CLOSE     FIRM
     C  N50              CLOSE     KODTV
     C  N50              CLOSE     VALUL01
     C  N50              CLOSE     KOSBU2
     C  N50              CLOSE     KODTGE
     C  N50              CLOSE     TRAVH1
     C  N50              CLOSE     TRAN
N03  C  N50              CLOSE     KODTA

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA      = zhbgetvar('TUAVD')
     C                   EVAL      WTUAVD  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('TUPRO')
     C                   EVAL      WTUPRO  = c2n2(WA)
N01  C                   EVAL      kopiRund= zhbgetvar('kopiRund')
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     C     TURKEY        klist
     C                   KFLD                    WTUAVD
     C                   KFLD                    WTUPRO
     C     *LIKE         DEFINE    TUAVD         WTUAVD
     C     *LIKE         DEFINE    TUPRO         WTUPRO
     C     FAK2KI        KLIST
     C                   KFLD                    TRAVD1
     C                   KFLD                    TROPD1
     C     KOSBK2        KLIST
     C                   KFLD                    XXST
     C                   KFLD                    TUPRO
     C                   MOVE      ' '           XXST              1
N01  C     KODTVK        KLIST
     C                   KFLD                    YVUNI
N01  C                   KFLD                    TUAVD
     C                   MOVE      'V'           YVUNI             1
     C     KODGEK        KLIST
     C                   KFLD                    YGEUNI
     C                   KFLD                    FAVK
     C                   MOVE      'GE'          YGEUNI            2
     C     VALKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUTVAL
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FAK2KU        KLIST
     C                   KFLD                    TRAVD2
     C                   KFLD                    TROPD2
n02  C     TraKey        KLIST
n02  C                   KFLD                    BIFIRM
N02  C                   KFLD                    TUKNT2
n03  C     KODTAK        KLIST
n03  C                   KFLD                    KOAUNI
N03  C                   KFLD                    TUAVD
n03  C                   move      'A'           KOAUNI

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      TURER
     C  N50              OPEN      HEAD11
     C  N50              OPEN      FAK2
     C  N50              OPEN      FIRM
     C  N50              OPEN      KODTV
     C  N50              OPEN      VALUL01
     C  N50              OPEN      KOSBU2
     C  N50              OPEN      KODTGE
     C  N50              OPEN      TRAVH1
     C  N50              OPEN      TRAN
N03  C  N50              OPEN      KODTA

     C  N50*LOVAL        SETLL     FIRM
     C  N50              READ      FIRM
     C                   ENDSR
N03   *********************************************************
N03  C     GetDft        BEGSR
N03   *********************************************************
N03   * default periode - men ikke i månedsskifte (kortere ved innland)
N03  C                   CALL      'SYGE15C'
N03  C                   PARM                    WDT               8
N03  C                   PARM                    WTM               6
N03  c                   move      WDT           DEFperi
N03  C     KODTAK        CHAIN     KODTA                              33
N03  c                   if        KOAIE='1' or KOAIE='2'                       innland
N03  c                             or KOAIE='D'                                 utlandsmodul men Dom
N03  c                   if        KOAIE<>'2'
N03  c                   move      WDT           TUDT                           Fradato, ikke v/utg.
N03  C                   endif
N03   * innland - 02-28
n03  c                   if        Dag >= 2 and Dag <= 28
N03  c                   move      DEFACC        TURACC
N03  c                   move      DEFAAR        TURAAR
N03  c                   move      DEFMND        TURMND
N03  C                   endif
N03  C                   else
N03   *  utland - 04-23
N03  c                   if        Dag >= 4 and Dag <= 23
N03  c                   move      DEFACC        TURACC
N03  c                   move      DEFAAR        TURAAR
N03  c                   move      DEFMND        TURMND
N03  C                   endif
N03  C                   endif
N03   *
N03  C                   ENDSR
      *********************************************************
     C     FIXVAR        BEGSR
      *********************************************************
N02  C     TRAKEY        CHAIN     TRAN
      *
     C                   MOVE      TUPRO         TUPRO2            8
     c                   if        TUBILN = '.' and TUHENG<>' '
     c                   eval      TUBILN = TUHENG
     c                   endif
     C                   MOVEL     TUSONF        TULKF             2
     C                   MOVEL     TUSONT        TULKT             2

     C                   Z-ADD     0             TURES             5 2
N01  C     KODTVK        CHAIN     KODTV                              33
N01  C   33              Z-ADD     1800          KOVLKG
N01  C   33              Z-ADD     300           KOVKKG
      *
     c*                  call      'SYGE83'
     c*                  parm                    tupro
     c*                  parm                    kodet             1
     c*                  parm                    ubelop           13 2
     C*                  Z-ADD     0             TURES             5 2
     c*                  z-add     tutbel        budsj            11 2
      *
      * eller hente budsjett
      *
     c*    budsj         ifeq      *zeros
     c*                  move      tupro         uproa             8
     c*                  call      'SYGE79TCL'
     c*                  parm                    uproa
     c*                  parm                    wstvalb           3
     c*                  parm                    budsj            11 2
     c*                  parm                    wstakob           1
     C*                  end
     c*    budsj         ifne      0
     C*    UBELOP        IFEQ      0
     C*                  Z-ADD     0             TURES
     C*                  ELSE
     c*    ubelop        div       budsj         work             15 5
     c*    work          sub       1             work
     c*    work          ifgt      999
     c*                  z-add     100           tures
     c*                  else
     c*    work          mult      100           tures
     c*                  end
     C*                  END
     c*                  else
     c*                  z-add     100           tures
     c*                  end
      * INITIER AKKUMULATORE/DS-ER
     C                   MOVE      *ZEROS        TOTIÅA           13 2
     C                   MOVE      *ZEROS        TOTIOA           13 2
     C                   MOVE      *ZEROS        TOTISA           13 2
     C                   MOVE      *ZEROS        TOTIÅG           13 2
     C                   MOVE      *ZEROS        TOTIOG           13 2
     C                   MOVE      *ZEROS        TOTISG           13 2
     C                   MOVE      *ZEROS        TOTKÅA           13 2
     C                   MOVE      *ZEROS        TOTKOA           13 2
     C                   MOVE      *ZEROS        TOTKSA           13 2
     C                   MOVE      *ZEROS        TOTKÅØ           13 2
     C                   MOVE      *ZEROS        TOTKOØ           13 2
     C                   MOVE      *ZEROS        TOTKSØ           13 2
     C                   MOVE      *ZEROS        TOTÅ             13 2
     C                   MOVE      *ZEROS        TOTO             13 2
     C                   MOVE      *ZEROS        TOTS             13 2
N01  C                   IF        kopiRund<>'J'
     C                   EXSR      SUMFRA
      * klokke med foranstilt
     c                   move      TUTM          TUTMA             4
     c                   if        TUTM = *zeros
     c                   move      *blanks       TUTMA
     c                   endif
     c                   move      TUTMT         TUTMTA            4
     c                   if        TUTMT= *zeros
     c                   move      *blanks       TUTMTA
     c                   endif
N01  C                   ELSE
N01  C                   exsr      BlankRund
N01  C                   ENDIF
      *
N02  C                   move      VMINCR        typKod
N02  C                   select
N02  c     VMINCR        wheneq    0
N02  c                   eval      typTxt='Provisjonsavregning '
N02  c     VMINCR        wheneq    1
N02  c                   eval      typTxt='Sender faktura      '
N02  c     VMINCR        wheneq    2
N02  c                   eval      typTxt='Egen bil(ikke avr)  '
N02  c     VMINCR        wheneq    3
N02  c                   eval      typTxt='Sender fakt(iht avr)'
N02  c     VMINCR        wheneq    4
N02  c                   eval      typTxt='Avregn iht frakttab.'
N02  c     VMINCR        wheneq    5
N02  c                   eval      typTxt='Sender fakt(iht tab)'
N02  C                   endsl
     C                   ENDSR
      ***********************************************
     C     SUMFRA        BEGSR
      ***********************************************
      *
     C                   MOVE      *ZEROS        TOTIÅA
     C                   MOVE      *ZEROS        TOTIOA
     C                   MOVE      *ZEROS        TOTISA
     C                   MOVE      *ZEROS        TOTIÅG
     C                   MOVE      *ZEROS        TOTIOG
     C                   MOVE      *ZEROS        TOTISG
     C                   MOVE      *ZEROS        TOTKÅA
     C                   MOVE      *ZEROS        TOTKOA
     C                   MOVE      *ZEROS        TOTKSA
     C                   MOVE      *ZEROS        TOTKÅØ
     C                   MOVE      *ZEROS        TOTKOØ
     C                   MOVE      *ZEROS        TOTKSØ
     C                   MOVE      *ZEROS        TOTÅ
     C                   MOVE      *ZEROS        TOTO
     C                   MOVE      *ZEROS        TOTS
N01  C                   MOVE      *ZEROS        SIMLM             5 2
N01  C                   MOVE      *ZEROS        SIMM3             9 3
N01  C                   MOVE      *ZEROS        TUAO
N01  C                   MOVE      *ZEROS        TUTS
      *
     C     STSUM1        TAG
     C     TUPRO         SETLL     HEAD11
     C     STSUM2        TAG
     C                   SETOFF                                       33
     C     TUPRO         READE     HEAD11                                 33
     C  N33              DO                                                     1<-B
N01   * stoler ikke på TUAO/TUTS - reakkumulerer når en likevel leser alle oppdrag
N01  C                   Add       1             TUAO
N01  C                   Add       HENT          TUTS
N01   * Akkumulere simmulert LM / M3 utfra F-vekt med avdelingens faktor.
N01  c                   z-add     HEVKT         TMPFBV            9 0
N01  c     HELM          mult      KOVLKG        TMPFBV1           9 0
N01  c     HEM3          mult      KOVKKG        TMPFBV2           9 0
N01  c     TMPFBV1       ifgt      TMPFBV
N01  c                   z-add     TMPFBV1       TMPFBV
N01  c                   end
N01  c     TMPFBV2       ifgt      TMPFBV
N01  c                   z-add     TMPFBV2       TMPFBV
N01  c                   end
     C                   IF        KOVLKG > *ZEROS
N01  c     TMPFBV        div(H)    KOVLKG        TMPLM            12 3
     C                   ELSE
N01  c                   EVAL      TMPLM=*ZEROS
     C                   ENDIF
     C                   IF        KOVKKG > *ZEROS
N01  c     TMPFBV        div(H)    KOVKKG        TMPM3            12 3
     C                   ELSE
N01  c                   EVAL      TMPM3=*ZEROS
     C                   ENDIF
N01  C                   ADD       TMPLM         SIMLM
N01  C                   ADD       TMPM3         SIMM3
      * ENNÅ IKKE OVERFØRT HENTE-KOSTNAD VIA DUP
     C     TROPD1        IFNE      *ZEROS
     C     FAK2KI        SETLL     FAK2                               34
     C     FAK2KI        READE     FAK2                                   34
     C     *IN34         DOWEQ     '0'
     C     FAOPKO        IFEQ      ' '
     C     FASK          ANDEQ     'I'
     C     FAKDA         ANDNE     'I'
     C                   SUB       FABELN        TOTKÅØ
     C                   SUB       FABELN        TOTKSØ
     C                   SUB       FABELN        TOTÅ
     C                   SUB       FABELN        TOTS
     C                   END
     C     FAK2KI        READE     FAK2                                   34
     C                   END
     C                   END
      * ENNÅ IKKE OVERFØRT UTKJ-KOSTNAD VIA DUP
     C     TROPD2        IFNE      *ZEROS
     C     FAK2KU        SETLL     FAK2                               34
     C     FAK2KU        READE     FAK2                                   34
     C     *IN34         DOWEQ     '0'
     C     FAOPKO        IFEQ      ' '
     C     FASK          ANDEQ     'I'
     C     FAKDA         ANDNE     'I'
     C                   SUB       FABELN        TOTKÅØ
     C                   SUB       FABELN        TOTKSØ
     C                   SUB       FABELN        TOTÅ
     C                   SUB       FABELN        TOTS
     C                   END
     C     FAK2KU        READE     FAK2                                   34
     C                   END
     C                   END
      ***************************************************
      * Hent inntekter/kostnader fra fakt-linjer
      ***************************************************
     C     FAK2K         SETLL     FAK2                               33
     C     STSUM3        TAG
     C     FAK2K         READE     FAK2                                   33
     C   33              GOTO      STSUM2
     C     FAKDA         IFEQ      'D'                                           2<-B
     C     FAOPKO        OREQ      'D'                                           2<-B
     C                   GOTO      STSUM3
     C                   END                                                     2<-E
      ****************************************************
      * INNTEKTER
      ****************************************************
     C     KODGEK        CHAIN     KODTGE                             33
     C   33              MOVE      ' '           KGEAVR
     C     FAKDA         IFNE      'K'
     C     KGEAVR        IFEQ      'T'                                           2<-B
     C                   ADD       FABELN        TOTISA
     C     FAOPKO        IFLT      'O'
     C                   ADD       FABELN        TOTIÅA
     C                   ADD       FABELN        TOTÅ
     C                   ELSE
     C                   ADD       FABELN        TOTIOA
     C                   ADD       FABELN        TOTO
     C                   END
     C                   ELSE                                                    2<-E
     C                   ADD       FABELN        TOTISG
     C     FAOPKO        IFLT      'O'
     C                   ADD       FABELN        TOTIÅG
     C                   ADD       FABELN        TOTÅ
     C                   ELSE
     C                   ADD       FABELN        TOTIOG
     C                   ADD       FABELN        TOTO
     C                   END
     C                   END                                                     2<-E
     C                   ADD       FABELN        TOTS
      *********************
      * FØRTE KOSTNADER!!
      *********************
     C                   ELSE
N03   * avregningsjusteringer må regnes inn under øvrige
N03  c     THFAKT        ifne      *zeros
N03  c     THFAKT        andne     FAFAKT
N03  c                   move      *blanks       FAVT
N03  c                   end
     C     FAVT          IFNE      AVRTXT
     C     FAVTX         ANDNE     AVRTXD
     C     FAVTX         ANDNE     AVRTXF
S13  C*    FAVTXX        ANDNE     '*T*'
N13  C     FAVTXX        IFNE      '*T*'
     C                   ADD       FABELN        TOTKOØ
     C                   ADD       FABELN        TOTKSØ
     C                   ADD       FABELN        TOTO
     C                   ADD       FABELN        TOTS
      * ta med kostnad nå transportør sender regning
N13  C                   else
N13  C     vmincr        ifeq      1
N13  C     vmincr        oreq      5
N13  C                   ADD       FABELN        TOTKOØ
N13  C                   ADD       FABELN        TOTKSØ
N13  C                   ADD       FABELN        TOTO
N13  C                   ADD       FABELN        TOTS
N13  C                   END
N13  C                   END
     C                   END
     C                   END
     C                   GOTO      STSUM3
     C                   END                                                    1<-E
      *
      * Avregnings-kostnader...
     C     THTOT         IFNE      *ZEROS
     C     VMINCR        ifeq      1
     C     VMINCR        oreq      3
     C                   Z-ADD     0             TOTKOA
     c                   else
     C                   Z-SUB     THTOT         TOTKOA
     C                   END
     C                   ELSE
     C                   Z-ADD     TUTBEL        TMPBL            11 2
     C     TUTVAL        IFNE      FICURR
     C                   MOVE      TUTVAL        TMPVAL            3
     C                   EXSR      VALUT
     C                   END
     C     vmincr        ifne      1
     C     vmincr        andne     5
     C   32              Z-SUB     TMPBL         TOTKOA
     C                   END
     C  N32              Z-SUB     TMPBL         TOTKÅA
     C                   END
     C                   ADD       TOTKOA        TOTKSA
     C                   ADD       TOTKOA        TOTO
     C                   ADD       TOTKOA        TOTS
     C                   ADD       TOTKÅA        TOTKSA
     C                   ADD       TOTKÅA        TOTÅ
     C                   ADD       TOTKÅA        TOTS
      * Hent åpne forv. kostnader. på turnivå
     C     KOSBK2        SETLL     KOSBU2
     C     KOSBK2        READE     KOSBU2                                 33
     C     *IN33         DOWEQ     '0'
     C     BUVK          IFEQ      'FEI'
     C                   MOVE      'T'           BUFER
     C                   END
     C     BUBNR         IFNE      TUTREF
     C     BUFER         ANDNE     'F'
     C     BUVAL         IFNE      FICURR
     C                   MOVE      BUVAL         TMPVAL
     C                   Z-ADD     BUBL          TMPBL
     C                   EXSR      VALUT
     C                   Z-ADD     TMPBL         BUBL
     C                   END
     C                   SUB       BUBL          TOTKÅØ
     C                   SUB       BUBL          TOTKSØ
     C                   SUB       BUBL          TOTÅ
     C                   SUB       BUBL          TOTS
     C                   END
     C     KOSBK2        READE     KOSBU2                                 33
     C                   END
     c                   move      TUPRO         uproa             8
     c                   call      'SYGE79TCL'
     c                   parm                    uproa
     c                   parm                    wstvalb           3
     c                   parm                    wstbelb          11 2
     c                   parm                    avtale            1
     C                   z-add     wstbelb       berbud            9 0
     c     TUTako        ifeq      'E'
     c     avtale        andeq     'A'
     c                   move      wstvalb       TUTVAL
     c                   move      wstbelb       TUTBEL
     c                   end
     C                   MOVE      'N'           XXVIS             1
CB   C*                  CALL      'SYTR12A'
CB   C*                  PARM                    TUAVD
CB   C*                  PARM                    TUPRO
CB   C*                  PARM                    XXVIS
CB   C*                  PARM                    N4                4 0
CB   C*                  PARM                    N7                7 0
CB   C*                  PARM                    N8                8 0
      *Hvis ennå ikke avregnet, trtype  0 - hent avr.tall til estimert
     C  N32VMINCR        IFEQ      0
     C     VMINCR        OREQ      4
     C     TURKEY        CHAIN     TRAVH1                             32
     C     *IN32         IFEQ      '0'
      * TREKK UT GAMMEL VERDI / NULL-STILL
     C                   SUB       TOTKÅA        TOTKSA
     C                   SUB       TOTKÅA        TOTÅ
     C                   SUB       TOTKÅA        TOTS
     C                   MOVE      *ZEROS        TOTKÅA
      * LEGG INN NY VERDI.......
     C                   Z-SUB     THTOT         TOTKÅA
     C                   ADD       TOTKÅA        TOTKSA
     C                   ADD       TOTKÅA        TOTÅ
     C                   ADD       TOTKÅA        TOTS
      * VERDIENE OVER I 9,0 SKJERMVARIABLE
     C                   Z-ADD(H)  TOTKÅA        TOXKÅA
     C                   Z-ADD(H)  TOTKSA        TOXKSA
     C                   Z-ADD(H)  TOTÅ          TOXÅ
     C                   Z-ADD(H)  TOTS          TOXS
     C                   END
     C                   MOVE      '0'           *IN32
     C                   END
      *
      * Dersom avregn.transportør og summer finnes. Rød om AvrBel foreløpig under beregnet kost
     c                   setoff                                       6061
     C     VMINCR        ifeq      0
     C     VMINCR        oreq      4
     C     BERBUD        ifne      0
     C     TOXKSA        andne     0
     c     TOXKSA        mult      -1            AvrBel            9 0
     c     AvrBel        iflt      BERBUD
     c                   seton                                        60        Rød / Revers
     c                   else
     c                   seton                                        61        Grønn / Revers
     C                   endif
     C                   endif
     C                   endif
      * LEGG I AKKUMULERTE VERDIER I SKJERMVARIABLE 9.0
     C                   Z-ADD(H)  TOTIÅA        TOXIÅA            9 0
     C                   Z-ADD(H)  TOTIOA        TOXIOA            9 0
     C                   Z-ADD(H)  TOTISA        TOXISA            9 0
     C                   Z-ADD(H)  TOTIÅG        TOXIÅG            9 0
     C                   Z-ADD(H)  TOTIOG        TOXIOG            9 0
     C                   Z-ADD(H)  TOTISG        TOXISG            9 0
     C                   Z-ADD(H)  TOTKÅA        TOXKÅA            9 0
     C                   Z-ADD(H)  TOTKOA        TOXKOA            9 0
     C                   Z-ADD(H)  TOTKSA        TOXKSA            9 0
     C                   Z-ADD(H)  TOTKÅØ        TOXKÅØ            9 0
     C                   Z-ADD(H)  TOTKOØ        TOXKOØ            9 0
     C                   Z-ADD(H)  TOTKSØ        TOXKSØ            9 0
     C                   Z-ADD(H)  TOTÅ          TOXÅ              9 0
     C                   Z-ADD(H)  TOTO          TOXO              9 0
     C                   Z-ADD(H)  TOTS          TOXS              9 0
      *
     C                   ENDSR
     C***********************************************
     C     VALUT         BEGSR
     C***********************************************
     C*
     C     VALKEY        CHAIN     VALUL01                            39
     C  N39              DO
     C     VALKU2        IFNE      0
     C                   MOVE      VALKU2        VALKU1
     C                   END
     C     TMPBL         MULT      VALKU1        XWORK            15 4
     C     XWORK         DIV(H)    OMRFAK        TMPBL
     C                   END
     C                   ENDSR
      *
N01   ***********************************************
N01  C     BlankRund     BEGSR
N01   ***********************************************
N01   * bevar gitte data
N01  c     *like         define    turund        XXrund
N01  c     *like         define    tuknt2        XXknt2
N01  c     *like         define    tunat         XXnat
N01  c     *like         define    tuad1t        XXad1t
N01  c     *like         define    tuad2t        XXad2t
N01  c     *like         define    tuad3t        XXad3t
N01  c     *like         define    tubiln        XXbiln
N01  c     *like         define    tutarf        XXtarf
N01  c     *like         define    tulk          XXlk
N01  c     *like         define    tuheng        XXheng
N01  c     *like         define    tulkh         XXlkh
N01  c     *like         define    tucon1        XXcon1
N01  c     *like         define    tulkc1        XXlkc1
N01  c     *like         define    tucon2        XXcon2
N01  c     *like         define    tulkc2        XXlkc2
N01  c     *like         define    tubilk        XXbilk
N01  c     *like         define    tusja1        XXsja1
N01  c     *like         define    tusjn1        XXsjn1
N01  c     *like         define    tusja2        XXsja2
N01  c     *like         define    tusjn2        XXsjn2
N01   *
N01  c                   eval      XXrund = turund
N01  c                   eval      XXknt2 = tuknt2
N01  c                   eval      XXnat  = tunat
N01  c                   eval      XXad1t = tuad1t
N01  c                   eval      XXad2t = tuad2t
N01  c                   eval      XXad3t = tuad3t
N01  c                   eval      XXbiln = tubiln
N01  c                   eval      XXtarf = tutarf
N01  c                   eval      XXlk   = tulk
N01  c                   eval      XXheng = tuheng
N01  c                   eval      XXlkh  = tulkh
N01  c                   eval      XXcon1 = tucon1
N01  c                   eval      XXlkc1 = tulkc1
N01  c                   eval      XXcon2 = tucon2
N01  c                   eval      XXlkc2 = tulkc2
N01  c                   eval      XXbilk = tubilk
N01  c                   eval      XXsja1 = tusja1
N01  c                   eval      XXsjn1 = tusjn1
N01  c                   eval      XXsja2 = tusja2
N01  c                   eval      XXsjn2 = tusjn2
N01   * Tøm turrecord ..
      * dummy output turerr for at clear skal funke
     c                   if        tust=' ' and tust <> ' '                     = aldri
     c                   write     turerr
     c                   endif
N01  c                   clear                   Turerr
      * Samt 2 hjelpefelt som IKKE er filvariable ... (og ikke i bruk i flg Oscar)
     C                   clear                   TULKF
     C                   clear                   TULKT
N01   * legg inn kun verdier som skal kopieres..
N01  c                   eval      turund = XXrund
N01  c                   eval      tuknt2 = XXknt2
N01  c                   eval      tunat  = XXnat
N01  c                   eval      tuad1t = XXad1t
N01  c                   eval      tuad2t = XXad2t
N01  c                   eval      tuad3t = XXad3t
N01  c                   eval      tubiln = XXbiln
N01  c                   eval      tutarf = XXtarf
N01  c                   eval      tulk   = XXlk
N01  c                   eval      tuheng = XXheng
N01  c                   eval      tulkh  = XXlkh
N01  c                   eval      tucon1 = XXcon1
N01  c                   eval      tulkc1 = XXlkc1
N01  c                   eval      tucon2 = XXcon2
N01  c                   eval      tulkc2 = XXlkc2
N01  c                   eval      tubilk = XXbilk
N01  c                   eval      tusja1 = XXsja1
N01  c                   eval      tusjn1 = XXsjn1
N01  c                   eval      tusja2 = XXsja2
N01  c                   eval      tusjn2 = XXsjn2
N01  C                   ENDSR
N01   *
