     A                                      REF(*LIBL/SYAV02F)
      *________
      * Forside
      *""""""""
     A          R SYAV04PA                  SKIPB(4)
     A                                      HIGHLIGHT
     A                                     2MSGCON(45 DIV2031 SYMSGF)
     A                                    84DATE EDTCDE(Y)
     A                                    93TIME EDTWRD('0 :  :  ')
     A                                   103'SYAV04R Side:'
     A                                    +1PAGNBR EDTCDE(Z)
      *
     A                                     2MSGCON(36 DIV2021 SYMSGF)
     A                                      SPACEB(3)
     A            ZAVDF          4  0     44EDTCDE(Z)
      *
     A                                     4MSGCON(34 DIV2022 SYMSGF)
     A                                      SPACEB(1)
     A            ZAVDT          4  0     44EDTCDE(Z)
      *
     A                                     2MSGCON(36 DIV2023 SYMSGF)
     A                                      SPACEB(2)
     A            ZFLYF          3  0     45EDTCDE(Z)
      *
     A                                     4MSGCON(34 DIV2022 SYMSGF)
     A                                      SPACEB(1)
     A            ZFLYT          3  0     45EDTCDE(Z)
      *
     A                                     2MSGCON(36 DIV2024 SYMSGF)
     A                                      SPACEB(2)
     A            ZDTF           6  0     40EDTCDE(Y)
      *
     A                                     4MSGCON(34 DIV2022 SYMSGF)
     A                                      SPACEB(1)
     A            ZDTT           6  0     40EDTCDE(Y)
      *________
      * Heading
      *""""""""
     A          R SYAV04PB                  SKIPB(4)
     A                                      HIGHLIGHT
     A                                     2MSGCON(38 DIV2032 SYMSGF)
     A            HEFLY     R             41REFFLD(HEFLY SYAV02F)
     A            KFSNAV    R             45REFFLD(KFSNAV KODTFS)
     A                                    84DATE EDTCDE(Y)
     A                                    93TIME EDTWRD('0 :  :  ')
     A                                   103'SYAV04R'
     A                                   111MSGCON(6 DIV2033 SYMSGF)
     A                                    +1PAGNBR EDTCDE(Z)
      *
     A                                     3MSGCON(130 DIV2034 SYMSGF)
     A                                      UNDERLINE
     A                                      SPACEB(2)
      *_____________
      * Detaljlinjer
      *"""""""""""""
     A          R SYAV04PC                  SPACEB(1)
     A            HEAVD     R              2EDTCDE(Z)
     A N31        HEOPD     R              7EDTCDE(Z)
     A  31        HERFA6         6         7
     A N31        HEST      R             15
SV5  A*           HEDTR     R             17EDTCDE(Y)
NV5  A            XEDTOP         6  0     17EDTCDE(Z)
     A N31        HEOT      R             24
     A N31        HEFR      R             27
     A            HEGN11        11        31
     A            HESDF     R             43
     A            HESDT     R             49
     A            DFNTT     R             55REFFLD(DFNTT DOKEF)
     A                                      EDTCDE(Z)
     A            DFVKTT    R             61REFFLD(DFVKTT DOKEF)
     A                                      EDTCDE(M)
     A            WFV01          7  1     71EDTCDE(M)
     A            WFB01          9  0     81EDTCDE(M)
     A            WCC01          9  2     93EDTCDE(K)
     A            WPP01          9  2    108EDTCDE(K)
     A            WMCBEL         3  0    125EDTCDE(K)
      *______________________
      * Totaler på flyselskap
      *""""""""""""""""""""""
     A          R SYAV04PD                  SPACEB(2)
     A                                     2MSGCON(7 DIV2035 SYMSGF)
     A            WANL1          5  0     10EDTCDE(Z)
     A                                    17MSGCON(3 DIV2036 SYMSGF)
     A            WNTL1          5  0     55EDTCDE(Z)
     A            WVEL1          7  1     61EDTCDE(M)
     A            WFVL1          7  1     71EDTCDE(M)
     A            WFBL1          9  0     81EDTCDE(M)
     A            WCCL1          9  2     93EDTCDE(K)
     A            WPPL1          9  2    108EDTCDE(K)
     A            WSSL1          3  0    125EDTCDE(K)
      *
     A                                    36MSGCON(10 DIV2037 SYMSGF)
     A                                      SPACEB(2)
     A            WPPL1X         9  2     72EDTCDE(K)
      *
     A                                    34'-'
     A                                      SPACEB(1)
     A            KFSKOM    R             36REFFLD(KFSKOM KODTFS)
     A                                      EDTCDE(Z)
     A                                    39MSGCON(13 DIV2038 SYMSGF)
     A            WCPL1         11  2     53EDTCDE(K)
     A            WKML1         11  2     70EDTCDE(K)
      *
     A                                    34MSGCON(12 DIV2039 SYMSGF)
     A                                      SPACEB(1)
     A            WFBL1X         9  0     70EDTCDE(K)
      *
     A                                    33MSGCON(9 DIV2040 SYMSGF)
     A                                      UNDERLINE
     A                                      SPACEB(1)
     A                                    42'                                  '
     A                                      UNDERLINE
     A                                    76'        ' UNDERLINE
     A            WSSL1X         3  0     78EDTCDE(K)
      *
     A                                    36MSGCON(5 DIV2041 SYMSGF)
     A                                      SPACEB(1)
     A            WTTL1         13  2     67EDTCDE(K)
      *____________
      * Last Record
      *""""""""""""
     A          R SYAV04PØ                  SPACEB(2)
     A                                     2MSGCON(131 DIV2042 SYMSGF)
