PGM    ( +
         &PCHKUSR      +
       )


   DCL   &PCHKUSR       *CHAR       10


   DCL   &USRSPC        *CHAR      10  VALUE( 'SHCHKUSRAC' )
   DCL   &USRSPCLIB     *CHAR      10  VALUE( 'QTEMP     ' )

   DCL   &QUSRSPC       *CHAR      20

   DCL   &US_HDR        *CHAR     150   /* SPACE HEADER      */


/* NUMBER OF LIST ENTRIES: LISTED JOBS...                           */

   DCL   &NBRLSTE       *DEC      (7)

   /*  THE QUALIFIED SEARCH JOB NAME...                                */
      DCL   &SCHJOB        *CHAR     26


      CHGVAR    &QUSRSPC        ( &USRSPC *CAT &USRSPCLIB )


   /*  CREATE A USRSPC TO HOLD THE JOB LIST DATA FROM THIS USRPRF...   */

      CALL  QUSCRTUS         ( +
                               &QUSRSPC                +
                               'TMPLST    '            +
                               X'00001000'             +
                               X'00'                   +
                               '*ALL      '            +
                               'TEMP LIST USR JOBS     ' +
                               '*YES      '            +
                               X'0000000000000000'     +
                          )



/*                                                                  */
/*  POPULATE THE USRSPC WITH APPROPRIATE JOB LIST DATA...           */
/*                                                                  */

   CHGVAR    &SCHJOB         ( +
                               '*ALL      ' *CAT         +
                               &PCHKUSR     *CAT         +
                               '*ALL      '              +
                             )
 CALL  QUSLJOB          ( +
                            &QUSRSPC                +
                            'JOBL0100'              +
                            &SCHJOB                 +
                            '*ACTIVE   '            +
                            X'0000000000000000'     +
                            'I'                     +
                            X'00000000'             +
                            X'00000000'             +
                          )


/*  RETRIEVE THE HEADER DATA...                                     */

   CALL  QUSRTVUS         ( +
                            &QUSRSPC                +
                            X'00000001'             +
                            X'00000096'             +
                            &US_HDR                 +
                          )

   CHGVAR    &NBRLSTE        %BIN( &US_HDR    133 4 )


   IF ( &NBRLSTE *EQ 0 )  DO
      SNDPGMMSG  MSGID( CPF9898 ) MSGF( QCPFMSG ) +
                   MSGDTA( 'NO ACTIVE JOBS FOUND' )
   ENDDO

   ELSE  IF ( &NBRLSTE *EQ 1 )  DO
      SNDPGMMSG  MSGID( CPF9898 ) MSGF( QCPFMSG ) +
                   MSGDTA( 'EXACTLY ONE JOB FOUND' )
   ENDDO

   ELSE  +
      SNDPGMMSG  MSGID( CPF9898 ) MSGF( QCPFMSG ) +
                   MSGDTA( 'MULTIPLE JOBS FOUND' )


CLEAN_UP:

   DLTUSRSPC   &USRSPCLIB/&USRSPC

   RETURN

ENDPGM
