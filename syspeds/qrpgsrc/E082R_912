     FEDIRE     IF   E           K DISK
     FEFTX912   O  A E             DISK
     FEGDS912   O  A E             DISK
     FEGID912   O  A E             DISK
     FELOC912   O  A E             DISK
     FEMEA912   O  A E             DISK
     FENAD912   O  A E             DISK
     FEPCI912   O  A E             DISK
     FETDT912   O  A E             DISK
     D NU              S              1    DIM(15)
      *____________________________________________________________
      * Syntax of message
      * POS 1-6=MSGTYPE    7-9=SEGM   10-11=GROUP 12-12 LEVEL
      * 13-13=HEADING SEG OF A NEW GROUP
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D SYN             S             13    DIM(12) CTDATA PERRCD(1)
      *____________________________________________________________
      * Last SEG of each BREAK GROUP
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D SEG             S              5    DIM(9)
      *____________________________________________________________
      * Last SEG of each level FROM 1 TO 8
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D SE1             S              3    DIM(99)
     D SE2             S              3    DIM(99)
     D SE3             S              3    DIM(99)
     D SE4             S              3    DIM(99)
     D SE5             S              3    DIM(99)
     D SE6             S              3    DIM(99)
     D SE7             S              3    DIM(99)
     D SE8             S              3    DIM(99)
      *____________________________________________________________
      * COUNTER of each SEG, LVL FROM 1 TO 8
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D RE1             S              4  0 DIM(99)
     D RE2             S              4  0 DIM(99)
     D RE3             S              4  0 DIM(99)
     D RE4             S              4  0 DIM(99)
     D RE5             S              4  0 DIM(99)
     D RE6             S              4  0 DIM(99)
     D RE7             S              4  0 DIM(99)
     D RE8             S              4  0 DIM(99)
     D                 DS
     D  TVIKEY                 1      9
     D  TVIMSG                 1      6
     D  TVISEG                 7      9
     D                 DS
     D  SYNKEY                 1     13
     D  SYNKE2                 1      9
     D  SYNMSG                 1      6
     D  SYNSRP                 7     11
     D  SYNSEG                 7      9
     D  SYNGRP                10     11  0
     D  SYNLVL                12     12  0
     D  SYNBKP                13     13
     D LDADTA         UDS          1024
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D  AA                   301    380
     D                                     DIM(80)
     D  AADS                 301    380
     D  AB                   401    480
     D                                     DIM(80)
     D  ABDS                 401    480
     D  U0065                501    506
     D  SEGNAM               507    509
     D  TN                   601    602  0
     D  EOF                  603    605
     D  TEGN                 606    606
     D  LOKAHE               607    607
     D  UMN                  608    616  0
     D  UMBR                 617    626
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  N91              EXSR      GETSEG
     C   91              GOTO      SLUTT
     C                   EXSR      SEGSYN
     C     SEGNAM        CABEQ     'UNB'         SLUTT
     C     SEGNAM        CABEQ     'UNH'         SLUTT
     C     SEGNAM        CABEQ     'BGM'         SLUTT
     C     SEGNAM        CABEQ     'UNT'         SLUTT
     C     SEGNAM        CABEQ     'UNZ'         SLUTT
      *
     C     SEGNAM        CASEQ     'FTX'         FTX
     C     SEGNAM        CASEQ     'GDS'         GDS
     C     SEGNAM        CASEQ     'GID'         GID
     C     SEGNAM        CASEQ     'LOC'         LOC
     C     SEGNAM        CASEQ     'MEA'         MEA
     C     SEGNAM        CASEQ     'NAD'         NAD
     C     SEGNAM        CASEQ     'PCI'         PCI
     C     SEGNAM        CASEQ     'TDT'         TDT
      *
     C                   END
     C                   GOTO      SEGSTA
     C     SLUTT         TAG
     C                   OUT       LDADTA
     C                   MOVE      *IN91         WIN91
     C                   MOVE      *IN99         WIN99
     C                   RETURN
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
     C     *DTAARA       DEFINE    *LDA          LDADTA
     C                   IN        LDADTA
      * INIT AV ARBEIDSFELT
     C                   MOVE      *ZEROS        NUMVRD           15 0
     C                   MOVE      *ZEROS        NUTEGN            1 0
     C     *LIKE         DEFINE    *IN91         WIN91
     C     *LIKE         DEFINE    *IN99         WIN99
     C     *LIKE         DEFINE    SYNLVL        WL
     C                   MOVEL     'UNOC'        TABLE            10
     C                   MOVEL     '*LIBL'       LIB              10
     C                   Z-ADD     80            LEN               5 0
     C                   MOVE      *BLANKS       STRING           80
      *
      * Datafelt AB i EDIRE (80 lang) går rett inn i array AB ved READ
      * TN=81 soerger for automatisk overflyt array AB->AA ved kall av GETCH
     C                   MOVE      '0'           *IN98
      * Spesielt reserverte tegn defineres her med både grafisk verdi og
      * med en logisk verdi. Den grafiske verdien testes det mot i rutinen
      * som henter tegn. Finner denne rutinen at at tegnet er ment som en
      * spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
      * (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
      * innkommende datastroemmen). '?' når brukt som opphevelse foran annet
      * spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
      * kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.
      *
      * Ved andre standarder for spesialtegn er det bare å bytte verdiene til
      * de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      * Logiske verdier for spesialtegn
      * LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
      * LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   BITON     '16'          LOGPLU            1
     C                   BITOFF    '023457'      LOGPLU
      * LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   BITON     '167'         LOGKOL            1
     C                   BITOFF    '02345'       LOGKOL
      * LOGBLA = utenlandske måten å vise blank.
     C                   BITON     '01234567'    LOGBLA            1
      *________________________
      * Input/output parameters
      *""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WIN91
     C                   PARM                    WIN99
     C                   MOVE      WIN91         *IN91
     C                   MOVE      WIN99         *IN99
      *____________
      * Init arrays
      *""""""""""""
     C                   MOVE      *BLANKS       SEG
     C                   MOVE      *BLANKS       SE1
     C                   MOVE      *BLANKS       SE2
     C                   MOVE      *BLANKS       SE3
     C                   MOVE      *BLANKS       SE4
     C                   MOVE      *BLANKS       SE5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE1
     C                   MOVE      *ZEROS        RE2
     C                   MOVE      *ZEROS        RE3
     C                   MOVE      *ZEROS        RE4
     C                   MOVE      *ZEROS        RE5
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      'BGM00'       SEG(1)
     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste tegn i datastroem             GETCH  /
      *////////////////////////////////////////////////////////////
     C     GETCH         BEGSR
      * Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
      * i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
      * 91 settes når en ved "Toemming" finner at AB-arrayet er tomt.
      *
      * "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
      *Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
      *    98 ON -> kun grafisk betydning (uforandret)
      *    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   MOVE      '0'           *IN98
     C     STACH         TAG
      * Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            91
     C                   GOTO      ENDCH
     C                   ENDIF
     C                   MOVE      ABDS          AADS
     C  N99UMBR          READE     EDIRE                                  99
     C  N99              EXSR      CVTCHR
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF
     C                   Z-ADD     1             TN
     C                   ENDIF
      * Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      80
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   ENDIF
      * Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
      * Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
      *        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
      * Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
      * foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   ELSE
     C     TEGN          IFEQ      LOGBLA
     C                   MOVE      ' '           TEGN
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C     ENDCH         ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste Segment                       GETSEG /
      *////////////////////////////////////////////////////////////
     C     GETSEG        BEGSR
      * "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   ENDDO
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
      * Har nå lest appostrof - de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          SEGNAM
      * La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
      * Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
      *////////////////////////////////////////////////////////////
      * Get Data Element                                   GETDED /
      *////////////////////////////////////////////////////////////
     C     GETDED        BEGSR
     C                   MOVEA     *BLANKS       FE
     C     1             DO        256           NR                3 0
     C                   EXSR      GETCH
     C     TEGN          IFEQ      LOGAPP
     C     TEGN          OREQ      LOGPLU
     C     TEGN          OREQ      LOGKOL
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C     ENDDED        ENDSR
      *////////////////////////////////////////////////////////////
      * CVT characters                                     CVTCHR /
      *////////////////////////////////////////////////////////////
     C     CVTCHR        BEGSR
     C                   CALL      'QDCXLATE'
     C                   PARM                    LEN
     C                   PARM                    RDATA
     C                   PARM                    TABLE
     C                   PARM                    LIB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GET SYNTAX OF MSG                                  GETDED /
      *////////////////////////////////////////////////////////////
     C     SEGSYN        BEGSR
      * FIND LAST BREAK SEGMENT
     C     SEG001        TAG
      *
     C                   MOVEL     U0065         SYNMSG
     C                   MOVE      '*'           SYNBKP
     C                   Z-ADD     9             WX
     C     SEG(WX)       DOWEQ     *BLANKS
     C                   SUB       1             WX
     C     WX            COMP      0                                    2020
     C  N20              ENDDO
     C     WX            IFGT      0
     C                   MOVEL     SEG(WX)       SYNSEG
     C                   MOVE      SEG(WX)       SYNGRP
     C                   Z-ADD     WX            SYNLVL
     C                   SUB       1             SYNLVL
     C                   ENDIF
     C     SYNSEG        IFEQ      *BLANKS
     C                   MOVEL     SEGNAM        SYNSEG
     C                   Z-ADD     0             SYNGRP
     C                   Z-ADD     0             SYNLVL
     C                   ENDIF
     C                   Z-ADD     WX            YX                3 0
      * POSITION TO PREVIOUS BREAK SEGMENT
     C                   Z-ADD     1             WX                3 0
     C     SYNKEY        DOWNE     SYN(WX)
     C                   ADD       1             WX
     C                   ENDDO
      * POSITION TO *CURRENT SEGMENT
     C                   MOVE      U0065         TVIMSG
     C                   MOVE      SEGNAM        TVISEG
     C     TVIKEY        DOWNE     SYNKE2
     C                   ADD       1             WX
     C     WX            IFGT      12
     C     YX            IFGT      0
     C                   MOVE      *BLANKS       SEG(YX)
     C                   GOTO      SEG001
     C                   ELSE
     C                   GOTO      SEG002
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      SYN(WX)       SYNKEY
     C                   ENDDO
      * CHECK IF THIS IS THE RIGHT SEGMENT AT RIGHT LEVEL
     C     SYNLVL        IFGE      2
     C                   MOVE      SYNKEY        WYNKEY           13
     C                   Z-ADD     SYNLVL        WL
     C                   SUB       1             WL
     C     1             DO        12
     C                   SUB       1             WX
     C                   MOVE      SYN(WX)       SYNKEY
     C     SYNLVL        IFEQ      WL
     C     SYNBKP        ANDEQ     '*'
     C                   ADD       1             WL
     C     SYNSRP        IFNE      SEG(WL)
     C                   MOVE      *BLANKS       SEG(YX)
     C                   GOTO      SEG001
     C                   ELSE
     C                   MOVE      WYNKEY        SYNKEY
     C                   GOTO      SEG002
     C                   ENDIF
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      * CLEAR ARRAY FOR ALL BREAK SEGMENTS AT *LE LEVEL
     C     SEG002        TAG
     C     SYNBKP        IFEQ      '*'
     C     SYNLVL        ADD       1             WX
     C     9             SUB       SYNLVL        WZ                3 0
     C     WX            DO        WZ            WY                3 0
     C                   MOVE      *BLANKS       SEG(WY)
     C                   ENDDO
      * ADD THIS SEGMENT TO ARRAY FOR BREAK
     C                   Z-ADD     SYNLVL        WX
     C                   ADD       1             WX
     C                   MOVEL     SYNSEG        SEG(WX)
     C                   MOVE      SYNGRP        SEG(WX)
     C                   ENDIF
      * CLEAR ARRAYS FOR SEGMENT AND REPETITIONS *LT LEVEL
     C     SYNBKP        IFEQ      '*'
     C                   SELECT
     C     SYNLVL        WHENEQ    0
     C                   MOVE      *BLANKS       SE1
     C                   MOVE      *BLANKS       SE2
     C                   MOVE      *BLANKS       SE3
     C                   MOVE      *BLANKS       SE4
     C                   MOVE      *BLANKS       SE5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE1
     C                   MOVE      *ZEROS        RE2
     C                   MOVE      *ZEROS        RE3
     C                   MOVE      *ZEROS        RE4
     C                   MOVE      *ZEROS        RE5
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP0
     C                   MOVE      *ZEROS        WREP1
     C                   MOVE      *ZEROS        WREP2
     C                   MOVE      *ZEROS        WREP3
     C                   MOVE      *ZEROS        WREP4
     C                   MOVE      *ZEROS        WREP5
     C                   MOVE      *ZEROS        WREP6             4 0
     C                   MOVE      *ZEROS        WREP7             4 0
     C                   MOVE      *ZEROS        WREP8             4 0
     C     SYNLVL        WHENEQ    1
     C                   MOVE      *BLANKS       SE2
     C                   MOVE      *BLANKS       SE3
     C                   MOVE      *BLANKS       SE4
     C                   MOVE      *BLANKS       SE5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE2
     C                   MOVE      *ZEROS        RE3
     C                   MOVE      *ZEROS        RE4
     C                   MOVE      *ZEROS        RE5
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP1
     C                   MOVE      *ZEROS        WREP2
     C                   MOVE      *ZEROS        WREP3
     C                   MOVE      *ZEROS        WREP4
     C                   MOVE      *ZEROS        WREP5
     C                   MOVE      *ZEROS        WREP6
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C     SYNLVL        WHENEQ    2
     C                   MOVE      *BLANKS       SE3
     C                   MOVE      *BLANKS       SE4
     C                   MOVE      *BLANKS       SE5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE3
     C                   MOVE      *ZEROS        RE4
     C                   MOVE      *ZEROS        RE5
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP2
     C                   MOVE      *ZEROS        WREP3
     C                   MOVE      *ZEROS        WREP4
     C                   MOVE      *ZEROS        WREP5
     C                   MOVE      *ZEROS        WREP6
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C     SYNLVL        WHENEQ    3
     C                   MOVE      *BLANKS       SE4
     C                   MOVE      *BLANKS       SE5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE4
     C                   MOVE      *ZEROS        RE5
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP3
     C                   MOVE      *ZEROS        WREP4
     C                   MOVE      *ZEROS        WREP5
     C                   MOVE      *ZEROS        WREP6
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C     SYNLVL        WHENEQ    4
     C                   MOVE      *BLANKS       SE5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE5
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP4
     C                   MOVE      *ZEROS        WREP5
     C                   MOVE      *ZEROS        WREP6
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C     SYNLVL        WHENEQ    5
     C                   MOVE      *BLANKS       SE6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE6
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP5
     C                   MOVE      *ZEROS        WREP6
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C     SYNLVL        WHENEQ    6
     C                   MOVE      *BLANKS       SE7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE7
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP6
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C     SYNLVL        WHENEQ    7
     C                   MOVE      *BLANKS       SE8
     C                   MOVE      *ZEROS        RE8
     C                   MOVE      *ZEROS        WREP7
     C                   MOVE      *ZEROS        WREP8
     C                   ENDSL
     C                   ENDIF
     C                   SELECT
      * ADD/UPDATE THIS SEGMENT AND REP LEVEL 1
     C     SYNLVL        WHENEQ    1
     C                   Z-ADD     1             WX
     C     SE1(WX)       DOWNE     *BLANKS
     C     SE1(WX)       COMP      SYNSEG                                 20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE1(WX)       IFEQ      *BLANKS
     C                   MOVEL     SYNSEG        SE1(WX)
     C                   Z-ADD     1             RE1(WX)
     C                   ELSE
     C                   ADD       1             RE1(WX)
     C                   ENDIF
     C                   MOVE      RE1(WX)       WREP0             4 0
      * ADD/UPDATE THIS SEGMENT AND REP LEVEL 2
     C     SYNLVL        WHENEQ    2
     C                   Z-ADD     1             WX
     C     SE2(WX)       DOWNE     *BLANKS
     C     SE2(WX)       COMP      SYNSEG                                 20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE2(WX)       IFEQ      *BLANKS
     C                   MOVEL     SYNSEG        SE2(WX)
     C                   Z-ADD     1             RE2(WX)
     C                   ELSE
     C                   ADD       1             RE2(WX)
     C                   ENDIF
     C                   MOVE      RE2(WX)       WREP1             4 0
      * GET NUMBER OF REP OF BREAK SEGMENT OVER THIS SEG LEVEL 2
     C                   Z-ADD     1             WX
     C     SE1(WX)       DOUEQ     *BLANKS
     C                   MOVEL     SEG(2)        WSEG              3
     C     SE1(WX)       COMP      WSEG                                   20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE1(WX)       IFEQ      *BLANKS
     C                   Z-ADD     1             WREP0             4 0
     C                   ELSE
     C                   MOVE      RE1(WX)       WREP0
     C                   ENDIF
      * ADD/UPDATE THIS SEGMENT AND REP LEVEL 3
     C     SYNLVL        WHENEQ    3
     C                   Z-ADD     1             WX
     C     SE3(WX)       DOWNE     *BLANKS
     C     SE3(WX)       COMP      SYNSEG                                 20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE3(WX)       IFEQ      *BLANKS
     C                   MOVEL     SYNSEG        SE3(WX)
     C                   Z-ADD     1             RE3(WX)
     C                   ELSE
     C                   ADD       1             RE3(WX)
     C                   ENDIF
     C                   MOVE      RE3(WX)       WREP2             4 0
      * GET NUMBER OF REP OF BREAK SEGMENT OVER THIS SEG LEVEL 3
     C                   Z-ADD     1             WX
     C     SE2(WX)       DOUEQ     *BLANKS
     C                   MOVEL     SEG(3)        WSEG
     C     SE2(WX)       COMP      WSEG                                   20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE2(WX)       IFEQ      *BLANKS
     C                   Z-ADD     1             WREP1             4 0
     C                   ELSE
     C                   MOVE      RE2(WX)       WREP1
     C                   ENDIF
      * ADD/UPDATE THIS SEGMENT AND REP LEVEL 4
     C     SYNLVL        WHENEQ    4
     C                   Z-ADD     1             WX
     C     SE4(WX)       DOWNE     *BLANKS
     C     SE4(WX)       COMP      SYNSEG                                 20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE4(WX)       IFEQ      *BLANKS
     C                   MOVEL     SYNSEG        SE4(WX)
     C                   Z-ADD     1             RE4(WX)
     C                   ELSE
     C                   ADD       1             RE4(WX)
     C                   ENDIF
     C                   MOVE      RE4(WX)       WREP3             4 0
      * GET NUMBER OF REP OF BREAK SEGMENT OVER THIS SEG LEVEL 4
     C                   Z-ADD     1             WX
     C     SE3(WX)       DOUEQ     *BLANKS
     C                   MOVEL     SEG(4)        WSEG
     C     SE3(WX)       COMP      WSEG                                   20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE3(WX)       IFEQ      *BLANKS
     C                   Z-ADD     1             WREP2             4 0
     C                   ELSE
     C                   MOVE      RE3(WX)       WREP2
     C                   ENDIF
      * ADD/UPDATE THIS SEGMENT AND REP LEVEL 5
     C     SYNLVL        WHENEQ    5
     C                   Z-ADD     1             WX
     C     SE5(WX)       DOWNE     *BLANKS
     C     SE5(WX)       COMP      SYNSEG                                 20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE5(WX)       IFEQ      *BLANKS
     C                   MOVEL     SYNSEG        SE5(WX)
     C                   Z-ADD     1             RE5(WX)
     C                   ELSE
     C                   ADD       1             RE5(WX)
     C                   ENDIF
     C                   MOVE      RE5(WX)       WREP4             4 0
      * GET NUMBER OF REP OF BREAK SEGMENT OVER THIS SEG LEVEL 5
     C                   Z-ADD     1             WX
     C     SE4(WX)       DOUEQ     *BLANKS
     C                   MOVEL     SEG(YX)       WSEG
     C     SE4(WX)       COMP      WSEG                                   20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE4(WX)       IFEQ      *BLANKS
     C                   Z-ADD     1             WREP3             4 0
     C                   ELSE
     C                   MOVE      RE4(WX)       WREP3
     C                   ENDIF
      * ADD/UPDATE THIS SEGMENT AND REP LEVEL 6
     C     SYNLVL        WHENEQ    6
     C                   Z-ADD     1             WX
     C     SE6(WX)       DOWNE     *BLANKS
     C     SE6(WX)       COMP      SYNSEG                                 20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE6(WX)       IFEQ      *BLANKS
     C                   MOVEL     SYNSEG        SE6(WX)
     C                   Z-ADD     1             RE6(WX)
     C                   ELSE
     C                   ADD       1             RE6(WX)
     C                   ENDIF
     C                   MOVE      RE6(WX)       WREP5             4 0
      * GET NUMBER OF REP OF BREAK SEGMENT OVER THIS SEG LEVEL 5
     C                   Z-ADD     1             WX
     C     SE5(WX)       DOUEQ     *BLANKS
     C                   MOVEL     SEG(YX)       WSEG
     C     SE5(WX)       COMP      WSEG                                   20
     C  N20              ADD       1             WX
     C  N20              ENDDO
     C     SE5(WX)       IFEQ      *BLANKS
     C                   Z-ADD     1             WREP4             4 0
     C                   ELSE
     C                   MOVE      RE5(WX)       WREP4
     C                   ENDIF
     C                   ENDSL
     C                   ENDSR
      /COPY QEDSRC,FTX_I_912
      /COPY QEDSRC,GDS_I_912
      /COPY QEDSRC,GID_I_912
      /COPY QEDSRC,LOC_I_912
      /COPY QEDSRC,MEA_I_912
      /COPY QEDSRC,NAD_I_912
      /COPY QEDSRC,PCI_I_912
      /COPY QEDSRC,TDT_I_912
** SYN
IFTMBFUNH000*
IFTMBFBGM000*
IFTMBFTDT051*
IFTMBFLOC052
IFTMBFNAD061*
IFTMBFGID101*
IFTMBFPCI102
IFTMBFFTX102
IFTMBFGDS122*
IFTMBFFTX123
IFTMBFMEA132*
IFTMBFUNT000*
