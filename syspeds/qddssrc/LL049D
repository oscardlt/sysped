     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '4')
     A            AUUSER        10A  O  5  5
     A            AUNA          30A  O  5 16
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lage ny')
     A                                      CA12(12 'Forrige')
     A                                  1  2'SYSPED/LL049R'
     A                                      COLOR(BLU)
     A                                  1 19'Brukers autorisasjon'
     A                                      DSPATR(HI)
     A                                  1 40DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Lagerkunde:'
     A            UNV           25A  O  2 14DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  3  2'Velg opsjon:   4=Slett'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 46SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Op Bruker-id  Brukernavn'
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lage ny')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F5=Frisk  F6=Lage ny  F12=Avslutt'
     A                                      COLOR(BLU)
     A  30                              1  2'Du må velge riktig opsjon.'
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(08)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'   Bruker-id  Brukernavn'
     A                                      COLOR(WHT)
     A            WSUSER        10A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(HI)
     A N89                                  DSPATR(PC)
     A            WSNA          30A  B  2 16
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A N30                              3  2'F12=avbryt'
     A                                      COLOR(BLU)
     A  90                              3  2'Bruker-id er ugyldig.'
     A                                      COLOR(WHT)
     A  89                              3  2'Brukernavn må oppgis.'
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA06(06 'Lage ny')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL049R'
     A                                      COLOR(BLU)
     A                                  1 19'Brukers autorisasjon'
     A                                      DSPATR(HI)
     A                                  1 40DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Lagerkunde:'
     A            UNV           25A  O  2 14DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4  2'   Bruker-id  Brukernavn'
     A                                      DSPATR(HI)
     A                                 10  2'F6=Lage ny  F12=Avslutt'
     A                                      COLOR(BLU)
