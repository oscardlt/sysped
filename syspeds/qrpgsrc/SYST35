      *---------------------------------------
      * FORELØPIG KUTTE OPPDATERING AV STATXR
      * SØK OP "SLOPDX" FOR Å SE ENDRINGER
      *---------------------------------------
      *
      *     OPPDATERE STATISTIKKREGISTER
      *
      *
      *     INNPARAM - EN FAKTURALINJE ELLER ETT OPPDRAG
      *         (NÅR ET OPPDRAG ER INPUT OPPDATERES KUN ANT./VEK)
      *     KAN OGSÅ BENYTTES FOR Å REGENERERE STATISTIKK M.M.
      *         (AVHENGI AV VERDIEN I INNPARAM XXOPD)
      *         (HE01=J  ANTALL/VEKT ER OPPDATERT FOR DETTE OPPDRAGET)
      * ORDINÆR KJØRING:
      * XXOPD='N' En FAKTlinje (inntekt/kostnad) er innparameter
      *           Oppdater beløpsfeltene.
      *           OM HE01 ULIK 'J' Oppdater ant/vekt OG HE01.
      * XXOPD='J' Et OPPDRAG (uavhengig av fakt.linjer) er innparameter
      *           OM HE01 ULIK 'J' Oppdater ant/vekt OG HE01.
      * REGENERERING M.M.:
      * XXOPD='0' En FAKTlinje (inntekt/kostnad) er innparameter
      *           Oppdater beløpsfeltene. IKKE OPPDATER ANT/VEKT - HE01
      * XXOPD='1' Et OPPDRAG (uavhengig av fakt.linjer) er innparameter
      *           Oppdater ant/vekt UAVHENGIG AV HE01 (Ikke oppd. HE01)
      *
      *
      *
     FBLV1      IF   E           K DISK
     FFAKT      UF   E           K DISK
     FFAK10     IF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR10)
     FKODTV     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTOT2   IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FSTED2D    IF   E           K DISK
     F                                     RENAME(STED2R:STED2RD)
     FFIRM      IF   E           K DISK
     FFIRSTA    UF   E           K DISK
     FSTATXR    IF A E           K DISK
     FDISP      IF   E           K DISK
     FTURER     IF   E           K DISK
     FHEADF     UF   E           K DISK
     FSTATHFL1  UF A F  590    25AIDISK    KEYLOC(1)
     FSTATDFL1  UF A E           K DISK
     FSTATOHL1  UF A F  590    25AIDISK    KEYLOC(1)
     FSTATODL1  UF A E           K DISK
     F                                     RENAME(STATDFR:STATODFR)
     D FV              S             11  0 DIM(13)
     D AV              S             11  0 DIM(13)
     D AS              S              9  0 DIM(13)
     D VG              S              9  0 DIM(13)
     D AT              S              9  0 DIM(13)
     D SS              S              9  0 DIM(13)
     D VS              S             11  0 DIM(13)
     D MS              S              9  3 DIM(13)
     D                 DS
     D  FADATO                 1      8  0
     D  FADATÅ                 1      4  0
     D  FADATM                 5      6  0
     D  FADATD                 7      8  0
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D                 DS
     D  HEDTR                  1      8  0
     D  HEDTRÅ                 1      4  0
     D  HEDTRM                 5      6  0
     D  HEDTRD                 7      8  0
     D                 DS
     D  STBL01                 1      5P 0
     D  STBL02                 6     10P 0
     D  STBL03                11     15P 0
     D  STBL04                16     20P 0
     D  STBL05                21     25P 0
     D  STBL06                26     30P 0
     D  STBL07                31     35P 0
     D  STBL08                36     40P 0
     D  STBL09                41     45P 0
     D  STBL10                46     50P 0
     D  STBL11                51     55P 0
     D  STBL12                56     60P 0
     D  STBL13                61     65P 0
     D  BL                     1     65P 0
     D                                     DIM(13)
     D                 DS
     D  TUSONF                 1      5
     D  TULANF                 1      2
     D                 DS
     D  TUSONT                 1      5
     D  TULANT                 1      2
     D*--------------------------------------------------------------
     D* DEFINERER DATASTRUKTURER
     D*--------------------------------------------------------------
     D                 DS
     D  KOVK1                  1      5  0
     D  KOVK2                  6     10  0
     D  KOVK3                 11     15  0
     D  KOVK4                 16     20  0
     D  KOVK5                 21     25  0
     D  KOVK6                 26     30  0
     D  KOVK7                 31     35  0
     D  KOVK8                 36     40  0
     D  KOVK9                 41     45  0
     D  KOVK10                46     50  0
     D  KOVK11                51     55  0
     D  FF                     1     65  0
     D                                     DIM(13) ASCEND
     D                 DS
     D  SFAST                  1      1
     D  YDTC                   2      3  0
     D  YDTÅ                   4      5  0
     D  HEAVD                  6      9  0
     D  KUNDE                 10     17
     D  SBLANK                18     25
     D  STAKEY                 1     25
     ISTATHFL1  NS
     I                             P   26  103  FV
     I                             P  104  181  AV
     I                             P  182  246  AS
     I                             P  247  311  VG
     I                             P  312  376  AT
     I                             P  377  441  SS
     I                             P  442  519  VS
     I                             P  520  584  MS
     I                             P  585  590 0STSTNR
     ISTATOHL1  NS
     I                             P   26  103  FV
     I                             P  104  181  AV
     I                             P  182  246  AS
     I                             P  247  311  VG
     I                             P  312  376  AT
     I                             P  377  441  SS
     I                             P  442  519  VS
     I                             P  520  584  MS
     I                             P  585  590 0STSTNR
     IFAKTR10
     I              FA01                        F001
     I              FALI                        F0LI
     I              FAAVD                       F0AVD
     I              FAOPD                       F0OPD
     I              FASK                        F0SK
     I              FAVK                        F0VK
     I              FAVT                        F0VT
     I              FAVAL                       F0VAL
     I              FABELV                      F0BELV
     I              FAKDM                       F0KDM
     I              FABELN                      F0BELN
     I              FAKDA                       F0KDA
     I              FAKDUL                      F0KDUL
     I              FA01B                       F001B
     I              FAKDUK                      F0KDUK
     I              FABELU                      F0BELU
     I              FAKUNR                      F0KUNR
     I              FAFAKT                      F0FAKT
     I              FAOPKO                      F0OPKO
     I              FAJOUR                      F0JOUR
     I              FAÅR                        F0ÅR
     I              FAMND                       F0MND
     I              FAFRBN                      F0FRBN
     I              FADATO                      F0DATO
     I              FADAFF                      F0DAFF
     I              FAAVDR                      F0AVDR
     I              FABÆR                       F0BÆR
     I              FALEVN                      F0LEVN
     I              FAMADE                      F0MADE
     I              FAAGRN                      F0AGRN
     I              FAACCN                      F0ACCN
     I              FAEXRA                      F0EXRA
     I              FATAXA                      F0TAXA
     I              FACURI                      F0CURI
     I              FAAMOU                      F0AMOU
     I              FADOCN                      F0DOCN
     I              FACD11                      F0CD11
     I              FACD12                      F0CD12
     I              FACD13                      F0CD13
     I              FACD21                      F0CD21
     I              FACD22                      F0CD22
     I              FACD23                      F0CD23
     I              FACD31                      F0CD31
     I              FACD32                      F0CD32
     I              FACU33                      F0CU33
     I              FAGGRP                      F0GGRP
     C*
     C                   EXSR      INIT
     C     *IN33         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END
     C* OPPDATER  "IKKE KUNDERELEATERTE" BEGREPER (Oppdrags-stat)
     C                   EXSR      OPSTSR
     C* OPPDATER  "KUNDERELEATERTE" BEGREPER (Kundestat)
     C                   MOVE      FAKUNR        KUNDE
     C                   EXSR      OPSTKU
     C*
     C     SLUTT         TAG
     C                   RETURN
     C*
     C******************************************
     C     OPSTSR        BEGSR
     C******************************************
     C* STATISTISKE OPPDATERINGER
     C*        "IKKE KUNDERELATERTE" BEGREP.
     C*        (SE OPSTKU FOR  "KUNDERELATERTE" BEGR.)
     C*
     C*
     C* BRUKTE UNIKE BEGREPSKODER (SFAST):
     C*
     C*      A =   AGENT
     C*      B =   AVDELING
     C*      C =   RUNDTUR
     C*      D =   FAKTURAMOTTAGER / OPPDRAGSTYPE
E08  C*      E =   AGENT / FRANKATUR
     C*      F =   FRANKATUR
     C*      G =   FAKTURAMOTTAGER / FRANKATUR
N01  C*      H =   FAKTURAMOTTAGER / FRA STED
N01  C*      I =   FAKTURAMOTTAGER / TIL STED
     C*      J =   KUNDENR. SOM "PART" PÅ OPPDRAG
     C*      K =   FAKTURERT KUNDE
     C*      L =   LANDKODE
     C*      M =   REGIONKODE
     C*      N =   FRA STED / TIL STED
     C*      O =   OPPDRAGSTYPE
     C*      P =   (STED FRA)
     C*      Q =   (STED TIL)
     C*      R =   TUR
     C*      S =   SIGNATUR
     C*      T =   TRANSPORTØR
     C*      U =   FAKTURERT KUNDE / FRA LAND - TIL LAND
     C*      V =   FAKTURERT KUNDE / AGENT
     C*      W =   VAREMOTTAKER (XUN NÅR KJØPER ULIK KJØPERS F.MOT.)
     C*      X =   FAKTURERT KUNDE / FRA REGION - TIL REGION
     C*      Y =   BILNUMMER   (KUN NÅR FRA TRANSP. MODUL)
     C*      Z =   BILKODE     (KUN NÅR FRA TRANSP. MODUL)
     C*
      *
      *==========================================================
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      'N'           KUNDUP            1
      *
     C                   MOVE      *BLANKS       XLKF              2
     C                   MOVE      *BLANKS       XLKT              2
     C                   MOVE      HESDF         XSDF              5
     C                   MOVE      HESDT         XSDT              5
     C     HEUR          IFEQ      'H'                                          1<-B
      * TRANSPORT MODUL
     C                   MOVE      HELKA         XLKF
     C                   MOVE      HETRI         XLKT
     C     KOAPOS        IFEQ      'J'                                           2<-B
      * POSTAL --> HENT BASIS STED
     C                   MOVE      HELKA         ST2LK
     C                   MOVE      XSDF          ST2KOD
     C     STE2DK        CHAIN     STED2D                             40
     C     *IN40         IFEQ      '0'                                            3<-B
     C     ST2KO2        ANDNE     *BLANKS
     C                   MOVE      ST2KO2        XSDF
     C                   END                                                      3<-E
     C                   MOVE      HETRI         ST2LK
     C                   MOVE      XSDT          ST2KOD
     C     STE2DK        CHAIN     STED2D                             40
     C     *IN40         IFEQ      '0'                                            3<-B
     C     ST2KO2        ANDNE     *BLANKS
     C                   MOVE      ST2KO2        XSDT
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C                   ELSE                                                   1
      * IKKE POSTAL BRUKER FOR FRAKT/VIDERE FRAKT HVIS DET FINNES.
      * ELLERS BRUKER FRA/TIL STED.
      *
     C     HESDFF        IFEQ      *BLANKS                                       2<-B
     C     KOAPOS        OREQ      'J'
     C     HEUR          IFEQ      'A'                                            3<-B
     C     HEUR          OREQ      'C'
     C     HEUR          OREQ      'F'
     C                   MOVE      HELKA         XLKF
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C                   MOVE      HESDFF        XSDF
     C                   MOVE      HELKS         XLKF
     C                   END                                                     2<-E
     C     XLKF          IFEQ      *BLANKS                                       2<-B
     C     XSDF          CHAIN     STED2                              40
     C     ST2LK         IFEQ      *BLANKS                                        3<-B
     C                   MOVE      FILAND        XLKF
     C                   ELSE                                                     3
     C                   MOVE      ST2LK         XLKF
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C     HESDVT        IFEQ      *BLANKS                                       2<-B
     C     KOAPOS        OREQ      'J'
     C     HEUR          IFNE      'A'                                            3<-B
     C     HEUR          ANDNE     'C'
     C     HEUR          ANDNE     'F'
     C                   MOVE      HELKA         XLKT
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C                   MOVE      HESDVT        XSDT
     C                   MOVE      HELKK         XLKT
     C                   END                                                     2<-E
     C     HEUR          IFEQ      'E'                                           2<-B
     C                   MOVE      XLKF          XLKT
     C                   END                                                     2<-E
     C     XLKT          IFEQ      *BLANKS                                       2<-B
     C     XSDT          CHAIN     STED2                              40
     C     ST2LK         IFEQ      *BLANKS                                        3<-B
     C                   MOVE      FILAND        XLKT
     C                   ELSE                                                     3
     C                   MOVE      ST2LK         XLKT
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
     C                   MOVEL     XLKF          XLK4              4            FRA/TIL LAND
     C                   MOVE      XLKT          XLK4
      * HENT REGION
     C                   MOVE      XLKF          KLKLK
     C     KODLKK        CHAIN     KODTLK                             40
     C                   MOVEL     KLKLK2        XRG4              4            F/T REGION
     C                   MOVE      XLKT          KLKLK
     C     KODLKK        CHAIN     KODTLK                             40
     C                   MOVE      KLKLK2        XRG4
      *
     C                   MOVEL     XLKF          XSD81             8            F LAND/STED
     C                   MOVE      XSDF          XSD81
     C                   MOVEL     XLKT          XSD82             8            T LAND/STED
     C                   MOVE      XSDT          XSD82
      *==========================================================
      *
     C*
     C* OPPDATERER BILNUMMER I TRANSPORTMODUL
     C     FISTY         IFEQ      'X'
     C     HEUR          ANDEQ     'H'
     C     TUBILN        ANDNE     *BLANKS
     C                   MOVE      'Y'           SFAST
     C                   MOVEL     TUBILN        KUNDE
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER AGENT
     C     FISTA         IFEQ      'X'
     C                   MOVE      HEKNA         KUNDE
     C     KUNDE         IFNE      *BLANKS
     C     KUNDE         ANDNE     *ZEROS
     C                   MOVE      'A'           SFAST
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER TRANSPORTØR
     C     FISTT         IFEQ      'X'
     C     HEUR          IFEQ      'H'
     C                   MOVE      TUKNT2        KUNDE
     C                   ELSE
     C                   MOVE      HEKNT         KUNDE
     C                   END
     C     KUNDE         IFNE      *BLANKS
     C     KUNDE         ANDNE     *ZEROS
     C                   MOVE      'T'           SFAST
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER LANDKODEN
     C     FISTL         IFEQ      'X'
     C     XLKF          ANDNE     *BLANKS
     C     XLKT          ANDNE     *BLANKS
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     XLK4          KUNDE
     C                   MOVE      'L'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER REGIONKODEN
     C     FISTM         IFEQ      'X'
     C     XLK4          ANDNE     XRG4
     C                   MOVEL     XRG4          XRGF              2
     C                   MOVE      XRG4          XRGT              2
     C     XRGF          IFNE      *BLANKS
     C     XRGT          ANDNE     *BLANKS
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     XRG4          KUNDE
     C                   MOVE      'M'           SFAST
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER SIGNATUR
     C     FISTS         IFEQ      'X'
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     HESG          KUNDE
     C                   MOVE      'S'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER BILKODE I DISPONERINGSSYSTEM
     C     FISTZ         IFEQ      'X'
     C     HEUR          ANDEQ     'H'
     C     TUBILK        ANDNE     *BLANKS
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     TUBILK        KUNDE
     C                   MOVE      'Z'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER AVDELINGEN
     C     FISTB         IFEQ      'X'
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     HEAVD         KUNDE
     C                   MOVE      'B'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER TUR
     C     FISTR         IFEQ      'X'
     C     HEPRO         ANDNE     0
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     HEPRO         KUNDE
     C                   MOVE      'R'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER RUNDTUR
     C     FISTC         IFEQ      'X'
     C     HEUR          ANDEQ     'H'
     C     TURUND        ANDNE     0
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     TURUND        KUNDE
     C                   MOVE      'C'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FRA STED / TIL STED
     C     FISTN         IFEQ      'X'
     C     XLKF          ANDNE     *BLANKS
     C     XSDF          ANDNE     *BLANKS
     C     XLKT          ANDNE     *BLANKS
     C     XSDT          ANDNE     *BLANKS
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVE      XSD81         KUNDE
     C                   MOVE      XSD82         SBLANK
     C                   MOVE      'N'           SFAST
     C                   EXSR      OPPDAT
     C                   MOVE      *BLANKS       SBLANK
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER OPPDRAGSTYPE
     C     FISTO         IFEQ      'X'
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     HEOT          KUNDE
     C                   MOVE      'O'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FRANKATUR
     C     FISTF         IFEQ      'X'
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVEL     HEFR          KUNDE
     C                   MOVE      'F'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FRASTED
     C     FISTP         IFEQ      'X'
     C     XLKF          ANDNE     *BLANKS
     C     XSDF          ANDNE     *BLANKS
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVE      XSD81         KUNDE
     C                   MOVE      'P'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER TILSTED
     C     FISTQ         IFEQ      'X'
     C     XLKT          ANDNE     *BLANKS
     C     XSDT          ANDNE     *BLANKS
     C                   MOVE      *BLANKS       KUNDE
     C                   MOVE      XSD82         KUNDE
     C                   MOVE      'Q'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C                   ENDSR
     C*
     C******************************************
     C     OPSTKU        BEGSR
     C******************************************
      * Sett flagg slik at Dupoppdrag ikke endre ant/vekt i kundestat.
     C     HEUR          IFEQ      'H'
     C     TRSLAG        ANDNE     'HO'
     C                   MOVE      'J'           KUNDUP
     C                   END
      *
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      *IN90         XIN90             1
     C     *IN90         IFEQ      '0'
     C     XSTFA         ANDEQ     'J'
     C                   MOVE      '1'           *IN90
     C                   END
     C* OPPDATERER FAKT.KUNDE
     C     FISTK         IFEQ      'X'
     C                   MOVE      'K'           SFAST
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FAKT.KUNDE/FRA STED
     C     FISTH         IFEQ      'X'
     C     XLKF          ANDNE     *BLANKS
     C     XSDF          ANDNE     *BLANKS
     C                   MOVE      'H'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      XSD81         SBLANK
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FAKT.KUNDE/TIL STED
     C     FISTI         IFEQ      'X'
     C     XLKT          ANDNE     *BLANKS
     C     XSDT          ANDNE     *BLANKS
     C                   MOVE      'I'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      XSD82         SBLANK
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FAKT.KUNDE/LAND
     C     FISTU         IFEQ      'X'
     C     XLKF          ANDNE     *BLANKS
     C     XLKT          ANDNE     *BLANKS
     C                   MOVE      'U'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      XLK4          SBLANK
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FAKT.KUNDE/REGION
     C     FISTX         IFEQ      'X'
     C     XLK4          ANDNE     XRG4
     C                   MOVEL     XRG4          XRGF              2
     C                   MOVE      XRG4          XRGT              2
     C     XRGF          IFNE      *BLANKS
     C     XRGT          ANDNE     *BLANKS
     C                   MOVE      'X'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      XRG4          SBLANK
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FAKT.KUNDE/AGENT
     C     FISTV         IFEQ      'X'
     C                   MOVE      'V'           SFAST
     C                   MOVE      HEKNA         SBLANK
     C     SBLANK        IFNE      *BLANKS
     C     SBLANK        ANDNE     *ZEROS
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER FAKT.KUNDE/OPPDRAGSTYPE
     C     FISTD         IFEQ      'X'
     C                   MOVE      'D'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      HEOT          SBLANK
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
      *
      * OPPDATERER FAKT.KUNDE/FRANKATUR
     C     FISTG         IFEQ      'X'
     C                   MOVE      'G'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      HEFR          SBLANK
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
      *
     C                   MOVE      XIN90         *IN90
      *
      * OPPDATERER KUNDER SOM "PART"
     C     FISTJ         IFEQ      'X'
     C                   MOVE      'J'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C     HEKNS         IFNE      0
     C                   MOVE      HEKNS         KUNDE
     C                   EXSR      OPPDAT
     C                   END
     C     HEKNSF        IFNE      0
     C     HEKNSF        ANDNE     HEKNS
     C                   MOVE      HEKNSF        KUNDE
     C                   EXSR      OPPDAT
     C                   END
     C     HEKNK         IFNE      0
     C     HEKNK         ANDNE     HEKNS
     C     HEKNK         ANDNE     HEKNSF
     C                   MOVE      HEKNK         KUNDE
     C                   EXSR      OPPDAT
     C                   END
     C     HEKNKF        IFNE      0
     C     HEKNKF        ANDNE     HEKNS
     C     HEKNKF        ANDNE     HEKNSF
     C     HEKNKF        ANDNE     HEKNK
     C                   MOVE      HEKNKF        KUNDE
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
     C*
     C* OPPDATERER UNDERKUNDE (VAREMOTTAKER NÅR FORSKJELLIG FRA F.MOT)
     C     FISTW         IFEQ      'X'
     C                   MOVE      'W'           SFAST
     C                   MOVE      *BLANKS       SBLANK
     C     HEKNS         IFNE      0
     C     HEKNS         ANDNE     HEKNSF
     C                   MOVE      HEKNS         KUNDE
     C                   EXSR      OPPDAT
     C                   END
     C     HEKNK         IFNE      0
     C     HEKNK         ANDNE     HEKNKF
     C     HEKNK         ANDNE     HEKNS
     C                   MOVE      HEKNK         KUNDE
     C                   EXSR      OPPDAT
     C                   END
     C                   END                                                    *FIST? IF...
      *
      * OPPDATERER AGENT/FRANKATUR
     C     FISTE         IFEQ      'X'
     C     HEKNA         ANDNE     0
     C                   MOVE      'E'           SFAST
     C                   MOVE      HEKNA         KUNDE
     C                   MOVE      *BLANKS       SBLANK
     C                   MOVE      HEFR          SBLANK
     C                   EXSR      OPPDAT
     C                   END                                                    *FIST? IF...
     C*
     C                   ENDSR
     C*
     C******************************************
     C     OPPDAT        BEGSR
     C******************************************
     C     KOVPRO        IFEQ      'F'
     C     KOVPRO        OREQ      'B'
     C                   MOVE      FACC          YDTC
     C                   MOVE      FAÅR          YDTÅ
     C     FAMND         IFEQ      13
     C                   Z-ADD     12            PMN
     C                   ELSE
     C                   MOVE      FAMND         PMN               2 0
     C                   END
     C     STAKEY        CHAIN     STATHFL1                           40
     C                   EXSR      OPDXRE                                       *XREF OPPDRA
     C   40              EXSR      MOVEZ
     C                   EXSR      ADDVER
     C   40              EXCEPT    NY
     C  N40              EXCEPT    GML
     C     FAGGRP        IFNE      *BLANKS
      * OPPDATER BELØP
     C     STAD1K        CHAIN     STATDFL1                           40
     C   40              MOVEA     *ZEROS        BL
     C                   ADD(H)    FABELN        BL(PMN)
     C                   ADD(H)    FABELN        BL(13)
     C   40              MOVE      FAGGRP        STGR
     C   40              WRITE     STATDFR
     C  N40              UPDATE    STATDFR
      * OPPDATER BELØP PÅ TOTALL AV INNTEKT/KOSTNAD
     C     STADXK        CHAIN     STATDFL1                           40
     C   40              MOVEA     *ZEROS        BL
     C                   ADD(H)    FABELN        BL(PMN)
     C                   ADD(H)    FABELN        BL(13)
     C   40              MOVE      XGRTOT        STGR
     C   40              WRITE     STATDFR
     C  N40              UPDATE    STATDFR
     C                   END
     C                   END
      *
     C     KOVPRO        IFEQ      'O'
     C     KOVPRO        OREQ      'B'
     C     HEDTM         IFEQ      0
     C                   MOVEL     HEDTRÅ        YDTC
     C                   MOVE      HEDTRÅ        YDTÅ
     C                   MOVE      HEDTRM        PMN
     C                   ELSE
     C                   MOVEL     HEDTÅ         YDTC
     C                   MOVE      HEDTÅ         YDTÅ
     C                   MOVE      HEDTM         PMN               2 0
     C                   END
     C     STAKEY        CHAIN     STATOHL1                           40
     C                   EXSR      OPDXRE                                       *XREF OPPDRA
     C   40              EXSR      MOVEZ
     C                   EXSR      ADDVER
     C   40              EXCEPT    NYO
     C  N40              EXCEPT    GMLO
     C     FAGGRP        IFNE      *BLANKS
      * OPPDATER BELØP
     C     STAD1K        CHAIN     STATODL1                           40
     C   40              MOVEA     *ZEROS        BL
     C                   ADD(H)    FABELN        BL(PMN)
     C                   ADD(H)    FABELN        BL(13)
     C   40              MOVE      FAGGRP        STGR
     C   40              WRITE     STATODFR
     C  N40              UPDATE    STATODFR
      * OPPDATER BELØP PÅ TOTALL AV INNTEKT/KOSTNAD
     C     STADXK        CHAIN     STATODL1                           40
     C   40              MOVEA     *ZEROS        BL
     C                   ADD(H)    FABELN        BL(PMN)
     C                   ADD(H)    FABELN        BL(13)
     C   40              MOVE      XGRTOT        STGR
     C   40              WRITE     STATODFR
     C  N40              UPDATE    STATODFR
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     OPDXRE        BEGSR
     C******************************************
     C* NY STATPOST - HENT UNIKT LØPENR.
     C   40              DO
     C     FIFIRM        CHAIN     FIRSTA                             41
     C  N41              Z-ADD     FISTNR        STSTNR
     C  N41              ADD       1             FISTNR
     C  N41              UPDATE    FIRSTAR
     C                   END
     C* LEGG UT X-REF MELLOM UNIKT NR OG OPPDRAGET -
     C* NY POST MED HE01=J:DETTE BEGREPET ER KUN VIA TILLEGSLINJER
      *
      *
      * FORELØPIG KUTTE !!!!!  OPPDATERING AV STATXR
      *
     C                   GOTO      SLOPDX
      *
     C     STAXRK        CHAIN     STATXR                             41
     C     *IN41         IFEQ      '1'
     C                   MOVE      STSTNR        STXNR
     C                   MOVE      HEAVD         STXAVD
     C                   MOVE      HEOPD         STXOPD
     C                   MOVE      HE01          STXTIL                          *TILLEGSLIN
     C                   WRITE     STATXRR
     C                   END
     C     SLOPDX        TAG
     C*
     C                   ENDSR
     C******************************************
     C     ADDVER        BEGSR
     C******************************************
     C*Ant/vekt: - sett av parametre sjekket i INIT  *in90 On=oppdater
      * MEN IKKE akkum. ant/vekt hvis det er kunde-stat av dup-oppdrag.
     C     KUNDUP        IFNE      'J'
     C     HE01          ANDEQ     'J'
     C     *IN90         IFEQ      '1'
     C                   EXSR      ADD2
     C                   ELSE
     C     UTREKK        IFEQ      'J'
     C                   EXSR      SUB2
     C                   END
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C******************************************
     C     ADD2          BEGSR
     C******************************************
     C* ADDERER ANTALL OPPDRAG OG VEKTER
     C* TIL STATISTIKK REGISTER
     C* UTFØRES IKKE HVIS OPPDRAGSTYPE SIER N=NEI
     C* ELLER STATUS = O - TILLEGGSFAKTURERING
     C*
     C                   EXSR      HENTCO
     C                   ADD       1             AS(PMN)
     C                   ADD       HEVKT         AV(PMN)
     C                   ADD       HEFBV         FV(PMN)
     C                   ADD       1             VG(VV)
     C                   ADD       1             AS(13)
     C                   ADD       HEVKT         AV(13)
     C                   ADD       HEFBV         FV(13)
     C                   ADD       1             VG(13)
     C     XANTCO        IFEQ      0                                             2<-B
     C                   ADD       1             SS(PMN)
     C                   ADD       1             SS(13)
     C                   ADD       HEVKT         VS(PMN)
     C                   ADD       HEVKT         VS(13)
     C                   ADD       HEM3          MS(PMN)
     C                   ADD       HEM3          MS(13)
     C                   ELSE                                                    2
     C                   ADD       XANTCO        AT(PMN)
     C                   ADD       XANTCO        AT(13)
     C                   END                                                     2<-E
      *
     C                   ENDSR
     C******************************************
     C     SUB2          BEGSR
     C******************************************
     C* TREKKER ANTALL OPPDRAG OG VEKTER
     C* FRA STATISTIKK REGISTER
     C*
     C                   EXSR      HENTCO
     C                   SUB       1             AS(PMN)
     C                   SUB       HEVKT         AV(PMN)
     C                   SUB       HEFBV         FV(PMN)
     C                   SUB       1             VG(VV)
     C                   SUB       1             AS(13)
     C                   SUB       HEVKT         AV(13)
     C                   SUB       HEFBV         FV(13)
     C                   SUB       1             VG(13)
     C     XANTCO        IFEQ      0                                             2<-B
     C                   SUB       1             SS(PMN)
     C                   SUB       1             SS(13)
     C                   SUB       HEVKT         VS(PMN)
     C                   SUB       HEVKT         VS(13)
     C                   SUB       HEM3          MS(PMN)
     C                   SUB       HEM3          MS(13)
     C                   ELSE                                                    2
     C                   SUB       XANTCO        AT(PMN)
     C                   SUB       XANTCO        AT(13)
     C                   END                                                     2<-E
      *
     C                   ENDSR
     C******************************************
     C     HENTCO        BEGSR
     C******************************************
     C     BLV1K         SETLL     BLV1                               35
     C                   MOVE      *ZEROS        XANTCO            9 0
     C     STHCO         TAG
     C     BLV1K         READE     BLV1                                   35
     C  N35              DO
     C     BVNT          MULT      BVENH         XWORK             7 0
     C                   ADD       XWORK         XANTCO
     C                   END
     C  N35              GOTO      STHCO
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVEZ         BEGSR
     C******************************************
     C                   MOVE      *ZEROS        FV
     C                   MOVE      *ZEROS        AV
     C                   MOVE      *ZEROS        AS
     C                   MOVE      *ZEROS        VG
     C                   MOVE      *ZEROS        AT
     C                   MOVE      *ZEROS        SS
     C                   MOVE      *ZEROS        VS
     C                   MOVE      *ZEROS        MS
     C                   ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UXOPD             1
     C                   PARM                    UEND              1
     C                   PARM                    UTREKK            1
      *
     C     UEND          IFEQ      'J'
     C                   SETON                                        LR
     C                   RETURN
     C                   END
      *
     C                   MOVE      UAVD          FAAVD
     C                   MOVE      UOPD          FAOPD
     C                   MOVE      ULI           FALI
     C                   MOVE      UXOPD         XXOPD             1
      * PARAM XXOPD:HVA SKAL OPPDATERES
      * XXOPD='N' En FAKTlinje (inntekt/kostnad) er innparameter
      *           Oppdater beløpsfeltene.
      *           OM HE01 ULIK 'J' Oppdater ant/vekt OG HE01.
      * XXOPD='J' Et OPPDRAG (uavhengig av fakt.linjer) er innparameter
      *           OM HE01 ULIK 'J' Oppdater ant/vekt OG HE01.
      * XXOPD='0' En FAKTlinje (inntekt/kostnad) er innparameter
      *           Oppdater beløpsfeltene. IKKE OPPDATER ANT/VEKT - HE01
      * XXOPD='1' Et OPPDRAG (uavhengig av fakt.linjer) er innparameter
      *           Oppdater ant/vekt UAVHENGIG AV HE01 (Ikke oppd. HE01)
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE V (AVD. VEKTGRUPPER)
     C*--------------------------------------------------------------
     C     KODVKE        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    KOVAVD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE A (AVD.OPPLYSNINER)
     C*--------------------------------------------------------------
     C     KODAKE        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    KOAAVD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE LK (LANDKODE)
     C*--------------------------------------------------------------
     C     KODLKK        KLIST
     C                   KFLD                    KLKUNI
     C                   KFLD                    KLKLK
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE GE (GEBYRKODER)
     C*--------------------------------------------------------------
     C     GEBKEY        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODE-TEKST-FIL TYPE O2 (OPPD.TYPER)
     C*--------------------------------------------------------------
     C     KOO2KE        KLIST
     C                   KFLD                    KO2UNI
     C                   KFLD                    HEOT
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAKT
     C*--------------------------------------------------------------
     C     FAKKEY        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C                   KFLD                    FALI
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK10
     C*--------------------------------------------------------------
     C     FAK10K        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FAKUNR
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEADINGREGISTER  HEAD
     C*--------------------------------------------------------------
     C     HEAKEY        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C*
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL X-REF REGISTER  STAT/HEAD
     C*--------------------------------------------------------------
     C     STAXRK        KLIST
     C                   KFLD                    STSTNR
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL TURER
     C*--------------------------------------------------------------
     C     TURKEY        KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL BLV1  VARELINJER AV BILL OF LADING
     C*--------------------------------------------------------------
     C     BLV1K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL STATDFL1/STATODL1
     C*--------------------------------------------------------------
     C     STAD1K        KLIST
     C                   KFLD                    STSTNR
     C                   KFLD                    FAGGRP
     C     STADXK        KLIST
     C                   KFLD                    STSTNR
     C                   KFLD                    XGRTOT
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL STED2D
     C*--------------------------------------------------------------
     C     STE2DK        KLIST
     C                   KFLD                    ST2LK
     C                   KFLD                    ST2KOD
     C*
     C                   MOVE      'V'           KOVUNI
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      'GE'          KGEUNI
     C                   MOVE      'O2Y'         KO2UNI
     C                   MOVE      'LK '         KLKUNI
     C*
     C* OPPDRAGSDATA SKAL HENTES UANSETT INNPARAMETRE
     C                   MOVE      *BLANKS       TUBILK
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'                                          1<-B
     C     HEUR          IFEQ      'H'                                           2<-B
     C     HEAKEY        CHAIN     DISP                               34
     C  N34TURKEY        CHAIN     TURER                              34
     C     *IN34         IFEQ      '1'                                            3<-B
     C                   MOVE      HELKA         TULANF
     C                   MOVE      HETRI         TULANT
     C                   MOVE      *BLANKS       TUBILN
     C                   MOVE      *BLANKS       TUHENG
     C                   MOVE      *BLANKS       TUBILK
     C                   MOVE      *ZEROS        TUKNT2
     C                   MOVE      *ZEROS        TURUND
     C                   END                                                      3<-E
     C     HETRI         IFNE      '  '                                           3<-B
     C                   MOVE      HELKA         TULANF
     C                   MOVE      HETRI         TULANT
     C                   END                                                      3<-E
     C*
     C                   END                                                     2<-E
     C*Sett indikator for hva som skal oppdateres avhengig av parametre
     C*  *IN90 ON = Oppdater antall og vekt
     C*
     C                   MOVEA     '00'          *IN(90)
     C                   MOVE      ' '           XSTFA             1
     C*ANT/VEKT:Oppdragstype på IKKE ha "N" for oppdater antall/vekt
     C                   MOVE      HEOT          KO2KOD
     C     KOO2KE        CHAIN     KODTOT2                            34
     C     KO2ST         IFNE      'N'                                           2<-B
      * XXOPD N/J (faktlinje/oppdrag-ordinær) når HE01<>J
      * VED STATISTIKK PÅ KUNDE, SKAL TESTE OM STATISTIKK PÅ DENNE
      * KUNDEN ER OPPDATERT FRA FØR.
     C     XXOPD         IFEQ      'J'                                            3<-B
     C     XXOPD         OREQ      'N'
     C     HE01          IFEQ      'J'                                             4<-B
     C     FAKKEY        CHAIN(N)  FAKT                               33
     C     *IN33         IFEQ      '0'                                              5<-B
     C     FAKDA         ANDNE     'K'
     C     FAK10K        CHAIN     FAK10                              33
     C     *IN33         DOWEQ     '0'                                               6<-B
     C     F0KDA         IFNE      'K'                                                7<-B
     C                   LEAVE
     C                   END                                                          7<-E
     C     FAK10K        READE     FAK10                                  33
     C  N33              ENDDO                                                       6<-E
     C   33              MOVE      'J'           XSTFA
     C                   END                                                        5<-E
     C                   ELSE                                                      4
     C     FAKKEY        CHAIN(N)  FAKT                               33
     C                   IF        *IN33                                            5<-B
     C                   MOVE      '1'           *IN90
     C                   ELSE                                                       5
     C     FAKDA         IFNE      'K'                                               6<-B
     C                   MOVE      '1'           *IN90
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C*    XXOPD 1 (oppdrag-Omkjøring m.m.) UAVHENGIG AV HE01=J
     C     XXOPD         IFEQ      '1'                                            3<-B
     C                   MOVE      '1'           *IN90
     C                   END                                                      3<-E
     C*    XXOPD 0 (Fakturalinjer - Omkjøring m.m.) ALDRI
     C                   END                                                     2<-E
     C*
     C* Oppdater HE01 - dersom original kjøring (oppdrag / linje)
     C     XXOPD         IFEQ      'N'                                           2<-B
     C     FAKDA         ANDNE     'K'
     C     FAKDA         ANDNE     'I'
     C     XXOPD         OREQ      'J'
     C                   MOVE      'J'           HE01
     C                   END                                                     2<-E
     C*
     C* FAKTURALINJE SOM INNPARAMETER :
     C     XXOPD         IFEQ      'N'                                           2<-B
     C     XXOPD         OREQ      '0'
     C     FAKKEY        CHAIN     FAKT                               33
     C*Beløp adderes kun ved XXOPD N/O (faktlinje i innparam.)
     C     GEBKEY        CHAIN     KODTGE                             33
      * VED TR.OPPDRAG BRUK BILKODEN FRA TUREN.
     C     *IN33         IFEQ      '0'
     C     KGEAVR        ANDEQ     'T'
     C     TUBILK        ANDNE     *BLANKS
     C                   MOVE      FAVK          GMVK              3
     C                   MOVE      TUBILK        FAVK
     C     GEBKEY        CHAIN     KODTGE                             33
     C   33              MOVE      GMVK          FAVK
     C                   SETOFF                                       33
     C                   END
      *
     C     *IN33         IFEQ      '1'                                            3<-B
     C                   MOVE      *BLANKS       FAGGRP
     C                   ELSE                                                     3
     C                   SELECT                                                    4<-B
     C     FASK          WHENEQ    'I'                                          INTERNT
     C     FABELN        IFGE      0                                                5<-B
     C                   MOVE      KGEII         FAGGRP                         I.INNTEKT
     C                   ELSE                                                       5
     C                   MOVE      KGEKI         FAGGRP                         I.KOSTNAD
     C                   END                                                        5<-E
     C     FAVK          WHENEQ    'AME'                                        EKSTERNT
     C     KGETYP        OREQ      'AME'
     C     FABELN        IFGE      0                                                5<-B
     C                   MOVE      KGEIE         FAGGRP                         E.INNTEKT
     C                   ELSE                                                       5
     C                   MOVE      KGEKE         FAGGRP                         E.KOSTNAD
     C                   END                                                        5<-E
     C     FAKDA         WHENEQ    'K'
     C                   MOVE      KGEKE         FAGGRP                         E.KOSTNAD
     C                   OTHER
     C                   MOVE      KGEIE         FAGGRP                         E.INNTEKT
     C                   ENDSL                                                     4<-E
     C                   MOVE      FAGGRP        XGRIK             1
     C     XGRIK         IFEQ      'I'
     C                   MOVE      'XI'          XGRTOT            2
     C                   ELSE
     C                   MOVE      'XK'          XGRTOT
     C                   END
     C                   END                                                      3<-E
     C                   EXCEPT    NYGRP
     C* UDEFINERT FAKTURAÅR/MND NÅR BEHANDLER EN VALELINJE
     C     FAMND         IFEQ      *ZEROS                                         3<-B
     C                   MOVE      FADATM        FAMND
     C                   MOVE      FADATÅ        FAÅR
     C                   MOVEL     FADATÅ        FACC
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C* OPPDRAG SOM   INNPARAMETER :
     C                   CLEAR                   FAKTR
     C                   MOVE      HEDTMP        FAMND
     C                   MOVE      HEDTÅP        FAÅR
     C                   MOVE      HEDTCP        FACC
     C* UDEFINERT FAKTURAÅR/MND NÅR BEHANDLER ET OPPDRAG
     C     FAMND         IFEQ      *ZEROS                                         3<-B
     C                   MOVE      HEDTM         FAMND
     C                   MOVE      HEDTÅ         FAÅR
     C                   MOVEL     HEDTÅ         FACC
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C     FAMND         IFEQ      0
     C                   RETURN
     C                   END
      *
      * IKKE OPPDATER STATUS PÅ HEADF DERSOM DET ER EN KOSTNADLINJE
     C     FAKDA         IFNE      'K'
     C     FASK          IFNE      'I'
     C                   MOVE      'O'           HEST
     C                   ELSE
     C     TRSLAG        IFNE      'HO'
     C     TRSLAG        ANDNE     '  '                                         SPED.-HO
     C                   MOVE      'O'           HEST
     C                   END
     C                   END
     C                   END
     C                   EXCEPT    HOVUPD
      *
     C                   MOVE      *ZEROS        KOVAVD
     C     KODVKE        CHAIN     KODTV                              33
     C                   MOVE      99999         FF(12)
     C                   MOVE      99999         FF(13)
     C                   Z-ADD     1             VV                2 0
      *
     C     HEFBV         IFGT      99999                                         2<-B
     C                   MOVE      99999         XFBV              5 0
     C                   ELSE                                                    2
     C                   MOVE      HEFBV         XFBV
     C                   END                                                     2<-E
     C     XFBV          LOOKUP    FF(VV)                             33  33
     C                   MOVE      HEAVD         KOAAVD
     C     KODAKE        CHAIN     KODTA                              33
     C* VV INNEHOLDER NÅ STATISTISK VEKTGRUPPE
     C*
     C* HENT FIRMAETS STATISTIKKPROFIL / FIRMAETS EGEN LANDKODE
     C     FIFIRM        IFEQ      *BLANKS                                       2<-B
     C                   READ(N)   FIRSTA                                 33
     C                   READ      FIRM                                   33
     C                   END                                                     2<-E
     C*
     C                   END                                                    1<-E
     C*
     C                   ENDSR
     C*
     OHEADFR    E            HOVUPD
     O                       HEST
     O                       HE01
     OSTATHFL1  EADD         NY
     O                       SFAST                1
     O                       YDTC                 3
     O                       YDTÅ                 5
     O                       KUNDE               13
     O                       SBLANK              21
     O                       HEAVD               25
     O                       FV                 103P
     O                       AV                 181P
     O                       AS                 246P
     O                       VG                 311P
     O                       AT                 376P
     O                       SS                 441P
     O                       VS                 519P
     O                       MS                 584P
     O                       STSTNR             590P
     OSTATHFL1  E            GML
     O                       FV                 103P
     O                       AV                 181P
     O                       AS                 246P
     O                       VG                 311P
     O                       AT                 376P
     O                       SS                 441P
     O                       VS                 519P
     O                       MS                 584P
     OSTATOHL1  EADD         NYO
     O                       SFAST                1
     O                       YDTC                 3
     O                       YDTÅ                 5
     O                       KUNDE               13
     O                       SBLANK              21
     O                       HEAVD               25
     O                       FV                 103P
     O                       AV                 181P
     O                       AS                 246P
     O                       VG                 311P
     O                       AT                 376P
     O                       SS                 441P
     O                       VS                 519P
     O                       MS                 584P
     O                       STSTNR             590P
     OSTATOHL1  E            GMLO
     O                       FV                 103P
     O                       AV                 181P
     O                       AS                 246P
     O                       VG                 311P
     O                       AT                 376P
     O                       SS                 441P
     O                       VS                 519P
     O                       MS                 584P
     OFAKTR     E            NYGRP
     O                       FAGGRP
