     FSYAV03FL  IF   E           K DISK
     FDOKUI     IF   E           K DISK
     FKODTFS    IF   E           K DISK
     FFAK2      IF   E           K DISK
     FSYAV05P   O    E             PRINTER OFLIND(*IN98)
      *__________________
      * Array for samlast
      *""""""""""""""""""
     D EAR             S              6    DIM(999)
     D                 DS
     D  HEDTOP                 1      8  0
     D  XEDTOP                 3      8  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    HEAVD         WEAVD
     C     *LIKE         DEFINE    HEOPD         WEOPD
     C     *LIKE         DEFINE    HEFLY         WEFLY
     C     *LIKE         DEFINE    DIRI          WIRI
     C     *LIKE         DEFINE    DIRI          WIRIC
     C     *LIKE         DEFINE    DIAWB         WEAWB
     C     *LIKE         DEFINE    FASK          WASK
     C     *LIKE         DEFINE    KFSUNI        WISUNI
     C     *LIKE         DEFINE    DIFBNR        WIFBNR
     C                   Z-ADD     0             WE                3 0
     C                   Z-ADD     0             WIV01
      *__________________
      * Definiere nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST                                                  SYAV03F
     C                   KFLD                    WEAWB
     C     KEYB          KLIST
     C                   KFLD                    WIRI                           DOKUI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    DIFBNR
     C     KEYD          KLIST                                                  KODTFS
     C                   KFLD                    WISUNI
     C                   KFLD                    HEFLY
     C     KEYE          KLIST                                                  FAK2
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    WASK
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     ZAVDF         PARM                    ZAVDF
     C     ZAVDT         PARM                    ZAVDT
     C     ZFLYF         PARM                    ZFLYF
     C     ZFLYT         PARM                    ZFLYT
     C     ZDTF          PARM                    ZDTF
     C     ZDTT          PARM                    ZDTT
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'I'           WIRI
     C                   MOVE      'M'           WIRIC
     C                   MOVE      'FS'          WISUNI
     C                   MOVE      'F'           WASK
     C                   MOVE      *ZEROS        WEAWB
     C                   Z-ADD     999           WEFLY
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv ut                                             SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_________________
      * Skriv ut forside
      *"""""""""""""""""
     C                   WRITE     SYAV05PA
      *________________________
      * Les poster fra workfile
      *""""""""""""""""""""""""
     C     KEYA          SETLL     SYAV03FL
     C                   READ      SYAV03FL                               51
     C     *IN51         DOWEQ     '0'
      *______________________
      * Brudd på flyselskap ?
      *""""""""""""""""""""""
     C     HEFLY         CASNE     WEFLY         SRBA
     C                   END
      *___________
      * Overflow ?
      *"""""""""""
     C     *IN98         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   WRITE     SYAV05PB
     C                   END
      *_________
      * Detaljer
      *"""""""""
     C                   EXSR      SRBB
      *
     C                   READ      SYAV03FL                               51
     C  N51              END
      *____________
      * Last Record
      *""""""""""""
     C                   EXSR      SRBA
     C                   WRITE     SYAV05PØ
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Totaltid                                            SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      * Skriv ut totaler for gammelt flyselskap, ikke første gang
     C     *IN61         IFEQ      '1'
      *
     C     WCCL1         ADD       WPPL1         WCPL1
     C     WCPL1         MULT      KFSKOM        WKML1
     C                   ADD       WPPL1         WTTL1
     C                   SUB       WKML1         WTTL1
     C                   SUB       WIBL1         WTTL1
     C                   ADD       WSSL1         WTTL1
      *
     C                   Z-ADD     WPPL1         WPPL1X
     C                   Z-ADD     WIBL1         WIBL1X
     C                   Z-ADD     WSSL1         WSSL1X
      *
     C                   WRITE     SYAV05PD
      *
      * PTF 2.XXXX  START
     C                   Z-ADD     0             WTTL1
      * PTF 2.XXXX  SLUTT
     C                   Z-ADD     0             WANL1
     C                   Z-ADD     0             WNTL1
     C                   Z-ADD     0             WVEL1
     C                   Z-ADD     0             WIVL1
     C                   Z-ADD     0             WCCL1
     C                   Z-ADD     0             WPPL1
     C                   Z-ADD     0             WCPL1
     C                   Z-ADD     0             WKML1
     C                   Z-ADD     0             WIBL1
     C                   Z-ADD     0             WSSL1
      *
     C                   END
      * Skriv ut heading for nytt flyselskap
     C     *IN51         IFEQ      '0'
     C     KEYD          CHAIN     KODTFS                             52
     C     *IN52         IFEQ      '1'
     C                   MOVE      *BLANKS       KFSNAV
     C                   Z-ADD     0             KFSKOM
     C                   Z-ADD     0             KFSMC
     C                   END
     C                   MOVE      '1'           *IN61
     C                   Z-ADD     HEFLY         WEFLY
     C                   Z-ADD     0             WE
     C                   MOVE      *BLANKS       EAR
     C                   WRITE     SYAV05PB
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Detaljtid                                           SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   MOVEA     '00'          *IN(30)
      *________________________
      * Bearbeide singeloppdrag
      *""""""""""""""""""""""""
     C     KEYB          CHAIN     DOKUI                              52
     C   52              GOTO      SRBB9
      *
     C                   MOVE      '1'           *IN30
     C     DICCPP        IFEQ      'CC'
     C                   Z-ADD     DIFRAK        WCC01
     C                   Z-ADD     0             WPP01
     C                   Z-ADD     0             WMCBEL
     C                   ELSE
     C                   Z-ADD     DIFRAK        WPP01
     C                   Z-ADD     0             WCC01
     C                   Z-ADD     KFSMC         WMCBEL
     C                   END
      *
     C                   EXSR      SRBBA
      *
     C                   ADD       WCC01         WCCL1
     C                   ADD       WPP01         WPPL1
     C                   ADD       DIANT         WNTL1
     C                   ADD       DIVEKT        WVEL1
     C                   Z-ADD     DIFVKT        WIV01
     C                   ADD       DIFVKT        WIVL1
     C                   ADD       WIB01         WIBL1
     C                   ADD       WMCBEL        WSSL1
      *
     C                   ADD       1             WANL1
     C                   WRITE     SYAV05PC
     C     SRBB9         ENDSR
      *//////////////////////////////////////////////////////////
      * SR FOR BEARBEDNING AV 01 DETALJ   B                 SRBBA
      *//////////////////////////////////////////////////////////
     C     SRBBA         BEGSR
      *__________________________________
      * Addere varebeløp (kun status='F')
      *""""""""""""""""""""""""""""""""""
     C                   Z-ADD     0             WIB01
     C     KEYE          CHAIN     FAK2                               53
     C     *IN53         DOWEQ     '0'
      *
     C     FAVK          IFNE      'FFE'
     C     FAVK          ANDNE     'FC '
     C     FAVK          ANDNE     'MC '
     C                   ADD       FABELN        WIB01
     C                   END
     C     KEYE          READE     FAK2                                   53
     C  N53              END
     C                   ENDSR
