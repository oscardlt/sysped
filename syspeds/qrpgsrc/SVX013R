********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Ny logikk vedr. PKI, ikke merket
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 KANSELLERING
11XXX * 03 YBC PTF11 UTVIDER FILSTØRRELSE FRA 10000 TIL 30000
********************************************************************
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
     FSVXH      UF   E           K DISK
     FSVXHK     UF A E           K DISK
     FSVXKODL01 IF   E           K DISK
     FEDIM2     IF   E           K DISK
     FSVXSTD    IF   E           K DISK
N02  FSVX013D   CF   E             WORKSTN USROPN
      *__________
      * Variabler
      *""""""""""
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
S03  D*Data            s          10000a   Varying
N03  D Data            s          30000a   Varying
     D Codepage        s              5a   Varying
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
      *   SYEDI data area
     D EDIDTA          DS           256
     D  UELIBF               171    180

     C                   EXSR      INIT
N02   *
N02  C                   IF        U1N07 = '014'
N02  C                   OPEN      SVX013D
N02  C                   EVAL      *IN27 = *ON
N02  C     START1        TAG
N02  C                   EXFMT     SVX013D01
N02  C                   MOVEA     '00'          *IN(97)
N02  C     *IN04         IFEQ      '1'
N02  C                   MOVE      '012'         UUNIK             3
N02  C                   MOVE      *BLANKS       UKODE            10
N02  C                   MOVEL     WSSK          UKODE
N02  C                   CALL      'SVX002R'
N02  C                   PARM                    UUNIK
N02  C                   PARM                    UKODE
N02  C                   PARM                    UTYPE2            3
N02  C                   MOVEL     UKODE         WSSK
N02  C                   GOTO      START1
N02  C                   END
N02  C   12              CLOSE     SVX013D
N02  C   12              GOTO      SLUTT
N02  C     WSFT1         COMP      *BLANKS                                98
N02  C     WSSK          IFNE      *BLANKS
N02  C                   MOVE      '012'         YUNIK
N02  C                   MOVE      *BLANKS       YKODE
N02  C                   MOVEL     WSSK          YKODE
N02  C     SVK01K        CHAIN     SVXKODL01                          97
N02  C                   END
N02  C   98
N02  COR 97              GOTO      START1
N02   *
N02  C                   EXSR      OPDFIL
N02  C                   CLOSE     SVX013D
N02  C                   END

     C     SVHKEY        CHAIN     SVXH                               33
     C                   MOVE      'Q'           THST
     C                   MOVE      *BLANKS       THWS
     C                   MOVE      'J'           THPKL
     C                   MOVE      U1N07         THMF
     C                   MOVE      *BLANKS       THSGUT

     C                   UPDATE    SVXHR

     C                   EXSR      KOMLIN

N02  C     SLUTT         TAG
     C                   EVAL      *INLR = *ON
      ***********************************************
     C     KOMLIN        BEGSR
      ***********************************************
     C                   CALL      'SVX101R'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    U1N07
S03  C*                  PARM                    UDATA         10000
N03  C                   PARM                    UDATA         30000
      * Lag ASCIIfil
     C                   MOVE      UAVD          WAVD              4
     C                   MOVE      UTDN          WOPD              7
     C                   MOVE      *BLANKS       WFILNAM         256
     C                   EVAL      WFILNAM = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/SVXU' + WAVD + '-' + WOPD

     C                   EVAL      Codepage = '819'
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w, codepage=' + Codepage )

     C                   EVAL      rc = close( FILE_o )

     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w' )

     C                   IF        FILE_o <> *Null
     C                   EVAL      Data = %trimr(UDATA)
     C                   EVAL      rc = fputs(Data : File_o)
     C                   EVAL      rc = close( FILE_o )
     C                   ENDIF
     C                   ENDSR
N02   ***********************************************
N02  C     OPDFIL        BEGSR
N02   ***********************************************
N02  C     SVHKEY        CHAIN     SVXHK                              33
N02  C                   MOVE      WSFT1         TKFT1
N02  C                   MOVE      WSFT2         TKFT2
N02  C                   MOVE      WSFT3         TKFT3
N02  C                   MOVE      WSFT4         TKFT4
N02  C                   MOVE      WSFT5         TKFT5
N02  C                   MOVE      WSSK          TKSK
N02  C     *IN33         IFEQ      '1'
N02  C                   MOVE      THAVD         TKAVD
N02  C                   MOVE      THTDN         TKTDN
N02  C                   WRITE     SVXHKR
N02  C                   ELSE
N02  C                   UPDATE    SVXHKR
N02  C                   END
N02   *
N02  C                   ENDSR
N02   *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *LIKE         DEFINE    M0068A        MAX0B
     C     *LIKE         DEFINE    M0068B        MAX0C
     C     *LIKE         DEFINE    M0068C        MAX0D
     C     *LIKE         DEFINE    S0004         X0004
     C     *LIKE         DEFINE    S0010         X0010
     C     SVHKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SVK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
     C     M2KDCK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    MAX0B
     C                   KFLD                    MAX0C
     C                   KFLD                    MAX0D
     C     M2KD2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    U1N07             3
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      '99999999'    MAX0B
     C                   MOVE      '999999'      MAX0C
     C                   MOVE      '99'          MAX0D
      *
     C     UAVD          CHAIN     SVXSTD                             33
     C                   MOVE      S0004         X0004
     C                   MOVE      S0010         X0010
     C     SVHKEY        CHAIN(N)  SVXH                               33
      *
     C                   MOVE      '0'           *IN12
      * Åpne dataarea
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
     C                   ENDSR
