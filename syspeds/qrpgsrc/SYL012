      *********************************************************************
      * Spørring på brukte lagerlokasjoner: vis på skjermen
      *
CRTPG+*  CRTPGM SYSPED/SYL012 MODULE(*LIBL/SYL012
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FCUNDL01   IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FKUFAST    UF   E           K DISK    USROPN
     FLLGRPK05  IF   E           K DISK    USROPN
     FLLTKF     IF   E           K DISK    USROPN
     FLLVADR3   IF   E           K DISK    USROPN
     FLLW043L1  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D WSVE            S              5
     D WSLEV           S             17
     D WSANR           S             15
     D WSDT            S             10
     D BWSDT           S              8  0
     D XTXT            S             80
     D UsrSpcName      s             10
     D lstbrd          s              1
      * Variables received from the input string
      * and converted to numeric
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C                   MOVE      GKGRP         UGRP              8
     C                   MOVE      GKPNR         UPNR              8
     C                   MOVE      BWSDT         UDT               8
     C                   CALL      'LL043CW'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    UGRP
     C                   PARM                    UPNR
     C                   PARM                    WSVE
     C                   PARM                    WSLEV
     C                   PARM                    WSANR
     C                   PARM                    UDT
      *
     C                   open      LLW043L1                             50
     C                   MOVE      *ZEROS        XLLT              3
     C                   MOVE      *ZEROS        XANT              5 0
     C                   MOVE      *ZEROS        XANT2             7 2
     C                   MOVE      *ZEROS        XANTT             7 0
     C                   MOVE      *ZEROS        XANTT2            9 2
     C     LLW043L1K     SETLL     LLW043L1
     C     LLW043L1K     READE     LLW043L1                               50
     C                   DOW       *IN50 = *OFF
     C                   SELECT
     C     XLLT          WHENEQ    *ZEROS
     C                   MOVE      L43LLT        XLLT
     C                   Z-ADD     1             XANT
     C     XLLT          WHENNE    L43LLT
     C     XLLT          CHAIN     LLTKF                              33
     C   33              EVAL      TKTXT  = '*UKJENT'
     C                   EVAL      XANT2  = XANT * TKOMRF
     C                   EVAL      XANTT  = XANTT + XANT
     C                   EVAL      XANTT2 = XANTT2 + XANT2
     C                   callp     updhtmlvar('XLLT':XLLT)
     C                   callp     updhtmlvar('TKTXT':TKTXT)
     C                   callp     updhtmlvar('XANT':
     C                             %trim(%editc(XANT:'Z')))
     C                   callp     updhtmlvar('XANT2':
     C                             %trim(%editc(XANT2:'4')))
     C                   callp     wrtsection('tablerow')
     C                   MOVE      L43LLT        XLLT
     C                   Z-ADD     1             XANT
     C                   OTHER
     C                   ADD       1             XANT
     C                   ENDSL
     C     LLW043L1K     READE     LLW043L1                               50
     C  N50              ENDDO                                                  *hival
      *
     C     XANT          IFGT      0
     C     XLLT          CHAIN     LLTKF                              33
     C   33              EVAL      TKTXT  = '*UKJENT'
     C                   EVAL      XANT2  = XANT * TKOMRF
     C                   EVAL      XANTT  = XANTT + XANT
     C                   EVAL      XANTT2 = XANTT2 + XANT2
     C                   callp     updhtmlvar('XLLT':XLLT)
     C                   callp     updhtmlvar('TKTXT':TKTXT)
     C                   callp     updhtmlvar('XANT':
     C                             %trim(%editc(XANT:'Z')))
     C                   callp     updhtmlvar('XANT2':
     C                             %trim(%editc(XANT2:'3')))
     C                   callp     wrtsection('tablerow')
     C                   END
      *
     C                   callp     updhtmlvar('XLLT':'')
     C                   callp     updhtmlvar('TKTXT':'TOTAL')
     C                   callp     updhtmlvar('XANT':
     C                             %trim(%editc(XANTT:'Z')))
     C                   callp     updhtmlvar('XANT2':
     C                             %trim(%editc(XANTT2:'3')))
     C                   callp     wrtsection('tablerow')
      *
     C                   callp     wrtsection('tableend')
      *
     C     slutt         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   MOVE      'N'           XFEIL             1
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSVE    = zhbgetvar('WSVE')
     C                   EVAL      WSLEV   = zhbgetvar('WSLEV')
     C                   EVAL      WSANR   = zhbgetvar('WSANR')
     C                   EVAL      WSDT    = zhbgetvar('WSDT')
     C                   EVAL      BWSDT   = c2n2(WSDT)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updhtmlvar('WSNA':KNAVN)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
     C                   IF        WSVE = '*ALL'
     C                   EVAL      XTXT = WSVE
     C                   ELSE
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   EVAL      XTXT = WSVE
     C                   CAT       KFTXT:1       XTXT
     C                   END
     C                   callp     updhtmlvar('WSVE':XTXT)
     C                   IF        WSLEV = '*ALL'
     C                   EVAL      XTXT = WSLEV
     C                   ELSE
     C     LLVADR3K      CHAIN     LLVADR3                            33
     C                   EVAL      XTXT = WSLEV
     C                   CAT       VADRNA:1      XTXT
     C                   END
     C                   callp     updhtmlvar('WSLEV':XTXT)
     C                   callp     updhtmlvar('WSDT':WSDT)
     C                   IF        WSANR = *BLANKS
     C                   callp     updhtmlvar('WSANR':'*ALL')
     C                   ELSE
     C                   callp     updhtmlvar('WSANR':WSANR)
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL012'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      LLGrpK05                             50
     C                   open      CUNDL01                              50
     C                   open      LLTKF                                50
     C                   open      KUFAST                               50
     C                   open      LLVADR3                              50
      *
     C     CUND1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     LLW043L1K     klist
     C                   kfld                    GKGRP
     C                   kfld                    GKPNR
     C     KUFASTK       klist
     C                   kfld                    KFTYP
     C                   kfld                    WSVE
     C                   MOVEL     'VE'          KFTYP
     C                   MOVE      BIKUNR        KFTYP
     C     LLVADR3K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    WSLEV
      *
     C     CUND1K        CHAIN     CUNDL01                            50
     C     BIKUNR        CHAIN     LLGRPK05                           33
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     LLGrpK05
     C                   close     LLTKF
     C                   close     LLW043L1
     C                   close     LLVADR3
     C                   close     CUNDL01
     C                   close     KUFAST
     C                   eval      *inlr = *on
      *
     C                   endsr
