      *
      *****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BILL OF LADING (BEKREFTELSE)    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-03 05-11 13-26                      ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-96                      ***
      *****************************************************************
      *
     FBILLOF    UF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FSYSJ50FJ  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BLBDTÅ                 1      4  0
     D  BLBDTM                 5      6  0
     D  BLBDTD                 7      8  0
     D  BLBDT                  1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C  N04
     CANN30              Z-ADD     7             LINNBR
     C  N04
     CANN30              Z-ADD     40            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C                   SETOFF                                       30
     C                   MOVEA     '000'         *IN(97)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
     C*
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
     C*
     C                   EXSR      TESTB
     C   30              GOTO      START
     C                   EXSR      OPDBL
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C*
     C*** TEST BÅTKODE
     C     WSOB          CHAIN     CDBOAT                             99
     C   99              MOVE      'SPB0104'     MSGNR
     C   99              SETON                                        30
     C*
     C*** TEST OMLASTEHAVN
     C     WSOMH         CHAIN     STED2                              98
     C   98
     CANN30              MOVE      'SPS0108'     MSGNR
     C   98
     CANN30              SETON                                        30
     C*
     C*** TEST DATO
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      97
     C   97
     CANN30              MOVE      'SPD0001'     MSGNR
     C   97
     CANN30              SETON                                        30
     C*
     C   30              Z-ADD     40            POSNBR
     C   97              Z-ADD     17            LINNBR
     C   98              Z-ADD     15            LINNBR
     C   99              Z-ADD     7             LINNBR
     C*
     C  N30WSBNA         IFEQ      *BLANKS
     C                   MOVE      CBNAB         WSBNA
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBLW        BEGSR
     C***********************************************
     C                   MOVE      BLBBK         WSOB
     C                   MOVE      BLBBNA        WSBNA
     C                   MOVE      BLBREI        WSREIS
     C                   MOVE      BLBBLN        WSBLNR
     C                   MOVE      BLBOMH        WSOMH
     C                   MOVE      BLBDTD        WSDTD
     C                   MOVE      BLBDTM        WSDTM
     C                   MOVE      BLBDTÅ        WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDBL         BEGSR
     C***********************************************
     C     BLK           CHAIN     BILLOF                             33
     C                   EXSR      MOVWBL
     C  N33              UPDATE    BILLOFR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWBL        BEGSR
     C***********************************************
     C                   MOVE      WSOB          BLBBK
     C                   MOVE      WSBNA         BLBBNA
     C                   MOVE      WSREIS        BLBREI
     C                   MOVE      WSBLNR        BLBBLN
     C                   MOVE      WSOMH         BLBOMH
     C                   MOVE      WSDTD         BLBDTD
     C                   MOVE      WSDTM         BLBDTM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          BLBDTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          BLBDTÅ
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     44
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSOB
     C                   Z-ADD     9             LINNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      15
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     44
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    WSOMH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     17            LINNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BLK           KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFLAGG            1 0
     C*
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        BLBDT
     C*
     C     BLK           CHAIN     BILLOF                             33
     C  N33              EXSR      MOVBLW
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
