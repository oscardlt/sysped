      *
      *****************************************************************
      *** PROGRAM SKAL VEDL. FREIGHT MANIFEST (INNGANNGS PARM.)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01 02 05-26                            ***
      *** LEDIGE INDIKATORER 27-29 31-96                            ***
      *****************************************************************
      *
     FBILLOF    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FSYSJ25FM  CF   E             WORKSTN
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
     D  UOPD                  22     28
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C  N30              MOVE      WSOPD         WSOPDG
     C  N30              MOVE      *ZEROS        WSOPD
     C                   EXFMT     BILDE
     C                   SETOFF                                       30
     C                   MOVEA     '000'         *IN(97)
     C*
     C   03              GOTO      SLUTT
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      START
     C*
     C                   Z-ADD     0             UFLAGG            1 0
     C                   CALL      'SYSJ25A'
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    WSSG
     C                   PARM                    UFLAGG
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   MOVE      WSAVD         UAVD
     C                   MOVE      WSOPDG        UOPD
     C                   MOVE      WSSG          USG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C*** TEST SIGNATUR
     C     KODSFK        CHAIN     KODTSF                             99
     C   99              MOVE      'SPS0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLT1
     C*
     C*** TEST AVDELING
     C     KODTAK        CHAIN     KODTA                              98
     C  N98KODTVK        CHAIN     KODTV                              98
     C   98              MOVE      'SPA0006'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      SLT1
     C*
     C*** TEST OPPDRAGSNR
     C     BLK           CHAIN     BILLOF                             97
     C   97              MOVE      'SPO0002'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      SLT1
     C     WSSG          COMP      BLSG                               9999
     C   99              MOVE      'SPS0014'     MSGNR
     C   99              SETON                                        30
     C*
     C     SLT1          ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BLK           KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    WSAVD
     C     KODSFK        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    WSSG
     C*
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      'V'           YVUNI             1
     C                   MOVE      'SF'          YSFUNI            2
     C*
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      USG           WSSG
     C*
     C                   ENDSR
     C*
