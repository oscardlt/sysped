********************************************************************
     FHEADF     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FTRACKL02  IF   E           K DISK
     FECMSG4    IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FEDIC      UF   E             DISK
     FEUNH      O    E             DISK
     FEDIM      O    E             DISK
     FECNT01B   O    E             DISK
     FECTA01B   O    E             DISK
     FEDTM01B   O    E             DISK
     FEFTX01B   O    E             DISK
     FELOC01B   O    E             DISK
     FENAD01B   O    E             DISK
     FERFF01B   O    E             DISK
     FETDT01B   O    E             DISK
     FETSR01B   O    E             DISK
      *____________
      * DS for Dato
      *""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTM4                   1      4
     D  WTM                    1      6
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  TRSDFK                 1      4  0
     D  TRSDFK_A               1      4
     D                 DS
     D  TRSDTK                 1      4  0
     D  TRSDTK_A               1      4
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12    DIM(12)
     D                SDS
     D  USER                 254    263
      *______________________
      * Initialize subroutine
      *""""""""""""""""""""""
     C                   EXSR      INIT
     C                   EXSR      SNDRQS
     C                   RETURN
      *////////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////////
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
      *
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FRISOKKCU     KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    CU_FSKODE
     C     *LIKE         DEFINE    FSKODE        CU_FSKODE
     C     TRACKL02K     KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C     ECMSG4K       KLIST
     C                   KFLD                    TRKNFA
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C     KODTSFK       KLIST
     C                   KFLD                    KOSFUN
     C                   KFLD                    HESG
     C     STED2DKF      KLIST
     C                   KFLD                    HELKA
     C                   KFLD                    HESDF
     C     STED2DKT      KLIST
     C                   KFLD                    HETRI
     C                   KFLD                    HESDT
      *__________________
      * Define workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    CREP0         WREP0
     C     *LIKE         DEFINE    CREP1         WREP1
      *__________________________
      * Retreive System Date/Time
      *""""""""""""""""""""""""""
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
     C                   MOVE      UAVD          HEAVD
     C                   MOVE      UOPD          HEOPD
     C                   MOVE      'S'           ECSR
     C                   MOVE      'IFTMBC'      EC0065
     C                   EVAL      KOSFUN = 'SF'
     C                   EVAL      TTACTI = 'UPD'
     C                   EVAL      CU_FSKODE = 'CU'
     C                   Z-ADD     0             ØX                2 0                      --I
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////////
     C     SNDRQS        BEGSR
      *////////////////////////////////////////////////////////////////
      *_____________________________________________________
      * Retrieve last used recordnumber from EDIC and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CMN
     C                   Z-ADD     CMN           WMN
     C                   UPDATE    EDICR
     C                   ENDIF
      *
     C     HEADFK        CHAIN     HEADF                              33
     C     ECMSG4K       CHAIN     ECMSG4                             33
      *_____________________
      * Write record to EUNH
      *"""""""""""""""""""""
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTMBC'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '01B'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     *blanks       U0057
      *
     C                   MOVEL     '770'         U1001
     C*                  MOVEL     HERFA         U1004
     C     FRISOKKCU     CHAIN     FRISOK                             33
     C                   EVAL      U1004 = FSSOK
     C                   MOVEL     WMN           U0062B
     C                   MOVEL     '9'           U1225
     C                   MOVEL     'AP'          U4343
     C                   WRITE     UNHR
      *
     C     KODTSFK       CHAIN     KODTSF                             33
      *____________________
      * Write record to CTA
      *""""""""""""""""""""
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     1             CREP0
     C                   EVAL      C3139 = 'CW'
     C                   EVAL      C3413 = KOSFNV
     C                   WRITE     CTAR
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     1             DREP0
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDT           D2380
     C     D2380         CAT       WTM4:0        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
      *____________________
      * Write record to TSR
      *""""""""""""""""""""
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     1             TREP0
     C                   EVAL      T4065  = '27'                                Door-to-Door
     C                   EVAL      T7273A = 'LCL'
     C                   EVAL      T4219  = '10'
     C                   EVAL      T1131D = '57'
     C                   EVAL      T3055D = '86'
     C                   WRITE     TSRR
      *____________________
      * Write record to FTX
      *""""""""""""""""""""
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     1             FREP0
     C                   EVAL      F4451  = 'AAI'
     C     TRACKL02K     CHAIN     TRACKL02                           33
     C                   IF        *IN33
     C                   EVAL      F4440A = 'CONSIGNMENT CREATED'
     C                   ELSE
     C                   EVAL      F4440A = 'CONSIGNMENT UPDATED'
     C                   END
     C                   WRITE     FTXR
      *_____________________________
      * Total number of consignments
      *"""""""""""""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVE      '11 '         C6069
     C                   Z-ADD     HENT          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR
      *___________________
      * Total gross weight
      *"""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVE      '7  '         C6069
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR
      *___________________
      * Total cube
      *"""""""""""""""""""
     C                   IF        HEM3 <> 0
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVE      '15 '         C6069
     C                   Z-ADD     HEM3          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'MTQ'         C6411
     C                   WRITE     CNTR
     C                   END
      *___________________
      * Total loading metres
      *"""""""""""""""""""
     C                   IF        HELM <> 0
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVE      '57 '         C6069
     C                   Z-ADD     HELM          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'MTR'         C6411
     C                   WRITE     CNTR
     C                   END
      *____________________
      * Write record to RFF
      *""""""""""""""""""""
     C                   Z-ADD     0             WREP0
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     2             RGN
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        FSKODE <> 'EDS' AND FSKODE <> 'KRF'
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   EVAL      R1153 = FSKODE
     C                   EVAL      R1154 = FSSOK
     C                   WRITE     RFFR
     C                   END
     C     HEADFK        READE     FRISOK                                 33
     C                   ENDDO
      *____________________
      * Write record to TDT
      *""""""""""""""""""""
     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     3             TGN
     C                   Z-ADD     1             TREP0
     C                   EVAL      T8051  = '20'                                Main-carriage transp
     C                   EVAL      T8067  = '12'                                Road transport
     C                   WRITE     TDTR
      *____________________
      * Write record to LOC FRA-STED
      *""""""""""""""""""""
     C                   MOVE      TREP0         WREP0
     C     STED2DKF      CHAIN     STED2D                             33
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     4             LGN
     C                   MOVE      WREP0         LREP0
     C                   Z-ADD     1             LREP1
     C                   EVAL      L3227  = '9'                                 Place/port of loadin
     C                   EVAL      L3224  = ST2NVN
     C                   WRITE     LOCR
      *____________________
      * Write record to DTM FRA-DATO
      *""""""""""""""""""""
     C                   MOVE      LREP1         WREP1
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     4             DGN
     C                   MOVE      WREP0         DREP0
     C                   MOVE      WREP1         DREP1
     C                   Z-ADD     1             DREP2
     C                   MOVEL     '133'         D2005
     C                   MOVEL     TRSDFD        D2380
     C     D2380         CAT       TRSDFK_A:0    D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *
      *____________________
      * Write record to LOC TIL-STED
      *""""""""""""""""""""
     C     STED2DKT      CHAIN     STED2D                             33
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     4             LGN
     C                   MOVE      WREP0         LREP0
     C                   MOVE      WREP1         LREP1
     C                   ADD       1             LREP1
     C                   EVAL      L3227  = '7'                                 Place of delivery
     C                   EVAL      L3224  = ST2NVN
     C                   WRITE     LOCR
      *____________________
      * Write record to DTM TIL-DATO
      *""""""""""""""""""""
     C                   MOVE      LREP1         WREP1
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     4             DGN
     C                   MOVE      WREP0         DREP0
     C                   MOVE      WREP1         DREP1
     C                   Z-ADD     1             DREP2
     C                   MOVEL     '132'         D2005
     C                   MOVEL     TRSDTD        D2380
     C     D2380         CAT       TRSDTK_A:0    D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *____________________
      * Write record to NAD CONSIGNOR
      *""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     6             NGN
     C                   Z-ADD     1             NREP0
     C                   EVAL      N3035  = 'CZ'
     C                   MOVEL     HEKNS         N3039
     C                   EVAL      N3055  = 'ZZZ'
     C                   EVAL      N3124A = HENAS
     C                   EVAL      N3036A = HEADS1
     C                   EVAL      N3042A = HEADS2
     C     ' '           SCAN      HEADS3        XPOS              3 0    33
     C                   IF        *IN33
     C                   EVAL      N3164  = %SUBST(HEADS3:XPOS+1:30-XPOS)
     C                   EVAL      N3251  = %SUBST(HEADS3:1:XPOS-1)
     C                   ELSE
     C                   EVAL      N3164  = HEADS3
     C                   MOVEL     HEADS3        N3251
     C                   END
     C                   EVAL      N3207  = HELKA
     C                   WRITE     NADR
      *____________________
      * Write record to NAD CONSIGNEE
      *""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     6             NGN
     C                   ADD       1             NREP0
     C                   EVAL      N3035  = 'CN'
     C                   MOVEL     HEKNK         N3039
     C                   EVAL      N3055  = 'ZZZ'
     C                   EVAL      N3124A = HENAK
     C                   EVAL      N3036A = HEADK1
     C                   EVAL      N3042A = HEADK2
     C     ' '           SCAN      HEADK3        XPOS              3 0    33
     C                   IF        *IN33
     C                   EVAL      N3164  = %SUBST(HEADK3:XPOS+1:30-XPOS)
     C                   EVAL      N3251  = %SUBST(HEADK3:1:XPOS-1)
     C                   ELSE
     C                   EVAL      N3164  = HEADK3
     C                   MOVEL     HEADK3        N3251
     C                   END
     C                   EVAL      N3207  = HETRI
     C                   WRITE     NADR
      *__________________________
      * Write record to EDIM file
      *""""""""""""""""""""""""""
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVEL     U0065         M0065
     C                   MOVEL     USER          M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   EVAL      M0062 = 'E341R_01B'
     C                   Z-ADD     WMN           MMN
     C                   MOVEL     'S'           MSR
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   MOVEL     UAVD          MAVD
     C                   MOVEL     UOPD          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'                                        <-------------I
     C                   MOVE      ØA153H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA153X        ØA12                                         I
     C*                  MOVE      ',   '        ØA12                                         I
     C                   MOVE      '.   '        ØA12                                         I
     C                   MOVE      ØA153D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'                                        <-------------I
     C*                  SUB       1             ØX                                           I
     C                   ENDIF                                                  <-------------I
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
