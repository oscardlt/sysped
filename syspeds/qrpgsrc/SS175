      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS175 MODULE(*LIBL/SS175
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FWRKCGISS  UF A E           K DISK    USROPN
     FGSSORL17  IF   E           K DISK    USROPN
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FVADR      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D DRIVER          s             15
     D SIGN            s             30
     D DATO            s              8
     D DATON           s              8  0
     D KLOKKE          s              4
     D KLOKKEN         s              4  0
     D WINT            s              6
     D WINTEG          s              6  0
     D MaxSN           s              5
     D MaxRC           s              5
     D MaxSN2          s              5  0
     D MaxRC2          s              5  0
     D AntSn           s              5  0
     D AntRC           s              5  0
     D lstbrd          s              1
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D                 DS
     D ORBCD                   1      5
     D ORBCD2                  2      5
     D                 DS
     D ORFT4                   1     30
     D ORAREA                  1      2
     D ORAAR                   4      7
     D ORMND                   8      9
     D ORAARMND                4      9
     D                 DS
     D WKEY2                   1    256
     D KEY2                    1    256    DIM(256)
     D                 DS
     D WDATA                   1   1024
     D DATA                    1   1024    DIM(1024)
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   exsr      GetData
     C                   IF        XFOUND = 'N'
     C                   callp     wrtsection('0found')
     C                   ELSE
     C                   exsr      SetData
     C                   callp     wrtsection('foundsome')
     C                   END
      *
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WINT    = zhbgetvar('WINT')
     C                   eval      WINTEG  = c2n2(WINT)
     c                   IF        WINTEG  = 0
     c                   eval      WINTEG  = random(1:999999)
     c                   move      WINTEG        WINT
     c                   END
     C                   eval      MaxSn   = zhbgetvar('MaxSn')
     C                   eval      MaxSn2  = c2n2(MaxSn)
     C                   IF        MaxSn2  = 0
     C                   eval      MaxSn2  = 100
     C                   MOVE      MaxSn2        MaxSn
     C                   END
     C                   eval      MaxRc   = zhbgetvar('MaxRc')
     C                   eval      MaxRc2  = c2n2(MaxRc)
     C                   IF        MaxRc2  = 0
     C                   eval      MaxRc2  = 1000
     C                   MOVE      MaxRc2        MaxRc
     C                   END
     C                   eval      DRIVER   = zhbgetvar('DRIVER')
     C                   eval      SIGN     = zhbgetvar('SIGN')
     C                   eval      DATO     = zhbgetvar('DATO')
     C                   eval      DATON    = c2n2(DATO)
     C                   eval      KLOKKE   = zhbgetvar('KLOKKE')
     C                   eval      KLOKKEN  = c2n2(KLOKKE)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('DRIVER':DRIVER)
     C                   callp     updhtmlvar('SIGN':SIGN)
     C                   IF        DATO = *BLANKS
     C                   MOVE      BIDTV         DATO
     C                   END
     C                   callp     updhtmlvar('DATO':DATO)
     C                   callp     updhtmlvar('KLOKKE':KLOKKE)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS175'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Hent data til arbeidsfil
      *=====================================================================******
     C     GetData       begsr
      *
      * Slett gamle data
     C     WINTEG        SETLL     WRKCGISS
     C     WINTEG        READE     WRKCGISS                               33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    WRKCGISR
     C     WINTEG        READE     WRKCGISS                               33
     C  N33              ENDDO
     C                   MOVE      *BLANKS       WKEY2
     C                   MOVE      *BLANKS       WDATA
      *
      * Oppdater ny data
     C                   MOVE      'N'           XFOUND            1
      *
     C     WSUSER        SETLL     BRPARF
     C     WSUSER        READE     BRPARF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        PAID = 'SSFAB'
     C                   MOVEL     PAVRDA        XXLC              5
     C                   LEAVE
     C                   END
     C     WSUSER        READE     BRPARF                                 33
     C  N33              ENDDO
      *
     C     XXLC          SETLL     GSSORL17
     C     XXLC          READE     GSSORL17                               33
     C                   DOW       *IN33 = *OFF
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 34
     C   34              MOVE      *BLANKS       POCONO
     C                   MOVEA     POCONO        KEY2(1)
      *
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 34
     C                   DOW       *IN34 = *OFF
     C                   IF        RFKD = 'CPO'
     C                   LEAVE
     C                   END
     C     ORONO         READE     GSSRFF                                 34
     C  N34              ENDDO
     C   34              MOVE      *BLANKS       RFREF
     C                   MOVEA     RFREF         KEY2(21)
      *
     C                   MOVE      ORBCD2        VADRNR
     C     VADRK         CHAIN     VADR                               34
     C   34              MOVE      *BLANKS       VADRNA
      *
     C                   MOVEA     POCONO        DATA(1)
     C                   MOVE      ORONO         XAN07             7
     C                   MOVEA     XAN07         DATA(21)
     C                   MOVEA     ORSNM         DATA(28)
     C                   MOVEA     RFREF         DATA(58)
     C                   MOVE      ORCLI         XAN05             5
     C                   MOVEA     XAN05         DATA(93)
     C                   MOVE      ORWT          XAN07
     C                   MOVEA     XAN07         DATA(98)
     C                   MOVE      ORVOL         XAN05
     C                   MOVEA     XAN05         DATA(105)
     C                   MOVEA     VADRNA        DATA(110)
     C                   MOVEA     ORAREA        DATA(140)
     C                   MOVE      ORDTWL        XAN08             8
     C                   MOVEA     XAN08         DATA(142)
      *
     C                   WRITE     WRKCGISR
     C                   MOVE      'J'           XFOUND            1
     C                   EVAL      AntRc = AntRc + 1
     C                   IF        AntRc >= MaxRc2
     C                   LEAVE
     C                   END
      *
     C     XXLC          READE     GSSORL17                               33
     C  N33              ENDDO
      *
     C                   EVAL      AntSn = AntSn
     C                   callp     updhtmlvar('MaxRc':
     C                             %trim(%editc(Maxrc2:'Z')))
     C                   callp     updhtmlvar('MaxSn':
     C                             %trim(%editc(MaxSn2:'Z')))
     C                   callp     updhtmlvar('AntRc':
     C                             %trim(%editc(Antrc:'3')))
     C                   callp     updhtmlvar('AntSn':
     C                             %trim(%editc(AntSn:'3')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section
      *  (search criteria)
      *=====================================================================******
     C     SetData       begsr
      *
     C                   callp     wrtsection('tablestr')
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updHTMLvar('DRIVER':DRIVER)
     C                   callp     updHTMLvar('SIGN':SIGN)
     C                   callp     updHTMLvar('DATO':DATO)
     C                   callp     updHTMLvar('KLOKKE':KLOKKE)
      *
     C     WINTEG        SETLL     WRKCGISS
     C     WINTEG        READE     WRKCGISS                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVEA     DATA(1)       POCONO
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   MOVEA     DATA(21)      XAN07
     C                   MOVE      XAN07         ORONO
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   MOVEA     DATA(28)      ORSNM
     C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   MOVEA     DATA(58)      RFREF
     C                   callp     updhtmlvar('WSACC':RFREF)
     C                   MOVEA     DATA(93)      XAN05
     C                   MOVE      XAN05         ORCLI
     C                   callp     updhtmlvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   MOVEA     DATA(98)      XAN07
     C                   MOVE      XAN07         ORWT
     C                   callp     updhtmlvar('ORWT':
     C                             %trim(%editc(ORWT:'3')))
     C                   MOVEA     DATA(105)     XAN05
     C                   MOVE      XAN05         ORVOL
     C                   callp     updhtmlvar('ORVOL':
     C                             %trim(%editc(ORVOL:'3')))
     C                   MOVEA     DATA(110)     VADRNA
     C                   callp     updhtmlvar('WSCNA':VADRNA)
     C                   MOVEA     DATA(140)     ORAREA
     C                   callp     updhtmlvar('WSAREA':ORAREA)
     C                   MOVEA     DATA(142)     XAN08
     C                   MOVE      XAN08         ORDTWL
     C                   callp     updhtmlvar('ORDTWL':
     C                             %trim(%editw(ORDTWL:'    .  .  ')))
     C                   callp     updhtmlvar('WSTT':'T&T')
      *
     C                   callp     wrtsection('tablerow')
      *
     C                   DELETE    WRKCGISR
     C     WINTEG        READE     WRKCGISS                               33
     C  N33              ENDDO
      *
     C                   callp     wrtsection('tableend')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      BRPARF
     C                   OPEN      WRKCGISS
     C                   OPEN      GSSORL17
     C                   OPEN      GSSPOF
     C                   OPEN      GSSRFF
     C                   OPEN      VADR
      *
     C     VADRK         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    ORCNO
     C                   KFLD                    VADRNR
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     WRKCGISS
     C                   close     GSSORL17
     C                   close     GSSPOF
     C                   close     GSSRFF
     C                   close     VADR
     C                   eval      *inlr = *on
      *
     C                   endsr
