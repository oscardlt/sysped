********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11048 * 01 CB  PTF11 Testindikator
********************************************************************
     FSVE051D   CF   E             WORKSTN
     F                                     SFILE(SVE051DA:ZRECNO)
     F                                     INFDS(FBAREA)
     FSVEA      UF A E           K DISK
     FSVEA1     IF   E           K DISK
     F                                     RENAME(SVEAR:SVEAR1)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    SVEA_SYAV     ZVEA_SYAV
     C     *LIKE         DEFINE    SVEA_SYOP     ZVEA_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVE051DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SVEA1
     C                   READ      SVEA1                                  94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVE051DA
     C                   READ      SVEA1                                  94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVE051DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE051DE
     C                   WRITE     SVE051DØ
     C                   WRITE     SVE051DC
     C                   EXFMT     SVE051DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVE051DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

     C                   CLEAR                   SVE051DD
     C                   MOVEA     '0000'        *IN(30)

     C     SRBA1         TAG

      *____________
      * Blanke felt
      *""""""""""""
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE051DØ
     C                   EXFMT     SVE051DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *_______
      * F4=Søk
      *"""""""
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     *IN04         IFEQ      '1'
S01  C*    LINNBR        ANDEQ     23
N01  C     LINNBR        ANDEQ     26
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
     C   30              GOTO      SRBA1
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      SVEA_SYAV=ZVEA_SYAV
     C                   EVAL      SVEA_SYOP=ZVEA_SYOP
     C                   EVAL      SVEA_OMEO=ZVEA_OMEO
     C                   EVAL      SVEA_OMHA=ZVEA_OMHA
     C                   EVAL      SVEA_OMTL=ZVEA_OMTL
     C                   EVAL      SVEA_OMTY=ZVEA_OMTY
N01  C                   EVAL      SVEA_0035=ZVEA_0035
     C                   WRITE     SVEAR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     SVE051DA

     C                   GOTO      SRBA1

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE051DA
      *____________
      * Flytte felt
      *""""""""""""
     C     SVEA_SYAV     CHAIN(N)  SVEA                               53
     C                   MOVEA     '0000'        *IN(30)
     C                   EVAL      ZVEA_SYAV=SVEA_SYAV
     C                   EVAL      ZVEA_SYOP=SVEA_SYOP
     C                   EVAL      ZVEA_OMEO=SVEA_OMEO
     C                   EVAL      ZVEA_OMHA=SVEA_OMHA
     C                   EVAL      ZVEA_OMTL=SVEA_OMTL
     C                   EVAL      ZVEA_OMTY=SVEA_OMTY
N01  C                   EVAL      ZVEA_0035=SVEA_0035

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE051DØ
     C                   EXFMT     SVE051DD
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *_______
      * F4=Søk
      *"""""""
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     *IN04         IFEQ      '1'
S01  C*    LINNBR        ANDEQ     23
N01  C     LINNBR        ANDEQ     26
     C                   ENDIF
      *____________
      * Teste input
      *""""""""""""
     C   30              GOTO      SRBB1
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     ZVEA_SYAV     CHAIN     SVEA                               53
     C                   EVAL      SVEA_SYOP=ZVEA_SYOP
     C                   EVAL      SVEA_OMEO=ZVEA_OMEO
     C                   EVAL      SVEA_OMHA=ZVEA_OMHA
     C                   EVAL      SVEA_OMTL=ZVEA_OMTL
     C                   EVAL      SVEA_OMTY=ZVEA_OMTY
N01  C                   EVAL      SVEA_0035=ZVEA_0035
     C                   UPDATE    SVEAR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     SVEA_SYAV     CHAIN     SVEA                               53
     C                   DELETE    SVEAR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVE051DA
     C                   UPDATE    SVE051DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
