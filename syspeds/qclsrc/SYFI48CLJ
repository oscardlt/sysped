/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10XXX * 01 JOV PTF10 KODE L I PARAMETER SUM(UDS 80) = PRT LABELS FOR TUR*/
/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
/* 10XXX * 02 YBC PTF10 NYTT PARAMETER FOR Å KALLE SYFI48ALC               */
/* 10XXX * 03 SH  PTF10 OVERFLOW                                           */
/* 10XXX * 04 JOV PTF11 AFP FRONT MARGIN                                   */
/* ********************************************************************    */
             PGM        PARM(&IN03 &IN04 &EDS)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('lo')
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&STAA) TYPE(*DEC) LEN(3 0) VALUE(011)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LPP) TYPE(*DEC) LEN(3 0) VALUE(72)
             DCL        VAR(&OF) TYPE(*DEC) LEN(3 0) VALUE(64)
             DCL        VAR(&ALLE) TYPE(*CHAR) LEN(3) /* ALLE */
             DCL        VAR(&IN03) TYPE(*LGL) LEN(1) /* Hvis &in03 +
                          på, skal losseliste  være stående. */
             DCL        VAR(&IN04) TYPE(*LGL) LEN(1) /* Hvis &in04 +
                          på, skal printetype være 4224. */
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til stående losseliste. */
             DCL        VAR(&PT1) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til stående losseliste. */
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til liggende losseliste. */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til liggende losseliste. */
             DCL        VAR(&CPI) TYPE(*DEC) LEN(2 0) VALUE(10) /* +
                          Antall tegn pr. tomm. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&EDS) TYPE(*CHAR) LEN(39)
/*N01*/      DCL        VAR(&TUR) TYPE(*CHAR) LEN(8)
/*N01*/      DCL        VAR(&SUM) TYPE(*CHAR) LEN(1)
             RTVJOBA    USER(&AUSER)
    /* */
/*N01*/      RTVDTAARA  DTAARA(*LDA (22   8)) RTNVAR(&TUR)
/*N01*/      RTVDTAARA  DTAARA(*LDA (80   1)) RTNVAR(&SUM)
             RTVDTAARA  DTAARA(*LDA (821 10)) RTNVAR(&PRT1)
             RTVDTAARA  DTAARA(*LDA (831 10)) RTNVAR(&PT1)
             RTVDTAARA  DTAARA(*LDA (841 10)) RTNVAR(&PRT2)
             RTVDTAARA  DTAARA(*LDA (851 10)) RTNVAR(&PT2)
             RTVDTAARA  DTAARA(*LDA (993 3)) RTNVAR(&ALLE)
    /* ----------------------------------------------------------- */
    /* SKRIVER UT LOSSELISTE */
    /* ----------------------------------------------------------- */
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             CALL       PGM(GETPRN) PARM(&AVD &STAA &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&LASER = 'J') THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('066')
             CHGVAR     VAR(&OF) VALUE('060')
             ENDDO
/*N03*/      IF         COND(&LASER = 'P') THEN(DO)
             CHGVAR     VAR(&OF) VALUE('060')
             ENDDO
/*N03*/      IF         COND(&LASER = 'F') THEN(DO)
             CHGVAR     VAR(&OF) VALUE('060')
             ENDDO
    /**************************************/
    /* START FOR IKKE PDF DOKUMENTER      */
    /**************************************/
             IF         COND(&LASER *NE 'P') THEN(DO)

    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR LOSSELISTE STÅENDE  */
    /* ----------------------------------------------------------- */
             IF         COND(&IN03 = '1') THEN(DO)
             OVRPRTF    FILE(SYFI48P) PAGESIZE(&LPP 120) CPI(15) +
                          OVRFLW(&OF) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES)
             ENDDO
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR LOSSELISTE LIGGENDE */
    /* ----------------------------------------------------------- */
             IF         COND(&IN03 = '0') THEN(DO)
             OVRPRTF    FILE(SYFI48P) PAGESIZE(48 120) CPI(10) +
                          OVRFLW(45) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT2) FORMTYPE(&PT2) COPIES(&COPIES)
             ENDDO
             ENDDO
    /**************************************/
    /* START FOR PDF DOKUMENTER      */
    /**************************************/
             IF         COND(&LASER *EQ 'P') THEN(DO)
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR LOSSELISTE STÅENDE  PDF  */
    /* ----------------------------------------------------------- */

             IF         COND(&IN03 = '1') THEN(DO)
             OVRPRTF    FILE(SYFI48P) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          120) CPI(15) OVRFLW(&OF) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          FRONTMGN(0 0.25) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES)
             ENDDO
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR LOSSELISTE LIGGENDE  PDF   */
    /* ----------------------------------------------------------- */
             IF         COND(&IN03 = '0') THEN(DO)
             OVRPRTF    FILE(SYFI48P) DEVTYPE(*AFPDS) PAGESIZE(48 +
                          120) CPI(10) OVRFLW(45) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT2) FORMTYPE(&PT2) COPIES(&COPIES)
             ENDDO
             ENDDO
    /**************************************/
    /* START FOR AFP DOKUMENTER      */
    /**************************************/
             IF         COND(&LASER *EQ 'F') THEN(DO)
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR LOSSELISTE STÅENDE  AFP  */
    /* ----------------------------------------------------------- */

             IF         COND(&IN03 = '1') THEN(DO)
             OVRPRTF    FILE(SYFI48P) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          120) CPI(15) OVRFLW(&OF) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          FRONTMGN(0 0.25) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES)
             ENDDO
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR LOSSELISTE LIGGENDE  AFP   */
    /* ----------------------------------------------------------- */
             IF         COND(&IN03 = '0') THEN(DO)
             OVRPRTF    FILE(SYFI48P) DEVTYPE(*AFPDS) PAGESIZE(48 +
                          120) CPI(10) OVRFLW(45) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT2) FORMTYPE(&PT2) COPIES(&COPIES)
             ENDDO
             ENDDO
    /* ----------------------------------------------------------- */
    /* */
    /* ----------------------------------------------------------- */
    /* SKRIVER UT LOSSELISTE */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
             RTVJOBA    NBR(&TIME)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)
             RMVM       FILE(ASSAM) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(ASSAM) MBR(&UNIK)
             OVRDBF     FILE(ASSAM) TOFILE(ASSAM) MBR(&UNIK)
             IF         COND(&ALLE = '***') THEN(DO)
/*S02        CALL       PGM(SYFI48ALC) PARM(&PRANT &IN03 &IN04) */
/*N02*/      CALL       PGM(SYFI48ALC) PARM(&PRANT &IN03 &LASER &IN04)
             GOTO       CMDLBL(UT)
             ENDDO
             CALL       PGM(SYFI48) PARM(&PRANT &IN03 &IN04 &LASER &EDS)
UT:          RMVM       FILE(ASSAM) MBR(&UNIK)
    /* ----------------------------------------------------------- */
    /* */
    /* ----------------------------------------------------------- */
    /* SKRIVER UT MEDLING TIL BRUKER */
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT 'losseliste er +
                          klare til utskrift.') MSGTYPE(*INFO)
    /* ----------------------------------------------------------- */
    /* */
    /* */
    /* ----------------------------------------------------------- */
    /* SKRIVER UT LABELS FOR TUREN HVIS USUM = L                   */
    /* ----------------------------------------------------------- */
             IF         COND(&SUM = 'L') THEN(DO)
             CALL       PGM(SYFA12CLL) PARM(&AVD '0000000' '000' +
                          &TUR '          ' '          ' 'J')
             ENDDO
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             ENDPGM
