      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQDRIV MODULE(*LIBL/TJINQDRIV *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDRIV      IF   E           K DISK    USROPN
     FDRIVL01   IF   E           K DISK    USROPN
     F                                     RENAME(DRIVR:DRIVR01)
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D SOKSJN          S             30
     D SOKSJA          S              8
     D SOKSJAN         S              8  0
     D user            S             10
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      Parse

     C                   EXSR      INIT

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬soksjn¬ : ¬'+%trim(SOKSJN)+'¬, '
        +'¬soksja¬ : ¬'+%trim(SOKSJA)+'¬, '
        +'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Find/send lines
     c     Error         IFEQ      *blanks
     C                   EXSR      LINES
     C                   ENDIF

     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Quit

     C                   EXSR      Exit
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         BEGSR
     C                   EVAL      USER = zhbgetvar('USER')
     C                   EVAL      SOKSJN  = zhbgetvar('SOKSJN')
     C                   EVAL      SOKSJN  = uppify(SOKSJN)
     C     LO:UPC        XLATE     SOKSJN        SOKSJN
     C                   EVAL      SOKSJA  = zhbgetvar('SOKSJA')
     C                   EVAL      SOKSJAN = c2n2(SOKSJA)
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DRIV
     C  N50              CLOSE     DRIVL01
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         BEGSR
     c                   EVAL      JSONstring ='"sjoflist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   MOVE      *zeros        AntVist           5 0
     C*    SOKSJN        IFEQ      *BLANKS
     C*    SOKSJAN       ANDEQ     *ZEROS
     C*                  GOTO      UTLES
     C*                  END

     C                   SELECT
     C                   WHEN      SOKSJAN <> 0
     C     SOKSJAN       SETLL     DRIV
     C                   READ      DRIV                                   33
     C                   WHEN      SOKSJN <> *BLANKS
     C     SOKSJN        SETLL     DRIVL01
     C                   READ      DRIVL01                                33
     C                   OTHER
     C     SOKSJN        SETLL     DRIVL01
     C                   READ      DRIVL01                                33
     C                   ENDSL

     C                   DOW       *IN33 = *OFF

     C                   ADD       1             ANTLEST
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬dransa¬ : ¬'+%trim(%editc(DRANSA:'Z'))+'¬, '
          +'¬drnavn¬ : ¬'+%trim(DRNAVN)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C     ANTLEST       CABGE     100           UTLES

     C                   SELECT
     C                   WHEN      SOKSJAN <> 0
     C*                  READ      DRIV                                   33
     C                   GOTO      UTLES
     C                   WHEN      SOKSJN <> *BLANKS
     C                   READ      DRIVL01                                33
     C                   OTHER
     C                   READ      DRIVL01                                33
     C                   ENDSL

     C                   ENDDO

     C     UTLES         TAG
     c                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END

      * hent Parametre som går igjen i mange prog:
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024

     C  N50              OPEN      DRIV
     C  N50              OPEN      DRIVL01
     C                   ENDSR
