     FSVT062D   CF   E             WORKSTN
     FSVTFI     IF   E             DISK
     FSVTHA2    UF   E           K DISK
     D                SDS
     D  WUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UOK               1            '1'=Ja  '0'=Nei
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
     C     1             CHAIN     SVTFI
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SRB1          TAG
     C                   WRITE     SVT062DØ
     C                   EXFMT     SVT062DA
      * F3 eller F12
     C     *IN03         IFEQ      *ON
     C     *IN12         OREQ      *ON
     C                   GOTO      SRB9
     C                   ENDIF
      *Special før Systema ?
     C     ZPWD          IFEQ      'Straffe12 '
     C                   GOTO      SRB8
     C                   ENDIF
      * Hæmta upplysningar från handlæggarregister
     C     WUSER         CHAIN(N)  SVTHA2                             51
     C                   IF        *IN51
     C                   EXFMT     SVT062DC
     C                   EVAL      UOK='0'
     C                   GOTO      SRB9
     C                   ENDIF
      * Identen låst ?
     C     SVTH_LOCK     IFEQ      'X'
     C                   EXFMT     SVT062DH
     C                   EVAL      UOK='0'
     C                   GOTO      SRB9
     C                   ENDIF
      * Kolla bruker id + løsenord
     C     ZPWD          IFNE      SVTH_PWD
     C                   ADD       1             X                 1 0
     C                   IF        X > 3
     C                   EXFMT     SVT062DF
     C     WUSER         CHAIN     SVTHA2                             51
     C                   IF        *IN51 = *OFF
     C                   EVAL      SVTH_LOCK = 'X'
     C                   UPDATE    SVTHAR
     C                   ENDIF
     C                   EVAL      UOK='0'
     C                   GOTO      SRB9
     C                   ELSE
     C                   EVAL      ZMSGNO='SVT0035'
     C                   EVAL      *IN30 = *ON
     C                   GOTO      SRB1
     C                   ENDIF
     C                   ENDIF
      * Løsenordet får inte vara øver 30 dagar gammalt
     C                   MOVE      SVTH_PWDD     FROMU
     C                   CALL      'SVT064R'
     C                   PARM                    FROMU             8
     C                   PARM                    WDT
     C                   PARM      *ZEROS        DAYS              5 0
     C     DAYS          IFGT      30
     C                   EXFMT     SVT062DG
     C                   IF        *IN09 = *OFF
     C                   EVAL      UOK='0'
     C                   GOTO      SRB9
     C                   ELSE
     C                   CALL      'SVT065R'
     C                   GOTO      SRB1
     C                   ENDIF
     C                   ENDIF

      *__________
      * Allt ok !
      *""""""""""
     C     SRB8          TAG
     C                   EVAL      UOK='1'

     C     SRB9          TAG
     C                   ENDSR
