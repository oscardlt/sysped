********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VED PROD.KODE=BRZxx SEND TSR+7+Zxx+3+12
********************************************************************
     FDOKUF     IF   E           K DISK
     FDOKUFM    IF   E           K DISK
     FDOKUFV    IF   E           K DISK
     FDOKUFE    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFAK13     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FDOK29     IF   E           K DISK
     FTELLGE    UF A E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      IF A E             DISK
     FECNT04A   O  A E             DISK
     FECPI04A   O  A E             DISK
     FEDTM04A   O  A E             DISK
     FEFTX04A   O  A E             DISK
     FEGID04A   O  A E             DISK
     FELOC04A   O  A E             DISK
     FEMEA04A   O  A E             DISK
     FEMOA04A   O  A E             DISK
     FENAD04A   O  A E             DISK
     FECTA04A   O  A E             DISK
     FECOM04A   O  A E             DISK
     FEPCI04A   O  A E             DISK
     FERFF04A   O  A E             DISK
     FETDT04A   O  A E             DISK
     FETOD04A   O  A E             DISK
     FETSR04A   O  A E             DISK
     FEUNH      O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *________________
      * DS for mottaker
      *""""""""""""""""
     D                 DS
     D  DFKNM                  1     12
     D  DFKNM2                 2     12
     D                 DS
     D  DFAD3M                 1     30
     D  WADM3A                 1      4
     D  WADM3B                 6     30
     D  QADM3                  1     30
     D                 DS
     D  DF1004                 1     35
     D  DF1004A                2     10
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      */////////////////////////////////////////////////////////////////////////
      *// HOVEDRUTINE                                                         //
      */////////////////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * GET RECORDNUMBER FOR EDIM
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     *ON           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      * Hent fraktbrev
     C     DOKKEY        CHAIN     DOKUF                              51
     C     *IN51         CABEQ     *ON           SRA9
      * Hent oppdrag
     C     HEAKEY        CHAIN     HEADF                              51
     C     *IN51         CABEQ     *ON           SRA9
      * GET INTERCHG.ID
     C     TRAKEY        CHAIN     TRAN                               51
     C     *IN51         CABEQ     *ON           SRA9
      *  HENTER EVENTUELLT UTLEGG PÅ FAKTURA
     C     FAKTKE        CHAIN     FAK13                              51
     C                   IF        *IN51
     C                   MOVE      *BLANKS       WSVALK            3
     C                   Z-ADD     0             WSBELV           13 2
     C                   ELSE
     C                   MOVEL     FAVAL         WSVALK            3
     C                   Z-ADD     FABELV        WSBELV
     C                   ENDIF
      * ADD TO EDI-DB
     C                   EXSR      DTM00
     C                   EXSR      TSR00
     C                   EXSR      MOA00
     C                   EXSR      FTX00
     C                   EXSR      TOD02
     C                   EXSR      RFF03
     C                   EXSR      TDT08
     C                   EXSR      NAD11A
     C                   EXSR      NAD11B
     C                   EXSR      GID18
     C                   EXSR      CNT00
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTMIN'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '04A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'BIG01'       U0057
     C                   MOVEL     '610'         U1001
     C                   MOVEL     DF1004        U1004
     C                   MOVEL     '9'           U1225
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   MOVEL     VM0004        M0004
     C                   MOVEL     VM0010        M0010
     C                   MOVE      VM0035        M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   Z-ADD     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   Z-ADD     DFAVD         MAVD
     C                   Z-ADD     DFOPD         MTDN
     C                   Z-ADD     DFFBNR        MFFBNR
     C                   MOVE      DFVEN         MVEN
     C                   WRITE     EDIMR

     C     SRA9          TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kontrolltotaler                                    CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     0             WREP0
      * Vekt
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '7 '          C6069
     C                   Z-ADD     DFVKT         ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR
      * Antall kolli
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '11'          C6069
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'PCE'         C6411
     C                   WRITE     CNTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sendningsdato                                      DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     0             WREP0

     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         DREP0

     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         D2380
     C                   MOVEL     '102'         D2379

     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR

     C                   Z-ADD     0             WREP0

     C                   IF        DFPROK = 'BR14'
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     0             FGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   EVAL      F4451  = 'TRR'
     C                   EVAL      F4440A = 'DAVA'
     C                   WRITE     FTXR
     C                   END

     C     R29KEY        SETLL     DOK29
     C     R29KEY        READE     DOK29                                  90
     C     *IN90         DOWEQ     *OFF

     C     D29TXT        IFNE      *BLANKS
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     0             FGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   MOVEL     'ICN'         F4451
     C     35            SUBST     D29TXT:1      F4440A
     C                   WRITE     FTXR
     C                   GOTO      FTX00B
     C                   ENDIF

     C     R29KEY        READE     DOK29                                  90
     C                   ENDDO

     C     FTX00B        ENDSR
      *////////////////////////////////////////////////////////////
      *  Steder                                             LOC09 /
      *////////////////////////////////////////////////////////////
     C     LOC09         BEGSR
      *_________________
      * Innleveringssted
      *"""""""""""""""""
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     9             LGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1

     C                   MOVEL     '202'         L3227
     C                   MOVEL     DFPNLS        L3225
     C                   MOVEL     '87'          L1131A

     C                   WRITE     LOCR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Steder                                             LOC11 /
      *////////////////////////////////////////////////////////////
     C     LOC11         BEGSR
      *_________________
      * Innleveringssted
      *"""""""""""""""""
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     11            LGN
     C                   Z-ADD     NREP0         WREP0
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1

     C                   MOVEL     '202'         L3227
     C                   MOVEL     '1000'        L3225
     C                   MOVEL     '87'          L1131A

     C                   WRITE     LOCR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Pengebeløp                                         MOA00 /
      *////////////////////////////////////////////////////////////
     C     MOA00         BEGSR

     C                   Z-ADD     0             WREP0
      * Girobeløp
      * eller "X" - i bildet / fra faktura
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '22'          M5025
     C                   Z-ADD     WSBELV        ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   MOVEL     'NOK'         M6345

     C                   WRITE     MOAR
     C                   else
      * eller etterkrav etter avsender
     C     DFBELE        IFGT      0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '22'          M5025
     C                   Z-ADD     DFBELE        ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   MOVEL     'NOK'         M6345

     C                   WRITE     MOAR
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Godsavsender                     NAD11A /
      *////////////////////////////////////////////////////////////
     C     NAD11A        BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     11            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVE      DFKNSS        WUNDNR
     C     CUNKEY        CHAIN     CUNDL01                            51
     C                   MOVEL     'CZ'          N3035
     C                   MOVEL     DFKNS         N3039
     C                   MOVEL     '87'          N3055
     C                   MOVEL     DFNAVS        N3036A
     C                   MOVEL     DFAD1S        N3042A
     C                   MOVEL     DFAD3S        N3164
     C                   MOVEL     DFPNLS        N3251
     C                   MOVEL     'NO'          N3207

     C                   WRITE     NADR

     C                   EXSR      LOC11

     C                   EXSR      RFF15

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Mottaker                         NAD11B /
      *////////////////////////////////////////////////////////////
     C     NAD11B        BEGSR
      * Juridisk mottaker
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     11            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'CN'          N3035
     C                   MOVEL     DFNAVM        N3036A
     C                   MOVEL     DFAD1M        N3042A
     C                   MOVEL     WADM3A        N3251
     C     N3251         IFEQ      *BLANKS
     C                   MOVEL     DFPOUL        N3251
     C                   ENDIF
     C     1             DO        24            XN                3 0
     C                   IF        %SUBST(WADM3B:XN:1) <> ' '
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   IF        XN < 24
     C                   EVAL      N3164 = %SUBST(WADM3B:XN:25-XN)
     C                   ELSE
     C                   EVAL      N3164 = *BLANKS
     C                   END
     C                   MOVEL     'NO'          N3207

     C                   WRITE     NADR

      * Fysisk mottaker
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     11            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'DP'          N3035
     C                   MOVEL     DFNAVL        N3036A
     C                   MOVEL     DFAD1L        N3042A
     C     N3042A        IFEQ      *BLANKS
     C                   MOVEL     DFAD1M        N3042A
     C                   ENDIF
     C                   MOVEL     DFPOUL        N3251
     C                   MOVEL     DFADUL        N3164
     C                   MOVEL     'NO'          N3207

     C                   WRITE     NADR

      * HVIS ETTERKRAV SÅ SKAL TLF.NR OPPGIS
     C     DFBELE        IFNE      *ZEROS
     C                   EXSR      CTA12
     C                   END

      * NOTIFY PARTY
     C                   IF        *IN33 AND *IN33 = *OFF
     C                   ADD       1             WREP0

     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     11            NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'NI'          N3035
     C     N3042A        IFEQ      *BLANKS
     C                   MOVEL     DFAD1M        N3042A
     C                   ENDIF
     C                   MOVEL     DFPOUL        N3251
     C                   MOVEL     DFADUL        N3164
     C                   MOVEL     'NO'          N3207

     C                   WRITE     NADR

      * KONTAKT INFORMASJON
     C     DOKUFEK       CHAIN     DOKUFE                             33
     C  N33              EXSR      CTA12NI
     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kontakt / teleofnnr                                CTA12 /
      *////////////////////////////////////////////////////////////
     C     CTA12         BEGSR

     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     12            CGN
     C                   Z-ADD     NREP0         CREP0
     C                   Z-ADD     1             CREP1

     C                   MOVEL     'IC'          C3139
     C                   WRITE     CTAR

      * Det SKAL være tlf..
     C                   EXSR      COM12

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *            teleofnnr                                COM12 /
      *////////////////////////////////////////////////////////////
     C     COM12         BEGSR

     C                   CLEAR                   COMR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     12            CGN
     C                   Z-ADD     NREP0         CREP0
     C                   Z-ADD     1             CREP1
     C                   Z-ADD     1             CREP2

     C                   MOVEL     DFLEVI        C3148
     C                   MOVEL     'TE'          C3155
     C                   WRITE     COMR

     C                   ENDSR

      *////////////////////////////////////////////////////////////
      *  Kontakt / teleofnnr                              CTA12NI /
      *////////////////////////////////////////////////////////////
     C     CTA12NI       BEGSR

     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     12            CGN
     C                   Z-ADD     NREP0         CREP0
     C                   Z-ADD     1             CREP1

     C                   MOVEL     'IC'          C3139
     C                   IF        FE_C3413 <> *BLANKS
     C                   MOVEL     FE_C3413      C3413
     C                   END
     C                   IF        FE_C3412 <> *BLANKS
     C                   MOVEL     FE_C3412      C3412
     C                   END
     C                   WRITE     CTAR

      * Det SKAL være tlf..
     C                   EXSR      COM12NI

     C                   ENDSR

      *////////////////////////////////////////////////////////////
      *            teleofnnr                              COM12NI /
      *////////////////////////////////////////////////////////////
     C     COM12NI       BEGSR

     C                   CLEAR                   COMR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     12            CGN
     C                   Z-ADD     NREP0         CREP0
     C                   Z-ADD     1             CREP1
     C                   MOVE      *ZEROS        CREP2

     C                   IF        FE_C3148A <> *BLANKS
     C                   ADD       1             CREP2
     C                   MOVEL     FE_C3148A     C3148
     C                   MOVEL     'TE'          C3155
     C                   WRITE     COMR
     C                   END

     C                   IF        FE_C3148B <> *BLANKS
     C                   ADD       1             CREP2
     C                   MOVEL     FE_C3148B     C3148
     C                   MOVEL     'TE'          C3155
     C                   WRITE     COMR
     C                   END

     C                   IF        FE_C3148C <> *BLANKS
     C                   ADD       1             CREP2
     C                   MOVEL     FE_C3148C     C3148
     C                   MOVEL     'FX'          C3155
     C                   WRITE     COMR
     C                   END

     C                   IF        FE_C3148D <> *BLANKS
     C                   ADD       1             CREP2
     C                   MOVEL     FE_C3148D     C3148
     C                   MOVEL     'TE'          C3155
     C                   WRITE     COMR
     C                   END

     C                   IF        FE_C3148E <> *BLANKS
     C                   ADD       1             CREP2
     C                   MOVEL     FE_C3148E     C3148
     C                   MOVEL     'EM'          C3155
     C                   WRITE     COMR
     C                   END

     C                   ENDSR

      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     0             WREP0
      * File identification number
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'AQY'         R1153
     C     'POSTEN04'    CHAIN     TELLGE                             33
     C                   IF        *IN33
     C                   MOVE      '1000'        XAN04             4
     C                   EVAL      GECO = 'POSTEN04'
     C                   Z-ADD     1001          GENO
     C                   WRITE     TELLGER
     C                   ELSE
     C                   MOVE      GENO          XAN04
     C                   IF        GENO = 9999
     C                   Z-ADD     1000          GENO
     C                   ELSE
     C                   ADD       1             GENO
     C                   END
     C                   UPDATE    TELLGER
     C                   END
     C                   MOVEL     XAN04         R1154
     C                   WRITE     RFFR
      * FRAKBREVBR
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'CMR'         R1153
     C                   EVAL      R1154 = DF1004
     C                   WRITE     RFFR
      * Senders referanse
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'CU'          R1153
     C                   MOVEL     DFAVD         R1154
     C                   CAT       '/':0         R1154
     C                   MOVEL     DFOPD         WFOPDA            7
     C                   CAT       WFOPDA:0      R1154
     C                   CAT       '/':0         R1154
     C                   MOVEL     DFFBNR        WFFBNRA           3
     C                   CAT       WFFBNRA:0     R1154
     C                   CAT       '/':0         R1154
     C                   CAT       HESG:0        R1154
     C                   WRITE     RFFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF15 /
      *////////////////////////////////////////////////////////////
     C     RFF15         BEGSR
     C                   Z-ADD     0             WREP1
      * Gironummer (bank/post)
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C     BANKG         ANDNE     *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     15            RGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'ADE'         R1153
     C                   MOVEL     BANKG         R1154
     C                   WRITE     RFFR
     C                   ELSE
      * eller etterkrav etter avsender
     C     DFBELE        IFGT      0
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     15            RGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'ADE'         R1153
     C                   MOVEL     DFPRO2        R1154
     C                   WRITE     RFFR
     C                   ELSE
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    DFRI          WFRI
     C     *LIKE         DEFINE    DFAVD         WFAVD
     C     *LIKE         DEFINE    DFOPD         WFOPD
     C     *LIKE         DEFINE    DFFBNR        WFFBNR
     C     *LIKE         DEFINE    VMFIRM        WFIRM
     C     *LIKE         DEFINE    KUNDNR        WUNDNR

     C                   EVAL      WFRI='F'
      * DEF AV KEYS
     C     DOKKEY        KLIST
     C                   KFLD                    WFRI
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     DOKUFEK       KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     TRAKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    DFTRAN
     C     R29KEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     CUNKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    WUNDNR
     C     HEAKEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C     FAKTKE        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
      * Input parameters
     C     *ENTRY        PLIST
     C                   PARM                    WFIRM
     C                   PARM                    WFAVD
     C                   PARM                    WFOPD
     C                   PARM                    WFFBNR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0
     C                   Z-SUB     ØN152         ØN152
     C                   MOVE      '-'           ØSIGN             1
     C                   ELSE
     C                   MOVE      '+'           ØSIGN
     C                   ENDIF
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'
     C                   MOVE      ØA152H        ØA12
     C                   ELSE
     C                   MOVEL     ØA152X        ØA12
      * PGA. UNB-SEGMENT BRUKER KOMMA FOR DESIMAL SKILLETEGN
     C*                  MOVE      '.  '         ØA12
     C                   MOVE      ',  '         ØA12
     C                   MOVE      ØA152D        ØA12
     C                   END
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C*    Ø12(ØX)       IFEQ      '.'
     C     Ø12(ØX)       IFEQ      ','
     C                   SUB       1             ØX
     C                   ENDIF
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'
     C                   SUB       1             ØX
     C                   MOVE      '-'           Ø12(ØX)
     C                   ENDIF
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportdetaljer                                  TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      * Other segments

     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     8             TGN
     C                   Z-ADD     WREP0         TREP0

     C                   MOVEL     '20'          T8051
     C*                  MOVEL     '3'           T8067
     C                   MOVEL     '31'          T8179
     C                   SELECT
     C                   WHEN      DFPROK = 'BR7'
     C                   MOVEL     'BPR'         T3127
     C                   MOVEL     '87'          T3055B
     C                   WHEN      DFPROK = 'BR14'
     C                   MOVEL     'NCS'         T3127
     C                   MOVEL     '87'          T3055B
     C                   OTHER
     C                   MOVEL     'NCS'         T3127
     C                   MOVEL     '87'          T3055B
     C                   ENDSL

     C                   WRITE     TDTR

     C*                  EXSR      LOC09

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Terms of Delivery                                  TOD02 /
      *////////////////////////////////////////////////////////////
     C     TOD02         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      * Other segments

     C                   CLEAR                   TODR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     2             TGN
     C                   Z-ADD     WREP0         TREP0

     C                   MOVEL     '5'           T4055
     C                   SELECT
     C                   WHEN      DFBELA = 'S'
     C                   MOVEL     'PP'          T4215
     C                   WHEN      DFBELA = 'M'
     C                   MOVEL     'CC'          T4215
     C                   WHEN      DFBELA = 'A'
     C                   MOVEL     'TP'          T4215
     C                   OTHER
     C                   MOVEL     'CC'          T4215
     C                   ENDSL

     C                   WRITE     TODR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportprodukt                                   TSR00 /
      *////////////////////////////////////////////////////////////
     C     TSR00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      * Hent produktkode
     C     DFPROK        IFEQ      *BLANKS
     C                   EVAL      DFPROK='BR7  '
     C                   ENDIF
      * Produkt ?
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
N01  C                   IF        %SUBST(DFPROK:3:1) = 'Z'
N01  C                   EVAL      T4065 = '7'
N01  C                   ELSE
     C     3             SUBST     DFPROK:3      T4065
N01  C                   END
     C                   SELECT
     C                   WHEN      DFPROK = 'BR7' OR DFPROK = 'BR14'
     C                   MOVEL     '5'           T7273A
     C                   MOVEL     '3'           T4219
N01  C                   WHEN      %SUBST(DFPROK:3:1) = 'Z'
N01  C                   EVAL      T7273A = %SUBST(DFPROK:3:3)
N01  C                   EVAL      T4219  = '3'
     C                   OTHER
     C                   MOVEL     '5'           T7273A
     C                   MOVEL     '3'           T4219
     C                   END
     C                   MOVEL     '12'          T7085

     C                   WRITE     TSRR
      * Giro ?
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C     3             SUBST     DFPROK:3      T4065
     C                   MOVEL     'ZPG'         T7273A
     C                   MOVEL     '3'           T4219
     C                   MOVEL     '12'          T7085

     C                   WRITE     TSRR
     C                   ELSE
     C     DFBELE        IFNE      *ZEROS
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C     3             SUBST     DFPROK:3      T4065
     C                   MOVEL     'ZPG'         T7273A
     C                   MOVEL     '3'           T4219
     C                   MOVEL     '12'          T7085

     C                   WRITE     TSRR
     C                   ENDIF
     C                   ENDIF
      *
     C                   IF        DFTPK1 <> *BLANKS
     C                   IF        ((((DFTPK1 <> 'BRZPG') OR
     C                                (DFKDE <> 'X')) OR
     C                                (WSBELV <> 0)) OR
     C                                (DFBELE <> 0))
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C     3             SUBST     DFPROK:3      T4065
     C     3             SUBST     DFTPK1:3      T7273A
     C                   MOVEL     '3'           T4219
     C                   MOVEL     '12'          T7085
     C                   WRITE     TSRR
     C                   END
     C                   END
      *
     C                   IF        DFTPK2 <> *BLANKS
     C                   IF        ((((DFTPK2 <> 'BRZPG') OR
     C                                (DFKDE <> 'X')) OR
     C                                (WSBELV <> 0)) OR
     C                                (DFBELE <> 0))
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C     3             SUBST     DFPROK:3      T4065
     C     3             SUBST     DFTPK2:3      T7273A
     C                   MOVEL     '3'           T4219
     C                   MOVEL     '12'          T7085
     C                   WRITE     TSRR
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Godsdetaljer                                       GID18 /
      *////////////////////////////////////////////////////////////
     C     GID18         BEGSR
      *____________
      * Varelinje-1
      *""""""""""""
      * Other segments (Write av GID er flyttet til PCI23)
     C                   EXSR      PCI23
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kolli ID                                           PCI23 /
      *////////////////////////////////////////////////////////////
     C     PCI23         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C     DOKUFMK1      KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C     DOKUFMK2      KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   MOVE      DFRI          FMRI
     C                   MOVE      DFAVD         FMAVD
     C                   MOVE      DFOPD         FMOPD
     C                   MOVE      DFFBNR        FMFBNR
     C                   MOVE      *ZEROS        FMLINR
     C                   MOVE      'N'           XMINPUT           1
      *
     C     DOKUFMK1      SETLL     DOKUFM
     C     DOKUFMK1      READE     DOKUFM                                 33
     C                   IF        *IN33
     C     DOKUFMK2      SETLL     DOKUFM
     C     DOKUFMK2      READE     DOKUFM                                 33
     C                   EVAL      XMINPUT = 'J'
     C                   END
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C     FMST          OREQ      'E'
     C                   IF        FMMRK1 = *BLANKS
      * Antall av
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     23            PGN
     C                   ADD       1             WREP0
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '24'          P4233
     C                   MOVE      *BLANKS       P7102A
     C                   MOVE      *BLANKS       W7102A1
     C                   MOVE      *BLANKS       W7102A2
     C                   Z-ADD     WREP0         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           W7102A1           7
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           W7102A2           7
     C                   MOVEL     W7102A1       P7102A
     C                   CAT       '/':0         P7102A
     C                   CAT       W7102A2:0     P7102A
     C                   WRITE     PCIR
     C                   ELSE
      * Kolliid
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     23            PGN
     C                   ADD       1             WREP0
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '30'          P4233
     C                   MOVEL     FMMRK1        P7102A
     C                   WRITE     PCIR
     C                   END
      *
     C                   EXSR      FTX18
      *
     C                   EXSR      MEA20
      * Legg ut GID som er på et nivå Høyere
     C                   CLEAR                   GIDR
     C                   Z-ADD     WMN           GMN
     C                   Z-ADD     18            GGN
     C                   Z-ADD     WREP0         GREP0
      *
     C                   Z-ADD     WREP0         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G1496
     C                   MOVEL     '1'           G7224A
     C                   MOVEL     'PK'          G7065A
      *
     C                   WRITE     GIDR
      *
     C                   END
     C                   IF        XMINPUT = 'N'
     C     DOKUFMK1      READE     DOKUFM                                 33
     C                   ELSE
     C     DOKUFMK2      READE     DOKUFM                                 33
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX18 /
      *////////////////////////////////////////////////////////////
     C     FTX18         BEGSR
      *
     C     DOKUFVKEY     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FVLINR
     C                   IF        FMLINR = 0
     C                   Z-ADD     FMMKNR        FVLINR
     C                   ELSE
     C                   Z-ADD     FMLINR        FVLINR
     C                   END
     C     DOKUFVKEY     CHAIN     DOKUFV                             33
     C   33              EVAL      FVVT = %TRIM(DFVS) + ' ' + %TRIM(DFVS2)
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     18            FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   EVAL      F4451  = 'AAA'
     C                   EVAL      F4453  = '4'
     C     1             DO        35            XN                3 0
     C                   IF        %SUBST(FVVT:XN:1) <> ' '
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   IF        XN < 35
     C                   EVAL      F4440A = %SUBST(FVVT:XN:36-XN)
     C                   ELSE
     C                   EVAL      F4440A = '.'
     C                   END
      *
     C                   WRITE     FTXR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kolli ID                                           MEA20 /
      *////////////////////////////////////////////////////////////
     C     MEA20         BEGSR
      *
     C                   CLEAR                   MEAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     20            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     'WT'          M6311
     C                   MOVEL     'G'           M6313
     C                   MOVEL     'KGM'         M6411
     C     DFVKT         DIV       DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
      *
     C                   WRITE     MEAR
      *
     C                   ENDSR
