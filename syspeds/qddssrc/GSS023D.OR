      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5037 * 01 YBC PTF5 MULIGHET FOR Å VELGE NY LEDERTEKST
P5117 * 02 YBC PTF5 NY SORTERINGSFELT
P6041 * 03 YBC PTF6 BESTILLE MERKELAPPE SAMMENTIDIG
P7041 * 04 YBC PTF7 OPSJON: L = LOKASJON, B = BOX
P8XXX * 05 YBC PTF8 OPSJON: 6 = PRINT MERKELAPP
P8XXX * 06 YBC PTF8 FARLIG GODS
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R GSS023DA                  SFL
     A            ORCLI          5Y 0H
     A            ORWT           7Y 1H
     A            ORVOL          5Y 2H
S04  A*           WSNR           1A  I  5  2VALUES(' ' '2' '4' '5' '8')
S05  A*           WSNR           1A  I  5  2VALUES(' ' '2' '4' '5' '8' 'L' 'B')
N05  A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '5' '8' 'L' 'B' -
N05  A                                      '6')
     A            ORONO          7Y 0O  5  5EDTCDE(4)
     A            POCONO        20A  O  5 13
N06  A  38                                  COLOR(RED)
     A            ORSNO          3Y 0O  5 34EDTCDE(4)
     A            WXCLI          5Y 0B  5 38EDTCDE(4)
     A  29                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A            WXWT           7Y 1B  5 44EDTCDE(4)
     A  29                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A            WXVOL          5Y 2B  5 53EDTCDE(4)
     A  29                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A            ORPOS1         5A  O  5 60
     A            ORLC           5A  H
     A            ORBCD          5A  H
     A            XLCBCD         5A  O  5 66
     A            WXCLNF         3Y 0B  5 72EDTCDE(4)
     A  29                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A            WXCLNT         3Y 0B  5 76EDTCDE(4)
     A  29                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A          R GSS023DB                  SFLCTL(GSS023DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS023D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with orders on trip'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Trip No.'
     A            TRTNO          9Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 23'Clients No.'
     A            TRCNO          8Y 0O  2 35EDTCDE(4)
     A                                      COLOR(WHT)
     A N40                              2 45'Vessel'
     A N40        TRBCD          5A  O  2 52COLOR(WHT)
     A  40                              2 45'locat.'
     A  40        TRLOCF         5A  O  2 52COLOR(WHT)
     A                                  2 60'Tot. weight:'
     A            WSWT           7Y 1O  2 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
E05  A N29                              3  2'Type opt:  2=Chg colli no.  4=Dlt -
E05  A                                      5=Dsp  6=Label  8=Inv.basis  L/B=Lo-
E05  A                                      k/Box'
     A                                      COLOR(BLU)
E05  A  29                              3  2'Type options:  5=Display  6=Label'
     A                                      COLOR(BLU)
     A N40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Locat-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A  40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Vesse-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A          R GSS023DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N29                                  CF06(06 'Puck up')
     A                                      CF08(08 'SØK')
     A N29                                  CF10(10 'Print')
     A                                      CF12(12 'Cancel')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N29N27                           1  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print packinglist  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A N29 27                           1  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print proforma  F12=Ca-
     A                                      ncel'
     A                                      COLOR(BLU)
     A  29                              1  2'F3=End  F5=Refresh  F8=Search  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R GSS023DE
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Search from --> '
N02  A  40                              2 19'Vessel:'
N02  A  40        W2BCD          5A  I  2 27DSPATR(UL)
N02  A                                      DSPATR(PC)
E02  A                                  2 36'Order No.'
E02  A            W2ONO          7Y 0I  2 46DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Cancel'
     A                                      COLOR(BLU)
     A          R GSS023DG
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N29                                  CF06(06 'Puck up')
     A                                      CF08(08 'SØK')
     A N29                                  CF10(10 'Print')
     A                                      CF12(12 'Cancel')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS023D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with orders on trip'
     A                                      DSPATR(HI)
     A                                  2  2'Trip No.'
     A            TRTNO          9Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 23'Clients No.'
     A            TRCNO          8Y 0O  2 35EDTCDE(4)
     A                                      COLOR(WHT)
     A N40                              2 45'Vessel'
     A N40        TRBCD          5A  O  2 52COLOR(WHT)
     A  40                              2 45'locat.'
     A  40        TRLOCF         5A  O  2 52COLOR(WHT)
E05  A N29                              3  2'Type opt:  2=Chg colli no.  4=Dlt -
E05  A                                      5=Dsp  6=Label  8=Inv.basis  L/B=Lo-
E05  A                                      k/Box'
     A                                      COLOR(BLU)
E05  A  29                              3  2'Type options:   5=Display  6=Label'
     A                                      COLOR(BLU)
     A N40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Locat-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A  40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Vesse-
     A                                       Col. f/t'
     A                                      DSPATR(HI)
     A                                  6  6'No order found'
     A                                      COLOR(RED)
     A N29N27                          23  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print packinglist  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A N29 27                          23  2'F3=End  F5=Refresh  F6=Pick up  F8-
     A                                      =Search  F10=Print proforma  F12=Ca-
     A                                      ncel'
     A                                      COLOR(BLU)
     A  29                             23  2'F3=End  F5=Refresh  F8=Search  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS023DX
     A                                      WINDOW(*DFT 7 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRT         10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'No. of copies:'
     A            WSCPY          1  0B  4 17DSPATR(UL)
     A                                  5  2'Send fax ?    '
     A            WSFAX          1A  B  5 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  5 20'Y/N'
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A     7  2MSGID(GSS0042 GSSMSGF)
      *
     A          R GSS023DY
E03  A                                      WINDOW(*DFT 12 73 *NOMSGLIN)
N01  A                                      CF04(04 'Søking')
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A            MSGHOD        25A  O  1  2MSGID(&MSGHN SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'HEADING TEXT'
     A            WSFT1         70A  B  3  2DSPATR(UL)
     A N98N99                               DSPATR(PC)
     A            WSFT2         70A  B  4  2DSPATR(UL)
     A                                  5  2'Printer name :'
     A            WSPRT         10A  B  5 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
     A                                  6  2'No. of copies:'
     A            WSCPY          1  0B  6 17DSPATR(UL)
     A                                  7  2'Layout:       '
     A            WSNYL          1A  B  7 17DSPATR(UL)
     A                                      VALUES('P' 'L' 'O')
     A                                  7 20'P=Portrait L=Landscape O=Portrait2'
     A                                  8  2'Curr. code   :'
     A            WSCUR          3A  B  8 17DSPATR(UL)
     A  98                                  DSPATR(HI)
     A  98                                  DSPATR(PC)
     A                                  9  2'Send fax ?    '
     A            WSFAX          1A  B  9 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  9 20'Y/N'
N03  A N28                             10  2'Labels?'
N03  A N28        WSML           1A  B 10 10DSPATR(UL)
N03  A                                      VALUES('Y' 'N')
N03  A N28                             10 12'P.name:'
N03  A N28        WSMLP         10A  B 10 20DSPATR(UL)
N03  A N28                             10 31'Free text:'
N03  A N28        WSFTXT        30A  B 10 42DSPATR(UL)
E03  A                                 11  2'Comm.invoice?:'
E03  A            WSCOMM         1A  B 11 17DSPATR(UL)
N01  A                                      VALUES('Y' 'N')
E03  A                                 11 22'Clientsnr:'
E03  A            WSKNC          8  0B 11 34DSPATR(UL)
N01  A  97                                  DSPATR(HI)
N01  A  97                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
S01  A*                                10  2'F12=Cancel'
E03  A N99N98N97                       12  2'F4= Søk   F12=Cancel'
     A                                      COLOR(BLU)
E03  A  99                             12  2MSGCON(25 GSS0042 SYMSGF)
     A                                      COLOR(WHT)
E03  A  98                             12  2MSGCON(25 GSS0066 SYMSGF)
     A                                      COLOR(WHT)
E03  A  97                             12  2MSGCON(25 SPK0001 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGHN          7A  P
      *
N05  A          R GSS023DZ
N05  A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
N05  A                                      CF12(12 'Avbryt')
N05  A                                      OVERLAY
N05  A                                  1  2'Print labels'
N05  A                                      COLOR(WHT)
N05  A                                  3  2'Printer name:'
N05  A            WSPRTM        10A  B  3 17DSPATR(UL)
N05  A                                      DSPATR(PC)
N05  A                                  4  2'Barcode     :'
N05  A            WSSK           1A  B  4 17DSPATR(UL)
N05  A                                      VALUES('Y' 'N')
N05  A                                  4 21'(Y/N)'
N05  A                                  5  2'F12=Cancel'
N05  A                                      COLOR(BLU)
N05  A  99        WSMSGN        25A     6  2MSGID(GSS0042 SYMSGF)
N05   *
