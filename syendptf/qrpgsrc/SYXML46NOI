     FGODSAF    IF   E           K DISK
     FGODSFI    IF   E           K DISK
     FGODSJL06  IF   E           K DISK
     F                                     RENAME(GODSJR:GODSJR6)
     F                                     PREFIX(T_)
     FGODSJF    UF A E             DISK
     FGODSGF    UF A E           K DISK
     FGODSHF    O  A E             DISK
     D                 DS
     D  WDATO                  1      8
     D  WYEAR                  1      4
     D  WDATOY                 3      4
     D  WDATOM                 5      6
     D  WDATOD                 7      8
     D                 DS
     D  WDATO6                 1      6
     D  WD                     1      2
     D  WM                     3      4
     D  WY                     5      6
     D                SDS
     D  ZUSER                254    263
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *____________
      * Hovedlogikk
      *""""""""""""
      * Flytt numerisk dato til alfa
     C                   MOVEL     UDATO         WDATO
      * Hent dagnummer fra input
     C                   EVAL      WY=WDATOY
     C                   EVAL      WM=WDATOM
     C                   EVAL      WD=WDATOD
     C                   CALL      'SYGE25CL'
     C                   PARM                    WDATO6
     C                   PARM                    WDAGIAR           3
      * Hent bevillingsnummer og enhetsnummer
     C                   MOVE      UAVD          WAVD34            2
     C     'XX'          CAT       WAVD34:0      WAVD14            4
     C     WAVD14        CHAIN     GODSAF
     C                   IF        %FOUND
     C                   MOVE      GFLBKO        WGFLBKO           5
     C                   MOVE      GFENH         WGFENH            1
     C                   IF        WGFENH = *BLANKS
     C                   EVAL      WGFENH = '0'
     C                   ENDIF
     C                   ELSE
     C                   EVAL      UERRORC='1'                                  Låst avd finnes ikke
     C                   GOTO      SLUTT
     C                   ENDIF
      * Hent navn på lager
     C     LAGKEY        KLIST
     C                   KFLD                    GFLBKO
     C                   KFLD                    GFENH
     C     LAGKEY        CHAIN     GODSFI
     C                   IF        %FOUND
     C                   MOVE      GFLBS1        WGFLBS1          30
     C                   ELSE
     C                   EVAL      UERRORC='2'                                  Lagerk. finnes ikke
     C                   GOTO      SLUTT
     C                   ENDIF
      * Turnummer må ikke finnes siden tidligere
     C     UTURN         CHAIN     GODSJL06
     C                   IF        %FOUND
     C                   EVAL      UERRORC='3'                                  Turnr finnes allered
     C*                  GOTO      SLUTT
     C                   ENDIF
      * Finnes dagens godsnummerteller allerede ?
     C     *LIKE         DEFINE    GGGN1         WGGGN1
     C     *LIKE         DEFINE    GGGN2         WGGGN2
     C                   EVAL      WGGGN1=WYEAR+WGFLBKO+WDAGIAR+WGFENH
     C     WGGGN1        CHAIN     GODSGF
      * Ja
     C                   IF        %FOUND
     C                   EVAL      WGGGN2=GGGN2
     C                   MOVEL     GGGN2         WGGGN2N           2 0
     C                   ADD       1             WGGGN2N
     C                   MOVE      WGGGN2N       GGGN2
     C                   UPDATE    GODSGFR
      * Nei
     C                   ELSE
     C                   EVAL      GGGN1=WGGGN1
     C                   EVAL      WGGGN2='01'
     C                   EVAL      GGGN2='02'
     C                   WRITE     GODSGFR
     C                   ENDIF
     C     *LIKE         DEFINE    GOGN          WGOGN
     C                   EVAL      WGOGN=WGGGN1+WGGGN2
      * Skriv til godsjournal
     C                   CLEAR                   GODSJR
     C                   EVAL      GOGN=WGOGN
     C                   EVAL      GOBILN=UKJENN
     C*                  EVAL      GOTRNR=UMRN
     C                   EVAL      GOTRDT=UDATO
     C                   EVAL      GOGREN=UGRENSP
     C                   EVAL      GOTURN=UTURN
     C                   MOVEL     WGFLBS1       GOMOTT
     C                   WRITE     GODSJR
      * Skriv til historikk
     C                   CLEAR                   GODSHR
     C                   EVAL      GOGN=WGOGN
     C*                  EVAL      GOTRNR=UMRN
     C                   EVAL      GOHPGM='SYXML46N'
     C                   MOVE      WDT           GOHDAT
     C                   MOVE      WTM           GOHTIM
     C                   EVAL      GOHUSR=ZUSER
     C                   WRITE     GODSHR
      * Gi tilbake nytt godsnummer
     C                   EVAL      UGODSN=WGOGN
      * Slutt
     C     SLUTT         TAG
     C
     C                   EVAL      *INLR=*ON
      *______________________________________________
      * Init av pgm                              INIT
      *""""""""""""""""""""""""""""""""""""""""""""""
     C     INIT          BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    USIGN             3
     C                   PARM                    UDATO             8 0
     C                   PARM                    UGRENSP          20
     C                   PARM                    UTURN            17
     C                   PARM                    UKJENN           13
     C                   PARM                    UMRN             20
     C                   PARM                    UGODSN           15
     C                   PARM                    UERRORC           1
      * Hent dagens dato og tid
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
