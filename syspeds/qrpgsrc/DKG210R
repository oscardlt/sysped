     FDKG210D   CF   E             WORKSTN
     F                                     SFILE(DKG210DB:ZRECNO)
     FDKTKD     UF A E           K DISK
     FDKTKD1    IF   E           K DISK
     F                                     RENAME(DKTKDR:DKTKDR1)
     D TABELL          S              3    DIM(49) CTDATA PERRCD(1) ASCEND
     D TABTXT          S             50    DIM(49) ALT(TABELL)
      *_________________
      * DS for constants
      *"""""""""""""""""
     D                 DS
     D  WTXT01                 1     40    INZ('(Ingen koder stemmer -
     D                                     med søket !)')
      *//////////////
      *  Main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *  Move blanks to PF                                  MOVEA /
      *////////////////////////////////////////////////////////////
     C     MOVEA         BEGSR
     C                   MOVE      *BLANKS       DKKD_KD2
     C                   MOVE      *BLANKS       DKKD_KD3
     C                   MOVE      *BLANKS       DKKD_TXT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move format to PF                                  MOVEB /
      *////////////////////////////////////////////////////////////
     C     MOVEB         BEGSR
     C                   MOVE      ÅDKKD_TXT     DKKD_TXT
     C                   MOVE      *BLANKS       DKKD_KD2
     C                   MOVE      *BLANKS       DKKD_KD3
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move PF to format                                  MOVEC /
      *////////////////////////////////////////////////////////////
     C     MOVEC         BEGSR
     C                   MOVE      DKKD_TXT      ÅDKKD_TXT
     C                   MOVE      DKKD_KD2      ÅDKKD_KD2
     C                   MOVE      DKKD_KD3      ÅDKKD_KD3
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move blanks to format                              MOVED /
      *////////////////////////////////////////////////////////////
     C     MOVED         BEGSR
     C                   MOVE      *BLANKS       ÅDKKD_TXT
     C                   MOVE      *BLANKS       ÅDKKD_KD2
     C                   MOVE      *BLANKS       ÅDKKD_KD3
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  INZ                                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    DKKD_KD       WDKKD_KD
     C     *LIKE         DEFINE    DKKD_TXT      WDKKD_TXT
     C                   MOVE      *BLANKS       WDKKD_KD
      *_____________
      * Define key's
      *"""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WDKKD_TYP
     C                   KFLD                    WDKKD_KD
     C     KEYB          KLIST
     C                   KFLD                    WDKKD_TYP
     C                   KFLD                    WDKKD_TXT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Calculate formats                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Select tag
      *"""""""""""
     C     SRB0          TAG

     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9

     C     SRB1          TAG
      *________________
      * Get name of tag
      *""""""""""""""""
     C     WDKKD_TYP     LOOKUP    TABELL        TABTXT                   20
     C   20              MOVEL     TABTXT        WTYPTXT
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     DKG210DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C     XDKKD_KD      IFEQ      '*ALL  '
     C     XDKKD_KD      OREQ      *BLANKS
     C                   MOVE      *BLANKS       WDKKD_KD
     C                   ELSE
     C                   MOVE      XDKKD_KD      WDKKD_KD
     C                   END
     C     XDKKD_TXT     IFEQ      '*ALL'
     C     XDKKD_TXT     OREQ      *BLANKS
     C                   MOVE      *BLANKS       WDKKD_TXT
     C                   MOVE      '0'           *IN82                          SORT BY CODE
     C                   ELSE
     C                   MOVE      '1'           *IN82                          SORT BY ALPHA
     C                   MOVEL     XDKKD_TXT     WDKKD_TXT
     C                   END
      *_____________
      * Fill Subfile
      *"""""""""""""
     C  N82KEYA          SETLL     DKTKD
     C   82KEYB          SETLL     DKTKD1
     C  N82WDKKD_TYP     READE(N)  DKTKD                                  94
     C   82WDKKD_TYP     READE     DKTKD1                                 94
     C     *IN94         IFEQ      '1'
     C                   MOVE      *BLANKS       DKKD_KD
     C                   MOVEL     WTXT01        DKKD_TXT
     C                   MOVE      '1'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     DKG210DB
     C                   GOTO      SRB3
     C                   ENDIF

     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO

     C  N94              DO        15
     C                   MOVE      '0'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   MOVEL     DKKD_TXT      ZDKKD_TXT
     C                   WRITE     DKG210DB
     C  N82WDKKD_TYP     READE(N)  DKTKD                                  94
     C   82WDKKD_TYP     READE     DKTKD1                                 94
     C  N94              END
      *______
      * Enter
      *""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     DKG210DØ
     C                   WRITE     DKG210DD
     C                   EXFMT     DKG210DC

      *________
      * F3=Exit
      *""""""""
     C   03              MOVE      *BLANKS       DKKD_KD
     C     *IN03         CABEQ     '1'           SRB9
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRB0

     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *___________
      * F5=Refresh
      *"""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *__________
      * F6=Create
      *""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBF
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_________
      * Option ?
      *"""""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN05
     C     *IN52         DOWEQ     '0'

     C                   READC     DKG210DB                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CABEQ     '1'           SRB9                           Select
     C     ZOP           CASEQ     '2'           SRBB                           Change
     C     ZOP           CASEQ     '3'           SRBC                           Copy
     C     ZOP           CASEQ     '4'           SRBD                           Delete
     C     ZOP           CASEQ     '5'           SRBE                           Display
     C                   END
     C                   END

     C  N52              END

     C     XDKKD_KD      IFNE      '*ALL'
     C     XDKKD_TXT     ORNE      '*ALL'
     C                   GOTO      SRB1
     C                   ENDIF

     C     *IN03         CABEQ     '1'           SRB9                           End
     C     *IN05         CABEQ     '1'           SRB1                           Refresh
     C     *IN05         CABEQ     '0'           SRB3                           Subfile

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Select tag                                          SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C     SRBA1         TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     DKG210DØ
     C                   EXFMT     DKG210DA
     C                   MOVEA     '00'          *IN(30)
      *________
      * F3=Exit
      *""""""""
     C     *IN03         CABEQ     '1'           SRBA9
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRBA9
      *______
      * Enter
      *""""""
      * Qualfier tag valid ?
     C*    WDKKD_TYP     COMP      '001'                              3030
     C*  30WDKKD_TYP     COMP      '002'                              3030
     C*  30WDKKD_TYP     COMP      '003'                              3030
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN31
     C                   MOVE      'SY00010'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Change                                              SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Move fields
      *""""""""""""
     C                   MOVE      DKKD_KD       WDKKD_KD
     C     KEYA          CHAIN(N)  DKTKD                              53
     C                   MOVE      DKKD_KD       ÅDKKD_KD
     C                   MOVE      '1'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)

     C     SRBB1         TAG

     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     DKG210DØ
     C                   EXFMT     DKG210DE
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *___________
      * Test input
      *"""""""""""
     C   30              GOTO      SRBB1
      *__________
      * Update PF
      *""""""""""
     C     KEYA          CHAIN     DKTKD                              53
     C                   EXSR      MOVEB
     C                   UPDATE    DKTKDR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    DKG210DB

     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Copy                                                SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *____________
      * Copy fields
      *""""""""""""
     C                   MOVE      DKKD_KD       WDKKD_KD
     C     KEYA          CHAIN(N)  DKTKD                              53
     C                   MOVE      DKKD_KD       ÅDKKD_KD
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   EXSR      MOVEA
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     DKG210DØ
     C                   EXFMT     DKG210DE
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅDKKD_KD      DKKD_KD
     C                   MOVE      WDKKD_TYP     DKKD_TYP
     C                   EXSR      MOVEB
     C                   WRITE     DKTKDR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     DKG210DB

     C                   END
     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Delete                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Delete record
      *""""""""""""""
     C                   MOVE      DKKD_KD       WDKKD_KD
     C     KEYA          CHAIN     DKTKD                              53
     C                   DELETE    DKTKDR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    DKG210DB
     C                   MOVE      '1'           *IN05

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Display                                             SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      DKKD_KD       WDKKD_KD
     C     KEYA          CHAIN(N)  DKTKD                              53
     C                   MOVE      DKKD_KD       ÅDKKD_KD
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     DKG210DØ
     C                   EXFMT     DKG210DE
     C                   MOVEA     '000'         *IN(30)
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Create                                              SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEA
     C                   EXSR      MOVED

     C     *IN03         DOWEQ     '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVE      *BLANKS       ÅDKKD_KD
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     DKG210DØ
     C                   EXFMT     DKG210DE
     C                   MOVEA     '0000'        *IN(30)
      *______________
      * End or Cancel
      *""""""""""""""
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅDKKD_KD      DKKD_KD
     C                   MOVE      WDKKD_TYP     DKKD_TYP
     C                   EXSR      MOVEB
     C                   WRITE     DKTKDR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     DKG210DB

     C                   ENDIF
     C                   ENDDO

     C                   ENDSR
**
001Angivelsesarter
002Angivelsesmedier
003Beregningsarter, præferencekoder
004Beregningslinier, arter
005Certifikatkoder R44.2
006Ekspeditionssted RA.2
007Emballagekoder R31.4
008Landekoder R15, R34
009Oplysningstypekoder R44.9
010Procedurekoder og bevillingskoder
011Procedurekoder R37
012Præferencedokumentationskoder R44.4
013Præferencekoder R36
014Præferencekoder, toldordninger, noter
015Statuskoder, toldordninger, noter
016Svartekstkoder
017Transportdokumentkoder R40
018Transportkoder R25, R26
019VAB, certifikatkoder R44.3, R44.2
020Valutakoder R22
021Suppl_vare_opl_kod R44.6a
022Suppl_enheder_kod R41.1
101Svartekstkoder
102Angivelsesart
103Ekspeditionssted
104Angivelsestype R1.1
105Bestemmelsesland
106Leveringsbetingelser
107Valutakoder
108Transportmåde
109Bet.måte transportutgifter
110Emballagekoder
111Indikator R.S32
112Procedurekoder R37
113Certifikattyper
114VAB-kode R44.3
115FN-kode R44.4
116Transportdokumenter R44.5.1
117Udgangstoldsted
118Summarisk angivelse
119Eksportartkoder
120Procedurekoder
121Procedurekoder ECS-YM
122Erklæringskoder
123T-status
124Alternativ mængde
125Lokationskoder i eEksport
126Angivelsestype R1.2
127Statuskoder CUSRES
