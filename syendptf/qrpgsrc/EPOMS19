      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS19 MODULE(*LIBL/EPOMS19
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 VIS ÅRSAK TIL FORSINKELSE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 02 YBC PTF12 Mulighet for skan ut kolli fra lager
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FEPOK02    IF   E           K DISK    USROPN
S02  F*EPOH      IF   E           K DISK    USROPN
N02  FEPOH      UF   E           K DISK    USROPN
     FEPOL      IF   E           K DISK    USROPN
N02  FEPOLSO    UF A E           K DISK    USROPN
     FVADR3     IF   E           K DISK    USROPN
     FKALENDER  IF   E           K DISK    USROPN
N01  FBRPARF    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
S02  D*WSKNK           S             35
N02  D WSKNK           S             36
N02  D WSBESSO         S             10
N02  D WSLINSO         S              7
N02  D BSLINSO         S              7  0
N02  D WSSCAN          S              1
     D WSPRT           S              1
     D WSCLT           S              1
     D BGCOL           S             20
     D IO              S             17    DIM(5000)
     D IN              S             35    DIM(5000)
     D ST              S              5    DIM(5000)
     D AN              S              7  0 DIM(5000)
     D NT              S              7  0 DIM(5000)
     D VKB             S             11  3 DIM(5000)
     D VKN             S             11  3 DIM(5000)
     D M3              S              9  3 DIM(5000)
     D DTIN            S              8  0 DIM(5000)
     D D07             S              1    DIM(5000)
     D D14             S              1    DIM(5000)
N01  D DLY             S             50    DIM(5000)
     D VisAChecked     s             10
     D VisAlle         s              1
     D Title           S            100
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D ELBES                   1     10
     D ELLIN                  11     17  0
     D IOREF                   1     17
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
N02   * Skan ut merket kolli
N02  C                   IF        WSSCAN = 'J'
N02  C     EPOLSOK       SETLL     EPOLSO
N02  C     EPOLSOK       READE     EPOLSO                                 33
N02  C                   DOW       *IN33 = *OFF
N02  C                   IF        SOSKUT = 'J'
N02  C                   EXSR      SRSCANOUT
N02  C                   END
N02  C                   DELETE    EPOLSOR
N02  C     EPOLSOK       READE     EPOLSO                                 33
N02  C                   ENDDO
N02  C                   END
N02   * Klargjør for skan ut
N02  C                   IF        BSLINSO = 0
N02  C     EPOLSOK       SETLL     EPOLSO
N02  C     EPOLSOK       READE     EPOLSO                                 33
N02  C                   DOW       *IN33 = *OFF
N02  C                   DELETE    EPOLSOR
N02  C     EPOLSOK       READE     EPOLSO                                 33
N02  C                   ENDDO
N02  C                   END
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')                            Programmets html
      *
      * SUMMER KOLLI I LAGER
     c     BSKN          SETLL     EPOK02
     c     BSKN          READE     EPOK02                                 33
     C                   DOW       *IN33 = *OFF
     C                   SELECT
     C                   WHEN      WSPRT = 'J'
     C                   WHEN      WSCLT = 'J'
     C     EKDTHK        CABEQ     0             LESNESTE
     C                   OTHER
     C     EKDTIN        CABEQ     0             LESNESTE
     C                   ENDSL
S02  C*    EPOHK         CHAIN     EPOH                               33
N02  C     EPOHK         CHAIN(N)  EPOH                               33
     C  N33EHINV         COMP      WSKNK                              3333
     C  N33EPOLK         CHAIN     EPOL                               33
     C  N33              EVAL      VAKURE = EHLEVN
     C  N33VADR3K        CHAIN     VADR3                              33
     C  N331             DO        5000          XN                5 0
     C                   SELECT
     C                   WHEN      IO(XN) = *BLANKS
     C                   EVAL      IO(XN) = IOREF
     C                   EVAL      IN(XN) = ELART
     C                   EVAL      ST(XN) = ELSTA
     C                   EVAL      AN(XN)  = ELNTKL
     C                   EVAL      NT(XN)  = 1
     C                   EVAL(H)   VKB(XN) = ELVKTL / ELNTKL
     C                   EVAL(H)   VKN(XN) = ELVKNL / ELNTKL
     C                   EVAL(H)   M3(XN)  = ELM3L / ELNTKL
     C                   LEAVE
     C                   WHEN      IO(XN) = IOREF
     C                   IF        ST(XN) < ELSTA
     C                   EVAL      ST(XN) = ELSTA
     C                   END
     C                   EVAL      NT(XN)  = NT(XN) + 1
     C                   EVAL(H)   VKB(XN) = VKB(XN) + ELVKTL / ELNTKL
     C                   EVAL(H)   VKN(XN) = VKN(XN) + ELVKNL / ELNTKL
     C                   EVAL(H)   M3(XN)  = M3(XN) + ELM3L / ELNTKL
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
      *
N01  C  N33              IF        ELREAS = *BLANKS
     C  N33              SELECT
     C*                  WHEN      EHDATO < XDT_14
     C                   WHEN      EKDTIN < XDT_14
     C                   EVAL      D14(XN) = 'J'
     C                   EVAL      D07(XN) = ' '
     C*                  WHEN      EHDATO < XDT_07 AND D14(XN) = *BLANKS
     C                   WHEN      EKDTIN < XDT_07 AND D14(XN) = *BLANKS
     C                   EVAL      D07(XN) = 'J'
     C                   ENDSL
N01  C                   END
      *
     C  N33              SELECT
     C                   WHEN      DTIN(XN) = 0
     C                   EVAL      DTIN(XN) = EKDTIN
     C                   WHEN      DTIN(XN) > EKDTIN
     C                   EVAL      DTIN(XN) = EKDTIN
     C                   ENDSL
N01   *
N01  C                   IF        *IN33 = *OFF
N01  C                   MOVE      *BLANKS       DLY(XN)
N01  C                   MOVEL     'POMDL'       PAID
N01  C                   MOVE      BIBRID        PABRID
N01  C     BRPARFK       CHAIN     BRPARF                             33
N01  c   33              Eval      PABRID = '*KUNDFT*'+BIFIRM
N01  C   33BRPARFK       CHAIN     BRPARF                             33
N01  C                   DOW       *IN33 = *OFF
N01  C                   MOVEL     PAVRDA        XAN02             2
N01  C                   IF        XAN02 = ELREAS
N01  C                   EVAL      DLY(XN) = %SUBST(PAVRDA:4:47)
N01  C                   LEAVE
N01  C                   END
N01  C     BRPARFK       READE     BRPARF                                 33
N01  C                   ENDDO
N01  C                   EVAL      *IN33 = *OFF
N01  C                   END
      *
     c     LESNESTE      TAG
     c     BSKN          READE     EPOK02                                 33
     C                   ENDDO
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete saker
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      *
      *
     C     1             DO        5000          XN
     C                   IF        IO(XN) = *BLANKS
     C                   LEAVE
     C                   END
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      *
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
      *
     C                   ENDDO
      *
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')                            programmets Html
     C                   callp     wrtsection('botStd')                         Body/htmlavsl
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSKNK   = zhbgetvar('WSKNK')
N02  C                   eval      WSBESSO = zhbgetvar('WSBESSO')
N02  C                   eval      WSLINSO = zhbgetvar('WSLINSO')
N02  C                   eval      BSLINSO = c2n2(WSLINSO)
N02  C                   eval      WSSCAN  = zhbgetvar('WSSCAN')
     C                   eval      WSPRT   = zhbgetvar('WSPRT')
     C                   eval      WSCLT   = zhbgetvar('WSCLT')
      * Checkboks for Vis alle
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellers kommer verdi fra siste valg via "VisAlle" som checked / blank(N)
     C     VisAchecked   Ifeq      'checked'
     C                   eval      VisAlle  = 'Y'
     C                   else
     C                   eval      VisAlle  = zhbgetvar('VisAlle')
     C                   end
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     EPOK02
     C                   CLOSE     EPOH
     C                   CLOSE     EPOL
N02  C                   CLOSE     EPOLSO
     C                   CLOSE     VADR3
     C                   CLOSE     KALENDER
N01  C                   CLOSE     BRPARF
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS19.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     c                   eval      Title='POMS Purchase Order Management System'
     c                             + ' (HubList)'
     C                   callp     updhtmlvar('TITLE':Title)
     c
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('WSKNK':WSKNK)
     C                   callp     updHTMLvar('WSPRT':WSPRT)
     C                   callp     updHTMLvar('WSCLT':WSCLT)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':BIBESK)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   MOVEL     IO(XN)        ELBES
     C                   callp     updHTMLvar('ELBES':ELBES)
     C                   MOVE      IO(XN)        ELLIN
     C                   callp     updHtmlVar('ELLIN':
     C                             %trim(%editc(ELLIN:'Z')))
     C*                  IF        ST(XN) >= 'D'
     C*                  callp     updHTMLvar('WSST':'Ready')
     C*                  ELSE
     C*                  callp     updHTMLvar('WSST':'')
     C*                  END
S02  C*    EPOHK2        CHAIN     EPOH                               33
N02  C     EPOHK2        CHAIN(N)  EPOH                               33
     C  N33              EVAL      VAKURE = EHLEVN
     C  N33VADR3K        CHAIN     VADR3                              33
     C   33              EVAL      VADRNA = EHLEVN
     C                   callp     updHTMLvar('VADRNA':VADRNA)
     C                   callp     updHTMLvar('ELART':IN(XN))
     C                   callp     updHtmlVar('WSAN':
     C                             %trim(%editc(AN(XN):'Z')))
     C                   callp     updHtmlVar('WSNT':
     C                             %trim(%editc(NT(XN):'Z')))
     C                   callp     updHtmlVar('WSVKB':
     C                             %trim(%editc(VKB(XN):'4')))
     C                   callp     updHtmlVar('WSVKN':
     C                             %trim(%editc(VKN(XN):'4')))
     C                   callp     updHtmlVar('WSM3':
     C                             %trim(%editc(M3(XN):'4')))
     C                   callp     updHtmlVar('WSDTIN':
     C                             %trim(%editw(DTIN(XN):'    .  .  ')))
     C                   SELECT
     C                   WHEN      D14(XN) = 'J'
     C                   callp     updHTMLvar('BGCOL':'bgcolor=red')
     C                   WHEN      D07(XN) = 'J'
     C                   callp     updHTMLvar('BGCOL':'bgcolor=yellow')
     C                   OTHER
     C                   callp     updHTMLvar('BGCOL':'')
     C                   ENDSL
N01  C                   callp     updHTMLvar('DELAY':DLY(XN))
N02   *
N02  C                   SELECT
N02  C                   WHEN      BSLINSO = 0
N02  C                   EVAL      SOKUND = BSKN
N02  C                   EVAL      SOINV  = WSKNK
N02  C                   EVAL      SOBES  = ELBES
N02  C                   EVAL      SOLIN  = ELLIN
N02  C                   EVAL      SOSTAT = ELSTA
N02  C                   EVAL      SOSKUT = 'J'
N02  C                   WRITE     EPOLSOR
N02  C                   callp     updHTMLvar('checked':'checked')
N02  C                   WHEN      WSBESSO = ELBES AND BSLINSO = ELLIN
N02  C     EPOLSOK1      CHAIN     EPOLSO                             33
N02  C                   IF        SOSKUT = 'J'
N02  C                   EVAL      SOSKUT = 'N'
N02  C                   callp     updHTMLvar('checked':'')
N02  C                   ELSE
N02  C                   EVAL      SOSKUT = 'J'
N02  C                   callp     updHTMLvar('checked':'checked')
N02  C                   END
N02  C                   UPDATE    EPOLSOR
N02  C                   OTHER
N02  C     EPOLSOK2      CHAIN(N)  EPOLSO                             33
N02  C                   IF        SOSKUT = 'J'
N02  C                   callp     updHTMLvar('checked':'checked')
N02  C                   ELSE
N02  C                   callp     updHTMLvar('checked':'')
N02  C                   END
N02  C                   ENDSL
      *
     C                   endsr
      *
N02   *=====================================================================******
N02   *  SKAN UT KOLLI FRA LAGER
N02   *=====================================================================******
N02  C     SRSCANOUT     begsr
N02   *
N02  C                   IF        *IN33 = *OFF
N02  C                   MOVE      *ZEROS        UMN               9
N02  C                   MOVE      SOLIN         ULIN              7
N02  C                   CALL      'CC024R'
N02  C                   PARM                    UMN
N02  C                   PARM                    WSKN
N02  C                   PARM                    SOBES
N02  C                   PARM                    ULIN
N02  C                   PARM                    USER
N02  C                   END
N02   *
N02  C     EPOHK3        CHAIN     EPOH                               33
N02  C                   IF        EHSTA <= '4'
N02  C     EPOHK3        SETLL     EPOL
N02  C     EPOHK3        READE     EPOL                                   33
N02  C                   MOVE      'J'           XDIRALL           1
N02  C                   DOW       *IN33 = *OFF
N02  C                   IF        ELAVD = 0
N02  C                   EVAL      XDIRALL = 'N'
N02  C                   LEAVE
N02  C                   END
N02  C     EPOHK3        READE     EPOL                                   33
N02  C                   ENDDO
N02  C                   IF        XDIRALL = 'J'
N02  C                   EVAL      EHSTA = '5'
N02  C                   END
N02  C                   UPDATE    EPOHR
N02  C                   ELSE
N02  C                   UPDATE    EPOHR
N02  C                   END
N02   *
N02  C                   endsr
N02   *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      EPOK02
     C                   OPEN      EPOH
     C                   OPEN      EPOL
N02  C                   OPEN      EPOLSO
     C                   OPEN      VADR3
     C                   OPEN      KALENDER
N01  C                   OPEN      BRPARF
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C     EPOHK         klist
     C                   kfld                    EKKUND
     C                   kfld                    EKBES
     C     EPOHK2        klist
     C                   kfld                    EKKUND
     C                   kfld                    ELBES
N02  C     EPOHK3        klist
N02  C                   kfld                    BSKN
N02  C                   kfld                    SOBES
     C     EPOLK         klist
     C                   kfld                    EKKUND
     C                   kfld                    EKBES
     C                   kfld                    EKLIN
N02  C     EPOLSOK       klist
N02  C                   kfld                    BSKN
N02  C                   kfld                    WSKNK
N02  C     EPOLSOK1      klist
N02  C                   kfld                    BSKN
N02  C                   kfld                    WSKNK
N02  C                   kfld                    WSBESSO
N02  C                   kfld                    BSLINSO
N02  C     EPOLSOK2      klist
N02  C                   kfld                    BSKN
N02  C                   kfld                    WSKNK
N02  C                   kfld                    ELBES
N02  C                   kfld                    ELLIN
     C     VADR3K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    EHKUND
     C                   kfld                    VAKURE
N01  C     BRPARFK       KLIST
N01  C                   kfld                    PABRID
N01  C                   kfld                    PAKUNR
N01  C                   kfld                    PAID
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           KADATE
     C     KADATE        SETLL     KALENDER
     C     1             DO        7
     C                   READP     KALENDER                               33
     C                   END
     C                   MOVE      KADATE        XDT_07            8 0
     C     1             DO        7
     C                   READP     KALENDER                               33
     C                   END
     C                   MOVE      KADATE        XDT_14            8 0
      *
     C                   endsr
      *
