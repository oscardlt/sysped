      *
      ***********************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV TILLEGS-GEBYRER FIRMA/KUNDENIVÅ
      ***********************************************************
      *
     FFAKTIL    UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FFAKTILFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KNAVN                  1     30
     D  KNAV7                  1      7
     D  KNA14                  1     14
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUND
     C     TILKEY        KLIST
     C                   KFLD                    WSKUND
     C                   KFLD                    WSNIV
     C                   KFLD                    WSTYPE
     C     KUFASK        KLIST
     C                   KFLD                    YKFTYP
     C                   KFLD                    KFKOD
     C     *LIKE         DEFINE    KFTYP         YKFTYP
     C                   MOVE      'N'           UUPDAT            1
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     WSKUND        SETLL     FAKTIL
     C                   READ(N)   FAKTIL                                 94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   if        FTKUND = 0
     c                   eval      KNAVN = '*FIRMASTANDARD'
     c                   else
     C                   MOVE      FTKUND        WSKUND
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     c                   endif
     C                   WRITE     SUBFIL
     C                   READ(N)   FAKTIL                                 94
     C  N94              END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     1             ZRECNO
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              move      *zeros        WSKUND
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     06            POSNBR
     C                   MOVE      *ZEROS        WSKUND
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       WSNIV
     C                   MOVE      *BLANKS       WSTYPE
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *ZEROS        WSMAX
     C                   MOVE      *BLANKS       WSLVK
     C                   MOVE      *BLANKS       WSLFK
     C                   MOVE      *ZEROS        WSLPRO
     C                   GOTO      START5
     C                   END
     C*** SØK  NY
     C   08              DO
     C                   MOVE      *ZEROS        WSKUND
     C                   GOTO      START6
     C                   END
     C*
     C     ZRECNO        IFNE      0
     c                   move      'N'           Refresh           1
     C                   EXSR      HENT
     c     Refresh       cabeq     'J'           Start
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       888786
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK
     C     START6        TAG
     C                   SETOFF                                       30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     06            POSNBR
     C                   EXFMT     SOKFRA
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      START6
     C                   END
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C* KUNDE MÅ VÆRE GYLDIG
     c     WSKUND        IfNE      *zeros
     C     KUNKEY        CHAIN     CUNDL01                            90
     C   90              MOVE      *BLANKS       KNAVN
     C   90              MOVE      'SPK0035'     MSGNR
     C   90              Z-ADD     6             POSNBR
     C   90              SETON                                        30
     C                   endif
      * KODE Nivå
     C     WSNIV         IFNE      'O'
     C     WSNIV         ANDNE     'L'
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1662'     MSGNR
     C                   Z-ADD     30            POSNBR
     C                   SETON                                        3089
     C                   END
     C* TYPE  hardkodet
     C     *IN30         IFNE      *ON
     C                   IF        %subst(WSTYPE:1:6)   <> 'LENGDE'
     C                             and %subst(WSTYPE:1:6)<>'BREDDE'
     C                             and %subst(WSTYPE:1:5)<>'HØYDE'
     C                             and %subst(WSTYPE:1:3)<>'ADR'
     C                   MOVE      'JOV1669'     MSGNR
     C                   Z-ADD     33            POSNBR
     c                   SETON                                        3088
     C                   END
     C                   END
      * *IN40 OFF = NY
     C  N40              DO
     C                   END
S04   *
      * Krav om tillegskode ved nivå O= Oppdrag
     C     WSNIV         IFeq      'O'
     C     WSKODE        ANDeq     ' '
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1663'     MSGNR
     C                   Z-ADD     44            POSNBR
     C                   SETON                                        3087
     C                   END
      * Krav om tillegskode ved nivå O= Oppdrag
     C     WSNIV         IFeq      'L'
     C     WSKODE        ANDne     ' '
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1666'     MSGNR
     C                   Z-ADD     44            POSNBR
     C                   SETON                                        3087
     C                   END
      * Krav om max CM (el.l) ved nivå O= Oppdrag
     C     WSNIV         IFeq      'O'
     C     WSMAX         ANDeq     *zeros
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1664'     MSGNR
     C                   Z-ADD     47            POSNBR
     C                   SETON                                        3086
     C                   END
      * Ved nivå O= Oppdrag  skal ikke Geb-kode mm angis (fikses i PRIK)
     C     WSNIV         IFeq      'O'
     C     *IN30         ANDNE     *ON
     c                   IF        WSLVK<>*blanks or WSLFK<>*blanks or
     c                             WSLPRO<>*zeros
     C                   MOVE      'JOV1665'     MSGNR
     C                   Z-ADD     55            POSNBR
     C                   SETON                                        30
     C                   SETON                                        858483
     C                   END
     C                   END
      * Krav om gebyrkode ved nivå L= Linje
     C     WSNIV         IFeq      'L'
     C     WSLVK         ANDeq     *blanks
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1667'     MSGNR
     C                   Z-ADD     55            POSNBR
     C                   SETON                                        3085
     C                   END
      * Krav om pros-sats ved nivå L= Linje
     C     WSNIV         IFeq      'L'
     C     WSLPRO        ANDeq     *zeros
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1668'     MSGNR
     C                   Z-ADD     70            POSNBR
     C                   SETON                                        3083
     C                   END
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     TILKEY        CHAIN     FAKTIL                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    FAKTILR
     C   33              WRITE     FAKTILR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KNAV7         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KNAV7         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '9'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KNAV7         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CPYSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     4             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C                   MOVE      FTNIV         WSNIV
     C                   MOVE      FTKUND        WSKUND
     C                   MOVE      FTTYPE        WSTYPE
     C     TILKEY        CHAIN     FAKTIL                             33
     C  N33              DELETE    FAKTILR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       88
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     CPYSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      FTKUND        WSFRAKN                                WSTYPE
     C     STCPY         TAG
     C                   EXFMT     CPYWND
     C                   SETOFF                                       3079
     C     *IN04         IFEQ      '1'
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSTILKN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   GOTO      STCPY
     C                   END
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   goto      UTCPY
     C                   END
      *
     C                   MOVE      'J'           Refresh
     C                   MOVE      WSTILKN       WSKUND
     C     KUNKEY        CHAIN     CUNDL01                            79
     C   79              goto      STCPY
     C*
     C     WSFRAKN       setll     faktil
     C     WSFRAKN       reade(n)  faktil                                 33
     C     *in33         doweq     '0'
     C                   move      WSTILKN       FTKUND
     C                   write     FAKTILR
     C     WSFRAKN       reade(n)  faktil                                 33
     C                   enddo
     C*
     C     UTCPY         ENDSR
     C*
     C***********************************************
     C     SØK           BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
      *
     C     *IN40         IFEQ      '0'                                           KUN V/NY
N01  C     POSNBR        ANDLT     28
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUND
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     30            POSNBR
     C                   GOTO      UTSØK
     C                   ENDIF
      * KODE
N01  C     POSNBR        IFEQ      44
N01  C                   EVAL      YKFTYP = 'AVTTIL'
N01  C                   MOVEL     WSKODE        KFKOD
N01  C                   CALL      'SYKUKD'
N01  C                   PARM                    YKFTYP
N01  C                   PARM                    UUPDAT
N01  C                   PARM                    KFKOD
N01  C                   MOVEL     KFKOD         WSKODE
N01  C                   ENDIF
     C*
      *
     C     UTSØK         ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       FTNIV
     C                   MOVE      *ZEROS        FTKUND
     C                   MOVE      *BLANKS       FTTYPE
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVEL     '*DELETE'     KNAV7
     C                   MOVE      *BLANKS       FTKODE
     C                   MOVE      *ZEROS        FTMAX
     C                   MOVE      *BLANKS       FTLVK
     C                   MOVE      *BLANKS       FTLFK
     C                   MOVE      *ZEROS        FTLPRO
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSNIV
     C                   MOVE      *ZEROS        WSKUND
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       WSTYPE
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *ZEROS        WSMAX
     C                   MOVE      *BLANKS       WSLVK
     C                   MOVE      *BLANKS       WSLFK
     C                   MOVE      *ZEROS        WSLPRO
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      FTNIV         WSNIV                                  WSNIV
     C                   MOVE      FTKUND        WSKUND                                 WSTYPE
     C                   if        WSKUND <> 0
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     c                   else
     c                   eval      KNAVN = '*FIRMASTANDARD'
     c                   endif
     C                   MOVE      FTTYPE        WSTYPE
     c                   clear                   FAKTILR                                WSKUND
     C     TILKEY        CHAIN(N)  FAKTIL                             33
     C                   MOVE      FTKODE        WSKODE
     C                   MOVE      FTMAX         WSMAX
     C                   MOVE      FTLVK         WSLVK
     C                   MOVE      FTLFK         WSLFK
     C                   MOVE      FTLPRO        WSLPRO
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     44            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSNIV         FTNIV
     C                   MOVE      WSKUND        FTKUND
     C                   MOVE      WSTYPE        FTTYPE
     C                   END
     C*
     C                   if        WSKUND <> 0
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     c                   else
     c                   eval      KNAVN = '*FIRMASTANDARD'
     c                   endif
     C     TILKEY        CHAIN     FAKTIL                             33
     C                   MOVE      WSKODE        FTKODE
     C                   MOVE      WSMAX         FTMAX
     C                   MOVE      WSLVK         FTLVK
     C                   MOVE      WSLFK         FTLFK
     C                   MOVE      WSLPRO        FTLPRO
     C*
     C                   ENDSR
     C*
