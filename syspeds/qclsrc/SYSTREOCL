             PGM
             DCL        VAR(&FILIBF) TYPE(*CHAR) LEN(10)
             RTVDTAARA  DTAARA(FIRMDT (141 10)) RTNVAR(&FILIBF)
   /* Fjern evt. hjelpefiler fra tidligere kjøringer */
             DLTF       FILE(STATGML)
             MONMSG     MSGID(CPF0000)
             DLTF       FILE(STATNY)
             MONMSG     MSGID(CPF0000)
   /* Bevar kopi av gammel STATFIL */
             CPYF       FROMFILE(*LIBL/STAT) TOFILE(STATGML) +
                          CRTFILE(*YES)
   /* Lag Ny tom fil (uten logiske = økt hastighet) */
             CRTDUPOBJ  OBJ(STAT) FROMLIB(&FILIBF) OBJTYPE(*FILE) +
                          NEWOBJ(STATNY)
   /* Override slik at regen.prog arbeider mot STATNY ikke STAT*/
             OVRDBF     FILE(STAT) TOFILE(STATNY)


   /* Antall vekt for alle oppdrag med HE01 = 'J' */
             CALL       PGM(SYSTREOP)
   /* Beløp hentet fra alle fakturalinjer (innt./kostn.) med status O*/
             CALL       PGM(SYSTREFA)


             DLTOVR     FILE(*ALL)
             CPYF       FROMFILE(STATNY) TOFILE(*LIBL/STAT) +
                          MBROPT(*REPLACE)

             DLTF       FILE(STATNY)
             MONMSG     MSGID(CPF0000)

             SNDPGMMSG  MSG('Ny STAT er generert - gammel er bevart +
                          (STATGML) - Slett når kontrollert!!!')
