      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SÆRAVGIFTER               ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     Fsysthlp   IF   E             DISK
     FTARI      IF   E           K DISK
     FSADSD     UF A E           K DISK
     FSADSDTST  IF   E           K DISK
     F                                     RENAME(SADSDR:SADSDRT)
     FKODTS8    IF   E           K DISK
     FSAD999FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  WXDTFD                 1      2  0
     D  WXDTFM                 3      4  0
     D  WXDTFÅ                 5      8  0
     D  WXDTF                  1      8  0
     D                 DS
     D  WSDTFD                 1      2  0
     D  WSDTFM                 3      4  0
     D  WSDTFÅ                 5      8  0
     D  WSDTF                  1      8  0
     D                 DS
     D  SDDTFÅ                 1      4  0
     D  SDDTFM                 5      6  0
     D  SDDTFD                 7      8  0
     D  SDDTF                  1      8  0
     D                 DS
     D  SXDTFÅ                 1      4  0
     D  SXDTFM                 5      6  0
     D  SXDTFD                 7      8  0
     D  SXDTF                  1      8  0
     D                 DS
     D  WXDTTD                 1      2  0
     D  WXDTTM                 3      4  0
     D  WXDTTÅ                 5      8  0
     D  WXDTT                  1      8  0
     D                 DS
     D  WSDTTD                 1      2  0
     D  WSDTTM                 3      4  0
     D  WSDTTÅ                 5      8  0
     D  WSDTT                  1      8  0
     D                 DS
     D  SDDTTÅ                 1      4  0
     D  SDDTTM                 5      6  0
     D  SDDTTD                 7      8  0
     D  SDDTT                  1      8  0
     D                 DS
     D  SXDTTÅ                 1      4  0
     D  SXDTTM                 5      6  0
     D  SXDTTD                 7      8  0
     D  SXDTT                  1      8  0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   Z-ADD     05            LINNBR
     C                   Z-ADD     02            POSNBR
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TNRF        YTNRF             8 0
     C                   MOVE      W2KDAÆ        YKDAÆ             2
     C                   MOVE      *BLANKS       YKDSÆ             3
     C                   MOVE      *ZEROS        YDTF              8 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SADSK1        SETLL     SADSD
     C     START1        TAG
     C                   READ(N)   SADSD                                  94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C     TARIK         CHAIN     TARI                               33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       TAALFA
     C                   MOVEL     '*UKJENT'     TAALFA
     C                   END
     C                   SELECT
     C     SDAKTK        WHENEQ    'I'
     C                   MOVEL     'INAKTIV'     SDST
     C     SDAKTK        WHENEQ    'V'
     C                   MOVEL     'VALGFRI'     SDST
     C                   OTHER
     C                   MOVEL     'AKTIV  '     SDST
     C                   END
     C                   MOVE      SDDTFD        WXDTFD
     C                   MOVE      SDDTFM        WXDTFM
     C                   MOVE      SDDTFÅ        WXDTFÅ
     C                   MOVE      SDDTTD        WXDTTD
     C                   MOVE      SDDTTM        WXDTTM
     C                   MOVE      SDDTTÅ        WXDTTÅ
     C                   WRITE     SUBFIL
     C                   READ(N)   SADSD                                  94
     C     *IN94         IFEQ      '0'
     C                   MOVE      SDTNRF        YTNRF
     C                   MOVE      SDKDAÆ        YKDAÆ
     C                   MOVE      SDKDSÆ        YKDSÆ
     C                   MOVE      SDDTF         YDTF
     C                   END
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     05            LINNBR
     C                   Z-ADD     02            POSNBR
     C     START3        TAG
     C                   SETOFF                                       98
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   END
     C                   EXFMT     BUNN
     c   01              do
     c                   movel     'sad999'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   SETOFF                                       9830
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY VARE
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     05            POSNBR
     C                   MOVE      *OFF          *IN40
     C                   GOTO      START5
     C                   END
      *** SØK FRA
     C   08              GOTO      START6
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94SADSK3        CHAIN(N)  SADSD                              94
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *** LAGE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000000'      *IN(86)
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     c   01              do
     c                   movel     'sad999'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START5
     c                   end
     C  N12              DO
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
      *** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     c   01              do
     c                   movel     'sad999'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START6
     c                   end
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      *
      * TEST TARIFFNR.
     C     TARIK1        CHAIN     TARI                               90
     C     *IN90         IFEQ      *ON
     C                   MOVE      'SPV0003'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        30
     C                   GOTO      SLT1
     C                   ENDIF                                                  <*IN90
      *
      * TEST AVGIFTSKODE
     C     KODS8K        CHAIN     KODTS8                             89
     C     *IN89         IFEQ      *ON
     C                   MOVE      'SPK0040'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     30            POSNBR
     C                   SETON                                        30
     C                   GOTO      SLT1
     C                   ENDIF                                                  <*IN89
      *
      * TEST F.O.M. DATO
     C                   CALL      'TESTDATO'
     C                   PARM      WSDTFD        XDD               2 0
     C                   PARM      WSDTFM        XMM               2 0
     C                   PARM      WSDTFÅ        XÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVE      'SPD0001'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   MOVE      '1'           *IN88
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLT1
     C                   END
      *
      * TEST F.O.M. DATO
     C     WSDTT         IFNE      99999999
     C                   CALL      'TESTDATO'
     C                   PARM      WSDTTD        XDD               2 0
     C                   PARM      WSDTTM        XMM               2 0
     C                   PARM      WSDTTÅ        XÅÅ               2 0
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVE      'SPD0001'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     55            POSNBR
     C                   MOVE      '1'           *IN87
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLT1
     C                   END
     C                   END
      *
     C                   MOVE      WSDTFD        SXDTFD
     C                   MOVE      WSDTFM        SXDTFM
     C                   MOVE      WSDTFÅ        SXDTFÅ
     C                   MOVE      WSDTTD        SXDTTD
     C                   MOVE      WSDTTM        SXDTTM
     C                   MOVE      WSDTTÅ        SXDTTÅ
     C     SXDTF         IFGT      SXDTT
     C                   MOVE      'SPD0007'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   MOVEA     '11'          *IN(87)
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLT1
     C                   END
      *
     C     SADSK         SETLL     SADSDTST
     C     SADSK0        READPE    SADSDTST                               33
     C     *IN33         IFEQ      '0'
     C     SXDTF         ANDNE     SDDTF
     C     SXDTF         ANDLT     SDDTT
     C                   MOVE      'YBC1795'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   MOVEA     '11'          *IN(87)
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLT1
     C                   END
     C     SADSK         SETLL     SADSDTST
     C     SADSK0        READE     SADSDTST                               33
     C     *IN33         IFEQ      '0'
     C     SXDTF         ANDNE     SDDTF
     C     SXDTT         ANDGT     SDDTF
     C                   MOVE      'YBC1795'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   MOVEA     '11'          *IN(87)
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLT1
     C                   END
      *
      * TEST SATS
     C     WSBLSÆ        IFEQ      *ZEROS
     C                   MOVE      'YBC0773'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     64            POSNBR
     C                   MOVE      '1'           *IN86
     C                   SETON                                        30
     C                   GOTO      SLT1
     C                   ENDIF                                                  <WSBLSM
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     SADSK         CHAIN     SADSD                              33
     C  N40
     CANN33              DO
     C                   UPDATE    SADSDR
     C                   MOVE      'YBC0774'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      '1'           *IN90
     C                   SETON                                        30
     C                   GOTO      SLT1
     C                   END
      *
     C                   EXSR      MOVWSS
     C     *IN40         IFEQ      '1'
     C                   UPDATE    SADSDR
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   WRITE     SADSDR
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
      *
     C     SLT1          ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
      *
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
      *
     C   98              DO
     C   99              MOVE      '1'           *IN98
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       14            XLIN              3 0
     C                   MVR                     XLIN
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C     SDST          COMP      'AKTIV'                            9898
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSS
     C                   END
      *
     C     WSNR          IFEQ      'A'
     C     SADSK2        CHAIN     SADSD                              33
     C                   MOVE      'A'           SDAKTK
     C                   UPDATE    SADSDR
     C                   MOVEL     'AKTIV  '     SDST
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      '1'           *IN99
     C                   END
      *
     C     WSNR          IFEQ      'I'
     C     SADSK2        CHAIN     SADSD                              33
     C                   MOVE      'I'           SDAKTK
     C                   UPDATE    SADSDR
     C                   MOVEL     'INAKTIV'     SDST
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C     WSNR          IFEQ      'V'
     C     SADSK2        CHAIN     SADSD                              33
     C                   MOVE      'V'           SDAKTK
     C                   UPDATE    SADSDR
     C                   MOVEL     'VALGFRI'     SDST
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGSS         BEGSR
     C***********************************************
     C                   MOVE      *ON           *IN40
     C                   EXSR      MOVSSW
     C                   MOVE      ' '           WSNR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     c   01              do
     c                   exsr      helpsr
     C                   GOTO      STCHG
     c                   end
     C   03              GOTO      SLUTT
     C     *IN12         IFEQ      '1'
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCHG
     C                   END
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWSS        BEGSR
     C***********************************************
     C                   MOVE      WSTNRF        SDTNRF
     C                   MOVE      WSKDAÆ        SDKDAÆ
     C                   MOVE      WSKDSÆ        SDKDSÆ
     C                   MOVE      WSBLSÆ        SDBLSÆ
     C                   MOVE      SXDTF         SDDTF
     C                   MOVE      SXDTT         SDDTT
     C     WSKDAÆ        IFNE      'MV'
     C                   MOVE      'A'           SDAKTK
     C                   MOVEL     'AKTIV  '     SDST
     C                   ELSE
     C                   MOVE      'I'           SDAKTK
     C                   MOVEL     'INAKTIV'     SDST
     C                   END
     C                   MOVE      SDDTFD        WXDTFD
     C                   MOVE      SDDTFM        WXDTFM
     C                   MOVE      SDDTFÅ        WXDTFÅ
     C                   MOVE      SDDTTD        WXDTTD
     C                   MOVE      SDDTTM        WXDTTM
     C                   MOVE      SDDTTÅ        WXDTTÅ
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVSSW        BEGSR
     C***********************************************
     C                   MOVE      SDTNRF        WSTNRF
     C                   MOVE      SDKDAÆ        WSKDAÆ
     C                   MOVE      SDKDSÆ        WSKDSÆ
     C                   MOVE      SDBLSÆ        WSBLSÆ
     C                   MOVE      SDBLSÆ        WSBLSÆ
     C                   MOVE      SDDTFD        WSDTFD
     C                   MOVE      SDDTFM        WSDTFM
     C                   MOVE      SDDTFÅ        WSDTFÅ
     C                   MOVE      SDDTTD        WSDTTD
     C                   MOVE      SDDTTM        WSDTTM
     C                   MOVE      SDDTTÅ        WSDTTÅ
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     64            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADSK         KLIST
     C                   KFLD                    WSTNRF
     C                   KFLD                    WSKDAÆ
     C                   KFLD                    WSKDSÆ
     C                   KFLD                    SXDTF
     C     SADSK0        KLIST
     C                   KFLD                    WSTNRF
     C                   KFLD                    WSKDAÆ
     C                   KFLD                    WSKDSÆ
     C     SADSK1        KLIST
     C                   KFLD                    W2TNRF
     C                   KFLD                    W2KDAÆ
     C     SADSK2        KLIST
     C                   KFLD                    SDTNRF
     C                   KFLD                    SDKDAÆ
     C                   KFLD                    SDKDSÆ
     C                   KFLD                    SDDTF
     C     SADSK3        KLIST
     C                   KFLD                    YTNRF
     C                   KFLD                    YKDAÆ
     C                   KFLD                    YKDSÆ
     C                   KFLD                    YDTF
     C     TARIK         KLIST
     C                   KFLD                    SDTNRF
     C                   KFLD                    WSTAR
     C     TARIK1        KLIST
     C                   KFLD                    WSTNRF
     C                   KFLD                    WSTAR
     C     KODS4K        KLIST
     C                   KFLD                    WS4UNI
     C                   KFLD                    WSTRM
     C     KODS8K        KLIST
     C                   KFLD                    WSKDAÆ
     C                   KFLD                    WSKDSÆ
      *
     C                   MOVE      'S2 '         WS2UNI            3
     C                   MOVE      'S4 '         WS4UNI            3
     C                   MOVE      *ZEROS        W2TNRF
     C                   MOVE      *BLANKS       WSTAR             3
     C                   MOVE      *ZEROS        WSDTF
     C                   MOVE      *ZEROS        WSDTT
     C                   MOVE      *ZEROS        WXDTF
     C                   MOVE      *ZEROS        WXDTT
     C                   MOVE      *ZEROS        SDDTF
     C                   MOVE      *ZEROS        SDDTT
     C                   MOVE      *ZEROS        SXDTF
     C                   MOVE      *ZEROS        SXDTT
      *
     C                   ENDSR
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
      *
