      ***********************************************************************
      *  Finn oppdrags transportstatus ut fra T&T     (skilt ut fra SYTR10 )
      *    obs SETTER IKKE *LR - viktige variable må alltid initieres
      ***********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Hvis ennå ikke ankommet terminal - sjekk komme (skyting påbegynt)
12XXX * 02 JOV PTF12 Skrevet totalt om for ryddigere logikk  OBS entry TTAVD/OPD-->UUAVD/OPD
********************************************************************
     FTRACKL02  IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSYPARL1   IF   E           K DISK
N01  FDOKUFM    IF   E           K DISK
      *
     C                   exsr      INIT
     C                   exsr      TTstatus
      *
     C                   RETURN
      *
     C***********************************************
     C     TTstatus      BEGSR
     C***********************************************
      * returnert status som TEKST - overlat til kallende prog å sette evt indikatore mm
N02
N02   * Omskrvet totalt for å få ryddigere logikk
N02   * LEVERT ??
N02  C                   MOVE      'POD'         TTACTI
N02  c     TT02key       Chain     TRACKl02                           33
N02  c                   if        %found
N02  C                   eval      Status='Levert'
N02  c                   goto      EndTTstat
N02  c                   endif
N02  C                   if        TRSLAG='FF'                                   FF+TEI=Levert
N02  C                   MOVE      'TEI'         TTACTI
N02  c     TT02key       Chain     TRACKl02                           33
N02  c                   if        %found
N02  C                   eval      Status='Levert'
N02  c                   goto      EndTTstat
N02  c                   endif
N02  c                   endif
N02   *
N02   * TE2 med "signed by.." regnes som POD (sluttlevert , men til en terminal)
N02  C                   MOVE      'TE2'         TTACTI
N02  c     TT02key       setll     TRACKl02
N02  c     TT02key       reade     TRACKl02                               33
N02  c     *in33         doweq     '0'
N02  c                   if        %subst(TTTEXT:1:9)='Signed by'
N02  C                   eval      Status='Levert'
N02  c                   goto      EndTTstat
N02  c                   endif
N02  c     TT02key       reade     TRACKl02                               33
N02  c                   enddo
N02   *
N02   * er ikke levert...
N02   *************************
N02   * PÅ VEI UT fra terminal
N02   *************************
N02  C                   MOVE      'TEU'         TTACTI
N02  c     TT02key       Chain     TRACKl02                           33
N02  c                   if        %found
N02  C                   eval      Status='Utkjøres'
N02   * bare delvis(=manko) er det merket TTEDEV , via da avvikende tekst 'Utkjø(D)'
N02  c                   if        TTEDEV='DEL'
N02  C                   eval      Status='Utkjø(D)'
N02  c                   endif
N02  c                   goto      EndTTstat
N02  c                   endif
N02   *
N02   * På term ?
N02   *************************
N02   * PÅ TERMINAL
N02   *************************
N02  C                   MOVE      'TEI'         TTACTI
N02  c     TT02key       Chain     TRACKl02                           33
N02   * hvis VF så KAN TEI befinne seg kun på HO (om VF er skapt ETTER TEI-eventen)
N02  c                   if        not %found and
N02  C                             TRSLAG='VF'
N02  c     TT02keyMor    Chain     TRACKl02                           33
N02  c                   endif
N02  c                   if        %found
N02  C                   eval      Status='På Term '
N02  c                   goto      EndTTstat
N02  c                   endif
N02   * TE2 regnes også som på term (om en ikke har en høyere status/alt hoppet ut)
N02  C                   MOVE      'TE2'         TTACTI
N02  c     TT02key       Chain     TRACKl02                           33
N02  c                   if        %found
N02  C                   eval      Status='På Term '
N02  c                   goto      EndTTstat
N02  c                   endif
N02   *************************
N02   * PÅ TERMINAL Delvis??
N02   *************************
N02   * Hvis ennå ikke ankommet teermina TEI/TE2 Sekk om påbegynt skyting..
N02  C                   move      HEAVD         FMAVD
N02  C                   move      HEOPD         FMOPD
N02   * (Dokufm inneholder også varelinjer men FMITER=X finnes kun på kolliIDs)
N02  c     DokufmKey     setll     DOKUFM
N02  c     DokufmKey     reade     DOKUFM
N02  c                   dow       not %eof
N02  c                   if        FMITER = 'X'
N02  C                   eval      Status='På Te(D)'
N02  c                   goto      EndTTstat
N02  c                   leave
N02  c                   endif
N02  c     DokufmKey     reade     DOKUFM
N02  c                   enddo
N02   *
N02   *************************
N02   * HENTET ??
N02   *************************
N02  C                   MOVE      'COL'         TTACTI
N02  c     TT02key       Chain     TRACKl02
N02  c                   if        %found
N02  C                   eval      Status='Hentet'
N02  c                   goto      EndTTstat
N02  c                   endif
N02
N02   **************************
N02   * URØRT = 10 = Rød
N02   **************************
N02   * hvis den er kommet hit uten å hoppe ut er den URØRT T&Tmessig
N02  C                   eval      Status='Urørt'
N02   *
N02  c     EndTTstat     ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
E02  C                   PARM                    UUAVD             4 0
E02  C                   PARM                    UUOPD             7 0
     C                   PARM                    UUmanu            1
     C                   PARM                    Status           15
     C                   eval      Status=*blanks
N02  C                   move      UUAVD         TTAVD
N02  C                   move      UUOPD         TTOPD
      *
     C     HeaKey        KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C     TT02KEY       KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
N02  C     TT02keyMor    KLIST
N02  C                   KFLD                    TRAVD0
N02  C                   KFLD                    TROPD0
N02  C                   KFLD                    TTFBNR
N02  C                   KFLD                    TTACTI
      *
     C     SyParKey      KLIST
     C                   KFLD                    SYKUNR
     C                   KFLD                    SYPAID
      *
N01  C     DokufmKey     KLIST
N01  C                   KFLD                    FMRI
N01  C                   KFLD                    FMAVD
N01  C                   KFLD                    FMOPD
N01  C                   KFLD                    FMFBNR
N01  C                   KFLD                    FMLINR
N01  C                   move      'F'           FMRI
N01  C                   move      001           FMFBNR
      *
      * vis kun dersom det er parametersatt
     c                   if        UUmanu = ' '
     C                   move      'VISTT'       SYPAID
     C     SyParKey      CHAIN     SYPARL1
     c                   if        SYVRDA <> 'J' and SYVRDA = 'V'
     c                   return
     c                   endif
     c                   endif
      *
     C     HeaKey        CHAIN     HEADF
     c                   if        not %found
     c                   return
     c                   endif
N01  c                   move      HEAVD         FMAVD
N01  c                   move      HEOPD         FMOPD
      *
     c                   ENDSR
     C*
