      *
      *         SYSPED400 FASTE KODER - VEDLIKEHOLD/SPØRRING
      *
      *  Hvis en avbryter ved F3/F12 i "VEDL-EN-MODE" (UPDATE=N) settes
      *  param. UPDATE til 'X'. Programmet som har gjort kall bøR da
      *  IKKE ta imot data (blank) fra parameter UUKOD.
      *  Dette er spesielt viktig på koder hvor blank er gyldig verdi.
      *
     FSYFAKDFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     FKOFAST    UF A E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  KFTXT                  1     50
     D  SLETT                  1      8
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    KFKOD         UUKOD
     C     *LIKE         DEFINE    WSTYP         WWTYP
     C     *LIKE         DEFINE    WSKOD         WWKOD
     C     *ENTRY        PLIST
     C                   PARM                    WSTYP
     C                   PARM                    UPDATE            1
     C                   PARM                    UUKOD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEY           KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C     DEFKEY        KLIST
     C                   KFLD                    W2TYP
     C                   KFLD                    DEF
     C     *LIKE         DEFINE    KFKOD         DEF
     C                   MOVE      ' DEFN'       DEF
      *
     C* Endring av TYPE  TILlatt ??
     C     WSTYP         COMP      *BLANKS                            5151       *PROTECT
     C* Update tillatt ??   50 ON=KUN VEDLIKEHOLD KODER PÅ SAMME TYPE
     C     UPDATE        COMP      'J'                                5050
     C   50UPDATE        COMP      'N'                                    49
      *
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      WSTYP         WWTYP
     C                   MOVE      WSKOD         WWKOD
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   MOVE      *BLANKS       XXTYP
     C                   MOVE      *BLANKS       XXTXT
     C     WSTYP         COMP      *BLANKS                                56     *LES ALLE
     C     WSTYP         SETLL     KOFAST
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C  N56WSTYP         READE(N)  KOFAST                                 94
     C   56              READ(N)   KOFAST                                 94
     C     *IN94         DOWEQ     '0'
      *
     C     FILAND        IFNE      'NO'
     C                   MOVE      KFTXTE        KFTXT
     C                   END
      * Vis kun typer
     C     *IN07         IFEQ      '1'                                           B2
     C     KFKOD         IFEQ      ' DEFN'                                        B3
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END                                                      E3
     C                   ELSE                                                    X2
      * Vis kun koder
     C     KFKOD         IFEQ      ' DEFN'                                        B3
      * Hvis søk på bare EN type - vis beskrivelse i header
     C     WSTYP         IFNE      *BLANKS                                         B4
     C                   MOVE      KFTYP         XXTYP
     C                   MOVE      KFTXT         XXTXT
     C                   END                                                       E4
      * Hvis endring tillatt - vis også typer
     C*          UPDATE    IFEQ 'J'                           B4
     C     *IN51         IFEQ      '0'                                             B4
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   ELSE                                                      E4
     C                   ADD       1             ZRECNO                           X3
     C                   WRITE     SUBFIL
     C                   END                                                      E3
     C                   END                                                     E2
      *
     C  N56WSTYP         READE(N)  KOFAST                                 94
     C   56              READ(N)   KOFAST                                 94
     C  N94              END
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0473'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   51              Z-ADD     16            POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F7=VIS ALLE TYPER
      *"""""""""""""
     C     *IN07         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      CRTNEW
     C                   GOTO      START
     C                   END
      *_____________
      * Options på topplinja
      *"""""""""""""
     C     TOPOPT        IFNE      ' '
     C     TOPOPT        IFEQ      '1'
     C     UPDATE        IFNE      'E'
     C                   EXSR      CRTNEW
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       SLETT
     C                   MOVE      TOPOPT        WSNR
     C                   MOVE      WSTYP         KFTYP
     C                   MOVE      WSKOD         KFKOD
     C                   EXSR      OPTION
     C                   END
      * Søk utvalg til gammel verdi.. Vis subfila fra front igjen..
     C                   MOVE      WWTYP         WSTYP
     C                   MOVE      WWKOD         WSKOD
     C                   MOVE      ' '           TOPOPT
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSTYP         IFNE      WWTYP
     C     WSKOD         ORNE      WWKOD
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         TAG
      *'X' I UPDATE VED RETUR FRA SPØR/PLUKK BETYR AVBRUDD/IKKE PLUKKET
     C                   MOVE      'X'           UPDATE
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDNE     'E'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN     KOFAST                             55
     C  N55              DELETE    KOFASTR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   MOVE      *BLANKS       KFKOD
     C                   MOVEL     '*SLETTET'    KFTXT
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C     WSNR          IFEQ      '2'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN(N)  KOFAST                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      KFTYP         W2TYP
     C                   MOVE      KFKOD         W2KOD
     C                   MOVE      KFTXT         W2TXT
     C                   MOVE      KFTXTE        W2TXTE
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     22            POSNBR
     C     CHGFEI        TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFEQ      '1'
     C                   GOTO      CHGUT
     C                   END
     C                   EXSR      TESTB
     C   30              GOTO      CHGFEI
     C                   MOVE      W2TYP         KFTYP
     C                   MOVE      W2KOD         KFKOD
     C     KEY           CHAIN     KOFAST                             55
     C                   MOVE      W2TXT         KFTXT
     C                   MOVE      W2TXTE        KFTXTE
     C                   UPDATE    KOFASTR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C                   END
     C     CHGUT         TAG
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTNEW        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       W2TXT
     C                   MOVE      *BLANKS       W2TXTE
     C                   MOVE      WSTYP         W2TYP
     C     TOPOPT        IFEQ      '1'
     C                   MOVE      WSKOD         W2KOD
     C                   ELSE
     C                   MOVE      *BLANKS       W2KOD
     C                   END
     C                   Z-ADD     22            LINNBR
     C     W2TYP         IFEQ      *BLANKS
     C                   Z-ADD     5             POSNBR
     C                   ELSE
     C     W2KOD         IFEQ      *BLANKS
     C                   Z-ADD     16            POSNBR
     C                   ELSE
     C                   Z-ADD     22            POSNBR
     C                   END
     C                   END
     C     NEWFEI        TAG
     C                   EXFMT     NEWREC
     C     *IN12         IFEQ      '1'
     C                   GOTO      NEWUT
     C                   END
     C                   EXSR      TESTB
     C   30              GOTO      NEWFEI
     C                   MOVE      W2TYP         KFTYP
     C                   MOVE      W2KOD         KFKOD
     C     KEY           CHAIN     KOFAST                             55
     C     *IN55         IFEQ      '1'
     C                   MOVE      W2TXT         KFTXT
     C                   MOVE      W2TXTE        KFTXTE
     C                   WRITE     KOFASTR
     C     XRECNO        ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C     NEWUT         TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   SETOFF                                       30
      * Type kan ikke være blank
     C     W2TYP         IFEQ      *BLANKS
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0477'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
      * "Header"/definisjon (kode = " DEFN") må finnes før en kan
      * opprette koder på den typen
     C     W2KOD         IFNE      ' DEFN'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C     DEFKEY        CHAIN(N)  KOFAST                             30
     C     *IN30         IFEQ      '1'
     C                   MOVE      'JOV0476'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
     C                   END
      * Både Norsk og engelsk beskrivelse må gis
     C     W2TXT         IFEQ      *BLANKS
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0477'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
     C     W2TXTE        IFEQ      *BLANKS
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0477'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
     C     UTTEST        ENDSR
