     FCARGOO    IF   E           K DISK
     FCARGOO1   UF   E           K DISK
     F                                     RENAME(CARGOOR:CARGOO1R)
     FCARGOF1   UF   E           K DISK
     FHEADF     UF   E           K DISK
     FTRACKL02  IF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDC03   IF   E           K DISK
     FCARGOSUMD CF   E             WORKSTN USROPN
      *Array over adresser en vil sende mankomail til
     D MAIL            S             70    DIM(5)
     D                 DS
     D  XXMANKO                1      3
     D  XXMANKO1               1      1
     D  XXMANKO2               2      2
     D                 DS
     D  XXTOTAL                1      3
     D  XXTOTAL1               1      1
     D  XXTOTAL2               2      2
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  FMMRK1                 1     35
     D  FMCLID                 1     18
      *
      * Foreløpig behandling kun på oppdragsnivå (i det en setter på tur)
      * Parametermessig også klargjort for å justere hente-oppdragssendinger
     C     *ENTRY        PLIST
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UULNR             7 0
     C                   PARM                    UULNR2            3 0
     C                   PARM                    UFLAGGI           1
     C                   PARM                    UFLAGGU           1
      * Avviksprosent / faktor for å beregne min/max f.vekt
     C                   z-add     5             AvvikPros         3 1
      *
     C* Flagg INN
      * Blank = test / hvis NY og diff vis vindu...
     C* Flagg UT/retur
      * CSretur = 0 = IKKE cargoscan
      * CSretur = 1 = Cargoscan, men sjekket evt justert før...
      * CSretur = 2 = Cargoscan, alt ok
      * CSretur = 3 = Cargoscan, Diff / infobilde er vist
      *
     C     HeaKey        KLIST
     C                   KFLD                    UUAVD
     C                   KFLD                    UUOPD
     C     TrackKey      KLIST
     C                   KFLD                    UUAVD
     C                   KFLD                    UUOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   Move      'TEI'         TTACTI
      * INTERNE mankomailmottakere
     C     MAILKEYI      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YYCONTA
     C                   KFLD                    BHRKUNR           8 0
     C                   z-add     1             BHRKUNR
      * INTERNE mankomailmottakere
     C     MAILKEYE      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YYCONTA
     C                   KFLD                    TRKNFA
     C                   MOVEL     '*MANKO'      YYCONTA          30
     C     SndKey        KLIST
     C                   KFLD                    UULNR
     C                   KFLD                    UULNR2
      *
     C     *loval        setll     FIRM
     C                   read      firm
      *
      * Når en Kommer inn med BÅDE avd/opd og lnr/lnr2 utfylt, så er det
      * for å KOBLE en henteoppdrags-basert CARGOO til aktuelt oppdrag
      * lage T&T osv.
     C     UUOPD         IFNE      *zeros
     C     UULNR         ANDNE     *zeros
     C                   exsr      KobleHent
     C                   goto      UT
     C                   end
      *
     C                   OPEN      CARGOSUMD
      *
     C     HeaKey        Chain(N)  HEADF                              35
     C     *in33         IFEQ      '1'
     C                   move      '0'           UFLAGGU
     C                   goto      UT
     C                   end
      *
     C     HeaKey        Chain     CARGOO                             33
     C     *in33         IFEQ      '0'
     C                   move      '1'           UFLAGGU
     C                   goto      UT
     C                   end
      *
      * Finnes ikke fra før... sjekk om aktuelle kolliif ligger i cargo
     C                   move      *zeros        UULNR
     C                   move      *zeros        UULNR2
     C     TRKDFF        IFEQ      'S'
     C                   move      HEKNSF        uukund
     C                   else
     C                   move      HEKNKF        uukund
     C                   end
     C                   z-add     hent          uunt
     C                   z-add     HXPALL        uuntp
     C                   z-add     hevkt         uuvkt
     C                   z-add     hem3          uum3
     C                   z-add     hefbv         uufbv
     C                   z-add     trfrab        uufrab
     C                   z-add     AvvikPros     uumxavvik
     C                   move      *zeros        uugjavvik
     C                   call      'CARGOSUM2'
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UULNR             7 0
     C                   PARM                    UULNR2            3 0
     C                   PARM                    uukund            8 0
     C                   PARM                    uunt              7 0
     C                   PARM                    uuntp             3 0
     C                   PARM                    uuvkt             9 0
     C                   PARM                    uum3              7 3
     C                   PARM                    uufbv             9 0
     C                   PARM                    uufrab           13 2
     C                   PARM                    uumxavvik         6 1
     C                   PARM                    uugjavvik         6 1
     C                   PARM                    KOINDT            8 0
     C                   PARM                    KOINTI            6 0
     C                   PARM                    KOTERM           20
     C                   PARM                    UFLAGGI           1
     C                   PARM                    UFLAGGU           1
BHR   * UFLAGGU = 0 = IKKE cargoscan
BHR   * UFLAGGU = 1 = Cargoscan, men sjekket evt justert før...
BHR   * UFLAGGU = 2 = Cargoscan, alt ok (men kanskje justere paller)
BHR   * UFLAGGU = 3 = Cargoscan, Diff fbv mer enn xx% - infobilde SKAL VISES
      *
      * 0 = Ikke Cs - hopp ut
     C     UFLAGGU       cabeq     '0'           UT
      * 0 = Cs men ferdig sjekket før - hopp ut
     C     UFLAGGU       cabeq     '1'           UT
     C     HeaKey        Chain     CARGOO                             33
      *
      * parm er nå enten 2 eller 3 - cargoscan utført
      *
      *
      * OM T&T-hendelse INN-terminal ikke er logget, gjør det nå..
     C     TrackKey      Chain     TrackL02                           33
     C   33              exsr      TTSR
      *
      * OM ant kll i cargoscan er lavere enn innmeldt - mankomail
     C     COMNT         iflt      COGNTA
     C                   exsr      MankoMail
     C                   end
      *
      * antall paller oppdateres uavhengig av om det er f.vektavvik...
     C     COMNTP        IFNE      HXPALL
     C     HeaKey        Chain     HEADF                              35
     C     *in35         IFEQ      '0'
     C                   z-add     COMNTP        HXPALL
     C                   update    HEADFR
     c                   end
     c                   end
      * Ferdig me Cskontrollert OK (i alle fall ikke mer enn xx % avvik)
     C     UFLAGGU       cabeq     '2'           UT
      *
      * Står nå igjen med kode 3 - varsel-vindu / evt oppdater oppdrag.
      *
     C                   move      *blanks       SKUTXT
     C     COMFBV        ifgt      HEFBV
     C                   Eval      SKUTXT = '(ekstra skumming?)'
     C                   end
     c     COMNT         comp      HENT                               5050
     C                   EXFMT     WINDOW1
     C     *in12         ifeq      '1'
     C                   MOVE      '2'           UFLAGGU
     c                   goto      UT
     c                   end
      * flagg til sytr33 om at en vil vente alle kolli er ikke skutt...
     c     COMNT         IFNE      HENT
     C                   MOVE      '4'           UFLAGGU
     c                   goto      UT
     c                   end
      * oppdater nye verdier / fjern godkj. / åpne for ny maskinell beregnin
     C     HeaKey        Chain     HEADF                              35
     C     *in35         IFEQ      '0'
     C     COMVKT        ifne      *zeros
     C                   z-add(H)  COMVKT        HEVKT
     C                   END
     C     COMM3         ifne      *zeros
     C                   z-add(H)  COMM3         HEM3
     C                   END
     C                   z-add(H)  COMFBV        HEFBV
     C                   Move      *blank        HXBLGK
     C     TRFRAK        IFEQ      'B'
     C                   MOVE      'M'           TRFRAK
     C                   end
      * ved kode for agentpris-og endret vekt/volum - sett REBEREGN-kode
     c     TRTSTA        ifeq      'J'
     C                   MOVE      'R'           TRTSTA
     c                   end
     C                   update    HEADFR
     C                   end
      *
     C                   CLOSE     CARGOSUMD
      *
     C     UT            TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C                   MOVE      UUAVD         TTAVD
     C                   MOVE      UUOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'TEI'         TTACTI
     C                   EVAL      TTTEXT = 'Arrived on terminal ' + FIKRTN
     C                   CAT       KOTERM:1      TTTEXT
     C                   MOVEL     FIKRTN        TTDEPO
     C                   CAT       KOTERM:1      TTDEPO
     C                   MOVE      KOINDT        TTDATE
     C                   MOVEL     KOINTI        TTTIME
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVEL     'CARGOSCAN'   TTUSER
     C                   MOVEL     'CARGOSCAN'   TTNAME
     C                   WRITE     TRACKFR
      *
     C                   ENDSR
      *
      *
      *=====================================================================******
      * Send mail vedr manko
      *=====================================================================******
     C     MANKOMAIL     begsr
      * Interne bruker som skal ha mail (max 2)
     c                   clear                   mail
     c                   move      *zeros        mn                2 0
     C     MAILKEYI      SETLL     CUNDC03
     C     MAILKEYI      READE     CUNDC03                                35
     C                   DOW       *IN35 = *OFF
     c                   add       1             mn
     c                   movel     CEMAIL        mail(mn)
     c     mn            ifeq      1
     c                   movel     CPHONE        XXAVID           10
     c                   movel     CMOBIL        XXAVADR          10
     C                   MOVEL     CTYPE         XXSIGN           30
     c                   end
     c     mn            cabeq     2             extmail
     C     MAILKEYI      READE     CUNDC03                                35
     C  N35              ENDDO
      *
      * Eksterne brukere (avsender)som skal ha mail (max 5) samlet
     c     extmail       tag
      *
     C     MAILKEYE      SETLL     CUNDC03
     C     MAILKEYE      READE     CUNDC03                                35
     C                   DOW       *IN35 = *OFF
     c                   add       1             mn
     c                   movel     CEMAIL        mail(mn)
     c     mn            cabeq     5             utmail
     C     MAILKEYE      READE     CUNDC03                                35
     C  N35              ENDDO
     c     utmail        tag
      *
     C     MAIL(1)       IFNE      *BLANKS
     C     XXAVID        IFEQ      *BLANKS
     c                   movel     '*CURRENT'    XXAVID
     c                   move      *BLANKS       XXAVADR
     c                   END
      *
     C                   MOVEL     HENAA         XXAVS            30
     C                   MOVEL     HXSNDN        XXSNR            20
     C                   MOVEL     HENAK         XXNAV            30
     C                   MOVEL     HEADK1        XXADR            30
     C                   MOVEL     HEADK3        XXADR2           30
      * COGNTA= ant poster i DOKUFM,   COMNT= ant poster i CARGOF
     C     COGNTA        SUB       COMNT         XXMANK            3 0
     C                   MOVE      XXMANK        XXMANKO           3
     C     XXMANKO1      IFEQ      '0'
     C                   MOVE      ' '           XXMANKO1
     C     XXMANKO2      IFEQ      '0'
     C                   MOVE      ' '           XXMANKO2
     C                   END
     C                   END
     C                   Z-ADD     COGNTA        XXANT             3 0
     C                   MOVE      XXANT         XXTOTAL           3
     C     XXTOTAL1      IFEQ      '0'
     C                   MOVE      ' '           XXTOTAL1
     C     XXTOTAL2      IFEQ      '0'
     C                   MOVE      ' '           XXTOTAL2
     C                   END
     C                   END
     C                   MOVEL     FIFT          XXFIRM           30
     C                   CAT       KOTERM:1      XXFIRM
     C                   MOVEL     MAIL(1)       XXINT1           70
     C                   MOVEL     MAIL(2)       XXINT2           70
     C                   MOVEL     MAIL(3)       XXINT3           70
     C                   MOVEL     MAIL(4)       XXINT4           70
     C                   MOVEL     MAIL(5)       XXINT5           70
      * Send makomail (kopi av STS13CM  sycgi)
     C                   CALL      'CARGO13CM'
     C                   PARM                    XXAVS
     C                   PARM                    XXSNR
     C                   PARM                    XXNAV
     C                   PARM                    XXADR
     C                   PARM                    XXADR2
     C                   PARM                    XXMANKO
     C                   PARM                    XXTOTAL
     C                   PARM                    XXFIRM
     C                   PARM                    XXSIGN
     C                   PARM                    XXAVID
     C                   PARM                    XXAVADR
     C                   PARM                    XXINT1
     C                   PARM                    XXINT2
     C                   PARM                    XXINT3
     C                   PARM                    XXINT4
     C                   PARM                    XXINT5
     C                   END
      *
     C                   endsr
      *
     C***********************************************
     C     KobleHent     BEGSR
     C***********************************************
      *
      *
     C     SndKey        Chain     Cargoo1                            33
     C     *IN33         cabeq     '1'           UtKoble
      *
     C                   move      UUAVD         COAVD
     C                   move      UUOPD         COOPD
     C                   update    CARGOO1R
      *
     C     SndKey        setll     CargoF1
     C     SndKey        reade     CargoF1                                33
     C     *in33         doweq     '0'
     C                   move      UUAVD         KOAVD
     C                   move      UUOPD         KOOPD
     C                   update    CARGOFR
     C     SndKey        reade     CargoF1                                33
     C                   end
      * terminal / dato - kl som brukes i T&T er nå hentet fra CARGOF
      * OM T&T-hendelse INN-terminal ikke er logget, gjør det nå..
     C     TrackKey      Chain     TrackL02                           33
     C   33              exsr      TTSR
      * (Mankomail skal IKKE være nødvendig - KUN de som er KOMPLETTE har
      *  fått post i CARGOO1 allerede på henteoppd-stadium. HVIS ukomplett
      *  så blir de fanget av rutine for å sette på godsliste utgående...)
      *
     C     UtKoble       endsr
