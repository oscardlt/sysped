     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 60 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            WSANT          7  0O  5  5EDTCDE(4)
     A            WSLOK1         5A  O  5 13
     A            WSLOK2        20A  O  5 34
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA23(23 'BEKREFT')
     A                                      CA06(06 'LAG NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL067R '
     A                                      COLOR(BLU)
     A                                  1 11'Arbeid med splitting av innlegg'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0B  1 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Artikkel:'
     A            UANR          15A  O  2 12COLOR(BLU)
     A                                  2 28'Antall:'
     A            UANT           7  0O  2 36COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 44'Splitt:'
     A            XANT           7  0O  2 52COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2'Velg opsjon:  2=Endre  4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op  Antall Lokasjon'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A  91N93N94                            ROLLUP(25)
     A                                      CA23(23 'BEKREFT')
     A                                      CA06(06 'LAG NY')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F6=Lage ny  F12=Avbryt  F23=Bekref-
     A                                      t'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'LL067R '
     A                                      COLOR(BLU)
     A                                  1 11'Arbeid med splitting av innlegg'
     A                                      COLOR(WHT)
     A                                  2  2'Artikkel:'
     A            UANR          15A  O  2 12COLOR(BLU)
     A                                  2 28'Antall:'
     A            UANT           7  0O  2 36COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 44'Splitt:'
     A            XANT           7  0O  2 52COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2'Velg opsjon:  2=Endre  4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op  Antall Lager Lokasjon'
     A                                      COLOR(WHT)
     A N30                             15  2'F6=Lage ny  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BILDE
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(13)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2' Antall Lokasjon             Lager'
     A            W2ANT          7  0B  2  2DSPATR(UL)
     A                                      EDTCDE(4)
     A  90
     AON30                                  DSPATR(PC)
     A  90                                  COLOR(WHT)
     A            W2LOK2        20A  B  2 10DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  COLOR(WHT)
     A            W2LOK1         5A  B  2 31DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  COLOR(WHT)
     A N30                              3  2'F12=Forige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
