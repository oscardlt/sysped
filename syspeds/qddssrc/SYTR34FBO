     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R MANKOW
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CA12(12 'Avslutt')
     A                                      BLINK
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'Valg av Manko-håndtering         '
     A                                      COLOR(WHT)
     A                                  2  2'Hvordan skal manko på valgt(e) pos-
     A                                      t(er) håndteres? '
     A                                      COLOR(WHT)
     A N58                              3  2'Du bør ha oversikt over ant. mank/-
     A                                      tot før kjøring! '
     A                                      COLOR(RED)
     A N58                              4  2'ER DU SIKKER - trykk ENTER=Kjør, e-
     A                                      llers F12 + F2 ! '
     A                                      COLOR(RED)
     A                                  5  2'Valg:'
     A            WSMANK         1   B  5 12VALUES('1' '2')
     A                                  5 16'1 = Manko skilles ut på nytt send.-
     A                                      nr '
     A                                  6 16'    som blir stående åpen. Origina-
     A                                      l  '
     A                                  7 16'    reduseres tilsv. og merkes fer-
     A                                      dig'
     A                                  9 16'2 = Sendingene merkes ferdig/klar -
     A                                      for'
     A                                 10 16'    overf. til trafikk. TROSS MANK-
     A                                      O!!'
     A                                 11 16'    (med gjeldende ant/vekt beløp.-
     A                                      .) '
     A                                 12 12'Enter=Utfør valg'
     A                                      COLOR(BLU)
     A                                 12 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R MANKOW2
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CF11(11 'OVERFØR')
     A                                      CA12(12 'Avslutt')
     A                                      BLINK
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'MANGLENDE SKYTING INN PÅ TERMINAL'
     A                                      COLOR(WHT)
     A                                  3  2'Dette er EDI-mottatt henteoppdrag -
     A                                      hvor ennå ikke      '
     A                                      COLOR(WHT)
     A                                  4  2'alle sendinger er ferdig SKUTT INN-
     A                                       på terminal.       '
     A                                      COLOR(WHT)
     A                                  5  2'Uferdige send.nr -  rød farge i li-
     A                                      sta (F2=Se mank/tot)'
     A                                  6  2'Valg 9 i lista er manuell manko-hå-
     A                                      ndtering.      '
     A                                  8  2'Totalt ant sendinger .............-
     A                                      .......:'
     A            ANTSEN         3Y 0O  8 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9  2'Antall send. IKKE SKUTT/IKKE MANUE-
     A                                      LT OK..:'
     A            IKKEKV         3Y 0O  9 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12  2'F11=Overf. de send SOM ER OK    '
     A                                      COLOR(BLU)
     A                                 12 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R PALLW
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      CF11(11 'OVERFØR')
     A                                      CA12(12 'Avslutt')
     A                                      BLINK
     A                                  1  2'SYTR34B'
     A                                      COLOR(BLU)
     A                                  1 12'MANGLENDE PALLER PÅ UTGÅENDE OPPDR-
     A                                      AG ?'
     A                                      COLOR(WHT)
     A                                  3  2'Dette er EDI-mottatt henteoppdrag -
     A                                      hvor antall paller  '
     A                                      COLOR(WHT)
     A                                  4  2'INN fra kunde er større enn det so-
     A                                      m viderebelastes UT!'
     A                                      COLOR(WHT)
     A                                  5  2'Velg F12, og sjekk/endre pallant. -
     A                                      på den enkelte send.'
     A                                  6  2'Tast F11 KUN NÅR manuelle pallkvit-
     A                                      t. dekker opp diff. '
     A                                  8  2'Antall paller INN fra kunde/hente--
     A                                      transp.:'
     A            HOPLL2         3Y 0O  8 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9  2'Antall paller beslastet på UTgåred-
     A                                      e send.:'
     A            ANTPLL         3Y 0O  9 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12  2'F11=Overfør tross manglende PLL '
     A                                      COLOR(BLU)
     A                                 12 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
