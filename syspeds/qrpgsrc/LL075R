      ***********************************************************
      *** HENT PLUKKLISTE FRA LØSNING1                        ***
      ***********************************************************
      *
     FISTAPLU2  UF   E           K DISK
     FDORDL09   IF   E           K DISK    USROPN
     FLLHEAD04  IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FLL075D    CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D UAN             S              5    DIM(90)
     D UCMD            DS          1024
     D                 DS
     D XCMD01          C                   CONST('OVRDBF -
     D                                     FILE(DORDL09) -
     D                                     TOFILE(IBMDAT/-
     D                                     DORDL09)')
     D XCMD02          C                   CONST('OVRDBF -
     D                                     FILE(DORDL09) -
     D                                     TOFILE(ABDAT/-
     D                                     DORDL09)')
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  PLFIRM                 1      2
     D  PLAAR2                 3      4  0
     D  PLPAKN                 5     11  0
     D  PLORDR                 1     17
      *______________________
      * Initiering
      *""""""""""""""""""""""
     C                   EXSR      INIT
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
     C                   MOVE      ' '           WSNR
      *
     C     *LOVAL        SETLL     ISTAPLU2
     C                   READ(N)   ISTAPLU2                               94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   ISTAPLU2                               94
     C  N94              ENDDO                                                  <DO 14
      *
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
      * TOM SUBFIL....
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NOREC
     C                   ELSE                                                   <ZRECNO
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   ENDIF                                                  <ZRECNO
      *
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   MOVE      *BLANKS       UORDR
     C                   GOTO      ENDPGM
     C                   ENDIF
      *** F5=FORNY
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   ENDIF                                                  <*IN05
      *** Roll
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      START3
     C                   ENDIF                                                  <*IN25
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C     XSLT          IFEQ      'J'
     C     XVIS          OREQ      'J'
     C     *IN30         OREQ      '1'
     C                   GOTO      START3
     C                   END
     C                   END
     C   98              GOTO      START3
      *
     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   MOVE      'N'           XSLT              1
     C                   MOVE      'N'           XVIS              1
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '1'
     C                   LEAVE
     C                   END
      *
     C     WSNR          IFEQ      '1'
     C     PLTXT         ANDNE     '*DELETE'
     C                   MOVE      '1'           *IN99
     C                   EXSR      PLKL
     C   30              GOTO      SLOPT
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C     PLTXT         ANDNE     '*DELETE'
     C                   MOVE      '1'           *IN99
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C     ISTL2K        CHAIN     ISTAPLU2                           33
     C                   MOVE      'S'           PLSTAT
     C                   MOVE      WDT           PLDATO
     C                   MOVE      WTM           PLTID
     C                   UPDATE    ISTAPLUR
     C                   MOVE      ' '           WSNR
     C                   MOVE      *BLANKS       PLTXT
     C                   MOVEL     '*DELETE'     PLTXT
     C                   UPDATE    SUBFIL
     C                   MOVE      'J'           XSLT
     C                   END
      *
     C     WSNR          IFEQ      '5'
     C     PLTXT         ANDNE     '*DELETE'
     C                   MOVE      '1'           *IN99
     C                   EXSR      VSKL
     C                   MOVE      'J'           XVIS
     C                   END
      *
     C                   ENDDO
      *
     C     SLOPT         TAG
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     PLKL          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       UCMD
     C     PLFIRM        IFEQ      '01'
     C                   MOVEL     XCMD01        UCMD
     C                   ELSE
     C                   MOVEL     XCMD02        UCMD
     C                   END
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      DORDL09
      *
     C     DORD9K        SETLL     DORDL09
     C     DORD9K        READE     DORDL09                                90
     C                   MOVE      PLAAR         PLAAR2
     C     LHE04K        CHAIN     LLHEAD04                           33
     C     *IN33         DOWEQ     '0'
     C     LHSTA         IFNE      'D'
     C                   LEAVE
     C                   END
     C     LHE04K        READE     LLHEAD04                               33
     C  N33              ENDDO
     C     *IN33         IFEQ      '0'
     C                   MOVE      '1'           *IN89
     C                   MOVE      'YBC1843'     MSGNR
     C                   GOTO      SLPLU
     C                   END
      *
     C                   MOVE      PLAAR         UAAR              4
     C                   MOVE      PLPAKN        UPAKN             7
     C                   CALL      'LL076C'
     C                   PARM                    UGRP
     C                   PARM                    UPNR
     C                   PARM                    PLFIRM
     C                   PARM                    UAAR
     C                   PARM                    UPAKN
     C                   MOVE      PLORDR        UORDR
      *
     C                   CLOSE     DORDL09
      *
     C     SLPLU         ENDSR
      *
      ***********************************************
     C     VSKL          BEGSR
      ***********************************************
     C                   MOVE      PLAAR         UAAR              4
     C                   MOVE      PLPAKN        UPAKN             7
     C                   CALL      'LL075CA'
     C                   PARM                    PLFIRM
     C                   PARM                    UAAR
     C                   PARM                    UPAKN
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UGRP              8
     C                   PARM                    UPNR              8
     C                   PARM                    UORDR            17
      *
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XXPNR
     C     ISTL2K        KLIST
     C                   KFLD                    PLFIRM
     C                   KFLD                    PLAAR
     C                   KFLD                    PLPAKN
     C     DORD9K        KLIST
     C                   KFLD                    PLFIRM
     C                   KFLD                    OTBKOD
     C                   KFLD                    PLAAR
     C                   KFLD                    PLPAKN
     C     LHE04K        KLIST
     C                   KFLD                    XXGRP
     C                   KFLD                    XXPNR
     C                   KFLD                    PLORDR
      *
     C                   MOVE      UGRP          XXGRP             8 0
     C                   MOVE      UPNR          XXPNR
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      'O'           OTBKOD
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     KUNKEY        CHAIN     CUNDL01                            33
      *
     C                   ENDSR
      *
