     A*%%TS  SD  20160906  092107  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P06XXA* 01 YBC PTF6 FETESKRIFT PÅ FLYFRAKTBREV
P0601A* 02 JOV PTF6 F4 / FRIT.KODE OGSÅ FOR F.BREV
P8068A* 11 TH  PTF8 NY HELPFUNKSJON KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/KODTP)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA28PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AVDELINGERS F-
     A                                      ASTE DATA')
     A                                      ALTHELP
     A                                      HELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20160905  075408  SVEIN       REL-V7R1M0  5770-WDS
     A            KOPFOV1        9A  H
     A            KOPDRAW        7A  H
     A            KOPOUTB       11A  H
     A            KOPCOPI        6A  H
     A            KOPLPI         2S 0H
     A            W2NR           1A  I  6  2VALUES(' ' '2' '9')
     A            KOPLNR         3Y 0O  6  5EDTCDE(4)
     A                                      DSPATR(HI)
     A            KOPTY         10A  O  6 32
     A            UTPL           3Y 0O  6 43EDTCDE(4)
     A            UTPK           3Y 0O  6 47EDTCDE(4)
     A            KOPNVN        10A  O  6 51
     A            KOPHEA         1A  O  6 62
     A            KOPLAS    R        O  6 64REFFLD(KOPR/KOPLAS *LIBL/KODTP)
     A            KOPLP2         1Y 0O  6 66EDTCDE(4)
     A            KOPFM          5A  O  6 68
     A            KOPDR3         3A  O  6 74
     A            KOPCO2         2A  O  6 78
     A            UTTX22        22A  O  6  9
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160906  092107  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0015)
     A  *DS4                                SFLSIZ(0015)
     A  *DS3                                SFLPAG(0014)
     A  *DS4                                SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYFA280200 SYFA28PNL)
     A  *DS3                                HLPARA(01 002 23 079)
     A  *DS4                                HLPARA(01 002 23 079)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A            KOSFSI         3A  O  2 19COLOR(WHT)
     A            KOSFNV        16A  O  2 23COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU1251 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1  2'SYSPED/SYFA28U'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU1345 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 20MSGCON(020 SHU1685 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(045 SHU1687 *LIBL/SYMSGF)
     A                                      COLOR(RED)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'Forrige')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A*%%TS  SD  20110513  094105  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØKE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYFA280210 SYFA28PNL)
     A                                      HLPARA(*RCD)
N11  A*                                     HLPARA(21 045 21 079)
     A            MSGNR          7A  P
     A                                  1  2MSGCON(078 YBC0028 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KOPLNR    R    3Y00O  2  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            UTPTXT        30A  O  2 14DSPATR(UL)
     A            W2PTY         10A  B  2 45DSPATR(UL)
     A            W2PNVN        10A  B  2 57DSPATR(UL)
     A N90N70                               DSPATR(PC)
     A            W2PHEA         1A  B  2 68DSPATR(UL)
     A            W2PLAS         1A  B  2 70
     A  70                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A            W2LPI          1Y 0B  2 72EDTCDE(4)
     A            W2PFM          5A  B  2 74DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(014 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 37MSGCON(009 SHU1249 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2DRAW         3A  B  3 47
     A            W2DRAU         3A  B  3 51
     A            W2COPY         2A  B  3 63
     A                                  3 66'Ovl:'
     A                                      COLOR(WHT)
     A            W2FOV1         9A  B  3 71
     A N30                              4  2MSGCON(024 DIV0000 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        77A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 55'kopier:'
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(12 DIV1216 SYMSGF)
     A            WSPLNR         3Y 0I  2 15DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
