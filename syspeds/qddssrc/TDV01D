     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LOGF)
     A                                      PRINT
     A                                      ALTPAGEUP
     A                                      ALTPAGEDWN
     A          R TDV01B20
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      PUTOVR
     A            MSGNR          7A  P
     A                                  1  3'TDV01'
     A                                  1 29MSGCON(013 BBA0003 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 57'Dato:'
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 57'Tid :'
     A                                  2 63TIME
     A                                  5  5MSGCON(020 BBA0004 *LIBL/SYMSGF)
     A            LGID      R        O  5 28OVRDTA
     A                                      OVRATR
     A                                  6  5MSGCON(020 BBA0005 *LIBL/SYMSGF)
     A            LGIDNE    R        O  6 28OVRDTA
     A                                      OVRATR
     A                                  7  5MSGCON(020 BBA0006 *LIBL/SYMSGF)
     A            LGSID     R        O  7 28OVRDTA
     A                                      OVRATR
     A                                  8  5MSGCON(020 BBA0005 *LIBL/SYMSGF)
     A            LGSNET    R        O  8 28OVRDTA
     A                                      OVRATR
     A                                  9  5MSGCON(020 BBA0007 *LIBL/SYMSGF)
     A            LGMSID    R        O  9 28OVRDTA
     A                                      OVRATR
     A                                 11  5MSGCON(020 BBA0008 *LIBL/SYMSGF)
     A            LGRECN    R        O 11 28REFFLD(LGRECN)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 12  5MSGCON(020 BBA0009 *LIBL/SYMSGF)
     A            LGTRID    R        O 12 28OVRDTA
     A                                      OVRATR
     A                                 14  5MSGCON(020 BBA0010 *LIBL/SYMSGF)
     A            LGTR      R        O 14 28OVRDTA
     A                                      OVRATR
     A            SRTXT         10A  O 14 32DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 15  5MSGCON(020 BBA0011 *LIBL/SYMSGF)
     A            LGSTAT    R        O 15 28OVRDTA
     A                                      OVRATR
     A            STATXT        30A  O 15 32DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16  5MSGCON(020 BBA0012 *LIBL/SYMSGF)
     A            LGDATE    R        O 16 28EDTCDE(Y)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 17  5MSGCON(020 BBA0013 *LIBL/SYMSGF)
     A            LGTIME    R        O 17 28EDTCDE(Y)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  4MSGCON(008 BBA0014 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 65MSGCON(012 BBA0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                      OVRDTA
     A                                      OVRATR
     A          R TDV01B30
     A                                      CF03(03 'Exit')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  3'TDV01'
     A                                  1 25MSGCON(020 BBA0017 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 57'Dato:'
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 57'Tid :'
     A                                  2 63TIME
     A                                  4  1MSGCON(030 BBA0018 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  3MSGCON(040 BBA0019 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(075 BBA0020 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 10  3MSGCON(060 BBA0021 *LIBL/SYMSGF)
     A                                 23  4MSGCON(008 BBA0014 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R TDV01B40
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Subset')
     A                                      CA21(21 'Print list')
     A                                      SLNO(23)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  3MSGCON(008 BBA0014 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(012 BBA0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78   O  2  2MSGID(&MSGNR SYMSGF)
     A          R TDV01H50                  SFL
     A                                      SFLNXTCHG
     A            SFRRN         10S 0H
     A            RECNUM        10S 0H
     A            LGRECN         9S 0H
     A            SFMSID         3A  H
     A            WSNR           1A  B  8  3VALUES(' ' '1' '5' '7')
     A            W0211     R        O  8  5REFFLD(FWB00R/W0211 *LIBL/FWB00F)
     A                                      EDTCDE(4)
     A            W0213     R        O  8  9REFFLD(FWB00R/W0213 *LIBL/FWB00F)
     A                                      EDTCDE(4)
     A            HAWB           1   O  8 18
     A            W0221     R        O  8 20REFFLD(FWB00R/W0221 *LIBL/FWB00F)
     A            W0222     R        O  8 24REFFLD(FWB00R/W0222 *LIBL/FWB00F)
     A            W0233     R        O  8 28REFFLD(FWB00R/W0233 *LIBL/FWB00F)
     A                                      EDTCDE(4)
     A            W0235     R        O  8 33REFFLD(FWB00R/W0235 *LIBL/FWB00F)
     A                                      EDTCDE(4)
     A            W0234     R        O  8 43REFFLD(FWB00R/W0234 *LIBL/FWB00F)
     A            AVS           15A  O  8 46
     A            MOT           15A  O  8 63
     A          R TDV01K60                  SFLCTL(TDV01H50)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A                                      PUTOVR
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  68                                  SFLMSGID(SMA0032 *LIBL/SMAMSGF)
     A  33                                  SFLMSGID(BBA0029 *LIBL/SYMSGF)
     A  34                                  SFLMSGID(BBA0030 *LIBL/SYMSGF)
     A  35                                  SFLMSGID(BBA0031 *LIBL/SYMSGF)
     A                                  1  3'TDV01'
     A                                  1 25MSGCON(020 BBA0017 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 57'Dato:'
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 57'Tid :'
     A                                  2 63TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 BBA0018 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  4MSGCON(040 BBA0019 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(075 BBA0020 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
