********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BEHANDLING AV XML-FIL
LEM   * xml fra SKY begynner litt ukurant,Tatt inn fo å kunne teste en feilsituasj hos oss
10XXX * 02 YBC PTF10 SEND EPOST TIL SYSTEMA UKJENT FIL
10030 * 03 CB  PTF10 Hent IFS bane slik at navn kan valideres
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 04 JOV PTF11 Finn også XML utf-16
      * 05 JOV PTF11 feil i PTF 04
      * 06 JOV PTF11 Reelt kundeeksempel tatt inn i standard - (god mal)
      * 07 CB  PTF11 XML-filer fra Tullverket skal IKKE behandles som XML
      * 08 SH  PTF11 Mottak kostnadsfil fra Navitro
      * 09 SH  PTF11 Mottak fra NAVIRTRO fortolling
********************************************************************
      **************************************************************
      * IDENTIFISER FLATFIL "MELDINGSTYPER" - LAGE LOGGELEMENT
      **************************************************************
      * "PRIVATE" TYPER UTOVER REMITERING OG BBS-OCR MÅ IDENTIDIFISERES
      * FORTRINNSVIS VIA EN ELLER ANNEN UNIK STRENG I FØRSTE RECORD....
      * SØK ETTER "SYSTE" I POS. 1-5. AKTIVER/ENDRE STATEMENTENE
      * MERK MED KUNDESPESIFIK POS 1-5. I STEDET FOR "SYSTE"
      * OBS! MED DAGENS LOGIKK MÅ ALLE TYPER "GODKJENNES" OGSÅ I EDI85R
      *     (MEN KANSKJE BURDE DEN UANSETT DA SLIPPES IGJENNOM OG SÅ
      *      LOGGES SOM UKJENT HER???????)
      **************************************************************
     FEDIRE1K   IF   E           K DISK
     FEDIS      O  A E             DISK
N03  FEDISX     IF   E           K DISK
     D                 DS
     D  RDATA                  1   1024
N08  D  NAVITRO                1      7
     D  RTYP1                  1      1
     D  RTYP2                  1      2
     D  AH100                  1      5
     D  RTYREM                41     48
     D  IFATYP                 1      4
     D  IFAFRA                 6     40
     D  IFATIL                41     75
     D  TDS1                  10     11
     D  TDS2                  12     12
N09  D  FORT                   1     11
     D                 DS
     D  WMBR                   1     10
     D  WMBRA                  1      1
     D  WMBRB                  2     10S 0
001  D TSTXML          C                   CONST('<?xml version=')
N04  D TSTXML16        C                   CONST('<?xml version=')
LEM  D TSTSKY          C                   CONST('<SkynetExchange>')
N07  D TSTTULL         C                   CONST('arcticgroup.se')
     C     *ENTRY        PLIST
     C                   PARM                    WTYP              3
     C                   PARM                    WFRREM
     C                   PARM                    WFROCR
     C                   PARM                    S0010
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR

     C     *LIKE         DEFINE    S0004         WFRREM
     C     *LIKE         DEFINE    S0004         WFROCR
N03
N03   * Hent IFS katalog og filnavn
N03   * Har nå variabel: SXIFSOBJ som inneholder katalogbane + filnavn
N03   * hvis en vill teste på filnavn isteden for innehold.
N03  C     SMBR          CHAIN     EDISX
      * HVIS NIT / RJE : FØRSTE RECORD=3 (VED ORIGINAL)
      * DA ER FØRSTE KARAKTER 'ø' I LINJE 1 (LINJE 2 ER BLANK)
      * VED RETRANSMITT FÅR VI IKKE INN "RUSK I LINJE" 1 OG 2
     C     SMBR          SETLL     EDIRE1K
     C     SMBR          READE     EDIRE1K                                21
     C   21              GOTO      SLUTT
      *
     C     RTYP1         IFEQ      'ø'
     C     1             DO        2
     C     SMBR          READE     EDIRE1K                                21
     C   21              GOTO      SLUTT
     C                   END
     C                   END
SYSTEC* PRIVATE MELDINGSTYPER LEGGES INN HER.........
SYSTEC* DISSE BLIR SOMREGEL UMIDDELBART BEARBEIDET AV ET PROG. SST->"C"
SYSTEC*          XXXTYP    IFEQ 'YYYYY'
SYSTEC*                    MOVE 'XXX'     WTYP
SYSTEC*                    MOVEL'ZZZZZZZZ'S0004            ID-AVSENDER
SYSTEC*                    MOVE 'C'       SST              Status sendi
SYSTEC*                    GOTO OK
SYSTEC*                    ENDIF
N01  C     TSTXML        SCAN      RDATA                                  53
S04  C*    TSTXML16      SCAN      RDATA                                  53
N05  C  N53TSTXML16      SCAN      RDATA                                  53
LEM  C  N53TSTSKY        SCAN      RDATA                                  53
N01  c     *in53         ifeq      *on
N01  C                   MOVE      'XML'         WTYP
N07  C     SMBR          READE     EDIRE1K                                21
N07  C   21              GOTO      SLUTT
N07  C     TSTTULL       SCAN      RDATA                                  71
N07  C   71              MOVE      'XM1'         WTYP
N01  C                   GOTO      SLUTT
N01  C                   ENDIF
      *
      * mottak CSV NAVITRO
      *
N09  C     FORT          IFEQ      'Export toll'
N09  C     FORT          OREQ      'Eksport_tol'
N09  C                   MOVE      'EXP'         WTYP
      * generell export inn
N09  C                   MOVEL     'EXPTOL'      S0004                                 ID-AVSENDER
N09  C                   MOVEL     'NO011701'    S0010
N09  C                   MOVE      'C'           SST                                   Status sendi
N09  C                   GOTO      OK
N09  C                   ENDIF
      * import fortolling generell csv inn
N09  C     FORT          IFEQ      'Import_toll'
N09  C                   MOVE      'IMP'         WTYP
N09  C                   MOVEL     'IMPTOL'      S0004                                 ID-AVSENDER
N09  C                   MOVEL     'NO011701'    S0010
N09  C                   MOVE      'C'           SST                                   Status sendi
N09  C                   GOTO      OK
N09  C                   ENDIF
     C     AH100         IFEQ      'AAANO'
     C                   MOVE      'CAS'         WTYP
     C                   MOVEL     'CASS'        S0004
     C                   MOVE      'C'           SST
     C                   GOTO      OK
     C                   ENDIF
     C     IFATYP        IFEQ      '*IN#'
     C                   MOVE      'IFA'         WTYP                           *BBS INNBET.
     C                   MOVE      IFAFRA        S0004
     C                   MOVE      IFATIL        S0010
     C                   MOVE      'C'           SST                            Status sendi
     C                   GOTO      OK
     C                   END
     C     TDS1          IFEQ      '_K'
     C     TDS2          ANDGE     'A'
     C     TDS2          ANDLE     'T'
     C     TDS1          OREQ      '_D'
     C     TDS2          ANDGE     'A'
     C     TDS2          ANDLE     'T'
     C                   MOVE      'TDS'         WTYP                           TDS TARIC
     C                   MOVE      'TARIC'       S0004
     C                   MOVE      'SYSPED'      S0010
     C                   MOVE      'C'           SST                            Status sendi
     C                   GOTO      OK
     C                   END
      * BBS/OCR-INNBET STARTER ALLTID MED RECORD MED 'NY' I POS 2-3
     C     RTYP2         IFEQ      'NY'
     C                   MOVE      'OCR'         WTYP                           *BBS INNBET.
     C                   MOVE      WFROCR        S0004
     C                   MOVE      'A'           SST                            Status sendi
     C                   GOTO      OK
     C                   END
N08   * NAVITRO kostnadsfil
N08  C     NAVITRO       IFEQ      'Navitro'
N08  C                   MOVE      'NAV'         WTYP                           *BBS INNBET.
N08  C                   MOVEL     'NAVITRO'     S0004
N08  C                   MOVE      'A'           SST                            Status sendi
N08  C                   GOTO      OK
N08  C                   END
      * REMITERING TYPE "BETFOR"
     C     RTYREM        IFEQ      'BETFOR00'
     C                   MOVE      'REM'         WTYP                           *REMIT.DATA
     C     AH100         IFEQ      'AH102'
     C     AH100         OREQ      'AH202'
     C                   MOVE      'A'           SST                            Status sendi
     C                   ELSE
     C                   MOVE      'C'           SST
     C                   END
     C                   MOVE      WFRREM        S0004
     C                   GOTO      OK
     C                   ENDIF
      * AVREGNING TYPE AH100
     C     AH100         IFEQ      'AH100'
     C     AH100         OREQ      'AH102'
     C                   MOVE      'AVR'         WTYP                           *REMIT.DATA
     C                   MOVE      'A'           SST                            Status sendi
     C                   MOVEL     'BANKAVST'    S0004
     C                   GOTO      OK
     C                   ENDIF
N03   * Exempellinje, bytt ut YYY.XXX med aktuell verdi
N03  C     'YYY.XXX'     SCAN      SXIFSOBJ                               22
N03  C                   IF        *IN22
N03  C                   EVAL      WTYP='XXX'
N03  C                   MOVEL     'ZZZZZZZZ'    S0004
N03  C                   EVAL      SST='C'
N03  C                   GOTO      OK
N03  C                   ENDIF
N06   * agent CTS-Csvfil
N06  C                   IF        %subst(RDATA:1:8)='Manifest'
N06  C                   EVAL      WTYP='CTS'
N06  C                   MOVEL     'CTS     '    S0004
N06  C                   MOVEL     'ACCELERATOR' S0010
N06  C                   EVAL      SST='C'
N06  C                   GOTO      OK
N06  C                   ENDIF
      * UKJENT TYPE FLATFIL......
     C                   MOVE      'UKJ'         WTYP                           *UKJENT
     C                   MOVEL     'UKJENT'      S0004
     C                   MOVE      'C'           SST                            Status sendi
N02  C                   CALL      'SYGE15C'
N02  C                   PARM                    WDT               8
N02  C                   PARM                    WTM               6
N02  C                   MOVE      WDT           XDT               8 0
N02  C                   MOVE      WTM           XTM               6 0
N02  C                   EVAL      XMSG = 'Ukjent fil. '
N02  C                             + 'PROGRAM = EDIOCCR1K, '
N02  C                             + 'FILNAVN = EDIRE1K, '
N02  C                             + 'KOMPONENT = ' + %TRIM(SMBR) + ', '
N02  C                             + 'DATO = ' + %TRIM(%EDITW(XDT:'    .  .  '))
N02  C                             + ', '
N02  C                             + 'TID = ' + %TRIM(%EDITW(XTM:'  :  :  '))
N02  C                             + '. '
N02  C                   CALL      'EDIERRC'
N02  C                   PARM                    XMSG            256
     C     OK            TAG
     C* LAG LOGG ELEMENT I SENDINGSLOGG
     C                   EXSR      LOGGS

     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
      *---------------------------------------
     C     LOGGS         BEGSR
      *"""""""""""""""""""""""""""""""""""""""
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
     C                   MOVE      ' '           S0035                          Testindikat.
     C                   MOVE      'R'           SSR                            Send/rec.sen
     C                   MOVEL     WDATO         SDT                            Dato inn sen
     C                   MOVEL     WTID          STM                            Tid inn  sen
      * Hent sendningsnummer fra pos 2-8 i komponentnavn
     C                   EVAL      WMBR=SMBR
     C                   EVAL      SSN=WMBRB
      * Som sendingsref legges 14 første tegn av første record
     C                   MOVEL     RDATA         S0020                          SENDINGSref.DI-ID
     C                   IF        WTYP = 'XMP'
     C                   MOVEL     'XML'         S0026                          APPL.REF    DI-ID
     C                   ELSE
     C                   MOVEL     WTYP          S0026                          APPL.REF    DI-ID
     C                   END
      * Som antall legges antall linjer i fila (Obs! en rec. alt lest)
     C     *IN21         DOWEQ     '0'
     C                   ADD       1             ANTALL            5 0
     C     SMBR          READE     EDIRE1K                                21
     C                   END
     C                   Z-ADD     ANTALL        S0036                          ANT. LINJER
      * Skriv til sendingslogg
     C                   WRITE     EDISR
     C                   ENDSR
