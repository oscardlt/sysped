      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSTURT2 MODULE(*LIBL/STSTURT2 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P10036* 01 JOV PTF10 HOPP UT HVIS UGYLDIG USER
10172 * 02 SH  PTF10 norsk text til Track and trace
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FHeadf     if   e           k disk    usropn
     FTRACKL02  UF A E           K DISK    usropn
     FBRPARF    IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D avd             s              4
     D avdN            s              4  0
     D opd             s              7
     D opdN            s              7  0
     D DAT             s              8
     D DATN            s              8  0
     D KL              s              4
     D KLN             s              4  0
     D UserId          s             10
     D User            s             10
     D TERM            s              5
     D HENTOK          s              9
     D HENTOKN         s              9  0
     D xxDATL          s              8  0
     D xxTIML          s              6  0
     D Merknad         s             70
     D FEILKOD         s              1
     D UPDAT           s              1
     D AVSLUTT         s              1
     D Allerede        s            500
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D PAVRDA                  1     50
     D  PAVRDA1_5              1      5
     D  PAVRDA7_46             7     46
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      AVD    = zhbgetvar('AVD')
     C                   eval      AvdN   = c2n2(AVD)
     C                   eval      OPD    = zhbgetvar('OPD')
     C                   eval      OpdN   = c2n2(OPD)
     C                   eval      DAT      = zhbgetvar('DAT')
     C                   eval      DATN     = c2n2(DAT)
     C                   eval      KL       = zhbgetvar('KL')
     C                   eval      KLN      = c2n2(KL)
     C                   eval      HENTOK   = zhbgetvar('HENTOK')
     C                   eval      HENTOKN  = c2n2(HENTOK)
     C                   eval      MERKNAD  = zhbgetvar('MERKNAD')
     C                   eval      TERM   = zhbgetvar('TERM')
     c     Term          ifeq      *blanks
     C                   eval      TERM   = 'IN'
     c                   end
     C                   eval      userid = zhbgetvar('user')
     C                   eval      UPDAT  = zhbgetvar('UPDAT')
      * Set libl/open files
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis kolli-id
     C                   exsr      MAIN
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     headf
     C                   close     TRACKL02
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *********************************************************************
     C     MAIN          begsr
      *********************************************************************
      *
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('TERM':TERM)
     C                   callp     updhtmlvar('TERMNAVN':TERMNAVN)
      *
      *
     c     HeaKey        chain     Headf
     c                   if        %subst(HXSNDN:1:3)='401'
     c                             and %subst(HXSNDN:20:1)<>' '
     c                   eval      Hxsndn=%subst(HXSNDN:4:17)
     c                   endif
     C                   callp     updhtmlvar('SNDN':HXSNDN)
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(AVDN:'3')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(OPDN:'3')))
     C                   callp     updhtmlvar('AVS':HENAS)
     C                   callp     updhtmlvar('MOT':HENAK)
     C                   callp     updhtmlvar('MOTADR':HEADK1)
     C                   callp     updhtmlvar('MOTPNR':HEADK3)
     C                   callp     updHTMLvar('HEVKT':
     c                             %trim(%editc(HEVKT:'Z')))
     c     UPDAT         ifeq      'Y'
     c                   exsr      UPDATSR
     c                   else
      * på de som ikke er manuelt merket forutsettes at antall OK er lik Antall
     c                   eval      HENTOKN = HENT
     c                   eval      MERKNAD=*blanks
     c                   exsr      tstAllerede
     c                   endif
     C                   callp     updHTMLvar('HENT':
     c                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('HENTOK':
     c                             %trim(%editc(HENTOKN:'Z')))
     C                   callp     updHTMLvar('DATN':
     c                             %trim(%editc(DATN:'Z')))
     C                   callp     updHTMLvar('KLN':
     c                             %trim(%editc(KLN:'Z')))
     C                   callp     updHTMLvar('DAT':
     c                             %trim(%editw(DATN:'    .  .  ')))
     C                   callp     updHTMLvar('KL':
     c                             %trim(%editw(KLN:'  :  ')))
     C                   callp     updHTMLvar('MERKNAD':MERKNAD)
     C                   callp     updHTMLvar('Allerede':Allerede)
     C                   callp     updHTMLvar('AVSLUTT':AVSLUTT)
      *
     C                   callp     wrtsection('top')
     C                   endsr
     C***********************************************
     C     tstAllerede   BEGSR
     C***********************************************
     c                   eval      xxDATL = *zeros
     c                   eval      xxTIML = *zeros
     c                   eval      Allerede = *blanks
     c                   move      *blank        MerkTXT          70
      * her henter en opp evt eksisterende TE2 fra T&T...
     c                   move      'TE2'         TTACTI
     c     FinnTT        tag
     c     TTKEY         setgt     TRACKL02
     c     TTKEY         readpe(N) TRACKL02                               33
     c     *in33         doweq     '0'
     c     TERM          ifeq      TTDEPO
     c                   eval      xxDATL = TTDATL
     c                   eval      xxTIML = TTTIML
     c                   eval      Allerede ='OBS!! Hendelse '+TTACTI+' finnes '
     c                             +'allerede: ' +%trim(TTTEXT)+'<br/>'
     c                             +'Registrert av '+TTUSER+' '
     c                             +%trim(%editw(TTDATE:'    .  .  '))+' '
     c                             +%trim(%editw(TTTIME:'  :  :  '))+'<br/>'
     c                   goto      UtAllerede                                   eldste/dette depot
     c                   end
     c     TTKEY         readpe(N) TRACKL02                               33
     c                   enddo
      * prøv med T2S
     c     TTACTI        ifeq      'TE2'
     c                   move      'T2S'         TTACTI
     c                   goto      finnTT
     c                   endif
      *
     C     UtAllerede    tag
      * Er avvik/merknad registrert
     c     Allerede      ifne      *blanks
     c                   move      'AVV'         TTACTI
     c     TTKEY         setgt     TRACKL02
     c     TTKEY         readpe(N) TRACKL02                               33
     c     *in33         doweq     '0'
     c     TERM          ifeq      TTDEPO
     c     xxDATL        ifeq      TTDATL
     c     xxTIML        andeq     TTTIML
     c                   eval      MERKTXT = TTTEXT
     c                   leave
     c                   endif
     c                   endif
     c     TTKEY         readpe(N) TRACKL02                               33
     c                   enddo
      *
     c     MerkTxt       ifne      *blanks
     c                   eval      Allerede =%trim(Allerede)
     c                             +'Merknad: '+%trim(Merktxt)+'<br/>'
     c                   endif
     c                   eval      Allerede =%trim(Allerede)
     c                             +'Trykk Close for å avbryte eller OK for å '
     c                             +'overskrive med ny hendelse.'
     c                   endif
     c                   ENDSR
      *
     C***********************************************
     C     UPDATSR       BEGSR
     C***********************************************
      * evt gammel skal slettes
     c                   eval      xxDATL = *zeros
     c                   eval      xxTIML = *zeros
      * henter opp evt eksisterende TE2  fra T&T...
     c                   move      'TE2'         TTACTI
     c     FinnTTU       tag
     c     TTKEY         setgt     TRACKL02
     c     TTKEY         readpe    TRACKL02                               33
     c     *in33         doweq     '0'
     c     TERM          ifeq      TTDEPO
     c                   eval      xxDATL = TTDATL
     c                   eval      xxTIML = TTTIML
     C                   delete    TRACKFR
     c                   goto      UtAlleredeU                                  eldste/dette depot
     c                   end
     C                   update    TRACKFR
     c     TTKEY         readpe    TRACKL02                               33
     c                   enddo
      * prøv med T2S = SHORT
     c     TTACTI        ifeq      'TE2'
     c                   move      'T2S'         TTACTI
     c                   goto      finnTTU
     c                   endif
      *
     C     UtAlleredeU   tag
      * Er avvik/merknad registrert
     c     xxDATL        ifne      *zeros
     c                   move      'AVV'         TTACTI
     c     TTKEY         setgt     TRACKL02
     c     TTKEY         readpe    TRACKL02                               33
     c     *in33         doweq     '0'
     c     TERM          ifeq      TTDEPO
     c     xxDATL        andeq     TTDATL
     c     xxTIML        andeq     TTTIML
     C                   delete    TRACKFR
     c                   leave
     c                   endif
     C                   update    TRACKFR
     c     TTKEY         readpe    TRACKL02                               33
     c                   enddo
     c                   endif
      *
      * LOGGFØR HENDELSE
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVE      DATN          TTDATE
     C                   MOVE      *zeros        TTTIME
     C                   MOVEL     KLN           TTTIME
     c                   move      *blanks       ExtraTxt         30
N02  c                   move      *blanks       ExtraTxtl        30
     c                   eval      ExtraTxt= ' Checked: '
     c                             + %trim(%editc(HENTOKN:'3'))
     c                             + '/' + %trim(%editc(HENT:'3'))
N02  c                   eval      ExtraTxtl= ' Sjekket: '
N02  c                             + %trim(%editc(HENTOKN:'3'))
N02  c                             + '/' + %trim(%editc(HENT:'3'))
     c     HENTOKN       ifgt      *zeros
     C                   MOVE      'TE2'         TTACTI
     C                   EVAL      TTTEXT = 'Scanned '
     c                             + %trim(TermNavn) + ExtraTxt
N02  C                   EVAL      TTTEXL = 'Skannet '
N02  c                             + %trim(TermNavn) + ExtraTxtl
     c                   else
     C                   MOVE      'T2S'         TTACTI
     C                   EVAL      TTTEXT = 'SHORT!! '
     c                             + %trim(TermNavn) + ExtraTxt
N02  C                   EVAL      TTTEXL = 'MANKO!! '
N02  c                             + %trim(TermNavn) + ExtraTxtL
     c                   end
     C                   MOVEL     TERM          TTDEPO
     C                   MOVEL     USERID        TTUSER
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
      * avvik-tekst
     c     MERKNAD       ifne      *blanks
     c                   move      ' '           TTMANU
     C                   MOVE      'AVV'         TTACTI
     C                   EVAL      TTTEXT = MERKNAD
     C                   WRITE     TRACKFR
     C                   endif
      *
     c                   eval      AVSLUTT='Y'
      *
     C                   ENDSR
      *
      *=====================================================================******
     C     INIT          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              50
N01  c     *in50         ifeq      '1'
N01  C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
N01  c                             '/CGITAG/')
N01  C                   callp     updhtmlvar('User':UserID)
N01  C                   callp     wrtsection('all')
N01  C                   callp     wrtsection('*fini')
N01  C                   close     BRIDF
N01  C                   eval      *inlr = *on
N01  C                   return
N01  c                   endif
N01   *
      *
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      headf
     C                   open      TRACKL02
     C                   open      BRPARF
      *
      *  keys mm
     C     HEAKEY        KLIST
     C                   KFLD                    avdN
     C                   KFLD                    opdN
     C     TTKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C     BrParKey      KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      USERID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVEL     'TERMK'       PAID
s01   * Ved fremmed terminal - finn terminaldefinisjon, enten på usernivå eller på firmanivå
     c                   move      *blanks       Termnavn         40
      *
     c     BrParKey      setll     BRPARF
     c     BrParKey      reade     BRPARF                                 33
     c     *in33         doweq     '0'
     c     Pavrda1_5     ifeq      TERM
     c                   eval      TermNavn= Pavrda7_46
     c                   leave
     c                   end
     c     BrParKey      reade     BRPARF                                 33
     c                   end
      *
     c     TermNavn      ifeq      *blanks
     c                   eval      TermNavn= TERM
     c                   end
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSTURT2'
      *------------------
     C                   endsr
