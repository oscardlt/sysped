      * sad eksport inn fra CSV fil
      *
     FTARI      IF   E           K DISK
     FEDIRE1K   IF   E           K DISK
     FZTELL     UF   E           K DISK
     FSADL      IF   E           K DISK
     FZAEF      O    E             DISK
     FZAEV      O    E             DISK
     FKODTVAL2  IF   E           K DISK
     D                 DS
     D  RDATA                  1   1024
     D  RTYP1                  1      6
     D  RTYP2                 13     22
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
     D  AA                     1   1024
     D                                     DIM(1024)
     D  AADS                   1   1024
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  A                      1     79
     D                                     DIM(79)
     D                 DS
     D  B                      1     18
     D                                     DIM(18)
     D                 DS
     D  WTARIFF                1     10
     D  WTAR08                 1      8
     D  WTAR06                 1      6
     C                   EXSR      SRINIT
     C     UMBR          SETLL     EDIRE1K
     C     UMBR          READE     EDIRE1K                                91
     C     *IN91         DOWEQ     *OFF

     C     'Export'      SCAN      RDATA                                  33
     C  N33'Ekspor'      SCAN      RDATA                                  33
     C                   SELECT
     C                   WHEN      *IN33
     C                   EXSR      SRFAKT
     C                   OTHER
     C                   EXSR      SRVAR
     C                   ENDSL

     C     UMBR          READE     EDIRE1K                                91
     C                   ENDDO


     C     LR            TAG
     C                   MOVE      '1'           *INLR
      *****************************************************************
     C     SRINIT        BEGSR
      *****************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    UMBR             10
      * Hente dagens dato og tid
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C     KODVAK        KLIST
     C                   KFLD                    SFVK28
     C                   KFLD                    WKVADT
      * Definisjon av arbetsfelt
     C     *LIKE         DEFINE    SVLI          WVLI
     C     *LIKE         DEFINE    KVADT         WKVADT
     C                   MOVEL     WDT           WKVADT
      * Hent unikt nummer for Z-filer
     C     '01'          CHAIN     ZTELL
     C                   ADD       1             ZUNIK
     C                   UPDATE    ZTELLR
      *
     C                   ENDSR
      *********************************************************************
     C     SRFAKT        BEGSR
      *********************************************************************
      * Header opplysninger
      * Init
     C                   EVAL      SFUNIK = ZUNIK
     C                   MOVEL     RDATA         AADS
     C                   Z-ADD     1             WX                4 0
      * 1. Fast verdi export tollklarering
      *
     C                   EXSR      GETDED
      *
      * 2.  kundenummer
      *
     C                   EXSR      GETDED
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SLKNR
      *
      *
      * 3. Fakturanr
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SFREFF
     C                   MOVEL     FELT          SFTXT
      * 4. Eksportdate
     C                   EXSR      GETDED
     C                   MOVEL     FELT          sfdt
     c*                  movel     '15'          dato08            8
     C*                  MOVE      dato06        dato08
     C*                  MOVE      dato08        SFDT
      * 5. Valutakode
     C                   EXSR      GETDED
     C                   EVAL      SFVK28 = FELT
     C     KODVAK        SETGT     KODTVAL2
     C     SFVK28        READPE    KODTVAL2                               33
     C                   IF        *IN33 = *OFF
     C                   EVAL      SFKR28 = KVAKRS
     C                   EVAL      SFOM28 = KVAOMR
      *
      * 6. fakturabeløp
     C                   EXSR      GETDED
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SFBL28
      *
      * 7. Bruttovekt
     C                   EXSR      GETDED
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          Bruttov          13 2
      *
     C                   ENDIF
      *
     C                   WRITE     SADFR
      *
     C                   ENDSR
      *
      ***************************************************************
     C     SRVAR         BEGSR
      ***************************************************************
     C                   CLEAR                   SAEVR
     C                   ADD       1             WVLI
     C                   MOVE      WVLI          SVLI
     C                   EVAL      SVUNIK = SFUNIK
     C                   EVAL      SVREFF = SFREFF
      * Init
     C                   MOVEL     RDATA         AADS
     C                   Z-ADD     1             WX                4 0
      * 1. Fylkeskode
      *
      * FYLKE
      *
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SVFYL
      * 2. Artikkelnummer
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SLALFA
     C                   MOVEL     FELT          SVFT
      * 3. Beskrivelse
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SLTXT
     C                   MOVEL     FELT          SVVT
      * 4. Tolltariffnummer
     C                   EXSR      GETDED
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          TATANR
     C     tatanr        CHAIN     TARI                               89
     c     *in89         ifeq      '0'
     C                   MOVE      tatanr        SVVNT
     C                   ELSE
      *
      * EVENTUELT KONVERTERE SVENSK TARIFFNUMMER
      *
     C                   EXSR      SADLSR
     C                   ENDIF
      * 5. Bruttovekt
     C                   EXSR      GETDED
     c                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVVKTB
      * 6. Nettovekt
     C                   EXSR      GETDED
     c                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVVKTN
      * 7. Tollverdi
     C                   EXSR      GETDED
     c                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVBELT
      *  8. Antall
     C                   EXSR      GETDED
     c                   MOVEA     *BLANKS       A
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVNTM
      *
      * Beregener vekter
      *
      * BRUTTO OPPGITT MED IKKE NETTO
      *
     C     SVVKTB        IFNE      0
     C     SVVKTN        ANDEQ     0
     C                   EVAL(H)   SVVKTN = SVVKTB * 0.9
     C                   END
      *
      * NETTO  OPPGITT MED IKKE BRUTTO
      *
     C     SVVKTB        IFEQ      0
     C     SVVKTN        ANDNE     0
     C                   EVAL(H)   SVVKTB = SVVKTN / 0.9
     C                   END
      *
      * INGER VEKT OPPGITT PÅ LINJE BEREGN HVIS BRUTTO TOTAL FINNES
      *
     C     SVVKTB        IFEQ      0
     C     SVVKTN        ANDEQ     0
     c     sfbl28        ANDNE     0
     C     SVBELT        div       SFBL28        faktor           15 5
     c     faktor        mult      bruttov       SVVKTB
     C                   EVAL(H)   SVVKTN = SVVKTB * 0.9
     c                   end
      *
      * Nettovekt
      *
      *
     C                   WRITE     SAEVR

     C                   ENDSR
      ***************************************************************
     C     GETDED        BEGSR
      ***************************************************************
     C                   MOVEA     *BLANKS       FE
     C     WX            IFGE      1023
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   IF        AA(WX) = '"'
     C                   MOVE      'J'           XAPS              1
     C                   ADD       1             WX
     C                   ELSE
     C                   MOVE      'N'           XAPS
     C                   END
     C     1             DO        256           NR                3 0
     C                   MOVE      AA(WX)        TEGN              1
     C                   ADD       1             WX
     C                   MOVE      AA(WX)        TEGN2             1
     C     TEGN          IFEQ      ';'
     C     XAPS          ANDEQ     'N'
     C                   GOTO      ENDDED
     C                   ENDIF
     C     TEGN          IFEQ      '"'
     C     TEGN2         ANDEQ     ';'
     C     XAPS          ANDEQ     'J'
     C                   ADD       1             WX
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   MOVEA     TEGN          FE(NR)
     C     WX            IFGE      1023
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   ENDDO
     C     ENDDED        ENDSR
      ****************************************************************
     C     GETNUM        BEGSR
      ****************************************************************
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *************************************************************************
     C     SADLSR        BEGSR
      *************************************************************************
     C     SADLK         KLIST
     C                   KFLD                    SLKNR
     C                   KFLD                    SLALFA
     C     SADLK         CHAIN     SADL                               88
     C     *in88         ifeq      '0'
     c                   z-add     sltanr        SVVNT
     c                   else
     c                   movel     tatanr        tata06            6
     c                   movel     tata06        SVVNT
     c                   end
     c                   endsr
