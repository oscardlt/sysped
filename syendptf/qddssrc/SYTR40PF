NXX  A*HERFAS ØKT TIL 33  *********************************************
     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=132, OVERFLOW=72      *
     A*****************************************************************
     A          R KJØPHH1
     A            KFMT1         75A     1  1
     A          R KJØPHH2
     A            KFMT2         75A     1  1
     A          R HEADP                     SKIPB(1)
     A            HOHT1         80A        1
     A                                      SPACEB(1)
     A            HOHT2         80A        1
     A                                      SPACEB(1)
     A            HOHT3         80A        1
     A                                      SPACEB(1)
     A            HOHT4         80A        1
     A                                      SPACEB(1)
     A            HOHT5         80A        1
     A                                      SPACEB(1)
     A            HOHT6         80A        1
     A                                      SPACEB(1)
     A            HOHT7         80A        1
     A                                      SPACEB(1)
     A          R HEAD0                     SKIPB(1)
     A                                     1' '
     A          R ANNUL                     SPACEB(1)
     A                                     2MSGCON(80 PTR0034 SYMSGF)
     A                                     2MSGCON(80 PTR0035 SYMSGF)
     A                                      SPACEB(1)
     A                                     2MSGCON(80 PTR0034 SYMSGF)
     A                                      SPACEB(1)
     A          R OMDISP                    SPACEB(1)
     A                                     2MSGCON(80 PTR0034 SYMSGF)
     A                                     2MSGCON(80 PTR0036 SYMSGF)
     A                                      SPACEB(1)
     A            OMTUAF         4S 0     50EDTCDE(Z)
     A                                    55'/'
     A            OMTURF         8S 0     56EDTCDE(Z)
     A                                    64'/'
     A            OMRUNF         8S 0     65EDTCDE(Z)
     A                                     2MSGCON(80 PTR0034 SYMSGF)
     A                                      SPACEB(1)
     A
     A          R HEAD1A                    SPACEB(2)
     A*OPPDRAGSGIVER
     A                                     2MSGCON(18 PTR0001 SYMSGF)
     A            PRNAA         30A       21
     A                                    55MSGCON(25 PTR0006 SYMSGF)
     A                                      HIGHLIGHT
     A            PRADA1        30A       21
     A                                      SPACEB(1)
     A N90N91                             55MSGCON(25 PTR0007 SYMSGF)
     A                                      HIGHLIGHT
     A  90 91                             55MSGCON(25 PTR0007 SYMSGF)
     A                                      HIGHLIGHT
     A  90N91                             55MSGCON(25 PTR0037 SYMSGF)
     A                                      HIGHLIGHT
     A            PRADA2        30A       21
     A                                      SPACEB(1)
     A  90                                55MSGCON(25 PTR0027 SYMSGF)
     A                                      HIGHLIGHT
     A N90                                55MSGCON(25 PTR0028 SYMSGF)
     A                                      HIGHLIGHT
     A            PRADA3        30A       21
     A                                      SPACEB(1)
     A                                    55MSGCON(25 PTR0006 SYMSGF)
     A                                      HIGHLIGHT
     A*FAKTURAMOTTAGER
     A                                     2MSGCON(18 PTR0002 SYMSGF)
     A                                      SPACEB(2)
     A            PRNAF         30A       21
     A                                    55MSGCON(10 PTR0008 SYMSGF)
     A            HEKNSF         8  0     66EDTCDE(Z)
     A            PRADF1        30A       21
     A                                      SPACEB(1)
     A                                    55MSGCON(10 PTR0009 SYMSGF)
     A            PRDATO         6  0     66EDTCDE(Y)
     A            PRADF2        30A       21
     A                                      SPACEB(1)
TRH  A*                                   53MSGCON(10 PTR0010 SYMSGF)
     A                                    55MSGCON(8 TRH0001 SYMSGF)
     A            HEAVD          4  0     64
     A                                    68'/'
     A            HEOPD          7  0     69
     A                                    76'/'
     A            HESG           3A       77
     A            PRADF3        30A       21
     A                                      SPACEB(1)
     A  50                                53MSGCON(10 JOV0877 SYMSGF)
     A  50        TRAVD0         4  0     64
     A  50                                68'/'
     A  50        TROPD0         7  0     69
     A  50                                76'/'
     A  50        DUPSG          3A       77
     A                                    55MSGCON(10 PTR0011 SYMSGF)
     A                                      SPACEB(1)
     A  51        DITUAV         4  0     66
     A  51                                70'/'
     A  51        DITUR          8  0     71
     A*A-REF
     A                                     2MSGCON(18 PTR0003 SYMSGF)
     A                                      SPACEB(1)
     A*           HERFA         15A       21
SXX  A*           HERFA15       15A       21
NXX  A            HERFA33       33A       21
     A                                    55MSGCON(10 PTR0012 SYMSGF)
     A  51        DIRUND         8  0     71
     A*TRANSPORTØR
     A                                     2MSGCON(18 PTR0029 SYMSGF)
     A                                      SPACEB(2)
     A            TUNAT         30A       21
     A                                    55MSGCON(10 PTR0013 SYMSGF)
     A N77        TUBILN        10A       66
     A*HENTEADRESSE
      * HIGHLIGHT PÅ HENTE/LEV.ADRESSE
     A                                     2MSGCON(18 PTR0004 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            HENAS         30A       21HIGHLIGHT
     A                                    55MSGCON(10 JOV0214 SYMSGF)
     A N77        TUHENG        10A       66
     A            HEADS1        30A       21HIGHLIGHT
     A                                      SPACEB(1)
     A                                    55MSGCON(10 PTR0014 SYMSGF)
     A N77        TUSJ15        15        66
     A            HEADS2        30A       21HIGHLIGHT
     A                                      SPACEB(1)
     A                                    55MSGCON(10 PTR0015 SYMSGF)
     A            HEOT           2A       66
     A            HEADS3        30A       21HIGHLIGHT
     A                                      SPACEB(1)
     A                                    55MSGCON(10 PTR0016 SYMSGF)
     A            HEFR           3A       66
     A*LEVERINGSADRESSE
     A                                     2MSGCON(18 PTR0005 SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            HENAK         30A       21HIGHLIGHT
     A                                    55MSGCON(10 PTR0017 SYMSGF)
     A            TRTSTA         1A       66
     A            HEADK1        30A       21HIGHLIGHT
     A                                      SPACEB(1)
     A                                    55MSGCON(10 PTR0018 SYMSGF)
     A            TRMVA          1A       66
     A            HEADK2        30A       21HIGHLIGHT
     A                                      SPACEB(1)
     A                                    55MSGCON(08 TRO0501 SYMSGF)
     A            HEGN          15A       64
     A            HEADK3        30A       21HIGHLIGHT
     A                                      SPACEB(1)
     A                                    55MSGCON(7 JOV0884 SYMSGF)
     A            HEPOS1         7A       66
     A            HEPOS2         4  0     74EDTCDE(4)
     A                                     2MSGCON(18 JOV0882 SYMSGF)
     A                                      SPACEB(1)
     A            HESDL         20A       21
     A  52                                43MSGCON(12 JOV0885 SYMSGF)
     A                                    55MSGCON(7 JOV0883 SYMSGF)
     A            HETLE          6A       63
     A            HETLL         10A       70
     A*FORTOLLINGSAGENT
     A          R FORT                      SPACEB(1)
     A                                     2MSGCON(18 PTR0026 SYMSGF)
     A            PRNAU         30A       21
     A            PRADU1        30A       21
     A                                      SPACEB(1)
     A            PRADU2        30A       21
     A                                      SPACEB(1)
     A            PRADU3        30A       21
     A                                      SPACEB(1)
     A* VAREVERDI
     A          R VARE                      SPACEB(1)
     A                                     2MSGCON(18 PTR0020 SYMSGF)
     A            TRVERV         3A       21
     A            TRVERB        13S 2     25EDTCDE(J)
     A* ETTERKRAV
     A          R ETT                       SPACEB(1)
     A                                     2MSGCON(18 PTR0021 SYMSGF)
     A            TRETTV         3A       21
     A            TRETTB        13S 2     25EDTCDE(J)
     A* FRAKTBELØP
     A          R FRAKT                     SPACEB(1)
     A                                     2MSGCON(18 PTR0022 SYMSGF)
     A            TRFRAV         3A       21
     A            TRFRAB        13S 2     25EDTCDE(J)
     A            TRFRAK         1A       45
     A* FRA TIL STEDER
     A          R HEAD1B                    SPACEB(2)
     A                                      HIGHLIGHT
     A                                     1'************************'
     A                                    25'************************'
     A                                    49'*********************'
     A                                     1'*'
     A                                      SPACEB(1)
     A* ALLE FELT I HEAD1B FLYTTET LITT TIL HØYRE UTEN DOKUMENTASJON
     A                                     4MSGCON(18 PTR0024 SYMSGF)
     A            HESDF          5A       23
     A            XRSDFD         6S 0     29EDTCDE(Y)
     A            TRSDFK         4S 0     39
     A                                    44'->'
     A            HESDT          5A       47
     A            XRSDTD         6S 0     53EDTCDE(Y)
     A            TRSDTK         4S 0     63
     A                                    69'*'
     A                                     4MSGCON(18 PTR0025 SYMSGF)
     A                                      SPACEB(1)
     A                                     1'*'
     A            HESDFF         5A       23
     A            XRSFFD         6S 0     29EDTCDE(Y)
     A            TRSFFK         4S 0     39
     A                                    44'->'
     A            HESDVT         5A       47
     A            XRSVTD         6S 0     53EDTCDE(Y)
     A            TRSVTK         4S 0     63
     A                                    69'*'
     A                                     1'************************'
     A                                      SPACEB(1)
     A                                    25'************************'
     A                                    49'*********************'
     A                                      SPACEA(1)
      * BARCODE - SKILLEARK
     A          R SKILLBAR
     A            SKILLE        13A       55BARCODE(CODE128 4 *HRI *HRZ +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A                                      SPACEA(1)
     A            SKILTEXT      47A        5HIGHLIGHT
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A                                      SPACEA(1)
     A            SKILLET       15A        5HIGHLIGHT
     A                                      SPACEA(2)
     A* FARLIG GODS
     A          R FARL                      SPACEB(1)
     A                                     2MSGCON(18 PTR0023 SYMSGF)
     A                                    21'I'
     A            TRFA11         7A       23
     A                                    31'UN'
     A            TRFA12         4A       33
     A                                    38'FLP'
     A            TRFLP1         3S 0     42EDTCDE(M)
     A                                    21'A'
     A                                      SPACEB(1)
     A            TRFA21         7A       23
     A                                    31'UN'
     A            TRFA22         4A       33
     A                                    38'FLP'
     A            TRFLP2         3S 0     42EDTCDE(M)
     A          R KJØPFS                    SPACEB(1)
     A            KFSOTX        30         2
     A                                    32':'
     A            FSSOK         35        35
     A          R KJØPUFB                   SPACEB(1)
     A                                     2MSGCON(24 YBC0491 SYMSGF)
     A                                      HIGHLIGHT
     A                                    32':'
     A                                      HIGHLIGHT
     A            HEFT1A        30        35HIGHLIGHT
     A            HEFT1B        15        66HIGHLIGHT
     A* FRITEKSTER  HEADING INTERN....
     A          R DET1C                     SPACEB(1)
     A                                     2MSGCON(20 DIV4048 SYMSGF)
     A* FRITEKSTER  HEADING EKSTERNE MELDINGER
     A          R DET1D                     SPACEB(1)
     A N90                                 2MSGCON(31 JOV0693 SYMSGF)
     A  90                                 2MSGCON(31 JOV0694 SYMSGF)
     A          R DETT                      SPACEB(1)
     A            FRTTXT        70A        2
     A* LINJER MED GODSINFORMASJONER
     A          R GODS
     A                                     1MSGCON(24 DIV0212 SYMSGF)
     A                                      SPACEB(2)
     A                                    25MSGCON(24 DIV0213 SYMSGF)
     A                                    50MSGCON(24 DIV0214 SYMSGF)
     A                                     1'________________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A            HENT           7  0      1EDTCDE(Z)
     A                                      SPACEB(1)
     A            HEVS1         25A       09
     A            XXVKT          5  0     39EDTCDE(Z)
     A            HEVKT          9  0     36EDTCDE(Z)
     A            HEM3           7  3     47EDTCDE(J)
     A            HELM           4  2     57EDTCDE(J)
     A            HEGM1         15A       63
     A            HEVS2         25A       09
     A                                      SPACEB(1)
     A            HEGM2         15A       63
     A                                     1'________________________'
     A                                      SPACEB(2)
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A N90                                17MSGCON(64 DIV0217 SYMSGF)
     A                                      SKIPB(63)
