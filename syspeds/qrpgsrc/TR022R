      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED MERKE & NR                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FTRUVMN    UF A E           K DISK
     FTRKODL01  IF   E           K DISK
     FTR022D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UMN           IFNE      *BLANKS
     C                   MOVE      UMN           TVMN
     C                   MOVE      UMNSK         TVMNSK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TRVMNK        SETLL     TRUVMN
     C     TRVMNK        READE(N)  TRUVMN                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     TRVMNK        READE(N)  TRUVMN                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVE      '0'           *IN90
     C   03
     COR 12              GOTO      SLUTT
      *
     C   04              DO
     C                   MOVE      '012'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     W2MNSK        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         W2MNSK
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C                   EXSR      CRTMN
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     TRVMNK        SETLL     TRUVMN
     C     TRVMNK        READE     TRUVMN                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    TRUVMNR
     C     TRVMNK        READE     TRUVMN                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UMN
     C                   MOVE      *BLANKS       UMNSK
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TVMN          IFNE      '*DELETE'
     C                   MOVE      TVMN          UMN
     C                   MOVE      TVMNSK        UMNSK
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TVMN          IFNE      '*DELETE'
     C     UMN           IFEQ      *BLANKS
     C                   MOVE      TVMN          UMN
     C                   MOVE      TVMNSK        UMNSK
     C                   ELSE
     C                   WRITE     TRUVMNR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     TVMN          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGMN
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     TVMN          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTMN
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTMN         BEGSR
     C***********************************************
     C     W2MN          COMP      *BLANKS                                90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'YBC0826'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
      *
     C     W2MNSK        IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     W2MNSK        YKODE
     C     TRK01K        CHAIN     TRKODL01                           89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'YBC0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
     C                   END
      *
     C                   MOVE      W2MN          TVMN
     C                   MOVE      W2MNSK        TVMNSK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2MN
     C                   MOVE      *BLANKS       W2MNSK
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGMN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      TVMN          WSMN
     C                   MOVE      TVMNSK        WSMNSK
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSMNSK        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSMNSK
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C     WSMN          COMP      *BLANKS                                90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'YBC0826'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
      *
     C     WSMNSK        IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSMNSK        YKODE
     C     TRK01K        CHAIN     TRKODL01                           89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'YBC0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
     C                   END
      *
     C                   MOVE      WSMN          TVMN
     C                   MOVE      WSMNSK        TVMNSK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTMN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       TVMN
     C                   MOVEL     '*DELETE'     TVMN
     C                   MOVE      *BLANKS       TVMNSK
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRVMNK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
     C     TRK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UMN              17
     C                   PARM                    UMNSK             2
     C     *IN27         PARM      *IN27         UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C                   MOVE      ULI           TVLI
      *
     C                   ENDSR
      *
