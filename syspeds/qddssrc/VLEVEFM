     A*%%TS  SD  20150129  162943  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9XXXA* 01 YBC PTF9 VALUTAKODE
P9XXXA* 03 JOV PTF9 POSTNR  UTLAND
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20131113  194325  SVEIN       REL-V7R1M0  5770-WDS
     A            LAND           3   H
     A            RNRALE        20   H
     A            KHENIT         3   H
     A            ADR2          30A  H
     A            ADR3          24A  H
     A            SONAVN        10A  H
     A            KUNDNR         8S 0H
     A            BETBET         2A  H
     A            KHENV          3A  H
N03  A            POSTNU        10A  H
N05  A            BANKG         35A  H
     A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4')
     A            LEVNR          8Y 0O  5  5EDTCDE(4)
     A            LNAVN         30A  O  5 14
     A            ADR1          30A  O  5 45
     A            POSTNR         4S 0O  5 76
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20120424  222558  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/VLEVE'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 JOV1560 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 70SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(075 JOV1561 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(079 JOV1562 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A*%%TS  SD  20120424  222558  JOVO        REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Avslutt')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3  2MSGCON(075 JOV1563 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
S01  A*                                     WINDOW(*DFT 12 55 *NOMSGLIN)
     A          R VARELI
     A*%%TS  SD  20150129  162943  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(*DFT 15 55 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 15 55 *NOMSGLIN)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/VLEVE'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 JOV1560 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(019 JOV1564 *LIBL/SYMSGF)
     A            WSLNR          8Y 0O  3 22COLOR(WHT)
     A                                      EDTCDE(4)
     A            XTYPE         20A  O  3 32COLOR(RED)
     A                                  4  2MSGCON(019 JOV1565 *LIBL/SYMSGF)
     A            WSNA          30A  B  4 22DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  2MSGCON(019 JOV1566 *LIBL/SYMSGF)
     A            WSAD1         30A  B  5 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(019 JOV1567 *LIBL/SYMSGF)
     A            WSAD2         30A  B  6 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2MSGCON(019 JOV1568 *LIBL/SYMSGF)
     A            WSPN           4Y 0B  7 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPS          24A  B  7 28DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8  2MSGCON(019 JOV1569 *LIBL/SYMSGF)
     A            WSNASØ        10A  B  8 22DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8 34'PnrUtl:'
     A            WSPNU         10A  B  8 42DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2MSGCON(019 JOV1570 *LIBL/SYMSGF)
     A            WSBB           2A  B  9 22DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(019 JOV1571 *LIBL/SYMSGF)
     A            WSKH           3A  B 10 22DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 27MSGCON(015 SHU1424 *LIBL/SYMSGF)
     A            WSKHI          3A  B 10 43COLOR(YLW)
     A  83                                  DSPATR(RI)
     A                                 11  2MSGCON(019 JOV1572 *LIBL/SYMSGF)
     A            WSKNR          8Y 0B 11 22DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(019 JOV1573 *LIBL/SYMSGF)
     A            WSVAL          3A  B 12 22DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 26MSGCON(005 SHU1502 *LIBL/SYMSGF)
     A            WSLAND         2A  B 12 32COLOR(YLW)
     A                                 13  2MSGCON(019 SHU1503 *LIBL/SYMSGF)
     A            WSRALE        20   B 13 22COLOR(YLW)
     A                                 14  2MSGCON(019 SHU1570 *LIBL/SYMSGF)
     A            WSBANK        30   B 14 22COLOR(YLW)
     A  30        MSGTXT        50A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             15  2MSGCON(040 JOV1574 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra leverandør --> '
     A                                      COLOR(BLU)
     A            W2NASØ        10A  B  2 26COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  3  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      WINDOW(*DFT 11 55 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/VLEVE'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av leverandør'
     A                                      COLOR(WHT)
     A                                  3  2'Leverandørsnr.....:'
     A            LEVNR          8Y 0O  3 22COLOR(WHT)
     A                                      EDTCDE(4)
     A            XTYPE         20A  O  3 32COLOR(RED)
     A                                  4  2'Leverandørsnavn...:'
     A            LNAVN         30A  O  4 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'Adresse1..........:'
     A            ADR1          30A  O  5 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'Adresse2..........:'
     A            ADR2          30A  O  6 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'Postnr. & postadr.:'
     A            POSTNR         4  0O  6 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            ADR3          24A  O  6 28DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Søkenavn..........:'
     A            SONAVN        10A  O  7 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2'Betalingsbetingels:'
     A            BETBET         2A  O  8 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2'Kontohenv.kode....:'
     A            KHENV          3A  O  9 22DSPATR(UL)
     A                                 10  2'Kundenr...........:'
     A            KUNDNR         8Y 0O 10 22COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11  2'F12=Forrige'
     A                                      COLOR(BLU)
     A          R NYVARE
     A*%%TS  SD  20120424  222558  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Avslutt program')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Avslutt')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/VLEVE'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 JOV1560 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(079 JOV1562 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(075 JOV1563 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
