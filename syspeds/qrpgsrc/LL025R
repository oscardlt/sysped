********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 SVENSK GODSJOURNAL
********************************************************************
      *
      **************************************************************
      *** PROGRAM FOR Å SKRIVE LAGERLISTE                        ***
      **************************************************************
      *
     FKODTFM    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FLLAGJL05  IF   E           K DISK
     FLLAGJL03  IF   E           K DISK    RENAME(LLAGJR:LLAGJR3)
     FLLAGJL02  IF   E           K DISK    RENAME(LLAGJR:LLAGJR2)
     FLLAGJD01  IF   E           K DISK
     F*LLVARE01IF  E           K        DISK
     FLLVARE01  IF   E           K DISK
N01  FLLMRF     IF   E           K DISK
N01  FLLHEAD    IF   E           K DISK
N01  FLLSERL03  IF   E           K DISK
N01  FSVIH      IF   E           K DISK
N01  FSVEH      IF   E           K DISK
N01  FSVEV      IF   E           K DISK
N01  FLL025F    O  A E             DISK
     FLL025PF   O    E             PRINTER OFLIND(*IN66)
     D                 DS
     D  FRADAT                 1      8  0
     D  WSDATF                 3      8  0
     D                 DS
     D  TILDAT                 1      8  0
     D  WSDATT                 3      8  0
     D                 DS
     D  VAVARE                 1     35
     D  VAVARX                 1     15
     D                 DS
     D  XTID                   1      6  0
     D  XDTD                   7      8  0
     D  XDTM                   9     10  0
     D  XDTÅ                  11     14  0
     D  XTIME                  1     14  0
     D                 DS
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D  XIDAG                  1      8  0
     D                 DS
     D  WSAG                   1     25
     D  WSAG01                 1      5
     D  WSAG02                 6     10
     D  WSAG03                11     15
     D  WSAG04                16     20
     D  WSAG05                21     25
     D  WAG                    1     25    DIM(5)
     D                 DS
     D  LVAG01                 1      5
     D  LVAG02                 6     10
     D  LVAG03                11     15
     D  LVAG04                16     20
     D  LVAG05                21     25
     D  VAG                    1     25    DIM(5)
N01  D                 DS
N01  D  SVIH_EUP1              1      4
N01  D  SVEV_EUP1              1      4
N01  D                 DS
N01  D  SVIH_TUID              1     10
N01  D  SVEH_TUID              1     10
      *
     C     *ENTRY        PLIST
     C                   PARM                    WSGRUA            8            GRUPPE
     C                   PARM                    WSKJEA            8            KUNDE/KJEDE
     C                   PARM                    WSDT              8            OPPTELLINGSDATO
     C                   PARM                    WSARTF           15            ARTIKKEL FRA
     C                   PARM                    WSARTT           15            ARTIKKEL TIL
     C                   PARM                    WSLAGF           25            LAG.LOK. FRA
     C                   PARM                    WSLAGT           25            LAG.LOK. TIL
     C                   PARM                    FRADAA            8            DATO FRA
     C                   PARM                    TILDAA            8            DATO TIL
     C                   PARM                    WSFORP            1            FORP.detalj
     C                   PARM                    WSDET             1            SUBDETALJER
     C                   PARM                    WSSALD            1            KUN SALDO...
     C                   PARM                    WSTYPE            3            KUN KRT/HPL
     C                   PARM                    WSBRUD            1            SIDEBRUDD
     C                   PARM                    WSSORT            1            TVUNGEN SORT
     C                   PARM                    WSBATC            1            UT BATCHNR/UTL.DATO
     C     WSAG          PARM                    UAG              25
     C                   PARM                    WSEIER
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C     COPY          PARM      COPY          COPY              2 0
N01  C                   PARM                    WSSVGS            1
      *
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
      * 50 ON=LES LF SOM BARE INKL. POSTER MED SALDO ULIK 0
     C     WSSALD        COMP      '+'                                    50
      * 50 ON = KUN POSTER MED SALDO ULIK 0
      * 50 OFF= HVIS WSSALD ' ' -> SKRIV ALLE POSTER
      * 50 OFF= HVIS WSSALD '0' -> SKRIV KUN POSTER MED SALDO 0
      *                                        |
     C     ARTKEY        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    WSARTF
     C     LAGKEY        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    WSLAGF
     C     DATKEY        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    FRADAT
     C     REQKEY        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C     VARKEY        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    VAANR
     C     KUNKEG        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VAGRUP
     C     KUNKEK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VAKJED
N01  C     SVIHK         KLIST
N01  C                   KFLD                    VDAVD
N01  C                   KFLD                    VDOPD
N01  C     LLSERL03K     KLIST
N01  C                   KFLD                    VDMN
N01  C                   KFLD                    VDVN
N01  C     LLMRFK        KLIST
N01  C                   KFLD                    VAGRUP
N01  C                   KFLD                    VAKJED
N01  C                   KFLD                    VAMRNR
      *
     C                   MOVE      *ZEROS        GTOPOS            9 0
     C                   MOVE      *ZEROS        GTOSAL            9 0
     C                   MOVE      *ZEROS        GTOUPL            9 0
     C                   TIME                    XTIME
     C                   MOVE      XDTD          XIDAGD
     C                   MOVE      XDTM          XIDAGM
     C                   MOVE      XDTÅ          XIDAGÅ
     C                   MOVE      WSDT          XDT
     C     XDT           COMP      XIDAG                              5454
     C     WSAG          COMP      *BLANKS                            5555
     C  N55WSEIER        COMP      *BLANKS                            5555
     C     WSFORP        COMP      'N'                                    60
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   30
     C*
     C                   MOVE      WSGRUA        VAGRUP
     C                   MOVE      WSKJEA        VAKJED
     C                   MOVE      FRADAA        FRADAT
     C                   MOVE      TILDAA        TILDAT
     C*
     C     KUNKEK        CHAIN     CUNDL01                            30
     C  N30              MOVEL     KNAVN         NVNKJE           30
     C*
     C     KUNKEG        CHAIN     CUNDL01                            30
     C  N30              MOVEL     KNAVN         NVNGRU           30
      *
      * 51 ON  = SORT PÅ ARTIKKEL
      * 52 ON  = SORT PÅ LAGERLOKASJON
      * 53 ON  = SORT PÅ INNLEGGSDATO
     C                   MOVEA     '000'         *IN(51)
      * TVUNGEN SORTERING..
     C     WSSORT        IFNE      ' '
     C     WSSORT        COMP      '1'                                    51
     C     WSSORT        COMP      '2'                                    52
     C     WSSORT        COMP      '3'                                    53
     C                   ELSE
      * ELLER FØRSTE UTFYLTE SELEKSJON ER SØKEVEI..
     C                   SELECT
     C     WSARTF        WHENNE    *BLANKS
     C                   MOVE      '1'           *IN51
     C     WSLAGF        WHENNE    *BLANKS
     C                   MOVE      '1'           *IN52
     C                   OTHER
     C                   MOVE      '1'           *IN53
     C                   ENDSL
     C                   END
      * 60 ON = IKKE vis forpakningsdetaljer
     C     WSFORP        COMP      'N'                                    60
      *
      * *IN66=SIDEBRUDD (FRAMTVING PÅ 1. SIDE)
     C                   MOVE      *ON           *IN66
     C     WSLAGF        IFEQ      '*ALL'
     C     *IN52         OREQ      '1'
     C     WSARTF        ANDEQ     '*ALL'
     C                   MOVEL     *BLANKS       WSLAGF
     C                   MOVEL     '99999999'    WSLAGT
     C                   END
     C     WSARTF        IFEQ      '*ALL'
     C                   MOVEL     *BLANKS       WSARTF
     C                   MOVEL     '99999999'    WSARTT
     C                   END
      *
     C                   SELECT
      * 51 = SORT PÅ ART
     C     *IN51         WHENEQ    '1'
     C     ARTKEY        SETLL     LLAGJL03                               39
      * 52 = SORT PÅ LAGERLOK
     C     *IN52         WHENEQ    '1'
     C     LAGKEY        SETLL     LLAGJL05                               39
      * 53 = SORT PÅ DATO
     C     *IN53         WHENEQ    '1'
     C     DATKEY        SETLL     LLAGJL02                               39
     C                   ENDSL
      *
     C     NYLIN         TAG
      *
     C                   SELECT
     C     *IN51         WHENEQ    '1'
     C     REQKEY        READE     LLAGJL03                               39
     C     *IN52         WHENEQ    '1'
     C     REQKEY        READE     LLAGJL05                               39
     C     *IN53         WHENEQ    '1'
     C     REQKEY        READE     LLAGJL02                               39
     C                   ENDSL
      *
     C     *IN39         IFEQ      *OFF                                         B1
     C   54VADATO        CABGE     XDT           NYLIN
      *
      * SJEKK NØKKELBASERTE VERDIER (TILVERDIER) - HOPP UT.
      *       BEVAR BRUDDNØKKEL HVIS OK
     C                   SELECT
     C     *IN51         WHENEQ    '1'
     C     VAANR         IFGT      WSARTT
     C                   GOTO      SLUTT
     C                   ENDIF
     C                   MOVE      VAANR         NYBRUD           25
     C     *IN52         WHENEQ    '1'
     C     VALLOK        IFGT      WSLAGT
     C                   GOTO      SLUTT
     C                   ENDIF
     C                   MOVE      VALLOK        NYBRUD
     C     *IN53         WHENEQ    '1'
     C     VADATO        IFGT      TILDAT
     C                   GOTO      SLUTT
     C                   ENDIF
     C                   MOVE      VADATO        NYBRUD
     C                   ENDSL
     C     WSARTT        IFGT      *BLANKS
     C     WSARTF        IFGT      VAANR
     C     WSARTT        ORLT      VAANR
     C                   GOTO      NYLIN
     C                   ENDIF
     C                   ENDIF
     C     WSLAGT        IFGT      *BLANKS
     C     WSLAGF        IFGT      VALLOK
     C     WSLAGT        ORLT      VALLOK
     C                   GOTO      NYLIN
     C                   ENDIF
     C                   ENDIF
     C     TILDAT        IFGT      0
     C     FRADAT        IFGT      VADATO
     C     TILDAT        ORLT      VADATO
     C                   GOTO      NYLIN
     C                   ENDIF
     C                   ENDIF
      *
      * SJEKK ANDRE FILTER, HOPP TIL NESTE
      * FRA/TIL ARTIKKEL
      * (PR I DAG ER ART.NR ALLTID KEY HVIS DEN I DET HELE TATT ER
      *  GJØR DENNE TESTEN NØDVENDIG)
      *  FYLLT UT, MEN FRAMTIDIG LØSNING MED VALG AV TVUNGEN SORTERING
     C     *IN51         IFNE      '1'
     C     WSARTF        IFNE      *BLANKS
     C     VAANR         ANDLT     WSARTF
     C                   GOTO      NYLIN
     C                   ENDIF
     C     WSARTT        IFNE      *BLANKS
     C     VAANR         ANDGT     WSARTT
     C                   GOTO      NYLIN
     C                   ENDIF
     C                   END
      * FRA/TIL LAGERLOKASJON
     C     *IN52         IFNE      '1'
     C     WSLAGF        IFNE      *BLANKS
     C     VALLOK        ANDLT     WSLAGF
     C                   GOTO      NYLIN
     C                   ENDIF
     C     WSLAGT        IFNE      *BLANKS
     C     VALLOK        ANDGT     WSLAGT
     C                   GOTO      NYLIN
     C                   ENDIF
     C                   END
      * FRA/TIL DATO...
     C     *IN53         IFNE      '1'
     C     FRADAT        IFNE      *ZEROS
     C     VADATO        ANDLT     FRADAT
     C                   GOTO      NYLIN
     C                   ENDIF
     C     TILDAT        IFNE      *ZEROS
     C     VADATO        ANDGT     TILDAT
     C                   GOTO      NYLIN
     C                   ENDIF
     C                   END
      *
     C     WSTYPE        IFNE      *BLANKS
     C     VAHK          ANDNE     WSTYPE
     C                   GOTO      NYLIN
     C                   END
      *
     C     *IN55         IFEQ      '1'
     C     VARKEY        CHAIN     LLVARE01                           33
     C     WSAG          IFNE      *BLANKS
     C     1             DO        5             XN                1 0
     C     VAG(XN)       IFNE      *BLANKS
     C     1             DO        5             X2                1 0
     C     WAG(X2)       IFNE      *BLANKS
     C     WAG(X2)       ANDEQ     VAG(XN)
     C                   GOTO      LINMED
     C                   END
     C                   ENDDO
     C                   END
     C                   ENDDO
     C                   GOTO      NYLIN
     C     LINMED        TAG
     C                   END
     C     WSEIER        IFNE      *BLANKS
     C     WSEIER        CABNE     LVEIER        NYLIN
     C                   END
     C                   END
      *
      * HENT SALDO PÅ GITT DATO
      * SUMMER ANTALL KOLLI PÅ UTLEVERTE
     C     DETKEY        SETLL     LLAGJD01
     C     DETKEY        READE     LLAGJD01                               33
     C     *IN33         DOWEQ     '0'
     c     XDT           cabeq     99999999      NxtsubT
     C     VDLDAT        cablt     XDT           NxtsubT
     C                   ADD       VDLANT        VASALD
     C                   ADD       VDSAE3        VASAE3
     C                   ADD       VDSAE2        VASAE2
     C                   ADD       VDSAE1        VASAE1
     C                   ADD       VDSAE0        VASAE0
     C     NxtSubT       Tag
     C     DETKEY        READE     LLAGJD01                               33
     C  N33              ENDDO
      *
     C                   SELECT
     C     WSSALD        WHENEQ    '+'
     C     VASALD        CABLE     0             NYLIN
     C     WSSALD        WHENEQ    '0'
     C     VASALD        CABNE     0             NYLIN
     C                   ENDSL
      *
      *
      *
     C     *IN50         IFEQ      '0'
     C     WSSALD        ANDEQ     '0'
     C     VASALD        ANDNE     *ZEROS
     C                   GOTO      NYLIN
     C                   END
      *
     C     VASAE3        MULT      VAVKT3        VAVKTS
     C     VASAE2        MULT      VAVKT2        XWORK             7 2
     C                   ADD       XWORK         VAVKTS
     C     VASAE1        MULT      VAVKT1        XWORK
     C                   ADD       XWORK         VAVKTS
     C     VASAE0        MULT      VAVKT0        XWORK
     C                   ADD       XWORK         VAVKTS
     C     VASAE3        MULT      VAKUB3        VAKUBS
     C     VASAE2        MULT      VAKUB2        XWORK2            7 4
     C                   ADD       XWORK2        VAKUBS
     C     VASAE1        MULT      VAKUB1        XWORK2
     C                   ADD       XWORK2        VAKUBS
     C     VASAE0        MULT      VAKUB0        XWORK2
     C                   ADD       XWORK2        VAKUBS
      *
      *
     C   52XANR          IFNE      VAANR
     C     NYBRUD        ORNE      GMBRUD
     C     XANTAL        IFGT      1
N01  C     WSSVGS        ANDNE     'J'
     C  N60              WRITE     DTOTAL
     C   60              WRITE     DTOTALU
     C                   END
     C                   MOVE      *ZEROS        TDTUPL
     C                   MOVE      *ZEROS        TDTSAL
     C                   MOVE      *ZEROS        TDTPAL
     C                   MOVE      *ZEROS        TDTKRT
     C                   MOVE      *ZEROS        TDTESK
     C                   MOVE      *ZEROS        TDTSTK
     C                   MOVE      *ZEROS        TDTKTS
     C                   MOVE      *ZEROS        TDTUBS
     C                   MOVE      *ZEROS        XANTAL            3 0
     C                   MOVE      VAANR         XANR             15
     C                   END
      *
      *  POSTEN ER OK - SJEKK FOR BRUDDVERDIER
      * BRUDD (OG DETTE ER IKKE FØRSTE LESTE POST) SKRIV DELTOTAL
      *
     C     NYBRUD        IFNE      GMBRUD
      *
     C     POST1         IFEQ      'N'
N01  C     WSSVGS        ANDNE     'J'
     C  N60              WRITE     TOTAL
     C   60              WRITE     TOTALU
     C                   MOVE      *ZEROS        TOTPOS
     C                   MOVE      *ZEROS        TOTUPL
     C                   MOVE      *ZEROS        TOTSAL
     C                   MOVE      *ZEROS        TOTKRT
     C                   MOVE      *ZEROS        TOTESK
     C                   MOVE      *ZEROS        TOTSTK
     C                   MOVE      *ZEROS        TOTKTS
     C                   MOVE      *ZEROS        TOTUBS
     C                   END
      *
      * SIDESKIFT VED BRUDD??
     C     WSBRUD        IFEQ      'J'
     C                   MOVE      *ON           *IN66
     C                   END
      *
     C                   END
      *
      *
      *
      * OVERFLOW (ELLER STYRT SIDEBRUDD PR LAGERLOKASJON/DATO)
     C     *IN66         IFEQ      *ON
N01  C     WSSVGS        ANDNE     'J'
     C                   WRITE     HEAD                                 66
     C     WSDET         IFNE      'J'
     C                   WRITE     HEASTR                                        STREK
     C                   END
     C                   END
      * DOBBELTTREKT STREK FØR NESTE POST KUN NÅR SUBDETALJER SKRIVES
N01  C                   IF        WSSVGS = 'J'
N01  C                   EXSR      SRSVGS01
N01  C                   ELSE
     C     WSDET         IFNE      'J'
     C  N60              WRITE     DETALJ                               66
     C   60              WRITE     DETALJU                              66
     C                   ELSE
     C                   WRITE     DETSTR                                        STREK
     C  N60              WRITE     DETALJ                               66
     C   60              WRITE     DETALJU                              66
     C                   EXSR      SUBDET
     C                   END
      *
     C     WSBATC        IFEQ      'J'
     C     VASENR        COMP      *BLANKS                            4141
     C     VAULDT        COMP      0                                  4242
     C   41
     COR 42              WRITE     DETALJ2
     C                   END
N01  C                   END
      *
     C                   MOVE      NYBRUD        GMBRUD           25
     C                   MOVE      'N'           POST1             1
      * Akkum. bruddtotal
     C                   ADD       1             TOTPOS
     C                   ADD       VASALD        TOTSAL
     C                   ADD       VAOANT        TOTUPL
     C                   ADD       VASAE3        TOTPAL
     C                   ADD       VASAE2        TOTKRT
     C                   ADD       VASAE1        TOTESK
     C                   ADD       VASAE0        TOTSTK
     C                   ADD       VAVKTS        TOTKTS
     C                   ADD       VAKUBS        TOTUBS
      * Akkum. grand total
     C                   ADD       1             GTOPOS
     C                   ADD       VASALD        GTOSAL
     C                   ADD       VAOANT        GTOUPL
     C                   ADD       VASAE3        GTOPAL
     C                   ADD       VASAE2        GTOKRT
     C                   ADD       VASAE1        GTOESK
     C                   ADD       VASAE0        GTOSTK
     C                   ADD       VAVKTS        GTOKTS
     C                   ADD       VAKUBS        GTOUBS
      * Akkum. total denne art/denne lokasjon
     C                   ADD       VAOANT        TDTUPL
     C                   ADD       VASALD        TDTSAL
     C                   ADD       VASAE3        TDTPAL
     C                   ADD       VASAE2        TDTKRT
     C                   ADD       VASAE1        TDTESK
     C                   ADD       VASAE0        TDTSTK
     C                   ADD       VAVKTS        TDTKTS
     C                   ADD       VAKUBS        TDTUBS
     C                   ADD       1             XANTAL
     C                   GOTO      NYLIN
     C                   ENDIF                                                  E1
      *
     C     SLUTT         TAG
      * SKRIV TOTALEN TILHØRENDE SIST LESTE BRUDD (MINST 1 POST LEST)
      * SAMT GRAND TOTAL
     C     POST1         IFEQ      'N'
N01  C     WSSVGS        ANDNE     'J'
     C   52XANTAL        IFGT      1
     C  N60              WRITE     DTOTAL
     C   60              WRITE     DTOTALU
     C                   END
     C  N60              WRITE     TOTAL
     C   60              WRITE     TOTALU
     C  N60              WRITE     GTOTAL
     C   60              WRITE     GTOTALU
     C                   ENDIF
      *
      *
     C                   SETON                                        LR
      *
      *
      *
      ******************************************************
     C     SUBDET        BEGSR
      ******************************************************
     C     DETKEY        KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    VAMN
      *
     C     DETKEY        SETLL     LLAGJD01
     C     DETKEY        READE     LLAGJD01                               33
      * DETALJOVERSKRIFT
     C  N33              WRITE     HEADET
     C     *IN33         DOWEQ     '0'
     c     XDT           IFNE      99999999
     C     VDLDAT        cabge     XDT           NxtsubD
     C                   END
      * SIDEBRUDD...
     C     *IN66         IFEQ      *ON
     C                   WRITE     HEAD                                 66
     C                   WRITE     HEADET                               66
     C                   END
     C                   WRITE     DETDET
     C     NxtSubD       Tag
     C     DETKEY        READE     LLAGJD01                               33
     C                   END
     C                   ENDSR
N01   ******************************************************
N01  C     SRSVGS01      BEGSR
N01   ******************************************************
N01  C     LLMRFK        CHAIN     LLMRF                              33
N01  C                   CLEAR                   LL025FR
N01  C                   EVAL      L25GN   = MRGN
N01  C                   EVAL      L25MRN  = MRTRNR
N01  C                   EVAL      L25DTI  = VADATO
N01  C                   EVAL      L25NTI  = VAOANT
N01  C                   EVAL      L25VKI  = VAOANT * VAVKT0
N01  C                   EVAL      L25VB   = VAVARE
N01  C                   EVAL      L25NA   = KNAVN
N01  C                   EVAL      L25LC   = VALLOK
N01  C                   EVAL      L25SALD = VAOANT
N01   *
N01  C     VAMN          SETLL     LLSERL03
N01  C     VAMN          READE     LLSERL03                               33
N01  C  N33              EVAL      L25SNR1 = SNSN
N01  C  N33VAMN          READE     LLSERL03                               33
N01  C                   Z-ADD     1             XANTSN            3 0
N01  C                   DOW       *IN33 = *OFF
N01  C                   IF        SNST <> 'P'
N01  C                   SELECT
N01  C                   WHEN      XANTSN < 50
N01  C                   EVAL      L25SNR1 = %TRIM(L25SNR1) + ',' + %TRIM(SNSN)
N01  C                   WHEN      XANTSN = 50
N01  C                   EVAL      L25SNR2 = SNSN
N01  C                   WHEN      XANTSN < 100
N01  C                   EVAL      L25SNR2 = %TRIM(L25SNR2) + ',' + %TRIM(SNSN)
N01  C                   WHEN      XANTSN = 150
N01  C                   EVAL      L25SNR2 = SNSN
N01  C                   WHEN      XANTSN < 150
N01  C                   EVAL      L25SNR2 = %TRIM(L25SNR2) + ',' + %TRIM(SNSN)
N01  C                   ENDSL
N01  C                   ADD       1             XANTSN
N01  C                   END
N01  C     VAMN          READE     LLSERL03                               33
N01  C                   ENDDO
N01  C                   WRITE     LL025FR
N01   *
N01  C                   EVAL      L25GN   = *BLANKS
N01  C                   EVAL      L25MRN  = *BLANKS
N01  C                   EVAL      L25DTI  = 0
N01  C                   EVAL      L25NTI  = 0
N01  C                   EVAL      L25VKI  = 0
N01  C                   EVAL      L25VB   = *BLANKS
N01  C                   EVAL      L25NA   = *BLANKS
N01  C                   EVAL      L25SNR1 = *BLANKS
N01  C                   EVAL      L25SNR2 = *BLANKS
N01  C                   EVAL      L25SNR3 = *BLANKS
N01   *
N01  C     DETKEY        SETLL     LLAGJD01
N01  C     DETKEY        READE     LLAGJD01                               33
N01  C                   DOW       *IN33 = *OFF
N01  C                   EVAL      L25DTU   = VDLDAT
N01  C     SVIHK         CHAIN     SVIH                               33
N01  C                   IF        *IN33
N01  C     SVIHK         CHAIN     SVEH                               33
N01  C     SVIHK         CHAIN     SVEV                               33
N01  C                   END
N01  C                   IF        *IN33
N01  C     VDMNL         CHAIN     LLHEAD                             33
N01  C  N33              EVAL      L25PR    = LHORDR
N01  C  N33              EVAL      L25TLL   = LHFLN
N01  C                   ELSE
N01  C                   EVAL      L25PR    = SVIH_EUP1
N01  C                   EVAL      L25TLL   = SVIH_TUID
N01  C                   END
N01  C                   EVAL      L25NTU   = VDLANT
N01  C                   EVAL      L25VKU   = VDLANT * VAVKT0
N01  C                   EVAL      L25SALD  = L25SALD - VDLANT
N01   *
N01  C     LLSERL03K     SETLL     LLSERL03
N01  C     LLSERL03K     READE     LLSERL03                               33
N01  C  N33              EVAL      L25SNR1 = SNSN
N01  C  N33LLSERL03K     READE     LLSERL03                               33
N01  C                   Z-ADD     1             XANTSN            3 0
N01  C                   DOW       *IN33 = *OFF
N01  C                   SELECT
N01  C                   WHEN      XANTSN < 50
N01  C                   EVAL      L25SNR1 = %TRIM(L25SNR1) + ',' + %TRIM(SNSN)
N01  C                   WHEN      XANTSN = 50
N01  C                   EVAL      L25SNR2 = SNSN
N01  C                   WHEN      XANTSN < 100
N01  C                   EVAL      L25SNR2 = %TRIM(L25SNR2) + ',' + %TRIM(SNSN)
N01  C                   WHEN      XANTSN = 150
N01  C                   EVAL      L25SNR2 = SNSN
N01  C                   WHEN      XANTSN < 150
N01  C                   EVAL      L25SNR2 = %TRIM(L25SNR2) + ',' + %TRIM(SNSN)
N01  C                   ENDSL
N01  C                   ADD       1             XANTSN
N01  C     LLSERL03K     READE     LLSERL03                               33
N01  C                   ENDDO
N01  C                   WRITE     LL025FR
N01   *
N01  C     DETKEY        READE     LLAGJD01                               33
N01  C                   ENDDO
N01   *
N01  C                   ENDSR
N01   *
