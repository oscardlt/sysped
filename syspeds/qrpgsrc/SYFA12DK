      * ***************************************************************
      *    DETTE PROGRAM SKRIVER ETIKETT PÅ ELTRON SKRIVER
      * ***************************************************************
      *
      * COMMAND Q=LENGDE / MELLOMROM M.M
      * Q p1,p2 (p1=lengde i dots, p2=mellomrom i dots )
      *
      * MÅLES I "DOTS",  PÅ ORION: 8 DOT PR MM
      * TOTALLENGDE MELLOM PERFORERING = 195MM  = 1560 DOTS
      * LENGDE PÅ DEN ENKELT LAPP      = 192MM  = 1536 DOTS
      * LENGDE PÅ MELLOMROM(GAP)       =   3MM  =   24 DOTS
      * (LENGDE FRA PERF.TIL START LAPP= 1,5MM  =   12 DOTS)
      * Kommandoer for tekst begynner på A, deretter 8 parametre :
      * A=ASCII-TEKST, PARAMETRE...
      *   P1=KOLONNE,P2=LINJE,P3=ROTASJON,P4=FONT,P5=HORIS MULTIPL,
      *   P6=VERTIKAL MULTIPL, P7=N=NORMAL/R=REVERSE,P8=DATA (i "   ")
      * Kommandoer for barcode begynner på B...
      * SE EPL2en.pdf side 244   (.f.eks )
      *       http://web.chd.lv/download/EPL2en.pdf
      **************************************************************
     FDOKUF     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FDKPOSTF   UF   E           K DISK
     FDOKUFM    UF A E           K DISK
     FKODFRI    IF   E           K DISK
     FFRISOK    IF A E           K DISK
     FSYLA03P   O    F   80        PRINTER OFLIND(*INOF)
     D                 DS
     D  DFAVD                  1      4  0
     D  DFOPD                  5     11  0
     D  DFFBNR                12     14  0
     D  DFAVDA                 1      4
     D  DFOPDA                 5     11
     D  DFFBNA                12     14
     D                 DS
     D  DF1004                 1     35
     D  DF100401               1     17
     D                 DS
     D  FSSOK                  1     35
     D  FSSOK01                1     10
     D  FSSOK02               12     15
     D  FSSOK03               17     33
     D                 DS
     D  XBK                    1     13
     D  XBK1                   1     10
     D  XBK2                  11     11
     D  XBK12                  1     11
     D  XBK3                  12     13
      * Input Parameter
     C     *ENTRY        PLIST
     C                   PARM                    WFIRM             2                         ID
     C                   PARM                    WMLAYO            2                         ID
     C                   PARM                    AVD               4 0                       ID
     C                   PARM                    OPD               7 0                       ID
     C                   PARM                    FBN               3 0                       ID
     C                   PARM                    uukopi            1
      *
     C                   EXSR      SRINIT
      *
     C     *IN90         IFEQ      *ON
     C                   GOTO      ENDPGM
     C                   ENDIF
      * LOOP FOR NO OF COPIES
     C                   DO        DFNTLA        WCOPN             4 0
      * Send field data to printer
     C                   EXSR      SR100
      * END LOOP
     C                   ENDDO
      * End Program
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *_____________________________________________________
      * Send all data fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR100         BEGSR
      *
      ******************************************************
      * Her begynner selve produksjon av utdata til merkelapp
      ******************************************************
      * Kommandoer for tekst begynner på A, deretter 8 parametre :
      * Prm v/A: 1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
      *
      * Ved lapp 2,3,4, endres kun variable data ( xx AV YY og kol-ID)
     C     *IN50         CABEQ     '1'           VARI1
      *
      * TRANSPORTØR OG PRODUKSJONSKODE
     C                   eval      DATA = 'A18,40,0,4,1,2,N,"Post Danmark A/S"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A400,50,0,4,2,2,N,"Erhvervspakker"'
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   eval      DATA = 'LE10,120,666,6'
     C                   EXCEPT    SYLA01
      *
      * FRA (AVSENDER)
      * Ledertekst
     C                   eval      DATA = 'A18,160,0,2,1,1,N,"a) Fra / From"'
     C                   EXCEPT    SYLA01
      * Avs.navn
     C                   eval      DATA = 'A40,190,0,1,2,2,N,"'+
     C                             %trim(DFNAVS)+
     C                             '"'
     C                   EXCEPT    SYLA01
      * Avs.adr1
     C                   eval      DATA = 'A40,220,0,1,2,2,N,"'+
     C                             %trim(DFAD1S)+
     C                             '"'
     C                   EXCEPT    SYLA01
      * Avs.Postnr /sted
     C                   MOVE      DFPNLS        PNRALF            4
     C                   eval      DATA = 'A40,250,0,1,2,2,N,"'+
     C                             %trim(PNRALF)+' '+
     C                             %trim(DFAD3S)+
     C                             '"'
     C                   EXCEPT    SYLA01
      * Strek over avsender
     C                   eval      DATA = 'LS18,190,3,500,270'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LS18,270,3,500,190'
     C                   EXCEPT    SYLA01
      * Ledertekst
     C                   eval      DATA = 'A18,280,0,2,1,1,N,"b) Tlf. / Phone"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A400,280,0,2,1,1,N,'+
     C                             '"c) Afs-dato / Desp.date"'
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   eval      DATA = 'LE10,320,666,6'
     C                   EXCEPT    SYLA01
      *
      * Til (MOTTAKER)
      * Ledetekst
     C                   eval      DATA = 'A80,360,0,2,1,1,N,"Til/To"'
     C                   EXCEPT    SYLA01
      * Ramme
     C                   eval      DATA = 'LO80,390,12,4'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO80,390,4,12'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO680,390,12,4'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO690,392,4,12'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO80,530,12,4'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO80,518,4,12'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO680,530,12,4'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'LO692,518,4,12'
     C                   EXCEPT    SYLA01
      *
      * Data
     C                   if        DFNAVL <> *BLANKS
     C                   eval      DATA = 'A40,410,0,2,1,1,N,"a)"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A90,410,0,2,2,2,N,"'+
     C                             %trim(DFNAVL)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A40,450,0,2,1,1,N,"b)"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A90,450,0,2,2,2,N,"'+
     C                             %trim(DFAD1L)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A40,490,0,2,1,1,N,"c)"'
     C                   EXCEPT    SYLA01
     C                   MOVEL     DFPOUL        PNRALF
     C                   eval      DATA = 'A90,490,0,2,2,2,N,"'+
     C                             %trim(PNRALF)+' '+
     C                             %trim(DFADUL)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   ELSE
     C                   eval      DATA = 'A40,410,0,2,1,1,N,"a)"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A90,410,0,2,2,2,N,"'+
     C                             %trim(DFNAVM)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A40,450,0,2,1,1,N,"b)"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A90,450,0,2,2,2,N,"'+
     C                             %trim(DFAD1M)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A40,490,0,2,1,1,N,"c)"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A90,490,0,2,2,2,N,"'+
     C                             %trim(DFAD3M)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   end
      *
      * STREK
     C                   eval      DATA = 'LE10,560,666,6'
     C                   EXCEPT    SYLA01
      *
     C     VARI1         TAG
      *
      * TRANSPORT instruks
      * Ledetekst
     C                   eval      DATA = 'A80,640,0,2,1,1,N,'+
     C                             '"Transportinst."'
     C                   EXCEPT    SYLA01
      * Postoppkrav
     C                   eval      DATA = 'A40,680,0,2,1,1,N,"a)"'
     C                   EXCEPT    SYLA01
      * Referansenr
     C                   eval      DATA = 'A40,720,0,2,1,1,N,'+
     C                             '"b) Reference INr"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A300,720,0,2,1,2,N,"'+
     C                             %trim(DFAVDA)+'/'+
     C                             %trim(DFOPDA)+'/'+
     C                             %trim(DFFBNA)+'/'+
     C                             %trim(DFSG)+
     C                             '"'
     C                   EXCEPT    SYLA01
      * ANTALL ... AV...
      * Ledetekst
     C                   eval      DATA = 'A40,760,0,2,1,1,N,'+
     C                             '"c) Kolli:"'
     C                   EXCEPT    SYLA01
      * Data ( xx AV yy )
     C                   eval      DATA = 'A300,760,0,3,2,2,N,"'+
     C                             %trim(%editc(WCOPN:'Z'))+' / '+
     C                             %trim(%editc(DFNTLA:'Z'))+
     C                             '"'
     C                   EXCEPT    SYLA01
      * Vekt
      * Ledetekst
     C                   eval      DATA = 'A40,800,0,2,1,1,N,'+
     C                             '"c) Weight:"'
     C                   EXCEPT    SYLA01
      * Data
     C                   eval      DATA = 'A300,800,0,3,2,2,N,"'+
     C                             %trim(%editc(DFVKT:'Z')) + ' kg ' + '"'
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   eval      DATA = 'LE10,850,666,6'
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   eval      DATA = 'LE10,1070,666,6'
     C                   EXCEPT    SYLA01
      *
      * FRAKTBREVSNR
      * Ledetekst
     C*                  eval      DATA = 'A18,1120,0,2,1,1,N,"'+
     C*                            'Sendings-ID/Consignment'+
     C*                            '"'
     C*                  EXCEPT    SYLA01
     C                   MOVEL     FSSOK01       XBK12
     C                   MOVE      'DK'          XBK3
      * Data
     C                   eval      DATA = 'B18,1140,0,1E,4,8,150,B,"'+
     C                             %trim(XBK)+
     C                             '"'
     C                   EXCEPT    SYLA01
      * Print 1 eksemplar
     C                   eval      DATA = 'P1'
     C                   EXCEPT    SYLA01
      *
     C                   MOVE      *ON           *IN50                           FØRSTE FERD
      *
     C                   ENDSR
      *_____________________________________________________
      * Initialise subroutine, set character set
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C                   MOVE      *OFF          *IN50
      *
     C                   Z-ADD     0             ØX                3 0          Index for Ø
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   MOVE      'N'           ØADEC             1            Decimal Indicator
     C                   Z-ADD     0             ÆX                3 0
     C                   Z-ADD     0             X                 1 0
     C                   Z-ADD     0             Y                 1 0
      *
     C     DOKKEY        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   MOVE      'F'           DFRI
     C     HEAKEY        KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C     FRISOKK       KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FSKODE
     C                   MOVE      'DKP'         FSKODE
      * HENT FRAKTBREV
     C     DOKKEY        CHAIN     DOKUF                              90
     C     HEAKEY        CHAIN     HEADF                              33
     C     FRISOKK       SETLL     FRISOK
      *
      * HENT DK POST NR
     C     FRISOKK       READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        FSSOK03 = DF100401
     C                   LEAVE
     C                   END
     C     FRISOKK       READE     FRISOK                                 33
     C                   ENDDO
      *
      * FINNES IKKE --> HENT FRA REGISTER
     C                   IF        *IN33
     C     WFIRM         CHAIN     DKPOSTF                            33
     C                   IF        ((DKPNNR > DKPMNR) AND (DKPNSN > 0))
     C                   MOVE      DKPNSN        DKPNNR
     C                   MOVE      DKPNSM        DKPMNR
     C                   MOVE      *ZEROS        DKPNSN
     C                   MOVE      *ZEROS        DKPNSM
     C                   END
     C                   IF        DKPNNR > DKPMNR
     C                   MOVE      *ZEROS        FSSOK
     C                   ELSE
     C                   CLEAR                   FRISOKR
     C                   MOVE      AVD           FSAVD
     C                   MOVE      OPD           FSOPD
     C                   MOVE      'DKP'         FSKODE
     C                   MOVEL     DKPNNR        FSSOK01
      * BEREGN KONTROLLSIFFER
     C                   MOVE      FSSOK01       XBK1
     C                   CALL      'MOD11PDK'
     C                   PARM                    XBK1
     C                   PARM                    XNR01             1 0
     C                   MOVE      XNR01         FSSOK01
      *
     C                   MOVE      'IFB:'        FSSOK02
     C                   MOVE      DF100401      FSSOK03
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C                   WRITE     FRISOKR
     C                   ADD       1             DKPNNR
     C                   END
     C                   UPDATE    DKPOSTFR
     C                   END
      *
     C* Initiering/ Printparam / label size
     C                   MOVE      *BLANKS       DATA             80
     C                   EXCEPT    SYLA01
     C                   MOVEL     'N       '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'D7      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'S2      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'Q1536,24'    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'q760    '    DATA
     C                   EXCEPT    SYLA01
      *
     C                   ENDSR
      *
     OSYLA03P   E            SYLA01
     O                       DATA                80
      *
      *
