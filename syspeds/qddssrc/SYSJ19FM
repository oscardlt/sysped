     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  5VALUES(' ' '1' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            CLKD           3A  O  5 14
     A            CLFT          30A  O  5 23
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ19'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19MSGCON(40 DIV3106 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3107 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A  27                                  CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              1  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 11MSGCON(60 DIV3108 SYMSGF)
     A                                      DSPATR(HI)
     A            WSKD           3A  B  2 14DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSFT          30A  B  2 23DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(24 DIV3109 SYMSGF)
     A            W2FT          30A  I  2 27DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 11MSGCON(60 DIV3108 SYMSGF)
     A                                      DSPATR(HI)
     A            CLKD           3A  O  2 14DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            CLFT          30A  O  2 23DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A  27                                  CF12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ19'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3106 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3107 SYMSGF)
     A                                      DSPATR(HI)
     A N27                             23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                             23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
