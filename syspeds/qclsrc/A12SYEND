/* *********************************************************************   */
/*       * ÅPNE SAD I PDF-FORMAT                                           */

             PGM        PARM(&AVD &OPD &KODE &STMF &BANE &VISE +
                          &FINST &FINSD)
             DCL        VAR(&FINST) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FINSD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FAXTYP) TYPE(*CHAR) LEN(1) VALUE('A')
             DCL        VAR(&VISE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OPD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(70)
             DCL        VAR(&BANE) TYPE(*CHAR) LEN(50)
             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)
             CHGVAR     VAR(&FINSD) VALUE('J')
             CHGVAR     VAR(&FINST) VALUE('J')
/*           SKJEKK OM PDF FINNES FRA FØR  */
             OVRPRTF    FILE(QSYSPRT) OUTQ(OUTMAIL)
             DSPLNK     OBJ(&BANE) OUTPUT(*PRINT) OBJTYPE(*STMF) +
                          DETAIL(*NAME) DSPOPT(*USER)
             MONMSG     MSGID(CPFA0A9) EXEC(GOTO CMDLBL(VIDERE))
             DLTSPLF    FILE(QSYSPRT) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             IF         COND(&VISE = 'J') THEN(DO)
             CALL       PGM(PDF001) PARM(&STMF)
             ENDDO
             RETURN
VIDERE:      OVRPRTF    FILE(TOLLDEK)  DEVTYPE(*AFPDS) +
                          PAGESIZE(72 83) LPI(6) CPI(10) +
                          FRONTMGN(0.14 0) OVRFLW(72) +
                          FRONTOVL(FSAD38) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD)
             OVRPRTF    FILE(TOLLDEK2) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          83) LPI(6) CPI(10) OVRFLW(72) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) HOLD(*YES)
             OVRPRTF    FILE(DATALIST) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          83) LPI(6) CPI(10) OVRFLW(72) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) HOLD(*YES)
             OVRPRTF    FILE(REGKORT) DEVTYPE(*SCS) PAGESIZE(48 132) +
                          LPI(6) CPI(10) OVRFLW(48) OUTQ(OUTMAIL) +
                          FORMTYPE(*STD)
             OVRDBF     FILE(WFILEIMP) MBR(ARKIV)
             OVRDBF     FILE(WFILEEXP) MBR(ARKIV)
             IF         COND(&KODE = 'I') THEN(DO)
             CALL       PGM(SAD61P) PARM(&FAXTYP &AVD &OPD &PRT1 +
                          &PRT2 &PRT3)
             ENDDO
             IF         COND(&KODE = 'E') THEN(DO)
             CALL       PGM(SAD52P) PARM(&FAXTYP &AVD &OPD &PRT1 +
                          &PRT2 &PRT3)
             ENDDO
/*                                       */
             ADDLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)
             CVTSPLFPDF SPLF(TOLLDEK) TOPDF(&BANE) +
                          JOB(&QNBR/&QUSER/&QJOB)
             RMVLIBLE   LIB(MMAIL)
             IF         COND(&VISE = 'J') THEN(DO)
             CALL       PGM(PDF001) PARM(&STMF)
             ENDDO
             DLTSPLF    FILE(TOLLDEK) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
/*FINNES TILLEGGSARK*/
             RLSSPLF    FILE(TOLLDEK2) JOB(&QNBR/&QUSER/&QJOB)
             MONMSG     MSGID(CPF3309) EXEC(CHGVAR VAR(&FINST) +
                          VALUE('N'))
/*FINNES DATALISTE*/
             RLSSPLF    FILE(DATALIST) JOB(&QNBR/&QUSER/&QJOB)
             MONMSG     MSGID(CPF3309 CPF3340 CPF3304) EXEC(CHGVAR +
                          VAR(&FINSD) VALUE('N'))
 SLUTT:      ENDPGM
