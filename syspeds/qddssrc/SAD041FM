     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SADE001' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01                   CSRLOC(LINNBR POSNBR)
     A                                      CF12(12 'Forrige')
     A          H                           HLPDOC('SADE000' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A                                  1  3'SYSPED/SAD041'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  1 20'Registrering av tolldeklarasjon (E-
     A                                      KSPORT)'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  5 18'Avdeling:'
     A            UAVD           4  0B  5 34EDTCDE(4)
     A  95                                  DSPATR(PC)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  9 18'Signatur:'
     A            WSSG           3A  B  9 34DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 18'Tolldek.nr.'
     A            WSTDN          7Y 0B 10 34DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 42'Tast oppdragsnr.'
     A                                      COLOR(BLU)
     A  35N32                          11 18'Sjø-oppdrag'
     A  35N32     WSUR           1A  B 11 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'G')
     A  35N32                          11 42'G = Sjø-oppdrag'
     A                                      COLOR(BLU)
     A                                 23  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT M/UTSKR.')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'TIL VARELINJER')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF12(12 'Avbryt program og gå til i-
     A                                      nngangsbilde')
     A                                      CF13(13 'Oppdater reg.nr og konto p-
     A                                      å kunde-register')
     A                                      CF14(14 'KUNDEINFO')
     A                                      CF24(24 'BLA I F-TASTER')
     A                                      OVERLAY
     A          H                           HLPDOC('SADE001' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 049 01 056)
     A          H                           HLPDOC('SADE001A' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 069 01 070)
     A          H                           HLPDOC('SADE002' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 001 06 039)
     A          H                           HLPDOC('SADE003' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 040 02 080)
     A          H                           HLPDOC('SADE004' 'HELPNU' 'HLP400')
     A                                      HLPARA(07 001 13 039)
     A          H                           HLPDOC('SADE005' 'HELPNU' 'HLP400')
     A                                      HLPARA(03 040 07 080)
     A          H                           HLPDOC('SADE006' 'HELPNU' 'HLP400')
     A                                      HLPARA(08 040 10 080)
     A          H                           HLPDOC('SADE007' 'HELPNU' 'HLP400')
     A                                      HLPARA(10 040 13 080)
     A          H                           HLPDOC('SADE008' 'HELPNU' 'HLP400')
     A                                      HLPARA(14 001 14 080)
     A          H                           HLPDOC('SADE008' 'HELPNU' 'HLP400')
     A                                      HLPARA(15 001 17 050)
     A          H                           HLPDOC('SADE020' 'HELPNU' 'HLP400')
     A                                      HLPARA(15 051 17 080)
     A          H                           HLPDOC('SADE021' 'HELPNU' 'HLP400')
     A                                      HLPARA(18 001 21 037)
     A          H                           HLPDOC('SADE022' 'HELPNU' 'HLP400')
     A                                      HLPARA(18 038 21 080)
     A          H                           HLPDOC('SADE023' 'HELPNU' 'HLP400')
     A                                      HLPARA(22 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A                                  1 10'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A            WSDTY          2A  B  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A N35                                  DSPATR(ND)
     A N35                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSDK           2A  B  1 55TEXT('DEKLARASJONSKODE')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES('  ' 'T1' 'T2' 'T-' 'TR')
     A                                  1 59'4.L.liste'
     A            WSLSTL         2Y 0B  1 69TEXT('LASTELISTE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2  2'2.Avsender'
     A            WSKNK          8Y 0B  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A N62                                  COLOR(YLW)
     A  62                                  DSPATR(BL)
     A  62                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
     A            WSRG          11A  B  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 41'5.V.post'
     A            WSVPOS         3Y 0B  2 50TEXT('VAREPOST')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 54'6.Kolli'
     A            WSNTK          7Y 0B  2 62TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAK         30A  B  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A N62                                  COLOR(YLW)
     A  62                                  DSPATR(BL)
     A  62                                  COLOR(RED)
     A                                  3 41'Brt vekt'
     A            WSVKB          9Y 0B  3 50TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 61'Ref'
     A            UAVD           4Y 0O  3 65TEXT('AVDELING')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSTDN          7Y 0O  3 70TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SESG           3A  O  3 78TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSADK1        30A  B  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.land'
     A                                  4 56'NO'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            WSLKAT        17A  B  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            WSLKB          2A  B  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFTB         17A  B  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 40'18.Tr.ID avgang'
     A            WSLKM          2A  B  6 56TEXT('LK MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFTM         17A  B  6 59TEXT('F.T. MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            WSKNS          8Y 0B  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A N61                                  COLOR(YLW)
     A  61                                  DSPATR(BL)
     A  61                                  COLOR(RED)
     A                                  7 40'19.Container'
     A            WSKDC          1A  B  7 56TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNAS         30A  B  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A N61                                  COLOR(YLW)
     A  61                                  DSPATR(BL)
     A  61                                  COLOR(RED)
     A                                  8 40'21.Tr.ID grense'
     A            WSLKT          2A  B  8 56TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTRID        17A  B  8 59TEXT('Transport ID')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'25.Tr.måte'
     A            WSTRM          2Y 0B  9 56TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A            WSADS2        30A  B 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 44'Tilbakesendes til:'
     A            WSTS01        14A  B 10 63DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTS02        14A  B 11 63DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'   Varenr.. Brutto vekt Netto vekt-
     A                                       Mengde.'
     A                                      COLOR(WHT)
     A            WSTS03        14A  B 12 63DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVNTI         8A  B 13  5DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVNT          8S 0H
     A            WSVKTB         9Y 0B 13 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVKTN         9Y 0B 13 26DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNTM          7Y 0B 13 36DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTS04        14A  B 13 63DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2'31.'
     A            WSFT          17A  B 14  6DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           6Y 0B 14 24DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSEH           4A  B 14 31DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVT          15A  B 14 36DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTS05        14A  B 14 63DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'44.'
     A            WSTOPL        45A  B 15  6DSPATR(UL)
     A                                      COLOR(YLW)
     A  27                             15 53'Transitteringsnr.'
     A  27        WSCTN          5Y 0B 15 72DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16  1'50.Hovedansvarlig'
     A            WSKNA          8Y 0B 16 19TEXT('HOVEDANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 39'51.'
     A            WSSTG1         8A  B 16 43TEXT('GARANTITOLLSTED-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG1         2A  B 16 52TEXT('GARANTI-land-1')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSTG2         8A  B 16 56TEXT('GARANTITOLLSTED-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG2         2A  B 16 65TEXT('GARANTI-land-2')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSTG3         8A  B 16 69TEXT('GARANTITOLLSTED-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG3         2A  B 16 78TEXT('GARANTI-land-3')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNAA         30A  B 17  2TEXT('Navn ANSVARLIG')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSTG4         8A  B 17 43TEXT('GARANTITOLLSTED-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG4         2A  B 17 52TEXT('GARANTI-land-4')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSTG5         8A  B 17 56TEXT('GARANTITOLLSTED-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG5         2A  B 17 65TEXT('GARANTI-land-5')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSTG6         8A  B 17 69TEXT('GARANTITOLLSTED-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG6         2A  B 17 78TEXT('GARANTI-land-6')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADA1        30A  B 18  2TEXT('Adresse-1 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 39'52.Garanti'
     A            WSGFT1        30A  B 18 50TEXT('GARATI FRITEKST-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADA3        30A  B 19  2TEXT('Adresse-3 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGKD          1Y 0B 19 40TEXT('GARATIKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1 2 3 4 6 7 8 9)
     A            WSGFT2        30A  B 19 50TEXT('GARATI FRITEKST-2')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                 20  1'Rubr. D/J'
     A            WSDFT1        15A  B 20 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 39'53.Tollsted'
     A            WSTLKB         2A  B 20 51TEXT('LANDK TIL B.T.STED')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSTB        20A  B 20 54TEXT('BESTEMMELSESTOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDFT2        22A  B 21 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 35'Dato'
     A                                 21 51'Løpenr'
     A            WSTLL         10A  B 21 58TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 69'Eksp.'
     A            WSTLE          6A  B 21 75TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDTG         10A  B 21 40TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDDT          6Y 0B 22 11DSPATR(UL)
     A                                      COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 22 31'54.Sted'
     A            SEDST         15A  O 22 39TEXT('Deklarantens sted')
     A                                      COLOR(WHT)
     A                                 22 55'Tariffør'
     A            SETARF        16A  O 22 64TEXT('TARIFFØRSNAVN')
     A                                      COLOR(WHT)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
