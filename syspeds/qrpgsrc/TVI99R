     Fsysthlp   IF   E             DISK
     FTVI99D    CF   E             WORKSTN
     F                                     SFILE(TVI99DA:ZRECNO)
     FTVINE     UF A E           K DISK
     FTVINE1    IF   E           K DISK
     F                                     RENAME(TVINEF:TVINEE)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    E9705         W9705
     C     *LIKE         DEFINE    E9705         W97052
     C     *LIKE         DEFINE    *IN03         WIN03
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    W9705
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     TVI99DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C                   MOVE      *BLANKS       W9705
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     TVINE1                             9494
     C                   READ      TVINE1                                 94
     C     *IN94         CABEQ     '1'           SRB9
      *
     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO
      *
     C  N94              DO        13
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     TVI99DA
     C                   READ      TVINE1                                 94
     C  N94              END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI99DØ
     C                   WRITE     TVI99DC
     C                   EXFMT     TVI99DB
     c   01              do
     c                   movel     'tvi99r'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      SRB3
     c                   end
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     TVI99DA                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
      *
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      *____________
      * Blanke felt
      *""""""""""""
     C                   MOVE      '0'           *IN41
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      *BLANKS       E9705
     C                   MOVE      *BLANKS       E4440
      *
     C     SRBA1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI99DØ
     C                   EXFMT     TVI99DD
     C                   MOVEA     '0000'        *IN(30)
     c   01              do
     c                   movel     'tvi99r'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      SRBA1
     c                   end
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
      * Post må ikke finnes fra før.
     C                   MOVE      Å9705         W9705
     C     KEYA          CHAIN     TVINE                              53
     C     *IN53         IFEQ      '0'
     C                   MOVE      'SY00100'     ZMSGNO
     C                   MOVEA     '1110'        *IN(30)
     C                   GOTO      SRBA1
     C                   END
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   MOVE      Å9705         E9705
     C                   MOVE      Å4440         E4440
     C                   WRITE     TVINEF
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   WRITE     TVI99DA
      *
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Flytte felt
      *""""""""""""
     C                   MOVE      '1'           *IN41
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      E9705         Å9705
     C                   MOVE      E4440         Å4440
      *
     C     SRBB1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI99DØ
     C                   EXFMT     TVI99DD
     C                   MOVEA     '000'         *IN(30)
     c   01              do
     c                   movel     'tvi99r'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      SRBB1
     c                   end
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
     C   30              GOTO      SRBB1
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   MOVE      Å9705         W9705
     C     KEYA          CHAIN     TVINE                              53
     C                   MOVE      Å4440         E4440
     C                   UPDATE    TVINEF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    TVI99DA
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   MOVE      E9705         W9705
     C     KEYA          CHAIN     TVINE                              53
     C                   DELETE    TVINEF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       E9705
     C                   MOVE      *BLANKS       E4440
     C                   UPDATE    TVI99DA
     C                   MOVE      '1'           *IN95
      *
     C                   ENDSR
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
