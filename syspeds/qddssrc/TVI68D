     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/TVINF)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R TVI68DA                   SFL
     A            F0077     R        O  8  2
     A            F4815     R        O  8  6
     A            F0078A    R        O  8 11
     A            F0078B    R        O  9 11
     A            F0078C    R        O 10 11
     A            F0078D    R        O 11 11
     A            F0078E    R        O 12 11
     A          R TVI68DB                   SFLCTL(TVI68DA)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 21'Meldingslogg - Feilmeldinger - Tek-
     A                                      st'
     A                                      COLOR(WHT)
     A                                  1 69'TVI68D A'
     A                                      COLOR(BLU)
     A                                  3  2'Avdeling:'
     A            ZAVD           4S 0O  3 12
     A                                  3 23'Oppdragsnummer:'
     A            ZTDN           7S 0O  3 39
     A            Z0068A         8S 0O  4 12
     A            Z0068B         6S 0O  4 39
     A            Z0068C         2S 0O  4 64
     A                                  5  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  5 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  7  2'Ref'
     A                                      COLOR(WHT)
     A                                  7  6'Funk'
     A                                      COLOR(WHT)
     A                                  7 11'Tekst'
     A                                      COLOR(WHT)
     A                                  4  2'Dato....:'
     A                                  4 23'Sekvensnummer :'
     A                                  4 48'Versjonsnummer:'
     A          R TVI68DC
     A                                      OVERLAY
     A                                 23  2'F3=Gå ut   F5=Forny   F12=Annuller'
     A                                      COLOR(BLU)
     A          R TVI68DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
