     FE004D     CF   E             WORKSTN
     F                                     SFILE(E004DB:ZRECNO)
     F                                     INFDS(FBAREA)
     FEXRSTC    UF A E           K DISK
     FEXRSTC2   IF   E           K DISK
     F                                     RENAME(EXRSTCR:EXRSTCR2)
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E             DISK
     FEQTA902   IF   E           K DISK
     FEQUA902   IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *_________________
      * DS for constants
      *"""""""""""""""""
     D                 DS
     D  WTXT01                 1     40    INZ('(No codes match the -
     D                                     subsetting criteria)')
      *//////////////
      *  Main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *  Move blanks to PF                                  MOVEA /
      *////////////////////////////////////////////////////////////
     C     MOVEA         BEGSR
     C                   MOVE      *BLANKS       EXKODE
     C                   MOVE      *BLANKS       EXTX
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move format to PF                                  MOVEB /
      *////////////////////////////////////////////////////////////
     C     MOVEB         BEGSR
     C                   MOVE      ÅXKODE        EXKODE
     C                   MOVE      ÅXTX          EXTX
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move PF to format                                  MOVEC /
      *////////////////////////////////////////////////////////////
     C     MOVEC         BEGSR
     C                   MOVE      EXKODE        ÅXKODE
     C                   MOVE      EXTX          ÅXTX
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move blanks to format                              MOVED /
      *////////////////////////////////////////////////////////////
     C     MOVED         BEGSR
     C                   MOVE      UXKNR         ÅXKNR
     C                   MOVE      *BLANKS       ÅXKODE
     C                   MOVE      *BLANKS       ÅXTX
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  INZ                                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    EXKNR         WXKNR
     C     *LIKE         DEFINE    EXKNR         UXKNR
     C     *LIKE         DEFINE    EXKODE        WXKODE
     C     *LIKE         DEFINE    EXKODE        UXKODE
     C     *LIKE         DEFINE    FIRMA         WFIRMA
     C                   MOVE      *ZEROS        WXKNR
      *_____________
      * Define key's
      *"""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WXTAG
     C                   KFLD                    WXKODI
     C                   KFLD                    WXKNR
     C     KEYB          KLIST
     C                   KFLD                    WXTAG
     C                   KFLD                    WXKODI
     C     KEYC          KLIST
     C                   KFLD                    WXTAG
     C                   KFLD                    EXKODI
     C                   KFLD                    EXKNR
     C                   KFLD                    EXKODE
     C     CUNDKEY1      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    EXKNR
     C     CUNDKEY2      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ÅXKNR
     C     EQUAKEY       KLIST
     C                   KFLD                    WXTAG
     C                   KFLD                    WXKODI
      *_______________________
      * Hent firmaopplysninger
      *""""""""""""""""""""""
     C                   READ      FIRM
      *________________________
      * Input/output parameters
      *""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WXTAG                          Input
     C                   PARM                    WXKODI                         Input
     C                   PARM                    UXKNR                          Input
     C                   PARM                    UXKODE                         Output
      *_____________________
      * Is this a ext.call ?
      *"""""""""""""""""""""
     C     WXTAG         IFEQ      *BLANKS
     C     WXKODI        ANDEQ     *BLANKS
     C                   EVAL      *IN81=*ON
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Calculate formats                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Select tag
      *"""""""""""
     C     SRB0          TAG
      *
     C     *IN81         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
     C                   ENDIF
      *
     C     WXTAG         CHAIN     EQTA902                            51
     C   51              MOVE      *BLANKS       WTAGTX
     C  N51              MOVEL     QTAGTX        WTAGTX
     C     EQUAKEY       CHAIN     EQUA902                            51
     C   51              MOVE      *BLANKS       WUALTX
     C  N51              MOVEL     QUALTX        WUALTX
     C     SRB1          TAG
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     E004DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C                   MOVE      UXKNR         WXKNR
     C                   MOVE      *BLANKS       WXKODE
     C                   MOVE      *BLANKS       WXKODE
      *_____________
      * Fill Subfile
      *"""""""""""""
     C     KEYA          SETLL     EXRSTC2
     C     UXKNR         IFEQ      *ZEROS
     C     KEYB          READE     EXRSTC2                                94
     C                   ELSE
     C     KEYA          READE     EXRSTC2                                94
     C                   ENDIF
     C     *IN94         IFEQ      '1'
     C                   MOVE      *ZEROS        EXKNR
     C                   MOVEL     *BLANKS       EXKODE
     C                   MOVEL     *BLANKS       EXTX28
     C                   MOVEL     WTXT01        KNAVN
     C                   MOVE      '1'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E004DB
     C                   GOTO      SRB3
     C                   ENDIF
      *
     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO
      *
     C  N94              DO        12
     C                   MOVE      '0'           *IN41
     C     CUNDKEY1      CHAIN     CUNDL01                            51
     C   51              EVAL      KNAVN=*BLANKS
     C                   MOVEL     EXTX          EXTX28
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E004DB
     C     UXKNR         IFEQ      *ZEROS
     C     KEYB          READE     EXRSTC2                                94
     C                   ELSE
     C     KEYA          READE     EXRSTC2                                94
     C                   ENDIF
     C  N94              END
      *______
      * Enter
      *""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E004DØ
     C                   WRITE     E004DD
     C                   EXFMT     E004DC
      *
      *________
      * F3=Exit
      *""""""""
     C   03              MOVE      *ZEROS        EXKNR
     C     *IN03         CABEQ     '1'           SRB9
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         IFEQ      '1'
     C     *IN81         CABEQ     '1'           SRB0
     C  N81              MOVE      *ZEROS        EXKNR
     C     *IN81         CABEQ     '0'           SRB9
     C                   ENDIF
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *___________
      * F5=Refresh
      *"""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *__________
      * F6=Create
      *""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBF
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_________
      * Option ?
      *"""""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN05
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     E004DB                                 52
     C     *IN52         IFEQ      '0'
     C     ZOP           CABEQ     '1'           SRB9                           Select
     C     ZOP           CASEQ     '2'           SRBB                           Change
     C     ZOP           CASEQ     '3'           SRBC                           Copy
     C     ZOP           CASEQ     '4'           SRBD                           Delete
     C     ZOP           CASEQ     '5'           SRBE                           Display
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9                           End
     C     *IN05         CABEQ     '1'           SRB1                           Refresh
     C     *IN05         CABEQ     '0'           SRB3                           Subfile
      *
     C     SRB9          TAG

     C                   IF        ZOP='1'
     C                   EVAL      UXKODE=EXKODE
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Select tag                                          SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C     SRBA1         TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E004DØ
     C                   EXFMT     E004DA
     C                   MOVEA     '000'         *IN(30)
      *________
      * F3=Exit
      *""""""""
     C     *IN03         CABEQ     '1'           SRBA9
      *__________
      * F4=Prompt
      *""""""""""
     C     *IN04         IFEQ      '1'
     C                   EXSR      SRBG
     C                   GOTO      SRBA1
     C                   ENDIF
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRBA9
      *______
      * Enter
      *""""""
     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Change                                              SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Move fields
      *""""""""""""
     C                   MOVE      EXKNR         WXKNR
     C     KEYC          CHAIN(N)  EXRSTC                             53
     C                   MOVE      EXKNR         ÅXKNR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
      *
     C     SRBB1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E004DØ
     C                   EXFMT     E004DE
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *___________
      * Test input
      *"""""""""""
     C   30              GOTO      SRBB1
      *__________
      * Update PF
      *""""""""""
     C     KEYC          CHAIN     EXRSTC                             53
     C                   EXSR      MOVEB
     C                   UPDATE    EXRSTCR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E004DB
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Copy                                                SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *____________
      * Copy fields
      *""""""""""""
     C                   MOVE      EXKNR         WXKNR
     C     KEYC          CHAIN(N)  EXRSTC                             53
     C                   MOVE      EXKNR         ÅXKNR
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   EXSR      MOVEA
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E004DØ
     C                   EXFMT     E004DE
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅXKNR         EXKNR
     C                   MOVE      WXTAG         EXTAG
     C                   MOVE      WXKODI        EXKODI
     C                   EXSR      MOVEB
     C                   WRITE     EXRSTCR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     E004DB
      *
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Delete                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Delete record
      *""""""""""""""
     C                   MOVE      EXKNR         WXKNR
     C     KEYC          CHAIN     EXRSTC                             53
     C                   DELETE    EXRSTCR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E004DB
     C                   MOVE      '1'           *IN05
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Display                                             SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      EXKNR         WXKNR
     C     KEYC          CHAIN(N)  EXRSTC                             53
     C                   MOVE      EXKNR         ÅXKNR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E004DØ
     C                   EXFMT     E004DE
     C                   MOVEA     '000'         *IN(30)
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Create                                              SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEA
     C                   EXSR      MOVED
      *
     C     *IN03         DOWEQ     '0'
     C     *IN12         ANDEQ     '0'
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E004DØ
     C                   EXFMT     E004DE
     C                   MOVEA     '0000'        *IN(30)
      *__________
      * F4=Prompt
      *""""""""""
     C                   IF        *IN04=*ON
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    ÅXKNR
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    UKNAVN           30
     C                   ENDIF
      *______________
      * End or Cancel
      *""""""""""""""
     C     *IN03         IFEQ      '0'
     C     *IN04         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *________________
      * Test kundenumer
      *""""""""""""""""
     C     CUNDKEY2      CHAIN     CUNDL01                            31
     C                   IF        *IN31
     C                   EVAL      *IN30=*ON
     C                   MOVE      'CBA0050'     ZMSGNO
     C                   ENDIF
     C                   IF        *IN30=*OFF
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅXKNR         EXKNR
     C                   MOVE      WXTAG         EXTAG
     C                   MOVE      WXKODI        EXKODI
     C                   EXSR      MOVEB
     C                   WRITE     EXRSTCR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     E004DB

     C                   EXSR      MOVED

     C                   ENDIF
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Søk                                                 SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   SELECT
     C     LINNBR        WHENEQ    5
     C                   CALL      'E002R'
     C                   PARM                    WXTAG
     C     LINNBR        WHENEQ    6
     C                   CALL      'E001R'
     C                   PARM                    WXTAG
     C                   PARM                    WXKODI
     C                   ENDSL
     C                   ENDSR
