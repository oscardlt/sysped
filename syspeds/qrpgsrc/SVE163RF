****Ç***************************************************************
     Ç* RETTINGER I DETTE PROGRAM:
PTFnÇ:*Sek Sig Vers  Beskrivelse...........................
""""Ç"*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXÇ* 01 CB  PTF10 Skriv rett til IFS  mye endret, ikke merket
11XXXÇ* 02 CB  PTF11 Feil i USC
     h option( *srcstmt ) dftactgrp( *no ) bnddir( 'QC2LE' )
     fedis      uf   e           k disk
     fedimc     uf   e           k disk
     fsveh      uf   e           k disk
     fsvtfi     if   e             disk
     fedim6     if   e           k disk    prefix(x_)
     f                                     rename(edimr:edimr6)
     fsvehh     o  a e             disk
     Ç*____________________
     Ç* DS for input string
     Ç*""""""""""""""""""""
     d                 ds
     d  a                      1  30000
     d                                     dim(30000)                           Input string
     d  ads                    1  30000
     Ç*_____________________
     Ç* DS for output string
     Ç*"""""""""""""""""""""
     d                 ds
     d  b                      1  30000
     d                                     dim(30000)                           Output string
     d  bds                    1  30000
     Ç*________________________________________
     Ç* DS for Â venstrestille numeriske felter
     Ç*""""""""""""""""""""""""""""""""""""""""
     d                 ds
     d ÿn152                         15  2
     d  ÿa152h                       13    overlay(ÿn152)
     d   ÿa152x                       9    overlay(ÿa152h:5)
     d  ÿa152d                        2    overlay(ÿn152:14)
     d                 ds
     d  ÿa12                   1     12
     d  ÿ12                    1     12
     d                                     dim(12)
     Ç*__________
     Ç* Variabler
     Ç*""""""""""
     d file_o          s               *
     d string          s            512a
     d rc              s             10i 0
     d data            s          30000a   varying
     d codepage        s              5a   varying
     Ç*___________________________
     Ç* IFS Stream file funksjoner
     Ç*"""""""""""""""""""""""""""
     dopen             pr              *   extproc( '_C_IFS_fopen' )
     d                                 *   value options( *string )
     d                                 *   value options( *string )

     dfputs            pr            10i 0 extproc( '_C_IFS_fputs' )
     d                                 *   value options( *string )
     d                                 *   value

     dclose            pr            10i 0 extproc( '_C_IFS_fclose' )
     d                                 *   value
     d runcmd          pr                  extpgm('QCMDEXC')
     d  cmd                         200a   const
     d  len                          15p 5 const
     Ç*   SYEDI data area
     d edidta          ds           256
     d uelibf                171    180
     Ç*  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
     Ç*  Prototype for 'SVE022RN'
     d sve022rn        pr                  extpgm('SVE022RN')
     d sveh_syav_                          like(sveh_syav)
     d sveh_syop_                          like(sveh_syop)
     d udata_                              like(udata)
     Ç*  Prototype for 'SVG004R'
     d svg004r         pr                  extpgm('SVG004R')
     d ufili_                              like(ufili)
     d ufilo_                              like(ufilo)
     d uchksum_                            like(uchksum)
     Ç*  Prototype for 'SVG005R'
     d svg005r         pr                  extpgm('SVG005R')
     d ufili_                              like(ufili)
     d ufilo_                              like(ufilo)
     d ukrypt_                             like(ukrypt)
     Ç*----------------------------------------------------------------------
     Ç* Stand Alone Fields - TOP
     Ç*----------------------------------------------------------------------
     d ÿan             s             12
     d ÿx              s              2  0
     d ads2            s          30000
     d ax              s              2  0
     d axh             s              5  0
     d axv             s              5  0
     d bx              s              5  0
     d len             s              5  0
     d lib             s             10
     d table           s             10
     d uchksum         s            128
     d udata           s          30000
     d ufili           s            128
     d ufilo           s            128
     d ukrypt          s            512
     d wapstr          s              1
     d wavd            s              4
     d wchar           s              1
     d wchart          s              1
     d wcmd            s            300
     d wdir            s              1
     d wdt             s              8
     d wfa             s            128
     d wfb             s            128
     d wfc             s            128
     d wfd             s            128
     d wfilnam         s            256
     d wnetw           s              1
     d wopd            s              7
     d wsn             s                   like(ssn)
     d wst             s                   like(mst)
     d wtm             s              6
     d wveh_syav       s                   like(sveh_syav)
     d wveh_syop       s                   like(sveh_syop)
     d wven            s                   like(mven)
     d w0036           s                   like(s0036)
     d w0062           s             11
     d w0074           s              7  0
     d xtar            s              3
     Ç*----------------------------------------------------------------------
     Ç* Stand Alone Fields - BOTTOM
     Ç*----------------------------------------------------------------------
     Ç*//////////////
     Ç*  Hodelogikk /
     Ç*//////////////
      /free
       exsr sra;
       exsr srb;
       exsr src;
       exsr aut;
       exsr unz;
       exsr catifs;
       *inlr = *on;
       //////////////////////////////////////////////////////////////
       //  Initiering                                           SRA /
       //////////////////////////////////////////////////////////////
       begsr sra;
         //___________________
         // Definiere Workfelt
         //"""""""""""""""""""

         ÿx = 0;
         ÿan = *blanks;
         w0074 = 0;
         xtar = *blanks;
         len = 80;
         %subst(table:1:7) = 'SVGNOSE';
         %subst(lib:1:5) = '*LIBL';
         //__________________
         // Definiere N¯kkler
         //""""""""""""""""""
      /end-free
     c     keya          klist
     c                   kfld                    wsn
     c     keyb          klist
     c                   kfld                    ssr
     c                   kfld                    wst
     c                   kfld                    wven
     c                   kfld                    s0004
     c                   kfld                    s0010
     c                   kfld                    s0035
     c     keyd          klist
     c                   kfld                    wveh_syav
     c                   kfld                    wveh_syop
     Ç*_____________
     Ç* Init av felt
     Ç*"""""""""""""
     Ç* Hente dagens dato og klokkeslett.
      /free
         syge15c ( wdt : wtm );
         wst = '+';
         wven = *blanks;
         // Workfields for Array's
         ax = 0;                                                                // Index for A
         bx = 1;                                                                // Index for B
         axv = 0;                                                               // Leftindx A
         axh = 0;                                                               // Rightindx A
         wdir = 'R';                                                            // Search direction
         wchar = *blanks;                                                       // Test character
         wchart = 'Y';                                                          // Test character
         // Workfields for separation character  '
      /end-free
     c                   biton     '123457'      wapstr
     c                   bitoff    '06'          wapstr
     Ç*_________________
     Ç* Input parametrer
     Ç*"""""""""""""""""
     c     *entry        plist
     c     wsn           parm                    wsn
     c                   parm                    wnetw
     Ç*_______________________________
     Ç* Hente firmarecord
     Ç*"""""""""""""""""""""""""""""""
      /free
         read svtfi;
         *in33 = %eof;
         // ≈pne dataarea
      /end-free
     c     *dtaara       define                  edidta
      /free
         in edidta;
       endsr;
       //////////////////////////////////////////////////////////////
       //  Start av file                                        SRB /
       //////////////////////////////////////////////////////////////
       begsr srb;
         //_____________________________
         // Hente data fra sendningslogg
         //"""""""""""""""""""""""""""""
         chain ( wsn ) edis;
         *in81 = not %found;
         // Legg in UNB-segment (Sendningsstart)
         exsr unb0;
       endsr;
       //////////////////////////////////////////////////////////////
       //  Les meldinger                                        SRC /
       //////////////////////////////////////////////////////////////
       begsr src;
         chain(e) ( ssr : wst : wven : s0004 : s0010 : s0035 ) edimc;
         *in82 = %error;
         *in81 = not %found;
         if *in81 = *off;

           if m0065 = 'CUSDEC'
             and m1n07 = 'SVE'
             and *in82 = *off;

             // Uppdatera SVEH
             wveh_syav=mavd;
             wveh_syop=mtdn;
             chain ( wveh_syav : wveh_syop ) sveh;
             *in82 = not %found;
             if *in82 = *off;
               msn = ssn;
               mst = 'A';
      /end-free
     c                   movel     wdt           mdt
     c                   movel     wtm           mtm
      /free
               update edimr;
               sveh_syst='A';
               update svehr;
               // Skriv til historik
               clear svehhr;
               svehh_sya=mavd;
               svehh_syo=mtdn;
               svehh_dth=wdt;
               svehh_tmh=wtm;
               svehh_sr='S';
               svehh_txh='CUSDEC';
               svehh_txh = %trimr(svehh_txh) + ' ' +
               m1001;
               svehh_tx1='ÿverf¯ringsreferans:';
               svehh_tx1 = %trimr(svehh_tx1) + ' ' +
               smbr;
               write svehhr;
               // Lag Tulldatafilen (UNH - UNT)
               sve022rn ( sveh_syav : sveh_syop : udata
               );

               ads=%trimr(udata);
               axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
               wchart='N';
               exsr sr≈;

               wavd = %editc(sveh_syav:'X');
               wopd = %editc(sveh_syop:'X');
               wfilnam = *blanks;
               wfilnam = '/' + %trimr(uelibf)
               + '/EDISE/TEMP/SVEU' + wavd + '-' + wopd;

               codepage = '819';
               file_o = open( %trimr( wfilnam )
               : 'w, codepage=' + codepage );

               rc = close( file_o );

               file_o = open( %trimr( wfilnam )
               : 'w' );

               if file_o <> *null;
                 data = %trimr(bds);
                 rc = fputs(data : file_o);
                 rc = close( file_o );
               endif;
               ads=*blanks;
               bds=*blanks;
               ax = 0;
               bx = 1;
               axv = 0;
               axh = 0;
               wdir = 'R';
               wchar = *blanks;
               wchart = 'Y';
               // Lag cheksum av tulldata (UNH - UNT)
               ufili = *blanks;
               wavd = %editc(sveh_syav:'X');
               wopd = %editc(sveh_syop:'X');
               ufili = '/' + %trimr(uelibf)
               + '/EDISE/TEMP/SVEU' + wavd + '-' + wopd;

               ufilo = *blanks;
               ufilo = '/' + %trimr(uelibf)
               + '/EDISE/TEMP/SVEC' + wavd + '-' + wopd;

               svg004r ( ufili : ufilo : uchksum );
               // Lag signatur(kryptert) av checksum
               ufili = *blanks;
               ufili = '/' + %trimr(uelibf)
               + '/EDISE/TEMP/SVEU' + wavd + '-' + wopd;
               ufilo = *blanks;
               ufilo = '/' + %trimr(uelibf)
               + '/EDISE/TEMP/SVEK' + wavd + '-' + wopd;
               svg005r ( ufili : ufilo : ukrypt );

             endif;
           endif;
         endif;
       endsr;
       //////////////////////////////////////////////////////////////
       //  Slutt pÂ file                                        UNZ /
       //////////////////////////////////////////////////////////////
       begsr unz;
         //_____________________________
         // Legg in UNZ (Slutt sendning)
         //"""""""""""""""""""""""""""""
         ads='UNZ+2+' + %trimr(s0020)+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //___________________________________
         // Lag streng for katalognavn/filnavn
         //"""""""""""""""""""""""""""""""""""
         wfilnam = *blanks;
         wfilnam = '/' + %trimr(uelibf)
         + '/EDISE/TEMP/'
         + %trimr(smbr) + 'UNZ';
         //_____________________________
         // Lag fil og set codepage (pc)
         //"""""""""""""""""""""""""""""
         codepage = '819';
         file_o = open( %trimr( wfilnam )
         : 'w, codepage=' + codepage );

         rc = close( file_o );
         //________________________________________
         // ≈pne fil, med tegnkonvertering for jobb
         //""""""""""""""""""""""""""""""""""""""""
         file_o = open( %trimr( wfilnam )
         : 'w' );

         if file_o <> *null;
           data = %trimr(bds);
           rc = fputs(data : file_o);
           rc = close( file_o );
         endif;
         //________________________
         // Oppdatere sendningslogg
         //""""""""""""""""""""""""
         sst='A';
         s0036=2;
         update edisr;
       endsr;
       //////////////////////////////////////////////////////////////
       // SR for output string, read input from left              SR≈
       //////////////////////////////////////////////////////////////
       begsr sr≈;
         //_________________
         // Trim left blanks
         //"""""""""""""""""
         ads2 = ads;
         ads=*blanks;
         ads=%triml(ads2);
         //_______________
         // Find positions
         //""""""""""""""""
         axv=1;
         axh = %checkr(' ':ads);
         //________________________________________________
         // Read input string and place it in output string
         //""""""""""""""""""""""""""""""""""""""""""""""""
         for axv = axv to axh;
           wchar = a(axv);
           // If invalid character (' + : ?) then replace with 'ÿ'
           if wchart = 'Y'
             and wdir = 'R';
             if wchar = ';';
               wchar=':';
             endif;
             *in83 = (wchar = wapstr);
             if *in83 = *off;
               *in83 = (wchar = '+');
             endif;
             if *in83 = *off;
               *in83 = (wchar = ':');
             endif;
             if *in83 = *off;
               *in83 = (wchar = '?');
             endif;
             if *in83 = *on;
               exsr sr≈a;
             endif;
           endif;
           // Move to output string
           b(bx) = wchar;
      /end-free
     c                   add       1             bx

      /free
         endfor;

         wchart='Y';
         ads=*blanks;

       endsr;
       //////////////////////////////////////////////////////////////
       // SR for replace control characters                      SR≈A
       //////////////////////////////////////////////////////////////
       begsr sr≈a;
         // Move '?' before the character
         b(bx) = '?';
      /end-free
     c                   add       1             bx
      /free
       endsr;
       //////////////////////////////////////////////////////////////
       //  Venstrestill numeriske verdier                      SRNE /
       //////////////////////////////////////////////////////////////
       begsr srne;
         // Flytte in rett verdi i Alfa 12-felt
         ÿa12 = '000000000000';
         if ÿa152d = '00';
           ÿa12 = %subst(ÿa152h:2:12);
         else;
           %subst(ÿa12:1:9) = ÿa152x;
           %subst(ÿa12:10:3) = '.  ';
           %subst(ÿa12:11:2) = ÿa152d;
           if ÿ12(12) = '0';
             ÿ12(12) = ' ';
             if ÿ12(11) = '0';
               ÿ12(11) = ' ';
             endif;
           endif;
         endif;
         // lete fra venstre og finn foerste siffer over 0
         for ÿx = 1 to 11;
           if ÿ12(ÿx) <> '0';
      /end-free
     c                   goto      srne1
      /free
           endif;
         endfor;
      /end-free
     c     srne1         tag
     Ç* Venstrestill til et nytt felt
      /free
         ÿan = *blanks;
      /end-free
     c                   movea     ÿ12(ÿx)       ÿan

      /free
       endsr;
       //////////////////////////////////////////////////////////////
       //  Sendningsstart                                      UNB0 /
       //////////////////////////////////////////////////////////////
       begsr unb0;
         //____
         // UNA
         //""""
         ads = 'UNA:+.?';
         ads = %trimr(ads) + ' ' + wapstr;
         axv = 1;
         axh = 9;
         wchart = 'N';
         exsr sr≈;
         //______________
         // START SEGMENT
         //""""""""""""""
         // UNB+UNOC:3+9368092194+2021000969+040427:1015+E0001980+ABC123+CUSDECI-IE++++1'
         ads = 'UNB+UNOC:3+';
         axv = 1;
         axh = 11;
         wchart = 'N';
         exsr sr≈;
         //____________________
         // OVERFÿRINGSAVSENDER
         //""""""""""""""""""""
         ads = s0004;
         axv = 1;
         axh = 35;
         exsr sr≈;
         // Vidareadress
         if svtf_0008 <> *blanks;
           ads = '::';
           axv = 1;
           axh = 2;
           wchart = 'N';
           exsr sr≈;

           ads = svtf_0008;
           axv = 1;
           axh = 14;
           exsr sr≈;
         endif;

         ads = '+';
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         //____________________
         // OVERFÿRINGSMOTTAKER
         //""""""""""""""""""""
         ads = s0010;
         axv = 1;
         axh = 35;
         exsr sr≈;
         // Vidareadress
         if svtf_0014 <> *blanks;
           ads = '::';
           axv = 1;
           axh = 2;
           wchart = 'N';
           exsr sr≈;

           ads = svtf_0014;
           axv = 1;
           axh = 14;
           exsr sr≈;
         endif;

         ads = '+';
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         //__________________________________________
         // DATO/TID FOR FREMSTILLING AV OVERFÿRINGEN
         //""""""""""""""""""""""""""""""""""""""""""
         ads = *blanks;
         ads = %subst(wdt:3:6);
         axv = 1;
         axh = 6;
         exsr sr≈;


         ads = ':';
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         // Tid
         ads = *blanks;
         %subst(ads:1:4) = %subst(wtm:1:4);
         axv = 1;
         axh = 4;
         exsr sr≈;

         ads = '+';
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         //________________________
         // OVERFÿRINGENS REFERANSE
         //""""""""""""""""""""""""
         ads = s0020;
         axv = 1;
         axh = 10;
         exsr sr≈;

         ads = '+';
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         //________
         // PASSORD
         //""""""""
         ads = svtf_0022;
         axv = 1;
         axh = 10;
         exsr sr≈;

         ads = '+';
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         //______________________
         // APPLIKASJONSREFERANSE
         //""""""""""""""""""""""
         ads = s0026;
         axv = 1;
         axh = 14;
         exsr sr≈;

         ads = '+++';
         axv = 1;
         axh = 3;
         wchart = 'N';
         exsr sr≈;
         //__________________________
         // 0032 Teckenrepresentation
         //""""""""""""""""""""""""""
         //______________
         // TESTINDIKATOR
         //""""""""""""""
         if s0035 > '0';
           ads = '+';
           axv = 1;
           axh = 1;
           wchart = 'N';
           exsr sr≈;

           ads = s0035;
           axv = 1;
           axh = 1;
           exsr sr≈;
         endif;
         //______________
         // SLUTT SEGMENT
         //""""""""""""""
         ads = wapstr;
         axv = 1;
         axh = 1;
         wchart = 'N';
         exsr sr≈;
         //___________________________________
         // Lag streng for katalognavn/filnavn
         //"""""""""""""""""""""""""""""""""""
         wfilnam = *blanks;
         wfilnam = '/' + %trimr(uelibf)
         + '/EDISE/TEMP/'
         + %trimr(smbr) + 'UNB';
         //_____________________________
         // Lag fil og set codepage (pc)
         //"""""""""""""""""""""""""""""
         codepage = '819';
         file_o = open( %trimr( wfilnam )
         : 'w, codepage=' + codepage );

         rc = close( file_o );
         //________________________________________
         // ≈pne fil, med tegnkonvertering for jobb
         //""""""""""""""""""""""""""""""""""""""""
         file_o = open( %trimr( wfilnam )
         : 'w' );

         if file_o <> *null;
           data = %trimr(bds);
           rc = fputs(data : file_o);
           rc = close( file_o );
         endif;

         ads=*blanks;
         bds=*blanks;
         ax = 0;
         bx = 1;
         axv = 0;
         axh = 0;
         wdir = 'R';
         wchar = *blanks;
         wchart = 'Y';
       endsr;
       //////////////////////////////////////////////////////////////
       //  AUTOACK                                              AUT /
       //////////////////////////////////////////////////////////////
       begsr aut;
         //____
         // UNH
         //""""
         ads='UNH+'+%trimr(s0020)
         + 'A+AUTACK:4:1:UN:SEA02A'+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //____________
         // USH Grupp 1
         //""""""""""""
         ads='USH+7+'+%trimr(s0020)+'S+3++2++1+ZH1::'
         + %trimr(svtf_sec1)+'++1:'+wdt+':'+wtm+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //____________
         // USA Grupp 1
         //""""""""""""
         ads='USA+1:::48'+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //____________
         // USC Grupp 2
         //""""""""""""
S02      //                  EVAL      ADS='USC+'+%trimr(SVTF_CER1)
S02      //                                + '+4:' + %trimr(SVTF_CER2) + '+3'
S02      //                                + WAPSTR
S02      //                  EVAL      AXV=1
S02      //    ' '           CHECKR(E) ADS           AXH
S02      //                  EVAL      WCHART='N'
S02      //                  EXSR      SR≈
N02      ads='USC+';
N02      axv=1;
      /end-free
N02  c     ' '           checkr(e) ads           axh
      /free
N02      wchart='N';
N02      exsr sr≈;
N02      ads = svtf_cer1;
N02      axv = 1;
N02      axh = 35;
N02      exsr sr≈;
N02
N02      ads = '+4:';
N02      axv = 1;
N02      axh = 3;
N02      wchart = 'N';
N02      exsr sr≈;
N02
N02      ads = svtf_cer2;
N02      axv = 1;
N02      axh = 35;
N02      exsr sr≈;
N02
N02      ads = '+3';
N02      axv = 1;
N02      axh = 2;
N02      wchart = 'N';
N02      exsr sr≈;
N02
N02      ads = wapstr;
N02      axv = 1;
N02      axh = 1;
N02      wchart = 'N';
N02      exsr sr≈;
         //____________
         // USA Grupp 2
         //""""""""""""
         ads='USA+6:::10'+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //____________
         // USX Grupp 3
         //""""""""""""
         %subst(w0062:1:4) = %editc(sveh_syav:'X');
         %subst(w0062:5:7) = %editc(sveh_syop:'X');
         ads='USX+'+%trimr(s0020)+'++++++'
         + w0062+'+CUSDEC:D:96B:UN'+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //____________
         // USY Grupp 3
         //""""""""""""
         ads='USY+'+%trimr(s0020)+'S+ZS3:'
            + %trimr(uchksum) + wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;

         ads='USY+'+%trimr(s0020)+'S+ZS4:'
            + %trimr(ukrypt) + wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
         //____
         // UNT
         //""""
         ads='UNT+9+'+%trimr(s0020)+'A'+wapstr;
         axv=1;
      /end-free
     c     ' '           checkr(e) ads           axh
      /free
         wchart='N';
         exsr sr≈;
       endsr;
       //////////////////////////////////////////////////////////////
       //  Samkj¯r nÂ flere *IFS filer til 1                 CATIFS /
       //////////////////////////////////////////////////////////////
       begsr catifs;
         // Filene som skal samkj¯res er:
         // SXXXXXXXUNB        Start av fil
         // SVEU1111-2222222   UNH til UNT Tullmeddelande
         // SXXXXXXXUNZ        Slutt pÂ fil
         // Samkj¯res til SXXXXXXXX   (Standard SYSPED)
         //  QSH CMD('cat a b c > d')


         wfa = *blanks;
         wfa = '/' + %trimr(uelibf)
         + '/EDISE/TEMP/'
         + %trimr(smbr) + 'UNB';

         wfb = *blanks;
         wavd = %editc(sveh_syav:'X');
         wopd = %editc(sveh_syop:'X');
         wfb = '/' + %trimr(uelibf)
         + '/EDISE/TEMP/'
         + 'SVEU'
         + wavd
         + '-'
         + wopd;

         wfc = *blanks;
         wfc = '/' + %trimr(uelibf)
         + '/EDISE/TEMP/'
         + %trimr(smbr) + 'UNZ';

         wfd = *blanks;
         wfd = '/' + %trimr(uelibf)
         + '/EDISE/'
         + %trimr(smbr);
         //----------------------TEST--------------------------------------
         codepage = '819';
         file_o = open( %trimr( wfd )
         : 'w, codepage=' + codepage );
         rc = close( file_o );

         //----------------------TEST--------------------------------------

         wcmd = *blanks;
         wcmd = 'QSH CMD(' + wapstr + 'cat'
         + ' '
         + %trimr(wfa)
         + ' '
         + %trimr(wfb)
         + ' '
         + %trimr(wfc)
         //                            + ' > '
         + ' >> '
         + %trimr(wfd)
         + wapstr + ')';

         runcmd(wcmd:300);
       endsr;
      /end-free
