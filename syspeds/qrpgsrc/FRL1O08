      *********************************************************************
     H debug(*yes)
      *
CRTPG+*  CRTPGM SYSPED/FRl1O08 MODULE(*LIBL/FRL1O08 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FVAREL01   IF   E           K DISK    USROPN
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FPRISLD    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Alle databasefelter med tilhørende skjermvariabel
      *
      * PRISLD
      *
     D PRD_PLN         S              5  0
     D WRD_PLN         S              5
     D PRD_FIR         S              2
     D WRD_FIR         S              2
     D PRD_ANRS        S             15
     D WRD_ANRS        S             15
     D PRD_NOBB        S             15
     D WRD_NOBB        S             15
     D PRD_EAN         s             14  0
     D ARD_EAN         s             14
     D WRD_EAN         s             14
     D PRD_ANRK        S             35
     D WRD_ANRK        S             35
     D PRD_PGR         S             35
     D WRD_PGR         S             35
     D PRD_PTX         s             30
     D WRD_PTX         s             30
     D PRD_PTX2        s             30
     D WRD_PTX2        s             30
     D PRD_ANTK        S              2  0
     D WRD_ANTK        S              2
     D PRD_PRIG        s             15  3
     D WRD_PRIG        s             15
     D PRD_PRIN        s             15  3
     D WRD_PRIN        s             15
     D PRD_PRIK        s             15  3
     D WRD_PRIK        s             15
     D PRD_END         s              1
     D WRD_END         s              1
     D PRD_HTMB        s             70
     D WRD_HTMB        s             70
     D PRD_HTMP        s             70
     D WRD_HTMP        s             70
     D PRD_HTMM        s             70
     D WRD_HTMM        s             70
      *
      * VAREL01
      *
     D PAKANT          S              5  0
     D WAKANT          S              5
OddEvD OddEven         s             10
     D USER            s             10
     D lenke           s            200
     D lenke0          s            200
     D lenket          s             20
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Other variables
      *
     D                 DS
     D  HOIREI                 1     15
     D  HOI                    1     15    DIM(15)
     D                 DS
     D  HOIREU                 1     15
     D  HOU                    1     15    DIM(15)
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Les iput parametr
     C                   exsr      Parse
      * Initiering
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * finn vare
     C                   exsr      lesvare
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *********************************************************************
     C     Parse         begsr
      *********************************************************************
      * Leser varens key
     C                   eval      USER      = zhbgetvar('USER')
     C                   eval      PRD_FIR   = zhbgetvar('FIRMA')
     C                   eval      PRD_ANRS  = zhbgetvar('ARTNR')
     C                   eval      WRD_PLN   = zhbgetvar('PRINR')
     C                   eval      PRD_PLN   = c2n2(WRD_PLN)
      * høyrejuster varenummer
     c                   move      PRD_ANRS      hoirei
     c                   exsr      hoiresr
     c                   move      hoireu        PRD_ANRS
     C                   endsr
      *********************************************************************
     C     Exit          begsr
      *********************************************************************
      * lukk filer
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   close     BRPARF
     C                   CLOSE     PRISLD
     C                   CLOSE     VAREL01
     C                   return
     C                   endsr
      *********************************************************************
     C     LoadHtml      begsr
      *********************************************************************
      * Read output skeleton html member into memory
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'FRL1O08'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *********************************************************************
     C     SetTop        begsr
      *********************************************************************
      *  Set variable data for section /$top
      *
     C* klargjøre HTMLvariable
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
      *
     C                   endsr
      *
      *********************************************************************
     C     lesvare       begsr
      *********************************************************************
      * leser vare informasjon i prislib
      *
     C     KPRISLD       chain     PRISLD                             33
     C     VAREKEY       chain     VAREL01                            33
      *
     C* klargjøre HTMLvariable - SEND ROW
      *
      * Prislistenummer
     C                   callp     updHTMLvar('WRD_PLN':
     C                             %trim(%editc(PRD_PLN:'Z')))
      * L1 Firmakode
     C                   callp     updHTMLvar('WRD_FIR':PRD_FIR)
      * L1 artikkelnummer
     C                   callp     updHTMLvar('WRD_ANRS':PRD_ANRS)
      * NOBB nummer
     C                   callp     updHTMLvar('WRD_NOBB':PRD_NOBB)
      * EAN artikkelnummer
     C                   callp     updHTMLvar('WRD_EAN':
     C                             %trim(%editc(PRD_EAN:'Z')))
      * Kundes artikkelnummer
     C                   callp     updHTMLvar('WRD_ANRK':PRD_ANRK)
      * Produktgruppe
     C                   callp     updHTMLvar('WRD_PGR':PRD_PGR)
      * Produktbeskrivelse 1
     C                   callp     updHTMLvar('WRD_PTX':PRD_PTX)
      * Produktbeskrivelse 2
     C                   callp     updHTMLvar('WRD_PTX2':PRD_PTX2)
      * Pris gammel
     C                   callp     updHTMLvar('WRD_PRIG':
     C                             %trim(%editc(PRD_PRIG:'Z')))
      * Pris ny
     c                   z-add     prd_prin      num152           15 2
     C                   callp     updHTMLvar('WRD_PRIN':
     C                             %trim(%editc(num152:'J')))
      * Pris kunde
     C                   callp     updHTMLvar('WRD_PRIK':
     C                             %trim(%editc(PRD_PRIK:'Z')))
      * Endret
     C                   callp     updHTMLvar('WRD_END':PRD_END)
      * kat.bane bilde
     C                   callp     updHTMLvar('WRD_HTMB':PRD_HTMB)
      * kat.bane produkt
     c     PRD_HTMP      ifne      *blanks
     c                   eval      lenke0=PRD_HTMP
     c                   eval      lenket='Produktbeskrivelse'
     C                   exsr      genlenke
     C                   callp     updHTMLvar('WRD_HTMP':LENKE)
     c                   else
     c                   eval      lenke='Produktbeskrivelse mangler'
     C                   callp     updHTMLvar('WRD_HTMP':LENKE)
     c                   end
      * kat.bane mont.anv
     c     PRD_HTMM      ifne      *blanks
     c                   eval      lenke0=PRD_HTMM
     c                   eval      lenket='Monteringsanvisning'
     C                   exsr      genlenke
     C                   callp     updHTMLvar('WRD_HTMM':LENKE)
     c                   else
     c                   eval      lenke='Monteringsanvisning mangler'
     C                   callp     updHTMLvar('WRD_HTMM':LENKE)
     c                   end
      * Pakningantall
     C                   callp     updHTMLvar('WAKANT':
     C                             %trim(%editc(PAKANT:'Z')))
      * skriver ut bilde
     C                   callp     wrtsection('head')
     c     PRD_HTMB      ifne      *blanks
     C                   callp     wrtsection('bilde')
     c                   else
     C                   callp     wrtsection('bildem')
     c                   end
      *
     C                   endsr
      *
      *********************************************************************
     C     INIT          begsr
      *********************************************************************
      *  INITIER
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      PRISLD
     C                   open      VAREL01                              50
      * Key for PRISLD
     C     KPRISLD       KLIST
     C                   KFLD                    PRD_FIR
     C                   KFLD                    PRD_PLN
     C                   KFLD                    PRD_ANRS
     C     varekey       Klist
     C                   KFLD                    PRD_FIR
     C                   KFLD                    PRD_ANRS
     c     BRPARFK       klist
     c                   kfld                    PABRID
     c                   kfld                    PAKUNR
     c                   kfld                    PAID
     C                   endsr
      *********************************************************************
     C     hoiresr       begsr
      *********************************************************************
      *  høyrejusterer varenummer
     c                   clear                   hoireu
     c                   z-add     15            nr1               2 0
     c                   z-add     15            nr2               2 0
     c                   do        15
     c     hoi(nr1)      ifne      *blanks
     c                   move      hoi(nr1)      hou(nr2)
     c                   sub       1             nr2
     c                   endif
     c                   sub       1             nr1
     c                   enddo
     c                   endsr
     C***********************************************
     C     genlenke      BEGSR
     C***********************************************
     c                   move      *BLANKS       LENKE
     c                   movel     '<a href="'   LENKE
     C     LENKE         CAT       LENKE0:0      LENKE
     C     LENKE         CAT       '" target="':0LENKE
     C     LENKE         CAT       '_BLANK"':0   LENKE
     C     LENKE         CAT       '">':0        LENKE
     C     LENKE         CAT       LENKET:0      LENKE
     C     LENKE         CAT       '</a>':0      LENKE
     c                   endsr
