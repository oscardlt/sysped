     FDRIV      UF   E           K DISK
     FBRIDF     UF A E           K DISK
     FUNITL01   UF   E           K DISK
     FUNITL04   UF   E           K DISK    RENAME(UNITR:UNITR4)
     FTURER20   UF   E           K DISK
     FFIRM      IF   E           K DISK
     FESTKU02D  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C     Tur20Key      KLIST
     C                   KFLD                    WSSJN
     C                   KFLD                    STAT              1
     c     *loval        setll     FIRM
     c                   read      FIRM
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      *
     c                   eval      INFO='Tast sjåførnummer for å få opp '
     C                             +'øvrige verdier.'
     c
     C     START         TAG
     C                   EXFMT     BILDE
     C                   MOVEA     '0000000'     *IN(85)
     c                   eval      INFO=*blanks
      *
     C   03              GOTO      SLUTT
      *
     c     *in04         ifeq      '1'
     c     *in50         ifeq      '0'                                          Øvre
     C                   CALL      'SYTR18'
     C                   PARM                    WSSJN
     C                   else                                                   Nedre
     C                   CALL      'SYTR19'
     C                   PARM                    WSBILN
     C                   endif
     C                   GOTO      START
     C                   endif
      *
     C     WSSJN         IFEQ      *zeros
     C                   SETON                                        85
     C                   GOTO      START
     C                   END
     C     WSSJN         CHAIN(N)  DRIV                               85
     C   85              GOTO      START
      *
      * Gyldig sjåfør er gitt - gå videre
     C     WSNAVN        IFEQ      *BLANKS
     c                   seton                                        50
     c                   movel     'PD'          WSUSER
     c                   move      WSSJN         WSUSER
     C     WSUSER        CHAIN(N)  BRIDF                              33
     c  N33              movel     BIPO          WSPASW
     c                   movel     'PDA:'        BilKey12         12
     c                   move      WSSJN         Bilkey12
     c                   movel     Bilkey12      Bilkey30         30
     C     Bilkey30      CHAIN(N)  UNITL04                            33
     c  N33              movel     UNBILN        WSBILN
      *
     c                   exsr      getAnt
     c                   move      WDATO         WSDATO
      *
     C                   MOVEL     DRNAVN        WSNAVN
     C     'PDA'         scan      WSNAVN                                 33
     c  N33              eval      WSNAVN = %trim(WSNAVN) + ' PDA'
     c                   eval      INFO='Sett passord og evt bilnr/dato + Enter'
     C                   GOTO      START
     C                   END
      *****
     c                   eval      INFO='Sett passord og evt bilnr/dato + Enter'
      *
     C     WSPASW        IFEQ      *Blanks
     C                   SETON                                        86
     C                   GOTO      START
     C                   END
     C     WSBILN        IFNE      *Blanks
     C     WSSJN         CHAIN(N)  DRIV                               87
     C   87              GOTO      START
     C                   END
      *
      * ALLE PARM. OK - KJØR ENDRING
     C                   EXSR      ENDRE
     c                   clear                   BILDE
     c                   eval      INFO='Sjåfør '+%trim(%editc(DRANSA:'Z'))
     c                             + ' er oppdatert. Velg ny eller F3=Avslutt'
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ****************************************************
     C     GetAnt        BEGSR
      ****************************************************
     c                   move      *zeros        ANTSTB
     c                   move      *zeros        ANTST1
      * stat blank
     c                   move      ' '           Stat
     c     Tur20Key      setll     TURER20
     c     Tur20Key      reade(n)  TURER20                                33
     c     *in33         doweq     '0'
     c                   add       1             ANTSTB
     c     Tur20Key      reade(n)  TURER20                                33
     c                   enddo
      * stat 1
     c                   move      '1'           Stat
     c     Tur20Key      setll     TURER20
     c     Tur20Key      reade(n)  TURER20                                33
     c     *in33         doweq     '0'
     c                   add       1             ANTST1
     c     Tur20Key      reade(n)  TURER20                                33
     c                   enddo
     C                   endsr
      ****************************************************
     C     ENDRE         BEGSR
      ****************************************************
      * 1 oppdatere Sjåførnav med PDA som del av navnet
     C     WSSJN         CHAIN     DRIV                               33
     c                   movel     WSNAVN        DRNAVN
     c  N33              update    DRIVR
      *
      * 2 oppdatere eSped UserID/PW
     C     WSUSER        CHAIN     BRIDF                              33
     c     *in33         ifeq      *off
     c                   movel     WSPASW        BIPO
     c                   update    BRIDFR
     c                   else
      *
      * hvis den ikke fines - fin føste og beste PD.. og bruk som mal
     C     'PD        '  setll     BRIDF
     C                   read(n)   BRIDF                                  33
     C     *in33         doweq     '0'
      * kun hvis en fakt en annen sjåfør (i samme firma) kan en ny opprettes
     c                   if        BIFIRM=FIFIRM and %subst(BIBRID:1:2) ='PD'
     c                   movel     WSUSER        BIBRID
     c                   movel     WSPASW        BIPO
     c                   movel     WSNAVN        BIBESK
     c                   write     BRIDFR
     c                   goto      UtBrid
     c                   endif
     C                   read(n)   BRIDF                                  33
     c                   enddo
      *
     c                   endif
     c     utBrid        tag
      *
      * 3 knytting sjåfør bil..
     c     WSBILN        ifeq      *blanks
     C     Bilkey30      CHAIN     UNITL04                            33
     c  N33              move      *blanks       UNRETU
     c  N33              update    UNITR4
     c                   else
     C     WSBILN        CHAIN     UNITL01                            33
     c  N33              movel     BILKEY12      UNRETU
     c  N33              update    UNITR
     c                   endif
      *
      * 4 ferdigmerke turer t.o.m  dato (ved 0 i dato = ingen)
      * stat blank
     c                   move      ' '           Stat
     c     Tur20Key      setll     TURER20
     c     Tur20Key      reade     TURER20                                33
     c     *in33         doweq     '0'
     c                   if        TUDT > WSDATO
     c                   update    TURERR
     c                   leave
     c                   endif
     c                   move      '3'           TUTST4
     c                   update    TURERR
     c     Tur20Key      reade     TURER20                                33
     c                   enddo
      * stat 1
     c                   move      '1'           Stat
     c     Tur20Key      setll     TURER20
     c     Tur20Key      reade     TURER20                                33
     c     *in33         doweq     '0'
     c                   if        TUDT > WSDATO
     c                   update    TURERR
     c                   leave
     c                   endif
     c                   move      '3'           TUTST4
     c                   update    TURERR
     c     Tur20Key      reade     TURER20                                33
     c                   enddo
      *
     C                   ENDSR
