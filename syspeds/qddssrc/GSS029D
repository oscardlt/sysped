********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004 * 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
      *              INGEN MERKING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 60 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '5')
     A            ORONO          7  0O  5  5EDTCDE(4)
     A            ORST           1A  O  5 13
     A            ORBCD          5A  O  5 16
     A            ORLC           5A  O  5 22
     A            ORSNM         30A  O  5 28
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 55SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'GSS029R'
     A                                      COLOR(BLU)
     A                                  1 10'Orders with same P.O.no.'
     A                                      COLOR(WHT)
     A                                  2  2'P.O.no.'
     A            UPO           20A  O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Select option:   5=Display'
     A                                      COLOR(BLU)
     A                                  4  2'Op OrderNo St BoatC Locat Supplier'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Cancel  F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS029R'
     A                                      COLOR(BLU)
     A                                  1 10'Orders with same P.O.no.'
     A                                      COLOR(WHT)
     A                                  2  2'P.O.no.'
     A            UPO           20A  O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Op OrderNo St BoatC Locat Supplier'
     A                                      COLOR(BLU)
     A                                  4  2'Op OrderNo St BoatC Locat Supplier'
     A                                      COLOR(WHT)
     A N30                             10  2'F3/F12=Cancel  F5=Refresh'
     A                                      COLOR(BLU)
      *
     A          R GSS029DA
     A                                      CA03(03 'Forrige')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS029R'
     A                                      COLOR(BLU)
     A                                  1 21'Display order'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  3  2'Order No. . . . .:'
     A                                      COLOR(RED)
     A            ORONO          7Y 0O  3 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 42'Status'
     A            ORST           1A  O  3 51DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 54'Trip No.'
     A            ORTNO         11Y 0O  3 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2'Return goods (R).:'
     A            ORRTNG         1A  O  4 22DSPATR(UL)
     A            ORTNO2        11Y 0O  4 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2'Vessel  . . . . .:'
     A            ORBCD          5A  O  5 22DSPATR(UL)
     A            ORBNM         30A  O  5 34DSPATR(UL)
     A                                      COLOR(WHT)
     A            ORTNO3        11Y 0O  5 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  2'Clients No. . . .:'
     A            ORCNO          8Y 0O  6 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            ORCNM         30A  O  6 34DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Supplier No.  . .:'
     A            ORSNO          5Y 0O  7 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            ORSNM         30A  O  7 34DSPATR(UL)
     A                                  9  2'Colli . . . . . .:'
     A            ORCLI          5Y 0O  9 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9 32'Weight:'
     A            ORWT           7Y 1O  9 41DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9 52'Volume:'
     A            ORVOL          5Y 2O  9 61DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 10  2'Goods description:'
     A            ORDSC         30A  O 10 22DSPATR(UL)
     A                                 11  2'Location  . . . .:'
     A            ORLC           5A  O 11 22DSPATR(UL)
     A            ORLC2          5A  O 11 38DSPATR(UL)
     A                                      COLOR(WHT)
     A            ORLC3          5A  O 11 45DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Date delivered  .:'
     A            XXDTL          6Y 0O 12 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 12 29'(DDMMYY)'
     A            XXDTL2         6Y 0O 12 38DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            XXDTL3         6Y 0O 12 45DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12 53'Del. date:'
     A            XXDTWL         6Y 0O 12 64DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 12 71'(DDMMYY)'
     A                                 13  2'Stockpositions  .:'
     A            ORPOS1         5A  O 13 22DSPATR(UL)
     A            ORPOS2         5A  O 13 28DSPATR(UL)
     A            ORPOS3         5A  O 13 34DSPATR(UL)
     A                                 13 44'Ord.type:'
     A            OROT           2   O 13 54DSPATR(UL)
     A                                 13 58'Del. way:'
     A            ORFR           3   O 13 68DSPATR(UL)
     A                                 14  2'Value . . . . . .:'
     A            ORAMT         11Y 2O 14 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            ORCURC         3A  O 14 36DSPATR(UL)
     A                                 15  2'Clients order No.:'
     A            UPO           20A  B 15 22DSPATR(UL)
     A                                 18  2'Free text . .:'
     A            ORFTCD         2A  O 18 18DSPATR(UL)
     A                                 18 25'Pick.Note/Pack.List/Inv./Inv.Basis'
     A            ORKDL1         2A  O 18 60DSPATR(UL)
     A            ORKDL2         2A  O 18 63DSPATR(UL)
     A            ORKDL3         2A  O 18 66DSPATR(UL)
     A            ORFT1         30A  O 19 18DSPATR(UL)
     A            ORFT2         30A  O 19 50DSPATR(UL)
     A            ORFT3         30A  O 20 18DSPATR(UL)
     A            ORFT4         30A  O 20 50DSPATR(UL)
     A                                 21  2'Colli No. from/to:'
     A            ORCLNF         3Y 0O 21 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            ORCLNT         3Y 0O 21 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 22 40'Reg. date:'
     A            XXDTR          6  0O 22 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23  2'F3/F12=Cancel'
     A                                      COLOR(BLU)
      *
