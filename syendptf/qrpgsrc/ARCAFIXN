********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 Test lengde på avdeling og oppdragsnr
********************************************************************
      *""""""""""""""""
      *  manipiler navn dersom det er uploadet med kortversjon (+ skilletegn)
      *""""""""""""""""
     H DFTACTGRP(*NO)
      * inkluder div /copy ... for å kunne bruke funskjoner fra CGIDEV2 serviceprog
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     C     *entry        PLIST
     C                   PARM                    Mappe            20
     C                   PARM                    FNavn           120
     C                   PARM                    FBeskr          120
     C                   PARM                    Fixed             1
      * test
     C*                  move      *blanks       MAPPE            20
     C*                  move      *blanks       FNavn           128
     C*                  move      *blanks       FBeskr          128
     C*                  move      *blanks       Fixed             1
     C*                  eval      MAPPE = 'TUR    '
     C*                  eval      Fnavn = 'ZT+501845+originalFilnavn.pdf'
      *
     c                   move      'N'           Fixed
      * DFT-tyep foreløpig hardkodet..
     c                   move      'ZO'          DFTTyp            2
      *
     c                   select
     c     Mappe         wheneq    'OPPDRAG'
     c                   exsr      FIXOPD
     c     Mappe         wheneq    'TUR'
     c                   exsr      FIXTUR
     c                   endsl
      *
      *
     C                   seton                                        LR
     c                   return
      *
      *
     C***********************************************
     C     FIXOPD        BEGSR
     C***********************************************
      *   ulike gyldige varianter
      * a) ZH1+15481.pdf
      * b) ZH1+15481+OrigFilnavnxxx.pdf
      * c) ZH+1+15481+OrigFilnavnxxx.pdf
      * d) 1+15481+Dette er original.pdf
      *   ulike UTdata varianter
      *    ZH00010155296.pdf_2015-03-23-07.38.51.51.pdf      (som en normal web-upload)
      *    ZH00010155296.pdf                                 (som et skannet dokument uten TS)
      *
      *
      * 2 første tegn = type
     c                   If        %subst(FNavn:1:2) < 'AA' or
     c                             %subst(FNavn:1:2) > 'Z9'
     c                   eval      FNavn=DftTyp+Fnavn
     c                   endif
      * dersomom 3.tegn = +, fjern det
     c                   If        %subst(FNavn:3:1) = '+'
     c                   eval      FNavn=%subst(FNavn:1:2)+%subst(FNavn:4)
     c                   endif
      * Fnavn skal nå være a.la ZH1+.... (som a eller b)
     c                   move      *zeros        AvdN              4 0
     c                   move      *zeros        OpdN              7 0
     c                   move      *zeros        AvdA              4
     c                   move      *zeros        OpdA              7
     c                   move      *blanks       ext              10            Extention
     c                   move      *blanks       ext              10            Extention
     c                   z-add     0             extStr            3 0          StartP For extent.
     c                   z-add     0             Str               3 0          StartP For søk
     c                   z-add     1             Pos               3 0          Funnet i Pos
      * Finn file extention..
     c                   dow       Pos<> 0
     c                   eval      Pos= %SCAN ('.' : FNavn : Str+1)
     c                   if        Pos<> 0
     c                   eval      Str= Pos
     c                   endif
     c                   enddo
     c                   if        Str = 0
     c                   goto      UtOpd
     c                   endif
     c                   eval      extStr = Str
     c                   eval      ext    = %subst(FNavn:Str)
      *
      * finn avd..
     c                   z-add     3             Str               3 0          StartP For søk
     c                   move      *zeros        Pos               3 0          Funnet i Pos
     c                   eval      Pos= %SCAN ('+' : FNavn : Str)
     c                   if        Pos=0
N01  c                             or (Pos - Str) > 4
N01   * Max lengde på avdeling er 4.
     c                   goto      UtOpd
     c                   endif
     c                   eval      AvdN = c2n(%subst(FNavn:Str:Pos-Str))
      * finn oppd.
     c                   eval      Str  = Pos+1
     c                   move      *zeros        Pos               3 0          Funnet i Pos
     c                   eval      Pos= %SCAN ('+' : FNavn : Str)
      * fant ikke +   så prøv  med .
     c                   if        Pos=0
     c                   eval      Pos= %SCAN ('.' : FNavn : Str)
     c                   endif
     c                   if        Pos=0
N01  c                             or (Pos - Str) > 7
N01   * Max lengde på oppdragsnr er 7.
     c                   goto      UtOpd
     c                   endif
      *
     c                   eval      OpdN = c2n(%subst(FNavn:Str:Pos-Str))
      *
      *
      * sett sammen fullt filnavn
     c                   move      AvdN          AvdA
     c                   move      OpdN          OpdA
      * Resten av filnavn....
      *   (kun extention gjenstår... Pos er '.' i file extention )
     c                   if        Pos = extStr
     c                   eval      Fnavn=%subst(FNavn:1:2)+AvdA+OpdA+%trim(ext)
     c                   else
      *   (beskrivelse/originalt filnavn finnes mellom avdOpd og extention. legg _ bak...
      *    (Pos er '+' etter OPD - hopp forbi + add 1 Pos )
     c                   eval      Pos = Pos + 1
     c                   eval      Fbeskr = %subst(FNavn:Pos:extStr-Pos)
     c                   eval      Fnavn=%subst(FNavn:1:2)+AvdA+OpdA+%trim(ext)
     c                             +'_' + %trim(%subst(FNavn:Pos))
     c                   endif
     C
      *
     c                   move      'J'           Fixed
      *
     C     UtOpd         ENDSR
     C***********************************************
     C     FIXTUR        BEGSR
     C***********************************************
      *   ulike gyldige varianter
      * a) ZT501845.pdf
      * a) ZT+501845.pdf
      * b) ZT+501845+OrigFilnavnxxx.pdf
      * d) 501845+Dette er original.pdf
      *   ulike UTdata varianter
      *    ZH00501845.pdf_2015-03-23-07.38.51.51.pdf      (som en normal web-upload)
      *    ZH00501845.pdf                                 (som et skannet dokument uten TS)
      *
      *
      * 2 første tegn = type
     c                   If        %subst(FNavn:1:2) < 'AA' or
     c                             %subst(FNavn:1:2) > 'Z9'
     c                   eval      FNavn=DftTyp+Fnavn
     c                   endif
      * dersomom 3.tegn = +, fjern det
     c                   If        %subst(FNavn:3:1) = '+'
     c                   eval      FNavn=%subst(FNavn:1:2)+%subst(FNavn:4)
     c                   endif
      * Fnavn skal nå være a la ZT5018.....
     c                   move      *zeros        TurN              8 0
     c                   move      *zeros        TurA              8
     c                   z-add     0             extStr            3 0          StartP For extent.
     c                   z-add     0             Str               3 0          StartP For søk
     c                   z-add     1             Pos               3 0          Funnet i Pos
      * Finn file extention..
     c                   dow       Pos<> 0
     c                   eval      Pos= %SCAN ('.' : FNavn : Str+1)
     c                   if        Pos<> 0
     c                   eval      Str= Pos
     c                   endif
     c                   enddo
     c                   if        Str = 0
     c                   goto      UtTur
     c                   endif
     c                   eval      extStr = Str
     c                   eval      ext    = %subst(FNavn:Str)
      *
      * finn Turnr
     c                   z-add     3             Str                            StartP For søk
     c                   move      *zeros        Pos                            Funnet i Pos
     c                   eval      Pos= %SCAN ('+' : FNavn : Str)
      * fant ikke +   så prøv  med .
     c                   if        Pos=0
     c                   eval      Pos= %SCAN ('.' : FNavn : Str)
     c                   endif
     c                   if        Pos=0
N01  c                             or (Pos - Str) > 8
N01   * For å takle ZT12345678.pdf+xxxxx.pdf
     c                   goto      UtTur
     c                   endif
      *
     c                   eval      TurN = c2n(%subst(FNavn:Str:Pos-Str))
      *
      *
      * sett sammen fullt filnavn
     c                   move      TurN          TurA
      * Resten av filnavn....
      *   (kun extention gjenstår... Pos er '.' i file extention )
     c                   if        Pos = extStr
     c                   eval      Fnavn=%subst(FNavn:1:2)+TurA+%trim(ext)
     c                   else
      *   (beskrivelse/originalt filnavn finnes mellom Tur og extention. legg _ bak...
      *    (Pos er '+' etter TUR - hopp forbi + add 1 Pos )
     c                   eval      Pos = Pos + 1
     c                   eval      Fbeskr = %subst(FNavn:Pos:extStr-Pos)
     c                   eval      Fnavn=%subst(FNavn:1:2)+TurA+%trim(ext)
     c                             +'_' + %trim(%subst(FNavn:Pos))
     c                   endif
     C
      *
     c                   move      'J'           Fixed
      *
     C     UtTur         ENDSR
