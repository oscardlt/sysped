      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKG003R MODULE(*LIBL/TDKG003R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FCUNDL02   IF   E           K DISK    USROPN
     F                                     RENAME(KUNDR:KUNDR2)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSONAVN         S             10
     D WKNR            S              8
     D WKNRN           S              8  0
     D STOPP           S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D ZB              S              1    DIM(10)

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬sonavn¬ : ¬'+%trim(WSONAVN)+'¬, '
        +'¬knr¬ : ¬'+%trim(WKNR)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"customerlist" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta kund/kunder
      *
     c     Error         ifeq      *blanks
     C     KEY1          SETLL     CUNDL01
     C     KEY2          SETLL     CUNDL02
     C                   IF        WKNRN > 0
     C     KEY1          READE     CUNDL01                                55
     C                   ELSE
     C     BIFIRM        READE     CUNDL02                                55
N01  C  N55SONAVN        CABGT     STOPP         UTLES
     C                   ENDIF
     C     *IN55         DOWEQ     *OFF
     C                   IF        AKTKOD='A'
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬kundnr¬ : ¬'+%trim(%editc(KUNDNR:'Z'))+'¬, '
          +'¬knavn¬ : ¬'+%trim(KNAVN)+'¬, '
          +'¬adr1¬ : ¬'+%trim(ADR1)+'¬, '
          +'¬adr2¬ : ¬'+%trim(ADR2)+'¬, '
          +'¬postnr¬ : ¬'+%trim(%editc(POSTNR:'Z'))+'¬, '
          +'¬adr3¬ : ¬'+%trim(ADR3)+'¬, '
          +'¬kpers¬ : ¬'+%trim(KPERS)+'¬, '
          +'¬tlf¬ : ¬'+%trim(TLF)+'¬, '
          +'¬sonavn¬ : ¬'+%trim(SONAVN)+'¬, '
          +'¬sypoge¬ : ¬'+%trim(SYPOGE)+'¬, '
          +'¬syland¬ : ¬'+%trim(SYLAND)+'¬, '
          +'¬syfr02¬ : ¬'+%trim(SYFR02)+'¬, '
          +'¬vatkku¬ : ¬'+%trim(VATKKU)+'¬, '
          +'¬golk¬ : ¬'+%trim(GOLK)+'¬, '
          +'¬eori¬ : ¬'+%trim(EORI)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C                   ENDIF
     C                   IF        WKNRN > 0
     C     KEY1          READE     CUNDL01                                55
     C                   ELSE
     C     BIFIRM        READE     CUNDL02                                55
N01  C  N55SONAVN        CABGT     STOPP         UTLES
     C                   ENDIF
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     CUNDL01
     C  N50              CLOSE     CUNDL02

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSONAVN = zhbgetvar('SONAVN')
     C                   EVAL      WKNR = zhbgetvar('KNR')
     C                   EVAL      WKNRN = c2n2(WKNR)
     C                   MOVEA     WSONAVN       ZB
     C     ZB(1)         IFNE      ' '
     C                   Z-ADD     0             ZAX               2 0
     C     1             DO        10            ZAX               2 0
     C     11            SUB       ZAX           WAX               2 0
     C     ZB(WAX)       CABNE     *BLANKS       SRB3
     C                   MOVE      '9'           ZB(WAX)
     C                   ENDDO
     C                   ENDIF
     C     SRB3          TAG
     C                   MOVEA     ZB            STOPP
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     KEY1          KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WKNRN
     C     KEY2          KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WSONAVN
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      CUNDL01
     C  N50              OPEN      CUNDL02

     c                   endsr
      *
