      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE033R MODULE(*LIBL/TSVE033R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVEF      IF   E           K DISK    USROPN
     FSVTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(SYAVA)+'¬, '
        +'¬opd¬ : ¬'+%trim(SYOPA)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invTot" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C                   EXSR      TOT
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tot_vakd¬ : ¬'+%trim(tot_vakd)+'¬, '
          +'¬tot_vaku¬ : ¬'+%trim(%editc(tot_vaku:'4'))+'¬, '
          +'¬tot_omr¬ : ¬'+%trim(%editc(tot_omr:'4'))+'¬, '
          +'¬tot_fabl¬ : ¬'+%trim(%editc(tot_fabl:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVEF
     C  N50              CLOSE     SVTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA= zhbgetvar('OPD')
     C                   EVAL      SYOP = c2n2(SYOPA)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVEFKEY       KLIST
     C                   KFLD                    SYAV
     C                   KFLD                    SYOP
     C     SVTVKKEY      KLIST
     C                   KFLD                    SVEF_VAKD
     C                   KFLD                    ZVVK_DTS
     C     *LIKE         DEFINE    SVEF_vakd     tot_vakd
     C     *LIKE         DEFINE    SVEF_vaku     tot_vaku
     C     *LIKE         DEFINE    SVVK_omr      tot_omr
     C     *LIKE         DEFINE    SVEF_fabl     tot_fabl
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS
     C     *LIKE         DEFINE    SVEF_fabl     W_FABL
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVEF
     C  N50              OPEN      SVTVK
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           ZVVK_DTS

     c                   endsr
      *_____________________________________________
      * Summera alla fakturor                    TOT
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     TOT           BEGSR
      * Finnes beløp ?
     C                   Z-ADD     0             X                 5 0
     C                   EVAL      *IN55=*OFF
     C     SVEFKEY       SETLL     SVEF
     C     SVEFKEY       READE     SVEF                                   55
     C     *IN55         DOWEQ     '0'
     C     SVEF_FATY     IFNE      *BLANKS
     C     SVEF_FATX     ANDNE     *BLANKS
     C     SVEF_VAKD     ANDNE     *BLANKS
     C     SVEF_VAKU     ANDNE     *ZEROS
     C     SVEF_FABL     ANDNE     *ZEROS

     C                   ADD       1             X
     C                   IF        X=1
     C                   EVAL      TOT_VAKD=SVEF_VAKD
     C                   ELSE
     C     SVEF_VAKD     IFNE      TOT_VAKD
     C                   EVAL      *IN71=*ON
     C                   EVAL      TOT_VAKD='SEK'
     C                   LEAVE
     C                   ENDIF
     C                   ENDIF

     C                   ENDIF
     C     SVEFKEY       READE     SVEF                                   55
     C                   ENDDO
     C     TOT_VAKD      CABEQ     *BLANKS       TOT9
      * Summera belopp
     C                   EVAL      *IN55=*OFF
     C     SVEFKEY       SETLL     SVEF
     C     SVEFKEY       READE     SVEF                                   55
     C     *IN55         DOWEQ     '0'
     C     SVEF_FATY     IFNE      *BLANKS
     C     SVEF_FATX     ANDNE     *BLANKS
     C     SVEF_VAKD     ANDNE     *BLANKS
     C     SVEF_VAKU     ANDNE     *ZEROS
     C     SVEF_FABL     ANDNE     *ZEROS
     C                   IF        *IN71
     C                   EXSR      TOTVAL
     C                   ADD       W_FABL        TOT_FABL
     C                   ELSE
     C                   ADD       SVEF_FABL     TOT_FABL
     C                   ENDIF
     C                   ENDIF
     C     SVEFKEY       READE     SVEF                                   55
     C                   ENDDO
      * Hæmta valutakurs och omrækningsfaktor
     C                   IF        TOT_VAKD='SEK'
     C                   EVAL      TOT_VAKU=1
     C                   EVAL      TOT_OMR=1
     C                   ELSE
     C                   EVAL      *IN64=*OFF
     C                   EVAL      SVEF_VAKD=TOT_VAKD
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  64
     C     *IN64         IFEQ      *OFF
     C                   EVAL      TOT_VAKU=SVVK_KRS
     C                   EVAL      TOT_OMR=SVVK_OMR
     C                   ENDIF
     C                   ENDIF
     C     TOT9          ENDSR
      *_____________________________________________
      * Rækna om till SEK                     TOTVAL
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     TOTVAL        BEGSR
     C                   EVAL      *IN64=*OFF
     C                   EVAL      W_FABL=*ZEROS
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  64
     C     *IN64         IFEQ      *OFF
     C     SVEF_FABL     MULT      SVVK_KRS      W_FABL
     C                   IF        SVVK_OMR > 0
     C     W_FABL        DIV       SVVK_OMR      W_FABL
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
