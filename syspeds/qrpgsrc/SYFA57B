     FSYFA57FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FCUNDL07   IF   E           K DISK
     F                                     RENAME(KUNDR:KUND7)
     FCUSAPR    UF A E           K DISK
     FCUSAAV1   IF   E           K DISK
     FCUFLD     UF A E           K DISK
     FKOFAST    IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D                 DS
     D  KNAVN                  1     30
     D  SLETT                  1      8
      *
      *
     C                   EXSR      INIT
     C* 51 ON=Kun en kunde - ikke vis subfil - (avslutt)
     C  N51              EXSR      HOVED
      *
      *
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C                   MOVEL     'SAMLEF.S'    DOKTYP           10
     C                   MOVE      'PE'          DOKTYP
      *
     C     *LIKE         DEFINE    WSKN          WWKN
     C     *ENTRY        PLIST
     C                   PARM                    WSKN
     C                   PARM                    UPDATE            1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     KUNKE7        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
      *
     C     CUFLDK        KLIST
     C                   KFLD                    FLDKN
     C                   KFLD                    DOKTYP
      *
     C     SORKEY        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'CPSORT'      KFTYP
      *
     C                   READ      FIRM                                   55
     C* Update tillatt ??
     C     UPDATE        COMP      'J'                                5050       *NONDISP
     C* Arbeid med KUN EN kunde (ikke subfil) rett til NEW/CHANGE
     C     WSKN          COMP      *ZEROS                             5151       *PROTECT
     C     *IN51         IFEQ      '1'
     C                   MOVE      WSKN          KUNDNR
     C     KUNDNR        CHAIN(N)  CUSAPR                             55
     C   55              EXSR      CRTNEW
     C  N55              EXSR      CHANGE
     C                   END
      * Sett indikator for fil som skal leses/skjerm overskrift
     C                   MOVEA     '100'         *IN(41)
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      WSKN          WWKN
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C   41KUNKE7        SETLL     CUNDL07
     C   42WSKN          SETLL     CUSAPR
     C     NESTE         TAG
     C   43WSKN          SETLL     CUSAAV1
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C   41FIFIRM        READE     CUNDL07                                94
     C   42              READ(N)   CUSAPR                                 94
     C   43              READ      CUSAAV1                                94
     C     *IN94         DOWEQ     '0'
      *
      * 43= KUN de kunder som HAR SATT KODE på avd.nivå,MEN BLANK HOVED
     C     *IN43         IFEQ      '1'
     C                   MOVE      CAKN          KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C*    aldri mer enn en post i subfila pr kunde
     C     KUNDNR        ADD       1             WSKN
     C*    hvis ikke gyldig kunde med blank i kode - neste kunde
     C     *IN33         IFEQ      '1'
     C     SFAKT         ORNE      ' '
     C                   GOTO      NESTE
     C                   END
     C                   END
      *
     C     *IN42         IFEQ      '1'
     C                   MOVE      CPKN          KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   END
      *
     C     *IN41         IFEQ      '1'
     C     *IN43         OREQ      '1'
     C                   MOVE      *ZEROS        CPKN
     C     KUNDNR        CHAIN(N)  CUSAPR                             33
     C                   END
      *
     C     CPKN          IFEQ      *ZEROS
     C                   MOVEL     'STD.'        XXSTD
     C                   MOVEL     'LAYOUT'      XXLAY
     C                   MOVE      ' '           XXSP
     C                   MOVE      ' '           XXSPAK
     C                   MOVE      ' '           XXKO
     C                   MOVE      ' '           XXKOAK
     C                   ELSE
     C                   MOVE      *BLANKS       XXSTD
     C                   MOVE      *BLANKS       XXLAY
     C                   MOVE      CPSP          XXSP
     C                   MOVE      CPSPAK        XXSPAK
     C                   MOVE      CPKO          XXKO
     C                   MOVE      CPKOAK        XXKOAK
     C                   END
      *
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C   41FIFIRM        READE     CUNDL07                                94
     C   42              READ(N)   CUSAPR                                 94
     C   43WSKN          SETLL     CUSAAV1
     C   43              READ      CUSAAV1                                94
     C  N94              END
      *
      * Reset fra-og-med-key..
     C   43              MOVE      WWKN          WSKN
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C   41              MOVE      'JOV0465'     ZMSGNO
     C   42              MOVE      'JOV0443'     ZMSGNO
     C   43              MOVE      'JOV0466'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C     *IN03         CABEQ     '1'           UTHOV
      *_____________
      * Endret modus (filvalg)
      *"""""""""""""
     C     *IN07         IFEQ      '1'
     C     *IN08         OREQ      '1'
     C     *IN09         OREQ      '1'
     C                   EXSR      CHGMOD
     C                   END
      * Nytt forsøk med endret modus / evt. endret utvalg/startpkt..
     C                   GOTO      START
     C                   END
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *
      *_____________
      * Endret modus (filvalg)
      *"""""""""""""
     C     *IN07         IFEQ      '1'
     C     *IN08         OREQ      '1'
     C     *IN09         OREQ      '1'
     C                   EXSR      CHGMOD
     C                   GOTO      START
     C                   END
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   MOVE      *ON           *IN55
     C                   MOVE      *ZEROS        KUNDNR
     C                   EXSR      CRTNEW
     C                   GOTO      START
     C                   END
      *_____________
      * Options på topplinja
      *"""""""""""""
     C     TOPOPT        IFNE      ' '
     C     TOPOPT        IFEQ      '1'
     C                   MOVE      *ON           *IN55
     C                   MOVE      WSKN          KUNDNR
     C                   EXSR      CRTNEW
     C                   ELSE
     C                   MOVE      *BLANKS       SLETT
     C                   MOVE      TOPOPT        WSNR
     C                   MOVE      WSKN          KUNDNR
     C                   EXSR      OPTION
     C                   END
      * Søk utvalg til gammel verdi.. Vis subfila fra front igjen..
     C                   MOVE      WWKN          WSKN
     C                   MOVE      ' '           TOPOPT
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSKN          IFNE      WWKN
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CHGMOD        BEGSR
      *////////////////////////////////////////////////////////////
      * Endring av modus (hvilke fil som leses)
      *
      *_____________
      * F7=Les på ny - Alle med hovedkode ulik blank
      *"""""""""""""
     C     *IN07         IFEQ      '1'
     C                   MOVEA     '100'         *IN(41)
     C                   MOVE      *ZEROS        WSKN
     C                   ELSE
      *_____________
      * F8=Les på ny - Alle som har egen S-fakt-profil
      *"""""""""""""
     C     *IN08         IFEQ      '1'
     C                   MOVEA     '010'         *IN(41)
     C                   MOVE      *ZEROS        WSKN
     C                   ELSE
      *_____________
      * F9=Les på ny - Alle med hovedkode LIK blank men AVDKODER ULIK
      *"""""""""""""
     C     *IN09         IFEQ      '1'
     C                   MOVEA     '001'         *IN(41)
     C                   MOVE      *ZEROS        WSKN
     C                   END
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
     C*
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         ANDNE     '*SLETTET'
     C     KUNDNR        CHAIN     CUSAPR                             55
     C  N55              DELETE    CUSAPRR
      * Sett også evt. felt i spesifikasj.layout
     C                   MOVE      KUNDNR        FLDKN
     C                   EXSR      DLTFLD
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   MOVE      *BLANKS       CPKO
     C                   MOVE      *ZEROS        CPKOAK
     C                   MOVE      *BLANKS       CPSP
     C                   MOVE      *ZEROS        CPSPAK
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVEL     '*SLETTET'    KNAVN
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         ANDNE     '*SLETTET'
     C     KUNDNR        CHAIN(N)  CUSAPR                             55
     C     *IN55         IFEQ      '0'
     C                   EXSR      CHANGE
     C                   ELSE
     C                   EXSR      CRTNEW
     C                   END
     C                   MOVE      ' '           WSNR
     C     *IN55         IFEQ      '0'                                          *ikke ny..
     C     TOPOPT        ANDEQ     ' '                                          *ikke topopt
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C     WSNR          IFEQ      '3'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         ANDNE     '*SLETTET'
     C     KUNDNR        CHAIN(N)  CUSAPR                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      '1'           *IN55
     C                   EXSR      CRTNEW
     C                   END
     C                   END
      *
     C     WSNR          IFEQ      '5'
     C     SLETT         ANDNE     '*SLETTET'
     C     KUNDNR        CHAIN(N)  CUSAPR                             55
     C     *IN55         IFEQ      '0'
     C                   EXSR      CHANGE
     C                   END
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '                                          *ikke topopt
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C     WSNR          IFEQ      '9'
     C     SLETT         ANDNE     '*SLETTET'
     C                   CALL      'SYFA57'
     C                   PARM                    KUNDNR
     C                   PARM                    UPDATE
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '                                          *ikke topopt
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     CHANGE        BEGSR
      *////////////////////////////////////////////////////////////
      * 55-ON - Se på / protect
     C     WSNR          COMP      '5'                                    56
     C                   EXSR      MOVFW
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     28            POSNBR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       ADR1
     C                   MOVE      *BLANKS       ADR2
     C                   MOVE      *ZEROS        POSTNR
     C                   MOVE      *BLANKS       ADR3
     C                   END
     C                   MOVE      '0'           *IN30
     C                   MOVE      'SY00000'     ZMSGNO
      * Vis forklarende tekst Sorteringskode
     C                   MOVE      *BLANKS       WSSONA
     C     WSSORT        IFNE      ' '
     C                   MOVEL     WSSORT        KFKOD
     C     SORKEY        CHAIN     KOFAST                             30
     C     *IN30         IFNE      '1'
     C     FILAND        IFEQ      'NO'
     C                   MOVEL     KFTXT         WSSONA
     C                   ELSE
     C                   MOVEL     KFTXTE        WSSONA
     C                   END
     C                   END
     C                   END
     C     STCHG         TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFNE      '1'
     C     WSNR          ANDNE     '5'                                            5=se på
      * "Manuelt" søk via F4  KUNDE/FRIE-SØKEVEIER/SORT-KODE
     C     *IN04         IFEQ      '1'
     C                   EXSR      SR04
     C                   MOVE      '0'           *IN30
     C                   MOVE      'SY00000'     ZMSGNO
     C                   GOTO      STCHG
     C                   END
     C* Test Sorteringskode
     C     WSSORT        IFNE      ' '
     C                   MOVEL     WSSORT        KFKOD
     C     SORKEY        CHAIN     KOFAST                             30
     C     *IN30         IFEQ      '1'
     C                   MOVE      'JOV0546'     ZMSGNO
     C                   Z-ADD     15            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      *BLANKS       WSSONA
     C                   GOTO      STCHG
     C                   END
     C     FILAND        IFEQ      'NO'
     C                   MOVEL     KFTXT         WSSONA
     C                   ELSE
     C                   MOVEL     KFTXTE        WSSONA
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       WSSONA
     C                   END
     C     KUNDNR        CHAIN     CUSAPR                             55
     C     *IN55         IFNE      '1'
     C                   EXSR      MOVWF
     C                   UPDATE    CUSAPRR
     C                   END
     C     *IN08         IFEQ      '1'
     C                   CALL      'SYFA57C'
     C                   PARM                    KUNDNR
     C                   PARM                    DOKTYP
     C                   END
     C                   END
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTNEW        BEGSR
      *////////////////////////////////////////////////////////////
     C                   CLEAR                   CHGREC
     C                   MOVEA     '10'          *IN(55)
     C* Ved kopiering. hent data fra annen lest profil
     C                   MOVE      *ZEROS        FLDKN             8 0
     C     WSNR          IFEQ      '3'
     C                   MOVE      ' '           WSNR
     C                   MOVE      KUNDNR        FLDKN
     C     TOPOPT        IFEQ      ' '                                          *ikke topopt
     C                   UPDATE    SUBFIL
     C                   END
     C                   EXSR      MOVFW
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     11            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0468'     ZMSGNO
     C                   GOTO      STNEW
     C                   END
     C* Kundenr fra innparam (v.kall fra kunde) eller fra topplinja..
     C                   MOVE      KUNDNR        CPKN
     C                   MOVE      'O'           WSKO
     C                   Z-ADD     1             WSKOAK
     C                   MOVE      'J'           WSSP
     C                   Z-ADD     1             WSSPAK
     C                   MOVE      'J'           WSFALI
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     28            POSNBR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       ADR1
     C                   MOVE      *BLANKS       ADR2
     C                   MOVE      *ZEROS        POSTNR
     C                   MOVE      *BLANKS       ADR3
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     11            POSNBR
     C                   END
     C                   MOVE      '0'           *IN30
     C                   MOVE      'SY00000'     ZMSGNO
     C*
     C     STNEW         TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFNE      '1'
      * "Manuelt" søk via F4  KUNDE/FRIE-SØKEVEIER/SORT-KODE
     C     *IN04         IFEQ      '1'
     C                   EXSR      SR04
     C                   MOVE      '0'           *IN30
     C                   MOVE      'SY00000'     ZMSGNO
     C                   GOTO      STNEW
     C                   END
      * "Autosøk"
     C     CPKN          IFEQ      0
     C     KNAVN         ANDNE     *BLANKS
     C                   CALL      'SOEKKUN'
     C                   PARM                    CPKN
     C                   PARM                    FIFIRM
     C                   PARM                    KNAVN
     C     CPKN          IFNE      0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   END
     C                   MOVE      'SY00000'     ZMSGNO
     C                   GOTO      STNEW
     C                   END
     C* Test kundenr
     C                   MOVE      CPKN          KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            30
     C     *IN30         IFEQ      '1'
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       ADR1
     C                   MOVE      *BLANKS       ADR2
     C                   MOVE      *ZEROS        POSTNR
     C                   MOVE      *BLANKS       ADR3
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     11            POSNBR
     C                   MOVE      'SPK0001'     ZMSGNO
     C                   GOTO      STNEW
     C                   END
     C* Kunden kan ikke ha profil fra før.
     C     KUNDNR        CHAIN(N)  CUSAPR                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      *ON           *IN30
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     11            POSNBR
     C                   MOVE      'JOV0569'     ZMSGNO
     C                   GOTO      STNEW
     C                   END
     C* Test Sorteringskode
     C     WSSORT        IFNE      ' '
     C                   MOVEL     WSSORT        KFKOD
     C     SORKEY        CHAIN     KOFAST                             30
     C     *IN30         IFEQ      '1'
     C                   MOVE      'JOV0546'     ZMSGNO
     C                   Z-ADD     15            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      *BLANKS       WSSONA
     C                   GOTO      STNEW
     C                   END
     C     FILAND        IFEQ      'NO'
     C                   MOVEL     KFTXT         WSSONA
     C                   ELSE
     C                   MOVEL     KFTXTE        WSSONA
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       WSSONA
     C                   END
     C* OK - Skriv NY post til fil (Hvis finnes - dummy update)
     C     KUNDNR        CHAIN     CUSAPR                             55
     C     *IN55         IFEQ      '0'
     C                   UPDATE    CUSAPRR
     C                   ELSE
     C                   EXSR      MOVWF
     C                   WRITE     CUSAPRR
      * Hvis det er kopiering - kopier også felt utvalg spesifikasjon
     C     FLDKN         IFNE      *ZEROS
     C     WSSP          ANDEQ     'J'
     C                   EXSR      KOPFLD
     C                   END
      * AKTIVT F8 ELLER PÅSATT AUTOM. I KOPFLD...
     C     *IN08         IFEQ      '1'
     C                   CALL      'SYFA57C'
     C                   PARM                    KUNDNR
     C                   PARM                    DOKTYP
     C                   END
     C* 42 ON= Vis de med egen profil - KUN DA ADD I SUBFILA....
     C* (i andre modus MÅ en ta F5 el.l. for å fornye bildet)  .
     C     *IN42         IFEQ      '1'
     C     XRECNO        ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   END
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     SR04          BEGSR
      *////////////////////////////////////////////////////////////
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     LINNBR        IFLT      6
     C                   CALL      'SOEKKUN'
     C                   PARM                    CPKN
     C                   PARM                    FIFIRM
     C                   PARM                    KNAVN
     C     CPKN          IFNE      0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   END
     C                   GOTO      UTSR04
     C                   END
      *
     C     LINNBR        IFEQ      14
     C     POSNBR        IFEQ      28
     C                   CALL      'SYFA65A'
     C                   PARM                    WSFS01
     C                   Z-ADD     32            POSNBR
     C                   GOTO      UTSR04
     C                   END
     C     POSNBR        IFEQ      32
     C                   CALL      'SYFA65A'
     C                   PARM                    WSFS02
     C                   Z-ADD     36            POSNBR
     C                   GOTO      UTSR04
     C                   END
     C     POSNBR        IFEQ      36
     C                   CALL      'SYFA65A'
     C                   PARM                    WSFS03
     C                   Z-ADD     40            POSNBR
     C                   GOTO      UTSR04
     C                   END
     C     POSNBR        IFEQ      40
     C                   CALL      'SYFA65A'
     C                   PARM                    WSFS04
     C                   Z-ADD     44            POSNBR
     C                   GOTO      UTSR04
     C                   END
     C     POSNBR        IFEQ      44
     C                   CALL      'SYFA65A'
     C                   PARM                    WSFS05
     C                   Z-ADD     28            POSNBR
     C                   Z-ADD     15            LINNBR
     C                   GOTO      UTSR04
     C                   END
     C                   END
      *
     C     LINNBR        IFEQ      15
     C                   MOVEL     'CPSORT'      KDTYPE           10
     C                   MOVE      *BLANKS       KDKODE            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    KDTYPE
     C                   PARM                    KDUPD             1
     C                   PARM                    KDKODE
     C     KDUPD         IFNE      'X'
     C                   MOVEL     KDKODE        WSSORT
     C                   Z-ADD     16            LINNBR
     C                   ELSE
     C                   Z-ADD     15            LINNBR
     C                   END
     C                   Z-ADD     28            POSNBR
     C                   GOTO      UTSR04
     C                   END
      *
     C     UTSR04        ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     MOVWF         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      WSKO          CPKO
     C                   MOVE      WSKOAK        CPKOAK
     C                   MOVE      WSKOHE        CPKOHE
     C                   MOVE      WSHEN1        CPHEN1
     C                   MOVE      WSHEN2        CPHEN2
     C                   MOVE      WSSP          CPSP
     C                   MOVE      WSSPAK        CPSPAK
     C                   MOVE      WSSPH1        CPSPH1
     C                   MOVE      WSSPH2        CPSPH2
     C                   MOVE      WSSPH3        CPSPH3
     C                   MOVE      WSFALI        CPFALI
     C                   MOVE      WSSPSU        CPSPSU
     C                   MOVE      WSSORT        CPSORT
     C                   MOVE      WSFS01        CPFS01
     C                   MOVE      WSFS02        CPFS02
     C                   MOVE      WSFS03        CPFS03
     C                   MOVE      WSFS04        CPFS04
     C                   MOVE      WSFS05        CPFS05
     C     CPSP          IFEQ      'J'
     C     CPSPAK        IFEQ      0
     C                   MOVE      1             CPSPAK
     C                   END
     C                   ELSE
     C                   MOVE      0             CPSPAK
     C                   MOVE      'N'           CPFALI
     C                   END
     C                   MOVE      *BLANKS       XXSTD
     C                   MOVE      *BLANKS       XXLAY
     C                   MOVE      CPSP          XXSP
     C                   MOVE      CPSPAK        XXSPAK
     C                   MOVE      CPKO          XXKO
     C                   MOVE      CPKOAK        XXKOAK
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     MOVFW         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      CPKO          WSKO
     C                   MOVE      CPKOAK        WSKOAK
     C                   MOVE      CPKOHE        WSKOHE
     C                   MOVE      CPHEN1        WSHEN1
     C                   MOVE      CPHEN2        WSHEN2
     C                   MOVE      CPSP          WSSP
     C                   MOVE      CPSPAK        WSSPAK
     C                   MOVE      CPSPH1        WSSPH1
     C                   MOVE      CPSPH2        WSSPH2
     C                   MOVE      CPSPH3        WSSPH3
     C                   MOVE      CPFALI        WSFALI
     C                   MOVE      CPSPSU        WSSPSU
     C                   MOVE      CPSORT        WSSORT
     C                   MOVE      CPFS01        WSFS01
     C                   MOVE      CPFS02        WSFS02
     C                   MOVE      CPFS03        WSFS03
     C                   MOVE      CPFS04        WSFS04
     C                   MOVE      CPFS05        WSFS05
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     KOPFLD        BEGSR
      *////////////////////////////////////////////////////////////
     C     CUFLDK        SETLL     CUFLD
     C     CUFLDK        READE     CUFLD                                  33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      *ON           *IN08
     C                   MOVE      CPKN          CFKEY1
     C                   WRITE     CUFLDR
     C     CUFLDK        READE     CUFLD                                  33
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     DLTFLD        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      CPKN          FLDKN
     C     CUFLDK        SETLL     CUFLD
     C     CUFLDK        READE     CUFLD                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    CUFLDR
     C     CUFLDK        READE     CUFLD                                  33
     C                   END
     C                   ENDSR
