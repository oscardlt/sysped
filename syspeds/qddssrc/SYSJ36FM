*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5081A* 01 YBC PTF5 +/- BELØP ELLER PROSENT VED KOPIERING
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HLPPNLGRP(HLPSCR SYSJ36PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV CONTAINER--
     A                                      RATER')
     A                                      HELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 YBC0128 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  5  2MSGCON(020 DIV1633 *LIBL/SYMSGF)
     A            WSKNR          8Y 0B  5 25DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  5 40MSGCON(030 YBC0129 *LIBL/SYMSGF)
     A                                  8  2MSGCON(020 SHU1156 *LIBL/SYMSGF)
     A            WSTRA          4   B  8 25
     A                                  7  2MSGCON(030 YBC0130 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10  2MSGCON(020 DIV1634 *LIBL/SYMSGF)
     A            WSFRA          5A  B 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A                                 12  2MSGCON(020 YBC0131 *LIBL/SYMSGF)
     A            WSTIL          5A  B 12 25DSPATR(UL)
     A                                 14  2MSGCON(020 YBC0132 *LIBL/SYMSGF)
     A            WSTYPE         2A  B 14 25DSPATR(UL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4')
     A            FTTRA          4A  O  5  6COLOR(WHT)
     A            FTFRA          5A  O  5 13
     A  34                                  COLOR(WHT)
     A            FTFRAN         3A  O  5 21
     A            FTOPDT         2A  O  5 25
     A            FTSK           1A  O  5 34
     A            FTTIL          5A  O  5 38
     A  35                                  COLOR(WHT)
     A            FTTYPE         2A  O  5 47
     A  36                                  COLOR(WHT)
     A            FTPRIS         9Y 2O  5 58EDTCDE(J)
     A            FTVAL          3A  O  5 72
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
     A* 27 AV = BRUTTO FRAKTAVTALER
     A N27                              1 20MSGCON(040 DIV1482 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A* 27 AV = KUNDERS FRAKTAVTALER
     A  27                              1 20MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              2  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  27        WSKNR          8Y 0O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        KNAVN         30A  O  2 24COLOR(WHT)
     A                                  3  2MSGCON(060 YBC0126 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 YBC0127 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'SLETTE ALLE AVT.')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2 62MSGCON(18 YBC0148 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A          H                           HLPPNLGRP(SYSJ36015 SYSJ36PNL)
     A                                      HLPARA(05 001 08 080)
     A          H                           HLPPNLGRP(SYSJ36020 SYSJ36PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(SYSJ36025 SYSJ36PNL)
     A                                      HLPARA(10 001 11 080)
     A          H                           HLPPNLGRP(SYSJ36030 SYSJ36PNL)
     A                                      HLPARA(12 001 13 080)
     A          H                           HLPPNLGRP(SYSJ36035 SYSJ36PNL)
     A                                      HLPARA(14 001 15 080)
     A          H                           HLPPNLGRP(SYSJ36999 SYSJ36PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(040 DIV1482 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A  27                              3  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  27        WSKNR          8Y 0O  3 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        KNAVN         30A  O  3 24COLOR(WHT)
     A  40                              5  2MSGCON(020 YBC0142 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40                              5  2MSGCON(020 YBC0143 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(020 SHU1156 *LIBL/SYMSGF)
     A            W2TRA          4A  B  6 25
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                  7  2MSGCON(020 DIV1635 *LIBL/SYMSGF)
     A            W2FRAN         3A  B  7 25TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                  8  2MSGCON(020 DIV1636 *LIBL/SYMSGF)
     A            W2OPDT         2A  B  8 25TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                  8 40MSGCON(032 DIV1650 *LIBL/SYMSGF)
     A                                  9  2MSGCON(020 YBC0165 *LIBL/SYMSGF)
     A            W2SK           1A  B  9 25TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      VALUES(' ' 'S' 'K' 'A')
     A                                 10  2MSGCON(020 DIV1634 *LIBL/SYMSGF)
     A            W2FRA          5A  B 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                 11  2MSGCON(020 YBC0131 *LIBL/SYMSGF)
     A            W2TIL          5A  B 11 25DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                 12  2MSGCON(020 YBC0132 *LIBL/SYMSGF)
     A            W2TYPE         2A  B 12 25DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  85                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                 14  2MSGCON(020 YBC0133 *LIBL/SYMSGF)
     A            W2PRIS         9Y 2B 14 25DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(J)
     A                                 15  2MSGCON(020 YBC0134 *LIBL/SYMSGF)
     A            W2VAL          3A  B 15 25DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A          H                           HLPPNLGRP(SYSJ36040 SYSJ36PNL)
     A                                      HLPARA(01 001 22 080)
     A          H                           HLPPNLGRP(SYSJ36999 SYSJ36PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(040 DIV1482 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4 25MSGCON(010 YBC0137 *LIBL/SYMSGF)
     A                                  4 40MSGCON(010 YBC0138 *LIBL/SYMSGF)
     A                                  4 52MSGCON(007 YBC0159 *LIBL/SYMSGF)
     A  27                              5  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  27        WSKNR          8Y 0O  5 25COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2KNR          8Y 0B  5 40COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  5 60MSGCON(015 YBC0149 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(020 SHU1156 *LIBL/SYMSGF)
     A            WKTRA          4   O  6 25
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2TRA          4   B  6 40
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2MSGCON(020 DIV1635 *LIBL/SYMSGF)
     A            WKFRAN         3A  O  7 25TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2FRAN         3A  B  7 40TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 60MSGCON(012 YBC0139 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(020 DIV1636 *LIBL/SYMSGF)
     A            WKOPDT         2A  O  8 25TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2OPDT         2A  B  8 40TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8 60MSGCON(012 YBC0139 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  9  2MSGCON(020 YBC0165 *LIBL/SYMSGF)
     A            WKSK           1A  O  9 25TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2SK           1A  B  9 40TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'S' 'K' 'A')
     A                                  9 60MSGCON(012 YBC0139 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 10  2MSGCON(020 DIV1634 *LIBL/SYMSGF)
     A            WKFRA          5A  O 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2FRA          5A  B 10 40TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 60MSGCON(012 YBC0139 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11  2MSGCON(020 YBC0131 *LIBL/SYMSGF)
     A            WKTIL          5A  O 11 25DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2TIL          5A  B 11 40DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 60MSGCON(012 YBC0139 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 12  2MSGCON(020 YBC0132 *LIBL/SYMSGF)
     A            WKTYPE         2A  O 12 25DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2TYPE         2A  B 12 40DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 60MSGCON(012 YBC0139 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 13 40MSGCON(010 YBC0712 *LIBL/SYMSGF)
     A            W2BL           7Y 0B 13 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                 13 62MSGCON(008 YBC0713 *LIBL/SYMSGF)
     A            W2PR           5Y 2B 13 71DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                 15 40MSGCON(035 YBC0150 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 16 40MSGCON(035 YBC0151 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 40MSGCON(035 YBC0152 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 18 40MSGCON(035 YBC0153 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 19 40MSGCON(035 YBC0154 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 21 40MSGCON(035 YBC0160 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 YBC0155 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A  27                              3  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  27        WSKNR          8Y 0O  3 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        KNAVN         30A  O  3 24COLOR(WHT)
     A                                  5  2MSGCON(040 YBC0144 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(020 SHU1156 *LIBL/SYMSGF)
     A            FTTRA          4A  O  6 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(020 DIV1635 *LIBL/SYMSGF)
     A            FTFRAN         3A  O  7 25TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(020 DIV1636 *LIBL/SYMSGF)
     A            FTOPDT         2A  O  8 25TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(020 YBC0165 *LIBL/SYMSGF)
     A            FTSK           1A  O  9 25TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(020 DIV1634 *LIBL/SYMSGF)
     A            FTFRA          5A  O 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(020 YBC0131 *LIBL/SYMSGF)
     A            FTTIL          5A  O 11 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2MSGCON(020 YBC0132 *LIBL/SYMSGF)
     A            FTTYPE         2A  O 12 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(020 YBC0133 *LIBL/SYMSGF)
     A            FTPRIS         9Y 2O 14 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(J)
     A                                 15  2MSGCON(020 YBC0134 *LIBL/SYMSGF)
     A            FTVAL          3A  O 15 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
      * 27 AV = BRUTTO FRAKTAVTALER
     A N27                              1 20MSGCON(40 DIV1482 SYMSGF)
     A                                      COLOR(WHT)
      * 27 AV = KUNDERS FRAKTAVTALER
     A  27                              1 20MSGCON(40 DIV1632 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              2  2MSGCON(10 DIV0027 SYMSGF)
     A  27        WSKNR          8  0O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        KNAVN         30   O  2 24COLOR(WHT)
     A                                  3  2MSGCON(60 YBC0126 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0127 SYMSGF)
     A                                      COLOR(WHT)
     A N27                              6  2MSGCON(78 YBC0136 SYMSGF)
     A                                      COLOR(RED)
     A  27                              6  2MSGCON(78 YBC0135 SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL2                   SFL
     A            XJN            1A  H
     A            FTKNR          8S 0O  5  2
     A N29                                  COLOR(RED)
     A            FTTRA          4A  O  5 11
     A N29                                  COLOR(RED)
     A            FTFRAN         3A  O  5 17
     A N29                                  COLOR(RED)
     A            FTOPDT         2A  O  5 23
     A N29                                  COLOR(RED)
     A            FTSK           1A  O  5 26
     A N29                                  COLOR(RED)
     A            FTFRA          5A  O  5 30
     A N29                                  COLOR(RED)
     A            FTTIL          5A  O  5 38
     A N29                                  COLOR(RED)
     A            FTTYPE         2A  O  5 46
     A N29                                  COLOR(RED)
     A            FTPRIS         9Y 2O  5 57EDTCDE(J)
     A N29                                  COLOR(RED)
     A            FTVAL          3A  O  5 71
     A N29                                  COLOR(RED)
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  61                                  SFLDSP
     A  62                                  SFLDSPCTL
     A  63                                  SFLCLR
     A  64                                  SFLEND
     A  61N63N64                            ROLLUP(25)
     A  61N63N64                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYSJ36045 SYSJ36PNL)
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYSJ36'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 YBC0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  28                              2  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  28        W2KNR          8Y 0O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  28        KNAVN         30A  O  2 24COLOR(WHT)
     A            ZRECN2         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(078 YBC0146 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'AVBRYD')
     A                                      CF21(21 'OVERSKRIVE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  31                              3 22MSGCON(45 YBC0145 SYMSGF)
     A                                      COLOR(BLU)
     A N31                              3 22MSGCON(25 YBC0157 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      CF10(10 'SLETTE')
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2MSGCON(25 YBC0158 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(18 YBC0148 SYMSGF)
     A                                  4  2MSGCON(18 DIV0024 SYMSGF)
