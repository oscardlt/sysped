     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/DOKUFE)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'SØK')
     A                                      CF07(07 'NÆRMESTE')
     A                                      CF10(10 'VIA BRING')
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'BEKREFT')
     A          R SYCTA01DA
     A                                  1  3'SYCTRMYR'
     A                                      COLOR(BLU)
     A                                  1 15'Kontaktopplysninger'
     A                                      COLOR(WHT)
     A            ZE_TYP        20O  O  1 39COLOR(WHT)
     A                                  2 15'Ved MyPack'
     A                                      COLOR(WHT)
     A                                  4  1'Avd/Opd/Fbnr:'
     A            ZE_DFAVD  R        O  4 15REFFLD(FE_DFAVD)
     A                                      EDTCDE(4)
     A            ZE_DFOPD  R        O  4 20REFFLD(FE_DFOPD)
     A                                      EDTCDE(4)
     A            ZE_DFFBNR R        O  4 28REFFLD(FE_DFFBNR)
     A                                      EDTCDE(4)
     A                                  5 15'MOTTAGER'
     A                                      COLOR(WHT)
     A                                  6  2'Navn . . . :'
     A            WSNAVM        30A  B  6 15TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Adresse. . :'
     A            WSAD1M        30A  B  7 15TEXT('Adresse Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2'Postn/sted.:'
     A            WSAD3M        30A  B  8 15TEXT('P-adr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2'Telefon  . :'
     A            ZE_C3148A R        B  9 15REFFLD(FE_C3148A)
     A                                 10  2'Mobil  . . :'
     A            ZE_C3148B R        B 10 15REFFLD(FE_C3148B)
     A                                 11  2'Telefax  . :'
     A            ZE_C3148C R        B 11 15REFFLD(FE_C3148C)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 12  2'SMS  . . . :'
     A            ZE_C3148D R        B 12 15REFFLD(FE_C3148D)
     A                                 13  2'E-Post . . :'
     A            ZE_C3148E R        B 13 15REFFLD(FE_C3148E)
     A                                      CHECK(LC)
     A            WARNING       75A  O 14  2COLOR(RED)
     A                                 15 15'TOLLPOST SERVICEPARTNER'
     A                                      COLOR(WHT)
     A  50                             15 50'---- ÅPNINGSTIDER ----'
     A                                      COLOR(WHT)
     A                                 16  2'Navn . . . :'
     A            WSNAVL        30A  B 16 15TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  50                             16 46'Man'
     A  50        OMAN          11A  O 16 50COLOR(WHT)
     A  50                             16 63'Tor'
     A  50        OTOR          11A  O 16 67COLOR(WHT)
     A                                 17  2'Adresse. . :'
     A            WSAD1L        30A  B 17 15TEXT('Leveringsadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  50                             17 46'Tir'
     A  50        OTIR          11A  O 17 50COLOR(WHT)
     A  50                             17 63'Fre'
     A  50        OFRE          11A  O 17 67COLOR(WHT)
     A                                 18  2'Postn/sted.:'
     A            WSPOUL    R        B 18 15REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTWRD('    ')
     A            WSADUL        25A  B 18 20TEXT('Utleveringssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  50                             18 46'Ons'
     A  50        OONS          11A  O 18 50COLOR(WHT)
     A  50                             18 63'Lør'
     A  50        OLØR          11A  O 18 67COLOR(WHT)
     A                                 19  2'Kundenr....:'
     A            WSKUNR        12A  O 19 15COLOR(YLW)
     A N50 51                          19 32'<-- KUNDE 0413898 = MYPACK VIA BRI-
     A                                      NG'
     A                                      COLOR(WHT)
     A  50                             19 46'Avst:'
     A  50        TPDIST7        7Y 0O 19 52EDTCDE(Z)
     A  50                                  COLOR(WHT)
     A                                      DSPATR(RI)
     A  50                             19 60'm'
     A  50                             19 63'Søn'
     A  50        OSØN          11A  O 19 67COLOR(WHT)
     A                                 20 15MSGCON(020 JOV1514 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 21  2'Søk/se web.:'
     A            TGURL        145A  O 21 15
     A                                 23  1'F3=Avslutt   F4=Søk Servicepartner-
     A                                            F12=Avbryt'
     A                                      COLOR(BLU)
     A                                 20 36MSGCON(020 JOV1545 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A          R SYCTA01DB
     A                                      ASSUME
     A                                  1  4' '
