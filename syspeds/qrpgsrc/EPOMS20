      *********************************************************************
      * Hent kundenr for å jobbe med POMS
      *
CRTPG+*  CRTPGM SYSPED/EPOMS20 MODULE(*LIBL/EPOMS20
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSKN            S              8
     D BSKN            S              8  0
     D XFEILTXT        S             80
     D XOK             S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEIL01          C                   CONST('Client number is not valid.')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   IF        XINTERN <> 'J' AND XINTERN <> 'j'
     C                   exsr      SetSkl01B
     C                   GOTO      SLUTT
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
      *
     C                   IF        BSKN = 0
     C                   MOVE      BIKUNR        BSKN
     C                   END
     C                   IF        BSKN = 0
     C                   EVAL      XOK = 'N'
     C                   MOVE      BIKUNR        BSKN
     C                   ELSE
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C   33              EVAL      XFEILTXT = FEIL01
     C  N33              EVAL      XOK = 'J'
     C                   END
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * bunn
     C                   callp     wrtsection('bot')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     updHTMLvar('FEILTXT':'.')
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('XOK':XOK)
     C                   callp     updHTMLvar('FEILTXT':XFEILTXT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     c                   callp     gethtmlifs(
cccccC                             '/syspeddev/html/EPOMS20.html':
cccccC                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      BRPARF
     C                   OPEN      CUNDL01
      *
     C     kunkey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     CUNDL01K      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BSKN
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVEL     'INTER'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      'N'           XINTERN           1
     C                   ELSE
     C                   MOVEL     PAVRDA        XINTERN
     C                   END
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
