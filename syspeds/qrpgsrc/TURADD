      ********************************************************
      * KOPIERE STANDARD TURER
      ********************************************************
      * Let gjennom alle poster i turkopieringsfil...
      * Aktuelle dag må finnes i postens "dagliste" (kopieres på dager...)
      * TUREN må finnes i TURER
      * Dersom parameter KUNTUR er fylt ut kopiere kun denne
      * Ved kall med KUNTUR i parameter ignorerer dag-settings
      *
     FTURADD    IF   E           K DISK
     FTURER     IF A E           K DISK
     FTELL      UF   E           K DISK
     FTELLRU    UF   E           K DISK
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  IDagÅR                 1      4  0
     D  IDagÅR2                3      4  0
     D  IDagMND                5      6  0
     D  IDagDAG                7      8  0
     D  IDag                   1      8  0
     D                 DS
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D  TUDT                   1      8  0
     D                 DS
     D  TUDTÅT                 1      4  0
     D  TUDTMT                 5      6  0
     D  TUDTDT                 7      8  0
     D  TUDTT                  1      8  0
     C                   EXSR      INIT
     c                   exsr      LagTur
      *
     C                   seton                                        LR
     c                   return
      *
      *
      ********************************************************
     C     INIT          BegSr
      ********************************************************
     C     *ENTRY        PLIST
     C                   PARM                    KUNTURA           8
     C     KUNTURA       IFNE      *BLANKS
     C     KUNTURA       ANDNE     *ZEROS
     C                   MOVE      KUNTURA       KUNTUR            8 0
     C                   END
     C                   clear                   TUDT
     C                   clear                   TUDTT
     C                   clear                   Idag
     C                   clear                   XULTID
      *
     C                   TIME                    XULTID
     C                   MOVE      XULDD         IDagDag
     C                   MOVE      XULMM         IDagMnd
     C                   MOVE      XULÅÅ         IDagÅr
      * finn Ukedag for dagens dato...
      * Ret-parameter: Ukedag - 0=Sø,1=Ma,2=Ti,3=On,4=To,5=Fr,6=Lø
     C                   Move      9             Dagnr             1 0
     C                   Move      Idag          DatoInn           8 0
     c                   CALL      'SYGE26'
     C                   PARM                    DatoInn
     C                   PARM                    Dagnr
     c                   Move      DagNr         DagNrA            1
      *
      * Keys
     C     TurKey        Klist
     C                   KFLD                    ADDAVD
     C                   KFLD                    ADDTUR
      *
     c                   move      *zeros        AVDZER            4 0
      *
     c                   EndSr
      *
      *
      ********************************************************
     C     LAGTUR        BegSr
      ********************************************************
      * Let gjennom alle poster i turkopieringsfil...
      * Aktuelle dag må finnes i turens "dagliste" (kopieres på dager...)
      * Ved kuntur må turnr matche ( men dagnr ignoreres)
      * TUREN må finnes i TURER
     C     *loval        SETLL     TURADD
     C                   Read      TURADD                                 33
     C     *in33         doweq     *OFF
     C     KunTur        ifne      *zeros
     C     KunTur        Ifne      ADDTUR
     c                   goto      NextRec
     c                   end
     c                   else
     C     DagNrA        SCAN      ADDDAG                                 34
     c     *IN34         CaBEQ     *OFF          NextRec
     c                   end
     c     Turkey        Chain(N)  TURER                              34
     c     *IN34         CaBEQ     *On           NextRec
     C                   exsr      LagTur2
     c     NextRec       Tag
     C                   Read      TURADD                                 33
     c                   End
      *
     c                   EndSr
      *
      *
      ********************************************************
     C     LAGTUR2       BegSr
      ********************************************************
      *
      * Hent nytt tur/rundtursnr - Blank div. felt
     C     ADDAVD        CHAIN     TELL                               34
     C   34AVDZER        CHAIN     TELL                               34
     C  N34              MOVE      TETURN        TUPRO
     C  N34              ADD       1             TETURN
     C  N34              UPDATE    TELLR
      *
     C     ADDAVD        CHAIN     TELLRU                             34
     C   34AVDZER        CHAIN     TELLRU                             34
     C  N34              MOVE      TERUND        TURUND
     C  N34              ADD       1             TERUND
     C  N34              UPDATE    TELLRUR
      *
     c                   MOVE      IDag          TUDT
     c                   MOVE      IDag          TUDTT
     c                   MOVE      IDagÅR2       TURAAR
     c                   MOVE      IDagMnd       TURMND
      *
     C                   Clear                   TUST
     C                   Clear                   TUAO
     C                   Clear                   TUTS
     C                   Clear                   TUTVKT
     C                   Clear                   TUTLM
     C                   Clear                   TUTM3
     C                   Clear                   TULAST
     C                   Clear                   TUBUSJ
     C                   Clear                   TUXXX2
     C                   Clear                   TUTST1
     C                   Clear                   TUTST2
     C                   Clear                   TUTST3
     C                   Clear                   TUTST4
     C                   Clear                   TUTST5
     C                   Clear                   TUAVRE
     C                   Clear                   TUAVNR
     C                   Clear                   TUTREF
      *
     C                   write     TURERR
      *
     c                   EndSr
