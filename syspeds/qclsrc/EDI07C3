             PGM
             /*_____________________*/
             /* Deklarere Variabler */
             /*"""""""""""""""""""""*/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OK) TYPE(*LGL) LEN(1)
             DCL        VAR(&SSN) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&SST) TYPE(*CHAR) LEN(1) VALUE(C)
             DCL        VAR(&NETW) TYPE(*CHAR) LEN(1) VALUE('3')
             DCL        VAR(&IIDR) TYPE(*CHAR) LEN(35)
             DCL        VAR(&IIDS) TYPE(*CHAR) LEN(35)
             DCL        VAR(&OKTCP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&STRTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ENDTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&QTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&UIBDLY) TYPE(*CHAR) LEN(5)
             /*__________________________________________________*/
             /* Denne Jobben kan bli startet *IMMED eller        */
             /* venter til et klokkeslett som finnes i firmans   */
             /* dataarea.                                        */
             /*""""""""""""""""""""""""""""""""""""""""""""""""""*/
             RTVDTAARA  DTAARA(EDIDTA (154 6)) RTNVAR(&STRTIME)
             RTVDTAARA  DTAARA(EDIDTA (166 5)) RTNVAR(&UIBDLY)
             IF         COND(&STRTIME *EQ *IMMED) THEN(GOTO +
                          CMDLBL(START))

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&QTIME)
             IF         COND(&STRTIME *LE &QTIME) THEN(GOTO +
                          CMDLBL(START))

             DLYJOB     RSMTIME(&STRTIME)
 /*__________________________*/
 /* Skal polling avsluttes ? */
 /*""""""""""""""""""""""""""*/
 START:      RTVDTAARA  DTAARA(EDIDTA (160 6)) RTNVAR(&ENDTIME)
             IF         COND(&ENDTIME *NE *NONE) THEN(DO)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&QTIME)
             IF         COND(&ENDTIME *LT &QTIME) THEN(GOTO +
                          CMDLBL(END))
             ENDDO
             /*_______________*/
             /* Mottak av MSG */
             /*"""""""""""""""*/
             CALL       PGM(E083R)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
/*IBM FMS*/  CALL PGM(E093R1) PARM(&LIB)
             DLYJOB     DLY(&UIBDLY)
             /*_______________*/
             /* Finn status A */
             /*"""""""""""""""*/
 START2:     CALL       PGM(EDI07R) PARM(&OK &LIB &FIL &MBR &SSN +
                          &NETW &IIDS &IIDR)
             IF         COND(&OK *EQ '1') THEN(DO)
             DLYJOB     DLY(&UIBDLY)
             GOTO       CMDLBL(START)
             ENDDO
             /*______*/
             /* Send */
             /*""""""*/
             SBMJOB     CMD(CALL PGM(E093C) PARM(&LIB &FIL &MBR +
                          &IIDS &IIDR &OKTCP)) JOB(EDISNDFTPJ) +
                          JOBD(SY400JOBD) JOBQ(SYJOBQNMAX)

             GOTO       CMDLBL(START2)

 END:        ENDPGM
