********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(LLGRPK LLGRPKR)
     A                                      PRINT
N02  A                                      HLPPNLGRP(HLPSCR LL050PNL)
N02  A                                      HLPTITLE('ARBEIDE MED GRUPPE/KUNDEA-
N02  A                                      VTALER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  I  6  2VALUES(' ' '2' '3')
N01  A            WSNR           1A  I  6  2VALUES(' ' '1' '2' '3')
     A            GKGRP     R        O  6  5EDTCDE(4)
     A            GKGSOK        10A  H
     A            XGNV          25A  O  6 14
     A            GKPNR     R        O  6 40EDTCDE(4)
     A            GKPSOK        10A  H
     A            XPNV          25A  O  6 49
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'Søk')
     A                                      CA05(05 'Frisk')
     A                                      OVERLAY
S02  A*         H                           HLPDOC('LL05001' 'LL050' 'SY400LO/L-
S02  A*                                     OGI')
N02  A          H                           HLPPNLGRP(LL05001 LL050PNL)
     A                                      HLPARA(01 001 24 080)
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL050R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1569 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(40 YBC1570 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1571 SYMSGF)
     A                                      DSPATR(HI)
     A            W2GRP          8  0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2GSOK        10A  B  5 14DSPATR(UL)
     A            W2PSOK        10A  B  5 49DSPATR(UL)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'Søk')
S02  A*         H                           HLPDOC('LL05001' 'LL050' 'SY400LO/L-
S02  A*                                     OGI')
N02  A          H                           HLPPNLGRP(LL05001 LL050PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL050R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1569 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  4  2MSGCON(78 YBC1571 SYMSGF)
     A                                      DSPATR(HI)
     A            W2GRP          8  0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2GSOK        10A  B  5 14DSPATR(UL)
     A            W2PSOK        10A  B  5 49DSPATR(UL)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R CPYKAVT
     A                                      WINDOW(*DFT 7 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL050R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 YBC1572 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(15 YBC1573 SYMSGF)
     A            GKGRP          8  0O  2 18DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            XGNV          25A  O  2 27DSPATR(HI)
     A                                  3  6MSGCON(11 YBC1574 SYMSGF)
     A            GKPNR          8  0O  3 18DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            XPNV          25A  O  3 27DSPATR(HI)
     A                                  4  2MSGCON(15 YBC1575 SYMSGF)
     A            WSGRP          8  0B  4 18DSPATR(UL)
     A  90                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  5  6MSGCON(11 YBC1574 SYMSGF)
     A            WSPNR          8  0B  5 18DSPATR(UL)
     A  90                                  DSPATR(HI)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(15 YBC1576 SYMSGF)
     A            WSDLT          1A  B  6 18DSPATR(UL)
     A                                      VALUES('J' 'N')
     A N30                              7  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A N30                              7 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGT58        58A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
