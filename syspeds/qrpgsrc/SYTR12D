********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 RETTELSE ETTER UTVIDELSE AV OPPDRAGSNR
********************************************************************
      *
      *** PROGRAM SOM GENERERE DATAGRUNNLAG FOR UTSKR AV.        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31    34-39 41-87             ***
      **************************************************************
      **************************************************************
OBS!! * OBS!! VED ENDRINGER I SR "AKKUM" -
OBS!! * NÅR EN KJØRER INTERAKTIVT/PÅ SKJERM BLIR GRUNNDATA SKAPT
OBS!! * (TURER SOM UTVELGES..) AV SYTR12 - DETTE PROGRAM INNEHOLDER
OBS!! * OGSÅ EN SR "AKKUM" - SØRG FOR SYNKRONISERING
      **************************************************************
      *
     FTRAVH     UF   E           K DISK
     FTRAVH1    UF   E           K DISK
     F                                     RENAME(TRAVHR:TRAH1R)
     FTURER     UF   E           K DISK
     FTURER11   IF   E           K DISK
     F                                     RENAME(TURERR:TUR11R)
     FTRAN      IF   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  W1PERC                 1      2  0
     D  W1PERÅ                 3      4  0
     D  W1PERM                 5      6  0
     D  DSW1ÅM                 1      6  0
     D                 DS
     D  TURACC                 1      2  0
     D  TURAAR                 3      4  0
     D  TURMND                 5      6  0
     D  DSTUÅM                 1      6  0
     D                 DS
     C                   EXSR      INIT
     C*
     C     W1TNR         IFNE      *ZEROS
     C                   EXSR      AKKUM
     C                   ELSE
     C                   EXSR      ALLE
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     ALLE          BEGSR
     C***********************************************
     C*
     C     TRANKE        SETLL     TRAN
     C                   READ      TRAN                                   94
     C     *IN94         DOWEQ     '0'
     C* VED BESTILLING ALLE - KUN DE SOM SKAL HA KREDITNOTA
     C     VMINCR        IFEQ      0
     C     VMINCR        OREQ      3
     C     VMINCR        OREQ      4
     C                   MOVE      VMTRAN        W1TNR
     C                   EXSR      AKKUM
     C                   END
     C                   SETOFF                                       94
     C                   READ      TRAN                                   94
     C  N94              END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     AKKUM         BEGSR
     C***********************************************
     C* SLETT FØRST ALLE ÅPNE TURER FRA TRAVH (FOR AKTUELL TRANSP.)
     C* UNTATT! BESKYTTEDE/I-ARBEID TURER SAMT DUMMY FOR GODSKR/TREKK
     C     TUR11K        SETLL     TRAVH
     C     TUR11K        READE     TRAVH                                  33
     C     *IN33         DOWEQ     '0'
     C     THRTNR        IFNE      99999998
     C     THSTAR        ANDEQ     ' '
     C                   DELETE    TRAVHR
     C                   ELSE
     C                   UPDATE    TRAVHR
     C                   END
     C     TUR11K        READE     TRAVH                                  33
     C                   END
     C*
     C*------------------------------------------------------*
     C*     FOR ALLE TURER MED TRANSPORTØREN I PERIODEN....  *
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""*
     C*
     C                   SETOFF                                       33
     C     TUR11K        SETLL     TURER11
     C     TUR11K        READE     TURER11                                33
     C  N33DSTUÅM        COMP      DSW1ÅM                             33
      * DE 2 NESTE ER MED I KEY FOR READE - BARE TULL MED EKSTRA TEST
     C     *IN33         DOWEQ     '0'
     C* FUNNET TUR SOM SKAL MED I AVREGNING - FINNES AVREGN. FRA FØR ??
     C                   MOVE      TUAVD         THTAVD
     C                   MOVE      TUPRO         THTUNR
     C* MERK SLETTEDE TURER SÅ EN SLIPPER Å DRAS MED DE FOR EVIG TID
     C     TUST          IFEQ      'D'                                          *SLETTET ??
     C     TURERK        CHAIN     TURER                              32
     C  N32              MOVE      'X'           TUAVRE
     C  N32              UPDATE    TURERR
     C     TRAH1K        CHAIN     TRAVH1                             32
     C  N32              DELETE    TRAH1R
     C                   ELSE                                                   *TURSTAT OK
     C     TRAH1K        CHAIN(N)  TRAVH1                             32
     C   32              MOVE      ' '           THST
     C   32              MOVE      ' '           THSTAR
     C* BLANK STATUS ?? / IKKE BESKYTTET - REGENERER
     C     THST          IFEQ      ' '
     C     THSTAR        ANDEQ     ' '
     C                   MOVE      TUAVD         XXAVD             4 0
     C                   MOVE      TUPRO         XXTUNR            8 0
     C                   MOVE      'N'           XXVIS             1
     C                   CALL      'SYTR12A'
     C                   PARM                    XXAVD
     C                   PARM                    XXTUNR
     C                   PARM                    XXVIS
     C                   PARM                    N4                4 0
S01  C*                  PARM                    N5                5 0
N01  C                   PARM                    N7                7 0
     C                   PARM                    N8                8 0
     C                   END
     C                   END                                                    *TUTSTAT OK
     C* FLERE TURER TIL AVREGNING???
     C     TUR11K        READE     TURER11                                33
     C  N33DSTUÅM        COMP      DSW1ÅM                             33
     C  N33              END
     C*
     C* DE SOM MOTTAR KREDITNOTA SKAL GODSKRIVES REKVISISJONER
     C* DER DE ER DEN UTFØRENDE PARTEN
     C* MEN IKKE DERSOM EN GAMMEL LIGGER UOVERFØRT.
     C     VMINCR        IFEQ      0                                                           OTA
     C     VMINCR        OREQ      3                                                           OTA
     C     VMINCR        OREQ      4                                                           OTA
     C                   MOVE      *ZEROS        THTAVD
     C                   MOVE      W1TNR         THTUNR
     C     TRAH1K        CHAIN(N)  TRAVH1                             32
     C   32              MOVE      ' '           THST
     C   32              MOVE      ' '           THSTAR
     C     THST          IFEQ      ' '
     C     THSTAR        ANDEQ     ' '
     C                   MOVE      W1TNR         XXTNR             8 0
     C                   MOVE      W1PERM        XXMND             2 0
     C                   MOVE      W1PERÅ        XXÅR              2 0
     C                   MOVE      'N'           XXVIS             1
     C                   CALL      'SYTR12C'
     C                   PARM                    XXTNR
     C                   PARM                    XXMND
     C                   PARM                    XXÅR
     C                   PARM                    XXVIS
     C                   END
     C                   END
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C**
     C     *ENTRY        PLIST
     C                   PARM      UUTNR         UUTNR             8            *TRANSP(ALP)
     C                   PARM                    UUPERÅ            2            *PERÅR (ALP)
     C                   PARM                    UUPERM            2            *PERMND(ALP)
     C     UUTNR         IFGE      '00000000'
     C     UUTNR         ANDLE     '99999999'
     C                   MOVE      UUTNR         W1TNR             8 0
     C                   ELSE
     C                   Z-ADD     99999999      W1TNR
     C                   END
     C                   MOVE      UUPERM        W1PERM
     C                   MOVE      UUPERÅ        W1PERÅ
     C     UUPERÅ        IFGE      '50'
     C                   MOVE      '19'          W1PERC
     C                   ELSE
     C                   MOVE      '20'          W1PERC
     C                   END
     C*
     C     TRAH1K        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C     TUR11K        KLIST
     C                   KFLD                    YTHST
     C                   KFLD                    W1TNR
     C     TURERK        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    W1TNR
     C* INITIER SDATASTRUKTURER
     C                   MOVE      *ZEROS        DSTUÅM
     C*
     C                   MOVE      ' '           YTHST             1
     C                   READ      FIRM                                   33
     C*
     C     UTI           ENDSR
     C*
