     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R BILDE01
     A                                      WINDOW(*DFT 10 75 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF12(12 'Avbryt')
     A                                  1  2'SYSPED/SYTK11'
     A                                      COLOR(BLU)
     A                                  1 19'Generere importfil for AutoRoute  -
     A                                             '
     A                                      COLOR(WHT)
     A                                  2 19'Alle biler i et gitt område       -
     A                                             '
     A                                      COLOR(WHT)
     A                                  4  2'Kun biler i område..........:'
     A            WSOMR         10A  B  4 33
     A            WSBESK        20A  O  4 44COLOR(WHT)
     A                                  5  2'Kun biler av type (unittype):'
     A            WSUNTY         4   B  5 33
     A            WSTYTX        20A  O  5 44COLOR(WHT)
     A                                  6  2'Kun biler m/pos ferskere enn:'
     A            WSTIM          3Y 0B  6 33EDTCDE(4)
     A                                  6 37'Timer /'
     A            WSMIN          2Y 0B  6 45EDTCDE(4)
     A                                  6 48'Minutter'
     A                                  6 57'(Blank = I DAG)'
     A                                  7  2'Filnavn som genereres.......:'
     A            WSFIL         30A  B  7 33
     A                                  8  2'i mappe:'
     A            WSMAPP        65A  O  8 11COLOR(WHT)
     A                                 10  2'ENTER = Generer fil               -
     A                                                                    '
     A                                      COLOR(BLU)
