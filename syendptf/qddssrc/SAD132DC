      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 60 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  5  2EDTCDE(4)
     A  41                                  DSPATR(PR)
     A            CMLI           2Y 0O  5  5TEXT('Linjenr')
     A                                      EDTCDE(4)
     A            CMST           1A  H
     A            CMETYP         2A  O  5  8TEXT('Type')
     A            CMETYPT       30A  H
     A            CMEID         18A  O  5 13TEXT('Eksport Id')
     A            CMESER         1A  O  5 32
     A            CMAVDE         4Y 0O  5 37TEXT('Avdeling')
     A                                      EDTCDE(Z)
     A            CMTDNE         7Y 0O  5 42TEXT('Oppdragsnr')
     A                                      EDTCDE(Z)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Plukk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 40SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD132RC'
     A                                      COLOR(BLU)
     A                                  1 20'Vedl. E.F. - Ordre'
     A                                      COLOR(WHT)
     A                                  1 50DATE
     A                                      EDTCDE(Y)
     A                                  2 50TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Opdnr:'
     A            UTDN           7Y 0O  2 20TEXT('Opdnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(M)
     A  49                              3  2'Velg   5=Vis'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg   2=Endre   4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Li Type EksportId          Sert-
     A                                       Avd. Oppd.nr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Plukk')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD132RC'
     A                                      COLOR(BLU)
     A                                  1 20'Vedl. E.F. - Ordre'
     A                                      COLOR(WHT)
     A                                  1 50DATE
     A                                      EDTCDE(Y)
     A                                  2 50TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Opdnr:'
     A            UTDN           7Y 0O  2 20TEXT('Opdnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(M)
     A  49                              3  2'Velg   5=Vis'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg   2=Endre   4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Li Type EksportId          Sert-
     A                                       Avd. Oppd.nr'
     A                                      COLOR(WHT)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
