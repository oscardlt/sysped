      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCE000R2 MODULE(*LIBL/TDCE000R2 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FDKXH      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d wspl            S              1
     D WTVDREF         S             20
     d MyPoDS          ds                  Qualified  dim(100)
     d  TVAVD                         4S 0
     d  TVTDN                         7S 0
     D  TVDREF                       20
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬tvdref¬ : ¬'+%trim(WTVDREF)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
                       ' TVDREF = ' + cQuote + WTVDREF + cQuote +' ';

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select TVAVD, TVTDN, TVDREF ' +
                             'from DKXV ' +
                              %trim(WhereC)  +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         wtvdref = zhbgetvar('TVDREF');
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).tvdref = *blanks;
         leave;
       endif;

       if THLSTL <> *zeros;
         wspl = 'J';
       else;
         wspl = 'N';
       endif;

           chain (mypods(x).TVAVD: mypods(x).TVTDN: '*CN') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;

           chain (mypods(x).TVAVD: mypods(x).TVTDN) DKXH;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).TVAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).TVTDN:'Z'))+'¬, '
          +'¬lrnNr¬ : ¬'+%trim(THTUID)+'¬, '
          +'¬sign¬ : ¬'+%trim(THSG)+'¬, '
          +'¬mrnNr¬ : ¬'+%trim(THTRNR)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(THDT:'Z'))+'¬, '
          +'¬status¬ : ¬'+%trim(THST)+'¬, '
          +'¬kolli¬ : ¬'+%trim(%editc(THNTK:'Z'))+'¬, '
          +'¬bruttoVikt¬ : ¬'+%trim(%editc(THVKB:'3'))+'¬, '
          +'¬forenklad¬ : ¬'+%trim(THENKL)+'¬, '
          +'¬elList¬ : ¬'+%trim(WSPL)+'¬, '
          +'¬eksternref¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(THNAS)+'¬, '
          +'¬motnavn¬ : ¬'+%trim(THNAK)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open brparf;
         open DKXH;
         open frisok;

         // Er dato= blank eller 0, sett  da dato 7 dager tilbake

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              pavrdn=7;
           endif;



       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close brparf;
         close DKXH;
         close frisok;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
