     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R GSS072DA
     A                                      CA03(03 'AVSLUTT')
     A                                  1  2'SHIPSYS/GSS072D'
     A                                      COLOR(BLU)
     A                                  1 21'Utskrift av statiske merkelapper'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  5  2'Oppgi printernavn:'
     A            WSPRT         10A  B  5 22DSPATR(UL)
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
     A                                  7  2'Type merkelapp   :'
     A            WSTYPE         1A  B  7 22VALUES('L' 'B')
     A N99                                  DSPATR(PC)
     A                                  7 26'(L=Lokasjonsnummer)'
     A                                  8 26'(B=Boxnummer)'
     A                                 23  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A            MSGNR          7A  P
     A                                 10  2'Size på ledetekst:'
     A            WSSIZE         1A  B 10 22VALUES('1' '2' '3' '4' '5')
     A N99                                  DSPATR(PC)
     A                                 10 26'(1=Normal, 2,3,4,5=Større)'
      *
     A          R GSS072DB
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A            MSGNR          7A  P
     A                                  1  2'SHIPSYS/GSS072D'
     A                                      COLOR(BLU)
     A                                  1 21'Utskrift av statisk merkelapp'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  5  2'Printernavn:'
     A            WSPRT         10A  O  5 22COLOR(WHT)
     A                                  6  2'Size'
     A                                  6  7'ledetekst'
     A                                  6 17':'
     A            WSSIZE         1A  O  6 22COLOR(WHT)
     A                                 10 22'Numeric'
     A N51                             10 31'Alphanumeric'
     A  51                             11  2'Oppgi boxnr:'
     A N51                             11  2'Oppgi loksajonsnr:'
     A            WSBOX          6Y 0B 11 22DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N51        WSBOXAL        6   B 11 31
     A                                 15  2'Siste utskrevet:'
     A            WSBOXS         6Y 0O 15 22COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSBOXSA        6   O 15 31
     A                                 23  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
