     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 55 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4' '9')
     A  27                                  DSPATR(PR)
     A            NIDFKD        20A  O  5  5
     A            NIDFSK         2A  O  5 26
     A            TIDFKD        20A  O  5 30
     A            TIDFSK         2A  O  5 51
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 48SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX069R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll ny plombering'
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny  2=Ændra 4=Radera 9=Ångra'
     A                                      COLOR(BLU)
     A                                  3  2'Op Ny plombering        SK  Plombe-
     A                                      ring           SK'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2DFKD        20A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            W2DFSK         2A  B  4 26DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N27                             11  2'F3/12=Avbryt F4=Søk F5=Uppdatera'
     A                                      COLOR(BLU)
     A  27                             11  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(10)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2'   Ny plombering        SK  Plombe-
     A                                      ring           SK'
     A                                      COLOR(WHT)
     A            WSDFKD        20A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90
     AON30                                  DSPATR(PC)
     A            WSDFSK         2A  B  2 26DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A            TIDFKD        20A  O  2 30
     A            TIDFSK         2A  O  2 51
     A N30                              3  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX069R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll ny plombering    '
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny '
     A                                      COLOR(BLU)
     A                                  3  2'Op Ny plombering        SK  Plombe-
     A                                      ring           SK'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A            W2DFKD        20A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            W2DFSK         2A  B  4 26DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A N30N27                          11  2'F3/12=Avbryt F4=Søk F5=Uppdatera'
     A                                      COLOR(BLU)
     A N30 27                          11  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
