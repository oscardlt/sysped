**********************************************************************************************
      *  Arkiver dokument ...
**********************************************************************************************
      * OBS - FØR KJØRING må typen være definert i fil ARKTXT
      *       og denne bør gjerne peke på "eksternt" arkivlager med
      *       avvikende bane (f.eks /pdf/scan ) (fil ARKEXT)
**********************************************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ignorer evt timestamp bakerst ...xlsx_2009-11-27
10XXX * 02 SH  PTF10 skjekk om dokument skal sendes som epost EMMA
10XXX * 03 SH  PTF10 skjekk også oppdragsnummer i TRIH TRUH
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 04 JOV PTF11 dersom navn inneholder + sjekk om det er kortversjon - fix navn (SYTMS)
      * 05 JOV PTF11 Utvidet 04 - behold orig filnavn ved upLoad
      * 06 JOV PTF11 TAKLE LANGE FILNAVN (SE OGSÅ ARCA001 - MAX 120 (MEN MED PATH ER DET MER)
      * 07 SH  PTF11 henter extension i SYGE112
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 08 SH  PTF12 DUMPER HVIS FILNAVN HAR EN '
12142 * 09 SH  PTF12 flytt til feilmappe hvis ikke finnes
12142 * 10 SH  PTF12 fjernet en slasj
12142 * 11 JOV PTF12 Kan også arkivere på SADEFCF = expressfortolling consignment.
12142 * 12 sh  PTF12 arc044 tillegsparametre
      * 13 JOV PTF12 Hvis lagring i google cloud send rett dit (den rutinen sletter lokalt)
12142 * 14 sh  PTF12 arc044 må til batch
********************************************************************
      *
     H DFTACTGRP(*NO)
      *""""""""""""""""
     FFIRMARC   IF   E           K DISK
N13  FFIRMGOOG  IF   E           K DISK
     FARKIVP    O    E             DISK
     FFIRM      IF   E             DISK
     FHEADF     IF   E           K DISK
     FSADH      IF   E           K DISK
     FSAEH      IF   E           K DISK
N03  FTRIH      IF   E           K DISK
N03  FTRUH      IF   E           K DISK
N11  FSADEFCL2  IF   E           K DISK
     FARKTXT    IF   E           K DISK    PREFIX(X_)
     FARKEXT    IF   E           K DISK    PREFIX(Y_)
      *""""""""""""""""
     D DirName         S            100A
     D EntryName       S            120A
     D EntryPath       S            256A
      *________________
      * Andre variabler
      *""""""""""""""""
     D AVD             S              4
     D OPD             S              7
     D TYPE            S              2
     D NULL            S              1A   INZ(X'00')
     D CMDLINE         S            512
     D CMDLEN          S             15  5
     D NEWDIR          S            100
     D NEWFILE         S            100
     D NEWOBJ          S            200
s01  D*NEWEND          S              4
s07  D*NEWEND          S             10
     D NEWRAN          S             10

     C                   EXSR      INIT
     C                   EXSR      MOV
     C     OkMov         IFEQ      'J'
     C                   EXSR      ARK
     C                   end
     C                   EVAL      *inlr = *on
     C                   RETURN

      *////////////////////////////////////////////////////////////
      *  Init                                                INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     *entry        PLIST
     C                   PARM                    DirName
     C                   PARM                    EntryName
     C                   PARM                    EntryPath
n08   *
n08   * FILEN KAN IKKE INNEHOLDE ' DA DUMPER RENAME
n08   *
N08  C     X'7D'         SCAN      EntryPath                              33
N08  C                   IF        *IN33=*on
N08  C                   EVAL      *INLR=*ON
N08  C                   RETURN
N08  C                   ENDIF
N04   *
N04   * dersom filnavn inneholder '+' kan det være upload med forenklet benevning - Fix navn
N04   * Kun EntryName (som ulike tester utføres på) endres,
N04   *     EntryPath (=hele originalen path+navn) forblir uendret - og det er den som flyttes
N04   *     FBeskr = evt original filnavn (mellom avd/opd og extention)
N04  C     '+'           SCAN      EntryName     X                 3 0
N04  C                   if        X <> *zeros
N05   *full "strekkode-navn + orig filnavn..: eks   ZH00010155511.docx+TEST ARKIVERING.docx  
N05  c                   if        X > 15
N05  c                   eval      Fixed = 'J'
N05  c                   eval      EntryNameTmp=%subst(EntryName:1:X-1)
N05  c                   eval      FBeskr      =%subst(EntryName:X+1)
N05  c                   else
N04  C                   movel     'OPPDRAG'     Mappe
N04  C                   movel     EntryName     EntryNameTmp    120
N04  C                   call      'ARCAFIXN'
N04  C                   PARM                    Mappe            20
N04  C                   PARM                    EntryNameTmp    120
N04  C                   PARM                    FBeskr          120
N04  C                   PARM                    Fixed             1
N05  c                   endif
N04  C                   if        Fixed = 'J'
N04  C                   movel     EntryNameTmp  EntryName
N04  C                   endif
N04  C                   endif
N04   *
      * Type er 2 første av filnavn
     C                   EVAL      TYPE = %subst(EntryName:1:2)
      * AVD  er 4 neste av filnavn (3-6)
     C                   EVAL      AVD = %subst(EntryName:3:4)
     C                   TESTN                   AVD                  30
     C                   IF        *IN30=*OFF
N09  c                   exsr      movtofeil
     C                   EVAL      *INLR=*ON
     C                   RETURN
     C                   ENDIF
     C                   MOVE      AVD           HEAVD
      * OPD  er 7 neste av filnavn (7-13)
     C                   EVAL      OPD = %subst(EntryName:7:7)
     C                   TESTN                   OPD                  30
     C                   IF        *IN30=*OFF
N09  c                   exsr      movtofeil
     C                   EVAL      *INLR=*ON
     C                   RETURN
     C                   ENDIF
     C                   MOVE      OPD           HEOPD
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEADFK        Chain     HEADF                              33
     C   33HEADFK        Chain     SADH                               33
     C   33HEADFK        Chain     SAEH                               33
N03  C   33HEADFK        Chain     TRIH                               33
N03  C   33HEADFK        Chain     TRUH                               33
N11  C   33HEADFK        Chain     SADEFCL2                           33
     C     *IN33         IFEQ      *ON
N09  c                   exsr      movtofeil
     C                   EVAL      *inlr = *on
     C                   RETURN
     C                   end
      * Definiere parameterliste
     C     QCMDEXC       PLIST
     C                   PARM                    CMDLINE
     C                   PARM                    CMDLEN
     C                   READ      FIRMARC                                41

     C     EXKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    X_ARKLAG
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Flytt og endre navn på fil                           MOV /
      *////////////////////////////////////////////////////////////
     C     MOV           BEGSR
      * Les firmafil for å få tak i firmakode.
     C                   READ      FIRM
      * Hent dato og klokkeslett
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Hent slutt på filnavn (ex. .pdf) fra frafil
s07  C*    '.'           SCAN      EntryName     X                 3 0
s01  C*                  EVAL      NEWEND = %SUBST(EntryName:X:4)
s07  C*    '_'           SCAN      EntryName:X   Y                 3 0
s07  c*    Y             ifeq      *zeros
s07  C*    ' '           SCAN      EntryName:X   Y                 3 0
s07  c*                  end
s07  C*                  EVAL      NEWEND = %SUBST(EntryName:X:y-x)
n07  c                   eval      par512=EntryName
n07  c                   call      'SYGE112'
n07  c                   parm                    par512          512
n07  c                   parm                    NEWEND           10
      * Generer random number
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN
      * Definier ny katalog Hent fra standard
     C                   EVAL      NEWDIR = '/' + %trimr(ARCANE)
      * Er det på dokumenttypenivå angitt avvikende katalog?(via EXT lager)
     C                   EVAL      *IN51 = *OFF
     C     TYPE          CHAIN     ARKTXT                             51
     C                   IF        *IN51 = *OFF
     C     X_ARKLAG      IFNE      *BLANKS
     C     EXKEY         CHAIN     ARKEXT                             51
     C                   IF        *IN51 = *OFF
     C                   EVAL      NEWDIR = '/' + %trimr(Y_ARCANE)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      * Definier nytt filnavn
     C                   EVAL      NEWFILE= %trim(TYPE)
     C                             + %SUBST(WDATO:1:4)
     C                             + %trimr(AVD)
     C                             + %trimr(OPD)
     C                             + NEWRAN
     C                             + NEWEND
      * Lag nytt objektnavn
     C                   EVAL      NEWOBJ = %trim(NEWDIR) + '/'
     C                             + %trimr(NEWFILE)
      * Flytte objekt
     C                   MOVE      'J'           OkMov             1
N06  C                   MOVEl     EntryPath     FRA             512
     C                   MOVEl     NewObj        Til             120
N06  C                   CALL      'ARCAMOVLC'
     C                   PARM                    FRA
     C                   PARM                    TIL
     C                   PARM                    OkMov
n13   *Hvis google cloud benyttes - send det i skyen med en gang
n13  C                   IF        OkMov = 'J'
n13  C     FIFIRM        chain     FIRMGOOG
n13  C                   if        %found and GOBUID <> *blanks
n13  c                   eval      Companyid = GOBUID
n13  c                   eval      UlocalPath= %trim(NEWDIR)+'/'
n13  c                   eval      USrcFile  = NEWFILE
n13  c                   eval      UNewFile  = *blanks
n13  c                   eval      Udelete   = 'J'
n13  c                   call      'ARC99WSU1'
n13  c                   parm                    Companyid        10
n13  c                   parm                    UlocalPath      100
n13  c                   parm                    uSrcFile        120
n13  c                   parm                    udelete           1
n13  c                   parm                    uNewFile         60
n13  c                   parm                    Uok               1
n13  c                   endif
n13  c                   endif
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv til ARKIVP                                     ARK /
      *////////////////////////////////////////////////////////////
     C     ARK           BEGSR
      * Skriv til ARKIVP
     C                   CLEAR                   ARKIVR

     C                   EVAL      ARSTAT = 'A'
     C                   EVAL      ARTYPE = TYPE
     C                   EVAL      ARFIRM = FIFIRM
     C                   EVAL      ARAVD  = HEAVD
     C                   EVAL      AROPD  = HEOPD
     C                   EVAL      ARUSER = 'SY400USR'
     C                   move      WDATO         ARDATE
     C                   movel     WTID          ARTIME
     C                   EVAL      ARKUND = 0
     C                   EVAL      ARLINK = NEWFILE
N04  C                   EVAL      ARRFK  = FBeskr

     C                   WRITE     ARKIVR
N14   * GIDDER IKKE MERKE
     C                   MOVE      ARAVD         ARAVDX            4
     C                   MOVE      AROPD         AROPDX            7
     C                   MOVE      ARDATE        ARDATEX           8
     C                   CALL      'ARC044CL'
     C                   PARM                    ARAVDX
     C                   PARM                    AROPDX
     C                   PARM                    ARTYPE
     c                   parm                    ardateX
     c                   parm                    arlink
     C                   ENDSR
N09   ****************************************************************
N09  c     movtofeil     begsr
N09   ****************************************************************
N09  C                   EVAL      TYPE = %subst(EntryName:1:2)
N09  C     TYPE          CHAIN     ARKTXT                             51
N09  C     *in51         ifeq      '0'
N09  c     x_arsban      andne     *blanks
s10  C*                  EVAL      NEWDIR = '/' + %trimr(X_ARSBAN)+
n10  C                   EVAL      NEWDIR = %trimr(X_ARSBAN)+
N09  C                                      '/feil'
N09  C                   MOVE      'J'           OkMov             1
N09  C                   MOVEl     EntryPath     FRA             512
N09  C                   eval      til=newdir
N09  C                   CALL      'ARCAMOVCF'
N09  C                   PARM                    FRA
N09  C                   PARM                    TIL
N09  C                   PARM                    OkMov
N09  c                   endif
N09  C                   endsr
