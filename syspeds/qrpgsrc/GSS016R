********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KLOKKE ER VARIABEL
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SKRIVE UT NYE REGISTRERTE ORDRE
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01-26
      *** LEDIGE INDIKATORER 28-32 38-99
      **************************************************************
      *
     FGSSBIF    IF   E           K DISK
     FGSSPCL02  IF   E           K DISK
     FGSSORL08  IF   E           K DISK
     FGSSPOF    IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFAXLK     IF   E           K DISK
     FFAXSEN    IF   E           K DISK
     FFIRM      IF   E           K DISK
N01  FKALENDER  IF   E           K DISK
N01  FGSSLGL02  IF   E           K DISK
N01  FGSSORF    IF   E           K DISK    RENAME(GSSORFR:GSSORFR0)
     FGSS016P   O    E             PRINTER INFDS(DSPRTF)
     F                                     OFLIND(*IN27)
     F                                     USROPN
     D DSPRTF          DS
     D  OPPRTF                 9      9
     D                 DS
     D  CBNAB                  1     30
     D  XNAB27                 1     27
     D                 DS
     D  ORSNM                  1     30
     D  XSNM27                 1     27
     D                 DS
     D  ORDSC                  1     30
     D  XDSC27                 1     27
     D                 DS
     D  ORDTL                  1      8  0
     D  ORDTLÅ                 1      4  0
     D  ORDTLM                 5      6  0
     D  ORDTLD                 7      8  0
     D                 DS
     D  XDTL                   1      6  0
     D  XDTLD                  1      2  0
     D  XDTLM                  3      4  0
     D  XDTLÅ                  5      6  0
     D                 DS
     D  ORDTWL                 1      8  0
     D  ORDTWÅ                 1      4  0
     D  ORDTWM                 5      6  0
     D  ORDTWD                 7      8  0
     D                 DS
     D  XDTWL                  1      6  0
     D  XDTWLD                 1      2  0
     D  XDTWLM                 3      4  0
     D  XDTWLÅ                 5      6  0
     D                 DS
     D  XCONO1                 1     20
     D  XCONO2                21     40
     D  XCONO3                41     60
     D  XCONO4                61     80
     D  XCONO5                81    100
     D  XCONO6               101    120
     D  CO                     1   1000    DIM(50)
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(GSS016P) -
     D                                     OUTQ(')
     D XCMD12          C                   CONST('PAGESIZE(72 80) -
     D                                     CPI(10) -
     D                                     OVRFLW(68)')
     D XCMD20          C                   CONST('OVRPRTF -
     D                                     FILE(GSS016P) -
     D                                     DEV(FAX3812) -
     D                                     DEVTYPE(*AFPDS) -
     D                                     PAGESIZE(72 90) -
     D                                     LPI(6) -
     D                                     CPI(12) -
     D                                     FRONTMGN(0 0.5) -
     D                                     PAGRTT(0) -
     D                                     OUTQ(QFAX/QFAXOUTQ) -
     D                                     FORMTYPE(*STD)')
     D XCPTXT          C                   CONST('NEW REGISTERED -
     D                                     ORDERS')
     D                 DS
     D  XTID                   1      6  0
     D  XTID4                  1      4  0
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  PCFAX                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D SSDTA           DS           100
     D  UBIUSI                 1     10
     D  UBCD                  11     15
     D  UPRT                  19     28
     D  USLUTT                29     29
     D  UFIRM                 30     31
N01  D  UKL                   32     37
      *
     C                   EXSR      INIT
      *
     C                   IF        UBCD = *BLANKS
     C                   EVAL      UBCD = 'A'
     C                   END
      *
     C     UBCD          SETLL     GSSPCL02
     C                   MOVE      *BLANKS       UBCD
     C     STPRT         TAG
     C                   READ      GSSPCL02                               33
     C     *IN33         IFEQ      '0'                                          1<-B
     C                   IF        ((PCAUTO <> 'E') AND (PCAUTO <> ' '))
     C                   GOTO      STPRT
     C                   END
N01   *
N01  C     OPPRTF        IFEQ      '1'                                          1<-B
N01  C                   CLOSE     GSS016P
N01  C     XAUTO         IFEQ      'E'                                           2<-B
N01  C                   EXSR      SNDFAX
N01  C                   END                                                     2<-E
N01  C                   MOVE      *BLANKS       UBCD
N01  C                   END                                                    1<-E
N01   *
     C                   EVAL      UEPOST2= UEPOST
     C                   MOVEL     PCEM          UEPOST           70
     C     UBCD          IFNE      PCBCD                                         2<-B
     C     UBCD          IFEQ      *BLANKS                                        3<-B
     C                   MOVE      PCBCD         UBCD
     C                   MOVE      PCNM          XNM              30
     C                   MOVE      PCAUTO        XAUTO             1
     C                   MOVE      PCCP          XCP               1
     C     CUND1K        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       SYLAND
     C                   MOVE      'J'           XNYL              1
     C                   EVAL      UEPOST2= UEPOST
     C                   ELSE                                                     3
     C                   MOVE      PCBCD         UBCD
     C                   MOVE      'N'           USLUTT
     C                   GOTO      SLUTT
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
N01  C                   IF        XKL = 0
N01  C                   MOVEL     PCTD          XKL
N01  C                   END
N01  C                   IF        XKL = 0
     C     GOR08K        SETLL     GSSORL08
     C     GOR08K        READE     GSSORL08                               34
N01  C                   ELSE
N01  C     GSSLGL02K1    SETLL     GSSLGL02
N01  C     STLES         TAG
N01  C     GSSLGL02K2    READE     GSSLGL02                               34
N01  C  N34              IF        LGHDT = YIDAG
N01  C     LGHTID        COMP      XKL                                34
N01  C                   END
N01  C  N34LGONO         CHAIN     GSSORF                             34
N01  C  N34ORST          CABEQ     'P'           STLES
N01  C  N34ORST          CABEQ     'D'           STLES
N01  C  N34ORBCD         CABNE     UBCD          STLES
N01  C                   END
     C     *IN34         IFEQ      '0'                                           2<-B
     C     XNYL          IFEQ      'J'                                            3<-B
     C     UFIRM         SETLL     FAXSEN
     C                   READ      FAXSEN                                 35
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
     C     XAUTO         IFNE      'E'                                             4<-B
     C     XCMD11        CAT       UPRT:0        UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD12:1      UCMD
     C                   ELSE                                                      4
     C                   MOVE      XCMD20        UCMD
     C                   END                                                       4<-E
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   MOVE      'N'           XNYL
     C                   MOVE      '0'           *IN27
     C                   OPEN      GSS016P
     C                   WRITE     GSS016PA
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C                   GOTO      STPRT
     C                   END                                                     2<-E
      *
     C     *IN34         DOWEQ     '0'                                           2<-B
     C     ORBCD         CHAIN     CDBOAT                             35
     C   35              MOVE      *BLANKS       CBNAB
     C   35              MOVEL     '*UNKNOW'     CBNAB
     C                   MOVE      *BLANKS       CO
     C     ORONO         SETLL     GSSPOF                             35
     C                   MOVE      *ZEROS        XN                3 0
     C     ORONO         READE     GSSPOF                                 35
     C     *IN35         DOWEQ     '0'                                            3<-B
     C                   ADD       1             XN
     C                   MOVE      POCONO        CO(XN)
     C     ORONO         READE     GSSPOF                                 35
     C  N35              END                                                      3<-E
      *
     C     XCONO4        COMP      *BLANKS                            3535
     C     XCONO5        COMP      *BLANKS                            3636
     C     XCONO6        COMP      *BLANKS                            3737
      *
     C                   MOVE      ORDTLD        XDTLD
     C                   MOVE      ORDTLM        XDTLM
     C                   MOVE      ORDTLÅ        XDTLÅ
     C                   MOVE      ORDTWD        XDTWLD
     C                   MOVE      ORDTWM        XDTWLM
     C                   MOVE      ORDTWÅ        XDTWLÅ
      *
     C   27              WRITE     GSS016PA
     C   27              MOVE      '0'           *IN27
     C                   WRITE     GSS016PB
      *
     C     7             DO        50            XN
     C                   IF        CO(XN) <> *BLANKS
     C                   MOVE      CO(XN)        XCONOX
     C   27              WRITE     GSS016PA
     C   27              MOVE      '0'           *IN27
     C                   WRITE     GSS016PC
     C                   END
     C                   ENDDO
      *
N01  C     XNESTE        TAG
N01  C                   IF        XKL = 0
     C     GOR08K        READE     GSSORL08                               34
N01  C                   ELSE
N01  C     GSSLGL02K2    READE     GSSLGL02                               34
N01  C  N34              IF        LGHDT = YIDAG
N01  C     LGHTID        COMP      XKL                                34
N01  C                   END
N01  C  N34LGONO         CHAIN     GSSORF                             34
N01  C  N34ORST          CABEQ     'P'           XNESTE
N01  C  N34ORST          CABEQ     'D'           XNESTE
N01  C  N34ORBCD         CABNE     UBCD          XNESTE
N01  C                   END
     C  N34              END                                                     2<-E
      *
     C                   GOTO      STPRT
     C                   ELSE                                                   1
     C                   MOVE      'J'           USLUTT
     C                   END                                                    1<-E
      *
     C     SLUTT         TAG
     C     OPPRTF        IFEQ      '1'                                          1<-B
     C                   CLOSE     GSS016P
     C     XAUTO         IFEQ      'E'                                           2<-B
     C                   EXSR      SNDFAX
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   OUT       SSDTA
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    PCPNO
     C     GOR08K        KLIST
     C                   KFLD                    PCBCD
     C                   KFLD                    YIDAG
N01  C     GSSLGL02K1    KLIST
N01  C                   KFLD                    LGFTYP
N01  C                   KFLD                    XDT
N01  C                   KFLD                    XKL
N01  C     GSSLGL02K2    KLIST
N01  C                   KFLD                    LGFTYP
N01   *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           YIDAG             8 0
     C                   MOVE      WTM           XTID
      *
     C     *ENTRY        PLIST
     C                   PARM                    UQJOB            10
     C                   PARM                    UQUSER           10
     C                   PARM                    UQNBR             6
      *
     C     *DTAARA       DEFINE                  SSDTA
     C     *LOCK         IN        SSDTA
N01   *
N01  C                   MOVE      *ZEROS        XKL               6 0
N01  C                   IF        UKL <> *BLANKS
N01  C                   MOVE      'CRTOR'       LGFTYP
N01  C                   MOVE      UKL           XKL
N01  C     YIDAG         SETLL     KALENDER
N01  c                   READP     KALENDER                               33
N01  c                   IF        KADATE = YIDAG
N01  c                   READP     KALENDER                               33
N01  C                   END
N01  c                   MOVE      KADATE        XDT               8 0
N01  C                   END
      *
     C     USLUTT        IFEQ      ' '
     C     UBIUSI        CHAIN     GSSBIF                             33
     C     *IN33         IFEQ      '1'
     C                   MOVEL     '*JOB'        UPRT
     C                   ELSE
     C                   MOVE      BIPRT         UPRT
     C                   END
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SNDFAX        BEGSR
     C***********************************************
     C     SYLAND        IFNE      *BLANKS
     C     SYLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
N01  C     PCFAX         IFEQ      *BLANKS
N01  C                   EVAL      FAXNRP = 'MAIL'
N01  C                   ELSE
     C                   MOVEL     PCFAX         FAXNRP
N01  C                   END
     C                   END
      *
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C                   MOVE      'B'           UUKODE            1
     C                   MOVEL     'NEWORDER'    DOKNAV           12
     C                   MOVE      'S   '        DOKNAV
     C                   MOVE      *ZEROS        FAXAVD            4
     C                   MOVE      *ZEROS        FAXOPD            7
     C                   MOVE      *ZEROS        FAXTUR            8
     C                   MOVEL     'GSS016P '    FILEN            10
     C     XCP           IFEQ      'Y'
     C                   MOVE      'J'           COVP              1
     C                   MOVEL     XCPTXT        COV01
     C                   MOVEL     XNM           COV02
     C                   MOVE      *BLANKS       COV03
     C                   MOVE      *BLANKS       COV04
     C                   MOVEL     FIFT          COV05
     C                   CALL      'SYGE12CL'
     C                   PARM                    USE              10
     C                   PARM                    NVN              40
     C                   MOVEL     NVN           COV06
     C                   MOVE      *BLANKS       COV07
     C                   MOVE      *BLANKS       COV08
     C                   END
     C     COVP          IFEQ      ' '
     C                   MOVE      'N'           COVP              1
     C                   END
      *
     C                   EVAL      UEMNE = 'New registered order'
     C                   CALL      'GSS016C3'
     C                   PARM                    COVP
     C                   PARM                    UQJOB
     C                   PARM                    UQUSER
     C                   PARM                    UQNBR
     C                   PARM                    FAXNRP
     C                   PARM                    FILEN
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    UEPOST2          70
     C                   PARM                    UEMNE            40
      *
     C                   MOVEL     UQUSER        MAPPE            20
N01  C                   EVAL      PTIME = WTM
N01  C                   EVAL      PDATE = WDT
N01  C                   EVAL      DOKNAV = 'GSS016P'
     C                   CALL      'FAX013'
     C                   PARM                    UQUSER
     C                   PARM                    UQNBR
     C                   PARM                    UQJOB
     C                   PARM                    FAXNRP
     C                   PARM                    DOKNAV
     C                   PARM                    MAPPE
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    XNM
     C                   PARM                    FAXAVD
     C                   PARM                    FAXOPD
     C                   PARM                    FAXTUR
     C                   PARM                    UUKODE
      *
     C                   ENDSR
      *
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
