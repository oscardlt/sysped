             PGM
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&FAKT) TYPE(*DEC) LEN(3 0) VALUE(004)
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0')
                       /* Hvis &in01 på, AVBRYT PROGRAM */
             DCL        VAR(&IN10) TYPE(*LGL) LEN(1) VALUE('0')
                       /* Hvis &in10 på, AMETA FERDIGMELDT */
             DCL        VAR(&P1) TYPE(*CHAR) LEN(2) VALUE('00')
             DCL        VAR(&P2) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&P3) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P4) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P5) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P6) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&P7) TYPE(*CHAR) LEN(1) VALUE('J')
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til fakturaer. */
             DCL        VAR(&PT) TYPE(*CHAR) LEN(10) /* Papirtype til +
                          fakturaer. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7) /* Unikkode +
                          til register SORT. */
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
    /* */
             DCL        VAR(&TURNR) TYPE(*DEC) LEN(8 0) VALUE(0)
             DCL        VAR(&AGENT) TYPE(*DEC)  LEN(8 0) VALUE(0)
             DCL        VAR(&FRA)   TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) /* ARB.STASJ*/
    /* */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* */
    /* KJØRE AMETAAVREGNING                  */
    /* */
             RTVJOBA    JOB(&WS) /* Hente arbeidstsjonskode WS */
 AVD:        CALL       PGM(SYFA00) PARM(&IN01)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
                                                          /**/
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* */
    /* BLANK ARBEIDSFILER ELLER                         */
    /* LEGG TIL NYTT MEMBER (WORKSTATION)               */
    /* */
             CLRPFM     FILE(AMDEWRK) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(AMDEWRK) +
                          MBR(&WS))
    /* */
             CLRPFM     FILE(AMGEWRK) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(AMGEWRK) +
                          MBR(&WS))
    /* */
             CLRPFM     FILE(AMFAKWRK) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(AMFAKWRK) +
                          MBR(&WS))
    /* */
    /* OVERRIDE FILE TIL MEMBER (=DSPSTN NAVN.) */
    /* */
             OVRDBF     FILE(AMDEWRK) TOFILE(AMDEWRK) MBR(&WS)
             OVRDBF     FILE(AMGEWRK) TOFILE(AMGEWRK) MBR(&WS)
             OVRDBF     FILE(AMFAKWRK) TOFILE(AMFAKWRK) MBR(&WS)
             CALL       PGM(SYAM00) PARM(&IN10 &TURNR &AGENT)
             IF         COND(&IN10 = '0') THEN(GOTO CMDLBL(SLUTT)) +
                          /* Hvis &IN10 av, avslutter program. */
    /* */
    /* HENTE OPPLYSNINGER OM HVOR UTSKRIFTEN ØNSKES */
    /* */
             CALL       PGM(SYAMBK) PARM(&IN01 &P3)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* */
    /* SKRIVER UT FAKTURA */
    /* */
             RTVDTAARA  DTAARA(*LDA (681 10)) RTNVAR(&PRT)
             RTVDTAARA  DTAARA(*LDA (691 10)) RTNVAR(&PT)
    /* */
    /* TESTER LASER OG LPI                   */
    /* */
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
    /*---------------------------------------*/
    /* LASER TYPE F GÅR SOM P             */
    /*---------------------------------------*/
             IF         COND(&LASER *EQ 'F') THEN(CHGVAR VAR(&LASER) +
                          VALUE('P'))

    /*                                    */
    /* START FOR IKKE PDF                 */
    /*                                    */
             IF         COND(&LASER *NE 'P') THEN(DO)
                            OVRPRTF FILE(SYAM20P) DEVTYPE(*SCS) +
                          PAGESIZE(72 80) LPI(6) CPI(10) +
                          OVRFLW(72) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES)
                          ENDDO
    /*                                    */
    /* START FOR PDF DOKUMENTER           */
    /*                                    */
             IF         COND(&LASER = 'P') THEN(DO)
                            OVRPRTF FILE(SYAM20P) DEVTYPE(*AFPDS) +
                          PAGESIZE(72 80) LPI(6) CPI(10) +
                          FRONTMGN(0.2 0.1) OVRFLW(72) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          FRONTOVL(&LOGO) OUTQ(&PRT) FORMTYPE(&PT) +
                          COPIES(&COPIES)
                          ENDDO
    /* */
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
    /*                                    */
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)
    /*                                    */
/* SORT OGSÅ I LØSNING 1 - INLIBL FOR Å SIKRE BIBLIOTEKSLISTE */
             CALL INLIBL
             RMVM       FILE(SORT) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(SORT) MBR(&UNIK)
             OVRDBF     FILE(SORT) TOFILE(SORT) MBR(&UNIK)
    /* */
             CALL       PGM(SYAM20) PARM(&P1 &P2 &P3 &P4 &P5 &P6 &P7)
             SNDPGMMSG  MSG(' ' *BCAT &P1 *BCAT 'ametafakturaer er +
                          klare til utskrift på printer ' *BCAT &PRT +
                          *BCAT '.') MSGTYPE(*INFO)
             RMVM       FILE(SORT) MBR(&UNIK)
    /* */
    /* UTSKRIFT AV AMETASKJEMA */
    /* */
             OVRPRTF    FILE(SYAM02PF) DEV(LARPRT01) PAGESIZE(48 +
                          180) CPI(15) OVRFLW(46) OUTQ(*DEV) +
                          FORMTYPE(*STD)
    /* */
             CALL       PGM(SYAM02) PARM(&TURNR &AGENT &FRA)
    /* */
             DLTOVR     FILE(*ALL)
 SLUTT:      ENDPGM
