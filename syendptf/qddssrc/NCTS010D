********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 VIS FRIST
********************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR010PNL)
     A                                      HLPTITLE('REGISTRERING AV TET EKSPO-
     A                                      RT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS010R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeide med NCTS avgang         '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4 13'Avdeling:'
     A            WSAAVD         4Y 0B  4 23TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 30TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20'Velg søkevei med startpunkt'
     A                                      COLOR(WHT)
     A                                  8 20'Ekstern ref:'
     A            WSSOK         35A  B  8 35TEXT('CL-REF')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 20'Dato:'
     A            WSDT           6Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 20'Tolldek.nr.'
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Avsendersnr.'
     A            WSKNS          8Y 0B 11 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 48'Dato:'
     A            WSDTS          6Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Mottakersnr.'
     A            WSKNK          8Y 0B 12 35TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  44
     AO 96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 48'Dato:'
     A            WSDTK          6Y 0B 12 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20'Tariffør:'
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 48'Dato:'
     A            WSDTT          6Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20'Transitnr.:'
     A            WSTRNR        18A  B 14 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 15 20'MottakersNV:'
     A            WSNAK         30A  B 15 35TEXT('Mottakersnavn')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 16 20MSGCON(014 YBC0053 *LIBL/SYMSGF)
     A            WSST           1A  B 16 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 18 20'Ikke avsluttet'
     A            WSAKT          1A  B 18 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  48                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 18 37'(X=Vis)'
     A*                                18 48'Dato:'
     A*           WSDTA          6Y 0B 18 55TEXT('Frist')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A*                                     EDTCDE(4)
     A*                                18 62'(Frist)'
     A                                 18 72'Kun for status P og N:'
     A            WSAKTK         1A  B 18 95TEXT('Kun status P/N')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 26 20'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                 26 36'F4 = Søk avsender/mottaker'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            XIN36          1A  H
     A            THST928        1A  H
     A            THST182        1A  H
     A            THST140        1A  H
     A            THST060        1A  H
     A            THST056        1A  H
     A            THST055        1A  H
     A            THST051        1A  H
     A            THST045        1A  H
     A            THST035        1A  H
     A            THST028        1A  H
     A            THST029        1A  H
     A            THST019        1A  H
     A            THST009        1A  H
     A            THST004        1A  H
     A            THST2          1A  H
     A            THST3          1A  H
     A            THDK           5A  H
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            THAVD          4Y 0O  6  5TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  29                                  COLOR(RED)
     A            THTDN          7Y 0O  6 10TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A  36                                  DSPATR(RI)
     A            THSG           3A  O  6 18TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            WSDT2          6S 0O  6 22TEXT('FRIST')
     A  41                                  COLOR(TRQ)
     A            THTRNR        18A  H
     A            XMRN           1A  H
     A            THGVK          3A  H
     A            THGBL         11S 0H
     A            THGKD          1A  H
     A            WSMLRN        18A  O  6 29TEXT('MRN/LRN')
     A  55                                  DSPATR(HI)
     A  46                                  COLOR(TRQ)
     A            THST           1A  O  6 48TEXT('Status')
     A N49                                  COLOR(RED)
     A  49                                  COLOR(TRQ)
     A            THNAK         30A  O  6 50
     A  43
     AO 40
     AO 44                                  COLOR(TRQ)
     A            THVKB         13Y 3H
     A            THVKB7         7Y 0O  6 81TEXT('Bruttovekt')
     A  38                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            THNTK          7Y 0O  6 89TEXT('KOLLI')
     A  37                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            THENKL         1A  O  6 97COLOR(YLW)
     A            WSPLL          1A  O  6 99
     A            FSSOK17       17A  O  6101
     A  40                                  COLOR(TRQ)
N01  A            WSDDT          6  0O  6119COLOR(RED)
N01  A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      CF15(15 'Skriv ut')
     A                                      CF16(16 'Lag flere nye')
     A                                      CF23(23 'FLERE OPSJON ')
     A                                      CF24(24 'FLERE TASTER ')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(TR010010 TR010PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(TR010020 TR010PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(TR010030 TR010PNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(TR010030 TR010PNL)
     A                                      HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(TR010040 TR010PNL)
     A                                      HLPARA(04 004 04 026)
     A          H                           HLPPNLGRP(TR010055 TR010PNL)
     A                                      HLPARA(04 027 04 044)
     A          H                           HLPPNLGRP(TR010060 TR010PNL)
     A                                      HLPARA(04 046 04 046)
     A          H                           HLPPNLGRP(TR010040 TR010PNL)
     A                                      HLPARA(04 047 04 076)
     A          H                           HLPPNLGRP(TR010070 TR010PNL)
     A                                      HLPARA(04 077 04 077)
     A          H                           HLPPNLGRP(TR010075 TR010PNL)
     A                                      HLPARA(04 078 04 080)
     A          H                           HLPPNLGRP(TR010030 TR010PNL)
     A                                      HLPARA(05 001 05 003)
     A          H                           HLPPNLGRP(TR010040 TR010PNL)
     A                                      HLPARA(05 004 05 026)
     A          H                           HLPPNLGRP(TR010055 TR010PNL)
     A                                      HLPARA(05 027 05 045)
     A          H                           HLPPNLGRP(TR010060 TR010PNL)
     A                                      HLPARA(05 046 05 046)
     A          H                           HLPPNLGRP(TR010040 TR010PNL)
     A                                      HLPARA(05 047 05 076)
     A          H                           HLPPNLGRP(TR010070 TR010PNL)
     A                                      HLPARA(05 077 05 077)
     A          H                           HLPPNLGRP(TR010075 TR010PNL)
     A                                      HLPARA(05 078 05 080)
     A          H                           HLPPNLGRP(TR010040 TR010PNL)
     A                                      HLPARA(06 001 06 026)
     A          H                           HLPPNLGRP(TR010055 TR010PNL)
     A                                      HLPARA(06 027 06 045)
     A          H                           HLPPNLGRP(TR010060 TR010PNL)
     A                                      HLPARA(06 046 06 046)
     A          H                           HLPPNLGRP(TR010040 TR010PNL)
     A                                      HLPARA(06 047 06 076)
     A          H                           HLPPNLGRP(TR010070 TR010PNL)
     A                                      HLPARA(06 077 06 077)
     A          H                           HLPPNLGRP(TR010075 TR010PNL)
     A                                      HLPARA(06 078 06 080)
     A          H                           HLPPNLGRP(TR010080 TR010PNL)
     A                                      HLPARA(07 001 23 080)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS010R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeide med NCTS avgang         '
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A N70                              2 62'41=Ikke ankommet  54=R. release  7-
     A                                      0=Pre-lodged'
     A                                      COLOR(BLU)
     A N70                              3  2'Velg: 1=Lag  2=Endre  3=Kopi  5=Vi-
     A                                      s  6=Print  8=Web  9=NCTS-log  14=K-
     A                                      anseller  16=Nød prosedyre  18=Noti-
     A                                      sblokk 35=Oppd.arkiv'
     A                                      COLOR(BLU)
     A  70                              3  2'12=Send fax/mail 51=dokumentarkiv -
     A                                      54=Release 70=Notific 99=Merke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Tdk.nr. Sig .Dato. MRN-nr -
     A                                      / LRN-nr    S Mottaker             -
     A                                                Brt.vkt   Kolli T L Ekste-
S01  A*                                     rn referanse'
N01  A                                      rn referanse Frist'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRNR        18A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 48DSPATR(UL)
     A            W2NAK         30A  B  5 50DSPATR(UL)
     A            W2SOK         17A  B  5101DSPATR(UL)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            MSGB31        18A  O  1  2MSGID(&MSGF31 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB32        18A  O  1 22MSGID(&MSGF32 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB33        18A  O  1 42MSGID(&MSGF33 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGF31         7A  P
     A            MSGF32         7A  P
     A            MSGF33         7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      CF15(15 'Skriv ut')
     A                                      CF16(16 'Lag flere nye')
     A                                      CF23(23 'FLERE OPSJON ')
     A                                      CF24(24 'FLERE TASTER ')
     A                                  1  2'SYSPED/NCTS010R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeide med NCTS avgang         '
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A N70                              2 62'41=Ikke ankommet  54=R. release  7-
     A                                      0=Pre-lodged'
     A                                      COLOR(BLU)
     A N70                              3  2'Velg: 1=Lag  2=Endre  3=Kopi  5=Vi-
     A                                      s  6=Print  8=Web  9=NCTS-log  14=K-
     A                                      anseller  16=Nød prosedyre  18=Noti-
     A                                      sblokk 35=Oppd.arkiv'
     A                                      COLOR(BLU)
     A  70                              3  2'12=Send fax/mail 51=dokumentarkiv -
     A                                      54=Release 70=Notific 99=Merke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Tdk.nr. Sig .Dato. MRN-nr -
     A                                      / LRN-nr    S Mottaker             -
     A                                                Brt.vkt   Kolli T L Ekste-
S01  A*                                     rn referanse'
N01  A                                      rn referanse Frist'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRNR        18A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 48DSPATR(UL)
     A            W2NAK         30A  B  5 50DSPATR(UL)
     A            W2SOK         17A  B  5101DSPATR(UL)
     A                                 23  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F15=Skriv ut   F16=Lage-
     A                                       flere'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 8 66 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'NCTS010R'
     A                                      COLOR(BLU)
     A                                  1 12'Endre status'
     A                                      COLOR(WHT)
     A            THAVD          4Y 0O  1 35TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            THTDN          7Y 0O  1 40TEXT('Oppdragsnr')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            THSG           3A  O  1 48TEXT('Signatur')
     A                                      COLOR(TRQ)
     A                                  2  2'Status:'
     A            WAST           1A  B  2 11DSPATR(UL)
     A                                  2 15'Bruker:'
     A            WAUSER        10A  O  2 23DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 37'Dato:'
     A            WADT           6  0O  2 43DSPATR(UL)
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A N89                              2 52'Fristil gar:'
     A N89        WAFGAR         1A  B  2 65DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  3  2'Årsaken:'
     A            WAFT1         50A  B  3 11DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            WAFT2         50A  B  4 11DSPATR(UL)
     A            WAFT3         50A  B  5 11DSPATR(UL)
     A            WAFT4         50A  B  6 11DSPATR(UL)
     A            WAFT5         50A  B  7 11DSPATR(UL)
     A N30                              8  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE03
     A                                      WINDOW(*DFT 10 66 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF10(10 'UTFØR')
     A                                      CA12(12 'Forrige')
     A                                  1  2'NCTS010R'
     A                                      COLOR(BLU)
     A                                  1 12'Manuelt fristill garanti'
     A                                      COLOR(WHT)
     A                                  2  2'Garantibeløp som bundet i denne -
     A                                      NCTSen blir normalt'
     A                                  3  2'fristillt automatisk ved mottak -
     A                                      av IE45.'
     A                                  4  2'Før du manuelt frigir skyldbeløpet-
     A                                       MÅ DU HA SJEKKET (OPS. 8)'
     A                                      COLOR(RED)
     A                                  5  2'OM DENNE NCTS ER "Arrived at -
     A                                      destination".'
     A                                      COLOR(RED)
     A                                  6  2'Om du vil manuelt fristille'
     A            THGVK          3A  O  6 30COLOR(YLW)
     A            THGBL         11  0O  6 34DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2'angi evt. kommentar og trykk F10.'
     A                                  8  2'OBS! Garantien er ikke frigitt -
     A                                      selv om skyldbeløpet er fristilt.'
     A                                      COLOR(YLW)
     A                                  9  2'Kommentar:'
     A            WSFT          35A  B  9 13DSPATR(UL)
     A N30                             10  2'F10=Utfør  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30                             10  2'Kommentar må oppgis.'
     A                                      COLOR(WHT)
      *
     A          R BILDE04
     A                                      WINDOW(*DFT 04 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'NCTS010R'
     A                                      COLOR(BLU)
     A                                  1 12'Nødprosedyre'
     A                                      COLOR(WHT)
     A                                  3  2'Nødprosedyre startes?'
     A            WSJN           1A  B  3 24DSPATR(UL)
     A                                      VALUES('J' 'N')
