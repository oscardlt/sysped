********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11062 * 01 YBC PTF11 TEST PÅ GODKJENNINGSDATO
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD006PNL)
     A                                      HLPTITLE('VEDLIKEHOLD')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            HEAVD          4 00H
     A            HEOPD          7 00H
     A            WSNR           1A  B  6  4VALUES(' ' '2')
     A  88                                  DSPATR(PR RI)
     A            HERFK17       17A  O  6 11TEXT('Tolldeklarasjonsnr')
     A            HENAK20       20A  O  6 32TEXT('Navn Kjøper')
     A            HETLL         10   O  6 53TEXT('Tollnr Løpenr')
     A            HETLE          6   O  6 64TEXT('Tollnr Ekspsted')
     A            HEDTMO         8A  O  6 71TEXT('Dekl. godkj.dato')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A            MSG101        50A  O  1  2MSGID(DIV1760 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG102        22A  O  1 58MSGID(DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A            MSG103        12A  O  2  2MSGID(DIV1705 SYMSGF)
     A            UAVD           4  0O  2 16COLOR(BLU)
     A                                      EDTCDE(4)
     A            MSG104        30A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        15A  O  4  2MSGID(DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG106        78A  O  5  2MSGID(SHU1238 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A            MSG201        18A  O  3  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 62MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV0012 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG301        45A  O  1 11MSGID(DIV1762 SYMSGF)
     A            HEOPD          7  0O  2 13TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTLL         10   B  2 23TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
S01  A*                                     DSPATR(PC)
N01  A N89                                  DSPATR(PC)
     A N90                                  COLOR(YLW)
     A            WSTLE          6   B  2 37TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A N90                                  COLOR(YLW)
     A            WSDTG          8   B  2 46TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
N01  A  89                                  DSPATR(HI)
N01  A  89                                  DSPATR(PC)
     A N90                                  COLOR(YLW)
     A            MSG302        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  90        MSG303        78A  O  4  2MSGID(SPT0025 SYMSGF)
     A                                      COLOR(WHT)
N01  A  89        MSG304        78A  O  4  2MSGID(YBC 2013 SYMSGF)
N01  A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        25A  O  2  2MSGID(DIV 1763 SYMSGF)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            W2EXT         35   B  2 28
     A                                      DSPATR(PC)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A            MSG601        50A  O  1  2MSGID(DIV1760 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG602        22A  O  1 58MSGID(DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A            MSG603        12A  O  2  2MSGID(DIV1705 SYMSGF)
     A            UAVD           4  0O  2 16COLOR(BLU)
     A                                      EDTCDE(4)
     A            MSG604        30A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG605        15A  O  4  2MSGID(DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG606        78A  O  5  2MSGID(DIV1761 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG607        18A  O 23  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG608        18A  O 23 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG609        18A  O 23 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG610        18A  O 23 62MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
