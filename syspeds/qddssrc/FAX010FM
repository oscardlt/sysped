     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR FAX010PNL)
     A                                      HLPTITLE('FAXADVISERING FRA OPPDRAG-
     A                                      ')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF10(10 'lage dokument')
     A                                      CF04(04 'søking på kunder')
     A                                      CF11(11 'sendbeskjed')
     A                                      CF06(06 'Rediger dokument')
     A                                      CF08(08 'Fri part')
     A                                      CF07(07 'søk faxlogg')
     A                                      CF09(09 'søk BILREG.')
     A                                      CF12(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(FAX0100010 FAX010PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(FAX010010 FAX010PNL)
     A                                      HLPARA(02 001 02 052)
     A          H                           HLPPNLGRP(FAX010015 FAX010PNL)
     A                                      HLPARA(02 053 02 080)
     A          H                           HLPPNLGRP(FAX010020 FAX010PNL)
     A                                      HLPARA(03 001 03 038)
     A          H                           HLPPNLGRP(FAX010025 FAX010PNL)
     A                                      HLPARA(03 039 03 080)
     A          H                           HLPPNLGRP(FAX010010 FAX010PNL)
     A                                      HLPARA(04 001 06 080)
     A          H                           HLPPNLGRP(FAX010030 FAX010PNL)
     A                                      HLPARA(07 001 07 045)
     A          H                           HLPPNLGRP(FAX010035 FAX010PNL)
     A                                      HLPARA(07 046 07 080)
     A          H                           HLPPNLGRP(FAX010040 FAX010PNL)
     A                                      HLPARA(08 001 08 080)
     A          H                           HLPPNLGRP(FAX010045 FAX010PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(FAX010050 FAX010PNL)
     A                                      HLPARA(10 001 11 080)
     A          H                           HLPPNLGRP(FAX010055 FAX010PNL)
     A                                      HLPARA(12 001 12 038)
     A          H                           HLPPNLGRP(FAX010060 FAX010PNL)
     A                                      HLPARA(12 039 12 080)
     A          H                           HLPPNLGRP(FAX010065 FAX010PNL)
     A                                      HLPARA(13 001 21 080)
     A          H                           HLPPNLGRP(FAX010990 FAX010PNL)
     A                                      HLPARA(22 001 22 039)
     A          H                           HLPPNLGRP(FAX010070 FAX010PNL)
     A                                      HLPARA(22 040 22 080)
     A          H                           HLPPNLGRP(FAX010990 FAX010PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/FAX010'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 73TIME
     A                                      COLOR(GRN)
     A N25                              2 42MSGCON(010 SHU0235 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSAM           1A  B  2 53VALUES('S' 'K' 'B')
     A                                  2 55'S/K/B'
     A N25 26                           2 61MSGCON(018 JOV0789 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  3MSGCON(025 SHU0166 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  3 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSPART         1A  B  3 39VALUES('S' 'K' 'A' 'T' ' ')
     A            KNAVN         30A  B  3 42
     A  25                                  DSPATR(PR)
     A                                  4  3MSGCON(015 SHU0195 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B  4 20COLOR(WHT)
     A                                      EDTCDE(4)
     A                                      DSPATR(PR)
     A            ADR1          30A  B  4 42
     A  25                                  DSPATR(PR)
     A            HEOPD          7Y 0B  5 20COLOR(WHT)
     A                                      EDTCDE(4)
     A                                      DSPATR(PR)
     A            ADR2          30A  B  5 42
     A  25                                  DSPATR(PR)
     A            SYADR3        30A  B  6 42
     A  25                                  DSPATR(PR)
     A                                  7  3MSGCON(025 SHU0191 *LIBL/SYMSGF)
     A            FAXSTD        12A  B  7 30
     A  73                                  DSPATR(RI)
     A                                  7 44MSGCON(010 SHU0239 *LIBL/SYMSGF)
     A            MAPPE         20A  B  7 55
     A                                  8  3MSGCON(025 SHU0540 *LIBL/SYMSGF)
     A            FAXNR         15A  B  8 30
     A  71                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A            WSLAND         2A  B  8 47
     A                                  8 50'='
     A            FAXRET         6A  O  8 52
     A            WSTIME         6Y 0B  8 59DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSDATE         6Y 0B  8 66DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B  9 11CHECK(LC)
     A                                 11  3MSGCON(025 SHU0170 *LIBL/SYMSGF)
     A            FRITXT        20A  B 11 30CHECK(LC)
     A                                      DSPATR(UL)
     A            WKPERS        30A  B 11 51
     A                                 12  3MSGCON(025 SHU0194 *LIBL/SYMSGF)
     A            DOKNA          8A  B 12 30COLOR(WHT)
     A                                 12 40MSGCON(025 SHU0212 *LIBL/SYMSGF)
     A            FRIKOD         1A  I 12 67
     A                                 13  3MSGCON(006 SHU0211 *LIBL/SYMSGF)
     A            WRI001        70A  B 13 10
     A                                 14  7'02'
     A            WRI002        70A  B 14 10
     A                                 15  7'03'
     A            WRI003        70A  B 15 10
     A                                 16  7'04'
     A            WRI004        70A  B 16 10
     A                                 17  7'05'
     A            WRI005        70A  B 17 10
     A                                 18  7'06'
     A            WRI006        70A  B 18 10
     A                                 19  7'07'
     A            WRI007        70A  B 19 10
     A                                 20  7'08'
     A            WRI008        70A  B 20 10
     A                                 21  7'09'
     A            WRI009        70A  B 21 10
     A                                 22  2MSGCON(018 SHU0192 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 21MSGCON(018 SHU0223 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 40MSGCON(025 SHU0200 *LIBL/SYMSGF)
     A            DOKNAO         8A  O 22 67COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 SHU0168 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 JOV0790 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 21MSGCON(030 SHU0541 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILD02GM
     A                                      CF03(03 'Avslutt program')
     A                                      CF10(10 'lage dokument')
     A                                      CF04(04 'søking på kunder')
     A                                      CF11(11 'sendbeskjed')
     A                                  1  3'SYSPED/FAX010'
     A                                      COLOR(BLU)
     A                                  1 21'SYSPED      FAX-UTILITY'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 73TIME
     A                                      COLOR(GRN)
     A                                  3  3MSGCON(025 SHU0166 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  3 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            KNAVN         30A  O  3 42
     A            ADR1          30A  O  4 42
     A            ADR2          30A  O  5 42
     A            SYADR3        30A  O  6 42
     A                                  7  3MSGCON(025 TBR0099 *LIBL/SYMSGF)
     A                                  8  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            FAXNR         15A  B  8 30
     A  71                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  9  3MSGCON(025 SHU0191 *LIBL/SYMSGF)
     A            FAXSTD        12A  B  9 30
     A            WKPERS        30   B  9 50
     A                                 10  3MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIME         6Y 0B 10 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 11  3MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B 11 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 12  3MSGCON(025 SHU0170 *LIBL/SYMSGF)
     A            FRITXT        20A  B 12 30CHECK(LC)
     A                                      DSPATR(UL)
     A                                 13  3MSGCON(060 SHU0175 *LIBL/SYMSGF)
     A            WSTEXT       720A  B 14  1DSPATR(UL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 SHU0168 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 SHU0176 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
