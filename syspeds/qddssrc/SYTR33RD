     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            HEAVD     R        H      REFFLD(HEADFR/HEAVD *LIBL/HEADF)
     A            HEOPD     R        H      REFFLD(HEADFR/HEOPD *LIBL/HEADF)
     A            WSPOS          3A  B  6  2
     A            SFNAVN        17A  O  6  7
     A  52                                  DSPATR(RI)
     A            SFADR1        17A  O  6 25
     A  52                                  DSPATR(RI)
     A            SFADR3        16A  O  6 43
     A  52                                  DSPATR(RI)
     A            SFNT           3Y 0O  6 60EDTCDE(4)
     A            SFVKT          5Y 0O  6 64EDTCDE(4)
     A            SFM3           4Y 2O  6 70EDTCDE(4)
     A            HELM           4Y 2O  6 76EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYTR33R'
     A                                      COLOR(BLU)
     A                                  1 21'Sette rekkefølge/posisjon på tur'
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Turnr:'
     A            UPRO           8S 0O  2 10COLOR(WHT)
     A            ZRECNO         4S 0O  2 66SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 73TIME
     A                                  4  2'Skriv inn posisjonsnummer og trykk-
     A                                       enter (tips 010, 020, 030 ) N=NEST-
     A                                      E!! '
     A                                      COLOR(BLU)
     A                                  5  2'Pos  Mottaker Navn     Gateadresse-
     A                                             Postn Sted       Ant  Vekt  -
     A                                      Kub. La.m.'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF07(07 'RENUM_01')
     A                                      CF10(10 'RENUM_10')
     A                                      CF12(03)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A  51                              1 17'Navn i'
     A  51                              1 24'motsatt kontrast'
     A                                      DSPATR(RI)
     A  51                              1 42'betyr inngående/henting'
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42'F7=Renum 01,02.. F10=Renum 010,020-
     A                                      ..'
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
