********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SJEKK OM DET ER LOV Å PLUKKE HELE PO
********************************************************************
     FEPOH      IF   E           K DISK
     FEPOL      IF   E           K DISK
      *
      * File open / keys
     C                   EXSR      INIT
      *
      * TEST PICK HEADING
     C     EPOHK         CHAIN     EPOH                               33
     C                   SELECT
     C                   WHEN      *IN33
      * BESTILLING FINNES IKKE. IKKE PLUKK
     C                   EVAL      UPCKUPH = 'N'
     C                   WHEN      HESDF <> EHFSTE OR HESDT <> EHTSTE
      * IKKE SAMME FRA/TIL STED, IKKE PLUKK
     C                   EVAL      UPCKUPH = 'N'
     C                   WHEN      EHAVD = 0
     C     EPOHK         SETLL     EPOL
     C     EPOHK         READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF
     C                   IF        ELAVD > 0 OR
     C                             (ELAVD = 0 AND ELETD <> EHETD)
      * MINST EN LINJE ER PLUKKET. IKKE PLUKK
     C                   EVAL      UPCKUPH = 'N'
     C                   LEAVE
     C                   END
N01   * MINST EN LINJE MED AVVIENDE FRA/TIL STED
N01  C                   IF        ELFSTE <> *BLANKS OR ELTSTE <> *BLANKS
N01  C                   EVAL      UPCKUPH = 'N'
N01  C                   LEAVE
N01  C                   END
     C     EPOHK         READE     EPOL                                   33
     C                   ENDDO
     C                   OTHER
      * BESTILLING ER PLUKKET. IKKE PLUKK
     C                   EVAL      UPCKUPH = 'N'
     C                   ENDSL
      *
      * TEST PICK LINJE
     C     EPOLK         CHAIN     EPOL                               33
     C                   SELECT
     C                   WHEN      *IN33
      * LINJE FINNES IKKE. IKKE PLUKK
     C                   EVAL      UPCKUPL = 'N'
S01  C*                  WHEN      HESDF <> EHFSTE OR HESDT <> EHTSTE
S01   * IKKE SAMME FRA/TIL STED, IKKE PLUKK
S01  C*                  EVAL      UPCKUPL = 'N'
N01  C                   WHEN      HESDF <> ELFSTE AND ELFSTE <> *BLANKS
N01   * LINJE MED AVVIENDE FRASTED SOM ER ULIK MED OPPDRAGS FRASTED, IKKE PLUKK
N01  C                   EVAL      UPCKUPL = 'N'
N01  C                   WHEN      HESDT <> ELTSTE AND ELTSTE <> *BLANKS
N01   * LINJE MED AVVIENDE TILSTED SOM ER ULIK MED OPPDRAGS TILSTED, IKKE PLUKK
N01  C                   EVAL      UPCKUPL = 'N'
N01  C                   WHEN      HESDF <> EHFSTE AND ELFSTE = *BLANKS
N01   * LINJE UTEN AVVIENDE FRASTED MED HODES FRASTED ER ULIK MED OPPDRAGS FRASTED, IKKE PLUKK
N01  C                   EVAL      UPCKUPL = 'N'
N01  C                   WHEN      HESDT <> EHTSTE AND ELTSTE = *BLANKS
N01   * LINJE UTEN AVVIENDE TILSTED MED HODES TILSTED ER ULIK MED OPPDRAGS TILSTED, IKKE PLUKK
N01  C                   EVAL      UPCKUPL = 'N'
     C                   WHEN      ELAVD > 0
      * LINJE ER PLUKKET. IKKE PLUKK
     C                   EVAL      UPCKUPL = 'N'
     C                   WHEN      ELSTA3 = 'R'
      * SPLITTET LINJE SOM IKKE HAR FÅTT LINJENR. IKKE PLUKK
     C                   EVAL      UPCKUPL = 'N'
     C                   ENDSL
      *
     C                   IF        UPCKUPL = 'N'
     C                   EVAL      UPCKUPH = 'N'
     C                   END
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
      *
     C     *ENTRY        PLIST
     C                   PARM                    EHKUND            8 0
     C                   PARM                    EHBES            10
     C                   PARM                    UELLIN            7 0
     C                   PARM                    UELULIN           5 0
     C                   PARM                    HESDF             5
     C                   PARM                    HESDT             5
     C                   PARM                    UPCKUPH           1
     C                   PARM                    UPCKUPL           1
      *
     C     EPOHK         klist
     C                   kfld                    EHKUND
     C                   kfld                    EHBES
     C     EPOLK         klist
     C                   kfld                    EHKUND
     C                   kfld                    EHBES
     C                   kfld                    UELLIN
     C                   kfld                    UELULIN
      *
     C                   EVAL      UPCKUPH = 'J'
     C                   EVAL      UPCKUPL = 'J'
      *
     C                   ENDSR
      *
