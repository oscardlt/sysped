      *********************************************************************
      *  Innsending av MRN for godsreg/Ankomstmelding (eller kontroll)
      *  OBS!! HARDKODINGER FOR KOMPLETT.NO - LIGGER I STANDARD SOM EN GOD KOPIERINGSMAL
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EKOMPANK MODULE(*LIBL/EKOMPANK *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *********************************************************************
      * 01 JOV PTF10 Uppify av MRN-nr
      ** %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 02 JOV PTF12 Flere Bevillingskoder
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FTRIH05    IF A E           K DISK    USROPN
     FGODSJF    O  A E             DISK    USROPN
     FTRISTD    UF   E           K DISK    USROPN
     FEDIC      UF   E             DISK    USROPN
     FEDIM4     IF A E           K DISK    USROPN
     FTVIND     UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D LAGER           S              1
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D FØRSTE          S              1
     D FeilText        S            250
     D TETSTS          S            100
     D FunkTxt         S            250
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D MRNNR                   1     18
     D  MRNNR1_2               1      2
     D  MRNNR3_4               3      4
     D  MRNNR18               18     18
     D                 DS
     D  TIAVD                  1      4  0
     D  XITDN                  5     11  0
     D  TISG                  12     14
     D  X14S                   1     14
     D                 DS
     D  X0068Æ                 1      8
     D  X0068A                 9     16  0
     D  X0068B                17     22  0
     D  X0068Å                 1     22
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C**                 callp     wrtsection('all')
     C                   callp     wrtsection('Top')
     C                   callp     wrtsection('FunkTop')
     c                   exsr      Funk
     C                   callp     wrtsection('FunkBot')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      LAGER   = zhbgetvar('LAGER')
     C                   eval      MRNNR   = zhbgetvar('MRNNR')
N01  C                   eval      MRNNR   = uppify(MRNNR)
     C     WSuser        IFEQ      *blanks
     c                   move      'J'           FØRSTE
     c                   endif
S    c***                eval      wsuser  = 'KOMPANK'
      *
     C                   endsr
      *
      *=====================================================================******
      * fyll dropdown-lista  for funksjon
      *=====================================================================******
     C     Funk          begsr
      * blank
     C                   callp     updhtmlvar('FUNKSEL':'')
     C                   callp     updhtmlvar('FUNKVAL':' ')
     c                   eval      FunkTxt ='--- Velg funksjon  ---'
     C                   callp     updhtmlvar('FUNKTXT':FunkTxt)
     C                   callp     wrtsection('FunkRow')
      * '0'
     c     Lager         ifeq      '0'
     C                   callp     updhtmlvar('FUNKSEL':'selected')
     c                   else
     C                   callp     updhtmlvar('FUNKSEL':'')
     c                   endif
     C                   callp     updhtmlvar('FUNKVAL':'0')
S02  c*                  eval      FunkTxt ='Ankomstmelding (be om losse'
S02  c*                            +'tillatelse) VANLIG LAGER PO 45000...'
N02  c                   eval      FunkTxt ='KOMPL. SERVICES - Ank.meld (be om '
N02  c                             +'lossetillatelse) VANLIG LAGER PO 45000'
     C                   callp     updhtmlvar('FUNKTXT':FunkTxt)
     C                   callp     wrtsection('FunkRow')
      * '1'
     c     Lager         ifeq      '1'
     C                   callp     updhtmlvar('FUNKSEL':'selected')
     c                   else
     C                   callp     updhtmlvar('FUNKSEL':'')
     c                   endif
     C                   callp     updhtmlvar('FUNKVAL':'1')
S02  c*                  eval      FunkTxt ='Ankomstmelding (be om losse'
S02  c*                            +'tillatelse) TOLLAGER PO. 48000...'
N02  c                   eval      FunkTxt ='KOMPL. SERVICES - Ank.meld (be om '
N02  c                             +'lossetillatelse) TOLLAGER PO 48000'
     C                   callp     updhtmlvar('FUNKTXT':FunkTxt)
     C                   callp     wrtsection('FunkRow')
N02   * '2'
N02  c     Lager         ifeq      '2'
N02  C                   callp     updhtmlvar('FUNKSEL':'selected')
N02  c                   else
N02  C                   callp     updhtmlvar('FUNKSEL':'')
N02  c                   endif
N02  C                   callp     updhtmlvar('FUNKVAL':'2')
N02  c                   eval      FunkTxt ='KOMPL. DISTRIB. - Ank.meld (be om '
N02  c                             +'lossetillatelse) VANLIG LAGER PO 45000'
N02  C                   callp     updhtmlvar('FUNKTXT':FunkTxt)
N02  C                   callp     wrtsection('FunkRow')
N02   * '3'
N02  c     Lager         ifeq      '3'
N02  C                   callp     updhtmlvar('FUNKSEL':'selected')
N02  c                   else
N02  C                   callp     updhtmlvar('FUNKSEL':'')
N02  c                   endif
N02  C                   callp     updhtmlvar('FUNKVAL':'3')
N02  c                   eval      FunkTxt ='KOMPL. DISTRIB. - Ank.meld (be om '
N02  c                             +'lossetillatelse) TOLLAGER PO 48000'
N02  C                   callp     updhtmlvar('FUNKTXT':FunkTxt)
N02  C                   callp     wrtsection('FunkRow')
      * 'S'
     c     Lager         ifeq      'S'
     C                   callp     updhtmlvar('FUNKSEL':'selected')
     c                   else
     C                   callp     updhtmlvar('FUNKSEL':'')
     c                   endif
     C                   callp     updhtmlvar('FUNKVAL':'S')
     c                   eval      FunkTxt ='Forespørsel på tidligere '
     c                             +'innsendt ankomsmelding.'
     C                   callp     updhtmlvar('FUNKTXT':FunkTxt)
     C                   callp     wrtsection('FunkRow')
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('KUNDE':BIBESK)
     c                   move      *blanks       FeilText
     c     FØRSTE        ifne      'J'
     C                   EXSR      TEST
     c                   endif
     C                   callp     updHTMLvar('MRNNR':MRNNR)
     C                   callp     updHTMLvar('FEILTEXT':FEILTEXT)
      *
     C                   endsr
      *=====================================================================******
      * TEST INPUT - SEND I FALL OK
      *=====================================================================******
     C     TEST          begsr
      *
      * div tester....
     c     LAGER         Ifeq      ' '
     C                   eval      FeilText ='Du må velge funksjon - Enten send'
     c                             +'e ankomstmelding (be om lossetillatelse) '
     c                             +'for gitt lagerkode, eller spørre etter '
     c                             +'status.'
     c                   goto      UtTest
     c                   endif
      *
     c     MRNNR1_2      Iflt      '09'
     c     MRNNR1_2      orgt      '99'
     C                   eval      FeilText ='MRNnr må starte med ÅR (2 lang)'
     c                             +'+ Landkode og være 18 langt '
     c                   goto      UtTest
     c                   endif
     c     MRNNR3_4      Iflt      'AA'
     c     MRNNR3_4      orgt      'ZZ'
     C                   eval      FeilText ='MRNnr må starte med ÅR (2 lang)'
     c                             +'+ Landkode og være 18 langt '
     c                   goto      UtTest
     c                   endif
     c     MRNNR18       Ifeq      ' '
     C                   eval      FeilText ='MRNnr må starte med ÅR (2 lang)'
     c                             +'+ Landkode og være 18 langt '
     c                   goto      UtTest
     c                   endif
      * Finnes fra før ??
     c     MRNNR         chain     TRIH05                             33
     c     *in33         ifeq      *off
     c     Lager         oreq      'S'
     c     *in33         ifeq      *on
     c                   eval      FeilText ='Ankomstmelding finnes ikke på '
     c                             +MRNNR+'.<BR>Send ankomstmelding '
     c                             +'for aktuelt lager om ønskelig'
     c                   else
     c                   exsr      FinnStat
     c                   eval      FeilText ='MRNnr '+MRNNR
     c                             +' ER allerede Ankomstmeldt.'
     c                             +'&nbsp;&nbsp; (Godsnr '+TIGN+')'
     c                             +'<BR>Status: '+TETSTS
     c     M1001         ifne      *blanks
     c                   eval      FeilText =%trim(FeilText)
     c                             +'&nbsp;&nbsp; Status mottatt: '
     c                             +%trim(%editw(MDT:'    .  .  '))
     c                             +' Kl: '+%trim(%editw(MTM:'  :  :  '))
     c                   endif
     c                   endif
     c                   move      *blanks       MRNNR
     c                   move      ' '           LAGER
     c                   goto      UtTest
     c                   endif
      *
      * kommet gjennom tester - send inn..
     c                   exsr      ANKMELD
     C                   eval      FeilText ='Du har nå sendt ankomstmelding '
     c                             +'på MRN-nummer ' + MRNNR
     c                             +'<BR>(Godsnr '+TIGN+')'
     c                   move      *blanks       MRNNR
     c                   move      ' '           LAGER
      *
     C     UtTest        endsr
      *=====================================================================******
      * Finn status / siste svar fra TET
      *=====================================================================******
     C     FinnStat      begsr
      *------------------
      *   finn siste svar...
     c                   move      *blanks       M1001
     c                   move      *blanks       M1225
     c                   move      *zeros        MDT
     c                   move      *zeros        MTM
     c     EDIMkey       setgt     EDIM4
     c     EDIMkey       readpe    EDIM4                                  33
     c     *in33         doweq     '0'
     c     M1001         cabeq     'TEI'         UtEDIM
     c     EDIMkey       readpe    EDIM4                                  33
     c                   enddo
     c     UtEdim        tag
      *
     c                   select
     c     M1225         wheneq    '043'
     c                   eval      TETSTS='IE43 Lossetillatelse gitt.'
     c     M1225         wheneq    '008'
     c                   eval      TETSTS='IE08 IKKE Lossetillatelse !!'
     c     M1225         wheneq    '025'
     c                   eval      TETSTS='IE25 Transitt avsluttet.'
     c     M1225         wheneq    '058'
     c                   eval      TETSTS='IE58 FEIL ved Lossemerknad !!'
     c                   Other
     c                   eval      TETSTS='Ukjent !!'
     c     M1001         ifeq      'TEI'
     c                   move      M1225         FUNK2             2
     c                   eval      TETSTS='IE'+FUNK2+' Ukjent status !!'
     c                   endif
     c                   endsl
      *
     c                   endsr
      *
      *=====================================================================******
      * Godsregistrer / Lag-send ankomstmelding
      *=====================================================================******
     C     AnkMeld       begsr
      *------------------
      * Tildel godsnummer (låner retur-parameter for å sende inn lagerkode/3-siste siffer..)
      * 0/1= Komplett services Normal/Tollager   - 2/3 =Komplett distribution Normal/Tollager
     c                   movel     Lager         Ulager
N02  C                   eval      GOMOTT ='KOMPLETT AS'
N02  c                   eval      UKODE ='06186'                               Services
N02  C                   If        ULager='3'
N02  c                   eval      ULager='1'
N02  c                   eval      UKODE ='06253'                               Distribution
N02  C                   eval      GOMOTT ='KOMPLETT DISTR.'
N02  c                   endif
N02  C                   If        ULager='2'
N02  c                   eval      ULager='0'
N02  c                   eval      UKODE ='06253'                               Distribution
N02  C                   eval      GOMOTT ='KOMPLETT DISTR.'
N02  c                   endif
     C                   CALL      'SYGE51'
     C                   PARM                    UÅR               4
     C                   PARM                    UKODE             5
     C                   PARM                    UDGNR             3
     C                   PARM                    ULager            1
     C                   PARM                    GOGN
      *
      * OPPDATER I GODSJF
S02  C*                  eval      GOMOTT ='KOMPLETT AS'
     C                   MOVEL     MRNNR         GOTRNR
     C                   MOVE      WDT           GOTRDT
     C                   WRITE     GODSJR
      *
      * SEND TET-IMPORT (IE07) TIL TOLLVESENET
      * OPPRETTE EN POST I TET-IMPORT
     c     Ulager        ifeq      '1'
     C                   Z-ADD     10            TIAVD
     c                   else
     C                   Z-ADD     1             TIAVD
     c                   end
N02   * avvikende avdelinger for distribution
N02  C     UKODE         ifeq      '06253'
N02  c     Ulager        ifeq      '1'
N02  C                   Z-ADD     20            TIAVD
N02  c                   else
N02  C                   Z-ADD     2             TIAVD
N02  c                   end
N02  c                   end
     C     TIAVD         CHAIN     TRISTD                             33
     C                   MOVE      TITDN         XTDN
     C                   ADD       1             TITDN
     C                   UPDATE    TRISTDR
     C                   EVAL      TIST   = 'Q'
     C                   EVAL      TITDN  = XTDN
     C                   EVAL      TISG   = 'GLG'
     C                   MOVE      WDT           TIDT
     C                   EVAL      TIENKL = 'J'
     C                   EVAL      TIGN   = GOGN
     C                   EVAL      TITRNR = MRNNR
     C**                 EVAL      TIALK  = 'DE'
     C                   EVAL      TI0035 = S0035
     C                   EVAL      TIMF   = '007'
     C                   WRITE     TRIHR
      *
      * OPPRETTE EN POST I EDIM
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           MMN
     C                   MOVE      WDT           M0068A
     C     M0068A        CHAIN     TVIND                              32
     C   32              Z-ADD     M0068A        D0068A
     C   32              Z-ADD     0             D0068B
     C                   ADD       1             D0068B
     C   32              WRITE     TVINDF
     C  N32              UPDATE    TVINDF
     C                   MOVE      D0068B        M0068B
     C                   EVAL      M0004  = S0004
     C                   EVAL      M0010  = S0010
     C                   EVAL      M0035  = S0035
     c                   eval      XITDN  = TITDN
     C                   EVAL      M0062  = X14S
     C                   EVAL      M0065  = 'CUSDEC'
     C                   EVAL      X0068Æ = S0004
     C                   EVAL      X0068A = M0068A
     C                   EVAL      X0068B = M0068B
     C                   MOVEL     X0068Å        M0068
     C                   EVAL      M1001  = 'TEI'
     C                   EVAL      M1225  = '007'
     C                   EVAL      M1N07  = '007'
     C                   EVAL      MSR    = 'S'
     C                   EVAL      MAVD   = TIAVD
     C                   EVAL      MTDN   = TITDN
     C                   WRITE     EDIMR
      *
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EKOMPANK'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      TRIH05
     C                   OPEN      GODSJF
     C                   OPEN      TRISTD
     C                   OPEN      EDIC
     C                   OPEN      EDIM4
     C                   OPEN      TVIND
      *
     C                   Z-ADD     10            XPNR              8 0
     C     *LIKE         DEFINE    TITDN         XTDN
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT08             8 0
      *
     C     EDIMKEY       KLIST
     c                   KFLD                    XSR               1
     c                   KFLD                    TIAVD
     c                   KFLD                    TITDN
     c                   move      'R'           XSR
     c
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     TRIH05
     C                   close     GODSJF
     C                   close     TRISTD
     C                   close     EDIC
     C                   close     EDIM4
     C                   close     TVIND
     C                   eval      *inlr = *on
      *
     C                   endsr
