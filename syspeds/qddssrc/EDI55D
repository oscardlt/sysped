     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/ESTYF)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      ALTHELP
     A                                      HELP
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
      *
     A          R EDI55DA                   SFL
     A            ZOP            1A  I  7  3VALUES(' ' '2' '3' '4' '5')
     A            ESFIKD    R        O  7  7REFFLD(ESTYR/ESFIKD *LIBL/ESTYF)
     A            ESPTYP    R        O  7 17REFFLD(ESTYR/ESPTYP *LIBL/ESTYF)
     A            ESPNUM    R        O  7 24EDTCDE(4)
     A            ESDKID    R        O  7 34REFFLD(ESTYR/ESDKID *LIBL/ESTYF)
     A            ESDKSM    R        O  7 43REFFLD(ESTYR/ESDKSM *LIBL/ESTYF)
     A            ESPNVN    R        O  7 51
      *
     A          R EDI55DB                   SFLCTL(EDI55DA)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Frisk opp')
     A                                      CF06(06 'Lage Ny')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 30'Styringsfil L1'
     A                                      COLOR(WHT)
     A                                  1 75'EDI55D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(020 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4MSGCON(008 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 15MSGCON(008 EDI0703 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 26MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 37MSGCON(008 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  5 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  6  2'Alt Firma  Part.type  Part.num  ED-
     A                                      I-dok S/M      Part.navn           -
     A                                               '
     A                                      COLOR(WHT)
      *
     A          R EDI55DC
     A                                      OVERLAY
     A                                 22  4MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 17MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 30MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 43MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI55DD
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 32'Styringsfil L1'
     A                                      COLOR(WHT)
     A                                  1 75'EDI55D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(028 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Firmakode . . . . . . . . . :'
     A            ÅSFIKD    R        B  5 34REFFLD(ESTYR/ESFIKD *LIBL/ESTYF)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A                                  6  4'Partnernummer . . . . . . :'
     A            ÅSPNUM    R        B  6 34REFFLD(ESPNUM)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A                                  7  4'Partnertype . . . . . . . :'
     A            ÅSPTYP    R        B  7 34REFFLD(ESTYR/ESPTYP *LIBL/ESTYF)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A                                  7 45'K=Kunde  L=Leverandør'
     A                                  8  4'EDI-dokument  . . . . . . :'
     A            ÅSDKID    R        B  8 34REFFLD(ESTYR/ESDKID *LIBL/ESTYF)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A                                  9  4'Sendning/Mottak . . . . . :'
     A            ÅSDKSM    R        B  9 34REFFLD(ESDKSM)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A                                      VALUES('S' 'M')
     A                                  9 41'S=Sendning  M=Mottak'
     A                                 10  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅSPNVN    R        B 10 34REFFLD(ESPNVN)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 12  4'Behandlingsregel  . . . . :'
     A            ÅSDKBH    R        B 12 34REFFLD(ESTYR/ESDKBH *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 13  4'Startdato . . . . . . . . :'
     A            ÅSSTDT    R        B 13 34REFFLD(ESTYR/ESSTDT *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 14  4'Mapping . . . . . . . . . :'
     A            ÅSMPID    R        B 14 34REFFLD(ESTYR/ESMPID *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 15  4'Nettverk  . . . . . . . . :'
     A            ÅSNTID    R        B 15 34REFFLD(ESTYR/ESNTID *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 16  4'Egen p.k  . . . . . . . . :'
     A            ÅSNTPK    R        B 16 34REFFLD(ESTYR/ESNTPK *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 17  4'ETS partner . . . . . . . :'
     A            ÅSTRPA    R        B 17 34REFFLD(ESTYR/ESTRPA *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 18  4'Eget nummer hos partner . :'
     A            ÅSRNUM    R        B 18 34REFFLD(ESTYR/ESRNUM *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A                                 22  2MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 11  4'St. . . . . . . . . . . . :'
     A            ÅSDKST    R        B 11 34REFFLD(ESTYR/ESDKST *LIBL/ESTYF)
     A  42                                  DSPATR(PR)
     A          R EDI55DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
