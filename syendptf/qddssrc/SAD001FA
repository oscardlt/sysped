     A*%%TS  SD  20240105  093129  JOVO        REL-V7R4M0  5770-WDS
     A*
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0606A* 01 CB  PTF6 NYE FELTER I SADL
P9XXXA* 02 YBC PTF9 NY OPSJON, 3=KOPIERING
P9039A* 03 YBC PTF9 SØK VIA VAREBESKRIVELSE
10244A* 04 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
     A*              MYE ENDRING. INGEN MERKING
10262A* 05 YBC PTF10 SATS ER UTVIDERT TIL 7.2, INGEN MERKING
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXXA* 06 YBC PTF10 FLERE LINJER VISES
11XXXA* 07 SH  PTF12 TESTER PÅ TARIFFNR LYSER HVIS FEIL
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R BILDE01
     A                                      CF04(04 'Søk kundenr.')
     A                                      CF02(02 'kopier')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD001A'
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(035 DIV0015 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1100DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2100TIME
     A                                      COLOR(BLU)
     A                                 11  2'Arbeid med kunde:'
     A            WSKUNR         8Y 0B 11 21DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2'Kopier fra kunde:'
     A            WSKUNRC        8Y 0B 13 21EDTCDE(Z)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0636 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42'F2=Start kopiering'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '2' '3' '4')
     A            LEVENR         8S 0H
     A            VARENR        28A  O  5  5TEXT('ALFASØKING')
     A            VAREBE        50A  O  5 34TEXT('ALFASØKING')
     A            W2VF           1A  O  5 85TEXT('OM TEKST TIL RUBR. 31')
     A            W2LK           2A  O  5 87TEXT('OPPRINGELSE LAND')
N07  A  78                                  DSPATR(RI)
     A            W2VNTI         8A  O  5 91TEXT('TARIFFNR.')
N07  A  77                                  DSPATR(RI)
     A            W2TN           1A  O  5100TEXT('TOLLNEDSETTELSE')
     A            W2PRE          1A  O  5103TEXT('PREFERANSE')
     A            W2PVA          1A  O  5111TEXT('PVA')
S05  A*           W2AS           5Y 2O  5114TEXT('PVA SATS')
N05  A            W2AS           7Y 2O  5114TEXT('PVA SATS')
     A                                      EDTCDE(4)
S05  A*           W2MFR          1A  O  5121TEXT('MOMS')
N05  A            W2MFR          1A  O  5123TEXT('MOMS')
     A            W2AKD1         2A  H      TEXT('AVGIFTSKODE')
     A            W2ASV1         3A  H      TEXT('AVGIFTS SEKVENS')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
S06  A*                                     SFLSIZ(0016)
S06  A*                                     SFLPAG(0015)
N06  A                                      SFLSIZ(0019)
N06  A                                      SFLPAG(0018)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SAD001A'
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(040 DIV0015 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1105DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 30COLOR(WHT)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34'2=Endre   3=Kopier   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S05  A*                                 4  5MSGCON(126 JOV1323 *LIBL/SYMSGF)
N05  A                                  4  5MSGCON(128 JOV1323 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A*%%TS  SD  20240105  092538  JOVO        REL-V7R4M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2MSGCON(020 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(020 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(020 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(020 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 82MSGCON(020 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 65'F8=Søk'
     A                                      COLOR(BLU)
     A*
     A          R VAREL2
     A*%%TS  SD  20240105  093129  JOVO        REL-V7R4M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(23)
     A                                      CLRL(05)
     A                                  1  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A                                  1 15'Varenr:'
     A            W2VANR        28A  I  1 23DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2 15'V.beskrivelse:'
     A            W2VABE        50A  I  2 30DSPATR(UL)
     A                                  3 15'Tariff:'
     A            W2TARI         8A  I  3 23DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32'Oppr.land:'
     A            W2LK           2A  I  3 43DSPATR(UL)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
S05  A*                                 1  5MSGCON(126 JOV1323 *LIBL/SYMSGF)
N05  A                                  1  5MSGCON(128 JOV1323 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            VARENR        28A  O  2  5TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            VAREBE        50A  O  2 34TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            W2VF           1A  O  2 85TEXT('TEKST TIL RUBR. 31')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W2LK           2A  O  2 87TEXT('OPPRINGELSE LAND')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W2VNTI         8A  O  2 91TEXT('TARIFFNR.')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W2TN           1A  O  2100TEXT('TOLLNEDSETTELSE')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W2PRE          1A  O  2103TEXT('PREFERANSE')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            W2PVA          1A  O  2111TEXT('TOLL')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
S05  A*           W2AS           5Y 2O  2114TEXT('PVA SATS')
N05  A            W2AS           7Y 2O  2114TEXT('PVA SATS')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
S05  A*           W2MFR          1A  O  2121TEXT('MOMS FRI')
N05  A            W2MFR          1A  O  2123TEXT('MOMS FRI')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SAD001A'
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(040 DIV0015 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1102DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 30COLOR(WHT)
     A                                  2 62TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34'2=Endre   3=Kopier   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S05  A*                                 4  5MSGCON(126 JOV1323 *LIBL/SYMSGF)
N05  A                                  4  5MSGCON(128 JOV1323 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(23)
     A                                      CLRL(05)
     A                                  2  5'Varenr:'
     A                                      COLOR(WHT)
     A            WSVANR        28A  B  2 16TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                  3  5'Varebeskr:'
     A                                      COLOR(WHT)
     A            WSVABE        50A  B  3 16TEXT('BESKRIVELSE')
     A  40N30                               DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
