      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE032R MODULE(*LIBL/TSVE032R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVEF3     UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D SVEF_SYAV       S              4S 0
     D SVEF_SYOP       S              7S 0
     D SVEF_FATY       S              4
     D TYP             S              4
     D SVEF_FATX       S             35
     D SVEF_VAKD       S              3
     D SVEF_VAKU       S              7S 4
     D SVEF_FABL       S             11S 3
     D SVEF_TUID       S             10

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SVEF_SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SVEF_SYOP:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(SVEF_FATX)+'¬, '
        +'¬typ¬ : ¬'+%trim(TYP)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invoiceUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVEF3

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      SVEF_SYAV  = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      SVEF_SYOP  = c2n2(WA)
     C                   EVAL      SVEF_FATX  = zhbgetvar('FAK')
     C                   EVAL      TYP        = zhbgetvar('TYP')
     C                   EVAL      SVEF_FATY  = zhbgetvar('SVEF_FATY')
     C                   EVAL      SVEF_VAKD  = zhbgetvar('SVEF_VAKD')
     C                   EVAL      WA      = zhbgetvar('SVEF_VAKU')
N01  C                   EVAL      SVEF_VAKU  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SVEF_FABL')
N01  C                   EVAL      SVEF_FABL  = c2n2(WA)
     C                   EVAL      SVEF_TUID   = zhbgetvar('SVEF_TUID')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVEFKEY       KLIST
     C                   KFLD                    SVEF_SYAV
     C                   KFLD                    SVEF_SYOP
     C                   KFLD                    TYP
     C                   KFLD                    SVEF_FATX
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVEF3

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     SVEFKEY       CHAIN     SVEF3                              55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   SVEFR
     C                   EVAL      XSVEF_SYAV=SVEF_SYAV
     C                   EVAL      XSVEF_SYOP=SVEF_SYOP
     C                   EVAL      XSVEF_FATY=TYP
     C                   EVAL      XSVEF_FATX=SVEF_FATX
     C                   WRITE     SVEFR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SVEFR
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C*                  EVAL      XSVEF_FATY=SVEF_FATY
     C                   EVAL      XSVEF_VAKD=SVEF_VAKD
     C                   EVAL      XSVEF_VAKU=SVEF_VAKU
     C                   EVAL      XSVEF_FABL=SVEF_FABL
     C                   UPDATE    SVEFR
     C                   ENDSR
