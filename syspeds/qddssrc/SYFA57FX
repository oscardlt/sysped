     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/FAK3)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(12)
     A                                      CF06(06 'LAG NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA57X'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 JOV0493 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 JOV0494 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            WSFNR     R        B  6  5REFFLD(SYFLDR/SFNR *LIBL/SYFLD)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(010 DIV2056 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 17MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WSNR           1A  B  7  2VALUES(' ' '2' '4' '1')
     A            SFNR      R        O  7  5REFFLD(SYFLDR/SFNR *LIBL/SYFLD)
     A            SFDBN     R        O  7  9REFFLD(SYFLDR/SFDBN *LIBL/SYFLD)
     A            SFLEN     R        O  7 65REFFLD(SYFLDR/SFLEN *LIBL/SYFLD)
     A            SFDEC     R        O  7 70REFFLD(SYFLDR/SFDEC *LIBL/SYFLD)
     A            SFEDIT    R        O  7 74REFFLD(SYFLDR/SFEDIT *LIBL/SYFLD)
     A            SFBESK    R        O  7 19REFFLD(SYFLDR/SFBESK *LIBL/SYFLD)
     A            SFOVER    R        O  7 40REFFLD(SYFLDR/SFOVER *LIBL/SYFLD)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF06(06 'LAG NY')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA57X'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 JOV0493 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A N50                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 JOV0494 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            WSFNR     R        B  6  5REFFLD(SYFLDR/SFNR *LIBL/SYFLD)
     A                                      EDTCDE(4)
     A          R BOTREC
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
     A          R CHGREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Annuller')
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 21  1MSGCON(078 JOV0494 *LIBL/SYMSGF)
     A            W2NR      R        B 22  5REFFLD(SYFLDR/SFNR *LIBL/SYFLD)
     A                                      EDTCDE(4)
     A            W2DBN     R        B 22  9REFFLD(SYFLDR/SFDBN *LIBL/SYFLD)
     A            W2BESK    R        B 22 18REFFLD(SYFLDR/SFBESK *LIBL/SYFLD)
     A            W2OVER    R        B 22 39REFFLD(SYFLDR/SFOVER *LIBL/SYFLD)
     A                                      CHECK(LC)
     A            W2LEN     R        B 22 65REFFLD(SYFLDR/SFLEN *LIBL/SYFLD)
     A                                      EDTCDE(4)
     A            W2DEC     R        B 22 70REFFLD(SYFLDR/SFDEC *LIBL/SYFLD)
     A            W2EDIT    R        B 22 74REFFLD(SYFLDR/SFEDIT *LIBL/SYFLD)
     A                                 23  3MSGCON(040 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
