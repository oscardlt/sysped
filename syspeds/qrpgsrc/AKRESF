     FKREHL01   IP   E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     FKUNDL01   UF   E           K DISK
     IKRESR         01
     I              FIRMA                       FIRMA         L1
     I              KUNDNR                      KUNDNR        L1
     IKREHR         01
     I              FIRMA                       FIRMA         L1
     I              KUNDNR                      KUNDNR        L1
      *
     C     KUNKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
      *
     C  N99              EXCEPT    PHEAD
     C  N99              SETON                                        99
     C                   ADD       BELOPN        SUM1
     C                   ADD       RESTN         SUM2
      *
     CL1   KUNKEY        CHAIN     KUNDL01                            33
     CL1   SUM1          IFNE      SUM2
     CL1   *INOF         IFEQ      '1'
     CL1                 EXCEPT    PHEAD
     CL1                 SETOFF                                       OF
     CL1                 END
     CL1                 EXCEPT    PDET
     CL1                 END
     CL1   SUM1          IFNE      KSALDO
     CL1                 Z-ADD     SUM1          KSALDO
     CL1N33              EXCEPT    OPDKUN
     CL1                 END
     C*
     CL1                 MOVE      *ZEROS        SUM1             13 2
     CL1                 MOVE      *ZEROS        SUM2             13 2
     OLISTE     E            PHEAD            02
     O                                           24 'DIFF KUNDRESKONTRO      '
     O                                           38 'DATO:'
     O                       UDATE         Y     48
     OLISTE     E            PHEAD          1 04
     O                                           11 'FIRMA/KUNDE'
     O                                           62 'POSTERINGER'
     O                                           82 'RESTBELØP'
     O                                          102 'SALDO KUNDEFIL'
     OLISTE     E            PDET           1
     O                       FIRMA                2
     O                                            3 '/'
     O                       KUNDNR              11
     O                       KNAVN               42
     O                       SUM1          J     62
     O                       SUM2          J     82
     O                       KSALDO        J    102
     OKUNDR     E            OPDKUN
     O                       KSALDO
