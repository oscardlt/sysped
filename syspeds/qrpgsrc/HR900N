      /TITLE  VALG AV MEMBER FOR POSTERINGER
      *SY*  START  19980421
      *   ENDRET FOR Å KUNNE KJØRES I BATCH - ENDRINGER MERKET *SY*
      *SY*  SLUTT  19980421
      *****************************************************************
      *   5797-AWE    IBM NORWAY 1988                                 *
      *   5797-AWE    COPYRIGHT IBM CORP 1988                         *
      *   REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE NO. G120-2083     *
      *****************************************************************
      *****************************************************************
      **      Det er foretatt endringer i dette program.
      **      Endringer er foretatt ifølge endringsmelding nr.24
      *       SLETTEDE LINJER ER FYSISK FJERNET
      *****************************************************************
      *
      ****************************************************************
      *               --- H R 9 0 0 ---
      *
      *  Program for registrering av nye member i posteringsfilen
      *  og finne tidligere midlertidige eller avbrutte registreringer.
      *  Programmet bruker filen MEMBF med member opplysninger, og
      *  SYSAF for å hente membernummer.
      *  De to første posisjonene i brukerprofilnavnet pluss MEMBNR
      *  +1 gir navnet på nytt member.
      *  Hvis behandling av tidligere registrerte bilag vises alle
      *  member med samme firmagruppe og firmakode med "C" i status.
      *  Hvis det finnes et member med status "B" og samme firmagruppe
      *  og firmakode vil det si at det har vært brudd i foregående
      *  registrering og dette member skal velges.
      *
      *----------------- INDIKATORER ---------------------------------
      *
      *  03 - F3 Gå ut
      *  07 - Roll framover
      *  08 - Roll bakover
      *  13 - F13 Nytt member
      *  14 - F14 Omstart av jobb
      *  31 - SFLDSP
      *  32 - SFLDSPCTL
      *  33 - SFLCLR
      *  34 - SFLEND
      *  36 - READ FOR SFL
      *  37 - READC SFL
      *  50 - CHAIN
      *  70 - Feilmelding
      *  71 - Feilmelding
      *  72 - Feilmelding
      *  90 - Styring av datoformat DDMMÅÅ/ÅÅMMDD
      *
      *----------------- SUBRUTINER ----------------------------------
      *
      *  SUBSR  - Vis member status "C"
      *
      *----------------- SKJERMBILDER --------------------------------
      *
      *  HR900B01 - Heading
      *  HR900B02 - Funksjonstaster
      *  HR900B03 - Feil på siste oppdatering/utskrift
      *  HR900H01 - Søk på member
      *  HR900K01 - ---"---
      *
      ****************************************************************
      *
      /EJECT
      *SY*
     F*HR900D01CF  E                    WORKSTN
     FHR900D01  CF   E             WORKSTN USROPN
      *SY*
     F                                     SFILE(HR900H01:RRN)
     F                                     INFDS(FEILDS)
     F                                     INFSR(FEILSR)
     FMEMBL02   UF A E           K DISK
     FSYSAL01   UF   E           K DISK
     FFEILLOGG  O    E             PRINTER USROPN
      /EJECT
     D ARRC            S             60    DIM(4) CTDATA PERRCD(1)
     D*
     D                SDS
     D  ZPROG                  1     10
     D  ZPMSG                 40     46
     D  ZBIBLO                81     90
     D  ZJOBB                244    253
     D  ZBRKR                254    263
     D  ZJNR                 264    269
     D*
     D FEILDS          DS
     D  ZSTAT            *STATUS
     D  ZFILE            *FILE
     D  ZLINE                 30     37
     D  ZFMT                  38     45
     D  ZFMSG                 46     52
     D  ZDSP                 273    282
     D  ZMAJ                 401    402
     D  ZMIN                 403    404
      *DATO FOR MEMBER
     D                 DS
     D  MEMDAT                 1      6  0
     D  MEMDA1                 1      2  0
     D  MEMDA2                 3      4  0
     D  MEMDA3                 5      6  0
      *----------------------------------------------------------------
      * LOCAL DATA AREA
      *----------------------------------------------------------------
     D DATA1         EUDS                  EXTNAME(LDADS)
E24  D  WIN03                403    403
E24  D  WIN14                402    402
      /EJECT
      *
      *----------------------------------------------------------------
      *   DEFINERING OG FLYTTING TIL HJELPEFELTER
      *----------------------------------------------------------------
      *
      *SY*START-SKAL ALDRI ÅPNES-DENNE METODE GIR MINIMALE ENDRINGER
     C   01
     CANN01              OPEN      HR900D01
      *SY*SLUTT
     C                   MOVE      '0'           WIN03
     C                   MOVE      '0'           WIN14
     C     *DTAARA       DEFINE    *LDA          DATA1
     C                   MOVE      ARRC(4)       ARRC(4)
     C                   MOVE      WFKODE        FKODE                          <FIRMAGRUPPE
     C                   MOVE      WFIRMA        FIRMA                          <FIRMA
     C                   MOVE      WUSER         USER                           <BRUKER
     C                   MOVE      WRUTIN        RUTINE                         <RUTINE
     C     *LIKE         DEFINE    FIRMA         WTST
      *
     C     KEYA          KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    USER
     C                   KFLD                    RUTINE
     C                   KFLD                    STATUS
     C     KEYB          KLIST
     C                   KFLD                    FKODE
     C                   KFLD                    FIRMA
      *
      /EJECT
      *
      *----------------------------------------------------------------
      *  HOVEDPROGRAM
      *----------------------------------------------------------------
      *
      ****
      *  Sjekk om status "B" avbrudd ved forrige registrering.
      *  Vis bilde om feil. F1 eller F14 er gyldige.
      ****
     C                   MOVEA     *ALL'0'       *IN
      *
     C     WDATOF        IFEQ      1
     C                   MOVE      '1'           *IN90                          <ÅÅMMDD
     C                   END
      *
     C                   MOVE      'B'           STATUS
     C     KEYA          CHAIN     MEMBR                              50
     C     *IN50         IFEQ      '0'
     C                   MOVE      '1'           *IN72                          <MELDING
     C                   WRITE     HR900B01                                     <HEADING
     C     FEILA         TAG
     C                   EXFMT     HR900B03                                     <MELDING
     C     *IN03         IFEQ      '0'
     C     *IN14         ANDEQ     '0'
     C                   GOTO      FEILA
     C                   END                                                    <IN03/IN14
     C                   MOVE      MEMBER        WMEMBE
     C                   MOVE      STATUS        WSTATU
     C                   MOVE      *IN03         WIN03                          <AVBRYT
     C                   MOVE      *IN14         WIN14                          <AVBRYT
     C                   GOTO      SLUTT
     C                   END                                                    <IN50
      ****
      *  Sjekk om status "C" ikke oppdaterte registreringer.
      *  Vis oversikt over membere. Kryss for den som skal brukes.
      ****
     C                   MOVE      'C'           STATUS
     C     KEYA          CHAIN     MEMBR                              50
     C     *IN50         IFEQ      '0'
     C                   EXSR      SUBSR                                        <SUBF MEMBER
     C                   MOVE      *IN03         WIN03                          <AVBRYT
     C     *IN13         IFEQ      '0'
     C                   GOTO      SLUTT
     C                   END
     C                   END
      ****
      *  Finn nytt member for posteringer.
      *  Membernavn skal bestå av 1.pos. av bruker, firma og membnr +1.
      ****
     C                   MOVE      FIRMA         WTST
     C                   MOVE      WTST          WUSER1
     C     WUSER1        IFEQ      *BLANKS
     C                   MOVE      '0'           WTST
     C                   END
     C                   MOVEL     WTST          WUSER1
     C     WUSER1        IFEQ      *BLANKS
     C                   MOVEL     '0'           WTST
     C                   END
     C                   MOVEL     USER          WUSER1            1
     C                   MOVEL     WUSER1        WUSER3            3
     C                   MOVE      WTST          WUSER3
     C     KEYB          CHAIN     SYSAR                              50
     C                   ADD       1             MEMBNR
     C                   UPDATE    SYSAR
     C                   MOVE      MEMBNR        MEMBER
     C                   MOVEL     WUSER3        MEMBER
     C                   MOVE      MEMBER        WMEMBE
      *
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      'A'           STATUS
     C                   MOVE      STATUS        WSTATU
     C                   MOVE      *ZEROS        FBILAG
     C                   MOVE      *ZEROS        SBILAG
     C                   Z-ADD     *YEAR         MEMAAR
     C                   Z-ADD     UMONTH        MEMMND
     C                   Z-ADD     UDAY          MEMDAG
     C                   MOVE      WRUTIN        RUTINE                         <RUTINE
     C                   WRITE     MEMBR
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
      /EJECT
      *
      *----------------------------------------------------------------
      *  S U B S R      VIS MEMBER STATUS "C"
      *----------------------------------------------------------------
      *
     C     SUBSR         BEGSR
      ****
      *  Tømmer subfilen
      ****
     C                   MOVE      '1'           *IN33
     C                   WRITE     HR900K01
     C                   MOVE      '0'           *IN33
     C                   MOVE      '0'           *IN34
      *
     C                   Z-ADD     0             RRN
      ****
      *  Leser filen
      ****
     C     KEYA          SETLL     MEMBR                              34        INGEN REC
     C  N34KEYA          READE     MEMBR                                  34    NESTE OM N34
      *
     C     *IN34         IFEQ      '1'
     C                   MOVE      '1'           *IN36
     C                   GOTO      SUB03
     C                   END
      ****
      *  Fyll subfilen med 10 poster om gangen
      ****
     C     SUB02         TAG
     C  N34              DO
     C                   DO        10
     C                   ADD       1             RRN
      ****
      *  Legg ut dato i format DDMMÅÅ eller ÅÅMMDD
      ****
     C     *IN90         IFEQ      '0'
     C                   Z-ADD     MEMDAG        MEMDA1
     C                   Z-ADD     MEMMND        MEMDA2
     C                   Z-ADD     MEMAAR        MEMDA3
     C                   ELSE
     C                   Z-ADD     MEMAAR        MEMDA1
     C                   Z-ADD     MEMMND        MEMDA2
     C                   Z-ADD     MEMDAG        MEMDA3
     C                   END                                                    <IN90
      *
     C                   WRITE     HR900H01
     C     KEYA          READE     MEMBR                                  34
     C  N34              END
     C                   END
      ****
      *  Skriv ut subfilen
      ****
     C     SUB03         TAG
     C                   MOVEA     '11'          *IN(31)
     C     RRN           IFNE      0
     C                   WRITE     HR900B01
     C                   WRITE     HR900B02
     C                   EXFMT     HR900K01
     C                   END
     C                   MOVEA     '00'          *IN(31)
      ****
      *  Fyll i nye poster hvis ROLLUP
      ****
     C     *IN07         IFEQ      '1'
     C                   MOVE      '0'           *IN07
     C                   GOTO      SUB02
     C                   END
      ****
      *  Hvis F3, gå ut
      ****
     C     *IN03         IFEQ      '1'
     C                   GOTO      SUB09
     C                   END
      ****
      *  Finn posten som er krysset av
      ****
     C     *IN36         IFEQ      '0'                                          REC PÅ SUBF
     C     *IN13         IFEQ      '0'
     C     LESNY         TAG
     C                   READC     HR900H01                               37
     C     *IN37         CABEQ     '1'           SUB03
     C     VELG          CABNE     '1'           LESNY
     C                   MOVE      '0'           *IN34
     C                   MOVE      MEMBER        WMEMBE
     C                   MOVE      STATUS        WSTATU
     C                   END
     C                   GOTO      SUB09
     C                   END
      ****
      *  Hvis ingen poster er krysset vises subfilen på nytt
      ****
     C                   GOTO      SUB03
     C     SUB09         TAG
      *
     C                   ENDSR
      /EJECT
      ********************************************************************
      * F E I L S R                                                    de
      * Subrutine som rydder opp etter en feil i forb. med et skjermbilde
      ********************************************************************
     C     FEILSR        BEGSR
      ****
      * Skriver feillogg
      ****
     C                   OPEN      FEILLOGG
     C                   MOVEL     ARRC(1)       ZMSG1
     C                   MOVEL     ARRC(2)       ZMSG2
     C                   WRITE     FEIL
     C                   CLOSE     FEILLOGG
      ****
      * Lukker programkontrollerte filer og tar CALL VC006
      ****
     C                   MOVE      '1'           *INLR
     C                   Z-ADD     15            LEN              15 5
     C                   MOVEL     ARRC(3)       KOMM             60
     C                   CALL      'QCMDEXC'
     C                   PARM                    KOMM
     C                   PARM                    LEN
      *
     C                   ENDSR
      ********************************************************************
      /EJECT
      *
**  COPYRIGHT LOAD
Brudd på arbeidsstasjonen i programmet for valg av komponenter.
Start rutinen fra meyen igjen.
CALL PGM(VC006)
5797-AWH IBM NORWAY 1988  5797-AWD COPYRIGHT IBM CORP. 1988
