     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A          R SVG514DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            YTITRNR       18A  O  8  4
     A            YTINTK         7Y 0O  8 26EDTCDE(4)
     A            OTINTK         7Y 0O  8 46EDTCDE(4)
     A          R SVG514DB                  SFLCTL(SVG514DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SVG514'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 30'Underhåll av lager'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 28'Inlægg extra kolli NCTS'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  6 23'__________Antal_kolli_________'
     A                                      COLOR(WHT)
     A                                  7  4'MRN-nummer'
     A                                      COLOR(WHT)
     A                                  7 23'Nytt_værde'
     A                                      COLOR(WHT)
     A                                  7 36'Urspungligt_værde'
     A                                      COLOR(WHT)
     A          R SVG514DC
     A                                      OVERLAY
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 26 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 26 35'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVG514DD
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZTITRNR       18A  B 24  4
     A  31                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A            ZTINTK         7Y 0B 24 26
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVG514DE
     A                                 23  2' '
     A                                 24  2' '
     A          R SVG514DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
