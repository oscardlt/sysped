     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R DKX068D1
     A                                      CA03(03 'Avslutt program.')
     A                                  1  3'SYSPED/DKX068R'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  4 23'Ændra status NCTS Import'
     A                                      COLOR(WHT)
     A                                  8 23'Avdelning'
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10 23'Signatur:'
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  ERRMSG('Tastet tariffør er ikke reg-
     A                                      istrert.' 98)
     A                                 12 23'Uppdragsnr.'
     A            WSOPD          7Y 0B 12 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 23'Ny status:'
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                 23 23'F3 = Avsluta'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R DKX068D2
     A                                      CA05(05 'Endre status')
     A                                  1  3'SYSPED/DKX068R'
     A                                      COLOR(BLU)
     A                                  1 20'Ændra status NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 48'Ref.'
     A            WSAVD          4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSOPD          7Y 0O  1 58COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSG           3A  O  1 64DSPATR(UL)
     A                                      COLOR(WHT)
     A            TITARF        16A  H
     A                                  3  2'MRN-nr.'
     A            TITRNR        18A  O  3 13TEXT('MRN-nr')
     A                                      COLOR(YLW)
     A                                  4  2'Godsnr.'
     A            TIGN          35A  O  4 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 52'Språkkod '
     A            TIGNSK         2A  O  4 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'Avs.land'
     A            TIALK          2A  O  5 13TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Avt.lag.pl'
     A            TIALSK        17A  O  7 13DSPATR(UL)
     A                                      COLOR(YLW)
     A            TIALS         35A  O  7 31DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 67'Språkkod '
     A            TIALSS         2A  O  7 77DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2'Godk.l.pl.'
     A            TIGLSK        17A  O  8 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2'Contr.pl..'
     A            TIACTS        17A  O  9 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  2'Framl.t.pl'
     A            TITSB          8A  O 11 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 22'Språkkod '
     A            TISKB          2A  O 11 32DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13  2'Næringsliv'
     A            TIKN           8Y 0O 13 13TEXT('Kundenr')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            TITIN         17A  O 13 22TEXT('Tin kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TINA          30A  O 14 13TEXT('Navn')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TIAD1         30A  O 15 13TEXT('Adresse-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TIPN           9A  O 16 13TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TIPS          24A  O 17 13TEXT('Poststed')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'Landkod '
     A            TILK           2A  O 18 13TEXT('Landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 22'Språkkod '
     A            TISK           2A  O 18 32TEXT('Språkkode')
     A                                      DSPATR(UL)
     A                                 19  2'Reg.dat '
     A            TIDT           8Y 0H
     A            WSDT           6Y 0O 17 49TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 23  1'Gammal status'
     A                                      COLOR(TRQ)
     A            TIST           1A  O 23 15TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                 23 18'Ny status'
     A                                      COLOR(TRQ)
     A            WSST           1A  O 23 28TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 24  2'F5=Ændra status'
     A                                      COLOR(BLU)
