********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 ingen oppdatering ved arkivering
10172 * 02 SH  PTF10 norsk text til Track and trace
10172 * 03 SH  PTF10 endring printerfil
10172 * 04 SH  PTF10 sletter eventuelle gamle arkivdokumenter
10172 * 05 SH  PTF10 feil i print detaljer
10172 * 06 SH  PTF10 OMREGNING TILLEGG BANKKURS
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SKRIVE UT FAKTURAGRUNNLAG
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01-26
      *** LEDIGE INDIKATORER 28-32 35-39 41-49 52-99
      **************************************************************
      *
     FARKTXT    IF   E           K DISK
N06  FKODTCC    IF   E           K DISK
N06  FKODTDIN   IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FARKIVP    O  A E             DISK
     FGSSORL22  UF   E           K DISK
     FGSSPOF    IF   E           K DISK
     FGSSSFF    IF   E           K DISK
     FGSSFIF    IF   E           K DISK
     FGSSOVL01  IF   E           K DISK
     FGSSTRF    UF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FGSSSTK    IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFAXSEN    IF   E           K DISK
     FFAXMANP   O  A E           K DISK    USROPN
     FFAXMANT   UF A E           K DISK
     FTRACKF    O    E             DISK
     FGSS041P   O    E             PRINTER OFLIND(*IN27)
     F                                     USROPN
     D F5              S             11  0 DIM(100)
     D F6              S              5    DIM(100)
     D T1              S              3    DIM(100)
     D T2              S             16    DIM(100)
     D T3              S             11  2 DIM(100)
     D T4              S              3    DIM(100)
     D                 DS
     D  XCONO1                 1     20
     D  XCONO2                21     40
     D  XCONO3                41     60
     D  XCONO4                61     80
     D  XCONO5                81    100
     D  XCONO6               101    120
     D  CO                     1   2000
     D                                     DIM(100)
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(GSS041P) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('PAGESIZE(72 120) -
     D                                     CPI(15) -
     D                                     OVRFLW(63) -
     D                                     COPIES(')
     D XCMD30          C                   CONST('OVRPRTF -
     D                                     FILE(GSS041P) -
     D                                     DEV(FAX3812) -
     D                                     DEVTYPE(*AFPDS) -
     D                                     PAGESIZE(72 120) -
     D                                     LPI(6) -
     D                                     CPI(15) -
     D                                     FRONTMGN(0 0.15) -
     D                                     OVRFLW(63) -
     D                                     DRAWER(3) -
     D                                     PAGRTT(0) -
     D                                     OUTQ(QFAX/QFAXOUTQ) -
     D                                     FORMTYPE(*STD)')
     D XCMD30e         C                   CONST('OVRPRTF -
     D                                     FILE(GSS041P) -
     D                                     DEV(FAX3812) -
     D                                     DEVTYPE(*AFPDS) -
     D                                     PAGESIZE(72 120) -
     D                                     LPI(6) -
     D                                     CPI(15) -
     D                                     OVRFLW(63) -
     D                                     PAGRTT(0) -
     D                                     OUTQ(OUTMAIL) -
     D                                     FORMTYPE(*STD)')
     D XCMD30B         C                   CONST('OVRPRTF -
     D                                     FILE(GSS041P) -
     D                                     DEVTYPE(*SCS) -
     D                                     PAGESIZE(70 120) -
     D                                     LPI(6) -
     D                                     CPI(15) -
     D                                     OVRFLW(63) -
     D                                     PAGRTT(0) -
     D                                     OUTQ(FAX/FAXOUT) -
     D                                     FORMTYPE(*STD)')
     D XCMD40          C                   CONST('OVRPRTF -
     D                                     FILE(GSS041P) -
     D                                     PAGESIZE(72 120) -
     D                                     LPI(6) -
     D                                     CPI(15) -
     D                                     FRONTMGN(0 0,15) -
     D                                     OVRFLW(63) -
     D                                     DRAWER(3) -
     D                                     PRTTXT(')
     D XCMD41          C                   CONST('OUTQ(')
     D XXTXT01         C                   CONST('Printed invoice basis.')
     D XXTXT02         C                   CONST('Trip No.')
n02  D XXTXT01l        C                   CONST('Skrevet Faktura basis.')
n02  D XXTXT02l        C                   CONST('Tur Nr.')
     D                SDS
     D  UUSER                254    263
     D                 DS
     D  X1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  X2                     1     15
     D                                     DIM(15)
     D  WSFAXN                 1     15
     D  FAX05A                 1      4
     D                 DS
     D  X3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D                 DS
     D  XTID                   1     14  0
     D  XTID4                  1      4  0
     D                 DS
     D  TRDTL                  1      8  0
     D  TRDTLÅ                 1      4  0
     D  TRDTLM                 5      6  0
     D  TRDTLD                 7      8  0
     D                 DS
     D  XTDTL                  1      6  0
     D  XTDTLD                 1      2  0
     D  XTDTLM                 3      4  0
     D  XTDTLÅ                 5      6  0
     D                 DS
     D  TRDTS                  1      8  0
     D  TRDTSÅ                 1      4  0
     D  TRDTSM                 5      6  0
     D  TRDTSD                 7      8  0
     D                 DS
     D  XTDTS                  1      6  0
     D  XTDTSD                 1      2  0
     D  XTDTSM                 3      4  0
     D  XTDTSÅ                 5      6  0
     D                 DS
     D  ORDTL                  1      8  0
     D  ORDTLÅ                 1      4  0
     D  ORDTLM                 5      6  0
     D  ORDTLD                 7      8  0
     D                 DS
     D  XODTL                  1      6  0
     D  XODTLD                 1      2  0
     D  XODTLM                 3      4  0
     D  XODTLÅ                 5      6  0
     D                 DS
     D  XFEEC1                 1      3
     D  XFEEC2                 4      6
     D  XFEEC3                 7      9
     D  F1                     1    300
     D                                     DIM(100)
     D                 DS
     D  XFT1                   1     16
     D  XFT2                  17     32
     D  XFT3                  33     48
     D  F2                     1   1600
     D                                     DIM(100)
     D                 DS
     D  XAMT1                  1     11  2
     D  XAMT2                 12     22  2
     D  XAMT3                 23     33  2
     D  F3                     1   1100  2
     D                                     DIM(100)
     D                 DS
     D  XVAT1                  1      1
     D  XVAT2                  2      2
     D  XVAT3                  3      3
     D  F4                     1    100
     D                                     DIM(100)
     D                 DS
     D  XVAL1                  1      3
     D  XVAL2                  4      6
     D  XVAL3                  7      9
     D  F7                     1    300
     D                                     DIM(100)
     D                UDS
     D  UFIRM                  1      2
     D  UCURR                  3      5
     D  UUSID                101    110
     D  UBICGR               111    112
     D  UBICNO               113    120  0
     D  UBIFC                121    123
     D  UBIPRT               124    133
     D  UBIPRM               134    143
     D  UWCNAM               544    573
     D  UWFAXN               574    588
     D  UWRET                591    596
     D  UWTIME               598    603  0
     D  UWDATE               606    611  0
     D  UCOVP                612    612
     D  UCOV01               613    652
     D  UCOV02               653    692
     D  UCOV03               693    732
     D  UCOV04               733    772
     D  UCOV05               773    812
     D  UCOV06               813    852
     D  UCOV07               853    892
     D  UCOV08               893    932
     D  UEMAIL               933   1002
     D  Uemailj             1003   1003
      *
     C                   EXSR      INIT
      *
     C     UTNO          CHAIN     GSSTRF                             33
     C  N33UTYPE         IFEQ      'PRT'                                        1<-B
     C     UTYPE         OREQ      'CPY'                                        1<-B
N01  C     UUTFAX        ifne      'A'
     C  N50              MOVE      '5'           TRST
     C  N50              UPDATE    GSSTRFR
N01  C                   end
     C                   MOVE      TRDTLD        XTDTLD
     C                   MOVE      TRDTLM        XTDTLM
     C                   MOVE      TRDTLÅ        XTDTLÅ
     C                   MOVE      TRDTSD        XTDTSD
     C                   MOVE      TRDTSM        XTDTSM
     C                   MOVE      TRDTSÅ        XTDTSÅ
     C     TRFC          CHAIN     GSSFIF                             33
     C     CUND1K        CHAIN     CUNDL01                            33
     C     TRBCD         CHAIN     CDBOAT                             33
     C                   TIME                    XTID
      *
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
      *
      * INGEN OVERSTYRINGER VED UUTFAX = A FOR ARKIVERING
     C     UUTFAX        IFne      'A'                                           2<-B
     C     UUTFAX        IFEQ      'J'                                          1<-B
     c     uemail        ifne      *blanks
     C                   MOVEL     XCMD30e       UCMD
     c                   else
     C     FAXLIB        IFEQ      *BLANKS                                       2<-B
     C     FAXSYS        IFEQ      'B'
     C                   MOVEL     XCMD30B       UCMD
     C                   ELSE
     C                   MOVEL     XCMD30        UCMD
     C                   END
     C                   ELSE                                                    2
     C     XCMD40        CAT       '.FAX':1      UCMD
     C                   CAT       WSFAXN:0      UCMD
     C                   CAT       ')':1         UCMD
     C                   CAT       XCMD41:0      UCMD
     C                   CAT       FAXLIB:0      UCMD
     C                   CAT       '/':0         UCMD
     C                   CAT       FAXUTK:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   END                                                     2<-E
     c                   end
     C                   ELSE
      * PRINT LØSNING
      *
      * pdf
     C     STRF03        IFEQ      '*PDF '
     C                   eval      UCMD = 'OVRPRTF FILE'+
     c                             '(GSS041P) DEV(FAX3812) ' +
     c                             'DEVTYPE(*AFPDS) ' +
     c                             'PAGESIZE(72 125) ' +
     c                             'LPI(6) CPI(15) ' +
     c                             'OVRFLW(63) ' +
     c                             'PAGRTT(0) ' +
     c                             'OUTQ('+
     c                             %trim(UPRT) + ')' +
     c                             ' FORMTYPE(*STD)' +
     c                             ' FRONTOVL(' +
     c                             'LOGOFAKT' +
     c                             ')' +
     c                             ' FRONTMGN(0 0.2)'
     C                   ELSE
     C     XCMD11        CAT       UPRT:0        UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD21:1      UCMD
     C                   CAT       UCPY:0        UCMD
     C                   CAT       ')':0         UCMD
     C                   END
     C                   END                                                    1<-E
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     c                   end
      *
     C                   OPEN      GSS041P
      *
     C                   MOVE      '0'           *IN27
     C                   WRITE     GSS041PA
      * TEST OM DET ER SAMME VALUTA PÅ HELE TUR
     C                   EXSR      TSTVAL
      * Les ordre
     C     UTNO          SETLL     GSSORL22                           33
     C     STPRT         TAG
     C     UTNO          READE     GSSORL22                               33
     C  N33              DO                                                      2<-B
N01  C     UUTFAX        ifne      'A'
     C  N50              MOVE      '5'           ORST
     C                   UPDATE    GSSORFR
n01  C                   end
     C                   MOVE      ORDTLD        XODTLD
     C                   MOVE      ORDTLM        XODTLM
     C                   MOVE      ORDTLÅ        XODTLÅ
     C     ORTNO2        COMP      0                                  4040
      *
     C     ORONO         SETLL     GSSPOF                             34
     C     ORONO         READE     GSSPOF                                 34
     C                   MOVE      *ZEROS        XN
     C                   MOVE      *BLANKS       CO
     C     *IN34         DOWEQ     '0'
     C                   ADD       1             XN
     C                   MOVE      POCONO        CO(XN)
     C     ORONO         READE     GSSPOF                                 34
     C  N34              END
      * Les fakturalinjer
     C                   MOVE      *BLANKS       F1
     C                   MOVE      *BLANKS       F2
     C                   MOVE      *ZEROS        F3
     C                   MOVE      *BLANKS       F4
     C                   MOVE      *ZEROS        F5
     C                   MOVE      *BLANKS       F6
     C                   MOVE      *BLANKS       F7
     C                   MOVE      *ZEROS        XTAMTO
     C                   MOVE      *ZEROS        XN                3 0
     C     ORONO         SETLL     GSSOVL01                           34
     C     STPRT2        TAG
     C     ORONO         READE     GSSOVL01                               34
     C  N34              DO                                                       3<-B
     C                   ADD       1             XN
     C                   MOVE      OVFEEC        F1(XN)
     C                   MOVEL     OVFT          F2(XN)
     C                   MOVE      OVAMT         F3(XN)
     C                   MOVE      OVVAT         F4(XN)
     C                   MOVE      OVCURC        F7(XN)
     C     *IN40         IFEQ      '1'
     C                   SELECT
     C     ORLC          WHENEQ    OVLC
     C                   MOVE      ORTNO         F5(XN)
     C     ORLC2         WHENEQ    OVLC
     C                   MOVE      ORTNO2        F5(XN)
     C                   OTHER
     C                   MOVE      ORTNO3        F5(XN)
     C                   ENDSL
     C                   MOVE      OVLC          F6(XN)
     C                   END
     C     OVCURC        IFEQ      XVAL
     C                   Z-ADD     OVAMT         XAMT             16 5
     C                   ELSE
N06   *
N06   * sjekk kunde om avtale
N06   *
N06  C                   Z-add     1             XTILBA
N06  C     CCK1          CHAIN     KODTCC                             33
N06  c  N33              Z-add     KCCVA         KDIPRO
N06  C   33KODDIK        CHAIN     KODTDINR                           33
N06  C  N33KDIPRO        DIV       100           XTILBA            5 4
N06  C  N33              ADD       1             XTILBA
     C     VALU1K        CHAIN     VALUL01                            33
     C   33              MOVE      *ZEROS        XAMT             16 5
     C  N33OVAMT         MULT(H)   VALKU2        XAMT
     C  N33              DIV(H)    OMRFAK        XAMT
N06  C  N33              MULT(H)   XTILBA        XAMT
     C                   END
     C                   ADD(H)    XAMT          XTAMTO
     C     1             DO        100           XN2               3 0             4<-B
     C     T1(XN2)       IFEQ      *BLANKS                                          5<-B
     C                   MOVE      OVFEEC        T1(XN2)
     C                   MOVEL     OVFT          T2(XN2)
     C                   Z-ADD(H)  XAMT          T3(XN2)
     C                   GOTO      STPRT2
     C                   ELSE                                                       5
     C     T1(XN2)       IFEQ      OVFEEC                                            6<-B
     C                   ADD(H)    XAMT          T3(XN2)
     C                   GOTO      STPRT2
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   GOTO      STPRT2
     C                   END                                                      3<-E
      *
     C     ORSNO         CHAIN     GSSSFF                             34
     C   34              MOVE      ORSNM         SFNM
     C   27              DO
     C                   WRITE     GSS041PH
     C                   WRITE     GSS041PA
     C                   WRITE     GSS041PH
     C                   MOVE      '0'           *IN27
     C                   END
     C                   WRITE     GSS041PB
      *
s05  C*    XN            IFLT      7
n05  C     XN            IFLT      4
     C                   SELECT
     C     XCONO6        WHENNE    *BLANKS
     C                   Z-ADD     6             XN
     C     XCONO5        WHENNE    *BLANKS
     C                   Z-ADD     5             XN
     C     XCONO4        WHENNE    *BLANKS
     C                   Z-ADD     4             XN
     C                   ENDSL
     C                   END
      *
     C     4             DO        XN            XN2                              3<-B
     C                   MOVE      CO(XN2)       XCONX
     C                   MOVE      F1(XN2)       XFEECO
     C                   MOVE      F2(XN2)       XFTO
     C                   MOVE      F3(XN2)       XAMTO
     C                   MOVE      F4(XN2)       XVATO
     C                   MOVE      F5(XN2)       XTNO
     C                   MOVE      F6(XN2)       XLC
     C                   MOVE      F7(XN2)       XVALO
     C   27              DO
     C                   WRITE     GSS041PH
     C                   WRITE     GSS041PA
     C                   WRITE     GSS041PH
     C                   MOVE      '0'           *IN27
     C                   END                                                      3<-E
     C                   WRITE     GSS041PC
     C                   END                                                      3<-E
     C   27              DO                                                       3<-B
     C                   WRITE     GSS041PH
     C                   WRITE     GSS041PA
     C                   WRITE     GSS041PH
     C                   MOVE      '0'           *IN27
     C                   END                                                      3<-E
     C                   WRITE     GSS041PD
     C                   ADD       XTAMTO        XTAMTT
     C                   GOTO      STPRT
     C                   END                                                     2<-E
      *
     C                   MOVE      '0'           *IN51
     C     1             DO        100           XN                              2<-B
     C     T1(XN)        IFEQ      *BLANKS                                        3<-B
     C                   GOTO      STPRT3
     C                   ELSE                                                     3
     C                   MOVE      T1(XN)        XTFEEN
     C     KODGEK        CHAIN     KODTGE                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KGEENT        XTFTN
     C                   ELSE
     C                   MOVE      T2(XN)        XTFTN
     C                   END
     C                   MOVE      T3(XN)        XTAMTN
     C   27              DO
     C                   WRITE     GSS041PH
     C                   WRITE     GSS041PA
     C                   MOVE      '0'           *IN27
     C                   END
     C                   WRITE     GSS041PE
     C                   MOVE      '1'           *IN51
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     STPRT3        TAG
     C   27              DO
     C                   WRITE     GSS041PH
     C                   WRITE     GSS041PA
     C                   MOVE      '0'           *IN27
     C                   END
     C                   WRITE     GSS041PF
      *
     C                   EXSR      OPDLOGG
      *
     c     UTYPE         ifeq      'PRT'
     C     UUTFAX        ANDNE     'A'
     c                   exsr      arkivsr
     c                   end
     C                   CLOSE     GSS041P
      *
     C     UUTFAX        IFEQ      'J'
     C                   EXSR      SNDFAX
     C                   END
      *
     C                   ELSE                                                   1
      *
N01  C     UUTFAX        ifne      'A'
     C                   MOVE      '6'           TRST
     C                   UPDATE    GSSTRFR
N01  C                   END
     C     UTNO          SETLL     GSSORL22                           33
     C     STCFM         TAG
     C     UTNO          READE     GSSORL22                               33
     C  N33              DO                                                      2<-B
N01  C     UUTFAX        ifne      'A'
     C                   MOVE      '6'           ORST
     C                   UPDATE    GSSORFR
N01  C                   END
     C                   GOTO      STCFM
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
     C     SLUTT         TAG
     c     arkiver       ifeq      'J'
     c                   call      'ARC002CLB'
     c                   parm                    artype
     c                   parm                    aruser
     c                   end
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C***********************************************
     C     TSTVAL        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       XVAL
     C     UTNO          SETLL     GSSORL22
     C     STTV01        TAG
     C     UTNO          READE(N)  GSSORL22                               33
     C  N33              DO
     C     ORONO         SETLL     GSSOVL01
     C     STTV02        TAG
     C     ORONO         READE     GSSOVL01                               34
     C  N34              DO
     C     XVAL          IFEQ      *BLANKS
     C                   MOVE      OVCURC        XVAL
     C                   ELSE
     C     XVAL          IFNE      OVCURC
     C                   MOVE      FICURR        XVAL
     C                   GOTO      SLTV
     C                   ENDIF
     C                   ENDIF
     C                   GOTO      STTV02
     C                   ENDDO
     C                   GOTO      STTV01
     C                   ENDDO
      *
     C     SLTV          ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
N06  C     CCK1          KLIST
N06  C                   KFLD                    UFIRM
N06  C                   KFLD                    TRCNO
N06  C     KODDIK        KLIST
N06  C                   KFLD                    XDIUNI
N06  C                   MOVE      'DINNF'       XDIUNI            5
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    TRCNO
     C     KODGEK        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    XTFEEN
     C                   MOVE      'GE'          KGEUNI
     C     VALU1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    OVCURC
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTNO2            11
     C                   PARM                    UTYPE             3
     C                   PARM                    UPRT             10
     C                   PARM                    UCPY              1
     C                   PARM                    UUTFAX            1
     C                   PARM                    UQJOB            10
     C                   PARM                    UQUSER           10
     C                   PARM                    UQNBR             6
      *
     C                   MOVE      UTNO2         UTNO             11 0
     C     UTYPE         COMP      'CPY'                                  50
     C     UFIRM         CHAIN     FIRM                               33
     C     UUTFAX        IFEQ      'J'
     C     UFIRM         CHAIN     FAXSEN                             28
     C                   MOVE      '1'           *IN28
     C                   END
      *
      *
     C     UUTFAX        IFNE      'J'
     C     UUTFAX        ANDNE     'A'
     C     KEYST         KLIST
     C                   KFLD                    STRPRT
     C                   MOVE      UPRT          STRPRT
     C     KEYST         CHAIN     GSSSTK                             33
     C     *IN33         IFEQ      '0'
     C     STRF03        CHAIN     KODTFM                             33
     C                   END
      *
      * ¤1B(¤1B(s0p15h6v0s0b0T'
     C     stresc        ifne      *blanks
     c                   move      *blanks       stri01           70
     C     stri01        CAT       stresc:0      stri01
     C     stri01        CAT       '(':0         stri01
     C     stri01        CAT       stresc:0      stri01
     C     stri01        CAT       '(s0p15h6v0':0stri01
     C     stri01        CAT       's0b0T':0     stri01
     c                   end
      *
     C                   END
      *
      * PDF =  77
      *
     C     UUTFAX        COMP      'A'                                    77
     C  N77STRF03        COMP      '*PDF '                                77
     C                   ENDSR
      *
     C***********************************************
     C     SNDFAX        BEGSR
     C***********************************************
     C     UWRET         IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
     C                   MOVEL     UWFAXN        FAXNRP
     C                   END
      *
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     UWTIME        IFNE      *ZEROS
     C     UWDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     UWTIME        PTIME
     C                   MOVEL     UWDATE        PDATE
     C                   END
     C                   END
     C                   MOVE      'B'           UUKODE            1
     C                   MOVEL     'SHIPINVB'    DOKNAV           12
     C                   MOVE      'ASIS'        DOKNAV
     C                   MOVE      *ZEROS        FAXAVD            4
     C                   MOVE      *ZEROS        FAXOPD            7
     C                   MOVE      *ZEROS        FAXTUR            8
     C                   MOVEL     'GSS041P '    FILEN            10
     C     UCOVP         IFEQ      ' '
     C                   MOVE      'N'           UCOVP
     C                   END
     C     UCOVP         IFEQ      'Y'
     C                   MOVE      'J'           UCOVP
     C                   END
      *
     C     FAXSYS        IFEQ      'F'
     c     uemail        orne      *blanks
     C     FAXLIB        IFEQ      *BLANKS
     c     uemail        orne      *blanks
     c     uemail        ifne      *blanks
     c                   movel     'MAIL        'FAXNRP
     c                   movel     uemail        uwcnam
     c                   move      'J'           uemailj
     c                   movel     'Inv.basis   'emne             40
     c                   end
     C                   CALL      'GSS015C2'
     C                   PARM                    UCOVP
     C                   PARM                    UQJOB
     C                   PARM                    UQUSER
     C                   PARM                    UQNBR
     C                   PARM                    FAXNRP
     C                   PARM                    FILEN
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    UCOV01
     C                   PARM                    UCOV02
     C                   PARM                    UCOV03
     C                   PARM                    UCOV04
     C                   PARM                    UCOV05
     C                   PARM                    UCOV06
     C                   PARM                    UCOV07
     C                   PARM                    UCOV08
     C                   PARM                    uemail
     C                   PARM                    emne
     C                   PARM                    uemailj
     C                   ELSE
      * VED FAX400 SENDER SYSTEMET AUTOMATISK FRA UTKØ
     C                   END
      *
     C                   MOVEL     UQUSER        MAPPE            20
     C                   CALL      'FAX013'
     C                   PARM                    UQUSER
     C                   PARM                    UQNBR
     C                   PARM                    UQJOB
     C                   PARM                    FAXNRP
     C                   PARM                    DOKNAV
     C                   PARM                    MAPPE
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    UWCNAM
     C                   PARM                    FAXAVD
     C                   PARM                    FAXOPD
     C                   PARM                    FAXTUR
     C                   PARM                    UUKODE
     C                   END
      *
     C     FAXSYS        IFEQ      'B'
     c     uemail        ANDEQ     *blanks
     C     FIFIRM        CHAIN     FAXMANT                            33
     C   33              Z-ADD     1             UNIK
     C  N33              ADD       1             UNIK
     C   33              WRITE     FAXMANTR
     C  N33              UPDATE    FAXMANTR
     C                   EVAL      PRTFIL = 'GSS041P'
     C                   OPEN      FAXMANP
     C                   MOVE      UQNBR         JOBNR
     C                   EVAL      KNAVN = UWCNAM
     C                   EVAL      FINAVN = FIFT
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           PUDATE
     C                   EXCEPT    SKRIV
     C                   CLOSE     FAXMANP
     C                   CALL      'GSS015C3'
     C                   PARM                    KNAVN
     C                   PARM                    FAXNRP
     C                   PARM                    UNIK
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    PRTFIL           10
     C                   PARM                    DOKNAV
     C                   PARM                    UCOVP
     C                   PARM                    UCOV01
     C                   PARM                    UCOV02
     C                   PARM                    UCOV03
     C                   PARM                    UCOV04
     C                   PARM                    UCOV05
     C                   PARM                    UCOV06
     C                   PARM                    UCOV07
     C                   PARM                    UCOV08
     C                   END
      *
     C                   ENDSR
      *
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   MOVE      UWRET         FAXRET
     C                   MOVE      UWFAXN        WSFAXN
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     X3(A3)        IFNE      *BLANKS
     C                   MOVEL     X3(A3)        X1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     X2(A2)        IFNE      *BLANKS
     C                   MOVEL     X2(A2)        X1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   MOVEL     UTNO          TTAVD
     C                   MOVE      UTNO          TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'PS4'         TTACTI
     C                   MOVE      UUSER         TTUSER
     C                   EVAL      TTTEXT = XXTXT01
     C                   CAT       XXTXT02:10    TTTEXT
     C                   CAT       UTNO2:1       TTTEXT
n02  C                   EVAL      TTTEXl = XXTXT01l
n02  C                   CAT       XXTXT02l:10   TTTEXl
n02  C                   CAT       UTNO2:1       TTTEXl
     C                   WRITE     TRACKFR
      *
     C                   endsr
      *
      *=====================================================================
      *  Oppdater arkivfil
      *=====================================================================
     C     arkivsr       begsr
      *=====================================================================
     C     FIFIRM        CHAIN     FIRMARC                            33
     C     *IN33         IFEQ      '0'
     C     'ib'          chain     arktxt                             33
     C     *IN33         IFEQ      '0'
     C     arkjn         andeq     'J'
      *
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVEl     UTNO          ARAVD
     C                   MOVE      UTNO          AROPD
     C                   MOVEL     UTNO          ARUNDE
     C                   MOVE      'ib'          ARTYPE
     C                   MOVE      UUSER         ARUSER
     C                   MOVE      WDT           ARDATE
     C                   MOVE      WTM           ARTIME
     C                   MOVE      KUNDNR        ARKUND
N04  C                   exsr      sletta
     C                   WRITE     ARKIVR
     C                   move      'J'           arkiver           1
     C                   END
     C                   END
     C                   ENDSR
      *
N04  C***********************************************
N04  C     Sletta        BEGSR
N04  C***********************************************
N04  C* sletter gamle arkivdokumenter
N04  C                   move      aravd         aravda
N04  C                   move      aropd         aropda
N04  C                   CALL      'SYGE59'
N04  C                   parm                    artype            2
N04  C                   PARM                    aravda            4
N04  C                   PARM                    aropda            7
N04   *
N04  C                   ENDSR
      *
     OFAXMANR   EADD         SKRIV
     O                       UNIK
     O                       FAXNRP
     O                       PTIME
     O                       PDATE
     O                       KUNDNR
     O                       KNAVN
     O                       TIL
     O                       FRA
     O                       EMNE
     O                       FINAVN
     O                       JOBNR
     O                       PUDATE
