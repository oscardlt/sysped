      *
      ***********************************************************
      *** PROGRAM SKAL REGISTRERE E.F. MANIFEST (OPPDRAG)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-48 50-81          ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEFCML1 UF A E           K DISK
     FSADEFCML2 IF   E           K DISK    RENAME(SADEFCMFR:SADEFCMR2)
     F                                     PREFIX(T2_)
     FSADEFCL2  IF   E           K DISK
     FSVEA      IF   E           K DISK
     FSVEH      IF   E           K DISK
     FSVZH      IF   E           K DISK
     FTRACKF    O  A E             DISK
     FKOFAST    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSAD134DB  CF   E             WORKSTN INFDS(FBAREA)
     D                SDS
     D  UUSR                 254    263
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WDT                    1      8
     D  WDT_N                  1      8  0
     D                 DS
     D  WTM                    1      6
     D  WTM_N                  1      6  0
      *  Prototype for 'SYFAKDA'
     d SYFAKDA         pr                  extpgm('SYFAKDA')
     d kftyp_                              like(kftyp)
     d uupdat_                             like(uupdat)
     d xkfkod_                             like(xkfkod)
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for SAD134RB
     d sad134rb        pr
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d uli_                                like(uli)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad134rb        pi
     d uavd                           4  0
     d utdn                           7  0
     d uli                            2  0
     d utype                          3a
     d uflagg                         1  0
      *
     d i               s              3  0
     d uupdat          s              1a
     d usped           s              1a
     d unvn            s             30a
     d ulk             s              2a
     d xkfkod          s              5a
     d xdg             s              2  0
     d xmn             s              2  0
     d xar             s              2  0
     d xli             s              2  0
      *
      /free

         exsr init;

         dow *in03 = *off and *in12 = *off;                       // 1<b
           exfmt bilde01;
           for i = 96 to 99;       // Sett indikator 96-99 av.    // 2<b
             *in(i) = *off;
           endfor;                                                // 2<e
           *in30 = *off;

           select;                                                // 2<b
           when *in49;                                            // 2
             leave;
           when *in04;                                            // 2
             exsr srsok;
           when *in12;                                            // 2
           when *in49;                                            // 2
           // Spørring med oppdrag
             *in12 = *on;
           other;                                                 // 2
             exsr testb1;
             if *in30 = *off;                                     // 3<b
               exsr opdopd;
               *in03 = *on;
             endif;                                               // 3<e
           endsl;                                                 // 2<e
         enddo;                                                   // 1<e

         select;
         when *in12;
           uflagg = 1;
         other;
           uflagg = 0;
         endsl;

         *inlr = *on;
         return;

      //*********************************************
       begsr srsok;

       linnbr = wscloc / 256;
       posnbr = wscloc - (%int(linnbr) * 256);

       select;
       when linnbr = 6 and posnbr >= 15 and posnbr <= 16;
       // EKSPORTTYPE
         kftyp = 'SADEFETYPE';
         kfkod = wsetyp;
         uupdat = 'N';
         syfakda (kftyp: uupdat: kfkod);
         wsetyp = kfkod;
         chain (kftyp: kfkod) kofast;
         if %found;
           wsetypt = kftxt;
         endif;
         linnbr = 7;
         posnbr = 15;
       other;
        msgnr = 'DIV1037';
        *in30 = *on;
       endsl;

       endsr;

      //*********************************************
       begsr testb1;

       if wsavde <> 0;
         chain wsavde svea;
         if not %found;
           *in99 = *on;
         endif;
       endif;

       if wsavde <> 0 or wstdne <> 0;
         chain (wsavde: wstdne) sveh;
         if not %found;
           *in98 = *on;
         else;
           chain sveh_tuid svzh;
           if not %found;
             *in98 = *on;
           else;
             if svzh_mrnn <> *blanks;
               wseid = svzh_mrnn;
             endif;
           endif;
         endif;
       endif;

       kftyp = 'SADEFETYPE';
       kfkod = wsetyp;
       chain (kftyp: kfkod) kofast;
       if %found;
         wsetypt = kftxt;
       else;
         *in97 = *on;
       endif;
       if wsetyp = '02';
         wseser = 'N';
       endif;

       if wseid = *blanks or wseid = cleid;
         *in96 = *on;
       else;
         chain (uavd: utdn: wseid) sadefcml2;
         if %found and cmli <> t2_cmli;
           *in96 = *on;
         endif;
       endif;

       exsr hmsg;

       endsr;

      //*********************************************
       begsr hmsg;

       select;
       when *in99;
         msgnr = 'SPA0001';
         *in30 = *on;
         linnbr = 4;
         posnbr = 15;
       when *in98;
         msgnr = 'SPO0001';
         *in30 = *on;
         linnbr = 4;
         posnbr = 29;
       when *in97;
         msgnr = 'YBC2094';
         *in30 = *on;
         linnbr = 6;
         posnbr = 15;
       when *in96;
         msgnr = 'YBC2128';
         *in30 = *on;
         linnbr = 7;
         posnbr = 15;
       endsl;

       endsr;

      //*********************************************
       begsr movefw;

       wsavde  = cmavde;
       wstdne  = cmtdne;
       wsetyp  = cmetyp;
       wsetypt = cmetypt;
       wseid   = cmeid;
       wseser  = cmeser;

       endsr;

      //*********************************************
       begsr movwef;

       cmavde  = wsavde;
       cmtdne  = wstdne;
       cmetyp  = wsetyp;
       cmetypt = wsetypt;
       cmeid   = wseid;
       cmeser  = wseser;

       endsr;

      //*********************************************
       begsr movsaw;

       wsavde  = 0;
       wstdne  = 0;
       wsetyp  = cletyp;
       wsetypt = *blanks;
       wseid   = *blanks;
       wseser  = cleser;

       endsr;

      //*********************************************
       begsr opdopd;

       chain (uavd: utdn: uli) sadefcml1;

       exsr ttsr;

       // FLYTTER SKJERMVARIABLE TIL FILVARIABLE
       exsr movwef;

       if %found;
         update sadefcmfr;
       else;
         cmavd = uavd;
         cmtdn = utdn;
         cmli  = xli;
         write sadefcmfr;
         uli   = xli;
       endif;

       endsr;

      //*********************************************
       begsr init;

       syge15c (wdt: wtm);

       select;
       when utype = 'DSP';
         *in49 = *on;
       when utype = 'CRT';
         setgt (uavd: utdn) sadefcml1;
         readpe(n) (uavd: utdn) sadefcml1;
         if not %eof;
           xli = cmli + 1;
         else;
           xli = 1;
         endif;
       endsl;

       chain (uavd: utdn) sadefcl2;
       chain(n) (uavd: utdn: uli) sadefcml1;

       if utype = 'DSP' or utype = 'CHG';
         exsr movefw;
       else;
         exsr movsaw;
       endif;

       setll *loval firm;
       read firm;

       write windowsz;

       endsr;

      //*********************************************
       begsr ttsr;

       ttavd  = uavd;
       ttopd  = utdn;
       tttur  = clpro;
       ttacti = 'EFM';
       ttdate = wdt_n;
       ttdate = wdt_n;
       tttime = wtm_n;
       tttiml = wtm_n;
       ttuser = uusr;
       if *in33;
         tttext = 'Deloppdrag (' + %trim(%editc(cmli:'Z'))
                 + ') er laget til E.F. manifest.';
         ttedev = 'CRT';
       else;
         tttext = 'Info. av deloppdrag (' + %trim(%editc(cmli:'Z'))
                 + ') i E.F. manifest er endret.';
         ttedev = 'CHG';
       endif;
       tttexl = tttext;

       write trackfr;

       endsr;

      /end-free
