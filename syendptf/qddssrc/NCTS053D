     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Gå til forrige bilde')
     A                                      HELP
      *
     A          R NC053D01
     A                                  1  3'SYSPED/NCTS053R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 24'NCTS ANKOMST'
     A                                      COLOR(WHT)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(034 SSI0030 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SPA0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NC053D02
     A                                  1  3'SYSPED/NCTS053R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  1 76'Del-1'
     A                                      COLOR(YLW)
     A                                  2 24'NCTS ANKOMST'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  4MSGCON(030 DIV1714 *LIBL/SYMSGF)
     A            WSTDN          7Y 0B  7 40TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 14  4MSGCON(033 DIV1722 *LIBL/SYMSGF)
     A            W0004         35A  B 14 40TEXT('NODI ID AVDELINGEN')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  COLOR(RED)
     A                                 15  6MSGCON(033 DIV1721 *LIBL/SYMSGF)
     A            W0010         35A  B 15 40TEXT('NODI ID TOLLVESENET')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A  97                                  COLOR(RED)
     A                                 17  4MSGCON(033 DIV1723 *LIBL/SYMSGF)
     A            W0035          1A  B 17 40TEXT('Testindikator')
     A                                      VALUES(' ' '1' '2')
     A                                 23  2'F3=Avslutt uten endring'
     A                                      COLOR(BLU)
     A                                 23 28'F12=Gå til forrige uten endring'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NC053D03
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/NCTS053R'
     A                                      COLOR(BLU)
     A                                  1 20'Default verdier av NCTS ANKOMST'
     A                                      COLOR(WHT)
     A                                  1 52'Ref.'
     A            WSAAVD         4Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A            TISG           3A  O  1 70COLOR(WHT)
     A                                  1 76'Del-2'
     A                                      COLOR(YLW)
     A                                  3  2'Godsnr.'
     A            WSGN          35A  B  3 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2'MRN-nr.:'
     A            WSTRNR        18A  B  4 13TEXT('MRN-nr')
     A                                      COLOR(YLW)
     A                                  4 42'Ankomst dato/tid:'
     A            WSDTA          6Y 0B  4 63TEXT('Ankomst dato')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTMA          4Y 0B  4 70TEXT('Ankomst tid')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5  2'Forenklet:'
     A            WSSPR          1Y 0B  5 13TEXT('FORENKLET PROSEDYRE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                  5 16'(1=Ja, 0=Nei)'
     A                                  5 32'Hendelse:'
     A            WSIFLG         1Y 0B  5 43TEXT('Hendelsesflagg')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                  5 46'(1=Ja, 0=Nei)'
     A                                  6  2'Autorisasjon:'
     A                                  6 22'Type:'
     A            WSATYP         4A  B  6 28TEXT('Autorisasjonstype')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  6 33'Ref:'
     A            WSAREF        35A  B  6 38TEXT('Autorisasjons ref.nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Destinasj:'
     A            WSTSBA         8A  B  7 13TEXT('Destinasjons tollsted')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8  2'Trader:'
     A            WSKNTA         8Y 0B  8 13TEXT('Trade kundenr')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNATA        30A  B  8 22TEXT('Trader navn')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTINTA       17A  B  8 53TEXT('Trader org.nr')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8 71'Språk'
     A            WSSKTA         2A  B  8 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Lokasjon:'
     A                                 10  4'Type:'
     A            WSLCTY         1A  B 10 13TEXT('Lokasjon type')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 15'Kvalif:'
     A            WSQID          1A  B 10 23TEXT('Qualifier')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 25'Autorisasjon:'
     A            WSANR         35A  B 10 39TEXT('Autorisasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  4'Till:'
     A            WSAID          4A  B 11 13TEXT('Tilleggs identifier')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 18'Lok:'
     A            WSSD          17A  B 11 23TEXT('Lokasjons UN kode')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12  2'Tollsted:'
     A            WSTS           8A  B 12 13TEXT('Tolldted')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'Økonomi:'
     A            WSKNE          8Y 0B 13 13TEXT('Økonomi operatørsnr')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINE        17A  B 13 22TEXT('Økonomi operatørs org.nr')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'Addresse:'
     A            WSADE1        30A  B 14 13TEXT('Gateadresse')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  2'PN/PS/LK:'
     A            WSPNE         17A  B 15 13TEXT('Postnr')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPSE         24A  B 15 31TEXT('Poststed')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKE          2A  B 15 56TEXT('Landkode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 16  2'Post adr.:'
     A            WSADE2        17A  B 16 13TEXT('Husnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNE2        17A  B 16 31TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKE2         2A  B 16 49TEXT('Landkode')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  2'KontaktP:'
     A            WSCPE         30A  B 17 13TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'Tlf.:'
     A            WSTLE         20A  B 18 13TEXT('Tlf')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2'Epost:'
     A            WSEME         50A  B 19 13TEXT('Epostadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  2'Registreringsdato'
     A            THDTR          8Y 0H
     A            WSDTR          6Y 0O 22 20TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 23  1'F3=Avslutt u/oppdatering'
     A                                      COLOR(BLU)
     A                                 23 28'F4=Kodesøking'
     A                                      COLOR(BLU)
     A                                 23 44'F12=Gå til forrige u/oppdatering'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
