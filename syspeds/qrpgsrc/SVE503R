********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 CB  PTF11 Extern TULLID TDS SE
********************************************************************
     FHEADF     IF   E           K DISK
     FLLHEAD07  IF   E           K DISK
     FLLLINE    UF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLAGJFEU  IF   E           K DISK
     FLLSERL03  IF   E           K DISK
     FLLTARF    IF   E           K DISK
     FLLMRF     IF   E           K DISK
     FSVEA      IF   E           K DISK
     FSVEH      UF A E           K DISK
     FSVEV      O  A E           K DISK
     FSVTHA     IF   E           K DISK
     FSVTVK     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKUNKO2    IF   E           K DISK
     D SN              S             17    DIM(9)
      *
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UDSAVD                15     18  0
     D  UDSSG                 19     21
     D                 DS
     D  VAFAR                  1     10
     D  VAVAL                  1      3
     D                 DS
     D  SVEH_SYAV              1      4  0
     D  SVEH_SYOP              5     11  0
     D  X_SYSREF               1     11
      *
     C                   EXSR      INIT
      *
     C     HEADFK        CHAIN(N)  SVEH                               33
     C     *IN33         CABEQ     *OFF          SLUTT
     C     HEADFK        CHAIN     HEADF                              33
     C  N33HEADFK        CHAIN     LLHEAD07                           33
     C     *IN33         CABEQ     *ON           SLUTT
     C                   EVAL      KUNDNR = LHPNR
     C     CUNDL01K      CHAIN     CUNDL01                            33
      *
     C     LLAGJD02K2    CHAIN(N)  LLAGJD02                           33
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C     LLMRFK        CHAIN     LLMRF                              33
      *
     C     SVEH_SYAV     CHAIN     SVEA                               33
      *
      * FORTOLLING HEADING
     c                   MOVE      *ZEROS        XLI
     C                   EVAL      SVEH_SYSG = HESG
     C                   EVAL      SVEH_SYDT = WDT08
     C                   EVAL      SVEH_MTYP = 'UGE'

N01  C                   IF        UTUID = *BLANKS
     C                   CALL      'SVG001R'
     C                   PARM                    SVEH_TUID
N01  C                   ELSE
N01  C                   EVAL      SVEH_TUID=UTUID
N01  C                   EVAL      SVEH_SYST='G'
N01  C                   ENDIF

     C     KUNKO2KEY     CHAIN     KUNKO2                             33
     C                   IF        *IN33=*OFF
     C                   EVAL      KUNDNR    = KUKUN2
     C                   ELSE
     C                   IF        LHPNR     = 3022
     C                   EVAL      KUNDNR    = 1433
     C                   ENDIF
     C                   ENDIF
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      SVEH_DEK1 = 'EU'
     C                   EVAL      SVEH_DEK2 = 'Z'
     C                   EVAL      SVEH_TART = '1'
     C                   EVAL      SVEH_AVKN = KUNDNR
     C                   EVAL      SVEH_AVNA = KNAVN
     C                   EVAL      SVEH_AVA1 = ADR1
     C                   EVAL      SVEH_AVA2 = ADR2
     C                   EVAL      SVEH_AVPN = SYPOGE
     C                   EVAL      SVEH_AVPA = ADR3
     C                   EVAL      SVEH_AVLK = SYLAND
     C                   EVAL      SVEH_AVUT = SYLAND
     C                   EVAL      SVEH_AVHA = KPERS
     C                   EVAL      SVEH_AVTL = TLF
     C                   EVAL      SVEH_KOTA = LHKLL
     C                   EVAL      SVEH_RFAB = X_SYSREF
     C                   EVAL      SVEH_MONA = LHBNA
     C                   EVAL      SVEH_MOA1 = LHBA1
     C                   EVAL      SVEH_MOA2 = LHBA2
     C                   IF        LHBPN = 0
     C                   EVAL      SVEH_MOPN = LHBPU
     C                   ELSE
     C                   MOVEL     LHBPN         SVEH_MOPN
     C                   END
     C                   EVAL      SVEH_MOPA = LHBA3
     C                   EVAL      SVEH_MOLK = LHBLK
     C                   EVAL      SVEH_DKKN = 1
     C                   EVAL      KUNDNR    = 1
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   IF        EORI = *BLANKS                               1<-B
     C                   MOVEL(P)  SYLAND        SVEH_DKEO
     C                   CAT       SYRG:0        SVEH_DKEO
     C                   CAT       '00':0        SVEH_DKEO
     C                   IF        SYRG=*BLANKS                                  2<-B
     C                   MOVEL(P)  SYLAND        SVEH_DKEO
     C                   CAT       VATKKU:0      SVEH_DKEO
     C                   ENDIF                                                   2<-E
     C                   ELSE                                                   1
     C                   EVAL      SVEH_DKEO = EORI
     C                   END                                                    1<-E
     C                   EVAL      SVEH_DKLK = SYLAND
     C                   EVAL      SVEH_DKNA = KNAVN
     C                   EVAL      SVEH_DKA1 = ADR1
     C                   EVAL      SVEH_DKA2 = ADR2
     C                   IF        POSTNR = 0                                   1<-B
      *
     C                   EVAL      P = %SCAN ('-' : SYPOGE)
     C     9             SUB       P             Q                 1 0
     C                   ADD       1             P                 3 0
     C                   EVAL      SVEH_DKPN = %SUBST(SYPOGE:P:Q)
      *
     C                   ELSE                                                   1
     C                   EVAL      SVEH_DKPN = *BLANKS
     C                   MOVEL     POSTNR        SVEH_DKPN
     C                   ENDIF                                                  1<-E
     C                   EVAL      SVEH_DKPA = ADR3
     C                   IF        SYLAND <> *BLANKS                            1<-B
     C                   EVAL      SVEH_DKLK = SYLAND
     C                   ENDIF                                                  1<-E
     C     SVEH_SYSG     CHAIN     SVTHA                              33
     C                   IF        *IN33=*OFF
     C                   EVAL      SVEH_OMHA = SVTH_NAMN
     C                   EVAL      SVEH_OMTL = SVTH_TLF
     C                   ENDIF
     C                   EVAL      SVEH_OMEO = EORI
     C                   EVAL      SVEH_OMTY = '3'
     C                   EVAL      SVEH_AVUT = 'SE'
     C                   EVAL      SVEH_AUBE = LHBLK
     C                   EVAL      SVEH_CONT = '0'
     C                   EVAL      SVEH_TRAL = 'SE'
     C                   EVAL      SVEH_FATY = 'N380'
     C                   EVAL      SVEH_FATX = LHPSD
     C                   EVAL      SVEH_TRGR = '3'
     C                   EVAL      SVEH_TRIN = '3'
     C                   EVAL      SVEH_GOLK = %SUBST(MRGN:1:3)
     C                   EVAL      SVEH_BRUT = LHWVEK
     C                   IF        LHPNR     = 3022
     C                   EVAL      SVEH_UPPS = 'T'
     C                   ELSE
     C                   EVAL      SVEH_UPPS = *BLANKS
     C                   END
     C                   EVAL      SVEH_UTFA = 'SE060340'
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        LLWNT0 <> 0                                   2<-B
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   SELECT                                                    4<-B
     C                   WHEN      SVEH_VAKD = *BLANKS                             4
     C                   IF        VAVAL = *BLANKS                                  5<-B
     C                   EVAL      SVEH_VAKD = 'SEK'
     C                   ELSE                                                       5
     C                   EVAL      SVEH_VAKD = VAVAL
     C                   END                                                        5<-E
     C                   WHEN      SVEH_VAKD <> VAVAL AND VAVAL <> *BLANKS         4
     C                   EVAL      SVEH_VAKD = 'SEK'
     C                   ENDSL                                                     4<-E
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   ENDDO                                                    3<-E
     C                   END                                                     2<-E
     C     LHMN          READE(N)  LLLINE                                 33
     C                   ENDDO                                                  1<-E
     C     SVTVKK        SETGT     SVTVK
     C     SVEH_VAKD     READPE    SVTVK                                  33
     C                   EVAL      SVEH_VAKD = 'EUR'
     C                   EVAL      SVEH_VAKU = SVVK_KRS
      *
     C                   WRITE     SVEHR
      *
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        LLWNT0 <> 0                                   2<-B
     C                   EXSR      OPPDLIN
     C                   MOVE      'SAD'         LLHK
     C                   END                                                     2<-E
     C                   UPDATE    LLLINER
     C     LHMN          READE     LLLINE                                 33
     C                   ENDDO                                                  1<-E
      *
     C     HEADFK        CHAIN     SVEH                               33
     C                   EVAL      SVEH_FABL = X_FABL
     C                   UPDATE    SVEHR
      *
N01  C                   IF        UTUID = *BLANKS
     C                   CALL      'SVE001R'
     C                   PARM      'E'           MODE              1            E=Endre N=Ny D=Vis
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   PARM                    SVEH_SYSG
N01  C                   ENDIF
      *
     C     SLUTT         TAG
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     OPPDLIN       BEGSR
      *////////////////////////////////////////////////////////////
     C                   CLEAR                   SVEVR
     C                   EVAL      SVEV_SYAV = SVEH_SYAV
     C                   EVAL      SVEV_SYOP = SVEH_SYOP
      *
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   IF        %SUBST(VALLOK:1:5) = 'VEB'
      *
     C                   EVAL      XLI       = XLI + 1
     C                   EVAL      SVEV_SYLI = XLI
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      SVEH_SYAV     VDAVD
     C                   MOVE      SVEH_SYOP     VDOPD
     C                   EVAL      VDTEXT = LHBNA
     C                   EVAL      VDST   = 'R'
     C                   EVAL      VDBUSE = 'SV-EXPORT'
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDST   = 'R'
     C                   UPDATE    LLAGJDR
      *
     C                   EVAL      SVEV_KOTA = VDLANT
     C                   EVAL      SVEV_KOSL = 'NE'
     C                   EVAL      SVEV_VASL = LLVB
     C                   EVAL      SVEV_GODM = 'ADR'
      * TARIC NR
     C     LLTARFK       CHAIN     LLTARF                             33
     C  N33              EVAL      SVEV_VATA = LTTANR
     C  N33              EVAL      SVEV_ULKD = LTLKOP
      *
     C                   EVAL      SVEV_VANO = SVEV_SYLI
     C                   EVAL      SVEV_VATI = '4099'
     C                   EVAL      SVEV_VAT4 = '4999'
     C                   EVAL      SVEV_BRUT = VDLANT * VAVKT0
     C                   EVAL      SVEV_EUP1 = '3171'
     C                   EVAL      SVEV_NETO = SVEV_BRUT * 0,9
     C                   EVAL      SVEV_ANKV = VDLANT
     C                   IF        SVEH_AVKN = 3022
     C                   EVAL      SVEV_SUTX = 'COLLICARE AGERAR SOM INDIREKT '
     C                             + 'OMBUD ÅT POLARIS SALES EUROPE SARL'
     C                   ENDIF
     C                   EVAL      SVEV_STVA = VAVERD * VDLANT
     C                   EVAL      SVEV_STVA2 = VAVERD * VDLANT
     C                   EVAL      X_FABL    = X_FABL + VAVERD * VDLANT
     C                   EVAL      SVEV_BIT1 = 'N380'
     C                   EVAL      SVEV_BIT2 = 'Y903'
     C                   EVAL      SVEV_BIT3 = 'Y935'
     C                   EVAL      SVEV_LAGI = MRGN
     C                   EVAL      SVEV_LAGT = 'C'
     C                   EVAL      SVEV_LAGL = 'SE'
     C                   EVAL      SVEV_CALL = '00'
      *
     C                   WRITE     SVEVR
      *
     C                   END
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
N01  C                   PARM                    UTUID            10
      *
     C     HEADFK        KLIST
     C                   KFLD                    SVEH_SYAV
     C                   KFLD                    SVEH_SYOP
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     SVEHK         KLIST
     C                   KFLD                    SVEH_SYAV
     C                   KFLD                    SVEH_SYOP
     C     SVTVKK        KLIST
     C                   KFLD                    SVEH_VAKD
     C                   KFLD                    XX_DTS
     C     LLAGJL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLAGJFEUK     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMN
     C     LLAGJD02K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGJD02K2    KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHMN
     C     LLMRFK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMRNR
     C     LLTARFK       KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C                   KFLD                    LTLK
     C     KUNKO2KEY     KLIST
     C                   KFLD                    LHPNR
     C                   KFLD                    LHBNR
      *
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C     *LIKE         DEFINE    SVEV_SYLI     XLI
     C     *LIKE         DEFINE    SVEH_FABL     X_FABL
     C     *LIKE         DEFINE    SVVK_DTS      XX_DTS
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      UAVD          UDSAVD
     C                   MOVE      UAVD          SVEH_SYAV
     C                   MOVE      UOPD          SVEH_SYOP
     C                   MOVE      'SE'          LTLK
     C                   EVAL      XX_DTS = 99999999
     C                   OUT       SAVUDS
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               8
     C                   MOVE      WDT           WDT08             8 0
      *
     C                   ENDSR
      *
