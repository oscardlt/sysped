      *****************************************************************************************
      * Importer csv fil med postnr styring (tidligere eksportert via f.eks meny epost pkt 11)*
      *                          (mappe  /SYSPEDFX/DIVCSV/NOPNR , med valgfritt filnavn)      *
      *****************************************************************************************
      * Deleter  alle poster i NOPNR  / writer så de nye
      * OBS!!! kolonnene må jo selvsagt være uendret siden scv-en ble eksportert !!!
      *****************************************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Ny kolonne sist - NOSBTI = Storby/miljø-tillegg
********************************************************************
      *
     FNOPNRSDV  IP   F 1024        DISK
N01  FNOPNR     UF A E           K DISK
      * FILA NOPNR/NOPNRR , * NUMERISK
      *  1 NOTERM* ; 2 NOPONR* ; 3 NONAVN ; 4 NOAVGD ; 5 NOFRFT* ; 6 NOHTAB ;
      *  7 NOAPR1 ; 8 NOATA1 ; 9 NOAPR2 ; 10 NOATA2 ; 11 NOAPR3 ; 12 NOATA3 ;
      *  13 NOAPR4;14 NOATA4;15 NOAPR5;16 NOATA5;17 NOTYPE;18 NOPNAT* ;19 NOATAB ;
      *  NOAVDU* ; NOAVDI* ; NOAVDH* ; NOSBTI
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  data                   1   1024
     D  da                     1   1024
     D                                     DIM(1024)
     D                 DS
     D  felta                  1     60
     D  fa                     1     60
     D                                     DIM(60)
      * ARRAY MED INTIL 600 KOLONNER A 100 TEGN etter dekoding.
     D                 DS
     D  dta                    1  60000
     D                                     DIM(600)
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D                 DS
     D  Test                   1      5
     D  Test1_3                1      3
     D  Test4_5                4      5
     D  Test5_5                5      5
     D  Test1_4                1      4
     D                 DS
     D  ADR1                   1     30
     D  ADR1_1_4               1      4
     INOPNRSDV  NC  01
     I                                  1    4  Data1_4
     I                                  1 1024  Data
      * Første syklus = TØM
     C     Ini           ifeq      ' '
     c                   exsr      delPnr
     c                   move      'X'           Ini               1
     c                   endif
      *
      * INITIERER VARIABLE
      *
     c                   z-add     1             fra               3 0
     c                   z-add     1             til               3 0
     c                   z-add     0             DN                3 0
     c                   clear                   dta
     C     DATA1_4       IFLE      '0000'
     C     DATA1_4       ORGE      '9999'
     C                   GOTO      NextRcd
     C                   END
      *
      * leser til neste skilletegn ';'
      *
     c     til           dowlt     900
     C     da(til)       ifne      ';'
     c                   add       1             til
     c                   else
      *
      * fra og til er funnet
      *
     c                   exsr      dekod
     c                   add       1             til
     c                   z-add     til           fra
     c                   endif
     c                   enddo
      *
      * siste post
      *
     c     fra           add       58            til
     c                   exsr      dekod
      * DETALJBEHANDLING
      *
     c                   exsr      DETSR
      *
     c     NextRcd       tag
      *
     c***************************************************************
     c     delPnr        begsr
     c***************************************************************
     c     *loval        setll     NOPNR
     c                   read      NOPNR                                  33
     c     *in33         doweq     '0'
     c                   delete    NOPNRR
     c                   read      NOPNR                                  33
     c                   enddo
     c                   endsr
     c***************************************************************
     c     dekod         begsr
     c***************************************************************
      *
      * dekoder innlest post
      *
     c     til           sub       fra           antp              3 0
     c     antp          ifgt      0
     c                   exsr      dokoda
     c                   add       1             dn
     c                   movel     felta         dta(dn)
     C                   ELSE
     c                   add       1             dn
     c                   end
     c                   endsr
     c***************************************************************
     c     DOKODA        begsr
     c***************************************************************
     c*    dekoder  TIL FELTA
     c*
     c                   move      *blanks       felta
     c                   z-add     fra           nr                3 0
     c                   z-add     1             n2                3 0
     c                   do        antp
     c                   move      da(nr)        fa(n2)
     c                   add       1             n2
     c                   add       1             nr
     c                   enddo
     c                   endsr
     c***************************************************************
     c     DETSR         begsr
     c***************************************************************
     c                   clear                   NOPNRR
      *
      *1 = Terminal
     c                   movea     dta(1)        a
     c                   exsr      getnum
     c                   z-add     znum          NOTERM
     c     Noterm        cabeq     0             Next
      *2 = Postnr
     c                   movea     dta(2)        a
     c                   exsr      getnum
     c                   z-add     znum          NOPONR
     c     NoPonr        cabeq     0             Next
      *
      *3 = Navn
     c                   movel     dta(3)        NONAVN
      *4 = avg.dager
     c                   movel     dta(4)        NOAVGD
      *5 = framføringstid
     c                   movea     dta(5)        a
     c                   exsr      getnum
     c                   z-add     znum          NOFRFT
      *6 = HOV-TAB
     c                   movel     dta(6)        NOHTAB
      *7 = AVVIKENDE PROD.KODE
     c                   movel     dta(7)        NOAPR1
      *8 = AVVIKENDE PRISTABELL
     c                   movel     dta(8)        NOATA1
      *9 = AVVIKENDE PROD.KODE
     c                   movel     dta(9)        NOAPR2
      *10= AVVIKENDE PRISTABELL
     c                   movel     dta(10)       NOATA2
      *11= AVVIKENDE PROD.KODE
     c                   movel     dta(11)       NOAPR3
      *12= AVVIKENDE PRISTABELL
     c                   movel     dta(12)       NOATA3
      *13= AVVIKENDE PROD.KODE
     c                   movel     dta(13)       NOAPR4
      *14= AVVIKENDE PRISTABELL
     c                   movel     dta(14)       NOATA4
      *15= AVVIKENDE PROD.KODE
     c                   movel     dta(15)       NOAPR5
      *16= AVVIKENDE PRISTABELL
     c                   movel     dta(16)       NOATA5
      *17= TYPE
     c                   movel     dta(17)       NOTYPE
      *18= PTAB
     c                   movel     dta(18)       NOPNAT
      *19= ATAB
     c                   movel     dta(19)       NOATAB
      *20= AVD UTKJØRING
     c                   movea     dta(20)       a
     c                   exsr      getnum
     c                   z-add     znum          NOAVDU
      *21= AVD INNHENTING
     c                   movea     dta(21)       a
     c                   exsr      getnum
     c                   z-add     znum          NOAVDI
      *22= AVD HENTING
     c                   movea     dta(22)       a
     c                   exsr      getnum
     c                   z-add     znum          NOAVDH
N01   *23= Storby/Miljø-tillegg
N01  c                   movea     dta(23)       a
N01  c                   exsr      getnum
N01  c                   z-add     znum          NOSBTI
      *
     c                   write     Nopnrr
     C     Next          tag
      *
      *
     c                   endsr
     c*****************************************
     c     getnum        begsr
     c*****************************************
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
     c
