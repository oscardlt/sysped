     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR STSCLI)
     A                                      HLPTITLE('MANUELL OPPFØLGING AV MAN-
     A                                      KO - KOLLINIVÅ')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '9')
     A            SCCLID        20A  O  6  6
     A            LNR            3Y 0O  6 28EDTCDE(4)
     A                                  6 33'av'
     A            TOT            3Y 0O  6 36EDTCDE(4)
     A            SCSTAT         1A  O  6 41TEXT('TERMKODE')
     A            SCDT           8Y 0O  6 58EDTWRD('    .  .  ')
     A            SCTID          4Y 0O  6 69EDTWRD('  :  ')
     A            SCUSER        10A  O  6 47
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/STSCLI'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 19'Kollinivå             '
     A                                      COLOR(WHT)
     A            XXTYPE         5A  O  2 44
     A                                  2 50'Tur'
     A            XXPRO          8Y 0O  2 54EDTCDE(4)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42'Avd/Op/Fb'
     A            XXAVD          4Y 0O  3 52EDTCDE(4)
     A            XXOPD          7Y 0O  3 57EDTCDE(4)
     A            XXFBNR         3Y 0O  3 65EDTCDE(4)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'9=Switch status                   -
     A                                                     '
     A                                      COLOR(BLU)
     A                                  4 52'SKUTT AV....'
     A                                      COLOR(WHT)
     A                                  5  2'Valg Kolli ID             Lnr  av -
     A                                      Tot Stat   BrukerID     Dato      T-
     A                                      id       '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/STSCLI'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 19'Kollinivå             '
     A                                      COLOR(WHT)
     A            XXTYPE         5A  O  2 44
     A                                  2 50'Tur'
     A            XXPRO          8Y 0O  2 54EDTCDE(4)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42'Avd/Op/Fb'
     A            XXAVD          4Y 0O  3 52EDTCDE(4)
     A            XXOPD          7Y 0O  3 57EDTCDE(4)
     A            XXFBNR         3Y 0O  3 65EDTCDE(4)
     A                                  4  2'9=Switch status                   -
     A                                                     '
     A                                      COLOR(BLU)
     A                                  4 52'SKUTT AV....'
     A                                      COLOR(WHT)
     A                                  5  2'Valg Kolli ID             Lnr  av -
     A                                      Tot Stat   BrukerID     Dato      T-
     A                                      id       '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
