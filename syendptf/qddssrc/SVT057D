     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SVT057DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            SVVK_KD        3A  O  8  4
     A            SVVK_KRS      10Y 6O  8  8EDTCDE(K)
     A            SVVK_OMR       7Y 0O  8 22EDTCDE(K)
     A            SVVK_DTS       8Y 0O  8 33EDTCDE(4)
     A            SVVK_DTE       8Y 0O  8 42EDTCDE(4)
      *
     A          R SVT057DB                  SFLCTL(SVT057DA)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SVT057'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 30'Underhåll av TDS Import'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 35'Valutakurser'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  6 33'Giltighetsdatum'
     A                                      COLOR(WHT)
     A                                  7  4'Kod'
     A                                      COLOR(WHT)
     A                                  7 16'Kurs'
     A                                      COLOR(WHT)
     A                                  7 25'Faktor'
     A                                      COLOR(WHT)
     A                                  7 33'Från'
     A                                      COLOR(WHT)
     A                                  7 42'Till'
     A                                      COLOR(WHT)
      *
     A          R SVT057DC
     A                                      OVERLAY
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 23 35'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVT057DD
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVVK_KD        3A  B 21  4
     A  31                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A            ZVVK_KRS      10Y 6B 21  8EDTCDE(K)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            ZVVK_OMR       7Y 0B 21 22EDTCDE(K)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            ZVVK_DTS       8Y 0B 21 33EDTCDE(4)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A            ZVVK_DTE       8Y 0B 21 42EDTCDE(4)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVT057DD2
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVVK_KD        3A  O 21  4
     A            ZVVK_KRS      10Y 6B 21  8EDTCDE(K)
     A  32                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A            ZVVK_OMR       7Y 0B 21 22EDTCDE(K)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            ZVVK_DTS       8Y 0O 21 33EDTCDE(4)
     A            ZVVK_DTE       8Y 0B 21 42EDTCDE(4)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVT057DE
     A                                 20  2' '
     A                                 21  2' '
      *
     A          R SVT057DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
