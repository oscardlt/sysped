********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Skriv rett til IFS  mye endret, ikke merket
11XXX * 02 CB  PTF11 Feil i USC
P11130* 03 CB  PTF11  Møjlighet till fler varuposter
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
     FEDIS      UF   E           K DISK
     FEDIMC     UF   E           K DISK
     FSVIH      UF   E           K DISK
     FSVTFI     IF   E             DISK
     FSVIHH     O  A E             DISK
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1  30000
     D                                     DIM(30000)                           Input string
     D  ADS                    1  30000
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1  30000
     D                                     DIM(30000)                           Output string
     D  BDS                    1  30000
      *________________________________________
      * DS for å venstrestille numeriske felter
      *""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15  2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      *__________
      * Variabler
      *""""""""""
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Data            s          30000a   Varying
     D Codepage        s              5a   Varying
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
      *   SYEDI data area
     D EDIDTA          DS           256
     D  UELIBF               171    180
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
     C                   EXSR      AUT
     C                   EXSR      UNZ
     C                   EXSR      CATIFS
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SSN           WSN
     C     *LIKE         DEFINE    MST           WST
     C     *LIKE         DEFINE    MVEN          WVEN
     C     *LIKE         DEFINE    SVIH_SYAV     WVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     WVIH_SYOP

     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W0074             7 0
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSN
     C     KEYB          KLIST
     C                   KFLD                    SSR
     C                   KFLD                    WST
     C                   KFLD                    WVEN
     C                   KFLD                    S0004
     C                   KFLD                    S0010
     C                   KFLD                    S0035
     C     KEYD          KLIST
     C                   KFLD                    WVIH_SYAV
     C                   KFLD                    WVIH_SYOP
      *_____________
      * Init av felt
      *"""""""""""""
      * Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      '+'           WST
     C                   MOVE      *BLANKS       WVEN
      * Workfields for Array's
     C                   Z-ADD     0             AX                2 0          Index for A
     C                   Z-ADD     1             BX                5 0          Index for B
     C                   Z-ADD     0             AXV               5 0          Leftindx A
     C                   Z-ADD     0             AXH               5 0          Rightindx A
     C                   MOVE      'R'           WDIR              1            Search direction
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   MOVE      'Y'           WCHART            1            Test character
      * Workfields for separation character  '
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WSN           PARM                    WSN
     C                   PARM                    WNETW             1
      *_______________________________
      * Hente firmarecord
      *"""""""""""""""""""""""""""""""
     C                   READ      SVTFI                                  33
      * Åpne dataarea
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________________________
      * Hente data fra sendningslogg
      *"""""""""""""""""""""""""""""
     C     KEYA          CHAIN     EDIS                               81
      * Legg in UNB-segment (Sendningsstart)
     C                   EXSR      UNB0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les melding                                          SRC /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C     KEYB          CHAIN     EDIMC                              8182
     C     *IN81         IFEQ      *OFF

     C     M0065         IFEQ      'CUSDEC'
     C     M1N07         ANDEQ     'SVI'
     C     *IN82         ANDEQ     *OFF

      * Uppdatera SVIH
     C                   EVAL      WVIH_SYAV=MAVD
     C                   EVAL      WVIH_SYOP=MTDN
     C     KEYD          CHAIN     SVIH                               82
     C     *IN82         IFEQ      '0'
     C                   Z-ADD     SSN           MSN
     C                   MOVE      'A'           MST
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   UPDATE    EDIMR
     C                   EVAL      SVIH_SYST='A'
     C                   UPDATE    SVIHR
      * Skriv til historik
     C                   CLEAR                   SVIHHR
     C                   EVAL      SVIHH_SYA=MAVD
     C                   EVAL      SVIHH_SYO=MTDN
     C                   EVAL      SVIHH_DTH=WDT
     C                   EVAL      SVIHH_TMH=WTM
     C                   EVAL      SVIHH_SR='S'
     C                   EVAL      SVIHH_TXH='CUSDEC'
     C                   CAT       M1001:1       SVIHH_TXH
     C                   EVAL      SVIHH_TX1='Øverføringsreferans:'
     C                   CAT       SMBR:1        SVIHH_TX1
     C                   WRITE     SVIHHR
      * Lag Tulldatafilen (UNH - UNT)
     C                   MOVEL     SVIH_MTYP     WVIH_MTYP1        1
     C                   CALL      'SVI022RN'
     C                   PARM                    SVIH_SYAV
     C                   PARM                    SVIH_SYOP
S03  C*                  PARM                    UDATA         30000
S03  C*                  EVAL      ADS=%trimr(UDATA)
S03  C*                  EVAL      AXV=1
S03  C*    ' '           CHECKR(E) ADS           AXH
S03  C*                  EVAL      WCHART='N'
S03  C*                  EXSR      SRÅ

     C                   MOVE      SVIH_SYAV     WAVD
     C                   MOVE      SVIH_SYOP     WOPD
     C                   MOVE      *BLANKS       WFILNAM         256
     C                   EVAL      WFILNAM = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/SVIU' + WAVD + '-' + WOPD

S03  C*                  EVAL      Codepage = '819'
S03  C*                  EVAL      FILE_o = open( %TrimR( WfilNam )
S03  C*                            : 'w, codepage=' + Codepage )

S03  C*                  EVAL      rc = close( FILE_o )

S03  C*                  EVAL      FILE_o = open( %TrimR( WfilNam )
S03  C*                            : 'w' )

S03  C*                  IF        FILE_o <> *Null
S03  C*                  EVAL      Data = %trimr(BDS)
S03  C*                  EVAL      rc = fputs(Data : File_o)
S03  C*                  EVAL      rc = close( FILE_o )
S03  C*                  ENDIF
     C                   EVAL      ADS=*BLANKS
     C                   EVAL      BDS=*BLANKS
     C                   Z-ADD     0             AX
     C                   Z-ADD     1             BX
     C                   Z-ADD     0             AXV
     C                   Z-ADD     0             AXH
     C                   MOVE      'R'           WDIR
     C                   MOVE      *BLANKS       WCHAR
     C                   MOVE      'Y'           WCHART
      * Lag cheksum av tulldata (UNH - UNT)
     C                   MOVE      *BLANKS       UFILI           128
     C                   MOVE      SVIH_SYAV     WAVD
     C                   MOVE      SVIH_SYOP     WOPD
     C                   EVAL      UFILI = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/SVIU' + WAVD + '-' + WOPD

     C                   MOVE      *BLANKS       UFILO           128
     C                   EVAL      UFILO = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/SVIC' + WAVD + '-' + WOPD

     C                   CALL      'SVG004R'
     C                   PARM                    UFILI           128
     C                   PARM                    UFILO           128
     C                   PARM                    UCHKSUM         128
      * Lag signatur(kryptert) av checksum
     C                   MOVE      *BLANKS       UFILI
     C                   EVAL      UFILI = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/SVIU' + WAVD + '-' + WOPD
     C                   MOVE      *BLANKS       UFILO           128
     C                   EVAL      UFILO = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/SVIK' + WAVD + '-' + WOPD
     C                   CALL      'SVG005R'
     C                   PARM                    UFILI           128
     C                   PARM                    UFILO           128
     C                   PARM                    UKRYPT          512

     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slutt på file                                        UNZ /
      *////////////////////////////////////////////////////////////
     C     UNZ           BEGSR
      *_____________________________
      * Legg in UNZ (Slutt sendning)
      *"""""""""""""""""""""""""""""
     C                   EVAL      ADS='UNZ+2+' + %trimr(S0020)+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *___________________________________
      * Lag streng for katalognavn/filnavn
      *"""""""""""""""""""""""""""""""""""
     C                   MOVE      *BLANKS       WFILNAM         256
     C                   EVAL      WfilNam = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + %trimr(SMBR) + 'UNZ'
      *_____________________________
      * Lag fil og set codepage (pc)
      *"""""""""""""""""""""""""""""
     C                   EVAL      Codepage = '819'
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w, codepage=' + Codepage )

     C                   EVAL      rc = close( FILE_o )
      *________________________________________
      * Åpne fil, med tegnkonvertering for jobb
      *""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w' )

     C                   IF        FILE_o <> *Null
     C                   EVAL      Data = %trimr(BDS)
     C                   EVAL      rc = fputs(Data : File_o)
     C                   EVAL      rc = close( FILE_o )
     C                   ENDIF
      *________________________
      * Oppdatere sendningslogg
      *""""""""""""""""""""""""
     C                   EVAL      SST='A'
     C                   EVAL      S0036=2
     C                   UPDATE    EDISR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_________________
      * Trim left blanks
      *"""""""""""""""""
     C                   MOVEL(P)  ADS           ADS2          30000
     C                   EVAL      ADS=*BLANKS
     C                   EVAL      ADS=%TRIML(ADS2)
      *_______________
      * Find positions
      *""""""""""""""""
     C                   EVAL      AXV=1
     C     ' '           CHECKR    ADS           AXH
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV
     C                   MOVE      A(AXV)        WCHAR
      * If invalid character (' + : ?) then replace with 'Ø'
     C     WCHART        IFEQ      'Y'
     C     WDIR          ANDEQ     'R'
     C     WCHAR         IFEQ      ';'
     C                   EVAL      WCHAR=':'
     C                   ENDIF
     C     WCHAR         COMP      WAPSTR                                 83
     C  N83WCHAR         COMP      '+'                                    83
     C  N83WCHAR         COMP      ':'                                    83
     C  N83WCHAR         COMP      '?'                                    83
     C   83              EXSR      SRÅA
     C                   ENDIF
      * Move to output string
     C                   MOVE      WCHAR         B(BX)
     C                   ADD       1             BX

     C                   ENDDO

     C                   EVAL      WCHART='Y'
     C                   EVAL      ADS=*BLANKS

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for replace control characters                      SRÅA
      *////////////////////////////////////////////////////////////
     C     SRÅA          BEGSR
      * Move '?' before the character
     C                   MOVE      '?'           B(BX)
     C                   ADD       1             BX

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'
     C                   MOVE      ØA152H        ØA12
     C                   ELSE
     C                   MOVEL     ØA152X        ØA12
     C                   MOVE      '.  '         ØA12
     C                   MOVE      ØA152D        ØA12
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   ENDIF
     C                   ENDDO
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sendningsstart                                      UNB0 /
      *////////////////////////////////////////////////////////////
     C     UNB0          BEGSR
      *____
      * UNA
      *""""
     C                   MOVEL(P)  'UNA:+.?'     ADS
     C     ADS           CAT       WAPSTR:1      ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     9             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *______________
      * START SEGMENT
      *""""""""""""""
      * UNB+UNOC:3+9368092194+2021000969+040427:1015+E0001980+ABC123+CUSDECI-IE++++1'
     C                   MOVEL(P)  'UNB+UNOC:3+' ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     11            AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *____________________
      * OVERFØRINGSAVSENDER
      *""""""""""""""""""""
     C                   MOVEL(P)  S0004         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     35            AXH
     C                   EXSR      SRÅ
      * Vidareadress
     C     SVTF_0008     IFNE      *BLANKS
     C                   MOVEL(P)  '::'          ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     2             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ

     C                   MOVEL(P)  SVTF_0008     ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     14            AXH
     C                   EXSR      SRÅ
     C                   ENDIF

     C                   MOVEL(P)  '+'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *____________________
      * OVERFØRINGSMOTTAKER
      *""""""""""""""""""""
     C                   MOVEL(P)  S0010         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     35            AXH
     C                   EXSR      SRÅ
      * Vidareadress
     C     SVTF_0014     IFNE      *BLANKS
     C                   MOVEL(P)  '::'          ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     2             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ

     C                   MOVEL(P)  SVTF_0014     ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     14            AXH
     C                   EXSR      SRÅ
     C                   ENDIF

     C                   MOVEL(P)  '+'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *__________________________________________
      * DATO/TID FOR FREMSTILLING AV OVERFØRINGEN
      *""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *BLANKS       ADS
     C                   EVAL      ADS = %SUBST(WDT:3:6)
     C                   Z-ADD     1             AXV
     C                   Z-ADD     6             AXH
     C                   EXSR      SRÅ

     C                   MOVEL(P)  ':'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Tid
     C                   MOVE      *BLANKS       ADS
     C     4             SUBST     WTM:1         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     4             AXH
     C                   EXSR      SRÅ

     C                   MOVEL(P)  '+'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *________________________
      * OVERFØRINGENS REFERANSE
      *""""""""""""""""""""""""
     C                   MOVEL(P)  S0020         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     10            AXH
     C                   EXSR      SRÅ

     C                   MOVEL(P)  '+'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *________
      * PASSORD
      *""""""""
     C                   MOVEL(P)  SVTF_0022     ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     10            AXH
     C                   EXSR      SRÅ

     C                   MOVEL(P)  '+'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *______________________
      * APPLIKASJONSREFERANSE
      *""""""""""""""""""""""
     C                   MOVEL(P)  S0026         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     14            AXH
     C                   EXSR      SRÅ

     C                   MOVEL(P)  '+++'         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     3             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *__________________________
      * 0032 Teckenrepresentation
      *""""""""""""""""""""""""""
      *______________
      * TESTINDIKATOR
      *""""""""""""""
     C     S0035         IFGT      '0'
     C                   MOVEL(P)  '+'           ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ

     C                   MOVEL(P)  S0035         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   EXSR      SRÅ
     C                   ENDIF
      *______________
      * SLUTT SEGMENT
      *""""""""""""""
     C                   MOVEL(P)  WAPSTR        ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1             AXH
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *___________________________________
      * Lag streng for katalognavn/filnavn
      *"""""""""""""""""""""""""""""""""""
     C                   MOVE      *BLANKS       WFILNAM         256
     C                   EVAL      WfilNam = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + %trimr(SMBR) + 'UNB'
      *_____________________________
      * Lag fil og set codepage (pc)
      *"""""""""""""""""""""""""""""
     C                   EVAL      Codepage = '819'
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w, codepage=' + Codepage )

     C                   EVAL      rc = close( FILE_o )
      *________________________________________
      * Åpne fil, med tegnkonvertering for jobb
      *""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %TrimR( WfilNam )
     C                             : 'w' )

     C                   IF        FILE_o <> *Null
     C                   EVAL      Data = %trimr(BDS)
     C                   EVAL      rc = fputs(Data : File_o)
     C                   EVAL      rc = close( FILE_o )
     C                   ENDIF

     C                   EVAL      ADS=*BLANKS
     C                   EVAL      BDS=*BLANKS
     C                   Z-ADD     0             AX
     C                   Z-ADD     1             BX
     C                   Z-ADD     0             AXV
     C                   Z-ADD     0             AXH
     C                   MOVE      'R'           WDIR
     C                   MOVE      *BLANKS       WCHAR
     C                   MOVE      'Y'           WCHART
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  AUTOACK                                              AUT /
      *////////////////////////////////////////////////////////////
     C     AUT           BEGSR
      *____
      * UNH
      *""""
     C                   EVAL      ADS='UNH+'+%trimr(S0020)
     C                             + 'A+AUTACK:4:1:UN:SEA02A'+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *____________
      * USH Grupp 1
      *""""""""""""
     C                   EVAL      ADS='USH+7+'+%trimr(S0020)+'S+3++2++1+ZH1::'
     C                             + %trimr(SVTF_SEC1)+'++1:'+WDT+':'+WTM+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *____________
      * USA Grupp 1
      *""""""""""""
     C                   EVAL      ADS='USA+1:::48'+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *____________
      * USC Grupp 2
      *""""""""""""
S02  C*                  EVAL      ADS='USC+'+%trimr(SVTF_CER1)
S02  C*                                + '+4:' + %trimr(SVTF_CER2) + '+3'
S02  C*                                + WAPSTR
S02  C*                  EVAL      AXV=1
S02  C*    ' '           CHECKR(E) ADS           AXH
S02  C*                  EVAL      WCHART='N'
S02  C*                  EXSR      SRÅ
N02  C                   EVAL      ADS='USC+'
N02  C                   EVAL      AXV=1
N02  C     ' '           CHECKR(E) ADS           AXH
N02  C                   EVAL      WCHART='N'
N02  C                   EXSR      SRÅ
N02  C                   MOVEL(P)  SVTF_CER1     ADS
N02  C                   Z-ADD     1             AXV
N02  C                   Z-ADD     35            AXH
N02  C                   EXSR      SRÅ
N02
N02  C                   MOVEL(P)  '+4:'         ADS
N02  C                   Z-ADD     1             AXV
N02  C                   Z-ADD     3             AXH
N02  C                   MOVE      'N'           WCHART
N02  C                   EXSR      SRÅ
N02
N02  C                   MOVEL(P)  SVTF_CER2     ADS
N02  C                   Z-ADD     1             AXV
N02  C                   Z-ADD     35            AXH
N02  C                   EXSR      SRÅ
N02
N02  C                   MOVEL(P)  '+3'          ADS
N02  C                   Z-ADD     1             AXV
N02  C                   Z-ADD     2             AXH
N02  C                   MOVE      'N'           WCHART
N02  C                   EXSR      SRÅ
N02
N02  C                   MOVEL(P)  WAPSTR        ADS
N02  C                   Z-ADD     1             AXV
N02  C                   Z-ADD     1             AXH
N02  C                   MOVE      'N'           WCHART
N02  C                   EXSR      SRÅ
      *____________
      * USA Grupp 2
      *""""""""""""
     C                   EVAL      ADS='USA+6:::10'+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *____________
      * USX Grupp 3
      *""""""""""""
     C                   MOVEL     SVIH_SYAV     W0062            11
     C                   MOVE      SVIH_SYOP     W0062
     C                   EVAL      ADS='USX+'+%trimr(S0020)+'++++++'
     C                             + W0062+'+CUSDEC:D:96B:UN'+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *____________
      * USY Grupp 3
      *""""""""""""
     C                   EVAL      ADS='USY+'+%trimr(S0020)+'S+ZS3:'
     C                                 + %trimr(UCHKSUM) + WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ

     C                   EVAL      ADS='USY+'+%trimr(S0020)+'S+ZS4:'
     C                                 + %trimr(UKRYPT) + WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
      *____
      * UNT
      *""""
     C                   EVAL      ADS='UNT+9+'+%trimr(S0020)+'A'+WAPSTR
     C                   EVAL      AXV=1
     C     ' '           CHECKR(E) ADS           AXH
     C                   EVAL      WCHART='N'
     C                   EXSR      SRÅ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Samkjør nå flere *IFS filer til 1                 CATIFS /
      *////////////////////////////////////////////////////////////
     C     CATIFS        BEGSR
      * Filene som skal samkjøres er:
      * SXXXXXXXUNB        Start av fil
      * SVIU1111-2222222   UNH til UNT Tullmeddelande
      * SXXXXXXXUNZ        Slutt på fil
      * Samkjøres til SXXXXXXXX   (Standard SYSPED)
      *  QSH CMD('cat a b c > d')


     C                   MOVE      *BLANKS       WFA             128
     C                   EVAL      WFA = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + %trimr(SMBR) + 'UNB'

     C                   MOVE      *BLANKS       WFB             128
     C                   MOVE      SVIH_SYAV     WAVD              4
     C                   MOVE      SVIH_SYOP     WOPD              7
     C                   EVAL      WFB = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + 'SVIU'
     C                             + WAVD
     C                             + '-'
     C                             + WOPD

     C                   MOVE      *BLANKS       WFC             128
     C                   EVAL      WFC = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + %trimr(SMBR) + 'UNZ'

     C                   MOVE      *BLANKS       WFD             128
     C                   EVAL      WFD = '/' + %trimr(UELIBF)
     C                             + '/EDISE/'
     C                             + %trimr(SMBR)
      *----------------------TEST--------------------------------------
     C                   EVAL      Codepage = '819'
     C                   EVAL      FILE_o = open( %TrimR( WFD )
     C                             : 'w, codepage=' + Codepage )
     C                   EVAL      rc = close( FILE_o )

      *----------------------TEST--------------------------------------
     C                   MOVE      *BLANKS       WCMD            300
     C                   EVAL      WCMD = 'QSH CMD(' + WAPSTR + 'cat'
     C                             + ' '
     C                             + %trimr(WFA)
     C                             + ' '
     C                             + %trimr(WFB)
     C                             + ' '
     C                             + %trimr(WFC)
     C*                            + ' > '
     C                             + ' >> '
     C                             + %trimr(WFD)
     C                             + WAPSTR + ')'

     C                   CALLP     RUNCMD(WCMD:300)
     C                   ENDSR
