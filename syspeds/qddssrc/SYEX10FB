********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
      *                 SKRIV MERKELAPP...
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX10BPNL)
     A                                      HLPTITLE('REGISTRERING AV CMR-FRAKT-
     A                                      BREV')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A  29                                  CF01(01 'Bytt avdelinger')
     A  29                                  CF02(02 'Gå til valgbilde')
     A                                      CF03(03 'Avslutt')
     A N29                                  CF04(04 'Søke kundenr.')
     A N29                                  CF05(05 'Videre')
     A  29                                  CF06(06 'TIL FAKTURA')
     A N29                                  CF16(16 'Neste pos')
     A                                      CF12(12 'Avbryt/TIL RULLBILDE')
     A                                      CF13(13 'KOLLIID')
     A  28                                  ALARM
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A            HEAVD          4Y 0O  1 55COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 60'/'
     A            HEOPD          7Y 0O  1 62COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 70'/'
     A            HESG           3A  O  1 72COLOR(BLU)
     A                                  2  6MSGCON(012 DIV0623 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  2 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAA         30A  O  2 43COLOR(WHT)
     A                                  3  6MSGCON(021 DIV0629 *LIBL/SYMSGF)
     A            WSKNFF         8Y 0B  3 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAFF        30A  O  3 43COLOR(WHT)
     A                                  4  6MSGCON(012 DIV0624 *LIBL/SYMSGF)
     A            WSKNM          8Y 0B  4 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4 43MSGCON(030 DIV0625 *LIBL/SYMSGF)
     A            WSNAM         30A  B  5  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNAL         30A  B  5 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADM1        30A  B  6  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADL1        30A  B  6 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADM2        30A  B  7  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADL2        30A  B  7 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADM3        30A  B  8  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADL3        30A  B  8 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9  6MSGCON(030 DIV0626 *LIBL/SYMSGF)
     A                                  9 43MSGCON(030 SHU0794 *LIBL/SYMSGF)
     A            WSAI1         30A  B 10  6DSPATR(UL)
     A  27                                  DSPATR(PC)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSR13A        30A  B 10 43COLOR(YLW)
     A            WSAI2         30A  B 11  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSR13B        30A  B 11 43COLOR(YLW)
     A            WSAI3         30A  B 12  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSR13C        30A  B 12 43COLOR(YLW)
     A            WSAI4         30A  B 13  6DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSR13D        30A  B 13 43COLOR(YLW)
     A                                 14  6MSGCON(030 SHU0795 *LIBL/SYMSGF)
     A                                 14 43MSGCON(030 DIV0627 *LIBL/SYMSGF)
     A            WSR12A        30A  B 15  6COLOR(YLW)
     A            WSVL1         30A  B 15 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSR12B        30A  B 16  6COLOR(YLW)
     A            WSVL2         30A  B 16 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSR12C        30A  B 17  6COLOR(YLW)
     A            WSVL3         30A  B 17 43DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 18  6MSGCON(021 JOV0093 *LIBL/SYMSGF)
     A            WSFR           3A  B 18 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSFRFT        26A  B 18 33DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 19  6MSGCON(021 DIV0630 *LIBL/SYMSGF)
     A            WSFT          50A  B 19 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 20  6MSGCON(021 DIV0631 *LIBL/SYMSGF)
     A                                 20 28MSGCON(010 DIV0632 *LIBL/SYMSGF)
     A N32        WSVAL          3A  B 20 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  32        TRETTV         3A  O 20 39DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 45MSGCON(006 DIV0633 *LIBL/SYMSGF)
     A N32        WSBEL          6Y 0B 20 52DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  32        TRETTB        13Y 2O 20 52DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 21  6MSGCON(021 DIV0634 *LIBL/SYMSGF)
     A            WSDT           6Y 0B 21 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 21 39MSGCON(036 DIV0635 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             22 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  29                             22 22MSGCON(018 DIV0114 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23  2MSGCON(018 DIV0636 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  29                             23  2MSGCON(018 DIV0067 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 22MSGCON(018 DIV0030 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  29                             23 22MSGCON(018 DIV0113 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 42MSGCON(018 JOV1332 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  29                             23 42MSGCON(018 DIV0177 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            UMSG          62A  O 24  6COLOR(WHT)
     A                                 22 43MSGCON(020 JOV1331 *LIBL/SYMSGF)
     A            WSMLAP         1A  B 22 64DSPATR(UL)
     A                                      COLOR(YLW)
     A  29                                  DSPATR(PR)
     A                                 22 67MSGCON(010 DIV0259 *LIBL/SYMSGF)
     A N29                             23 62MSGCON(018 DIV0031 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  6'SYEX10B'
     A                                      COLOR(BLU)
     A                                  1 28'CMR'
     A                                      COLOR(WHT)
