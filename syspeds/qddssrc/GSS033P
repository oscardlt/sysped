     A          R GSS033PA
     A            KFMT1         75      1  1
     A            KFMT2         75      2  1
     A            FINM          30A     3 49HIGHLIGHT
     A            FIAD1         30A     4 49
     A            FIAD2         30A     5 49
     A            TRAD1         30A     6  2
     A            FIAD3         30A     6 49
     A            TRAD2         30A     7  2
     A            TRAD3         30A     8  2
     A            TRAD4         30A     9  2
     A            TRAD5         30A    10  2
     A            TRAD6         30A    11  2
     A                                 14 49'DATE'
     A            XIDAG          6  0  14 54EDTCDE(Y)
     A                                 18 12'P R O F O R M A  -  I N V O I C E'
     A                                      CHRSIZ(1.5 3)
     A                                 19 70'PAGE'
     A                                 19 75PAGNBR
     A  50                             20 70'C O P Y'
     A                                      HIGHLIGHT
     A                                 22  1'CL.NO COL/WT  DESCRIPTION/SUPPLIER-
     A                                                 ORDER NO./P.O.N.     AMO-
     A                                      UNT/CURR.'
     A                                 23  1' '
      *
     A          R GSS033PB                  SPACEB(1)
     A            ORCLNF         3  0      1EDTCDE(Z)
     A            ORCLI          5  0      9EDTCDE(Z)
     A            ORDSC         30A       15
     A            XCONO1        20A       46
     A            ORAMT         11  2     67EDTCDE(4)
     A                                      SPACEA(1)
     A N39        ORCLNT         3  0      2EDTCDE(Z)
     A            ORWT           7  1      6EDTCDE(4)
     A            SFNM          30A       15
     A            XCONO2        20A       46
     A            ORCURC         3A       76SPACEA(1)
     A            GDDSC1        30A       15
     A            XCONO3        20A       46SPACEA(1)
     A  35        GDDSC2        30A       15
     A  35        XCONO4        20A       46SPACEA(1)
     A  36        GDDSC3        30A       15
     A  36        XCONO5        20A       46SPACEA(1)
     A  37        GDDSC4        30A       15
     A  37        XCONO6        20A       46SPACEA(1)
      *
     A          R GSS033PC                  SPACEB(1)
     A                                     1'TOTAL'
     A                                      HIGHLIGHT
     A            XTCLI          5  0      9EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                    15'COLLIS'
     A                                      HIGHLIGHT
     A            XTWT           7  1     22EDTCDE(4)
     A                                      HIGHLIGHT
     A                                    31'KG'
     A                                      HIGHLIGHT
     A                                    49'TOTAL INVOICE'
     A                                      HIGHLIGHT
     A            XVAL           3A       63HIGHLIGHT
     A            XTAMT         11  2     67EDTCDE(4)
     A                                      HIGHLIGHT
      *
     A          R GSS033PD
     A                                 63  2'MARKED'
     A            TRAD1         30A    63 10
     A                                 64 10'"IN TRANSIT"'
     A                                 66  2'SENT'
     A            XTDTS          6  0  66  7EDTCDE(Y)
     A                                 66 16'BY'
     A            TRDW          15A    66 19
      *
     A          R GSS033PE                  SPACEB(1)
     A                                     1'TOTAL THIS COLLI'
     A                                      HIGHLIGHT
     A            XTWTC          7  1     22EDTCDE(4)
     A                                      HIGHLIGHT
     A                                    31'KG'
     A                                      HIGHLIGHT
     A                                    49'        TOTAL'
     A                                      HIGHLIGHT
     A            XVALC          3A       63HIGHLIGHT
     A            XTAMTC        11  2     67EDTCDE(4)
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
      *
     A          R GSS033PF
     A                                 66 70'CONTINUOUS'
     A            KFMT3         75     66  1
      *
     A          R GSS033PG
     A                                    70'CONTINUOUS'
     A            KFMT3         75         1
     A                                      SPACEA(2)
      *
     A          R GSS033PH
     A            GDDSCX        30A       15SPACEA(1)
      *
     A          R GSS033PI
     A            XXFT          30A       15SPACEA(1)
      *
     A          R GSS033PJ
     A            XCONOX        20A       46SPACEA(1)
