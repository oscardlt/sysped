N01   * PRIS=0 GODTAS HVIS BEKREFTET MED F23
N02   * EDTCDE(4) PÅ PRISER I STEDET FOR EDTWRD
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
SV5  A*           FEDAY     R        H      REFFLD(FERRR/FEDAY *LIBL/FERR)
SV5  A*           FEMONT    R        H      REFFLD(FERRR/FEMONT *LIBL/FERR)
SV5  A*           FEYEAR    R        H      REFFLD(FERRR/FEYEAR *LIBL/FERR)
NV5  A            FEDATE    R        H      REFFLD(FERRR/FEDATE *LIBL/FERR)
SV5  A*           FEDAY2    R        H      REFFLD(TUFER/FEDAY2 *LIBL/TUFE)
SV5  A*           FEMON2    R        H      REFFLD(TUFER/FEMON2 *LIBL/TUFE)
SV5  A*           FEYEA2    R        H      REFFLD(TUFER/FEYEA2 *LIBL/TUFE)
NV5  A            FEDAT2    R        H      REFFLD(TUFER/FEDAT2 *LIBL/TUFE)
     A            WSFIRM    R        H      REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4')
     A  90                                  DSPATR(ND)
     A  90                                  DSPATR(PR)
     A            WSDATE         6Y 0B  6  6EDTCDE(4)
     A            WSTIME         4Y 0B  6 13EDTCDE(4)
     A            FEFROM    R        O  6 18REFFLD(FERRR/FEFROM *LIBL/FERR)
     A            FETO      R        O  6 24REFFLD(FERRR/FETO *LIBL/FERR)
     A            FELENG    R        O  6 30REFFLD(FERRR/FELENG *LIBL/FERR)
     A            FENAME        17A  O  6 34
     A            FEBILN    R        O  6 52REFFLD(TUFER/FEBILN *LIBL/TUFE)
     A            FEOPKO    R        O  6 78REFFLD(TUFER/FEOPKO *LIBL/TUFE)
     A                                      COLOR(WHT)
     A            FECURR    R        O  7 46REFFLD(TUFER/FECURR *LIBL/TUFE)
     A  97                                  DSPATR(ND)
     A            WSPRI1    R        B  7 50REFFLD(FERRR/FEPRI1 *LIBL/FERR)
S02  A*                                     EDTWRD('           .  ')
N02  A                                      EDTCDE(4)
     A  97                                  DSPATR(ND)
     A            WSPRI2    R        B  7 65REFFLD(FERRR/FEPRI2 *LIBL/FERR)
S02  A*                                     EDTWRD('           .  ')
N02  A                                      EDTCDE(4)
     A  97                                  DSPATR(ND)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTR17A'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 DIV3786 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            FETUR          8S 0O  2  2COLOR(WHT)
     A            VMNAVN    R        O  2 11REFFLD(TRANR/VMNAVN *LIBL/TRAN)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV3787 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 35MSGCON(040 DIV4016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3785 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
N01  A                                      CF23(23 'BEKREFTE 0-PRIS')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV3788 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(020 DIV2416 *LIBL/SYMSGF)
     A            W2OPKO         1A  B  2 24
     A            W2BILN        10A  B  2 26
     A            W2DATE         6  0B  2 37
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYTR17A'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 DIV3786 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            FETUR          8S 0O  2  2COLOR(WHT)
     A            VMNAVN    R        O  2 11REFFLD(TRANR/VMNAVN *LIBL/TRAN)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV3787 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3785 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV3788 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
