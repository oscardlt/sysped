     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
     A                                  1  2'SYSPED/SYXML12'
     A                                      COLOR(BLU)
     A                                  1 20'Spørring på logg (XML-POSTEN)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  7 10'Velg søkekriterium'
     A                                      COLOR(WHT)
     A                                  9 10'1. Status:'
     A            WSST           1A  B  9 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'C' 'M' 'N' 'X')
     A                                  9 50'C=Sendt og ikke mottatt'
     A                                 10 50'X=Ikke sendt'
     A                                 11 50'M=Mottatt, OK'
     A                                 12 50'N=Mottatt, ikke OK'
     A                                 14 10'2. Fra og med dato:'
     A            WSDT           6Y 0B 14 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 50'(DDMMÅÅ)'
     A                                 16 10'3. Fra og med faktura:'
     A            WSFN           7Y 0B 16 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 23 20'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '1')
     A            XFFN           7Y 0O  5  5EDTCDE(4)
     A            WXDTS          6Y 0O  5 13EDTCDE(Y)
     A            WXTDS          4Y 0O  5 22EDTWRD('  :  ')
     A            XREFS         10A  O  5 28
     A            WXDTM          6Y 0O  5 39EDTCDE(Y)
     A            WXTMM          4Y 0O  5 48EDTWRD('  :  ')
     A            XREFM         14A  O  5 54
     A            WXST           8A  O  5 69
     A            XFST           1A  H
     A            WX0004        35A  H
     A            WX0010        35A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA08(08 'Søk')
     A                                      CA12(12 'Søk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYXML12'
     A                                      COLOR(BLU)
     A                                  1 20'Spørring på logg (XML-POSTEN)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Velg opsjon:   1=Manuelt kvitt'
     A                                      COLOR(BLU)
     A                                  3  2'Op Fakt.nr Send.dato/tid  Send.ref-
     A                                      .  Mott.dato/tid  Kvitt.ref.     St-
     A                                      atus'
     A                                      COLOR(WHT)
     A            W2FN           7Y 0B  4  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2DT           6Y 0B  4 13DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4 23'Status:'
     A            W2ST           1A  B  4 31DSPATR(UL)
     A                                      VALUES(' ' 'C' 'M' 'N' 'X')
     A                                  4 33'(C/M/N/X)'
      *
     A          R BUNN
     A                                 23  2'F3=Avslutt  F5=Frisk  F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA08(08 'Søk')
     A                                      CA12(12 'Søk')
     A                                  1  2'SYSPED/SYXML12'
     A                                      COLOR(BLU)
     A                                  1 20'Spørring på logg (XML-POSTEN)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Velg opsjon:   1=Manuelt kvitt'
     A                                      COLOR(BLU)
     A                                  3  2'Op Fakt.nr Send.dato/tid  Send.ref-
     A                                      .  Mott.dato/tid  Kvitt.ref.     St-
     A                                      atus'
     A                                      COLOR(WHT)
     A            W2FN           7Y 0B  4  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2DT           6Y 0B  4 13DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4 23'Status:'
     A            W2ST           1A  B  4 31DSPATR(UL)
     A                                      VALUES(' ' 'C' 'M' 'N' 'X')
     A                                  4 33'(C/M/N/X)'
     A                                 23  2'F3=Avslutt  F5=Frisk  F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
