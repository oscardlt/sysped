     A*%%TS  SD  20150903  183149  CB          REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 CB  PTF10 PKI, IKKE MERKET
11084A* 02 CB  PTF11 UPPLÆGGNIGSANMÆLAN
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX060PNL)
     A                                      HLPTITLE('REGISTRERING AV TET IMPOR-
     A                                      T')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Sök kunde')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVX060R'
     A                                      COLOR(BLU)
     A                                  1 29'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4 13'Avdelning'
     A            WSAAVD         4Y 0B  4 23TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet avdeling er ikke reg-
     A                                      istrert.' 99)
     A            KOANVN        33A  O  4 30TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20'Välj sökväg med startpunkt '
     A                                      COLOR(WHT)
     A                                  9 20'Datum'
     A            WSDT           8Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  98                                  ERRMSG('Du må velge et av de startp-
     A                                      unktene.' 98)
     A                                 10 20'Ärende     '
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Kundnr. '
     A            WSKN           8Y 0B 11 35TEXT('Kundenr')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 48'Dato:'
     A            WSDTN          8Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20'Signatur '
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 48'Dato:'
     A            WSDTT          8Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20'MRN-nr. '
     A            WSTRNR        18A  B 14 35TEXT('Tollnr Löpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 15 20'Godsnr. '
     A            WSGN          35A  B 15 35TEXT('Tollnr Löpenr')
     A                                      DSPATR(UL)
     A  47                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 16 20MSGCON(014 YBC0053 *LIBL/SYMSGF)
     A            WSST           1A  B 16 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 23 20'F3 = Avsluta'
     A                                      COLOR(BLU)
     A                                 23 36'F4 = Sök kund '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            TIAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A  29                                  COLOR(RED)
     A            TITDN          7Y 0O  6 10TEXT('Oppdragsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A            TISG           3A  O  6 18TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            WXDT           6S 0O  6 22TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
     A            TITRNR        18A  O  6 29TEXT('Transitnr')
     A  46                                  COLOR(TRQ)
     A            TIST           1A  O  6 48TEXT('Status')
     A N49                                  COLOR(RED)
     A  49                                  COLOR(TRQ)
     A            TIENKL         1A  O  6 50
     A N28                                  COLOR(YLW)
     A  28                                  COLOR(RED)
     A            TILSTL         4  0H
     A            TINA          30A  H
     A            WXNA           7A  O  6 52
     A  43                                  COLOR(TRQ)
     A            TIGN          35A  H
     A            WXGN          13A  O  6 60
     A  47                                  COLOR(TRQ)
     A            WXDTF          6S 0O  6 74TEXT('FRIGIVELSESDATO')
     A            TIDTF          8  0H
     A            TINAK         30A  H
     A            TITAID        27A  H
     A            NINTK          7  0H
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20150903  183149  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Sök')
     A                                      CA12(12 'Forrige')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SVX060010 SVX060PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SVX060020 SVX060PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SVX060030 SVX060PNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(SVX060040 SVX060PNL)
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPPNLGRP(SVX060030 SVX060PNL)
     A                                      HLPARA(05 001 05 003)
     A          H                           HLPPNLGRP(SVX060055 SVX060PNL)
     A                                      HLPARA(05 027 05 044)
     A          H                           HLPPNLGRP(SVX060060 SVX060PNL)
     A                                      HLPARA(05 046 05 046)
     A          H                           HLPPNLGRP(SVX060070 SVX060PNL)
     A                                      HLPARA(05 048 05 048)
     A          H                           HLPPNLGRP(SVX060080 SVX060PNL)
     A                                      HLPARA(07 001 23 080)
     A                                  1  2'SYSPED/SVX060R'
     A                                      COLOR(BLU)
     A                                  1 25'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2'Avdelning'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'1=Skapa ny 2=Ändra 3=Kopiera 8=Web-
     A                                       9=Logg 44/45=Lossmark 99=Märke 11=-
     A                                      Skicka'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Ärende  Sig .Datum MRN-nr -
     A                                                  S T Ansvar  Godsnummer -
     A                                         F.dat '
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRNR        18A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 48DSPATR(UL)
     A            W2GN          15A  B  5 60DSPATR(UL)
     A                                  3 79'16=Skriv Upplæggningsanmælan'
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2'F3=Avsluta   F5=Uppdatera   F6=Ska-
     A                                      pa ny   F8=Sök'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Sök')
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVX060R'
     A                                      COLOR(BLU)
     A                                  1 29'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Avdelning'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                  3  2'1=Ny 2=Ändra 3=Kopiera 5=Visa 8=We-
     A                                      b 9=Logg 44/45=Lossmark 99=Märke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Ärende  Sig Datum  MRN-nr -
     A                                                  S T Ansvar  Godsnummer -
     A                                      d  F.dat '
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRNR        18A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 48DSPATR(UL)
     A            W2GN          15A  B  5 60DSPATR(UL)
     A                                 23  2'F3=Avsluta   F5=Uppdtarea   F6=Ska-
     A                                      pa ny   F8=Sök'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 9 62 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'SVX060R'
     A                                      COLOR(BLU)
     A                                  1 10'Ändra status NCTS Imp  '
     A                                      COLOR(WHT)
     A            TIAVD          4Y 0O  1 35TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            TITDN          7Y 0O  1 40TEXT('Oppdragsnr')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            TISG           3A  O  1 48TEXT('Signatur')
     A                                      COLOR(TRQ)
     A                                  2  2'Status:'
     A            WAST           1A  B  2 11DSPATR(UL)
     A                                  2 14'Signatur'
     A            WAUSER        10A  O  2 23DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 36'Datum'
     A            WADT           8Y 0O  2 43DSPATR(UL)
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'Orsak:  '
     A            WAFT1         50A  B  3 11DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            WAFT2         50A  B  4 11DSPATR(UL)
     A            WAFT3         50A  B  5 11DSPATR(UL)
     A            WAFT4         50A  B  6 11DSPATR(UL)
     A            WAFT5         50A  B  7 11DSPATR(UL)
     A  50                              8  2'Uppd.godsjournal?'
     A  50        WAOPGJ         1A  B  8 20DSPATR(UL)
     A  50                              8 22'(J/N)'
     A N30                              9  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O  9  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE03
     A                                      WINDOW(*DFT 7 52 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'SVX060R'
     A                                      COLOR(BLU)
     A                                  1 10'Uppdatera godsjournal'
     A                                      COLOR(WHT)
     A            TIAVD          4Y 0O  1 35TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            TITDN          7Y 0O  1 40TEXT('Oppdragsnr')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            TISG           3A  O  1 48TEXT('Signatur')
     A                                      COLOR(TRQ)
     A                                  2  2'Gr.pass:'
     A            WBGREN        20A  B  2 11DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A                                  3  2'Avs. DT:'
     A            WBTRDT         8Y 0B  3 11DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(HI)
     A                                      EDTCDE(Y)
     A                                  4  2'Bilnr..:'
     A            WBBILN        13A  B  4 11DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(HI)
     A                                  5  2'Mottag.:'
     A            WBMOTT        15A  B  5 11DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(HI)
     A                                  6  2'Konform:'
     A            WBKONF         1A  B  6 11DSPATR(UL)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(HI)
     A                                      VALUES(' ' '0' '1')
     A                                  6 14'1=Konform, 0=Ej konform  '
     A N30                              7  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
