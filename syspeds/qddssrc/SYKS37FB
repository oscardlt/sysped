     A*  FAKTURANUMMER 7   LANG
N02  A*  F10 = KJØR TIL TROS FOR AT VARSEL ER GITT
N02  A*  F10 = IKKE TILLATT NÅR 54 IN (STATUS BLANK ..)
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE
     A                                      CF03(03 'Avbryt')
     A N52                                  CF05(05 'KJØR')
N02  A N52                                  CF10(10 'KJØR ANYWAY')
     A                                      OVERLAY
     A                                  1  4'SYSPED/SYKS37B'
     A                                      COLOR(BLU)
     A                                  1 20'Manuell kjøring av EDI Internfaktu-
     A                                      ra           '
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  3  4'Gi fakturanr og tast enter.       -
     A                                                                   '
     A                                  4  4'Systemet sjekker da gyldighet, fin-
     A                                      ner kundens EDI-parametre m.m.'
     A                                  7  4'Fakturanr..........:'
SF7  A*           WSFAKT         6Y 0B  7 29EDTCDE(4)
NF7  A            WSFAKT         7Y 0B  7 29EDTCDE(4)
     A                                      DSPATR(PC)
S02  A* 51                                  ERRMSG('Fakt-nr tilhører IKKE kunde-
S02  A*                                      som kan motta int-EDI' 51)
     A            AVRTXT        20   O  8  4COLOR(WHT)
     A            VMTRAN         8Y 0O  8 27COLOR(WHT)
     A                                      EDTCDE(4)
     A            VMNAVN        30   O  8 36COLOR(WHT)
     A                                 10  4'Statuskode.........:'
     A            WSOPKO         1A  B 10 34DSPATR(HI)
     A                                      DSPATR(PR)
     A                                 11  4'Kundenr............:'
     A            WSKUNR         8Y 0B 11 27EDTCDE(4)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A                                 13  4'EDI-senders ID.....:'
     A            WSSEID        35A  B 13 27DSPATR(HI)
     A                                      DSPATR(PR)
     A                                 14  4'EDI-mottakers ID...:'
     A            WSMOID        35A  B 14 27DSPATR(HI)
     A                                      DSPATR(PR)
     A  50                             18  4'EDI Internfaktura er kjørt ......'
     A                                      COLOR(WHT)
     A  50                             19  4'Fakturanr'
SF7  A* 50        FAFAKT         6Y 0B 19 15EDTCDE(4)
NF7  A  50        FAFAKT         7Y 0B 19 15EDTCDE(4)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A  50                             19 23'Kunde'
     A  50        FAKUNR         8Y 0B 19 31EDTCDE(4)
     A                                      COLOR(WHT)
N02  A  52                             21  4'                                 '
N02  A N52 53                          21  4'Denne ER ALT SENDT !!!(F10=OMKJ.)'
N02  A                                      COLOR(RED)
N02  A N52 54                          21  4'Denne LIGGER ALT KLAR I FILA!!   '
N02  A                                      COLOR(RED)
     A                                 23  4'F3=Avslutt'
     A                                      COLOR(BLU)
S02  A*N52                             23 26'F5=Send EDI Internfaktura'
N03  A N54
     A N52N53                          23 26'F5=Send EDI Internfaktura'
     A                                      COLOR(BLU)
N02  A  52                             23 26'                         '
N02  A N52 53                          23 26'F10=OMSEND! Internfaktura'
N02  A                                      COLOR(RED)
N03  A N52 54                          23 26'(Sendes først ved F3.)   '
N03  A                                      COLOR(WHT)
N02  A  30        MSGTXT        77A  O 24  2MSGID(&MSGNR SYMSGF)
N02  A                                      COLOR(WHT)
N02  A            MSGNR          7A  P
