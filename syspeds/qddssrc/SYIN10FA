     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/SAMLA)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 11 69 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '1' '5')
     A            SMSD           3   H
     A            SMLNR          3Y 0H
EV5  A            SMDT           8Y 0H
     A            SMSDF          3   O  6  5
     A            WSDT           6Y 0O  6  9EDTCDE(4)
     A            SMKL           4D 0O  6 16
     A            SMKNF          3  0O  6 21EDTCDE(4)
     A            SMKNS          8  0O  6 25EDTCDE(4)
     A            SMROA1         3   O  6 34
     A            SMROB1         6   O  6 38
     A            SMROC1         2D 0O  6 45
     A            SMROA3         3   H
     A            SMROB3         6   H
     A            SMROC3         2  0H
     A            SMVKT1         5Y 0H
     A            SMPRO          8  0H
     A            SMVKT2         5Y 0O  6 48EDTCDE(4)
     A            SMVKT3         5  0H
     A            SMMAWB        11D 0O  6 54
     A            SMNT           3  0O  6 66EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'NY SAMLAST')
     A                                      CA12(12 'FORRIGE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 55SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYIN10A'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(045 DIV1130 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 55DATE
     A                                      EDTCDE(Y)
     A                                  2 55TIME
     A                                  2  2MSGCON(004 DIV1131 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2  7EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 12MSGCON(004 DIV1132 *LIBL/SYMSGF)
     A            UOPD           7Y 0O  2 17EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 26MSGCON(004 DIV1133 *LIBL/SYMSGF)
     A            USG            3A  O  2 30COLOR(WHT)
     A            USDT           3A  B  2 42COLOR(WHT)
     A                                  3  2MSGCON(060 YBC0499 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(067 YBC0497 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2DT           6Y 0B  5  9DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KL           4Y 0B  5 16DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KNF          3Y 0B  5 21DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KNS          8Y 0B  5 25DSPATR(UL)
     A                                      EDTCDE(4)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(11)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(60 YBC0498 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        66   O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'NY SAMLAST')
     A                                      CA12(12 'FORRIGE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYIN10A'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(045 DIV1130 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 55DATE
     A                                      EDTCDE(Y)
     A                                  2 55TIME
     A                                  2  2MSGCON(004 DIV1131 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2  7EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 12MSGCON(004 DIV1132 *LIBL/SYMSGF)
     A            UOPD           7Y 0O  2 17EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 25MSGCON(004 DIV1133 *LIBL/SYMSGF)
     A            USG            3A  O  2 30COLOR(WHT)
     A            USDT           3A  B  2 42COLOR(WHT)
     A                                  3  2MSGCON(060 YBC0499 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(067 YBC0497 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2DT           6Y 0B  5  9DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KL           4Y 0B  5 16DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KNF          3Y 0B  5 21DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KNS          8Y 0B  5 25DSPATR(UL)
     A                                      EDTCDE(4)
     A N30                             11  2MSGCON(60 YBC0498 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        66   O 11  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
