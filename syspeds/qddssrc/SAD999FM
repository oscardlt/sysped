     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      HELP(01)
     A                                      CF01(01)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' 'A' 'I' 'V')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            SDTNRF         8S 0O  5  5
     A            TAALFA        15A  O  5 14
     A            SDKDAÆ         2A  O  5 30
     A            SDKDSÆ         3A  O  5 35
     A            SDHKOD         2Y 0H
     A            SDVF           1A  H
     A            SDTRM          2Y 0H
     A            SDDTF          8Y 0H
     A            WXDTF          8Y 0O  5 42EDTWRD('0 .  .    ')
     A            SDDTT          8Y 0H
     A            WXDTT          8Y 0O  5 53EDTWRD('0 .  .    ')
     A            SDBLSÆ         7Y 2O  5 64EDTCDE(4)
     A            SDAKTK         1A  H
     A            SDST           7A  O  5 73
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD999'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 TRO0145 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A                                  3  2'Velg opsjon:   2=Endre   A=Aktiver-
     A                                         I=Gjør inaktiv   V=Valgfri'
     A                                      COLOR(BLU)
     A                                  4  2'Op Tariffnr Beskrivelse     Avg. S-
     A                                      ekv.  F.o.m.dato Til dato       Sat-
     A                                      s Status'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=Avslutt   F5=Forny   F6=Lag   F-
     A                                      8=Søk   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Tariffnr Beskrivelse     Avg. S-
     A                                      ekv.  F.o.m.dato Til dato       Sat-
     A                                      s'
     A                                      COLOR(WHT)
     A            WSTNRF         8Y 0B  2  5EDTCDE(4)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSKDAÆ         2A  B  2 30
     A  89                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSKDSÆ         3A  B  2 35
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSHKOD         2Y 0H
     A            WSVF           1A  H
     A            WSTRM          2Y 0H
     A            WSDTF          8Y 0B  2 42EDTCDE(4)
     A  88                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSDTT          8Y 0B  2 53EDTCDE(4)
     A  87                                  DSPATR(RI)
     A            WSBLSÆ         7Y 2B  2 64DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            W2TNRF         8S 0I  2 27DSPATR(UL)
     A                                  2 17MSGCON(009 TRO0212 *LIBL/SYMSGF)
     A                                  2 38MSGCON(012 TRO0208 *LIBL/SYMSGF)
     A            W2KDAÆ         2   I  2 52
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD999'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 TRO0145 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A                                  4  2'Op Tariffnr Beskrivelse     Avg.KD-
     A                                       Sekvens  Fom.dato Tildato      Sat-
     A                                      s Status'
     A                                      COLOR(WHT)
