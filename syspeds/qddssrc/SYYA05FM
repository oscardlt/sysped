********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK KUNDE')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A            MSGF01        22A  O  2 58MSGID(DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A            MSGF02        40A  O  4 22MSGID(DIV1276 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGF03        45A  O  5 22MSGID(DIV1277 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF04        15A  O  9 22MSGID(DIV0757 SYMSGF)
     A            WSKNF          8Y 0B  9 38DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSGF05        25A  O  9 48MSGID(DIV1278 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF06        15A  O 11 22MSGID(DIV0759 SYMSGF)
     A            WSKNT          8Y 0B 11 38DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSGF10        15A  O 15 22MSGID(DIV0763 SYMSGF)
     A            WSVALG         1A  B 15 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'U' 'G')
     A            MSGF11        30A  O 15 48MSGID(DIV0764 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF12        30A  O 16 48MSGID(DIV0765 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF13        30A  O 17 48MSGID(DIV0766 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF14        15A  O 19 22MSGID(DIV0767 SYMSGF)
     A            WSDT           6Y 0B 19 38DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            MSGF15        30A  O 19 48MSGID(DIV0768 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF16        18A  O 23 22MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF17        18A  O 23 42MSGID(DIV0636 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
     A            MSGNR          7A  P
