      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TRANSITERINGSNR.TELLER    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-84 95-97
      ***********************************************************
      *
     FSADTRTB   UF A E           K DISK
     FFIRM      IF   E           K DISK
     FSAD044FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  STTDS                  1     19
     D  XDLT                   1      7
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     ' '           SETLL     SADTRTB
     C     START1        TAG
     C                   READ(N)   SADTRTB                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        100
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   SADTRTB                                94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   EXSR      MOVB2
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     6             POSNBR
     C                   GOTO      START5
     C                   END
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000000'      *IN(85)
     C                   SETOFF                                           30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSTYP         COMP      *BLANKS                                90
     C   90              MOVE      'YBC0161'     MSGNR
     C   90              SETON                                        30
     C  N90WSTYP         CHAIN(N)  SADTRTB                            33
     C  N90
     CANN33              DO
     C                   MOVE      'YBC0162'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     22            LINNBR
     C   90              Z-ADD     6             POSNBR
     C                   END
      *
     C     WSTNR         IFEQ      0
     C  N30              MOVE      'YBC0163'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     11            POSNBR
     C                   SETON                                        8930
     C                   END
      *
     C     WSDS          IFEQ      *BLANKS
     C  N30              MOVE      'YBC0164'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     20            POSNBR
     C                   SETON                                        8830
     C                   END
      *
     C     WSNV          IFEQ      *BLANKS
     C  N30              MOVE      'YBC0166'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     43            POSNBR
     C                   SETON                                        8730
     C                   END
      *
     C     WSAN          IFEQ      *BLANKS
     C  N30              MOVE      'YBC0167'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     66            POSNBR
     C                   SETON                                        8630
     C                   END
      *
     C     WSLK          IFEQ      *BLANKS
     C  N30              MOVE      'YBC0168'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     77            POSNBR
     C                   SETON                                        8530
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSTYP         CHAIN     SADTRTB                            33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWTR
     C  N33              UPDATE    SADTRTR
     C   33              WRITE     SADTRTR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGTR
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTTR
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTR         BEGSR
     C***********************************************
     C                   EXSR      MOVTRW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000000'      *IN(85)
     C                   SETOFF                                         30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTTR         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     STTTYP        CHAIN     SADTRTB                            33
     C  N33              DELETE    SADTRTR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWTR        BEGSR
     C***********************************************
     C                   MOVE      WSTYP         STTTYP
     C                   MOVE      WSTNR         STTNR
     C                   MOVE      WSDS          STTDS
     C                   MOVE      WSNV          STTNV
     C                   MOVE      WSAN          STTAN
     C                   MOVE      WSLK          STTLK
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVTRW        BEGSR
     C***********************************************
     C                   MOVE      STTTYP        WSTYP
     C                   MOVE      STTNR         WSTNR
     C                   MOVE      STTDS         WSDS
     C                   MOVE      STTNV         WSNV
     C                   MOVE      STTAN         WSAN
     C                   MOVE      STTLK         WSLK
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     11            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       STTTYP
     C                   MOVE      *ZEROS        STTNR
     C                   MOVE      *BLANKS       STTDS
     C                   MOVE      *BLANKS       STTNV
     C                   MOVE      *BLANKS       STTAN
     C                   MOVE      *BLANKS       STTLK
     C                   MOVE      '*DELETE'     XDLT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSTYP
     C                   MOVE      *ZEROS        WSTNR
     C                   MOVE      *BLANKS       WSDS
     C                   MOVE      *BLANKS       WSNV
     C                   MOVE      *BLANKS       WSAN
     C                   MOVE      FILAND        WSLK
      *
     C                   ENDSR
      *
