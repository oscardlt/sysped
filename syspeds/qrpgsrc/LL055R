      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED LEGERLEIER                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FLLGRPK01  IF   E           K DISK
     FLLAGJL02  IF   E           K DISK
     FLLGSFL04  IF   E           K DISK
     FLLGSFL05  IF   E           K DISK
     F                                     RENAME(LLGSFR:LLGSFR5)
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FHEADF     UF   E           K DISK
     FLL055D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  GSFDT                  1      8  0
     D  GSFDT≈                 1      4  0
     D  GSFDTM                 5      6  0
     D  GSFDTD                 7      8  0
     D                 DS
     D  WSFDT                  1      6  0
     D  WSFDTD                 1      2  0
     D  WSFDTM                 3      4  0
     D  WSFDT≈                 5      6  0
     D                 DS
     D  GS≈≈MU                 1      6  0
     D  GS≈MU≈                 1      4  0
     D  GS≈MUM                 5      6  0
     D                 DS
     D  WS≈≈MU                 1      4  0
     D  WS≈MUM                 1      2  0
     D  WS≈MU≈                 3      4  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   MOVE      *ZEROS        GKGRP
     C     GKGRP         SETLL     LLGRPK01
     C                   READ      LLGRPK01                               94
     C     *IN94         DOWEQ     '0'
     C     GKPNR         IFNE      0
     C                   EXSR      GETINF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   READ      LLGRPK01                               94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     GETINF        BEGSR
      ***********************************************
     C     CUND1K        CHAIN     CUNDL01                            33
      *
     C                   MOVE      *ZEROS        WSFN
     C                   MOVE      *ZEROS        WSFDT
     C                   MOVE      *ZEROS        WS≈≈MU
     C                   MOVE      ' '           WSMMUU
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      USG           WSSG
     C                   MOVE      'NEI'         WSSTAT
      * HENT SISTE FAKTURANR, DATO, PERIODE, ...
     C                   MOVE      999999        GS≈≈MU
     C     GS≈≈MU        SETLL     LLGSFL04
     C                   READP     LLGSFL04                               33
     C     *IN33         DOWEQ     '0'
     C     HEADFK        CHAIN(N)  HEADF                              34
     C  N34HEKNKF        IFEQ      GKPNR
     C     HEKNSF        OREQ      GKPNR
     C                   MOVE      HESG          WSSG
     C                   MOVE      GSFN          WSFN
     C                   MOVE      GSFDTD        WSFDTD
     C                   MOVE      GSFDTM        WSFDTM
     C                   MOVE      GSFDT≈        WSFDT≈
     C                   MOVE      GS≈MU≈        WS≈MU≈
     C                   MOVE      GS≈MUM        WS≈MUM
     C                   MOVE      GSMMUU        WSMMUU
     C                   MOVE      GSAVD         WSAVD
     C                   MOVE      GSOPD         WSOPD
     C                   MOVE      'NP '         WSSTAT
     C                   LEAVE
     C                   END
     C                   READP     LLGSFL04                               33
     C  N33              ENDDO
      * HENT EVT. OPPDRAGSNR
     C                   MOVE      999999        GS≈≈MU
     C     GS≈≈MU        SETLL     LLGSFL05
     C                   READP     LLGSFL05                               33
     C     *IN33         DOWEQ     '0'
     C     HEADFK        CHAIN(N)  HEADF                              34
     C  N34HEKNKF        IFEQ      GKPNR
     C     HEKNSF        OREQ      GKPNR
     C                   MOVE      HESG          WSSG
     C                   MOVE      GSMMUU        WSMMUU
     C                   MOVE      GSAVD         WSAVD
     C                   MOVE      GSOPD         WSOPD
     C                   MOVE      'DP '         WSSTAT
     C                   LEAVE
     C                   END
     C                   READP     LLGSFL05                               33
     C  N33              ENDDO
      * TEST OM DET FINNES MINST EN LINJE
     C     WSSTAT        IFNE      'DP '
     C     LAGS2K        SETLL     LLAGJL02
     C     LAGS2K        READE     LLAGJL02                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'NP '         WSSTAT
     C                   ELSE
     C                   MOVE      'NEI'         WSSTAT
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     GETOPT        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   SELECT
     C     WSNR          WHENEQ    '1'
     C     WSSTAT        ANDNE     'NEI'
     C                   EXSR      WRKLL
     C                   ENDSL
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       3             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     WRKLL         BEGSR
     C***********************************************
     C     WSSTAT        IFEQ      'NP '
     C                   MOVE      'CR1'         UTYPE             3
     C                   MOVE      *ZEROS        UOPD2             7 0
     C                   MOVE      USG           USG2              3
     C                   ELSE
     C                   MOVE      'CHG'         UTYPE
     C                   MOVE      WSOPD         UOPD2
     C                   MOVE      WSSG          USG2
     C                   END
     C                   MOVE      WSAVD         UAVD2             4 0
     C                   MOVE      ' '           UKODE             1
     C                   MOVE      ' '           UDUMMY            1
     C                   MOVE      *ZEROS        UAVDNY            4 0
      *
     C     WSSTAT        IFEQ      'DP '
     C     HEADOK        CHAIN     HEADF                              33
     C                   MOVE      HEST          XST               1
     C     HEST          IFNE      'C'
     C     HEST          ANDNE     'U'
     C                   MOVE      ' '           HEST
     C                   END
     C                   EXCEPT    NYOPDS
     C                   END
      *
     C                   CALL      'SYFA10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE             1
     C                   PARM                    UDUMMY            1
     C                   PARM                    UAVDNY
      *
     C     WSSTAT        IFEQ      'DP '
     C     HEADOK        CHAIN     HEADF                              33
     C     XST           IFEQ      'U'
     C     HEOT          ANDNE     *BLANKS
     C                   MOVE      ' '           HEST
     C                   ELSE
     C                   MOVE      XST           HEST
     C                   END
     C                   EXCEPT    NYOPDS
     C                   END
      * HENT EVT. OPPDRAGSNR
     C     WSSTAT        IFEQ      'NP '
     C                   MOVE      999999        GS≈≈MU
     C                   MOVE      GKPNR         HEKNKF
     C     GS≈≈MU        SETLL     LLGSFL05
     C                   READP     LLGSFL05                               33
     C     *IN33         DOWEQ     '0'
     C     HEKNKF        IFEQ      GKPNR
     C                   MOVE      HESG          WSSG
     C                   MOVE      GSMMUU        WSMMUU
     C                   MOVE      GSAVD         WSAVD
     C                   MOVE      GSOPD         WSOPD
     C                   MOVE      'DP '         WSSTAT
     C                   LEAVE
     C                   END
     C                   READP     LLGSFL05                               33
     C  N33              ENDDO
     C                   END
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    GKPNR
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    UAVD
     C     HEADFK        KLIST
     C                   KFLD                    GSAVD
     C                   KFLD                    GSOPD
     C     HEADOK        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     LAGS2K        KLIST
     C                   KFLD                    GKGRP
     C                   KFLD                    GKPNR
      *
     C                   MOVE      'A'           KOAUNI
     C     KODTAK        CHAIN     KODTA                              33
      *
     C                   ENDSR
      *
     OHEADFR    E            NYOPDS
     O                       HEST
