      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJSYFRAL MODULE(*LIBL/TJSYFRAL *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FKODTOTY   IF   E           K DISK    USROPN
     FKUFAST    IF   E           k DISK    USROPN
     Favfrh     IF   E           k DISK    USROPN
     Favfrhl05  IF   E           k DISK    USROPN
     F                                     RENAME(AVFRHR:AVFRH05R)
     FKodtfr    IF   E           K DISK    USROPN
     Fcundl01   IF   E           K DISK    USROPN
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D lng             S              2
     D UsrSpcName      S             10
     D WSUSER          S             10
     D AVVKNR          S              8
     D AVVKNN          S              8  0
     D AVVKNTXT        S            500
     D OVERSKR         S            150
     D WSOT            S              2
     D FRALK           S              2
     D FRA             S              5
     D TILLK           S              2
     D TIL             S              5
     D VKT             S              5
     D ANT             S              5
     D LM              S              5
     D M3              S              5
     D PROD            S              1
     D FRAN            S              3
     D FRANKOD         S              3
     D FRANTXT         S             28
     D PRODKOD         S              1
     D WSAVD1          S              7
     D WSAVD2          S             45
     D WSOT1           S              2
     D WSOT2           S             29
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0

     D Lib             S             10
     D Fn              S             10
     D Mbr             S             10

     D                 DS
     D  FrString               1     48                                         Frankaturer
     D  FR                     1     48    DIM(12)
     D                 DS
     D  OtString               1     48                                         Oppdragstyper
     D  OT                     1     48    DIM(16)
     D                 DS
     D  PrString               1     50                                         Prod-kode
     D  PR                     1     50    DIM(25)
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA1                1      2
     D  PAVRDA2                5      5
     D  PAVRDA3                6     50
     D                 DS
     D  WSAVD                  1      7
     D  WSAVDMOD               1      2
     D  WSAVDSK                3      3
     D  WSAVDAVD               4      7
     D                 DS
     D  XAVD                   1      7
     D  XAVDMOD                1      2
     D  XAVDSK                 3      3
     D  XAVDAVD                4      7  0
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   EXSR      Parse

     C                   EXSR      INIT

      * Blank passord=anonym, OK, Ellers må være "Inne"
     C     BIPO          IFNE      *BLANKS
     C     ERROR         ANDEQ     *BLANKS
     C                   IF        ((*IN50 = *ON) OR
     C                              (BIINN = *BLANKS))
     C*                  EVAL      ERROR='Not allowed'
     C                   END
     C                   END
     c     Error         CABNE     *BLANKS       SLUTT
      * 2-Prod.koder
     C                   EXSR      DspProd

     C     SLUTT         TAG

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬wsuser¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬wsot¬ : ¬'+%trim(WSOT)+'¬, '
        +'¬fra¬ : ¬'+%trim(FRA)+'¬, '
        +'¬fralk¬ : ¬'+%trim(FRALK)+'¬, '
        +'¬til¬ : ¬'+%trim(TIL)+'¬, '
        +'¬tillk¬ : ¬'+%trim(TILLK)+'¬, '
        +'¬vkt¬ : ¬'+%trim(VKT)+'¬, '
        +'¬ant¬ : ¬'+%trim(ANT)+'¬, '
        +'¬lm¬ : ¬'+%trim(LM)+'¬, '
        +'¬m3¬ : ¬'+%trim(M3)+'¬, '
        +'¬prod¬ : ¬'+%trim(PROD)+'¬, '
        +'¬fran¬ : ¬'+%trim(FRAN)+'¬, '
        +'¬avvknr¬ : ¬'+%trim(%editc(AVVKNN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"pricecalclist_levv" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * 3-Frankaturer
     C     BIKUNR        IFNE      *ZEROS
     C                   IF        ((XKUNFRAKT <> 'J') AND (XKUNFRAKT <> 'j'))
     C                   EXSR      DSPMOD
     C                   EXSR      DSPFR
     C                   ELSE
     C                   EXSR      DspFran
     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSAVD   = zhbgetvar('WSAVD')
     C                   EVAL      WSOT    = zhbgetvar('WSOT')
     C                   EVAL      FRA     = zhbgetvar('FRA')
     C                   EVAL      FRALK   = zhbgetvar('FRALK')
     C                   EVAL      TIL     = zhbgetvar('TIL')
     C                   EVAL      TILLK   = zhbgetvar('TILLK')
     C     FRALK         IFEQ      *blanks
     C                   MOVE      'NO'          FRALK
     C                   END
     C     TILLK         IFEQ      *blanks
     C                   MOVE      'NO'          TILLK
     C                   END
     C                   EVAL      VKT     = zhbgetvar('VKT')
     C                   EVAL      ANT     = zhbgetvar('ANT')
     C                   EVAL      LM      = zhbgetvar('LM')
     C                   EVAL      M3      = zhbgetvar('M3')
     C                   EVAL      PROD    = zhbgetvar('PROD')
     C                   EVAL      FRAN    = zhbgetvar('FRAN')
     C                   EVAL      AVVKNR  = zhbgetvar('AVVKNR')
     C                   EVAL      AVVKNN  = c2n2(AVVKNR)
     C                   ENDSR
      *=====================================================================******
      *  Display Product codes to select from
      *=====================================================================******
     C     DspProd       BEGSR
      * Vis kun de kunden har i param HEPRO i ESPEDPARM..
     C                   MOVE      'HEPRO'       PAID
     C                   EXSR      SETKEY
     C     *in33         IFEQ      '0'
     C                   EXSR      DspProdP
     C                   GOTO      Utprod
     C                   END
      * Vis kun de kunden har i postal pkt 9
     C     KufasKey      SETLL     KUFAST
     C     kufasKey      READE     KUFAST                                 40
     C     *in40         DOWEQ     '0'
     C                   MOVEL     KFKOD         AFPROD
     C     BIKUNR        IFEQ      *ZEROS
     C                   EVAL      *IN33 = *OFF
     C                   ELSE
     C     KuProKey      CHAIN     AVFRH                              33
     C                   END
     C     *in33         IFEQ      '0'
     C     KFKOD         ANDNE     ' DEFN'
     C                   MOVEL     KFTXT         PRODTXT          20
     C                   EVAL      PRODKOD=AfProd
     C                   END
     C     KufasKey      READE     KUFAST                                 40
     C                   ENDDO

     C     UtProd        tag
     C                   ENDSR
      *=====================================================================******
      *  Display Product codes to select from  - NÅR angitt i parameter
      *=====================================================================******
     C     DspProdP      BEGSR
      ****************************************************************************
     C                   MOVE      PAVRDA        PrString

     C     1             DO        25            XN                3 0
     C                   MOVEL     PR(XN)        AFPROD
     C                   IF        ((AFPROD=*BLANKS) and (XN > 1))
     C                   LEAVE
     C                   ENDIF
     C                   EVAL      KFKOD  = AFPROD
     C     KufaskeyK     CHAIN     KUFAST                             33        Kun gyldige koder..
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFTXT         PRODTXT          20

     C                   IF        PROD = AFPROD
     C                   EVAL      PRODKOD=AfProd
     C                   EVAL      PRODTXT=prodtxt
     C                   ENDIF
     C                   ENDIF
     C                   ENDDO

     C                   ENDSR
      *=====================================================================******
      *  Display Frankaturer  codes to select from
      *=====================================================================******
     C     DspFran       BEGSR

     C                   MOVE      ' '           TopFrSkevet       1
     C                   MOVE      *BLANKS       AFFRAN
     C     Avfr05Key     CHAIN     AVFRHL05                           33
     C     Avfr05Key     SETGT     AVFRHL05
     C     BIKUNR        READE     AVFRHL05                               40
     C     *in40         Cabeq     '1'           UtFran
     C                   MOVE      'J'           TopFrSkevet
      * Det finnes Blank i franka.. - den er første linje
     C     *in33         IFEQ      '0'
     C                   IF        FRAN = AFFRAN
     C                   EVAL      FRANKOD=*BLANKS
     C                   EVAL      FRANTXT=*BLANKS
     C                   END
     C                   END
     C     *in40         DOWEQ     '0'
     C     FranKey       CHAIN     KODTFR                             33
     C                   EVAL      FRANKOD=AFFRAN
     C  N33              EVAL      FRANTXT=KFRNTX
     C   33              EVAL      FRANTXT=AFFRAN

     C                   Add       1             AntLest
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬frankod¬ : ¬'+%trim(frankod)+'¬, '
          +'¬frantxt¬ : ¬'+%trim(frantxt)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')


     C     Avfr05Key     SETGT     AVFRHL05
     C     BIKUNR        READE     AVFRHL05                               40
     C                   ENDDO

     C     UtFran        ENDSR
      *=====================================================================******
      *  Hent ev. avdeling
      *=====================================================================******
     C     DSPMOD        BEGSR

     C                   MOVE      'HEUR '       PAID

     C                   EXSR      SETKEY

     C                   IF        WSAVDMOD = *BLANKS
     C                   MOVE      PAVRDA1       XXAVDMOD          2
     C                   END

     C                   DOW       *IN33 = *OFF
     C                   IF        ((PAVRDA1 <> ' ') AND (PAVRDN <> 0))
     C                   MOVE      PAVRDA1       XAVDMOD
     C                   MOVE      PAVRDA2       XAVDSK
     C                   Z-ADD     PAVRDN        XAVDAVD
     C                   IF        WSAVD = XAVD
     C                   EVAL      WSAVD1=XAVD
     C                   EVAL      WSAVD2=PAVRDA3
     C                   ENDIF
     C                   ENDIF
     C     BRPARFK       READE     BRPARF                                 33
     C                   ENDDO
     C                   ENDSR
      *=====================================================================******
      *  Display LEVERINGSVILKÅR codes to select from
      *=====================================================================******
     C     DspFR         BEGSR

     C                   MOVE      *blanks       FrString
     C                   MOVE      'HEURF'       PAID

     C                   IF        WSAVDMOD = *BLANKS
     C                   MOVE      XXAVDMOD      WSAVDMOD
     C                   ENDIF

     C                   EXSR      SETKEY

     C                   DOW       *IN33 = *OFF
     C                   IF        PAVRDA1 = WSAVDMOD
     C                   MOVE      PAVRDA        FrString
     C                   LEAVE
     C                   ENDIF
     C     BRPARFK       READE     BRPARF                                 33
     C                   ENDDO

     C     1             DO        12            XN                3 0
     C                   MOVEL     FR(XN)        KFRKOD
     C                   IF        KFRKOD <> *BLANKS
     C*                  IF        KFRKOD = FRAN
     C     KODTFRK       CHAIN     KODTFR                             40
     C                   EVAL      FRANKOD=KFRKOD
     C                   EVAL      FRANTXT=KFRNTX

     C                   Add       1             AntLest
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬frankod¬ : ¬'+%trim(frankod)+'¬, '
          +'¬frantxt¬ : ¬'+%trim(frantxt)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C*                  END
     C                   END
     C                   ENDDO

     C                   ENDSR
      *=====================================================================******
      *  Display OPPDRAGSTYPE codes to select from
      *=====================================================================******
     C     DspOT         BEGSR

     C                   MOVE      'HEURO'       PAID

     C                   IF        WSAVDMOD = *BLANKS
     C                   MOVE      XXAVDMOD      WSAVDMOD
     C                   END

     C                   EXSR      SETKEY

     C                   DOW       *IN33 = *OFF
     C                   IF        PAVRDA1 = WSAVDMOD
     C                   MOVE      PAVRDA        OtString
     C                   LEAVE
     C                   END
     C     BRPARFK       READE     BRPARF                                 33
     C                   ENDDO

     C     1             DO        16            XN                3 0
     C                   MOVEL     OT(XN)        KO1KOD
     C                   IF        KO1KOD <> *BLANKS
     C*                  IF        KO1KOD = WSOT
     C     KODTOTYK      CHAIN     KODTOTY                            40
     C                   EVAL      WSOT1=KO1KOD
     C                   EVAL      WSOT2=KO1NTX
     C*                  END
     C                   END
     C                   ENDDO

     C                   ENDSR
      *=====================================================================******
      *  SETT NØKKEL OG LES FØRST POST
      *=====================================================================******
     C     SETKEY        BEGSR

     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR

     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = BIKNG
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 33
     C                   END
     C                   END
     C                   END
     C                   END

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
      * Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      BRPARF
     C  N50              OPEN      KODTOTY
     C  N50              OPEN      kufast
     C  N50              OPEN      avfrh
     C  N50              OPEN      avfrhl05
     C  N50              OPEN      kodtfr
     C  N50              OPEN      cundl01
     C     KufasKey      KLIST
     C                   KFLD                    KFTYP
     C                   MOVEL     'PRODTYPE'    KFTYP
     C     KufasKeyK     KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C     KuProKey      KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    AFPROD
     C     kunkey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     Avfr05Key     KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    AFFRAN
     C     FranKey       KLIST
     C                   KFLD                    KFRUNI
     C                   KFLD                    AFFRAN
     C                   MOVE      'FR'          KFRUNI
     C     KODTFRK       KLIST
     C                   KFLD                    KFRUNI
     C                   KFLD                    KFRKOD
     C     BRPARFK       KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C     KODTOTYK      KLIST
     C                   KFLD                    KO1UNI
     C                   KFLD                    KO1KOD
     C                   MOVE      'OTY'         KO1UNI

     C  N50Bikunr        IFNE      *ZEROS
     C     kunkey        CHAIN     cundl01                            33
     C  N33              MOVE      *blanks       XXSDF             5
     C  N33              MOVEL     POSTNR        XXSDF
     C                   END

     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'KUNFR'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = BIKNG
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   END
     C                   END
     C                   END
     C                   END
     C                   IF        *IN33
     C                   MOVE      'J'           XKUNFRAKT         1
     C                   ELSE
     C                   MOVEL     PAVRDA        XKUNFRAKT
     C                   END
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C     FRA           IFEQ      *blanks
     C     Bikunr        IFNE      *ZEROS
     C                   EVAL      FRA=XXSDF
     C                   ELSE
     C                   EVAL      FRA='1000 '
     C                   ENDIF
     C                   ENDIF
     C                   EVAL      Overskr='FRAKTKALKULATOR'
     C                   IF        ((XKUNFRAKT <> 'J') AND (XKUNFRAKT <>'j'))
     C                   EVAL      Overskr='FULL FAKTURABEREGNING'
     C                   ENDIF
     C                   ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          BEGSR
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     KODTOTY
     C                   CLOSE     kufast
     C                   CLOSE     avfrh
     C                   CLOSE     avfrhl05
     C                   CLOSE     kodtfr
     C                   CLOSE     cundl01
     C                   callp     wrtsection('*fini')
     C                   EVAL      *INLR = *ON
     C                   return
     C                   ENDSR
