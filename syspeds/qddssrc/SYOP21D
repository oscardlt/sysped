     A*%%TS  SD  20150825  110121  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 BEGRENSE AVDELING VED SØKING
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/ZEADF)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A          R BILDEBL
     A          R SY20B10
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk opp')
     A                                      CF08(08 'FILTER')
     A                                      CF12(12 'Tilbake')
     A                                      OVERLAY
     A                                  1  3'SYOP21'
     A                                  1 27MSGCON(025 BBA0127 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 63TIME
     A            FILTTXT       30   O  3 27COLOR(RED)
     A                                  4  4MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  6MSGCON(070 BBA0124 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(078 BBB0125 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            WREFF         17A  B  8  4
N01  A                                  8 25MSGCON(010 YBC1965 *LIBL/SYMSGF)
N01  A            W2AVD          4Y 0B  8 36EDTCDE(4)
N01  A                                  8 76MSGCON(004 YBC1966 *LIBL/SYMSGF)
N01  A                                      DSPATR(HI)
     A                                  9 16MSGCON(040 BBA0126 *LIBL/SYMSGF)
     A                                 23  5MSGCON(020 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SY20B20
     A                                      CF03(03 'Exit')
     A                                      SLNO(23)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  4MSGCON(020 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 25MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 45'F8=Filter'
     A                                      COLOR(BLU)
     A                                  1 68'F12=tilbake'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R SY20H30                   SFL
     A*%%TS  SD  20150825  110121  JOVO        REL-V7R1M0  5770-WDS
     A            RECNUM        10S 0H
     A            WSNR           1A  B  9  2VALUES(' ' '1' '2' '4' '5' '7' '8' -
     A                                      'E' 'F' 'O')
     A            HEREFF    R        O  9  4
     A  50                                  DSPATR(RI)
S01  A*           HEUNIK    R        O  9  4
     A            HEUNIK    R        H
     A            HEDTOP         8S 0O  9 23
     A            WSNA01        14A  O  9 33
     A  50                                  DSPATR(RI)
     A            WSNA02        14A  O  9 48
     A            HENT      R        O  9 63REFFLD(HEADFR/HENT *LIBL/ZEADF)
     A                                      EDTCDE(4)
     A            HEVKT     R        O  9 71REFFLD(HEADFR/HEVKT *LIBL/ZEADF)
     A                                      EDTCDE(4)
     A            WSAD01        14A  O 10 33
     A            WSAD02        14A  O 10 48
     A            HELKA          2A  O 10 63
     A            HEAVD          4Y 0O 10 76EDTCDE(4)
     A            BOOKTX         9   O 10 66COLOR(RED)
     A          R SY20K40                   SFLCTL(SY20H30)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk opp')
     A                                      CF08(08 'FILTER')
     A                                      CF12(12 'Tilbake')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYOP21'
     A                                  1 27MSGCON(025 BBA0127 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 21'Mode:'
     A            NIVÅ           3A  O  2 27COLOR(WHT)
     A                                  2 63TIME
     A            CURS1          4S 0O  2 76SFLRCDNBR(CURSOR)
     A                                      DSPATR(ND)
     A                                      COLOR(BLU)
     A            FILTTXT       30A  O  3 27COLOR(RED)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'1=Plukk  2=Endre tur  4=Slett  5=V-
     A                                      is  7=Frisøk  8=Notis  E=Edi  F=Fil-
     A                                        O=Oppla'
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(078 BBA0125 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            WREFF         17A  B  8  4
N01  A                                  8 25MSGCON(010 YBC1965 *LIBL/SYMSGF)
N01  A            W2AVD          4Y 0B  8 36EDTCDE(4)
N01  A                                  8 76MSGCON(004 YBC1966 *LIBL/SYMSGF)
N01  A                                      DSPATR(HI)
     A*
     A          R SY20B50
     A*%%TS  SD  20150825  095544  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Avbryt')
     A                                  1  2MSGCON(020 DIV0586 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 23MSGCON(004 DIV0071 *LIBL/SYMSGF)
     A            HEAVD          4Y 0O  1 28TEXT('Avdeling')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0O  1 33TEXT('Oppdragsnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 41MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            HESG           3A  O  1 45TEXT('Signatur')
     A                                      COLOR(WHT)
     A                                  1 49MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            HESTD          4Y 0O  1 54TEXT('Kostnadssted')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 61MSGCON(004 DIV0588 *LIBL/SYMSGF)
     A*           HERFA         15A  O  1 66TEXT('Referanse Agent')
     A            HERFA15       15A  O  1 66TEXT('Referanse Agent')
     A                                      COLOR(WHT)
     A            HEDT           6Y 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 43MSGCON(005 DIV0587 *LIBL/SYMSGF)
     A            HEGN          15A  O  2 49TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(004 DIV0082 *LIBL/SYMSGF)
     A            HELKA          2A  O  3  8TEXT('Landkode')
     A                                      COLOR(WHT)
     A                                  3 11MSGCON(004 DIV0589 *LIBL/SYMSGF)
     A            HESDF          5A  O  3 16TEXT('Frakt fra')
     A                                      COLOR(WHT)
     A            HESDT          5A  O  3 27TEXT('Frakt til')
     A                                      COLOR(WHT)
     A  57                              3 66'EDI adresser'
     A                                      DSPATR(HI)
     A                                  4  2MSGCON(005 DIV0084 *LIBL/SYMSGF)
     A            HEKNA          8Y 0O  4  8TEXT('Kundenr Agent')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            HENAA         30A  O  4 17TEXT('Navn Agent')
     A                                      COLOR(WHT)
     A                                  4 48MSGCON(005 DIV0085 *LIBL/SYMSGF)
     A            HEKNT          8Y 0O  4 54TEXT('Kundenr Transportør')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(005 DIV0086 *LIBL/SYMSGF)
     A            HETRM          2Y 0O  5  8TEXT('Transportmåte')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(003 DIV0083 *LIBL/SYMSGF)
     A            HESDL         20A  O  6  8TEXT('Lossested')
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            HEKNS          8Y 0O  7  8TEXT('Kundenr Selger')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            HEVALS         3A  O  7 33TEXT('Valutakode Selger')
     A                                      COLOR(WHT)
     A                                  7 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  7 42TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENASF        30A  O  7 51TEXT('Navn Selg.fakm')
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            HENAS         30A  O  8  8TEXT('Navn Selger')
     A                                      COLOR(WHT)
     A            HEADS1        30A  O  8 42TEXT('Adresse-1 Selger')
     A                                      COLOR(WHT)
     A            HEADS2        30A  O  9  8TEXT('Adresse-3 Selger')
     A                                      COLOR(WHT)
     A            HEADS3        30A  O  9 42TEXT('Adresse-2 Selger')
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(012 DIV0592 *LIBL/SYMSGF)
     A            HELKS          2A  O 10 19COLOR(WHT)
     A            HESDFF         5A  O 10 22TEXT('Forfrakt fra')
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            HEKNK          8Y 0O 11  8TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 11 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            HEVALK         3A  O 11 33TEXT('Valutakode Kjøper')
     A                                      COLOR(WHT)
     A                                 11 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O 11 42TEXT('Kundenr Kjøp.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAKF        30A  O 11 51TEXT('Navn Kjøp.fakm')
     A                                      COLOR(WHT)
     A                                 12  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            HENAK         30A  O 12  8TEXT('Navn Kjøper')
     A                                      COLOR(WHT)
     A            HEADK1        30A  O 12 42TEXT('Adresse-1 Kjøper')
     A                                      COLOR(WHT)
     A            HEADK2        30A  O 13  8TEXT('Adresse-2 Kjøper')
     A                                      COLOR(WHT)
     A            HEADK3        30A  O 13 42TEXT('Adresse-3 Kjøper')
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(015 DIV0593 *LIBL/SYMSGF)
     A            HELKK          2A  O 14 19COLOR(WHT)
     A            HESDVT         5A  O 14 22TEXT('Viderefrakt til')
     A                                      COLOR(WHT)
     A                                 15  2MSGCON(077 DIV0591 *LIBL/SYMSGF)
     A            HEOT           2A  O 16  2TEXT('Oppdragstype')
     A                                      COLOR(WHT)
     A            HEFR           3A  O 16  5TEXT('Frankatur')
     A                                      COLOR(WHT)
     A            HEGM1         15A  O 16  9TEXT('Godsmerking-1')
     A                                      COLOR(WHT)
     A            HENT           7Y 0O 16 25TEXT('Antall')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            HEVS1         25A  O 16 33TEXT('Vareslag-1')
     A                                      COLOR(WHT)
     A            HEVKT          9Y 0O 16 59TEXT('Vekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            HEM3           7Y 3O 16 71TEXT('Kubikk')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            HEGM2         15A  O 17  9TEXT('Godsmerking-2')
     A                                      COLOR(WHT)
     A            HEVS2         25A  O 17 33TEXT('Vareslag-2')
     A                                      COLOR(WHT)
     A            HELM           4Y 2O 17 66TEXT('Lastemeter')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(006 SHU0150 *LIBL/SYMSGF)
     A            HEKF           2A  O 18  9COLOR(WHT)
     A            HEKFT1         1A  O 18 12COLOR(WHT)
     A            HEFT1A        30A  O 18 15COLOR(WHT)
     A            HEFT1B        15A  O 18 46COLOR(WHT)
     A                                 18 71MSGCON(008 DIV0594 *LIBL/SYMSGF)
     A            HEKDPL         1A  O 18 80TEXT('Planleggingskode')
     A                                      COLOR(WHT)
     A                                 19 22MSGCON(008 DIV5022 *LIBL/SYMSGF)
     A            TRVERV         3A  O 19 31COLOR(WHT)
     A            TRVERB        13Y 2O 19 35EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 19 60MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            HEKDL         15A  O 19 66TEXT('Lagerkode')
     A                                      COLOR(WHT)
     A*           HERFA         15A  O  1 66TEXT('Referanse Agent')
     A            BOOKTXT       15   O  2 66TEXT('Referanse Agent')
     A                                      COLOR(RED)
     A                                  2  2'Opdgi'
     A            TRKNFA         8Y 0O  2  8TEXT('Kundenr Agent')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A          R SY20B60
     A                                      OVERLAY
     A                                      CF04(04 'Søk agent')
     A                                      CF12(12 'Avbryt')
     A                                  1  2'SYOP21'
     A                                  1 27MSGCON(025 BBA0127 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A                                  2 73TIME
     A                                  3 28'Fellesopplysninger'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'SYSPED    Referanse avdeling . . .-
     A                                       :'
     A            ÆEAVD          4Y 0B  6 41EDTCDE(4)
     A  31                                  COLOR(RED)
     A                                  7  4'Signatur for saksbehandler . . . :'
     A            ÆESG           3A  B  7 41
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                  8  6'Agentnummer  . . . . . . . . . :'
     A            ÆEKNA          8Y 0B  8 41EDTCDE(4)
     A  34                                  DSPATR(PC)
     A  34                                  COLOR(RED)
     A                                 10  4'Turnummer  . . . . . . . . . . . :'
     A            ÆEPRO          8Y 0B 10 41EDTCDE(4)
     A                                      DSPATR(PC)
     A  33                                  COLOR(RED)
     A                                 11  6'Godsnummer . . . . . . . . . . :'
     A            ÆEGN          15A  B 11 41
     A  35                                  DSPATR(PC)
     A  35                                  COLOR(RED)
     A                                 12  6'Landkode . . . . . . . . . . . :'
     A            ÆELKA          2A  B 12 41
     A  36                                  DSPATR(PC)
     A  36                                  COLOR(RED)
     A                                 13  6'Transportmåte  . . . . . . . . :'
     A            ÆEKDTM         2A  B 13 41
     A  37                                  DSPATR(PC)
     A  37                                  COLOR(RED)
     A                                 14  6'Transportidentitet . . . . . . :'
     A            ÆETRI          2A  B 14 41
     A  38                                  DSPATR(PC)
     A  38                                  COLOR(RED)
     A                                 22  2'F4=Søk agent   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 23  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(7 25 5 30)
     A                                      OVERLAY
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'søking')
     A                                  1  2'SYOP00'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC0231 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(10 YBC0232 SYMSGF)
     A            WSVAVD         4Y 0B  2 14DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  3  2MSGCON(10 YBC0233 SYMSGF)
     A            WSSFSI         3A  B  3 14DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A N30                              4  2MSGCON(24 YBC0234 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 19 70 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(03)
     A            MSGNR          7A  P
     A                                  1  2'SYOP21'
     A                                      COLOR(BLU)
     A                                  1 11MSGCON(050 JOV1472 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2'Kun ....'
     A                                  4  3'Avdeling    '
     A            W2AVD          4Y 0B  4 17EDTCDE(4)
     A                                  4 23'(Avd tildelt i EDI-mottaket)'
     A                                  6  3'Kontor/avd  '
     A            W2KTR          4Y 0B  6 17EDTCDE(4)
     A                                  6 23'Kontor/avd. (via postnr styringsfi-
     A                                      l)'
     A                                  7  3'Postnr fra  '
     A            W2LKF          2A  B  7 17
     A            W2PNF          5A  B  7 20
     A                                  7 27'Hele eller deler av postnr kan opp-
     A                                      gis'
     A                                  8  3'Postnr til  '
     A            W2LKT          2A  B  8 17
     A            W2PNT          5A  B  8 20
     A                                  8 27'Hele eller deler av postnr kan opp-
     A                                      gis'
     A                                  9  4'Postnr-test(3 foreg.) primært oppd-
     A                                      ragets STED.(evt VIA ved TR-mod.)'
     A                                 10  4'Aviks-kode'
     A            W2PNKO         1A  B 10 17
     A                                 10 19'A=Pnr fra Adresse, E=Endepunkt(TR--
     A                                      modul)'
     A                                 12  3'EDI-refer.'
     A            W2REFF        17A  B 12 17
     A                                 15  3'Kun oppdrtyp'
     A            W2KOT1         2A  B 15 17
     A            W2KO02         2A  B 15 20
     A            W2KO03         2A  B 15 23
     A            W2KO04         2A  B 15 26
     A            W2KO05         2A  B 15 29
     A            W2KO06         2A  B 15 32
     A                                 15 36'IKKE oppdrtype'
     A            W2IOT1         2A  B 15 51
     A            W2IO02         2A  B 15 54
     A            W2IO03         2A  B 15 57
     A            W2IO04         2A  B 15 60
     A            W2IO05         2A  B 15 63
     A            W2IO06         2A  B 15 66
     A  30        MSGTXT        28A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 19 50'F3/F12=Forrige '
     A                                      COLOR(BLU)
