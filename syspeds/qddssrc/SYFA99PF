     A*****************************************************************
     A* N01 SH PTF5   DATO PÅ DETALJLINJER   A-TIL FAKTURANR.)        *
     A*         FAKTURAKOPI PÅ LISTEPAPIR (FRA-TIL FAKTURANR.)        *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=120, OVERFLOW=43      *
     A*****************************************************************
     A          R HEADER
     A            MSG01         60A    16 39
     A            MSG02         24A    22 39
     A            WSFNF          7  0  22 65EDTCDE(3)
     A            MSG03         24A    25 39
     A            WSFNT          7  0  25 65EDTCDE(3)
     A            MSG04         24A    40  9
     A            MSG05         24A    41  9
     A            MSG06        120A    42  1
     A          R NEWP
     A                                  1  1' '
     A          R FEILFA                    SPACEB(1)
     A            WSFN           7  0      1EDTCDE(3)
     A                                      HIGHLIGHT
     A            MSG07         80A        9
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
     A          R FEILOP                    SPACEB(1)
     A            WSFN           7  0      1EDTCDE(3)
     A                                      HIGHLIGHT
     A            MSG08         14A        9
     A                                      HIGHLIGHT
     A            FIAVD          4  0     25HIGHLIGHT
     A                                    29'/'
     A                                      HIGHLIGHT
     A            FIOPD          7  0     30HIGHLIGHT
     A            MSG09         80A       38
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
     A          R DET                       SPACEB(1)
     A  44                                23'________________________'
     A  44                                47'________________________'
     A  44                                71'__________'
     A  41        WSFN           7  0      1EDTCDE(3)
     A  41        XFATXT         8A        8
     A  41        XFN            7  0     16EDTCDE(3)
     A  41        HEAVD          4  0     23EDTCDE(3)
     A  41                                27'/'
     A  41        HEOPD          7  0     28EDTCDE(3)
     A* 41                                33'/'
     A* 41        HESG           3A       34
     A  41        MSG10          4A       38
     A  41        KUNDNR         8  0     42EDTCDE(3)
     A  41N48     KNAVN         30A       51
     A  41 48     XNAD          30A       51
     A  42        FADD           2  0      1
     A  42                                 3'/'
     A  42        FAMM           2  0      4
     A  42                                 6'/'
     A  42        FAAA           2  0      7
     A  42        MSG11          4A       23
     A  42        HESDF          5A       28
     A  42                                33'/'
     A  42        HESDT          5A       34
     A  42        HEFR           3A       40
     A  42                                43'/'
     A  42        HEOT           2A       44
     A  42        MSG12          4A       47
     A  42        HENAS         30A       51
     A  43        FADDFF         2  0      1
     A  43                                 3'/'
     A  43        FAMMFF         2  0      4
     A  43                                 6'/'
     A  43        FAAAFF         2  0      7
     A  43        MSG13          6A       20
     A  43        HEGN          15A       27
     A  43                                38'/'
     A  43        HEPOS1         7A       39
     A  43        MSG14          4A       47
     A  43        HENAK         30A       51
     A  44        MSG15         57A       23
     A  45        HENT           7  0     13EDTCDE(3)
     A  45        HEVS1         25A       21
     A  45        HEVKT          9  0     47EDTCDE(3)
     A  45        HEM3           7  3     57EDTCDE(3)
     A  45        HELM           4  2     66EDTCDE(3)
     A  45        HEFBV          9  0     72EDTCDE(3)
     A N46        KGENOT        20A       82
     A N46 71     XVAL           3A      104
     A N46        XPFNOK         9  2    108EDTCDE(L)
     A N46 34                            120'*'
     A*
     A* DERSOM MINDRE ENN 5 VARELINJER (46 PÅ) KAN MOMSLINJE SKRIVES UT
     A          R TOT
     A     72     MSG16          8A       82
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A     72     XMVAGR         9  2     91EDTCDE(J)
     A                                      HIGHLIGHT
     A     72 71  XVAL           3A      104HIGHLIGHT
     A     72     XPMOMS         8  2    109EDTCDE(L)
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A            MSG17          8A       82SPACEB(1)
     A  71        XVAL2          3A       99HIGHLIGHT
     A            XBRUTT         9  2    108EDTCDE(L)
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
