********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9041 * 01 YBC PTF9 FORHÅNDSVARSLING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11133 * 02 YBC PTF11 VEKT ER ENDRET FRA 9.1 TIL 11.3
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R SVX015D01
     A                                      CA03(03 'Avslutt program.')
     A                                  1  3'SYSPED/SVX015R'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  4 23'Ændra status NCTS Export   '
     A                                      COLOR(WHT)
     A                                  8 23'Avdelning:'
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10 23'Signatur:'
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  ERRMSG('Tastet tariffør er ikke reg-
     A                                      istrert.' 98)
     A                                 12 23'Ærende:'
     A            WSOPD          7Y 0B 12 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 23'Ny status:'
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                 23 23'F3 = Avsluta'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SVX015D02
     A                                      CA05(05 'Endre status')
     A                                  1  3'SYSPED/SVX015R'
     A                                      COLOR(BLU)
     A                                  1 20'Ændra status NCTS Export'
     A                                      COLOR(WHT)
     A                                  1 46'Ref.'
     A            WSAVD          4Y 0O  1 51COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 56COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSG           3A  O  1 64DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  1 68'Tur'
     A            THPRO          8Y 0O  1 72DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2  2'1.Dekl.'
     A            THDK           5A  O  2 10TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 16'4.L.lista'
     A            THLSTL         4Y 0O  2 26EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 31'5.V.pos'
     A            THVPOS         5Y 0O  2 39EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 45'6.Kolli'
     A            THNTK          7Y 0O  2 53EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S02  A*                                 2 61'B.vikt'
S02  A*           THVKB          9Y 1O  2 68EDTCDE(4)
N02  A                                  2 61'Bvikt'
N02  A            THVKB         11Y 3O  2 67EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'2.Avsænd. '
     A            THKNS          8Y 0O  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            THTINS        17A  O  3 22TEXT('Tin kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 40'9.Skickas'
     A            THTSN1        35A  H
     A            WSTSN1        30A  O  3 50DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAS         30A  O  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 41'tillbak'
     A            THTSN2        35A  H
     A            WSTSN2        30A  O  4 50DSPATR(UL)
     A                                      COLOR(YLW)
     A            THADS1        30A  O  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'15.Avs/utf.land'
     A            THALK          2A  O  5 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THPNS          9A  O  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THPSS         24A  O  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 40'17.Be.land'
     A            THBLK          2A  O  6 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Landkod '
     A            THLKS          2A  O  7 12TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 22'Språkkod '
     A            THSKS          2A  O  7 32TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 40'18.Tr.middel ved avgång ID/LK/SK'
N01  A                                  7 78'TM'
     A                                  8  2'8.Mottag. '
     A            THKNK          8Y 0O  8 13TEXT('Kundenr kjøper')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            THTINK        17A  O  8 22TEXT('Tin kode kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTAID        27A  O  8 43TEXT('Identitet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTALK         2A  O  8 71TEXT('Landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTASK         2A  O  8 74TEXT('Språkkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
N01  A            THTRMI         2Y 0O  8 78TEXT('Transportmåte')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(9)
     A            THNAK         30A  O  9  2TEXT('Navn kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'19.Cont.'
     A            THKDC          1A  O  9 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THADK1        30A  O 10  2TEXT('Adresse-1 kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 40'21.Tr.middel ved grense ID/LK/SK'
N01  A                                 10 78'TM'
     A            THPNK          9A  O 11  2TEXT('Postnr kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THPSK         24A  O 11 12TEXT('Poststed kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTGID        27A  O 11 43TEXT('Identitet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTGLK         2A  O 11 71TEXT('Landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTGSK         2A  O 11 74TEXT('Språkkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
N01  A            THTRM          2Y 0O 11 78TEXT('Transportmåte')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(9)
     A                                 12  2'Landkod '
     A            THLKK          2A  O 12 12TEXT('Landkode kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 22'Språkkod '
     A            THSKK          2A  O 12 32TEXT('Språkkode kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*                                12 40'25.Tr.sætt'
S01  A*           THTRM          2Y 0O 12 51TEXT('Transportmåte')
S01  A*                                     DSPATR(UL)
S01  A*                                     COLOR(YLW)
S01  A*                                     EDTCDE(9)
N01  A                                 12 40'L.st:'
N01  A            THLASD        17A  O 12 46TEXT('Laststed')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            THLAS2        17A  O 12 64TEXT('SUB Laststed')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
     A                                 13  2'50. Ansvar'
     A            THKNA          8Y 0O 13 13TEXT('Kundenr H.ansvalig')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            THTINA        17A  O 13 22TEXT('Tin kode H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 40'27.Lastplats'
     A            THLSD         17A  O 13 53TEXT('Lastested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THNAA         30A  O 14  2TEXT('Navn H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 40'C. Avg. tollkont'
     A            THCATS         8A  O 14 57TEXT('AVG.TOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THADA1        30A  O 15  2TEXT('Adresse-1 H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 40'Språkkod  på føljedok.'
     A            THSKFD         2A  O 15 63TEXT('Språkkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THPNA          9A  O 16  2TEXT('Postnr H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THPSA         24A  O 16 12TEXT('Poststed H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2'Landkod '
     A            THLKA          2A  O 17 12TEXT('Landkode H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 22'Språkkod '
     A            THSKA          2A  O 17 32TEXT('Språkkode H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 40'Reg.dat '
     A            THDT           8Y 0H
     A            WSDT           6Y 0O 17 49TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 17 58'Tar'
     A            THTARF        16A  O 17 62TEXT('Tariffer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'51.'
     A            THTSD1         8A  O 18  6TEXT('Transitt tollsted 1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD2         8A  O 18 15TEXT('Transitt tollsted 2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD3         8A  O 18 24TEXT('Transitt tollsted 3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD4         8A  O 18 33TEXT('Transitt tollsted 4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD5         8A  O 18 42TEXT('Transitt tollsted 5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD6         8A  O 18 51TEXT('Transitt tollsted 6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD7         8A  O 18 60TEXT('Transitt tollsted 7')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTSD8         8A  O 18 69TEXT('Transitt tollsted 8')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2'52.'
     A            THGKD          1Y 0O 19  6TEXT('Garanti kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THGFT1        24A  O 19  8TEXT('Garanti ref.nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THGFT2        35A  O 19 33TEXT('Ref. til annen garanti')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THGADK         4A  O 19 69TEXT('Adgangkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THGBGI         1Y 0O 19 74TEXT('Begrensing innen EF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THGBGU         2A  O 19 76TEXT('Begrensing utenfor')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20  2'53. B.tullkont'
     A            THTSB          8A  O 20 17TEXT('Bestemmels tollsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 32'Dekl.plat'
     A            THDST         15A  O 20 42DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 58'Dekl.språk'
     A            THDSK          2A  O 20 69DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21  2'D.'
     A                                 21  5'Ktl.res'
     A            THDKR          2A  O 21 13TEXT('Kontroll resultat')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 16'Frist'
     A            THDDT          8Y 0H
     A            WSDDT          6Y 0O 21 22TEXT('Tiodsfrist')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 21 31'Førsegling Ant/KD/SK'
     A            THDANT         4Y 0O 21 52TEXT('Antall forsegling')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            THDFKD        20A  O 21 57TEXT('FORSEGLING-ID KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THDFSK         2A  O 21 78TEXT('FORSEGLING-ID Språk')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  5'Ktl.Tsd'
     A            THDATS         8A  O 22 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 22'Ktl.DT'
     A            THDDTK         8Y 0H
     A            WSDDTK         6Y 0O 22 29DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 22 41'Trans.nr/dt'
     A            THTRNR        18A  O 22 53TEXT('Transittnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            THTRDT         8Y 0H
     A            WSTRDT         6Y 0O 22 72TEXT('TransittDATO')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 23  1'Gammal status'
     A                                      COLOR(TRQ)
     A            THST           1A  O 23 15TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                 23 18'Ny status'
     A                                      COLOR(TRQ)
     A            WSST           1A  O 23 28TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 24  2'F5=Ændre status'
     A                                      COLOR(BLU)
