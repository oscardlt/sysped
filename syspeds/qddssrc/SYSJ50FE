********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10199 * 01 YBC PTF10 TO NYE DATO FOR GATE IN OG OUT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYSJ50EPNL)
     A                                      HLPTITLE('B/L MODELL 1 (VARELI-
     A                                      NJER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            BVLI           3Y 0H
     A            BVSORT         3Y 0H
N01  A            BVGTIN         8Y 0H
N01  A            BVGTUT         8Y 0H
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            BVMN          17A  O  5  7
     A            BVNT           5Y 0O  5 25EDTCDE(4)
     A            BVVS          25A  O  5 32
     A            BVVKT          9Y 0O  5 58EDTCDE(4)
     A            BVM3           9Y 3O  5 69EDTCDE(4)
     A            BVKDC          2   H
     A            BVENH          1Y 0H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYSJ50E010 SYSJ50EPNL)
     A                                      HLPARA(01 001 21 080)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ50E'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  1 20MSGCON(40 DIV3171 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  5MSGCON(4 DIV3163 SYMSGF)
     A            UAVD           4Y 0O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 15MSGCON(6 DIV3164 SYMSGF)
     A            UOPD           7Y 0O  2 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 30MSGCON(5 DIV3155 SYMSGF)
     A            UDTR           6S 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(5 DIV3165 SYMSGF)
     A            USG            3A  O  2 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 55MSGCON(3 DIV3166 SYMSGF)
     A            UUKE           2Y 0O  2 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 63MSGCON(4 DIV3167 SYMSGF)
     A            UAAR           2S 0O  2 68DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3172 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF11(11 'B/L VARELINJE TYPE 2')
     A                                      CF12(12 'FORRIGE')
     A                                      CF13(13 'TIL SIDE 2')
     A  27                                  CF15(15 'TIL OPPDRAG')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYSJ50E999 SYSJ50EPNL)
     A                                      HLPARA(01 001 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  29                              1 62MSGCON(18 DIV3175 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(18 DIV3170 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              1 42MSGCON(18 DIV3136 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV3179 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYSJ50E015 SYSJ50EPNL)
     A                                      HLPARA(02 001 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(78 DIV3173 SYMSGF)
     A                                      DSPATR(HI)
     A            WSMN          17A  B  2  2DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSNT           5Y 0B  2 20DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKD           2A  B  2 28DSPATR(UL)
     A  90
     AO 89                                  DSPATR(RI)
     A            WSVS          25A  B  2 32DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSVKT          9Y 0B  2 58DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSM3           9Y 3B  2 69DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 42'Gate ut/in:'
N01  A            WSGTUT         6Y 0B  3 54DSPATR(UL)
N01  A  88                                  DSPATR(RI)
N01  A                                      EDTCDE(4)
N01  A            WSGTIN         6Y 0B  3 61DSPATR(UL)
N01  A  87                                  DSPATR(RI)
N01  A                                      EDTCDE(4)
     A                                  3 71MSGCON(5 YBC0703 SYMSGF)
     A            WSSORT         3Y 0B  3 77DSPATR(UL)
     A                                      EDTCDE(4)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(78 DIV3173 SYMSGF)
     A                                      DSPATR(HI)
     A            BVMN          17A  O  2  2DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            BVNT           5Y 0O  2 20DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            BVVS          25A  O  2 32DSPATR(UL)
     A                                      DSPATR(HI)
     A            BVVKT          9Y 0O  2 58DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            BVM3           9Y 3O  2 69DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 42'Gate ut/in:'
N01  A            WSGTUT         6Y 0O  3 54DSPATR(UL)
N01  A                                      DSPATR(HI)
N01  A                                      EDTCDE(4)
N01  A            WSGTIN         6Y 0O  3 61DSPATR(UL)
N01  A                                      DSPATR(HI)
N01  A                                      EDTCDE(4)
     A                                  3 71MSGCON(5 YBC0703 SYMSGF)
     A            BVSORT         3Y 0O  3 77DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF11(11 'B/L VARELINJE TYPE 2')
     A                                      CF12(12 'AVSLUTT')
     A                                      CF13(13 'TIL SIDE 2')
     A  27                                  CF15(15 'TIL OPPDRAG')
     A          H                           HLPPNLGRP(SYSJ50E010 SYSJ50EPNL)
     A                                      HLPARA(01 001 21 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ50E'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3171 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  5MSGCON(4 DIV3163 SYMSGF)
     A            UAVD           4Y 0O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 15MSGCON(6 DIV3164 SYMSGF)
     A            UOPD           7Y 0O  2 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 30MSGCON(5 DIV3155 SYMSGF)
     A            UDTR           6S 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(5 DIV3165 SYMSGF)
     A            USG            3A  O  2 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 55MSGCON(3 DIV3166 SYMSGF)
     A            UUKE           2Y 0O  2 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 63MSGCON(4 DIV3167 SYMSGF)
     A            UAAR           2S 0O  2 68DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3172 SYMSGF)
     A                                      DSPATR(HI)
     A  29                             22 62MSGCON(18 DIV3175 SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(18 DIV3170 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  27                             22 42MSGCON(18 DIV3136 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 32MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV3179 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
