********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 M = SPLITT PLUKKLISTE
11XXX * 03 JOV PTF11 F15=DK SAD IMP  F15=DK SAD IMP
11XXX * 04 CB  PTF11 EXTERN TULLID MULIGT PÅ TDS SE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLHEAD LLHEADR)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CA02(02 'SPØR')
      *
     A          R NOREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                      CF04(04 'SØKING')
     A                                      CF06(06 'LAG NY')
     A                                      CA10(10 'PLUKK')
     A                                      CA08(08 'FILER')
     A                                      CF05(05 'Forny')
     A                                      CA23(23 'Flere opsjoner')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL011R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0977 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(11 YBC0978 SYMSGF)
     A            LHPNR     R        O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 23COLOR(WHT)
     A                                  2 73TIME
     A                                  4  2MSGCON(78 YBC0979 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC0980 SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            W2BNR     R        B  6  5REFFLD(LLHEADR/LHBNR LLHEAD)
     A            W2TDT     R        B  6 28REFFLD(LLHEADR/LHTDT LLHEAD)
     A                                      EDTCDE(4)
     A            W2PAK     R        B  6 37REFFLD(LLHEADR/LHPAK LLHEAD)
     A            W2ORDR    R        B  6 55REFFLD(LLHEADR/LHORDR LLHEAD)
     A            W2ALN     R        B  6 73REFFLD(LLHEADR/LHALN LLHEAD)
     A                                      EDTCDE(4)
     A            W2FLN     R        H      REFFLD(LLHEADR/LHFLN LLHEAD)
     A                                 22  2'F8=Søk pl.liste   F10=Plukk fra L1'
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(78 YBC0981 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            LHMN           7S 0H
     A            WSNR           1A  B  7  2VALUES(' ' '2' '4' '7' '9' 'O' 'F' -
N01  A                                      'K' 'P' 'Q' 'S' 'D')
S01  A*                                     'K' 'P' 'Q' 'S')
     A            LHBNR     R        O  7  5
     A            LHBPN     R        O  7 23
     A            LHTDT          8  0O  7 28EDTCDE(4)
     A            LHPAK     R        O  7 37
     A            LHORDR    R        O  7 55
     A N61                                  COLOR(WHT)
     A            LHALN     R        O  7 73EDTCDE(4)
     A            LHFLN     R        H
     A            LHTN      R        H
     A            LHPRKD    R        H
     A            LHBNA     R        H
     A            XHBNA         21A  O  8  5DSPATR(UL)
     A            LHBA1     R        H
     A            LHBA2     R        H
     A            LHBA3     R        H
     A            LHBLK     R        H
     A            LHBPU     R        H
     A            LHST1     R        O  8 28DSPATR(UL)
     A            LHST3     R        H
     A            LHKLL     R        O  8 31EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHTLIN    R        H
     A            LHANT0    R        H
     A            LHANTL    R        O  8 37EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHANT1    R        O  8 45EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHANT2    R        O  8 51EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHANT3    R        O  8 57EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHWVEK    R        O  8 63EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHWKUB    R        O  8 72EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHLSD     R        H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF04(04 'SØKING')
     A                                      CF05(05 'Forny')
     A                                      CF06(06 'LAG NY')
     A                                      CA08(08 'FILER')
     A                                      CA10(10 'Plukk')
N03  A                                      CA15(15 'DK SAD IMP')
N03  A                                      CA16(16 'DK SAD EXP')
     A                                      CA12(12 'Annuller')
     A                                      CA23(23 'Flere opsjoner')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL011R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0977 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(11 YBC0978 SYMSGF)
     A            LHPNR     R        O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 23COLOR(WHT)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 73TIME
     A N50N51N49                        3  2MSGCON(78 YBC0982 SYMSGF)
     A                                      COLOR(BLU)
     A N50 51N49                        3  2MSGCON(78 YBC0983 SYMSGF)
     A                                      COLOR(BLU)
     A N50 51 49                        3  2MSGCON(78 YBC0984 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0979 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC0980 SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            W2BNR         17A  B  6  5
     A            W2TDT          8Y 0B  6 28EDTCDE(4)
     A            W2PAK     R        B  6 37REFFLD(LLHEADR/LHPAK LLHEAD)
     A            W2ORDR    R        B  6 55REFFLD(LLHEADR/LHORDR LLHEAD)
     A            W2ALN     R        B  6 73REFFLD(LLHEADR/LHALN LLHEAD)
     A                                      EDTCDE(4)
      *
     A          R BOTREC
     A                                      OVERLAY
     A                                 22  2'F8=Søk pl.liste   F10=Plukk fra L1'
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(078 YBC0985 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N03  A                                 22 38MSGCON(040 JOV1636 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
      *
     A          R CHGREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A  40                                  CF05(05 'TIL LINJE')
     A                                      CA12(12 'Annuller')
     A                                      CF18(18 'NOTISBLOKK')
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZMSGNO         7A  P
     A                                  1  2'SYSPED/LL011R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0986 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(16 YBC0987 SYMSGF)
     A            LHPNR     R        O  2 19EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 28COLOR(WHT)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(16 YBC0988 SYMSGF)
     A            LHSTA     R        O  3 19COLOR(WHT)
     A                                  4  2MSGCON(16 YBC0989 SYMSGF)
     A            LHMN      R        O  4 19DSPATR(UL)
     A  61                                  COLOR(WHT)
     A N61                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(16 YBC0990 SYMSGF)
     A            WSBNR     R        B  5 19REFFLD(LLHEADR/LHBNR LLHEAD)
     A  90                                  DSPATR(HI)
     A            KUKUN2         8Y 0O  5 37EDTCDE(4)
     A            KUVADR         5Y 0O  5 46EDTCDE(4)
     A            WSBNA     R        B  6 19REFFLD(LLHEADR/LHBNA LLHEAD)
     A            WSBA1     R        B  6 51REFFLD(LLHEADR/LHBA1 LLHEAD)
     A            WSBA2     R        B  7 19REFFLD(LLHEADR/LHBA2 LLHEAD)
     A            WSBPN     R        B  7 51REFFLD(LLHEADR/LHBPN LLHEAD)
     A                                      EDTCDE(4)
     A            WSBA3     R        B  7 57REFFLD(LLHEADR/LHBA3 LLHEAD)
     A                                  8 19MSGCON(16 YBC0991 SYMSGF)
     A            WSBLK     R        B  8 36REFFLD(LLHEADR/LHBLK LLHEAD)
     A                                  8 41MSGCON(18 YBC0992 SYMSGF)
     A            WSBPU     R        B  8 60REFFLD(LLHEADR/LHBPU LLHEAD)
     A                                  9  2MSGCON(16 YBC0993 SYMSGF)
     A            WSTDT     R        B  9 19REFFLD(LLHEADR/LHTDT LLHEAD)
     A  89                                  DSPATR(HI)
     A                                      EDTWRD('    .  .  ')
     A                                  9 41MSGCON(18 YBC0994 SYMSGF)
     A            WSTN      R        B  9 60REFFLD(LLHEADR/LHTN LLHEAD)
     A  88                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 69'Prod.'
     A            WSPRKD    R        B  9 75REFFLD(LLHEADR/LHPRKD LLHEAD)
     A  87                                  DSPATR(HI)
     A                                 10  2MSGCON(16 YBC0995 SYMSGF)
     A            WSPAK     R        B 10 19REFFLD(LLHEADR/LHPAK LLHEAD)
     A                                 10 41'Prio plukklager:'
     A            WSLSD     R        B 10 60REFFLD(LLHEADR/LHLSD LLHEAD)
     A  86                                  DSPATR(HI)
     A                                 11  2MSGCON(16 YBC0996 SYMSGF)
     A            WSORDR    R        B 11 19REFFLD(LLHEADR/LHORDR LLHEAD)
     A                                 12  2MSGCON(16 YBC0997 SYMSGF)
     A            WSFLN     R        B 12 19REFFLD(LLHEADR/LHFLN LLHEAD)
     A                                 13  2MSGCON(16 YBC0998 SYMSGF)
     A            LHALN     R        O 13 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(16 YBC0999 SYMSGF)
     A            LHAVD     R        O 14 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 24'/'
     A                                      COLOR(WHT)
     A            LHOPD     R        O 14 26EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 34'/'
     A                                      COLOR(WHT)
     A            LHFBN     R        O 14 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 15  2MSGCON(16 YBC1000 SYMSGF)
     A            LHANT0    R        O 15 19EDTCDE(4)
     A                                 15 30MSGCON(14 YBC1001 SYMSGF)
     A            LHANTL    R        O 15 45EDTCDE(4)
     A                                 15 53'STK'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 30MSGCON(16 YBC1002 SYMSGF)
     A            LHANT1    R        O 16 47EDTCDE(4)
     A            LHAEH1    R        O 16 53DSPATR(HI)
     A                                 17 30MSGCON(16 YBC1003 SYMSGF)
     A            LHANT2    R        O 17 47EDTCDE(4)
     A            LHAEH2    R        O 17 53DSPATR(HI)
     A                                 18 30MSGCON(16 YBC1004 SYMSGF)
     A            LHANT3    R        O 18 47EDTCDE(4)
     A            LHAEH3    R        O 18 53DSPATR(HI)
     A                                 19  2MSGCON(16 YBC1005 SYMSGF)
     A            LHVEK     R        O 19 19EDTCDE(M)
     A                                 19 42MSGCON(16 YBC1006 SYMSGF)
     A            LHWVEK    R        O 19 59EDTCDE(M)
     A                                 20  2MSGCON(16 YBC1007 SYMSGF)
     A            LHKUB     R        O 20 19EDTCDE(M)
     A                                 20 42MSGCON(16 YBC1008 SYMSGF)
     A            LHWKUB    R        O 20 59EDTCDE(M)
     A                                 21  2MSGCON(16 YBC1009 SYMSGF)
     A            LHST1     R        O 21 19
     A                                 21 21'/'
     A            LHST3     R        O 21 23
     A                                 21 42MSGCON(16 YBC1010 SYMSGF)
     A            LHTLIN    R        O 21 59EDTCDE(4)
     A                                 22  2MSGCON(16 YBC1011 SYMSGF)
     A            WSKLL     R        B 22 19REFFLD(LLHEADR/LHKLL LLHEAD)
     A                                      EDTCDE(4)
     A            WSMLP     R        B 22 26REFFLD(LLHEADR/LHMLP LLHEAD)
     A                                      EDTCDE(4)
     A            LHST2     R        O 22 32
     A N40                             23  2MSGCON(78 YBC1013 SYMSGF)
     A                                      COLOR(BLU)
     A  40                             23  2MSGCON(78 YBC1014 SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R DLTREC
     A                                      WINDOW(*DFT 6 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                      CA23(23 'BEKREFTE')
     A                                  1  2'LL011R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC1015 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(38 YBC1016 SYMSGF)
     A                                      COLOR(RED)
     A                                  5  2MSGCON(38 YBC1017 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BESTAL
     A                                      WINDOW(*DFT 5 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL011R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC1018 SYMSGF)
     A                                      DSPATR(HI)
     A N45                              2  2MSGCON(15 YBC1019 SYMSGF)
     A N45        WSALNG         7Y 0B  2 18DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N45                              2 26MSGCON(20 YBC1020 SYMSGF)
     A                                  3  2MSGCON(15 YBC1021 SYMSGF)
     A            WSPRT         10A  B  3 18DSPATR(UL)
     A  88                                  DSPATR(HI)
     A  88                                  DSPATR(PC)
     A                                  4  2MSGCON(15 YBC1022 SYMSGF)
     A            WSSORT         1A  B  4 18DSPATR(UL)
     A                                      VALUES('L' 'F')
     A N45                              4 20MSGCON(30 YBC1023 SYMSGF)
     A  45                              4 20MSGCON(30 YBC1024 SYMSGF)
     A  88                                  DSPATR(PC)
     A N30                              5  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  90                              5  2MSGCON(48 YBC1025 SYMSGF)
     A                                      COLOR(WHT)
     A  89                              5  2MSGCON(48 YBC1026 SYMSGF)
     A                                      COLOR(WHT)
     A  88                              5  2MSGCON(48 YBC1027 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R OVROPPD
S04  A*                                     WINDOW(*DFT 14 50 *NOMSGLIN)
N04  A                                      WINDOW(*DFT 15 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL011R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC1028 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(15 YBC1701 SYMSGF)
     A            WSMOD          1A  B  2 18DSPATR(UL)
     A                                      VALUES('A' 'B' 'C' 'D' 'H')
     A                                  2 20MSGCON(30 YBC1852 SYMSGF)
     A                                  3  2MSGCON(15 YBC1029 SYMSGF)
     A            WSAVD          4  0B  3 18DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(15 YBC1030 SYMSGF)
     A            WSSG           3A  B  4 18DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                  5  2MSGCON(15 YBC1736 SYMSGF)
     A            WSFBR          1A  B  5 18DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  5 21'(J/N)'
     A                                  6  2MSGCON(15 YBC1031 SYMSGF)
     A            WSPSDL         1A  B  6 18DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  6 21'(J/N)'
     A                                  7  2MSGCON(15 YBC1032 SYMSGF)
     A            WSPRT         10A  B  7 18DSPATR(UL)
     A  88                                  DSPATR(HI)
     A  88                                  DSPATR(PC)
     A                                  8  2'Plassert......:'
     A            WSPSD         30A  B  8 18DSPATR(UL)
     A  87                                  DSPATR(HI)
     A  87                                  DSPATR(PC)
     A                                  9  2'Påbegynt tid..:'
     A            WSPTDF         4Y 0B  9 18DSPATR(UL)
     A  86                                  DSPATR(HI)
     A  86                                  DSPATR(PC)
     A                                      EDTWRD('0 .  ')
     A                                  9 25'Ferdig tid:'
     A            WSPTDT         4Y 0B  9 37DSPATR(UL)
     A  85                                  DSPATR(HI)
     A  85                                  DSPATR(PC)
     A                                      EDTWRD('0 .  ')
     A                                 10  2'Signatur......:'
     A            WSPSG         30A  B 10 18DSPATR(UL)
     A  84                                  DSPATR(HI)
     A  84                                  DSPATR(PC)
     A                                 11  2MSGCON(15 YBC1011 SYMSGF)
     A            WSKLL     R        B 11 18REFFLD(LLHEADR/LHKLL LLHEAD)
     A  83                                  DSPATR(HI)
     A  83                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSMLP     R        B 11 25REFFLD(LLHEADR/LHMLP LLHEAD)
     A  82                                  DSPATR(HI)
     A  82                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 11 32'Tot.vekt:'
     A            WSWVEK    R        B 11 42REFFLD(LLHEADR/LHWVEK LLHEAD)
     A  81                                  DSPATR(HI)
     A  81                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(15 YBC1012 SYMSGF)
     A            WSBPAL    R        B 12 18REFFLD(LLHEADR/LHBPAL LLHEAD)
     A                                      EDTCDE(4)
     A            WSAPAL    R        B 12 25REFFLD(LLHEADR/LHAPAL LLHEAD)
     A                                      EDTCDE(4)
     A                                 12 32'Tot.kub.:'
     A            WSWKUB    R        B 12 42REFFLD(LLHEADR/LHWKUB LLHEAD)
     A                                      EDTCDE(4)
     A                                 13  2'Auto OPD:'
     A            WSPAFF         1A  B 13 12VALUES(' ' 'J' 'N')
     A                                 13 16'Autofakt:'
     A            WSPFAK         1A  B 13 26VALUES(' ' 'J' 'N')
     A                                 13 30'Autof.kø:'
     A            WSPFKØ         1A  B 13 40VALUES(' ' 'J' 'N')
N04  A                                 14  2'Ext. TULLID SE:'
N04  A            WTUID         10A  B 14 18
N04   * FLYTTET LINJE 14 TIL 15 START
     A N30                             15  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A N30                             15 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSG36        36A  O 15  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
N04   * FLYTTET LINJE 14 TIL 15 SLUTT
     A            ZMSGNO         7A  P
      *
     A          R FILTERD
     A                                      WINDOW(*DFT 11 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL011R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC1028 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2'Referanse 3:'
     A            W2FLN         17A  B  2 18DSPATR(UL)
     A                                  3  2'Postnr fra/til:'
     A            W2BPNF         4  0B  3 18DSPATR(UL)
     A                                  3 23'/'
     A            W2BPNT         4  0B  3 25DSPATR(UL)
     A                                  4  2'Landkode:'
     A            W2BLK          2A  B  4 18DSPATR(UL)
     A                                  4 22'Postnr fra/til:'
     A            W2BPUF         9A  B  4 38DSPATR(UL)
     A                                  4 48'/'
     A            W2BPUT         9A  B  4 50DSPATR(UL)
     A                                  5  2'Transportørnr:'
     A            W2TN           8  0B  5 18DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  6  2'Varemottaker:'
     A            W2BNR         17A  B  6 18DSPATR(UL)
     A                                  7  2'Leveringsdato:'
     A            W2TDT     R        B  7 18REFFLD(LLHEADR/LHTDT LLHEAD)
     A                                      EDTWRD('    .  .  ')
     A                                  8  2'Referanse 1:'
     A            W2PAK         17A  B  8 18DSPATR(UL)
     A                                  9  2'Referanse 2:'
     A            W2ORDR        17A  B  9 18DSPATR(UL)
     A                                 10  2'Plukklistenr:'
     A            W2ALN          7  0B 10 18DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
