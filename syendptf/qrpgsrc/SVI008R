********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Ændrade ledtexter + nytt fælt
P11081* 02 SH  PTF12  SUBMITT ARKIVJOBB
P11081* 03 SH  PTF12  opdnr til arkivp
********************************************************************
     FSVIF      IF   E           K DISK
     FSVIH      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSVI008P   O    E             PRINTER OFLIND(*IN27) USROPN
     FFIRM      IF   E             DISK
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FARKIVP    O  A E           K DISK
     D                SDS
     D  ZUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIHASYAV         4
     C                   PARM                    UVIHASYOP         7
     C                   PARM                    CALLTYP           1
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVIH_SYAV     UVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     UVIH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
CB   C                   MOVEL     UVIHASYAV     UVIH_SYAV
CB   C                   MOVEL     UVIHASYOP     UVIH_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVIH_SYAV
     C                   KFLD                    UVIH_SYOP
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UVIH_SYAV
     C                   EVAL      UOPLNR=253
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
      *______________________________________
      * Hente firmaopplysninger fra spedisjon
      *""""""""""""""""""""""""""""""""""""""
     C                   READ      FIRM
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C     CALLTYP       IFEQ      'A'
     C                   CALL      'SVI008CA'
     C                   ELSE
     C                   EVAL      UCMD='OVRPRTF FILE(SVI008P) OUTQ('
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   ENDIF
     C                   OPEN      SVI008P
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs fakturor                                         SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVIH
     C                   WRITE     SVI008PA

     C     KEYA          SETLL     SVIF
     C     KEYA          READE     SVIF                                   51
     C     *IN51         DOWEQ     *OFF
     C                   EVAL      WVIF_FATY=SVIF_FATY
     C                   EVAL      WVIF_FATX=SVIF_FATX
     C                   EVAL      WVIF_VAKD=SVIF_VAKD
     C                   EVAL      WVIF_VAKU=SVIF_VAKU
     C                   EVAL      WVIF_FABL=SVIF_FABL

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SVI008PA
     C                   ENDIF
     C                   WRITE     SVI008PB

     C     KEYA          READE     SVIF                                   51
     C                   ENDDO

     C                   WRITE     SVI008PZ
     C     CALLTYP       IFEQ      'C'
     C                   EXSR      ARKSR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *    Arkiver                                          ARKSR /
      /////////////////////////////////////////////////////////////
     C     ARKSR         BEGSR
      * SKAL DOKUMENT ARKIVERES ?
     C                   MOVE      'S2'          ARTYPE
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     ARKJN         ANDEQ     'J'

     C                   Z-ADD     SVIH_MOKN     ARKUND
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      UVIH_SYAV     ARAVD
s03  C*                  MOVE      *ZEROS        AROPD
n03  C                   MOVE      UVIH_SYOP     AROPD
     C                   MOVE      WDT           ARDATE
     C                   MOVE      WTM           ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   MOVE      UVIH_SYOP     ARUNDE
     C                   MOVEL     'S2:'         ARUNDE
     C                   WRITE     ARKIVR
N05   *_________________________
N05   * Aktivera arkivfunktionen
N05   *"""""""""""""""""""""""""
N05  C                   CALL      'ARC002CLB'
N05  C                   PARM      'S2'          WATYPE            2
N05  C                   PARM                    ARUSER
     C                   ENDIF
     C                   ENDSR
