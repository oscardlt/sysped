********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 Ny dangerous logikk
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STS153 MODULE(*LIBL/STS153 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FGSSORF    IF   E           K DISK    usropn
     FGSSOLF    UF   E           K DISK    usropn
     FGSSOLL01  UF   E           K DISK    usropn
     F                                     RENAME(GSSOLFR:GSSOLFR1)
     FGSSOLL04  UF   E           K DISK    usropn
     F                                     RENAME(GSSOLFR:GSSOLFR4)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D XXUSER          S             10
     D XXTURNR         S             11
     D FEILKODE        s              1
     D lstbrd          s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D i               s             10i 0
      *
     D                 DS
     D  XXCLID35               1     35
     D  XXPRE3                 1      2
      *
     D                 DS
     D  XXCLID1                1     12
     D  XXPRE1                 1      3
     D  XXONO                  4     10
     D  XXLNO                 11     12
      *
     D                 DS
     D  XXCLID2                1     17
     D  XXPRE2                 1      3
     D  XXTRNO                 4     14
     D  XXBNO                 15     17
      *
     D                 DS
     D  XXLCNR                 1      9
     D  XXLCPRE                1      3
     D  XXLCNO                 4      9
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      *IN60
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl51B
      *
     C                   WHEN      BIINN = ' '
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl51B
      *
     C                   WHEN      XXLCPRE <> '999'
     C                   MOVE      '2'           FEILKODE
     C                   EXSR      SetSkl153
      *
     C                   OTHER
     C                   SELECT
     C                   WHEN      XXPRE3 = '00'
     C     XXCLID35      SETLL     GSSOLL04
     C     XXCLID35      READE     GSSOLL04                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      OLLC = XXLCNO
     C                   MOVE      'M'           OLST
     C                   UPDATE    GSSOLFR4
N01  c                   call      'GSS077R'
N01  C                   PARM                    OLONO
     C     XXCLID35      READE     GSSOLL04                               33
     C  N33              ENDDO
     C                   WHEN      XXPRE2 = '995'
     C                   MOVE      XXTRNO        OLTNO
     C                   MOVE      XXBNO         OLBOX
     C     GSSOLL01K     SETLL     GSSOLL01
     C     GSSOLL01K     READE     GSSOLL01                               33
     C                   IF        *IN33 = *OFF
     C                   DOW       *IN33 = *OFF
     C                   EVAL      OLLC = XXLCNO
     C                   MOVE      'M'           OLST
     C     OLONO         CHAIN     GSSORF                             33
     C                   IF        ORPOS1 <> OLSTD
     C                   MOVE      ORPOS1        OLSTD
     C                   END
     C                   UPDATE    GSSOLFR1
N01  c                   call      'GSS077R'
N01  C                   PARM                    OLONO
     C     GSSOLL01K     READE     GSSOLL01                               33
     C  N33              ENDDO
     C                   ELSE
      * KANSELLERTE BOKSER
     C     XXCLID35      SETLL     GSSOLL04
     C     XXCLID35      READE     GSSOLL04                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      OLLC = XXLCNO
     C                   MOVE      'M'           OLST
     C                   UPDATE    GSSOLFR4
N01  c                   call      'GSS077R'
N01  C                   PARM                    OLONO
     C     XXCLID35      READE     GSSOLL04                               33
     C  N33              ENDDO
     C                   END
     C                   OTHER
     C                   MOVE      XXONO         OLONO
     C                   MOVE      XXLNO         OLLNO
     C     GSSOLFK       CHAIN     GSSOLF                             33
     C                   EVAL      OLLC = XXLCNO
     C                   MOVE      'M'           OLST
     C                   UPDATE    GSSOLFR
N01  c                   call      'GSS077R'
N01  C                   PARM                    OLONO
     C                   ENDSL
     C                   exsr      SetSkl152
     C                   ENDSL
      *
     C     SLUTT         TAG
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   SELECT
     C                   WHEN      *IN60
     C                   callp     wrtsection('all')
     C                   WHEN      BIINN = ' '
     C                   callp     wrtsection('all')
     C                   OTHER
     C                   callp     wrtsection('top')
     C                   ENDSL
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSOLF
     C                   close     GSSOLL01
     C                   close     GSSOLL04
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
      *
     C                   eval      XXCLID35 = zhbgetvar('CLID')
     C                   eval      XXCLID1 = XXCLID35
     C                   eval      XXCLID2 = XXCLID35
     C                   eval      XXLCNR = zhbgetvar('LCNR')
     C                   eval      XXUSER = zhbgetvar('USER')
     C                   eval      XXTURNR= zhbgetvar('TURNR')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl51B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS51B'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl152     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS152'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl153     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS153'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updhtmlvar('CLID':XXCLID2)
     C                   callp     updhtmlvar('TURNR':XXTURNR)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
      *
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GSSOLFK       KLIST
     C                   KFLD                    OLONO
     C                   KFLD                    OLLNO
     C     GSSOLL01K     KLIST
     C                   KFLD                    OLTNO
     C                   KFLD                    OLBOX
      *
     C                   open      BRIDF
     C     XXUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSORF
     C                   open      GSSOLF
     C                   open      GSSOLL01
     C                   open      GSSOLL04
     C                   END
     C                   MOVE      *BLANKS       FEILKODE
      *
     C                   ENDSR
