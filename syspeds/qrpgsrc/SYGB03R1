      *
      ***************************************************************
      *** LAGER WORKFILE (RAPPORTGRUNNLAG)                        ***
      ***                                                         ***
      *** PROGRAMMERT AV TORE BRÅSTAD, SYSTEMA A/S, APRIL 1994    ***
      ***************************************************************
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER        ***
      *** LEDIGE INDIKATORER 01-99                                ***
      ***************************************************************
      *
     FG1WREG    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTG2    IF   E           K DISK
     FGBREGLF   IF   E           K DISK
     FGBWREG    O    E           K DISK
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UFPER             2
     C                   PARM                    UFAAR             2
     C                   PARM                    UTPER             2
     C                   PARM                    UTAAR             2
     C*
     C                   MOVE      UAVD          WSAVD             4 0
     C                   MOVEL     'GJELDSAV'    GJETXT           15
     C                   MOVE      'SETNING'     GJETXT
     C                   MOVEL     'BEH. AVS'    BE1TXT           15
     C                   MOVE      'ETN. I '     BE1TXT
     C                   MOVEL     'BEH. AVS'    BE2TXT           15
     C                   MOVE      'ETN. II'     BE2TXT
     C*
     C                   Z-ADD     100           FLD100            3 0
     C*
     C     GBKY1         KLIST
     C                   KFLD                    AVD               4 0
     C                   KFLD                    WAVK
     C*
     C     HEDKEY        KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WOPD
     C*
     C     KKEY          KLIST
     C                   KFLD                    WUNI              2
     C                   KFLD                    WAVK
     C*
     C     K2KEY         KLIST
     C                   KFLD                    W2UNI             2
     C                   KFLD                    WAVK
     C*
     C                   MOVE      'GE'          WUNI
     C                   MOVE      'G2'          W2UNI
     C*
     C     START         TAG
     C     *LOVAL        SETLL     G1WRR
     C                   READ      G1WRR                                  10
     C                   Z-ADD     G1AVD         WAVD              4 0
     C                   Z-ADD     G1OPD         WOPD              7 0
     C                   MOVEL     G1AVK         WAVK              3
     C                   MOVEL     G1MVA         WAKDM             1
     C     *IN10         DOWEQ     '0'
     C     G1AVD         IFNE      WAVD
     C     G1OPD         ORNE      WOPD
     C     G1AVK         ORNE      WAVK
     C                   Z-ADD     WAVD          GAVD
     C                   Z-ADD     WOPD          GOPD
     C                   MOVE      WAVK          GAVK
     C                   MOVEL     WAKDM         GMVA
     C                   EXSR      AVSETN
     C     *IN40         IFEQ      '1'
     C     HEDKEY        CHAIN     HEADFR                             11
     C     *IN11         IFEQ      '0'
     C                   MOVE      *BLANKS       GSIGN
     C                   MOVEL     HESG          GSIGN
     C                   Z-ADD     HEPRO         GTUR
     C                   END
     C                   WRITE     GBWRR
     C                   SETOFF                                       40
     C                   END
     C                   Z-ADD     0             GBKOST
     C                   Z-ADD     0             GBFAKT
     C                   Z-ADD     0             GFKOST
     C                   Z-ADD     0             GFFAKT
     C                   Z-ADD     0             GAVS
     C                   END
     C                   Z-ADD     G1AVD         WAVD
     C                   Z-ADD     G1OPD         WOPD
     C                   MOVEL     G1AVK         WAVK
     C                   MOVEL     G1MVA         WAKDM
     C     G1KDA         IFEQ      'K'
     C                   ADD       G1BELN        GBKOST
     C                   ELSE
     C                   ADD       G1BELN        GBFAKT
     C                   END
     C                   READ      G1WRR                                  10
     C                   END
     C                   Z-ADD     WAVD          GAVD
     C                   Z-ADD     WOPD          GOPD
     C                   MOVE      WAVK          GAVK
     C                   MOVEL     WAKDM         GMVA
     C                   EXSR      AVSETN
     C     *IN40         IFEQ      '1'
     C     HEDKEY        CHAIN     HEADFR                             11
     C     *IN11         IFEQ      '0'
     C                   MOVE      *BLANKS       GSIGN
     C                   MOVEL     HESG          GSIGN
     C                   Z-ADD     HEPRO         GTUR
     C                   END
     C                   WRITE     GBWRR
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***************************************************************
     C*
     C     AVSETN        BEGSR
     C*
     C                   Z-ADD     G1AVD         AVD
     C     GBKY1         CHAIN     GBREGR                             20
     C     *IN20         IFEQ      '1'
     C                   Z-ADD     0             AVD
     C     GBKY1         CHAIN     GBREGR                             21
     C                   END
     C*
     C                   Z-ADD     0             HLPAVI            3 0
     C                   Z-ADD     0             HLPRED            3 0
     C                   Z-ADD     0             FAKAVI            3 2
     C                   Z-ADD     0             FAKRED            3 2
     C                   Z-ADD     0             TILKST           11 2
     C                   Z-ADD     0             TILINT           11 2
     C                   Z-ADD     GBKOST        WRKKST           11 2
     C                   MULT      -1            WRKKST
     C*
     C     AKF           IFNE      'X'
     C     FLD100        SUB       AAVIK         HLPAVI
     C     HLPAVI        DIV       FLD100        FAKAVI
     C     FLD100        SUB       ARED          HLPRED
     C     HLPRED        DIV       FLD100        FAKRED
     C     GBFAKT        IFGT      WRKKST
     C     GBFAKT        MULT      FAKAVI        TILKST
     C     WRKKST        IFLT      TILKST
     C     GBFAKT        MULT      FAKRED        GFKOST
     C                   MULT      -1            GFKOST
     C     GFKOST        SUB       GBKOST        GAVS
     C                   MULT      -1            GAVS
     C                   MOVE      GJETXT        GAVTXT
     C                   EXSR      GJEKTO
     C                   SETON                                        40
     C                   END
     C                   ELSE
     C     WRKKST        MULT      FAKAVI        TILINT
     C     GBFAKT        IFLT      TILINT
     C     WRKKST        DIV       FAKRED        GFFAKT
     C     GFFAKT        SUB       GBFAKT        GAVS
     C                   MULT      -1            GAVS
     C                   MOVE      BE1TXT        GAVTXT
     C                   EXSR      BEHKTO
     C                   SETON                                        40
     C                   END
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***************************************************************
     C*
     C     GJEKTO        BEGSR
     C*
     C     K2KEY         CHAIN     KOG2R                              21
     C     *IN21         IFEQ      '0'
     C     WAKDM         IFNE      'N'
     C                   Z-ADD     KG2MOT        GKONTO
     C                   ELSE
     C     KG2UTG        IFEQ      0
     C                   Z-ADD     KG2MOT        GKONTO
     C                   ELSE
     C                   Z-ADD     KG2UTG        GKONTO
     C                   END
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***************************************************************
     C*
     C     BEHKTO        BEGSR
     C*
     C     KKEY          CHAIN     KOGER                              22
     C     *IN22         IFEQ      '0'
     C     WAKDM         IFNE      'N'
     C                   Z-ADD     KGEKON        GKONTO
     C                   ELSE
     C     K2KEY         CHAIN     KOG2R                              23
     C     *IN23         IFEQ      '0'
     C     KG2KON        IFEQ      0
     C                   Z-ADD     KGEKON        GKONTO
     C                   ELSE
     C                   Z-ADD     KG2KON        GKONTO
     C                   END
     C                   END
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
