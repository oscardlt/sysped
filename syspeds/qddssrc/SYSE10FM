     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF12(12 'Avslutt program')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF07(07 'SPØRRING OPPDRAG')
     A                                      CF09(09 'Bytt signatur')
     A          H                           HLPDOC('XXOPRE01' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/SYSE10'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  2  3MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            UFIRM          2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3 15MSGCON(45 DIV0596 SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 15MSGCON(45 DIV0615 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 15MSGCON(45 DIV3283 SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 15MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11 15MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                 13 15MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 35TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 50MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15 15MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 41TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 50MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18 15MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 18 35COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23  3MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A N31
     AO 31N32N34                            CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'Søke')
     A N31
     AO 31N32N34                            CF05(05 'Gå til faktura')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31
     AO 31N32N34                            CF09(09 'Ny tur')
NV5  A                                      CF11(11 'DUP-OPPDRAG ')
     A                                      CF12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning til kjøper-
     A                                       / SELGER')
     A                                      CF15(15 'FrI SØK')
     A N31                                  CF16(16 'Neste')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF18(18 'Freetext')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPDOC('EXOPRE02' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPDOC('EXOPRE02' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 001 02 060)
     A          H                           HLPDOC('EXOPRE03' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 061 02 080)
     A          H                           HLPDOC('EXOPRE03' 'HELPNU' 'HLP400')
     A                                      HLPARA(03 001 04 050)
     A          H                           HLPDOC('EXOPRE04' 'HELPNU' 'HLP400')
     A                                      HLPARA(03 051 04 080)
     A          H                           HLPDOC('EXOPRE04' 'HELPNU' 'HLP400')
     A                                      HLPARA(04 001 06 080)
     A          H                           HLPDOC('EXOPRE05' 'HELPNU' 'HLP400')
     A                                      HLPARA(07 001 07 025)
     A          H                           HLPDOC('EXOPRE06' 'HELPNU' 'HLP400')
     A                                      HLPARA(07 026 07 080)
     A          H                           HLPDOC('EXOPRE06' 'HELPNU' 'HLP400')
     A                                      HLPARA(08 001 09 080)
     A          H                           HLPDOC('EXOPRE07' 'HELPNU' 'HLP400')
     A                                      HLPARA(11 001 15 080)
     A          H                           HLPDOC('EXOPRE07' 'HELPNU' 'HLP400')
     A                                      HLPARA(16 001 17 031)
     A          H                           HLPDOC('EXOPRE08' 'HELPNU' 'HLP400')
     A                                      HLPARA(16 032 17 080)
     A          H                           HLPDOC('EXOPRE09' 'HELPNU' 'HLP400')
     A                                      HLPARA(18 001 18 080)
     A          H                           HLPDOC('EXOPRE10' 'HELPNU' 'HLP400')
     A                                      HLPARA(19 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSE10'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(020 DIV3286 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  2  2MSGCON(004 DIV0616 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  2  8TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  58                              2 13MSGCON(005 YBC0035 *LIBL/SYMSGF)
     A                                      DSPATR(RI)
     A                                      COLOR(YLW)
     A N58        WSOPD          7Y 0O  2 13TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 22MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            WSSG           3A  B  2 26TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  2 33MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            WSSTD          4Y 0B  2 38TEXT('Kostnadssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(005 DIV3249 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  3  8TEXT('Prosjektnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                  3 17MSGCON(002 DIV0076 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  3 20DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 27MSGCON(005 DIV0618 *LIBL/SYMSGF)
     A            WSTLE          6A  B  3 33DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41MSGCON(006 DIV0619 *LIBL/SYMSGF)
     A            WSTLL         10A  B  3 48DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 61MSGCON(008 DIV0620 *LIBL/SYMSGF)
     A            WSTLKU         1A  B  3 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 61MSGCON(004 DIV5019 *LIBL/SYMSGF)
     A            WSRFA         15A  B  2 66TEXT('Referanse Agent')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(004 DIV0589 *LIBL/SYMSGF)
     A            WSSDF          5A  B  4  8TEXT('Frakt fra')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 14'-'
     A            WSSDT          5A  B  4 16TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 23MSGCON(008 DIV0617 *LIBL/SYMSGF)
     A            WSVALT         3A  B  4 32TEXT('V.kode tillegg')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVALP         3Y 1B  4 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                  4 46MSGCON(004 DIV0082 *LIBL/SYMSGF)
     A            WSLKA          2A  B  4 51TEXT('Landkode')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 57MSGCON(003 DIV0786 *LIBL/SYMSGF)
     A            WSSDL         20A  B  4 61TEXT('Lossested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  5  2MSGCON(005 DIV0084 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  5  8TEXT('Kundenr Agent')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  92                                  DSPATR(BL)
     A  92                                  COLOR(RED)
     A N92                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAA         30A  O  5 17TEXT('Navn Agent')
     A  92                                  DSPATR(BL)
     A  92                                  COLOR(RED)
     A N92                                  COLOR(WHT)
     A                                  5 48MSGCON(005 DIV0085 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  5 54TEXT('Kundenr Transportør')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTRNV        18A  O  5 63COLOR(WHT)
     A                                  6  2MSGCON(005 DIV3200 *LIBL/SYMSGF)
     A            WSLB           5A  B  6  8TEXT('LOKAL BÅT')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6 19MSGCON(005 DIV3209 *LIBL/SYMSGF)
     A            BLLSTY         3A  O  6 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 30MSGCON(005 DIV3240 *LIBL/SYMSGF)
     A            BLOB           5A  O  6 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 43MSGCON(005 DIV3192 *LIBL/SYMSGF)
     A            BLREIS        14A  O  6 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  8  8TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  91                                  DSPATR(BL)
     A  91                                  COLOR(RED)
     A N91                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  8 22TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFS         1A  B  8 27TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  8 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALS         3A  B  8 33TEXT('Valutakode Selger')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  8 42TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNASF        30A  O  8 51TEXT('Navn Selg.fakm')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(WHT)
     A                                  9  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            WSNAS         30A  B  9  8TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  9 42TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS2        30A  B 10  8TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B 10 42TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 11  8TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  90                                  DSPATR(BL)
     A  90                                  COLOR(RED)
     A N90                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANK          1Y 0B 11 22TEXT('Avtalenr Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFK         1A  B 11 27TEXT('Fakturakode Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                 11 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALK         3A  B 11 33TEXT('Valutakode Kjøper')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 11 42TEXT('Kundenr Kjøp.fakm')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAKF        30A  O 11 51TEXT('Navn Kjøp.fakm')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(WHT)
     A                                 12  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            WSNAK         30A  B 12  8TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK1        30A  B 12 42TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B 13  8TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 13 42TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 15  2MSGCON(078 DIV3282 *LIBL/SYMSGF)
     A            WSOT           2A  B 16  2TEXT('Oppdragstype')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFR           3A  B 16  5TEXT('Frankatur')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGM1         15A  B 16  9TEXT('Godsmerking-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 16 25TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A            WSVS1         25A  B 16 33TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 16 60TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM3           7Y 3B 16 70TEXT('Kubikk')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSGM2         15A  B 17  9TEXT('Godsmerking-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 17 33TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2MSGCON(009 DIV0592 *LIBL/SYMSGF)
     A            WSLKS          2   B 18 12COLOR(YLW)
     A            WSSDFF         5A  B 18 15TEXT('Forfrakt fra')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18 22MSGCON(011 DIV0593 *LIBL/SYMSGF)
     A            WSLKK          2   B 18 35COLOR(YLW)
     A            WSSDVT         5A  B 18 38TEXT('Viderefrakt til')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18 60MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            WSKDL         15A  B 18 66TEXT('Lagerkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2MSGCON(022 YBC0205 *LIBL/SYMSGF)
     A            WSPK3          1A  B 20  6TEXT('Printerkode-3')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK6          1A  B 20 12TEXT('Printerkode-6')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK7          1A  B 20 18TEXT('Printerkode-7')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK1          1A  B 20 22TEXT('Printerkode-1')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A                                 20 50MSGCON(007 YBC0203 *LIBL/SYMSGF)
     A            W2KDME         1A  B 20 58TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'F' 'X' 'L' 'A' 'B')
     A                                 20 60MSGCON(007 YBC0204 *LIBL/SYMSGF)
     A            W2KDE          1A  B 20 68TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 22  2MSGCON(007 DIV0607 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 22 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 22 18DSPATR(BL)
     A                                      COLOR(RED)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
