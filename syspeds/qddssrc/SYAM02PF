      *****************************************************************
      * ALLE BELOP RETTET FRA 13 TIL 8 TEGN                           *
      * AMETAUTSKRIFT                                                 *
      * ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=198, OVERFLOW=45      *
      * 81 = TYSK    82 = ENGELSK                                     *
      *****************************************************************
     A          R HEAD1                     SKIPB(9)
     A
      * HEADING
     A  81 42                             60MSGCON(10 TRO0246 SYMSGF)
     A  82 42                             60MSGCON(17 DIV2494 SYMSGF)
     A  81N42                             60MSGCON(17 DIV2453 SYMSGF)
     A  82N42                             60MSGCON(17 DIV2494 SYMSGF)
     A  81                                 5MSGCON(14 DIV2454 SYMSGF)
     A                                      SPACEB(1)
     A  82                                 5MSGCON(14 DIV2454 SYMSGF)
     A                                      SPACEB(1)
     A            HEAREF        15A       20
     A  81                                65MSGCON(04 DIV2456 SYMSGF)
     A  82                                65MSGCON(04 DIV2495 SYMSGF)
      * FIRMANAVN INN HER
     A                                   130'           '
     A  81                                 5MSGCON(14 TRO0387 SYMSGF)
     A                                      SPACEB(1)
     A  82                                 5MSGCON(14 TRO0388 SYMSGF)
     A                                      SPACEB(1)
     A            UDAY           2S 0    150
     A                                   152'.'
     A            UMONTH         2S 0    153
     A                                   155'.'
     A            UYEAR          2S 0    156
     A            HEEREF        15A       20
     A            HENAVN        30A       60
     A  81                                 5MSGCON(14 DIV2457 SYMSGF)
     A                                      SPACEB(1)
     A  82                                 5MSGCON(14 DIV2496 SYMSGF)
     A                                      SPACEB(1)
     A            HEBILN        10A       20
     A  81                                 5MSGCON(14 DIV2458 SYMSGF)
     A                                      SPACEB(1)
     A  82                                 5MSGCON(14 DIV2497 SYMSGF)
     A                                      SPACEB(1)
     A            HEADAT         6  0     20EDTCDE(Y)
     A                                      SPACEA(1)
     A N41                                80' '
     A                                      SPACEA(3)
     A  41 81                              5MSGCON(14 TRO0252 SYMSGF)
     A                                      SPACEB(1)
     A  41 82                              5MSGCON(14 TRO0253 SYMSGF)
     A                                      SPACEB(1)
     A  41        AHDTMP         2  0     20
     A  41                                22'-'
     A  41        AHDTÅP         2  0     23
     A                                      SPACEA(3)
     A*DETALJ HEADINGER - STANDARD
     A          R HEAD2                     SPACEB(2)
     A                                    80MSGCON(07 DIV2459 SYMSGF)
     A                                   102MSGCON(11 TRO0187 SYMSGF)
     A                                      SPACEA(2)
     A                                     5MSGCON(03 DIV2461 SYMSGF)
     A  81                                10MSGCON(07 DIV2462 SYMSGF)
     A  82                                10MSGCON(07 DIV2498 SYMSGF)
     A  81                                23MSGCON(06 DIV2463 SYMSGF)
     A  82                                23MSGCON(06 DIV2463 SYMSGF)
     A  81                                30MSGCON(09 DIV2464 SYMSGF)
     A  82                                30MSGCON(09 DIV2499 SYMSGF)
     A  81                                73MSGCON(09 DIV2465 SYMSGF)
     A  82                                73MSGCON(09 DIV2500 SYMSGF)
     A  81                                87MSGCON(09 DIV2466 SYMSGF)
     A  82                                87MSGCON(09 DIV2501 SYMSGF)
     A  81                                98MSGCON(09 DIV2465 SYMSGF)
     A  82                                98MSGCON(09 DIV2500 SYMSGF)
     A  81                               111MSGCON(09 DIV2466 SYMSGF)
     A  82                               111MSGCON(09 DIV2501 SYMSGF)
     A*DETALJ HEADINGER - ALTERNATIV
     A          R HEAD2A                    SPACEB(2)
     A  81                                93MSGCON(06 TRO0199 SYMSGF)
     A  82                                94MSGCON(06 TRO0399 SYMSGF)
     A  41 42                            127MSGCON(07 DIV2459 SYMSGF)
     A  41 42                            155MSGCON(11 TRO0187 SYMSGF)
     A                                      SPACEA(1)
     A N42                               127MSGCON(07 DIV2459 SYMSGF)
     A            AHVALA         3A      140
     A N42                               155MSGCON(11 TRO0187 SYMSGF)
     A            AHVALE         3A      164
     A                                      SPACEA(1)
     A                                     4MSGCON(03 TRO0188 SYMSGF)
     A  81                                 8MSGCON(05 TRO0189 SYMSGF)
     A  82                                 8MSGCON(05 TRO0389 SYMSGF)
     A  81                                15MSGCON(05 TRO0190 SYMSGF)
     A  82                                15MSGCON(05 TRO0390 SYMSGF)
     A  81                                24MSGCON(08 TRO0191 SYMSGF)
     A  82                                24MSGCON(08 TRO0391 SYMSGF)
     A  81                                39MSGCON(03 TRO0192 SYMSGF)
     A  82                                39MSGCON(06 TRO0392 SYMSGF)
     A  81                                46MSGCON(10 TRO0193 SYMSGF)
     A  82                                46MSGCON(10 TRO0393 SYMSGF)
     A  81                                62MSGCON(03 TRO0192 SYMSGF)
     A  82                                62MSGCON(06 TRO0392 SYMSGF)
     A  81                                70MSGCON(07 TRO0194 SYMSGF)
     A  82                                70MSGCON(07 TRO0394 SYMSGF)
     A  81                                81MSGCON(05 TRO0195 SYMSGF)
     A  82                                81MSGCON(06 TRO0395 SYMSGF)
     A  81                                94MSGCON(07 TRO0194 SYMSGF)
     A  82                                94MSGCON(07 TRO0394 SYMSGF)
     A  81                               105MSGCON(04 TRO0196 SYMSGF)
     A  82                               105MSGCON(05 TRO0396 SYMSGF)
     A N41 42                            113MSGCON(06 TRO0197 SYMSGF)
     A  41 81                            120MSGCON(09 DIV2465 SYMSGF)
     A  41 82                            120MSGCON(09 DIV2500 SYMSGF)
     A  41 81                            133MSGCON(09 DIV2466 SYMSGF)
     A  41 82                            133MSGCON(09 DIV2501 SYMSGF)
     A  41 81                            146MSGCON(09 DIV2465 SYMSGF)
     A  41 82                            146MSGCON(09 DIV2500 SYMSGF)
     A  41 81                            159MSGCON(09 DIV2466 SYMSGF)
     A  41 82                            159MSGCON(09 DIV2501 SYMSGF)
     A N42 81                            120MSGCON(09 DIV2465 SYMSGF)
     A N42 82                            120MSGCON(09 DIV2500 SYMSGF)
     A N42 81                            133MSGCON(09 DIV2466 SYMSGF)
     A N42 82                            133MSGCON(09 DIV2501 SYMSGF)
     A N42 81                            146MSGCON(09 DIV2465 SYMSGF)
     A N42 82                            146MSGCON(09 DIV2500 SYMSGF)
     A N42 81                            159MSGCON(09 DIV2466 SYMSGF)
     A N42 82                            159MSGCON(09 DIV2501 SYMSGF)
     A*DETALJER - STANDARD
     A          R DETALJA                   SPACEB(1)
     A            DETXT         26A       10
     A          R DETALJB                   SPACEB(2)
     A            DETXT         26A       10
     A          R DETALJ                    SPACEB(1)
     A N44        DEPOS          7A        6
     A  44        DETXT         26A       10
     A N44        DEVKT          5S 0     10EDTCDE(L)
     A N44        DEM3           7S 3     20EDTCDE(L)
     A N44        DEVTXT         2A       29
     A N44        DEFR           3A       33
     A N44        DENAS         10A       39
     A N44                                50MSGCON(01 TRO0050 SYMSGF)
     A N44        DENAK         10A       52
     A N43        DEAINT         8S 2     69EDTCDE(M)
     A N43        DEAUTG         8S 2     81EDTCDE(M)
     A N43        DEEINT         8S 2     93EDTCDE(M)
     A N43        DEEUTG         8S 2    105EDTCDE(M)
     A  43        NEAINT         8S 2     69EDTCDE(M)
     A  43        NEAUTG         8S 2     81EDTCDE(M)
     A  43        NEEINT         8S 2     93EDTCDE(M)
     A  43        NEEUTG         8S 2    105EDTCDE(M)
     A*DETALJER - ALTERNATIV
     A          R DETA                      SPACEB(1)
     A N44        HEPOS1         7A        4
     A N44        HEOPD          7S 0      7EDTCDE(L)
     A  44        DETXT         26A       10
     A N44        UERFA         10A       15
     A N44        UENASF        15A       26
     A N44        HESDFF         5A       42
     A N44        UENAKF        15A       48
     A N44        HESDVT         5A       64
     A N44        HEVKT          9S 0     70EDTCDE(L)
     A N44        UEM3           7S 3     81EDTCDE(L)
     A N44        ULMM3          2A       90
     A N44        UEFBV          9S 0     93EDTCDE(L)
     A N44        HEFR           3A      107
     A            DEEUTG         8S 2    105EDTCDE(M)
     A*DETALJER - ALTERNATIV PERIODEAVSLUTNING
     A          R DETF                      SPACEB(1)
     A N44        UEPOS1         7A        4
     A N44        UEOPD          7S 0      7EDTCDE(L)
     A  44        DETXT         26A       10
     A N44        UERFA         10A       15
     A N44        UENASF        15A       26
     A N44        HESDFF         5A       42
     A N44        UENAKF        15A       48
     A N44        HESDVT         5A       64
     A N44        UEVKT          9S 0     70EDTCDE(L)
     A N44        UEM3           7S 3     81EDTCDE(L)
     A N44        ULMM3          2A       90
     A N44        UEFBV          9S 0     93EDTCDE(L)
     A N44        UEFR           3A      107
ERTN9A N43N90     DEAINT         8S 2    117EDTCDE(M)
ERTN9A N43N90     DEAUTG         8S 2    129EDTCDE(M)
ERTN9A N43N90     DEEINT         8S 2    141EDTCDE(M)
ERTN9A N43N90     DEEUTG         8S 2    153EDTCDE(M)
ERTN9A  43N90     NEAINT         8S 2    117EDTCDE(M)
ERTN9A  43N90     NEAUTG         8S 2    129EDTCDE(M)
ERTN9A  43N90     NEEINT         8S 2    141EDTCDE(M)
ERTN9A  43N90     NEEUTG         8S 2    153EDTCDE(M)
ERT  A*DETALJER - ALTERNATIV PERIODEAVSLUTNING
ERT  A          R DETFGEB                   SPACEB(1)
ERT  A            WGETXT        20A       77
ERT  A            AGVK           3A      107
ERT  A            AGAINT        13S 2    117EDTCDE(M)
ERT  A            AGAUTG        13S 2    129EDTCDE(M)
ERT  A            AGEINT        13S 2    141EDTCDE(M)
ERT  A            AGEUTG        13S 2    153EDTCDE(M)
     A* SUM1 - POOL
     A          R PSUMF1                    SPACEB(1)
     A                                     4'TOTAL'
     A            S1AINT         8S 2    117EDTCDE(M)
     A            S1AUTG         8S 2    129EDTCDE(M)
     A            S1EINT         8S 2    141EDTCDE(M)
     A            S1EUTG         8S 2    153EDTCDE(M)
     A* SUM2 - POOL
     A          R PSUMF2                    SPACEB(1)
     A                                     4'TOTAL'
     A            S2AINT         8S 2    117EDTCDE(M)
     A            S2AUTG         8S 2    129EDTCDE(M)
     A            S2EINT         8S 2    141EDTCDE(M)
     A            S2EUTG         8S 2    153EDTCDE(M)
     A* SUM - STANDARD
     A          R SUM                       SPACEB(2)
     A  81                                20MSGCON(05 DIV2468 SYMSGF)
     A  82                                20MSGCON(05 DIV2503 SYMSGF)
     A N43        SMAINT         8S 2     69EDTCDE(M)
     A N43        SMAUTG         8S 2     81EDTCDE(M)
     A N43        SMEINT         8S 2     93EDTCDE(M)
     A N43        SMEUTG         8S 2    105EDTCDE(M)
     A  43        NMAINT         8S 2     69EDTCDE(M)
     A  43        NMAUTG         8S 2     81EDTCDE(M)
     A  43        NMEINT         8S 2     93EDTCDE(M)
     A  43        NMEUTG         8S 2    105EDTCDE(M)
     A* SUM - ALTERNATIV
     A          R SUMA                      SPACEB(2)
     A            SMPOS          4S 0      3EDTCDE(L)
     A            SMFBV         11S 0     89EDTCDE(L)
     A            SMEUTG         8S 2    105EDTCDE(M)
     A* SUM - ALTERNATIV PERIODEAVSLUTNING
     A          R SUMF                      SPACEB(2)
     A                                     4'SUM'
     A            SMVKT          9S 0     68EDTCDE(L)
     A            SMFBV         11S 0     89EDTCDE(L)
     A N43        SMAINT         8S 2    117EDTCDE(M)
     A N43        SMAUTG         8S 2    129EDTCDE(M)
     A N43        SMEINT         8S 2    141EDTCDE(M)
     A N43        SMEUTG         8S 2    153EDTCDE(M)
     A  43        NMAINT         8S 2    117EDTCDE(M)
     A  43        NMAUTG         8S 2    129EDTCDE(M)
     A  43        NMEINT         8S 2    141EDTCDE(M)
     A  43        NMEUTG         8S 2    153EDTCDE(M)
     A* SALDO
     A          R SALDO                     SPACEB(1)
     A  81                                20MSGCON(06 DIV2469 SYMSGF)
     A  82                                20MSGCON(06 DIV2504 SYMSGF)
     A  51        SLAINT         8S 2    117EDTCDE(M)
     A  52        SLAUTG         8S 2    129EDTCDE(M)
     A  53        SLEINT         8S 2    141EDTCDE(M)
     A  54        SLEUTG         8S 2    153EDTCDE(M)
     A* KURS
     A          R KURS                      SPACEB(1)
     A  81                                20MSGCON(04 DIV2470 SYMSGF)
     A  82                                20MSGCON(04 DIV2505 SYMSGF)
     A            KSKURS         7S 4     30EDTCDE(M)
     A  51        KSAINT         8S 2    117EDTCDE(M)
     A  52        KSAUTG         8S 2    129EDTCDE(M)
     A  53        KSEINT         8S 2    141EDTCDE(M)
     A  54        KSEUTG         8S 2    153EDTCDE(M)
     A* AMETA RESULTAT - POOL
     A          R GEVINSTP                   SPACEB(1)
     A  81N50                             20MSGCON(09 DIV2471 SYMSGF)
     A  82N50                             20MSGCON(11 DIV2506 SYMSGF)
     A  81 50                             20MSGCON(09 DIV2472 SYMSGF)
     A  82 50                             20MSGCON(11 DIV2507 SYMSGF)
     A            GTBEL1         8S 2    153EDTCDE(M)
     A* OMKOSTNINGER EGEN - POOL
     A          R OMKE                       SPACEB(1)
     A  81N50                             20MSGCON(30 TRO0409 SYMSGF)
     A  82N50                             20MSGCON(30 TRO0409 SYMSGF)
     A  81 50                             20MSGCON(30 TRO0409 SYMSGF)
     A  82 50                             20MSGCON(30 TRO0409 SYMSGF)
     A            S1EUTG         8S 2    153EDTCDE(M)
     A* OMKOSTNINGER PARTNER - POOL
     A          R OMKP                       SPACEB(1)
     A  81N50                             20MSGCON(30 TRO0410 SYMSGF)
     A  82N50                             20MSGCON(30 TRO0410 SYMSGF)
     A  81 50                             20MSGCON(30 TRO0410 SYMSGF)
     A  82 50                             20MSGCON(30 TRO0410 SYMSGF)
     A            S1AUTG         8S 2    153EDTCDE(M)
     A* INNTEKT EGEN 50% - POOL
     A          R INNE                       SPACEB(1)
     A  81N50                             20MSGCON(30 TRO0411 SYMSGF)
     A  82N50                             20MSGCON(30 TRO0411 SYMSGF)
     A  81 50                             20MSGCON(30 TRO0411 SYMSGF)
     A  82 50                             20MSGCON(30 TRO0411 SYMSGF)
     A            H1EINT         8S 2    153EDTCDE(M)
     A* INNTEKT PARTNER 50% - POOL
     A          R INNP                       SPACEB(1)
     A  81N50                             20MSGCON(30 TRO0412 SYMSGF)
     A  82N50                             20MSGCON(30 TRO0412 SYMSGF)
     A  81 50                             20MSGCON(30 TRO0412 SYMSGF)
     A  82 50                             20MSGCON(30 TRO0412 SYMSGF)
     A            H1AINT         8S 2    153EDTCDE(M)
     A* FRAKTUTGIFT EGEN 50% - POOL
     A          R FRAE                       SPACEB(1)
     A  81N50                             20MSGCON(30 TRO0413 SYMSGF)
     A  82N50                             20MSGCON(30 TRO0413 SYMSGF)
     A  81 50                             20MSGCON(30 TRO0413 SYMSGF)
     A  82 50                             20MSGCON(30 TRO0413 SYMSGF)
     A            H2EDIF         8S 2    153EDTCDE(M)
     A* FRAKTUTGIFT PARTNER 50% - POOL
     A          R FRAP                       SPACEB(1)
     A  81N50                             20MSGCON(30 TRO0414 SYMSGF)
     A  82N50                             20MSGCON(30 TRO0414 SYMSGF)
     A  81 50                             20MSGCON(30 TRO0414 SYMSGF)
     A  82 50                             20MSGCON(30 TRO0414 SYMSGF)
     A            H2ADIF         8S 2    153EDTCDE(M)
     A* GEVINST/TAP
     A          R GEVINST                   SPACEB(1)
     A  81N50                             20MSGCON(09 DIV2471 SYMSGF)
     A  82N50                             20MSGCON(11 DIV2506 SYMSGF)
     A  81 50                             20MSGCON(09 DIV2472 SYMSGF)
     A  82 50                             20MSGCON(11 DIV2507 SYMSGF)
     A            GTBEL1         8S 2    153EDTCDE(M)
     A* PARTNERS ANDEL
     A          R PANDEL                    SPACEB(1)
     A  81                                20MSGCON(13 DIV2473 SYMSGF)
     A* 82                                20MSGCON(13 DIV2508 SYMSGF)
     A  81        PAPRO1         3S 0     45EDTCDE(M)
     A  82        PAPRO2         3S 0     20EDTCDE(M)
     A  81                                48MSGCON(01 DIV2511 SYMSGF)
     A  82                                23MSGCON(01 DIV2511 SYMSGF)
     A            PABEL1         8S 2    153EDTCDE(M)
     A* TIL RESKONTRO - ALTERNATIV
     A          R RESKONA                   SPACEB(2)
     A  81 55                             88MSGCON(11 DIV2474 SYMSGF)
     A                                      SPACEB(1)
     A  81 56                             88MSGCON(11 DIV2475 SYMSGF)
     A                                      SPACEB(1)
     A  82 55                             88MSGCON(11 DIV2509 SYMSGF)
     A                                      SPACEB(1)
     A  82 56                             88MSGCON(11 DIV2510 SYMSGF)
     A                                      SPACEB(1)
ERT  A**>                                101MSGCON(03 DIV2513 SYMSGF)
ERT  A            WVALA          3       101
     A  55        RSBEL1         8S 2    105EDTCDE(M)
     A  56        RSBEL2         8S 2    105EDTCDE(M)
     A                                    88MSGCON(32 DIV2514 SYMSGF)
     A                                      SPACEB(1)
     A* TIL RESKONTRO - STANDARD + PERIODEAVREGNING
     A          R RESKONF                   SPACEB(2)
     A  81 55                            136MSGCON(11 DIV2474 SYMSGF)
     A                                      SPACEB(1)
     A  81 56                            136MSGCON(11 DIV2475 SYMSGF)
     A                                      SPACEB(1)
     A  82 55                            136MSGCON(11 DIV2509 SYMSGF)
     A                                      SPACEB(1)
     A  82 56                            136MSGCON(11 DIV2510 SYMSGF)
     A                                      SPACEB(1)
ERT  A**>                                149MSGCON(03 DIV2513 SYMSGF)
ERT  A            WVALF          3       149
     A            RSBEL          8S 2    153EDTCDE(M)
     A                                   136MSGCON(32 DIV2514 SYMSGF)
     A                                      SPACEB(1)
     A* STREKLINJE
     A          R STREK                     SPACEB(1)
     A                                     4MSGCON(123 DIV2512 SYMSGF)
     A  41                               115MSGCON(055 TRO0251 SYMSGF)
     A N42                               115MSGCON(055 TRO0251 SYMSGF)
