     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R TOLLDEK
     A                                      CF12(12 'Gå tilbake til tolldek.rul-
     A                                      lbilde.')
     A                                  1 10'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A            SEDTY          2A  O  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEDK           2A  O  1 55TEXT('DEKLARASJONSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  1 59'4.L.liste'
     A            SELSTL         2Y 0O  1 69TEXT('LASTELISTE')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2  2'2.Avsender'
     A            SEKNK          8Y 0O  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
     A            SERG          11A  O  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 41'5.V.post'
     A            SEVPOS         3Y 0O  2 50TEXT('VAREPOST')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 54'6.Kolli'
     A            SENTK          7Y 0O  2 62TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SENAK         30A  O  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41'Brt vekt'
     A            SEVKB          9Y 0O  3 50TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 61'Ref'
     A            SEAVD          4Y 0O  3 65TEXT('AVDELING')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  3 70TEXT('Tolldeklarasjonsnr')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SESG           3A  O  3 78TEXT('Signatur')
     A                                      COLOR(WHT)
     A            SEADK1        30A  O  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.Land'
     A            SELKA          2A  O  4 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKAT        17A  O  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK2        30A  O  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            SELKB          2A  O  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFTB         17A  O  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK3        30A  O  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 40'18.Mid.land'
     A            SELKM          2A  O  6 56TEXT('LK MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFTM         17A  O  6 59TEXT('F.T. MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            SEKNS          8Y 0O  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 40'19.Cont.'
     A            SEKDC          1A  O  7 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAS         30A  O  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 40'21.Ident.'
     A            SELKT          2A  O  8 51TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SETRID        17A  O  8 55TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS1        30A  O  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS2        30A  O 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS3        30A  O 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 35'25.Tr.måte'
     A            SETRM          2Y 0O 11 46TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 14  2'Fritekst ved dataliste'
     A                                 15  1'31.'
     A            SEFT1         45A  O 15  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 51'51.'
     A            SESTG1         8A  O 15 55TEXT('GARANTITOLLSTED-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG1         2A  O 15 64TEXT('GARANTI-land-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG2         8A  O 15 69TEXT('GARANTITOLLSTED-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG2         2A  O 15 78TEXT('GARANTI-land-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFT2         45A  O 16  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG3         8A  O 16 55TEXT('GARANTITOLLSTED-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG3         2A  O 16 64TEXT('GARANTI-land-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG4         8A  O 16 69TEXT('GARANTITOLLSTED-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG4         2A  O 16 78TEXT('GARANTI-land-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  1'44.'
     A            SEFT3         45A  O 17  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG5         8A  O 17 55TEXT('GARANTITOLLSTED-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG5         2A  O 17 64TEXT('GARANTI-land-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG6         8A  O 17 69TEXT('GARANTITOLLSTED-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG6         2A  O 17 78TEXT('GARANTI-land-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  1'50.Hovedansvarlig'
     A            SEKNA          8Y 0O 18 19TEXT('HOVEDANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 39'52.Garanti'
     A            SEGFT1        30A  O 18 50TEXT('GARATI FRITEKST-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAA         30A  O 19  2TEXT('Navn ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEGKD          1Y 0O 19 40TEXT('GARATIKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SEGFT2        30A  O 19 50TEXT('GARATI FRITEKST-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADA1        30A  O 20  2TEXT('Adresse-1 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 39'53.Tollsted'
     A            SETLKB         2A  O 20 51TEXT('LANDK TIL B.T.STED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SETSTB        20A  O 20 54TEXT('BESTEMMELSESTOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADA3        30A  O 21  2TEXT('Adresse-3 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 34'Dato'
     A            SEDTG         10A  O 21 39TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 50'Løpenr'
     A            SETLL         10A  O 21 57TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 68'Eksp.'
     A            SETLE          6A  O 21 74TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  1'54.Sted'
     A            SEDST         15A  O 22  9TEXT('Deklarantens sted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22 25'Tarifør'
     A            SETARF        16A  O 22 33TEXT('TARIFFØRSNAVN')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22 50'Reg.dato'
EV5  A            SEDT           8Y 0H
NV5  A            WSDT           6Y 0O 22 59TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 22 69'Status'
     A            SEST           1A  O 22 76TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 23  1'F12 = Til forrige'
     A                                      COLOR(BLU)
      *
     A          R HEADER
     A                                  1 30'Utskrift av lasteliste'
     A                                      COLOR(WHT)
     A                                  2 10'Avdeling:'
     A            UAVD           4  0O  2 21COLOR(BLU)
     A                                      EDTCDE(4)
     A            UAVDNV        33   O  2 27COLOR(BLU)
     A                                  3  2'Lnr. Tdeknr. Merker og nr.----  An-
     A                                      ll kolli og slag----------------- B-
     A                                      ruttovekt'
     A                                      COLOR(WHT)
     A                                  4 13'Tollnr.     Varebeskrivelse--------
     A                                      ---------------------------------'
     A                                      COLOR(WHT)
      *
     A          R SUBFIL
     A                                      SLNO(*VAR)
     A                                      CLRL(02)
     A            WSN            2Y 0O  1  3EDTCDE(4)
     A                                      COLOR(YLW)
     A            SETDN          7Y 0O  1  6EDTCDE(4)
     A  59                                  COLOR(RED)
     A            WSTXT1        17A  O  1 14
     A            SENTK          7Y 0O  1 32EDTCDE(4)
     A            WSTXT2        30A  O  1 40
     A            SEVKB          9Y 0O  1 71EDTCDE(4)
     A            SETLE          6A  O  2 13
     A                                  2 20'-'
     A            SETLL         10A  O  2 22
     A            WSTXTX        47A  O  2 33
      *
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Se tolldeklarasjon')
     A                                      CF08(08 'Plukker til lasteliste')
     A                                      CF09(09 'Fjern fra lasteliste')
     A                                      CF07(07 'Be om utskrift')
     A                                      ROLLUP(25 'Bla frem')
     A                                      ROLLDOWN(26 'Bla tilbake')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  9'Linjenr.'
     A            WSN            2Y 0B  1 19EDTCDE(4)
     A                                      DSPATR(UL)
     A  99                                  ERRMSG('Du må velge et av linjenr. -
     A                                      på skjermen.' 99)
     A  97                                  ERRMSG('Denne tolldek. kan du ikke -
     A                                      gjøre noe med nå.' 97)
     A                                  1 28'Tariffør:'
     A            WSSG           3A  B  1 39
     A  98                                  ERRMSG('Tarifførkode for utskrift e-
     A                                      r ugyldig.' 98)
     A                                      DSPATR(UL)
     A                                  2  9'Søking:'
     A            WSTDN          7N 0I  2 19DSPATR(UL)
     A                                  2 28'Listenr.'
     A            WSLNR          1Y 0B  2 39EDTCDE(4)
     A                                      DSPATR(UL)
     A                                  3  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                  3 13'F4=Se tolldek.'
     A                                      COLOR(BLU)
     A                                  3 40'F8=Plukk'
     A                                      COLOR(BLU)
     A                                  3 49'F9=Fjern'
     A                                      COLOR(BLU)
     A                                  3 28'F7=Utskrift'
     A                                      COLOR(BLU)
     A                                  3 58'Bla frem/tilbake'
     A                                      COLOR(BLU)
      *
     A          R BILDE02
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            SETDN          7Y 0O  1  9DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1  3'Tdek.'
     A                                  1 16'Tekst-1'
     A            WSTXT1        17A  B  1 24DSPATR(UL)
     A                                  1 42'Tekst-2'
     A            WSTXT2        30A  B  1 50DSPATR(UL)
     A                                  2 16'Tollnr.'
     A            SETLE          6A  B  2 24DSPATR(UL)
     A                                  2 31'-'
     A            SETLL         10A  B  2 33DSPATR(UL)
     A                                  3 16'Tekst-3'
     A            WSTXT3        55A  B  3 24DSPATR(UL)
     A  99                                  ERRMSG('Du må fylle minst en av de -
     A                                      tre fritekstlinjene.' 99)
