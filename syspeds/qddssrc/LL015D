********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXX * 01 YBC PTF6 SALDO ER ØKT FRA 5.0 TIL 7.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 40 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            VAMN           7  0H
     A            VASENR        25A  H
     A            VAANR         15A  O  5  5
     A            VADATO         8  0O  5 21EDTWRD('    .  .  ')
S01  A*           VASALD         5  0O  5 32EDTCDE(4)
N01  A            VASALD         7  0O  5 32EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL022R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC1130 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(30 YBC1131 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(10 YBC1132 SYMSGF)
     A            ULOK1          5A  O  3 13DSPATR(UL)
     A                                      COLOR(WHT)
     A            ULOK2         20A  O  3 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(38 YBC1133 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(30 YBC1134 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
