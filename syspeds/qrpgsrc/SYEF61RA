      *
      ***********************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV FLYFRAKTPRISER (NETTO)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-79 95-97    ***
      ***********************************************************
     FAIRNRD    UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYEF61DA  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       YIRCOD            1
     C                   MOVE      *BLANKS       YIRCCP            1
     C                   MOVE      *ZEROS        YIRKN             8 0
     C                   MOVE      *BLANKS       YIRCOM            4
     C                   MOVE      *ZEROS        YIRWEI            5 0
     C                   MOVE      *BLANKS       YIRCP             1
     C                   MOVE      *BLANKS       YIRTRC            1
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIKU         SETLL     AIRNRD
     C     START1        TAG
     C     PRIKU         READE(N)  AIRNRD                                 94
      *
     C     START2        TAG
     C                   MOVE      *BLANKS       XDLT
      *
     C     *IN94         IFEQ      '0'
     C                   DO        7
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  AIRNRD                                 94
     C     *IN94         IFEQ      '0'
     C                   MOVE      AIRCOD        YIRCOD
     C                   MOVE      AIRCCP        YIRCCP            1
     C                   MOVE      AIRKN         YIRKN
     C                   MOVE      AIRCOM        YIRCOM
     C                   MOVE      AIRWEI        YIRWEI
     C                   MOVE      AIRCP         YIRCP
     C                   MOVE      AIRTRC        YIRTRC
     C                   ENDIF
     C  N94              ENDDO
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
      *
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *** VEDL. FREIGHTNR.
     C   09              DO
     C                   CALL      'SYEF64R'
     C                   GOTO      START3
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C  N94PRISK3        CHAIN(N)  AIRNRD                             94
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVEA     '000'         *IN(80)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
     C   09              DO
     C                   CALL      'SYEF64R'
     C                   GOTO      START3
     C                   END
     C   12              EXSR      MOVB2
     C   12              GOTO      START3
      * Søk på gyldige koder
     C     *IN04         IFEQ      *ON
     C                   EXSR      SOEK
     C                   GOTO      START5
     C                   ENDIF
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
      * TEST KODE FOR RATETYPE
     C                   MOVEL     'AIRCOD'      TYPE             10
     C                   MOVEL     WSRCOD        VERDI             5
     C                   MOVEL     '0'           WIN30             1
     C                   CALL      'SYFAKDT'
     C                   PARM                    TYPE
     C                   PARM                    VERDI
     C                   PARM                    WIN30
     C     WIN30         IFEQ      '1'
     C                   MOVE      'BBA0132'     MSGNR
     C                   MOVE      *ON           *IN90
     C                   MOVE      *ON           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   ENDIF
      * TEST KUNDENR.
     C     WSRKN         IFNE      0
     C     CUND1K        CHAIN     CUNDL01                            89
     C  N30*IN89         IFEQ      '1'
     C                   MOVE      'SPK0001'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     12            POSNBR
     C                   ENDIF
     C                   ENDIF
      * TEST VEKTGRUPPE
     C  N30WSRWEI        IFEQ      0
     C                   MOVE      'SHU0160'     MSGNR
     C                   SETON                                        3088
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     26            POSNBR
     C                   END
      *
     C  N30PRISK         CHAIN     AIRNRD                             33
     C  N30
     CANN33              MOVE      'SHU0132'     MSGNR
     C  N30
     CANN33              SETON                                        3090
     C   30              GOTO      SLTB1
     C                   END
      * TEST GRUNNPRIS/MIN.
     C  N30WSRGCD        IFEQ      *BLANKS
     C     WSRGRN        ANDNE     0
     C                   MOVE      'YBC0542'     MSGNR
     C                   SETON                                        3087
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     38            POSNBR
     C                   ELSE
     C     WSRGCD        IFNE      *BLANKS
     C     WSRGRN        ANDEQ     0
     C                   MOVE      'YBC0543'     MSGNR
     C                   SETON                                        3086
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     41            POSNBR
     C                   END
     C                   END
      * TEST KIROPRIS
     C  N30WSRACD        IFEQ      *BLANKS
     C     WSRAWB        ANDNE     0
     C                   MOVE      'YBC0542'     MSGNR
     C                   SETON                                        3085
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     56            POSNBR
     C                   ELSE
     C     WSRACD        IFNE      *BLANKS
     C     WSRAWB        ANDEQ     0
     C                   MOVE      'YBC0543'     MSGNR
     C                   SETON                                        3084
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     58            POSNBR
     C                   END
     C                   END
      * TEST GRUNNPRIS/MIN. 2
     C  N30WSRGC2        IFEQ      *BLANKS
     C     WSRGR2        ANDNE     0
     C                   MOVE      'YBC0542'     MSGNR
     C                   SETON                                        3083
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     38            POSNBR
     C                   ELSE
     C     WSRGC2        IFNE      *BLANKS
     C     WSRGR2        ANDEQ     0
     C                   MOVE      'YBC0543'     MSGNR
     C                   SETON                                        3082
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     41            POSNBR
     C                   END
     C                   END
      * TEST KIROPRIS 2
     C  N30WSRAC2        IFEQ      *BLANKS
     C     WSRAW2        ANDNE     0
     C                   MOVE      'YBC0542'     MSGNR
     C                   SETON                                        3081
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     56            POSNBR
     C                   ELSE
     C     WSRAC2        IFNE      *BLANKS
     C     WSRAW2        ANDEQ     0
     C                   MOVE      'YBC0543'     MSGNR
     C                   SETON                                        3080
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     58            POSNBR
     C                   END
     C                   END
     C   30              GOTO      SLTB1
      *
     C     PRISK         CHAIN     AIRNRD                             33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    AIRNRDR
     C   33              WRITE     AIRNRDR
     C                   MOVE      *BLANKS       XDLT
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB1
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     2             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     2             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     XLIN              3 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     XDLT          ANDNE     '*DELETE'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGPR
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C     XDLT          ANDNE     '*DELETE'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CPYPR
     C   03
     COR 12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C     XDLT          ANDNE     '*DELETE'
     C                   MOVE      ' '           WSNR
     C                   EXSR      DLTPR
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGPR         BEGSR
     C***********************************************
     C                   EXSR      MOVPRW
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     40            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVEA     '000'         *IN(80)
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYPR         BEGSR
     C***********************************************
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVPRW
     C                   MOVE      '1'           *IN99
     C                   MOVE      '0'           *IN40
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVEA     '000'         *IN(80)
     C                   MOVE      '0'           *IN30
      *
     C     *IN04         IFEQ      *ON
     C                   EXSR      SOEK
     C                   GOTO      STCPY
     C                   ENDIF
     C     *IN09         IFEQ      *ON
     C                   CALL      'SYEF64R'
     C                   GOTO      STCPY
     C                   END
     C     *IN03         IFEQ      *ON
     C     *IN12         OREQ      *ON
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCPY
     C                   END
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTPR         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISK2        CHAIN     AIRNRD                             33
     C  N33              DELETE    AIRNRDR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      ULIN          AIRLIN
     C                   MOVE      UPFR          AIRPFR
     C                   MOVE      UPTO          AIRPTO
     C                   MOVE      WSRCOD        AIRCOD
     C                   MOVE      WSRCCP        AIRCCP
     C                   MOVE      WSRCP         AIRCP
     C                   MOVE      WSRTRC        AIRTRC
     C                   MOVE      WSRKN         AIRKN
     C                   MOVE      WSRCOM        AIRCOM
     C                   MOVE      WSRSRT        AIRSRT
     C                   MOVE      WSRWEI        AIRWEI
     C                   MOVE      WSRGCD        AIRGCD
     C                   MOVE      WSRGRN        AIRGRN
     C                   MOVE      WSRGC2        AIRGC2
     C                   MOVE      WSRGR2        AIRGR2
     C                   MOVE      WSRACD        AIRACD
     C                   MOVE      WSRAWB        AIRAWB
     C                   MOVE      WSRAC2        AIRAC2
     C                   MOVE      WSRAW2        AIRAW2
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN40
     C                   MOVE      AIRCOD        WSRCOD
     C                   MOVE      AIRCCP        WSRCCP
     C                   MOVE      AIRCP         WSRCP
     C                   MOVE      AIRTRC        WSRTRC
     C                   MOVE      AIRKN         WSRKN
     C                   MOVE      AIRCOM        WSRCOM
     C                   MOVE      AIRSRT        WSRSRT
     C                   MOVE      AIRWEI        WSRWEI
     C                   MOVE      AIRGCD        WSRGCD
     C                   MOVE      AIRGRN        WSRGRN
     C                   MOVE      AIRGC2        WSRGC2
     C                   MOVE      AIRGR2        WSRGR2
     C                   MOVE      AIRACD        WSRACD
     C                   MOVE      AIRAWB        WSRAWB
     C                   MOVE      AIRAC2        WSRAC2
     C                   MOVE      AIRAW2        WSRAW2
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       AIRCOD                                     AIRLD)
     C                   MOVE      *BLANKS       AIRCCP
     C                   MOVE      *BLANKS       AIRCP
     C                   MOVE      *BLANKS       AIRTRC
     C                   MOVE      *ZEROS        AIRKN                                      AIRLD)
     C                   MOVE      *BLANKS       AIRCOM                                     AIRLD)
     C                   MOVE      *ZEROS        AIRSRT                                     AIRLD)
     C                   MOVE      *ZEROS        AIRWEI                                     AIRLD)
     C                   MOVE      *BLANKS       AIRGCD                                     AIRLD)
     C                   MOVE      *ZEROS        AIRGRN                                     AIRLD)
     C                   MOVE      *BLANKS       AIRGC2                                     AIRLD)
     C                   MOVE      *ZEROS        AIRGR2                                     AIRLD)
     C                   MOVE      *BLANKS       AIRACD                                     AIRLD)
     C                   MOVE      *ZEROS        AIRAWB                                     AIRLD)
     C                   MOVE      *BLANKS       AIRAC2                                     AIRLD)
     C                   MOVE      *ZEROS        AIRAW2                                     AIRLD)
     C                   MOVE      '*DELETE'     XDLT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSRCOD
     C                   MOVE      *BLANKS       WSRCCP
     C                   MOVE      *BLANKS       WSRCP
     C                   MOVE      *BLANKS       WSRTRC
     C                   MOVE      *ZEROS        WSRKN
     C                   MOVE      *BLANKS       WSRCOM
     C                   MOVE      *ZEROS        WSRSRT
     C                   MOVE      *ZEROS        WSRWEI
     C                   MOVE      *BLANKS       WSRGCD
     C                   MOVE      *ZEROS        WSRGRN
     C                   MOVE      *BLANKS       WSRGC2
     C                   MOVE      *ZEROS        WSRGR2
     C                   MOVE      *BLANKS       WSRACD
     C                   MOVE      *ZEROS        WSRAWB
     C                   MOVE      *BLANKS       WSRAC2
     C                   MOVE      *ZEROS        WSRAW2
      *
     C                   ENDSR
      *
     C***********************************************
     C     SOEK          BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C  N40LINNBR        IFEQ      21
     C     POSNBR        ANDEQ     5
     C                   MOVEL     'AIRCOD'      WFTYP1           10
     C                   MOVE      *BLANKS       WFKOD1            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    WFTYP1
     C                   PARM                    UUPDAT            1
     C                   PARM                    WFKOD1
     C                   MOVEL     WFKOD1        WSRCOD
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     12            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C  N40LINNBR        IFEQ      21
     C     POSNBR        ANDGE     12
     C     POSNBR        ANDLE     19
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSRKN
     C                   PARM                    UFIRM
     C                   PARM                    USØKNV
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     21            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK2        ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     PRIKU         KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C     PRISK         KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C                   KFLD                    WSRCCP            1
     C                   KFLD                    WSRCOD
     C                   KFLD                    WSRKN
     C                   KFLD                    WSRCOM
     C                   KFLD                    WSRWEI
     C                   KFLD                    WSRCP
     C                   KFLD                    WSRTRC
     C     PRISK2        KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C                   KFLD                    AIRCCP
     C                   KFLD                    AIRCOD
     C                   KFLD                    AIRKN
     C                   KFLD                    AIRCOM
     C                   KFLD                    AIRWEI
     C                   KFLD                    AIRCP
     C                   KFLD                    AIRTRC
     C     PRISK3        KLIST
     C                   KFLD                    ULIN
     C                   KFLD                    UPFR
     C                   KFLD                    UPTO
     C                   KFLD                    YIRCCP            1
     C                   KFLD                    YIRCOD
     C                   KFLD                    YIRKN
     C                   KFLD                    YIRCOM
     C                   KFLD                    YIRWEI
     C                   KFLD                    YIRCP
     C                   KFLD                    YIRTRC
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSRKN
      *
     C     *ENTRY        PLIST
     C                   PARM                    ULIN              3 0
     C                   PARM                    UPFR              3
     C                   PARM                    UPTO              3
     C                   PARM                    USDT             30
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UFIRM             2
      *
     C                   ENDSR
      *
